     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ABS FF - OTC Maintenance - CAD Details setup')
      *****************************************************************
      *                                                               *
      *  Midas¦ABS - Future & Option module                           *
      *                                                               *
      *  FF0140X6 - FF OTC Maintenance - CAD Details                  *
      *             setup LF/FFITX2L0                                 *
      *                                                               *
      *  Function:  This program allow to setup Instrument type       *
      *             details extension file   (INTYPTY6).              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027A            Date 03May06               *
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX008             Date 23Mar00               *
      *                 ULX008             Date 23Mar00               *
      *                 ULX004             Date 19DEC96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027A - Conversion of customer number to alpha (post       *
      *            build 103). Recompiled.                            *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      * ULX008 - "Commissariat aux Bourses" - Change control A        *
      *            Recompiled due to changes in PF/ERLUICPD           *
      * ULX008 - CSSF Statutory Reporting - Commissariat aux Bourses  *
      *          Recompiled due to changes in PF/ERLUICPD             *
      *  ULX004  -  Capital Adequacy                                  *
      *                                                               *
      *****************************************************************
     FINTYPD  IF  E                    DISK
     F***INTYPDY6UF  E           K        DISK                      A                         CER001
     FFFITX2L0UF  E           K        DISK                      A                            CER001
     FERLUICL1IF  E           K        DISK
      /EJECT
      *****************************************************************
      /TITLE E LINES SPECIFICATIONS
      *****************************************************************
     E                    CPY@    1   1 80               COPYRIGHT  ARRAY
      *****************************************************************
      /EJECT
      *****************************************************************
     ILDA       E DSLDA                         256
     I              SPARE                           W24
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      **************************************************************************
      *                          P R O C E S S I N G
      **************************************************************************
     C           *ENTRY    PLIST
     C                     PARM           @RTCD   7
     C                     PARM           @MODE   1
      *
      ** Key to chain to FFITX2L0
      *
     C           K@KEY     KLIST
     C                     KFLD           ISTT
      *
      ** Reference Copyright Array
     C                     MOVEACPY@      CPY@@   1
      *
      ** Define the LDA for error handling
     C           *NAMVAR   DEFN           LDA
      *
      * First time access LF/ERLUICL1 to obtain ICD value for Option calculated
      * Method
      *
     C           'LU'      CHAINERLUICL1             86
     C           *IN86     IFEQ '1'                        - B1
     C           *LOCK     IN   LDA
     C                     MOVEL'001'     DBASE
     C                     MOVEL'ERLUICL1'DBFILE
     C                     MOVEL'LU'      DBKEY
     C                     MOVEL'FF0140X6'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END                             - E1
      *
     C           @MODE     IFEQ '1'                        - B1
     C           @MODE     OREQ '2'
      *
      ** Read FF instruments file until end of file
      *
     C                     READ INTYPD                   31
     C           *IN31     DOWEQ'0'                        - B2
      *
      ** If FF instrument doesn't exist in the extension file, add one
      ** with default value.
      *
     C********** K@KEY     CHAININTYPDY6             32                                       CER001
     C           K@KEY     CHAINFFITX2L0             32                                       CER001
      *
     C           *IN32     IFEQ '1'                        - B3
      *
     C                     MOVE ISTT      CBISTT
     C                     MOVE VPOPCM    CBOPCM
     C                     MOVE *BLANKS   CBSESN
     C                     MOVE *ZEROS    CBBBIX
     C                     WRITEINTYPDD6
      *
     C                     END                             - E3
      *
     C                     READ INTYPD                   31
     C                     ENDDO                           - E2
      *
     C                     ENDIF                           - E1
      *
     C                     SETON                     LR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK                     *B1
     C                     MOVE 'Y'       @RUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
      *
     C                     ENDIF                           *E1
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
