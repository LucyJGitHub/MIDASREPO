     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FF Pay/receive extract file split')
      ****************************************************************
      *                                                              *
      *  Midas - Dealing Module                                      *
      *                                                              *
      *  FF0307 - OTC Pay/Receive Extract File Split                 *
      *                                                              *
      *  Function:  This program passes records on the OTC           *
      *             pay/receive extract file to the appropriate      *
      *             Dealing module pay/receive extract file.         *
      *                                                              *
      *  Called By: DLC0211 - Input cycle pay/receive processing.    *
      *             DLC04   - Input cycle termination confirmation   *
      *                       and pay/receive processing.            *
      *             DLC0604 - COB pay/receive processing.            *
      *                                                              *
      *  (c) Finastra International Limited 2001                      *
      *                                                              *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CDL025             Date 27Jan05               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 189053             Date 31May01               *
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                    CFF003 *CREATE     DATE 14APR97            *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *     CDL025  - FX Netting Payment Generation (Recompile)       *
      *     189053  - Include checking of carry over before adding    *
      *               total of FF postings to previous amount.        *
      *     CFF003  - ENHANCED OTC PROCESSING - PHASE II              *
      *               UPGRADE FROM BLI LBL001                         *
      *                                                               *
      *****************************************************************
      * INDICATORS USED                                              *
      * ~~~~~~~~~~~~~~~                                              *
      * 84 - Error on array/table lookup                             *
      * 85 - EOF for FFOTPRPD                                        *
      * 86 - EOF for PREX1ZZ                                         *
      * 87 - EOF for PREX2ZZ                                         *
      * 88 - EOF for PREX3ZZ                                         *
      * 89 - EOF for PREX4ZZ                                         *
      *                                                              *
      * 90 to 99 - In subroutine GLZADD, GLZSUM                      *
      *                                                              *
      * U7 - Program Error                                           *
      * U8 - Database error                                          *
      * LR - Last record                                             *
      ****************************************************************
     F/EJECT
     FFFOTPRPDUF  E                    DISK
     F                                              KINFSR *PSSR
     FPREX1ZZ UF  E                    DISK                      A
     F            PREXZZF                           KRENAMEPREXZZA
     F                                              KINFSR *PSSR
     FPREX2ZZ UF  E                    DISK                      A
     F            PREXZZF                           KRENAMEPREXZZB
     F                                              KINFSR *PSSR
     FPREX3ZZ UF  E                    DISK                      A
     F            PREXZZF                           KRENAMEPREXZZC
     F                                              KINFSR *PSSR
     FPREX4ZZ UF  E                    DISK                      A
     F            PREXZZF                           KRENAMEPREXZZD
     F                                              KINFSR *PSSR
     FPREX1PH O   E                    DISK
     F            PREXPHF                           KRENAMEPREXPHA
     F                                              KINFSR *PSSR
     FPREX2PH O   E                    DISK
     F            PREXPHF                           KRENAMEPREXPHB
     F                                              KINFSR *PSSR
     FPREX3PH O   E                    DISK
     F            PREXPHF                           KRENAMEPREXPHC
     F                                              KINFSR *PSSR
     FPREX4PH O   E                    DISK
     F            PREXPHF                           KRENAMEPREXPHD
     F                                              KINFSR *PSSR
     FFF0307AUO   E             65     PRINTER
     F*
      /SPACE 3
     E* Array containing Copyright Statement
     E                    CPY@    1   1 80
     E*
     E* Tables and Arrays
     E/COPY WNCPYSRC,FF0307E001
     E                    TABMTI  4   4  2 0 TABN    1 0 MSG Type/Index
     E/COPY WNCPYSRC,FF0307E002
     E                    INT         4 15 0             First 15
     E                    DEC         4  3 0             Last 3
     E                    TOT         4  5 0             MSG Type Totals
      /SPACE 3
     I*
     ISDBANK    E DSSDBANKPD
     I* External DS for Bank details
     I*
     IDSFDY     E DSDSFDY
     I* First DS for access programs, short data structure
     I*
     ILDA       E DSLDA
     I* Data area giving installation control details
     I*
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     I/EJECT
     C****************************************************************
     C* MAIN PROCESSING                                              *
     C*                                                              *
     C* Calls     : INIT, PROCES, UPDAT                              *
     C****************************************************************
     C                     EXSR INIT
     C                     EXSR PROCES
     C                     EXSR UPDAT
     C/EJECT
     C****************************************************************
     C* SR/INIT   : To perform all initialisation processing         *
     C*                                                              *
     C* Called by : Main Processing                                  *
     C* Calls     : *PSSR                                            *
     C****************************************************************
     C           INIT      BEGSR
     C*
     C** Set up copyright parameter
     C*
     C                     MOVEACPY@      ACT@   80
     C*
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           AGDFF     IFEQ 'M'
     C                     SETON                     98
     C                     END
     C** Set up LDA
     C*
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'FF0307'  DBPGM
     C                     MOVE *BLANKS   DBASE
     C*
     C** Call AOBANKR0 to access the bank's ICD
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG'    @RTCD   7
     C                     PARM '*FIRST'  @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C** Check for Data base error in file
     C*
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD01        DBASE            *************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 01*
     C                     MOVEL'*FIRST'  DBKEY            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C                     ENDSR
     C****************************************************************
     C* SR/PROCES : Process FFOTPRPD and calculate totals            *
     C*                                                              *
     C* Called by : Main Processing                                  *
     C* Calls     : GLZADD                                           *
     C****************************************************************
     C           PROCES    BEGSR
     C*
     C                     READ FFOTPRPD                 85
     C*
     C** Do While not End of File
     C*
     C           *IN85     DOWEQ*OFF
     C*
     C** Write a record to PREXnPH
     C*
     C                     SELEC
     C           MTIN      WHEQ 09
     C                     WRITEPREXPHA
     C           MTIN      WHEQ 10
     C                     WRITEPREXPHB
     C           MTIN      WHEQ 11
     C                     WRITEPREXPHC
     C           MTIN      WHEQ 12
     C                     WRITEPREXPHD
     C                     ENDSL
     C*
     C           MTIN      LOKUPTABMTI    TABN           84
     C                     Z-ADDTABN      N       10
     C*
     C** Increment the count of records written to PREXnPH by one
     C** and also the number of records read from FFOTPRPD
     C*
     C           TOT,N     ADD  1         TOT,N
     C           TOTT      ADD  1         TOTT    50
     C*
     C** Maintain hash totals
     C*
     C           EAMT      DIV  1000      ZZAMT
     C*
     C                     Z-ADDINT,N     ZZTOTI
     C                     Z-ADDDEC,N     ZZTOTD
     C                     EXSR GLZADD
     C                     Z-ADDZZTOTI    INT,N
     C                     Z-ADDZZTOTD    DEC,N
     C*
     C** Update record ID on FFOTPRPD to 'P'
     C*
     C                     MOVEL'P'       RECI
     C                     UPDATFFOTPRPF
     C*
     C                     READ FFOTPRPD                 85
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C****************************************************************
     C* SR/UPDAT  : Update Dealing Module pay/receive extract files  *
     C*                                                              *
     C* Called by : Main Processing                                  *
     C* Calls     : None                                             *
     C****************************************************************
     C           UPDAT     BEGSR
     C*
     C** Update PREX1ZZ
     C*
     C                     READ PREX1ZZ                  86
     C           *IN86     IFEQ *ON
     C                     MOVE 'T'       RECI
     C                     Z-ADDTOT,1     NORE
     C                     Z-ADDDEC,1     HRDC
     C                     Z-ADDINT,1     HRWN
     C                     WRITEPREXZZA
     C                     ELSE
      *                                                                                       189053
     C                     Z-ADDHRWN      ZZTOTI                                              189053
     C                     Z-ADDHRDC      ZZTOTD                                              189053
     C                     Z-ADDINT,1     ZZAMTI                                              189053
     C                     Z-ADDDEC,1     ZZAMTD                                              189053
     C                     EXSR GLZSUM                                                        189053
     C                     Z-ADDZZTOTI    HRWN                                                189053
     C                     Z-ADDZZTOTD    HRDC                                                189053
      *                                                                                       189053
     C                     ADD  TOT,1     NORE
     C**********           ADD  DEC,1     HRDC                                                189053
     C**********           ADD  INT,1     HRWN                                                189053
     C                     UPDATPREXZZA
     C                     ENDIF
     C*
     C** Update PREX2ZZ
     C*
     C                     READ PREX2ZZ                  87
     C           *IN87     IFEQ *ON
     C                     MOVE 'T'       RECI
     C                     Z-ADDTOT,2     NORE
     C                     Z-ADDDEC,2     HRDC
     C                     Z-ADDINT,2     HRWN
     C                     WRITEPREXZZB
     C                     ELSE
      *                                                                                       189053
     C                     Z-ADDHRWN      ZZTOTI                                              189053
     C                     Z-ADDHRDC      ZZTOTD                                              189053
     C                     Z-ADDINT,2     ZZAMTI                                              189053
     C                     Z-ADDDEC,2     ZZAMTD                                              189053
     C                     EXSR GLZSUM                                                        189053
     C                     Z-ADDZZTOTI    HRWN                                                189053
     C                     Z-ADDZZTOTD    HRDC                                                189053
      *                                                                                       189053
     C                     ADD  TOT,2     NORE
     C**********           ADD  DEC,2     HRDC                                                189053
     C**********           ADD  INT,2     HRWN                                                189053
     C                     UPDATPREXZZB
     C                     ENDIF
     C*
     C** Update PREX3ZZ
     C*
     C                     READ PREX3ZZ                  88
     C           *IN88     IFEQ *ON
     C                     MOVE 'T'       RECI
     C                     Z-ADDTOT,3     NORE
     C                     Z-ADDDEC,3     HRDC
     C                     Z-ADDINT,3     HRWN
     C                     WRITEPREXZZC
     C                     ELSE
      *                                                                                       189053
     C                     Z-ADDHRWN      ZZTOTI                                              189053
     C                     Z-ADDHRDC      ZZTOTD                                              189053
     C                     Z-ADDINT,3     ZZAMTI                                              189053
     C                     Z-ADDDEC,3     ZZAMTD                                              189053
     C                     EXSR GLZSUM                                                        189053
     C                     Z-ADDZZTOTI    HRWN                                                189053
     C                     Z-ADDZZTOTD    HRDC                                                189053
      *                                                                                       189053
     C                     ADD  TOT,3     NORE
     C**********           ADD  DEC,3     HRDC                                                189053
     C**********           ADD  INT,3     HRWN                                                189053
     C                     UPDATPREXZZC
     C                     ENDIF
     C*
     C** Update PREX4ZZ
     C*
     C                     READ PREX4ZZ                  89
     C           *IN89     IFEQ *ON
     C                     MOVE 'T'       RECI
     C                     Z-ADDTOT,4     NORE
     C                     Z-ADDDEC,4     HRDC
     C                     Z-ADDINT,4     HRWN
     C                     WRITEPREXZZD
     C                     ELSE
      *                                                                                       189053
     C                     Z-ADDHRWN      ZZTOTI                                              189053
     C                     Z-ADDHRDC      ZZTOTD                                              189053
     C                     Z-ADDINT,4     ZZAMTI                                              189053
     C                     Z-ADDDEC,4     ZZAMTD                                              189053
     C                     EXSR GLZSUM                                                        189053
     C                     Z-ADDZZTOTI    HRWN                                                189053
     C                     Z-ADDZZTOTD    HRDC                                                189053
      *                                                                                       189053
     C                     ADD  TOT,4     NORE
     C**********           ADD  DEC,4     HRDC                                                189053
     C**********           ADD  INT,4     HRWN                                                189053
     C                     UPDATPREXZZD
     C                     ENDIF
     C*
     C** Write the audit report
     C*
     C                     Z-ADDINT,1     INT1
     C                     Z-ADDINT,2     INT2
     C                     Z-ADDINT,3     INT3
     C                     Z-ADDINT,4     INT4
     C                     Z-ADDDEC,1     DEC1
     C                     Z-ADDDEC,2     DEC2
     C                     Z-ADDDEC,3     DEC3
     C                     Z-ADDDEC,4     DEC4
     C                     Z-ADDTOT,1     TOT1
     C                     Z-ADDTOT,2     TOT2
     C                     Z-ADDTOT,3     TOT3
     C                     Z-ADDTOT,4     TOT4
     C*
     C                     WRITEFF0307AH
     C                     WRITEFF0307D1
     C*
     C                     MOVE *ON       *INLR
     C                     RETRN
     C*
     C                     ENDSR
     C/EJECT
     C****************************************************************
     C* SR/GLZADD : Subroutine to add an amount (ZZAMT) to the       *
     C*             total (ZZTOTI,ZZTOTD) IND '99' is set by an      *
     C*             Overflow error                                   *
     C*                                                              *
     C* Called by : PROCES                                           *
     C* Calls     : GLZSUM                                           *
     C****************************************************************
     C           GLZADD    BEGSR
     C*
     C                     Z-ADDZZAMT     ZZAMT  153     91-DEFINE ZZAMT
     C   91                GOTO ZZAEND                     AMT = 0.BYPASS
     C*
     C* SPLIT ZZAMT INTO INTEGER AND DECIMAL FIELDS
     C                     Z-ADDZZAMT     ZZAMTI 150       INTEGER FIELD
     C                     MOVE ZZAMT     ZZAMTD  30       DECIMAL FIELD
     C* BOTH ZZAMTI AND ZZAMTD CONTAIN A 'SIGN' ZONE NOW
     C*
     C                     EXSR GLZSUM
     C           ZZAEND    ENDSR
     C*
     C/EJECT
     C****************************************************************
     C* SR/GLZSUM : Subroutine to carry out the addition for         *
     C*             subroutines GLZADD, GLZSUB, GLZCMP.              *
     C*             Parameters passed - I/P ZZAMTI ZZAMTD            *
     C*                                 O/P ZZTOTI ZZTOTD ZZNEGD     *
     C*                                 IND 96 IND 99.               *
     C*                                                              *
     C* Called by : GLZADD                                           *
     C* Calls     : None                                             *
     C****************************************************************
     C           GLZSUM    BEGSR
     C*
     C                     Z-ADDZZTOTI    ZZTOTI 150       DEFINE ZZTOTI
     C                     Z-ADDZZTOTD    ZZTOTD  30       DEFINE ZZTOTD
     C                     SETOF                     919293
     C                     SETOF                     949599
     C*
     C*    DETERMINE SIGN OF ZZAMTI & D,   92 IF NEG
     C           ZZAMTI    COMP 0                      9293
     C   93      ZZAMTD    COMP 0                      9293
     C   93                GOTO ZZSEND                     ZERO BYPASS
     C*
     C*    DETERMINE SIGN OF ZZTOTI & D, 91 IF NEG.
     C           ZZTOTI    COMP 0                      9193
     C   93      ZZTOTD    COMP 0                      9193
     C*
     C*    IF ZZTOTAL IS ZERO, RETURN ZZAMOUNT.
     C   93                Z-ADDZZAMTI    ZZTOTI
     C   93                Z-ADDZZAMTD    ZZTOTD
     C   93                GOTO ZZSEND                     ZERO BYPASS
     C*    IF SIGNS DIFFER BYPASS OVERFLOW CHECKS.
     C   91N92
     CORN91 92             GOTO ZZOFPS
     C*
     C           ZZAMTD    ADD  ZZTOTD    ZZWK2   40
     C           ZZWK2     COMP 999                  93    '93' = CARRY
     C  N93      ZZWK2     COMP -999                   93    INTO INTEGER.
     C*
     C   93N92   ZZAMTI    ADD  1         ZZWK3  150       ADD 'CARRY' +VE
     C      93 92ZZAMTI    SUB  1         ZZWK3            SUB 'CARRY' -VE
     C   93      ZZTOTI    ADD  ZZWK3     ZZWK3
     C  N93      ZZTOTI    ADD  ZZAMTI    ZZWK3
     C*
     C* IF THE MODULUS OF ZZWK3 IS LT MOD. ZZTOTI THEN O/F HAS OCCURED
     C  N92      ZZWK3     COMP ZZTOTI                 99  -92 MEANS NOS.
     C   92      ZZWK3     COMP ZZTOTI               99     NEGATIVE
     C  N99                Z-ADDZZWK2     ZZTOTD
     C  N99                Z-ADDZZWK3     ZZTOTI
     C*
     C* IF O/F ZEROIZE ZZAMT , IND '99' SET AND ZZTOT FIELDS LEFT INTACT.
     C   99                Z-ADD0         ZZAMT  153
     C                     GOTO ZZSEND
     C*
     C* THE 'SIGNS' ARE DIFFERENT
     C           ZZOFPS    TAG                             ** ZZOFPS TAG**
     C* IF ZZAMT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZTOT
     C*
     C   92                Z-SUBZZAMTI    ZZAMTI 150
     C   92                Z-SUBZZAMTD    ZZAMTD  30
     C*
     C* IF ZZTOT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZAMT.
     C*
     C   91                Z-SUBZZTOTI    ZZTOTI
     C   91                Z-SUBZZTOTD    ZZTOTD
     C* BOTH ZZAMT AND ZZTOT ARE NOW POSITIVE.
     C*
     C           ZZTOTI    COMP ZZAMTI               93  95
     C   95      ZZTOTD    COMP ZZAMTD               93  95
     C*
     C* IF ZZTOT EQ. ZZAMT RETURN ZERO.
     C   95                Z-ADD0         ZZTOTI
     C   95                Z-ADD0         ZZTOTD
     C   95                GOTO ZZSEND
     C*
     C* IF ZZTOT GT. ZZAMT.
     C   93      ZZAMTD    COMP ZZTOTD               94
     C   93 94   ZZTOTI    SUB  1         ZZTOTI
     C   93 94   ZZTOTD    ADD  1000      ZZWK2
     C   93      ZZTOTI    SUB  ZZAMTI    ZZTOTI
     C   93 94   ZZWK2     SUB  ZZAMTD    ZZTOTD
     C   93N94   ZZTOTD    SUB  ZZAMTD    ZZTOTD
     C*
     C*
     C* IF ZZAMT GT. ZZTOT.
     C  N93      ZZTOTD    COMP ZZAMTD               94
     C  N93 94   ZZAMTI    SUB  1         ZZWK3  150
     C  N93 94   ZZAMTD    ADD  1000      ZZWK2
     C  N93 94   ZZWK3     SUB  ZZTOTI    ZZTOTI
     C  N93N94   ZZAMTI    SUB  ZZTOTI    ZZTOTI
     C  N93 94   ZZWK2     SUB  ZZTOTD    ZZTOTD
     C  N93N94   ZZAMTD    SUB  ZZTOTD    ZZTOTD
     C*
     C* REVERSE SIGN OF ZZTOT IF LARGER I/P FIELDS WERE NEGATIVE
     C                     SETOF                     94
     C   93 91
     CORN93 92             SETON                     94
     C   94                Z-SUBZZTOTI    ZZTOTI
     C   94                Z-SUBZZTOTD    ZZTOTD
     C**
     C**   RESTORE SIGN OF ZZAMTI & ZZAMTD IF IT WAS REVERSED.
     C   92                Z-SUBZZAMTI    ZZAMTI
     C   92                Z-SUBZZAMTD    ZZAMTD
     C           ZZSEND    TAG                             ** ZZSEND TAG *
     C**
     C**   IF ZZTOTD IS ZERO, AND ZZTOTI IS NEG. SET UP ZZNEGD.
     C                     SETOF                     96
     C           ZZTOTD    COMP 0                        91
     C   91      ZZTOTI    COMP 0                      96
     C   96                MOVE '.000-'   ZZNEGD  5
     C                     ENDSR
     C/EJECT
     C****************************************************************
     C* SR/*PSSR  : Program exception error routine                  *
     C*                                                              *
     C* Called by : INIT                                             *
     C* Calls     : None                                             *
     C****************************************************************
     C           *PSSR     BEGSR
     C*
     C** Write error details to error report
     C*
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     WRITEFF0307AH
     C                     WRITEFF0307AE
     C                     DUMP
     C*
     C** End program
     C*
     C                     ENDIF
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C                     ENDSR
      *
** CPY@
(c) Finastra International Limited 2001
** TABMTI/TABN - MESSAGE TYPE/INDEX
     X/COPY WNCPYSRC,FF0307X001
091102113124
