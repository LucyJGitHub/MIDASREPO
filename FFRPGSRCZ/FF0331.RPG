     H        1
      ****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FF Update pay/receive indicators on OTCs')
      *****************************************************************
      *                                                              *
      *  Midas - Dealing Module                                      *
      *                                                              *
      *  FF0331 - Update Pay/Receive Indicator on OTC's              *
      *                                                              *
      *  Function:  This program updates the pay/receive indicator   *
      *             on TRANSD for each trade recorded on the OTC     *
      *             pay/receive extract file.                        *
      *                                                              *
      *  Called By: DLC0211 - Input cycle pay/receive processing.    *
      *             DLC04   - Input cycle termination confirmation   *
      *                       and pay/receive processing.            *
      *             FFC1710 - OTC Pay/receive reporting (COB).       *
      *                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CAS002             Date 14Jan02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.01 Patch E ---------------------------------------*
      *                 170520             Date 19Nov99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPL002             Date 08Mar99               *
      *                    CFF003 *CREATE     DATE 14APR97           *
      *                                                              *
      ****************************************************************
      *                                                              *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CAS002 - Hedge Strategy/Linkage (Recompile)                  *
      *     170520 -  Recompile over changed TRANSD file             *
      *     CPL002  - Midas-Plato Interface (Recompiled)             *
      *     CFF003  - ENHANCED OTC PROCESSING - PHASE II             *
      *               UPGRADE FROM BLI LBL001                        *
      *                                                              *
      ****************************************************************
      * INDICATORS USED                                              *
      * ~~~~~~~~~~~~~~~                                              *
      * 88 - EOF for FFOTPRPD                                        *
      * 89 - EOF for TRANSD                                          *
      *                                                              *
      * U7 - Program Error                                           *
      * U8 - Database error                                          *
      * LR - Last record                                             *
      ****************************************************************
     F/EJECT
     FFFOTPRPDUF  E                    DISK
     F                                              KINFSR *PSSR
     FTRANS2  UF  E           K        DISK
     F                                              KINFSR *PSSR
     FFF0331AUO   E             65     PRINTER
     F*
      /SPACE 3
     E* Array containing Copyright Statement
     E                    CPY@    1   1 80
     E*
      /SPACE 3
     I*
     ISDBANK    E DSSDBANKPD
     I* External DS for Bank details
     I*
     IDSFDY     E DSDSFDY
     I* First DS for access programs, short data structure
     I*
     ILDA       E DSLDA
     I* Data area giving installation control details
     I*
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     I/EJECT
     C****************************************************************
     C* MAIN PROCESSING                                              *
     C*                                                              *
     C* Calls     : INIT, UPDAT                                      *
     C****************************************************************
     C                     EXSR INIT
     C                     EXSR UPDAT
     C/EJECT
     C****************************************************************
     C* SR/INIT   : To perform all initialisation processing         *
     C*                                                              *
     C* Called by : Main Processing                                  *
     C* Calls     : *PSSR                                            *
     C****************************************************************
     C           INIT      BEGSR
     C*
     C** Set up copyright parameter
     C*
     C                     MOVEACPY@      ACT@   80
     C*
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           AGDFF     IFEQ 'M'
     C                     SETON                     98
     C                     END
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
     C** Set up LDA
     C*
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'FF0331'  DBPGM
     C                     MOVE *BLANKS   DBASE
     C*
     C** Call AOBANKR0 to access the bank's ICD
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG'    @RTCD   7
     C                     PARM '*FIRST'  @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C** Check for Data base error in file
     C*
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD01        DBASE            *************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 01*
     C                     MOVEL'*FIRST'  DBKEY            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C                     ENDSR
     C/EJECT
     C****************************************************************
     C* SR/UPDAT  : Update pay/receive indicator on TRANSD           *
     C*                                                              *
     C* Called by : Main Processing                                  *
     C* Calls     : None                                             *
     C****************************************************************
     C           UPDAT     BEGSR
     C*
     C                     Z-ADD0         TOT     50
     C*
     C                     READ FFOTPRPD                 88
     C*
     C** Do While not End of File
     C*
     C           *IN88     DOWEQ*OFF
     C*
     C** Check the record id
     C*
     C           RECI      IFEQ 'P'
     C*
     C** Update the pay/receive indicator to 'Y'
     C** NB. TRANS2 is used because TRANS is overridden to DTRIN
     C*
     C           DLNO      CHAINTRANS2               89
     C           *IN89     IFEQ *OFF
     C                     MOVE 'Y'       PYRC
     C                     UPDATTRANSDF
     C                     ENDIF
     C*
     C** Delete the record on FFOTPRPD
     C*
     C                     DELETFFOTPRPF
     C*
     C** Increment count of records deleted by one
     C*
     C           TOT       ADD  1         TOT
     C*
     C                     ENDIF
     C*
     C                     READ FFOTPRPD                 88
     C*
     C                     ENDDO
     C*
     C** Write the audit report
     C*
     C                     WRITEFF0331AH
     C                     WRITEFF0331D1
     C*
     C                     MOVE *ON       *INLR
     C                     RETRN
     C*
     C                     ENDSR
     C/EJECT
     C****************************************************************
     C* SR/*PSSR  : Program exception error routine                  *
     C*                                                              *
     C* Called by : INIT                                             *
     C* Calls     : None                                             *
     C****************************************************************
     C           *PSSR     BEGSR
     C*
     C** Write error details to error report
     C*
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     WRITEFF0331AH
     C                     WRITEFF0331AE
     C                     DUMP
     C*
     C** End program
     C*
     C                     ENDIF
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C                     ENDSR
      *
** CPY@
(c) Misys International Banking Systems Ltd. 2001
