     H        1                                                           S01117
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FF New Customer Broker Positions')
     H***********                                                         S01117
     F********************************************************************
     F*                                                                  *
     F*  Midas - Futures and Options Module
     F*                                                                  *
     F*   FF0430  -  NEW CUSTOMER BROKER POSITIONS.                      *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.01 Patch E ---------------------------------------*
      *  Prev Amend No. 171172             Date 25Nov99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 CFF004             Date  12SEP96              *
     F*                  S01411             DATE  16APR93                *
     F*                  S01117             DATE  22JAN90                *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*                                                                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*   171172 - Recompile over changed POSTNCD/POSTNKD             *
     F*   CFF004 - Increase in size of Price Fields                      *
     A*   S01411 - RECOMPILED BECAUSE 'ORIGINAL ENTRY DATE' HAS BEEN     *
     A*            ADDED TO TRANSD.                                      *
     F*   S01117  -  MULTIBRANCHING                                      *
     F*                                                                  *
     F********************************************************************
     F*
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FPOCHG2  UP  E           K        DISK
     F*
     FPOSTNC  UF  E           K        DISK
     FTSMKT   UF  E           K        DISK                      A
     FTRANS   IF  E           K        DISK
      /SPACE 2
     F********************************************************************
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*             L8   CHANGE OF BRANCH CODE OF TRADE ON POCHG2.
     F*             L7   CHANGE OF CUSTOMER BROKER CODE ON POCHG2.
     F*             L6   CHANGE OF INSTRUMENT CURRENCY ON POCHG2.
     F*             L5   CHANGE OF INSTRUMENT TYPE ON POCHG2.
     F*             L4   CHANGE OF YEAR NUMBER ON POCHG2.
     F*             L3   CHANGE OF MONTH NUMBER ON POCHG2.
     F*             L2   CHANGE OF PUT/CALL ON POCHG2.
     F*             L1   CHANGE OF STRIKE PRICE ON POCHG2.
     F*
     F*
     F*    01            POCHGCC RECORD
     F*    02            POCHGRC RECORD
     F*    03            POCHGTC RECORD
     F*
     F*        50        RECORD NOT FOUND ON TSMKT
     F*
     F*     80-89        Reserved for Standard FF Subroutines
     F*
     F*     90-99        Reserved for Standard MIDAS Subroutines
     F*
     F*        99        GENERAL CHAIN/READ FAIL INDICATOR FOR DB ERRORS
     F*
     F*     U7+U8        Database Errors
     F*
     F********************************************************************
     E                    CPY@    1   1 80
     IPOCHGCCF    01
     I***********                                   BRCD  L8              S01117
     I                                              BRCA  L8              S01117
     I                                              CBCD  L7
     I                                              ISCY  L6
     I                                              ISTT  L5
     I                                              YRNO  L4
     I                                              MTHN  L3
     I                                              PCAL  L2
     I                                              STRP  L1
     IPOCHGRCF    02
     I***********                                   BRCD  L8              S01117
     I                                              BRCA  L8              S01117
     I                                              CBCD  L7
     I                                              ISCY  L6
     I                                              ISTT  L5
     I                                              YRNO  L4
     I                                              MTHN  L3
     I                                              PCAL  L2
     I                                              STRP  L1
     IPOCHGTCF    03
     I***********                                   BRCD  L8              S01117
     I                                              BRCA  L8              S01117
     I                                              CBCD  L7
     I                                              ISCY  L6
     I                                              ISTT  L5
     I                                              YRNO  L4
     I                                              MTHN  L3
     I                                              PCAL  L2
     I                                              STRP  L1
     I*DA********UDS                            256                       S01117
     ILDA         DS                            256                       S01117
     I*  LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I** *LOCK IN LDA immediately before and OUT LDA immediately          S01117
     I** after each database handling.                                    S01117
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 1770DBASE                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I**                                                                  S01117
     IRUNDT       DS                                                      S01117
     I** DATA AREA RUNDAT                                                 S01117
     I                                        1   7 RUNA                  S01117
     I                                    P   8  100RUND                  S01117
     I                                       11  11 SSF                   S01117
     I                                       12  12 DATF                  S01117
     I                                       13  13 MBIN                  S01117
      **                                                                  S01117
     I* POSTNC KEY TO PUT IN LDA FOR DATABASE ERRORS.
     IPOSKEY      DS
     I***********                             1   30BRCD                  S01117
     I                                        1   3 BRCA                  S01117
     I**********                              4   90CBCD                                      CSD027
     I                                        4   9 CBCD                                      CSD027
     I                                       10  14 ISTT
     I                                       15  160YRNO
     I                                       17  180MTHN
     I                                       19  19 PCAL
     I***********                            20  307STRP                  CFF004
     I                                       20  348STRP                  CFF004
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     C*
      ** RECEIVE MARKET END OF CENTRE PARAMETER
     C*
     C           *ENTRY    PLIST
     C                     PARM           MKTEOC  2
     C*
      ** KEY LIST FOR FILE POSTNC
     C*
     C           POSTCK    KLIST
     C***********          KFLD           BRCD                            S01117
     C                     KFLD           BRCA                            S01117
     C                     KFLD           CBCD
     C                     KFLD           ISTT
     C                     KFLD           YRNO
     C                     KFLD           MTHN
     C                     KFLD           PCAL
     C                     KFLD           STRP
     C*
      ** KEY LIST FOR FILE TSMKT
     C*
     C           TSMKTK    KLIST
     C***********          KFLD           BRCD                            S01117
     C                     KFLD           BRCA                            S01117
     C                     KFLD           CBCD
     C                     KFLD           CCY
     C*
     C           *LIKE     DEFN TOPL      RLPLT
     C           *LIKE     DEFN CMTM      TCOM
     C           *LIKE     DEFN CHTM      TCHG
     C           *LIKE     DEFN PMTM      TPRM
     C           *LIKE     DEFN UPLM      ULPLT
     C*
      ** SET UP PROGRAM NAME IN LDA ON FIRST CYCLE
     C*
     C           CYCLE     IFNE 'NOTFIRST'
     C                     MOVE 'NOTFIRST'CYCLE   8
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE 'FF0430  'DBPGM
     C                     OUT  LDA                                       S01117
     C*                                                                   S01117
      ** IN THE DATAAREA RUNDAT                                           S01117
     C*                                                                   S01117
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           S01117
     C                     IN   RUNDT                                     S01117
     C                     END
     C*
     C                     ADD  1         RECS    60
     C*
      ** PROCESS POCHG2 RECORD TYPES
     C*
     C           *IN01     CASEQ'1'       POCGCP
     C*
     C           *IN02     CASEQ'1'       POCGRP
     C*
     C           *IN03     CASEQ'1'       POCGTP
     C*
     C                     END
     C********************************************************************
      /EJECT
     C*
      ** UPDATE POSTNC FILE
     C*
     CL1         RECS      IFGE 1
     CL1                   EXSR CHGIST
     CL1                   END
     C*
      ** UPDATE TSMKT FILE
     C*
     CL6         RECS      IFGE 1
     CL6                   EXSR CHGCCY
     CL6                   END
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
      ** S/R POCGCP TO PROCESS POCHGCC RECORDS (CLOSURE REALISED P/L)
     C*
      ** CALLED FROM  MAIN
     C*
      ** CALLS NO OTHER SUBROUTINES
     C*
     C* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
     C*
     C           POCGCP    BEGSR
     C*
      ** IF THE REVERSAL INDICATOR ON POCHG2 IS 0
      **  ADD THE REALIZED PROFIT/LOSS TO
      **  THE TOTAL OF REALIZED PROFIT/LOSS CHANGES FOR THE POSITION
      **  ELSE
      **  SUBTRACT THE REALIZED PROFIT/LOSS FROM
      **  THE TOTAL OF REALIZED PROFIT/LOSS CHANGES FOR THE POSITION
     C*
     C           REVI      IFEQ 0
     C                     ADD  RLPL      RLPLT
     C                     ELSE
     C                     SUB  RLPL      RLPLT
     C                     END
     C*
      ** SET THE POSITION UPDATE INDICATOR TO 'Y'
      ** AND UPDATE THIS FIELD ON THE FORMAT.
     C*
     C                     MOVE 'Y'       PUPI
     C*
     C                     EXCPTRPOCHC
     C*
     C                     ENDSR
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
      ** S/R POCGRP TO PROCESS POCHGRC RECORDS ( REVALUATION UNREALISED P/L
     C*
      ** CALLED FROM  MAIN
     C*
      ** CALLS NO OTHER SUBROUTINES
     C*
     C* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
     C*
     C           POCGRP    BEGSR
     C*
      **  ADD THE UNREALIZED PROFIT/LOSS TO
      **  THE TOTAL OF UNREALIZED PROFIT/LOSS CHANGES FOR THE POSITION
     C*
     C                     ADD  ULPL      ULPLT
     C*
      ** SET THE POSITION UPDATE INDICATOR TO 'Y'
      ** AND UPDATE THIS FIELD ON THE FORMAT.
     C*
     C                     MOVE 'Y'       PUPI
     C*
     C                     EXCPTRPOCHR
     C*
     C                     ENDSR
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
      ** S/R POCGTP TO PROCESS POCHGTC RECORDS (TRANSACTION CHANGES)
     C*
      ** CALLED FROM  MAIN
     C*
      ** CALLS NO OTHER SUBROUTINES
     C*
     C* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
     C*
     C           POCGTP    BEGSR
     C*
      ** POSITION CHANGE TYPE IS 'A' (CHARGES)
     C*
     C           PCGT      IFEQ 'A'
     C*
      ** IF THE REVERSAL INDICATOR ON POCHG2 IS 0
      **  ADD THE POSITION CHANGE AMOUNT TO
      **  THE TOTAL OF CHARGES CHANGE AMOUNTS FOR THE POSITION
      **  ELSE
      **  SUBTRACT THE POSITION CHANGE AMOUNT FROM
      **  THE TOTAL OF CHARGES CHANGE AMOUNTS FOR THE POSITION
     C*
     C           REVI      IFEQ 0
     C                     ADD  PCGA      TCHG
     C                     ELSE
     C                     SUB  PCGA      TCHG
     C                     END
     C*
     C                     END
     C*
      ** POSITION CHANGE TYPE IS 'B' (COMMISSIONS)
     C*
     C           PCGT      IFEQ 'B'
     C*
      ** IF THE REVERSAL INDICATOR ON POCHG2 IS 0
      **  ADD THE POSITION CHANGE AMOUNT TO
      **  THE TOTAL OF COMMISSION CHANGE AMOUNTS FOR THE POSITION
      **  ELSE
      **  SUBTRACT THE POSITION CHANGE AMOUNT FROM
      **  THE TOTAL OF COMMISSION CHANGE AMOUNTS FOR THE POSITION
     C*
     C           REVI      IFEQ 0
     C                     ADD  PCGA      TCOM
     C                     ELSE
     C                     SUB  PCGA      TCOM
     C                     END
     C*
     C                     END
     C*
      ** POSITION CHANGE TYPE IS 'C'  (PREMIUMS)
     C*
     C           PCGT      IFEQ 'C'
     C*
      ** ACCESS TRANS FILE FOR TRANSACTION TYPE
     C*
     C           TNBR      CHAINTRANS                99
     C*
     C           *IN99     IFEQ '1'
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE 'TRANS'   DBFILE
     C                     MOVELTNBR      DBKEY            ***************
     C                     Z-ADD2         DBASE            * DB ERROR 02 *
     C                     OUT  LDA                        ***************S01117
     C                     EXSR DBERR                      ***************
     C                     END
     C*
      ** IF THE REVERSAL INDICATOR ON POCHG2 IS 0
      **  ADD THE POSITION CHANGE AMOUNT TO ( IF NECESSARY )
      **  THE TOTAL OF PREMIUM CHANGE AMOUNTS FOR THE POSITION
      **  THE TOTAL OF REALISED PROFIT/LOSS AMOUNTS FOR THE POSITION
      **  ELSE
      **  SUBTRACT THE PREMIUM CHANGE AMOUNT FROM ( IF NECESSARY )
      **  THE TOTAL OF PREMIUM CHANGE AMOUNTS FOR THE POSITION
      **  THE TOTAL OF REALISED PROFIT/LOSS AMOUNTS FOR THE POSITION
     C*
     C           REVI      IFEQ 0
     C*
     C** IF THE APPEARED ON STATEMENT IND IS 'N'
     C*
     C           AOSI      IFEQ 'N'
     C                     ADD  PCGA      TPRM
     C                     END
     C*
     C** IF THE TRANSACTION IS AN EXERCISE THIS IS ALSO REALISED P/L
     C*
     C           TRTY      IFEQ 'E'
     C                     ADD  PCGA      RLPLT
     C                     END
     C*
     C                     ELSE
     C*
     C** IF THE APPEARED ON STATEMENT IND IS 'N'
     C*
     C           AOSI      IFEQ 'N'
     C                     SUB  PCGA      TPRM
     C                     END
     C*
     C** IF THE TRANSACTION IS AN EXERCISE THIS IS ALSO REALISED P/L
     C*
     C           TRTY      IFEQ 'E'
     C                     SUB  PCGA      RLPLT
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
      ** SET THE POSITION UPDATE INDICATOR TO 'Y'
      ** AND UPDATE THIS FIELD ON THE FORMAT.
     C*
     C                     MOVE 'Y'       PUPI
     C*
     C                     EXCPTRPOCHT
     C*
     C                     ENDSR
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
      ** S/R CHGIST TO UPDATE POSTNC FILE.
     C*
      ** CALLED FROM
     C*
      ** CALLS NO OTHER SUBROUTINES
     C*
     C* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
     C*
     C           CHGIST    BEGSR
     C*
      ** IF AT LEAST ONE POCHG2 RECORD PROCESSED
      **      ACCESS RELEVANT POSTNC RECORD.
      **      ADD ACCUMULATED TOTALS INTO CORRESPONDING POSTNC FIELDS
      **      UPDATE POSTNC RECORD.
      **      RESET PROGRAM ACCUMULATOR FIELDS TO ZERO.
     C*
     C           POSTCK    CHAINPOSTNC               99
     C*
     C           *IN99     IFEQ '1'
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVEL'POSTNC'  DBFILE
     C                     MOVELPOSKEY    DBKEY            ***************
     C                     Z-ADD1         DBASE            * DB ERROR 01 *
     C                     OUT  LDA                        ***************S01117
     C                     EXSR DBERR                      ***************
     C                     END
     C*
     C** UPDATE TOTAL P/L TO DATE
     C*
     C                     ADD  RLPLT     TOPL
     C*
     C** UPDATE MONTH TO DATE TOTALS
     C*
     C                     ADD  TCOM      CMTM
     C                     ADD  TCHG      CHTM
     C                     ADD  TPRM      PMTM
     C                     ADD  ULPLT     UPLM
     C                     ADD  RLPLT     RPLM
     C*
     C                     EXCPTRPOSTN
     C*
     C                     Z-ADD0         TCOM
     C                     Z-ADD0         TCHG
     C                     Z-ADD0         TPRM
     C                     Z-ADD0         ULPLT
     C                     Z-ADD0         RLPLT
     C*
     C                     ENDSR
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
      ** S/R CHGCCY TO UPDATE TSMKT FILE
     C*
      ** CALLED FROM
     C*
      ** CALLS NO OTHER SUBROUTINES
     C*
     C* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
     C*
     C           CHGCCY    BEGSR
     C*
      ** IF AT LEAST ONE POCHG2 RECORD PROCESSED
      **      ACCESS RELEVANT TSMKT RECORD.
     C*
     C                     MOVE ISCY      CCY
     C*
     C           TSMKTK    CHAINTSMKT                50
     C*
      ** IF BROKER INDICATOR ON LAST ACCESSED POSTNC RECORD IS 'Y'
      **      SET END OF CENTRE INDICATOR FOR TSMKT TO 'B'
      ** ELSE
      **      SET END OF CENTRE INDICATOR FOR TSMKT TO 'C'
     C*
     C           BRKI      IFEQ 'Y'
     C                     MOVE 'B'       EOCI
     C                     ELSE
     C                     MOVE 'C'       EOCI
     C                     END
     C*
      ** IF TSMKT RECORD FOUND UPDATE END OF CENTRE INDICATOR
      **  ELSE WRITE NEW RECORD FOR TSMKT.
     C*
     C           *IN50     IFEQ '0'
     C                     EXCPTRTSMKT
     C                     ELSE
     C                     MOVE MKTEOC    MRKT
     C                     MOVE ISCY      CCY
     C                     Z-ADD0         MGRQ
     C                     MOVE 'D'       RECI
     C                     WRITETSMKTDF
     C                     END
     C*
     C                     ENDSR
      /EJECT
     C********************************************************************
     C*
      ** S/R DBERR TO PERFORM DATABASE ERROR EXIT FROM PROGRAM
     C*
      ** CALLED FROM
     C*
      ** CALLS NO OTHER SUBROUTINES
     C*
     C* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
     C*
     C           DBERR     BEGSR
     C*
      ** Seton U7 U8 LR
      ** Return
     C*
     C                     EXSR *PSSR                                     S01117
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C                     ENDSR
     C*
     C********************************************************************
     C/EJECT                                                              S01117
     C********************************************************************S01117
      **                                                                  S01117
      ** *PSSR --- SUBROUTINE TO HANDLE ABNORMAL ERROR CONDITIONS         S01117
      ** CALLED FROM: AFTER ABNORMAL OPERATION OCCURS                     S01117
      ** CALLS: NOTHING                                                   S01117
      **                                                                  S01117
     C********************************************************************S01117
     C*                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C*                                                                   S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C                     ENDSR                           ** *PSSR **    S01117
     C*                                                                   S01117
     C********************************************************************S01117
     C/EJECT                                                              S01117
     O*
      ** SET POSITION UPDATE INDICATOR TO 'Y' ON PROCESSED POCHG2 RECS.
     O*
     OPOCHGCCFE                RPOCHC
     O*
     O                         PUPI
     O*
     OPOCHGRCFE                RPOCHR
     O*
     O                         PUPI
     O*
     OPOCHGTCFE                RPOCHT
     O*
     O                         PUPI
     O*
     OPOSTNCDFE                RPOSTN
     O*
     O                         TOPL
     O                         CMTM
     O                         CHTM
     O                         PMTM
     O                         UPLM
     O                         RPLM
     O*
     OTSMKTDF E                RTSMKT
     O*
     O                         EOCI
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
