     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FF Open market center with today's date')
     F********************************************************************
     F*                                                                  *
     F*  Midas - Futures and Options Module                              *
     F*                                                                  *
     F*   FF0472B - OPEN MARKET CENTRE WITH TODAY' DATE                  *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*   LAST AMEND NO. 092954 *CREATE     DATE 13SEP95                 *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*                                                                  *
     F*   092954 - New program to update the market centre date with     *
     F*            Midas run date, and open the market centre. This was  *
     F*            done as part of a change to automatically open any    *
     F*            market centres whose EOC is run as part of COB.       *
     F*                                                                  *
     F********************************************************************
     F*
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FMKCTL   UF  E           K        DISK
     FMARKT   IF  E           K        DISK
     F*
     F********************************************************************
     E                    CPY@    1   1 80
     I/EJECT
     I*****************************************************************
     I*
     IMARKTDF     01
     I              RECI                            RECIMA
     I              LCD                             LCDMA
     I              CHTP                            CHTPMA
     I              TNLU                            TNLUMA
     I*
     ILDA         DS                            256
     I*
     I**   Local Data Area for Database Error Details
     I*
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I*****************************************************************
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     IDSFDY     E DSDSFDY
     I*
     I** FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
     I*
     ISDBANK    E DSSDBANKPD
     I*
     I** DATA STRUCTURE FOR BANK DETAILS
     I*
     C/EJECT
     C*****************************************************************
     C*
     C*
     C**  DEFINITIONS
     C*
     C           *ENTRY    PLIST
     C                     PARM           MRKT    2
     C*
     C           *NAMVAR   DEFN           LDA
     C*
     C** Access to ICD record to obtain run date
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C** ERROR IN SDBANKPD
     C*
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'FF0472B' DBPGM
     C                     MOVEL'SDBANKPD'DBFILE           ***************
     C                     MOVEL@OPTN     DBKEY            * DBERROR 001 *
     C                     Z-ADD001       DBASE            ***************
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C                     ELSE
     C*
     C**   ACCESS LF/MKCTL
     C*
     C           MRKT      CHAINMKCTLDF              99
     C*
     C           *IN99     IFEQ '1'
     C           RECI      ORNE 'D'
     C*
     C                     MOVEL'FF0472B' DBPGM
     C                     MOVE 'MKCTL'   DBFILE           *************
     C                     MOVELMRKT      DBKEY            * DBERR 002 *
     C                     Z-ADD002       DBASE            *************
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C                     ELSE
     C*
     C**   ACCESS LF/MARKT
     C*
     C           MRKT      CHAINMARKTDF              99
     C*
     C           *IN99     IFEQ '1'
     C           RECIMA    ORNE 'D'
     C*
     C                     MOVEL'FF0472B' DBPGM
     C                     MOVE 'MARKT'   DBFILE           *************
     C                     MOVELMRKT      DBKEY            * DBERR 003 *
     C                     Z-ADD003       DBASE            *************
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C                     ELSE
     C*
     C           AEOC      IFEQ *BLANK
     C                     MOVE 'I'       MKCI
     C                     Z-ADDBJRDNB    BUSD
     C                     END
     C*
     C                     END
     C*
     C                     UPDATMKCTLDF
     C*
     C                     END
     C*
     C                     END
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
     C*****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
