     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FF Set up Recovery Areas for Market')
     F********************************************************************
     F*                                                                  *
     F*  Midas FUTURES AND OPTIONS MODULE
     F*                                                                  *
     F*   FF0490  -  SET UP RECOVERY AREAS FOR A MARKET                  *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*   LAST AMEND NO. S01117             DATE 05JAN90                 *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*                                                                  *
     F*   S01117  -  MULTIBRANCHING                                      *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*     CREATION REQUIREMENTS                                        *
     F*                                                                  *
     F*     IGNDECERR(*NO)                                               *
     F*                                                                  *
     F********************************************************************
     F*
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FJRNEF   IF  E           K        DISK
     FRSTRDF  O   E                    DISK
     F*
     F/SPACE 2
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*      10         END OF FILE ON LF/JRNEF
     F*
     F*     80-89       Reserved for Standard FF Subroutines
     F*
     F*     90-99       Reserved for Standard MIDAS Subroutines
     F*
     F*     U7+U8       Database Errors
     F*
     F/SPACE 2
     F/EJECT
     E                    CPY@    1   1 80
     I*
     ILDA         DS                            256
     I*
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 1770DBASE                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I*
     I**   DATA STRUCTURE FOR FIELD DEFINITION FOR USER ENTRY TYPES
     I*
     I            DS
     I                                        1  50 INFO
     I                                        1  100SQNRTP
     I                                       11  12 JMARKT
     I/EJECT
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     C********************************************************************
      **                M A I N   P R O C E S S I N G
     C********************************************************************
     C*
     C**
     C**   NOTE: FOR THE MARKET CENTRE RECEIVED, A SERIES OF 'FROM' AND
     C**   'TO' POINTS ARE SET UP AS RECORDS ON PF/RSTRDF AS AREAS
     C**   BETWEEN WHICH JOURNALLED CHANGES SHOULD BE REMOVED FOR THAT
     C**   MARKET'S MEMBERS.
     C**
     C**
     C**   THE JOURNAL IS READ THROUGH IN REVERSE ORDER, BUT THE 'FROM'
     C**   POINTS ARE CONSIDERED TO BE THE POINT FROM WHICH CHANGES
     C**   WILL BE APPLIED WHEN READING FORWARDS (TO THE 'TO' POINT)
     C**
     C********************************************************************
     C/SPACE 5
     C*
     C**   RECEIVE PARAMETERS FROM CALLING PROGRAM
     C**                  - MARKET CENTRE
     C**                  - TOPOINT - RESTORE TO POINT
     C**                  - FAILED INDICATOR
     C*
     C           *ENTRY    PLIST
     C                     PARM           PMRKT
     C                     PARM           PTOPNT  1
     C                     PARM           PFLIND  1
     C**
     C           *LIKE     DEFN JMARKT    PMRKT
     C           *LIKE     DEFN JOENTT    PTYPE
     C/SPACE 5
     C*
     C**   DEFINE THE MARKET MEMBER RECEIVED AS A PARAMETER  ( 2A )
     C**   - AS A 10A FIELD ( MARKET CODE FOLLOWED BY 8 BLANKS )
     C**   FOR COMPARISON TO THE SYSTEM JOURNAL ENTRY MEMBERS
     C*
     C           *LIKE     DEFN JOMBR     PMBR
     C                     MOVELPMRKT     PMBR
     C/SPACE 5
     C*
     C**   RESET LDA
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C***********          MOVE *BLANKS   DBASE                           S01117
     C                     Z-ADD*ZEROS    DBASE                           S01117
     C                     MOVE *BLANKS   DBKEY
     C                     MOVE *BLANKS   DBPGM
     C                     MOVE *BLANKS   DBFILE
     C                     MOVEL'FF0490'  DBPGM
     C                     OUT  LDA
     C*
     C/SPACE 5
     C*
     C**   READ THROUGH THE JOURNAL IN DESCENDING SEQUENCE FROM THE
     C**   LATEST RECORD UNTIL AN ENTRY TYPE 'IE' IS FOUND, FOR THE
     C**   SPECIFIED MARKET. THE POSITION FOLLOWING THIS WILL BE THE
     C**   REQUIRED 'FROM-POINT'.
     C*
     C                     Z-ADD*HIVAL    TOPNT
     C           TOPNT     SETGTJRNEF
     C                     READ JRNEF                    10
     C*
     C**   DATABASE ERROR IF NO RECORD IS FOUND
     C*
     C           *IN10     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD01        DBASE            ** DBERR  01 **
     C                     MOVEL'JRNEF'   DBFILE
     C                     MOVELTOPNT     DBKEY
     C                     EXSR DBERR
     C                     END
     C*
     C           JOENTT    DOWNE'IE'
     C           JMARKT    ORNE PMRKT
     C*
     C                     READ JRNEF                    10
     C*
     C**   DATABASE ERROR IF END OF FILE IS REACHED BEFORE THE
     C**   MARKET SPECIFIC 'IE' ENTRY IS REACHED
     C*
     C           *IN10     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD02        DBASE            ** DBERR  02 **
     C                     MOVEL'JRNEF'   DBFILE
     C                     MOVELJOSEQN    DBKEY
     C                     EXSR DBERR
     C                     END
     C**
     C                     END
     C**
     C*
     C**   'REMOVE FROM' POINT REACHED. SET 'FROM-POINT' TO ONE
     C**   BEFORE CURRENT SEQUENCE NUMBER.
     C*
     C                     Z-ADDJOSEQN    FRPNT
     C                     SUB  1         FRPNT
     C*
     C**   HAVING REACHED INITIAL 'FROM-POINT' DETERMINE WHETHER
     C**   RESTART-COMPONENT OR REOPEN-MARKET HAS BEEN REQUESTED
     C**   AND CONTINUE PROCESSING ACCORDINGLY.
     C*
     C           PTOPNT    IFEQ 'N'
     C                     MOVE 'NC'      PTYPE
     C                     ELSE
     C                     MOVE 'FC'      PTYPE
     C                     END
     C*
     C                     EXSR JPROC
     C*
     C**   SET LAST 'TO-POINT' TO LAST SEQUENCE NUMBER READ, AND,
     C**   IF THIS IS LESS THAN OR EQUAL TO THE FROM POINT, OUTPUT
     C**   A RECORD TO PF-RSTRDF
     C*
     C                     Z-ADDJOSEQN    TOPNT
     C           TOPNT     IFLE FRPNT
     C                     WRITERCVRYF
     C                     END
     C*
     C                     SETON                     LR
     C**
     C********************************************************************
     C/EJECT
     C*
     C********************************************************************
     C*
      ** INDEX OF SUBROUTINES
     C*
      ** JPROC    PROCESSES BOTH RESTART AND REOPEN
     C*
      ** JTYP1    'EE' AND 'ER' ENTRY TYPES
     C*
      ** JTYP2    'IE' ENTRY TYPE
     C*
      ** JTYP3    'SR' AND 'RC' ENTRY TYPES
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
      ** S/R JPROC PROCESSES BOTH RESTART AND REOPEN
     C*
      ** CALLED FROM MAIN PROCESSING
     C*
      ** CALLS  SR.JTYP1
      **        SR.JTYP2
      **        SR.JTYP3
      **        SR.DBERR
     C*
     C********************************************************************
     C*
     C**
     C           JPROC     BEGSR
     C*
     C                     READ JRNEF                    10
     C*
     C**   PROCESS UNTIL AN 'NC' OR 'FC' ENTRY FOR SPECIFIED MARKET
     C**   IS ENCOUNTERED - PTYPE CONTAINS 'NC' FOR RESTART OR 'FC'
     C**   FOR REOPEN REQUESTED.
     C*
     C           JOENTT    DOWNEPTYPE
     C           *IN10     ANDNE'1'
     C           JMARKT    ORNE PMRKT
     C           *IN10     ANDNE'1'
     C*
     C**   IF A MARKET SPECIFIC ENTRY 'EE' OR AN ENTRY 'ER' IS
     C**   ENCOUNTERED SET 'TO-POINT' ACCORDINGLY
     C*
     C           JOENTT    IFEQ 'EE'
     C           JMARKT    ANDEQPMRKT
     C           JOENTT    OREQ 'ER'
     C*
     C                     EXSR JTYP1
     C                     END
     C*
     C*
     C**   IF A MARKET SPECIFIC ENTRY 'IE' IS
     C**   ENCOUNTERED RESET 'FROM-POINT' ACCORDINGLY
     C*
     C           JOENTT    IFEQ 'IE'
     C           JMARKT    ANDEQPMRKT
     C*
     C                     EXSR JTYP2
     C                     END
     C*
     C**   IF A MARKET SPECIFIC ENTRY 'SR' OR 'RC' IS
     C**   ENCOUNTERED SET 'TO-POINT' ACCORDINGLY
     C*
     C           JOENTT    IFEQ 'SR'
     C           JOMBR     ANDEQPMBR
     C           JOENTT    OREQ 'RC'
     C           JOMBR     ANDEQPMBR
     C*
     C                     EXSR JTYP3
     C                     END
     C*
     C*
     C**   READ NEXT JOURNAL RECORD
     C*
     C                     READ JRNEF                    10
     C*
     C**
     C                     END
     C*
     C**   DATABASE ERROR IF EOF REACHED BEFORE 'FC' OR 'NC'
     C*
     C           *IN10     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD03        DBASE            ** DBERR  03 **
     C                     MOVEL'JRNEF'   DBFILE
     C                     MOVELJOSEQN    DBKEY
     C                     EXSR DBERR
     C                     END
     C*
     C**
     C                     ENDSR
     C**
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
      ** S/R JTYP1 - ENTRY TYPES 'EE' OR 'ER' FOUND FOR MARKET
      **             BEING PROCESSED
     C*
      ** CALLED FROM SR.JPROC
     C*
      ** CALLS  NO OTHER SUBROUTINES
     C*
     C********************************************************************
     C*
     C           JTYP1     BEGSR
     C*
     C**   JOURNAL ENTRY IMMEDIATELY BEFORE 'ER' OR 'EE' ENTRY
     C**   BEING PROCESSED WILL BE 'TO-POINT'
     C*
     C           JOSEQN    ADD  1         TOPNT
     C*
     C**   IF 'TO-POINT' IS LESS THAN OR EQUAL TO 'FROM-POINT'
     C**   OUTPUT RECORD TO PF-RSTRDF
     C*
     C           TOPNT     IFLE FRPNT
     C                     WRITERCVRYF
     C                     END
     C*
     C**   OBTAIN SEQUENCE NO. OF CORRESPONDING ENTRY FOR 'FROM POINT'
     C**   OF NEXT AREA AND RESUME SEARCH OF JOURNAL FROM
     C**   THIS POINT.
     C*
     C           SQNRTP    SUB  1         FRPNT
     C*
     C           FRPNT     SETGTJRNEF
     C**
     C                     ENDSR
     C**
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
      ** S/R JTYP2 - ENTRY TYPE 'IE' FOUND FOR MARKET
      **             BEING PROCESSED
     C*
      ** CALLED FROM SR.JPROC
     C*
      ** CALLS  NO OTHER SUBROUTINES
     C*
     C********************************************************************
     C*
     C           JTYP2     BEGSR
     C*
     C**   IF A 'IE' ENTRY FOR THE MARKET IS ENCOUNTERED WHILST
     C**   SEARCHING FOR THE 'TO-POINT' , RESET THE 'FROM-POINT'
     C**   TO THE POSITION DIRECTLY AFTER THE CURRENT SEQUENCE
     C**   NUMBER
     C*
     C           JOSEQN    SUB  1         FRPNT
     C*
     C**
     C                     ENDSR
     C**
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
      ** S/R JTYP3 - ENTRY TYPES 'SR' OR 'RC' FOUND FOR MARKET
      **             BEING PROCESSED
     C*
      ** CALLED FROM SR.JPROC
     C*
      ** CALLS  NO OTHER SUBROUTINES
     C*
     C********************************************************************
     C*
     C           JTYP3     BEGSR
     C*
     C**   JOURNAL ENTRY IMMEDIATELY AFTER 'SR' OR 'RC' ENTRY
     C**   BEING PROCESSED WILL BE 'TO-POINT'
     C*
     C           JOSEQN    ADD  1         TOPNT
     C*
     C**   IF 'TO-POINT' IS LESS THAN OR EQUAL TO 'FROM-POINT'
     C**   OUTPUT RECORD TO PF-RSTRDF
     C*
     C           TOPNT     IFLE FRPNT
     C                     WRITERCVRYF
     C                     END
     C*
     C**   SET FAIL PARAMETER TO 'F'
     C*
     C                     MOVE 'F'       PFLIND
     C*
     C**   SET NEW 'FROM-POINT' TO SEQUENCE NUMBER AFTER CURRENT
     C**   ONE AND RESUME SEARCH OF JOURNAL FROM THIS POINT.
     C*
     C           JOSEQN    SUB  1         FRPNT
     C*
     C           FRPNT     SETGTJRNEF
     C**
     C                     ENDSR
     C**
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C**   DBERR SR. - DATABASE ERROR HANDLING : TERMINATE PROGRAM       *
     C**                                                                 *
     C**      CALLED FROM MAIN                                           *
     C**                                                                 *
     C********************************************************************
     C*
     C           DBERR     BEGSR                           ** DBERR SR **
     C**
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C*                                                                   S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C                     RETRN
     C**
     C                     ENDSR
     C*
     C********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
