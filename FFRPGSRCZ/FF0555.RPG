     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FF Unauthorised File Updates Report')
     F********************************************************************
     F*                                                                  *
     F*  Midas FUTURES AND OPTIONS MODULE
     F*                                                                  *
     F*   FF0555  -  UNAUTHORISED FILE UPDATES REPORT                    *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01117             Date 21Feb90               *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*   S01117  -  MULTIBRANCHING                                      *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*     CREATION REQUIREMENTS                                        *
     F*                                                                  *
     F*     IGNDECERR(*NO)                                               *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*ABTB10*IF* E                    DISK                               S01117
     FJRNCG1  IF  E           K        DISK
     FFF0555P1O   E                    PRINTER
     F***********                                   KINFDS PRINT          S01117
     F                                              KINFDS SPOOL          S01117
     FFF0555AUO   E                    PRINTER                            S01117
     F                                              KINFDS SPOOLU         S01117
     F********************************************************************
     F*
     F*           FUNCTION OF INDICATORS
     F*
     F*   20      END OF FILE FOR JRNCG1
     F*
     F*   30      BIT 0/4 IS ON  }
     F*   31      BIT 1/5 IS ON  }    USED IN SUBROUTINE CVTHEX
     F*   32      BIT 2/6 IS ON  }    WHEN TESTING CHARACTER TO
     F*   33      BIT 3/7 IS ON  }    BE CONVERTED
     F*
     F****99******CHAIN*FAIL*TO*TABTB10***********************************S01117
     F*
     F*   U7 & U8 DATABASE ERROR
     F*
     E                    CPY@    1   1 80
     E                    C         100  1
     E                    H1        100  1
     E                    H2        100  1
     E                    DATA        6100
     E                    HEX    16  16  1
     E*
      **     ARRAY OF HEX VALUES (0 - F) INDEX CALLED DEC (1-16)
     E*
     E                    SWS         8  1
     I*RINT****** DS                                                      S01117
     I***********                                                         S01117
     I********************DATA*STRUCTURE*FOR*PAGE*FULL*LINE*COUNT*********S01117
     I***********                                                         S01117
     I*                                                                   S01117
     I** FILE INFORMATION DATA STRUCTURE                                  S01117
     I*                                                                   S01117
     ISPOOL       DS                                                      S01117
     I                                       83  92 SFILE                 S01117
     I                                    B 123 1240SFNUM                 S01117
     I                                    B 367 3680LINE
     ISPOOLU      DS                                                      S01117
     I                                       83  92 SFILEU                S01117
     I                                    B 123 1240SFNUMU                S01117
     I*                                                                   S01117
     ILDA         DS                            256
     I*  LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 1770DBASE                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I*                                                                   S01117
     I** DATA AREA RUNDAT                                                 S01117
     I*                                                                   S01117
     IRUNDT       DS                                                      S01117
     I                                        1   7 RUNA                  S01117
     I                                    P   8  100RUND                  S01117
     I                                       11  11 SSF                   S01117
     I                                       12  12 DATF                  S01117
     I                                       13  13 MBIN                  S01117
      *                                                                   S01117
     ISDBANK    E DSSDBANKPD                                              S01117
      **  EXTERNAL DS FOR BANK DETAILS                                    S01117
     I              BJURPT                          TITL                  S01117
      *                                                                   S01117
     I@DSFDY    E DSDSFDY                                                 S01117
      **  FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE              S01117
     I*
      /SPACE 10
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     C*
     C           *ENTRY    PLIST
     C                     PARM           FILDES 30
     C                     PARM           SWS
      /EJECT
     C*
      ***INITIALISE*FIELDS*&*ACCESS*ICD*1*********************************S01117
      ** INITIALISE FIELDS, ACCESS RUNDAT & SDBANKPD                      S01117
     C*
     C                     EXSR INIT
     C*
      ** CONVERT TO HEX AND PRINT
     C*
     C           *IN20     DOWEQ'0'
     C                     ADD  1         COUNT                           S01117
     C                     EXSR CVTHEX
     C                     READ JRNCG1                   20
     C                     END
     C*
      ** PRINT END OF REPORT
     C*
     C                     WRITEFMTP105
     C                     WRITEFMTAU02                                   S01117
     C*
      ** RESET SWITCHES AND TERMINATE PROGRAM
     C*
     C                     MOVE *INU1     SWS,1
     C                     MOVE *INU2     SWS,2
     C                     MOVE *INU3     SWS,3
     C                     MOVE *INU4     SWS,4
     C                     MOVE *INU5     SWS,5
     C                     MOVE *INU6     SWS,6
     C                     MOVE *INU7     SWS,7
     C                     MOVE *INU8     SWS,8
     C                     SETON                     LR
     C                     RETRN
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
     C           INIT      BEGSR
     C*
     C* INITIALISING THE SWITCHES
     C*
     C                     MOVE SWS,1     *INU1
     C                     MOVE SWS,2     *INU2
     C                     MOVE SWS,3     *INU3
     C                     MOVE SWS,4     *INU4
     C                     MOVE SWS,5     *INU5
     C                     MOVE SWS,6     *INU6
     C                     MOVE SWS,7     *INU7
     C                     MOVE SWS,8     *INU8
     C*
     C*
      ** GET BANK TITLE AND RUNDATE
     C*
     C***********1         CHAINTABTB10F             99                   S01117
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           S01117
     C                     IN   RUNDT                                     S01117
     C*                                                                   S01117
     C                     CALL 'AOBANKR0'                                S01117
     C                     PARM '*MSG   ' @RTCD   7                       S01117
     C                     PARM '*FIRST ' @OPTN   7                       S01117
     C           SDBANK    PARM SDBANK    @DSFDY                          S01117
     C*                                                                   S01117
      **   SPOOL AU REPORT THROUGH RCF                                    S01117
     C*                                                                   S01117
     C                     WRITEFMTAU01                                   S01117
     C                     EXSR RCFAU                                     S01117
     C*
      ** D-B ERROR IF NOT FOUND - UPDATE LDA,RESET SWITCHES AND EXIT
     C*
     C************IN99     IFEQ '1'                                       S01117
     C***********RECI      ORNE 'D'                                       S01117
     C*                                                                   S01117
     C           @RTCD     IFNE *BLANK                                    S01117
     C                     SETON                     U7U8LR
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C***********          MOVE 'TABLE'   DBFILE                          S01117
     C                     Z-ADD1         DBASE
     C***********          MOVEL'ICD 1'   DBKEY                           S01117
     C***********          MOVE 'FF0555'  DBPGM                           S01117
     C                     MOVE *BLANK    DBFILE                          S01117
     C                     MOVE *BLANK    DBKEY                           S01117
     C                     MOVE *BLANK    DBPGM                           S01117
     C                     MOVEL'SDBANKPD'DBFILE           ***************S01117
     C                     MOVEL@OPTN     DBKEY            **DB ERROR 01**S01117
     C                     MOVEL'FF0555'  DBPGM            ***************S01117
     C                     MOVEL@OPTN     DBOPTN  7                       S01117
     C                     OUT  LDA
     C                     EXSR *PSSR                                     S01117
     C***********          WRITEFMTP101                                   S01117
     C***********          WRITEFMTP106                                   S01117
     C                     WRITEFMTAU04                                   S01117
     C*
     C                     MOVE *INU1     SWS,1
     C                     MOVE *INU2     SWS,2
     C                     MOVE *INU3     SWS,3
     C                     MOVE *INU4     SWS,4
     C                     MOVE *INU5     SWS,5
     C                     MOVE *INU6     SWS,6
     C                     MOVE *INU7     SWS,7
     C                     MOVE *INU8     SWS,8
     C*
     C                     RETRN
     C                     END
     C*
      ** ACCESS JRNCG1
     C*
     C                     READ JRNCG1                   20
     C*
      ** NO RECORDS, WRITE HEADING AND 'NO DETAILS' LITERAL
     C*
     C           *IN20     IFEQ '1'
     C***********          WRITEFMTP101                                   S01117
     C***********          WRITEFMTP104                                   S01117
     C                     WRITEFMTAU02                                   S01117
     C                     WRITEFMTAU03                                   S01117
     C*                                                                   S01117
     C                     MOVE *INU1     SWS,1                           S01117
     C                     MOVE *INU2     SWS,2                           S01117
     C                     MOVE *INU3     SWS,3                           S01117
     C                     MOVE *INU4     SWS,4                           S01117
     C                     MOVE *INU5     SWS,5                           S01117
     C                     MOVE *INU6     SWS,6                           S01117
     C                     MOVE *INU7     SWS,7                           S01117
     C                     MOVE *INU8     SWS,8                           S01117
     C*                                                                   S01117
     C                     SETON                     LR                   S01117
     C                     RETRN                                          S01117
     C*                                                                   S01117
     C                     ELSE
     C*
      ** RECORDS EXIST, CALCULATE RECORD LENGTH VARIABLES
     C*
     C           JOENTL    DIV  100       JLHUND  50
     C                     MVR            JLREM   50
     C*
      ** JLHUND CONTAINS THE NUMBER OF 100 CHARACTER CHUNKS OF JOESD
      ** ADD 1 IF NECESSARY FOR THE REMAINDER OF THE DATA
     C*
     C           JLREM     IFGT 0
     C                     ADD  1         JLHUND
     C                     END
     C*
      ** CALCULATE OVERFLOW LINE - 100 CHARACTERS TAKES 5 LINES
      ** SO 5 TIMES NO. OF HUNDREDS NECESSARY TO FIT ONE RECORD
     C*
     C           JLHUND    MULT 5         MAXLIN  20
     C           55        SUB  MAXLIN    OFLOW   20
     C*                                                                   S01117
      ** INITIALIZE HASH COUNT                                            S01117
     C*                                                                   S01117
     C                     Z-ADD*ZERO     COUNT   50                      S01117
     C*
      ** WRITE REPORT HEADINGS
     C*
     C                     WRITEFMTP101
     C                     EXSR RCFP1                                     S01117
     C*
      ** END OF RECORDS/NO RECORDS PROCESSING
     C*
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
     C           CVTHEX    BEGSR
     C*
      ** MOVE INPUT DATA TO WORK ARRAY
     C*
     C                     MOVEAJOESD     DATA
     C*
      ** WRITE RECORD HEADERS
     C*
      ** THROW PAGE IF NOT ENOUGH ROOM FOR THIS ENTRY
     C*
     C           LINE      IFGT OFLOW
     C                     WRITEFMTP101
     C                     END
     C*
      ** PROCESS REQUIRED NUMBER OF 100 CHARACTER CHUNKS
     C*
     C           1         DO   JLHUND    I       10
     C                     MOVEA*BLANK    C
     C                     MOVEA*BLANK    H1
     C                     MOVEA*BLANK    H2
     C*
      ** MOVE NEXT 100 CHARACTERS OF DATA TO CHARACTER ARRAY
     C*
     C                     MOVEADATA,I    C
     C*
      ** CALCULATE NUMBER OF TIMES TO LOOP
     C*
     C                     Z-ADD100       LOOP    30
     C*
     C           I         IFEQ JLHUND
     C           JLREM     ANDGT0
     C                     Z-ADDJLREM     LOOP
     C                     END
     C*
      ** LOOP TO CONVERT CHARACTERS
     C*
     C           1         DO   LOOP      J       30
     C*
      ** CONVERT CHARACTER TO RESPECTIVE HEX PORTIONS
     C*
      ** FIRST FOUR BITS
     C*
     C                     TESTB'0'       C,J            30
     C                     TESTB'1'       C,J            31
     C                     TESTB'2'       C,J            32
     C                     TESTB'3'       C,J            33
     C*
      ** CONVERT BINARY CHARACTER TO DECIMAL NUMBER
     C*
     C                     Z-ADD0         BIT     20
     C   33                ADD  1         BIT
     C   32                ADD  2         BIT
     C   31                ADD  4         BIT
     C   30                ADD  8         BIT
     C*
      ** CONVERT DECIMAL VALUE TO HEX
     C*
      ** ADD ONE TO RESULT FOR ARRAY INDEX, AS ARRAY STARTS AT 0
     C*
     C           BIT       ADD  1         DEC     20
     C*
      ** MOVE RELEVANT HEX CHARACTER TO NEXT POSITION IN ARRAY
     C*
     C                     MOVE HEX,DEC   H1,J
     C*
      ** SECOND FOUR BITS
     C*
     C                     TESTB'4'       C,J            30
     C                     TESTB'5'       C,J            31
     C                     TESTB'6'       C,J            32
     C                     TESTB'7'       C,J            33
     C*
      ** CONVERT BINARY CHARACTER TO DECIMAL NUMBER
     C*
     C                     Z-ADD0         BIT
     C   33                ADD  1         BIT
     C   32                ADD  2         BIT
     C   31                ADD  4         BIT
     C   30                ADD  8         BIT
     C*
      ** CONVERT DECIMAL VALUE TO HEX
     C*
      ** ADD ONE TO RESULT FOR ARRAY INDEX, AS ARRAY STARTS AT 0
     C*
     C           BIT       ADD  1         DEC     20
     C*
      ** MOVE RELEVANT HEX CHARACTER TO NEXT POSITION IN ARRAY
     C*
     C                     MOVE HEX,DEC   H2,J
     C*
      ** END OF DOWHILE LOOP FOR CHARACTERS IN C, H1, H2 ARRAYS
     C*
     C                     END
     C*
      ** WRITE OUT 100 CHARACTERS OF DATA. IF FIRST SET, WRITE
      ** OTHER ENTRY DATA AS WELL
     C*
     C                     MOVEAC         OC
     C                     MOVEAH1        OH1
     C                     MOVEAH2        OH2
     C           I         IFEQ 1
     C                     WRITEFMTP102
     C                     ELSE
     C                     WRITEFMTP103
     C                     END
     C*
      ** END OF DOWHILE LOOP FULL DATA LENGTH
     C*
     C                     END
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT                                                              S01117
     C********************************************************************S01117
      **                                                                  S01117
      ** RCFP1 -- SUBROUTINE TO SPOOL P1 THROUGH RCF                      S01117
      ** CALLED FROM: INIT                                                S01117
      ** CALLS: NOTHING                                                   S01117
      **                                                                  S01117
     C********************************************************************S01117
     C*                                                                   S01117
     C           RCFP1     BEGSR                           ** RCFP1 **    S01117
     C*                                                                   S01117
      **      ENSURE REPORT SPOOL FILE RECORDED BY RCF                    S01117
     C*                                                                   S01117
     C                     Z-ADDSFNUM     ZSNUM   60                      S01117
     C*                                                                   S01117
     C                     CALL 'ZSFILE'                                  S01117
     C                     PARM           SEQ     5                       S01117
     C                     PARM *BLANK    SENTY   3                       S01117
     C                     PARM           SFILE                           S01117
     C                     PARM           ZSNUM                           S01117
     C                     PARM           ZSERR   1                       S01117
     C*                                                                   S01117
     C           ZSERR     IFEQ 'Y'                                       S01117
      **  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       S01117
     C                     SETON                     U7U8LR               S01117
     C                     RETRN                                          S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C                     ENDSR                                          S01117
     C*                                                                   S01117
     C********************************************************************S01117
     C/EJECT                                                              S01117
     C********************************************************************S01117
      **                                                                  S01117
      ** RCFAU -- SUBROUTINE TO SPOOL AU THROUGH RCF                      S01117
      ** CALLED FROM: INIT                                                S01117
      ** CALLS: NOTHING                                                   S01117
      **                                                                  S01117
     C********************************************************************S01117
     C*                                                                   S01117
     C           RCFAU     BEGSR                           ** RCFAU **    S01117
     C*                                                                   S01117
      **      ENSURE TOTAL SPOOL FILE RECORDED BY RCF                     S01117
     C*                                                                   S01117
     C                     Z-ADDSFNUMU    ZSNUMU  60                      S01117
     C*                                                                   S01117
     C                     CALL 'ZSFILE'                                  S01117
     C                     PARM           SEQ     5                       S01117
     C                     PARM *BLANK    SENTY   3                       S01117
     C                     PARM           SFILEU                          S01117
     C                     PARM           ZSNUMU                          S01117
     C                     PARM           ZSERR   1                       S01117
     C*                                                                   S01117
     C           ZSERR     IFEQ 'Y'                                       S01117
      **  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       S01117
     C                     SETON                     U7U8LR               S01117
     C                     RETRN                                          S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C                     ENDSR                                          S01117
     C*                                                                   S01117
     C********************************************************************S01117
     C/EJECT                                                              S01117
     C********************************************************************S01117
      **                                                                  S01117
      ** *PSSR --- SUBROUTINE TO HANDLE ABNORMAL ERROR CONDITIONS         S01117
      ** CALLED FROM: AFTER ABNORMAL OPERATION OCCURS                     S01117
      ** CALLS: NOTHING                                                   S01117
      **                                                                  S01117
     C********************************************************************S01117
     C*                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C*                                                                   S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C                     ENDSR                           ** *PSSR **    S01117
     C*                                                                   S01117
     C********************************************************************S01117
**  CPY@
(c) Finastra International Limited 2001
**  HEX - HEX VALUES OF NUMBERS 0 TO 15
0123456789ABCDEF
