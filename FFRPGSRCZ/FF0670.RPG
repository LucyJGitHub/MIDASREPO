     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FF Margin A/C Interest Rate Changes')
     F********************************************************************
     F*                                                                  *
     F*  Midas FUTURES AND OPTIONS MODULE
     F*                                                                  *
     F*   FF0670  -  UPDATE MARGIN ACCOUNT INTEREST RATE CHANGES         *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 MD039256           Date 28Jun16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01117             Date 20Mar90               *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*                                                                  *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *           (Recompile)                                         *
      *  MD046248 - Finastra Rebranding                               *
      *  MD039256 - Error in COB                                      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*   S01117  -  MULTIBRANCHING                                      *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*     CREATION REQUIREMENTS                                        *
     F*                                                                  *
     F*     IGNDECERR(*YES)                                              *
     F*                                                                  *
     F********************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FTSCCY4  IF  E           K        DISK
     F***RCHG2** UF  F     160 16AI     2 DISK                                                CGL029
     F***RCHG2** UF  F     170 16AI     2 DISK                                       CGL029 MD039256
     FRCHG3   UF  F     170 16AI     2 DISK                                                 MD039256
      ********************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *        03        TYPE 3 RECORD IS PRESENT FOR THIS KEY
      *
      *     80-89        ( Reserved for Standard FF Subroutines )
      *
      *     90-99        ( Reserved for Standard MIDAS Subroutines )
      *       98         Date Format is MMDDYY
      *
      *     U7+U8        Database Error
      ********************************************************************
      /EJECT
      ********************************************************************
     E                    CPY@    1   1 80
     E                    BL1         6 15 0
      * Redefine a field from Sequence 2 record as first 6 Balances
      *
     E                    BL2         5 15 0
      * Redefine a field from Sequence 3 record as last 5 Balances
      *
     E                    BAL        11 15 0
      * All 11 Balances
      *
     E                    XBAL       11 15 0
      * All 11 Balances :  Storage
      *
     E                    ABAL       11 15
      * All 11 Balances :  Storage for MOVEA
      *
     E                    TBAL       11 15
      * All 11 Balances :  Storage for MOVEA
      *
     E                    RT1         6 11 7
      * Redefine a field from Sequence 2 record as first 6 Rates
      *
     E                    RT2         5 11 7
      * Redefine a field from Sequence 3 record as last 5 Rates
      *
     E                    RAT        11 11 7
      * All 11 Rates
      *
     E                    XRAT       11 11 7
      * All 11 Rates :  Storage
      *
     E                    ARAT       11 11
      * All 11 Rates :  Storage for MOVEA
      *
     E                    TRAT       11 11
      * All 11 Rates :  Storage for MOVEA
      *
      ********************************************************************
      /EJECT
      ********************************************************************
      *
     I***RCHG2** NS      17 C2                                                              MD039256
     IRCHG3   NS      17 C2                                                                 MD039256
      * Rate-change record  Sequence 2
      *
     I                                        1   1 RECI
     I**********                              2  16 RDKEY2                                    CGL029
     I                                        2  10 RCNCY2                                    CGL029
     I                                       15  16 RSEQN2                                    CGL029
     I                                       17  170RSEQ
     I                                       81  810DRCR
     I                                      103 105 BRCA                  S01117
     I                                    P  39  74 RT1
     I                                    P 106 153 BL1
     I                                      161 170 RACOD2                                    CGL029
      *
     I        NS      17 C3
      * Rate-change record  Sequence 3
      *
     I                                        1   1 RECI
      *
      *  "Key" for database error processing
     I**********                              2  16 RDKEY3                                    CGL029
     I                                        2  10 RCNCY3                                    CGL029
     I                                       15  16 RSEQN3                                    CGL029
      *
     I                                       17  170RSEQ
     I                                       81  810DRCR
     I                                      103 105 BRCA                  S01117
     I                                    P  39  68 RT2
     I                                    P 106 145 BL2
     I                                      161 170 RACOD3                                    CGL029
     I        NS
      *
      * CATCHALL
      *
     I                                       17  170RSEQ
     I**********                              2  16 RDKEY                                     CGL029
     I                                        2  10 RCNCY                                     CGL029
     I                                       15  16 RSEQN                                     CGL029
     I                                      161 170 RACOD                                     CGL029
      *
      * Combine  Balances from both records into one array
      *
     I            DS
     I                                        1 165 BAL
     I                                        1  90 BL1
     I                                       91 165 BL2
      *
      * Combine  Rates from both records into one array
      *
     I            DS
     I                                        1 121 RAT
     I                                        1  66 RT1
     I                                       67 121 RT2
      *
      * LOCAL DATA AREA FOR DATABASE ERROR
      *
     I*                                                                   S01117
     I*DA********UDS                            256                       S01117
     ILDA         DS                            256                       S01117
     I***********                           134 175 DBBLK                 S01117
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 1770DBASE                 S01117
     I                                      134 180 DBBLK                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
      /EJECT
      *
      * INITIALISE WORK FIELDS
      *
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     C*                                                                   S01117
     C* KEY STRUCTURE FOR LF-TSCCY4                                       S01117
     C*                                                                   S01117
     C           RDKEY4    KLIST                                          S01117
     C                     KFLD           BRCA                            S01117
     C                     KFLD           RDKEY2                          S01117
     C*                                                                   S01117
     C* INITIALISE LOCAL DATA AREA                                        S01117
     C*                                                                   S01117
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE *BLANKS   DBBLK
     C                     Z-ADD0         DBASE
     C                     OUT  LDA                                       S01117
     C*                                                                   S01117
     C                     Z-ADD0         BAL
     C                     Z-ADD0         RAT
     C                     Z-ADD0         Y       20
     C                     Z-ADD0         Z       20
      *
      * TYPE 3 RECORDS WILL BE READ BEFORE TYPE 2
     C*
     C**********           READ RCHG2                    LR                                 MD039256
     C                     READ RCHG3                    LR                                 MD039256
     C*
     C           *INLR     DOWEQ'0'
     C*
     C           RSEQ      IFNE 2
     C           RSEQ      ANDNE3
     C           *LOCK     IN   LDA                                       S01117
     C                     Z-ADD1         DBASE
     C***********          MOVE 'FF0670'  DBPGM                           S01117
     C                     MOVEL'FF0670'  DBPGM                           S01117
     C***********          MOVE 'RCHG2'   DBFILE                          S01117
     C**********           MOVEL'RCHG2'   DBFILE                                     S01117 MD039256
     C                     MOVEL'RCHG3'   DBFILE                                            MD039256
     C           RCNCY     CAT  RACOD     RKEY1  19                                           CGL029
     C           RKEY1     CAT  RSEQN     RDKEY  21                                           CGL029
     C                     MOVELRDKEY     DBKEY            ***************
     C                     OUT  LDA                                       S01117
     C                     SETON                     LRU7U8**DB ERROR 01**
     C                     EXSR *PSSR                      **             S01117
     C                     RETRN                           ***************
     C                     END
     C*
      ** RESET WORK FIELDS
     C*
     C                     Z-ADD0         TMGP
     C                     Z-ADD0         Z
     C*
      ** IF RECORD JUST READ IS OF TYPE 3, SET INDICATOR
      ** READ PRECEDING TYPE 2 RECORD
     C*
     C           RSEQ      IFEQ 3
     C                     SETON                     03
     C*
     C**********           READ RCHG2                    99                                 MD039256
     C                     READ RCHG3                    99                                 MD039256
     C*
      ** DATABASE ERROR IF RECORD NOT FOUND
     C*
     C           *IN99     IFEQ '1'
     C********** RDKEY2    ORNE RDKEY3                                                        CGL029
     C           RCNCY2    ORNE RCNCY3                                                        CGL029
     C           RACOD2    ANDNERACOD3                                                        CGL029
     C           RSEQN2    ANDNERSEQN3                                                        CGL029
     C           *LOCK     IN   LDA                                       S01117
     C                     Z-ADD2         DBASE
     C***********          MOVE 'FF0670'  DBPGM                           S01117
     C                     MOVEL'FF0670'  DBPGM                           S01117
     C***********          MOVE 'RCHG2'   DBFILE                          S01117
     C**********           MOVEL'RCHG2'   DBFILE                                     S01117 MD039256
     C                     MOVEL'RCHG3'   DBFILE                                            MD039256
     C                     MOVE '- TYPE 2'DBKEY
     C           RCNCY3    CAT  RACOD3    RKEY3  19                                           CGL029
     C           RKEY3     CAT  RSEQN     RDKEY3 21                                           CGL029
     C                     MOVELRDKEY3    DBKEY            ***************
     C                     OUT  LDA                                       S01117
     C                     SETON                     LRU7U8**DB ERROR 02**
     C                     EXSR *PSSR                      **             S01117
     C                     RETRN                           ***************
     C                     END
     C*
     C                     END
     C*
      ** IF BALANCE NUMBER 1 IS -1, THEN MARGINS REQUIRED (BAL,1 IS NO. 0)
     C*
     C           BAL,2     IFLT 0
     C*
     C           DRCR      IFEQ 1
     C                     EXSR CREDIT
     C                     END
     C*
      ** IF MARGIN NOT GREATER THAN 0, OR DEBIT BALANCE,
     C*
     C           TMGP      IFLE 0
     C           DRCR      OREQ 0
     C                     EXSR DEBIT
     C                     END
     C*
     C                     END
     C*
      ** SET OF RECORD 3 INDICATOR AND READ 'NEXT' AVAILABLE RECORD
     C*
     C                     SETOF                     03
     C                     Z-ADD0         BAL
     C                     Z-ADD0         RAT
     C**********           READ RCHG2                    LR                                 MD039256
     C                     READ RCHG3                    LR                                 MD039256
     C*
      ** END OF DOWHILE NOT LR. PROGRAM WILL TERMINATE HERE
     C*
     C                     END
     C*
     C*********************************************************************
      /EJECT
     C*********************************************************************
     C*
      ** S/R CREDIT TO PROCESS DRCR INDICATOR IS ON (CREDIT) MARGINS
     C*
      ** CALLED FROM MAIN PROCESSING
     C*
      ** CALLS NO OTHER SUBROUTINES
     C*
     C*********************************************************************
     C*
     C           CREDIT    BEGSR
     C*
      ** ATTEMPT TO ACCESS TSCCY4 FOR TOTAL MARGIN POSTED
     C*
     C***********RDKEY2    CHAINTSCCY4               99                   S01117
     C           RCNCY2    CAT  RACOD2    RKEY2  19                                           CGL029
     C           RKEY2     CAT  RSEQN2    RDKEY2 21                                           CGL029
     C           RDKEY4    CHAINTSCCY4               99                   S01117
     C*
      ** IF A RECORD IS FOUND, UPDATE BALANCES FROM NUMBER 1 ONWARD
     C*
     C           *IN99     IFEQ '0'
     C           TMGP      ANDGT0
     C*
     C                     Z-ADDTMGP      BAL,2
     C                     Z-ADD3         Z
     C*
     C           Z         DOWLT12
     C           BAL,Z     ANDNE0
     C                     ADD  TMGP      BAL,Z
     C                     ADD  1         Z
     C                     END
     C*
     C                     EXCPTRCHG22
     C*
      ** ACCESS RECORD 3, SAVING ARRAY, AND RESET FILE POINTER IF
      ** RECORD 3 WAS FOUND
     C*
     C           *IN03     IFEQ '1'
     C*
      ** ONLY PERFORM UPDATE IF RECORD HAS NON-ZERO BALANCES
     C*
     C           BAL,7     ANDNE0
     C                     Z-ADDBAL       XBAL
     C                     Z-ADDRAT       XRAT
     C*
     C**********           READPRCHG2                    99                                 MD039256
     C                     READPRCHG3                    99                                 MD039256
     C                     Z-ADDXBAL      BAL
     C                     Z-ADDXRAT      RAT
     C                     EXCPTRCHG23
     C**********           READ RCHG2                    99                                 MD039256
     C                     READ RCHG3                    99                                 MD039256
     C*
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C*********************************************************************
      /EJECT
     C*********************************************************************
     C*
      ** S/R DEBIT TO PROCESS WHERE DRCR INDICATOR IS OFF (DEBIT)
     C*
      ** CALLED FROM MAIN PROCESSING
     C*
      ** CALLS S/R RESET
     C*
     C*********************************************************************
     C*
     C           DEBIT     BEGSR
     C*
      ** MOVE BALANCE ARRAY TO WORK ARRAYS SO THAT ARRAY ABAL
      ** CONTAINS ALL BUT SECOND ELEMENT OF BAL, IN ORDER
     C*
     C                     MOVE *ZEROS    TBAL
     C                     MOVE BAL       ABAL
     C                     MOVEAABAL,3    TBAL
     C                     MOVEATBAL      ABAL,2
     C                     MOVE ABAL      XBAL
     C                     MOVE ABAL      BAL
     C*
      ** MOVE RATE ARRAY TO WORK ARRAYS SO THAT ARRAY ARAT
      ** CONTAINS ALL BUT FIRST ELEMENT OF RAT, IN ORDER
     C*
     C                     MOVE *ZEROS    TRAT
     C                     MOVE RAT       ARAT
     C                     MOVEAARAT,2    TRAT
     C                     MOVEATRAT      ARAT
     C                     MOVE ARAT      XRAT
     C                     MOVE ARAT      RAT
     C*
     C                     EXCPTRCHG22
     C*
     C           *IN03     IFEQ '1'
     C*
      ** ONLY PERFORM UPDATE IF RECORD HAS NON-ZERO BALANCES
      ** AND HAS NO BALANCE TO BE ZEROISED
     C*
     C           BAL,6     ANDNE0
     C**********           READPRCHG2                    99                                 MD039256
     C                     READPRCHG3                    99                                 MD039256
     C                     MOVE XBAL      BAL
     C                     MOVE XBAL      RAT
     C                     EXCPTRCHG23
     C**********           READ RCHG2                    99                                 MD039256
     C                     READ RCHG3                    99                                 MD039256
     C                     END
     C*
     C                     ENDSR
     C*
     C*********************************************************************
      /EJECT                                                              S01117
     C********************************************************************S01117
      **                                                                  S01117
      ** *PSSR --- SUBROUTINE TO HANDLE ABNORMAL ERROR CONDITIONS         S01117
      ** CALLED FROM: AFTER ABNORMAL OPERATION OCCURS                     S01117
      ** CALLS: NOTHING                                                   S01117
      **                                                                  S01117
     C********************************************************************S01117
     C*                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C*                                                                   S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C                     ENDSR                           ** *PSSR **    S01117
     C*                                                                   S01117
     C********************************************************************S01117
      /EJECT
     O*********************************************************************
      *
      * SEQUENCE 2 RECORD UPDATE
      *
     O**********RCHG2   E                RCHG22                                             MD039256
     ORCHG3   E                RCHG22                                                       MD039256
     O                         BL1      153P
     O                         RT1       74P
      *
      * SEQUENCE 3 RECORD UPDATE
      *
     O**********RCHG2   E                RCHG23                                             MD039256
     ORCHG3   E                RCHG23                                                       MD039256
     O                         BL2      145P
     O                         RT2       68P
**  CPY@
(c) Finastra International Limited 2001
