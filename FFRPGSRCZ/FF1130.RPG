     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FF Chargable Rates List')
      *****************************************************************
      *                                                               *
      *  Midas - Futures and Options Module                           *
      *                                                               *
      *   FF1130  -  CHARGEABLE RATES LIST                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CGP001             Date 11Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 150367             Date 07Apr00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC003             Date 25Feb97               *
      *                  S01455             Date 05NOV93              *
      *                  E20461             DATE 02APR90              *
      *                  S01117             DATE 12FEB90              *
      *                  HK0000             DATE 12FEB90              *
      *                  E18343             DATE 11NOV89              *
      *                  E15864             DATE 07AUG89              *
      *                                                               *
      *----------------------------------------------------------------
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGP001 - Global Processing (Recompiled)                      *
      *  150367 - Recompile over changed /copy FFSTDPZ4.              *
      *  CAC003 - Profit Centre Accounting Development Phase 3.       *
      *   S01455  -  'FF' ACCOUNT KEY ENHANCEMENTS.                   *
      *              Recompiled due to changes in file TABFF.         *
      *   E20461  -  The field RMAXC was not blanked between records  *
      *   S01117  -  MULTIBRANCHING                                   *
      *   HK0000  -  STORE TEMP FIELDS ONLY IF NOT EOF                *
      *   E18343  -  PROGRAM RE-COMPILED DUE TO CHANGE IN ZSRSRC MEMBER
      *              ZFRPEDZ3                                         *
      *   E15864  -  AMENDS REPORT TO SHOW DELETED RECORDS TOO.       *
      *                                                               *
      *****************************************************************
     FTABFF   IF  E           K        DISK
     F**ICD*1**** TABTB10F                          KIGNORE               S01117
     F            TABTB10F                          KIGNORE               S01117
     F            TABTB11F                          KIGNORE
     F            TABTB20F                          KIGNORE
     F            TABTB40F                          KIGNORE
     F            TABTB80F                          KIGNORE
     F            TABTE10F                          KIGNORE
     F* CCY 1     TABTG10F                          KIGNORE
     F            TABLETHF                          KIGNORE
     F            TABLETLF                          KIGNORE
     F            TABLET2F                          KIGNORE
     F            TABLETRF                          KIGNORE
     FCHGCM   IF  E           K        DISK                           UC
     FJRNCGD  IF  E           K        DISK                           U3
     FFF1130P1O   E                    PRINTER
     F***********                                   KINFDS PRINT          S01117
     F                                              KINFDS SPOOL          S01117
     FFF1130AUO   E                    PRINTER                            S01117
     F                                              KINFDS SPOOLU         S01117
     F*
     F*****************************************************************
     F/EJECT
     F************        FUNCTIONS OF INDICATORS        *************
     F*                 20            ANALYTICAL ACCOUNTING AND SAR       CAC003
     F*                               CAC003 INDICATOR                    CAC003
     F*                 30            ON IF U3 ON                         S01117
     F*                 40            NO RECORD FOUND ON TABFF FILE
     F*                 41            NO MORE RECORDS FOUND ON CHGCM
     F*                 42            RECORD FOUND BUT NOT VALID
     F*                 50-53         USED TO SHOW NUMBER OF DECIMAL
     F*                               PLACES FOR A CURRENCY
     F*
     F*                    50         ZERO DECIMAL PLACES
     F*                    51         ONE DECIMAL PLACE
     F*                    52         TWO DECIMAL PLACES
     F*                    53         THREE DECIMAL PLACES
     F*
     F*                 80-89         USED IN FF-SUBROUTINES
     F*
     F*                 90-95         USED IN Z-SUBROUTINES
     F*                 98            DATE FORMAT IS MMMDDYY
     F*
     F*                     NU3       PROCESS CHGCM
     F*                      U3       PROCESS JRNCGD (AUDIT)
     F*                      U7 U8    DATABASE ERROR, REQUIRED RECORD
     F*                               NOT FOUND.
     F*                      LR       SETON IN SUBROUTINE END
     F*
     F*****************************************************************
     F/EJECT
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZDATE2Z1
     E**   FOR CONVERTING A DAY NUMBER TO A DATE FORMAT
     E*
     E                    ALR         9  5 0
     E                    ACA         9 13 0
     E** RANGE/AMOUNT ARRAYS
     E*
     E                    DET       512  1
     E** RECORD DETAIL OF CHGCMD
     E*
     E                    SWS         8  1
     E** SWITCH SETTINGS
     E*
     E*****************************************************************
     E*
     E/COPY ZSRSRC,FFSTDPZ1
     E*
     E**   FOR CURRENCY DECIMAL PLACE SUBROUTINE
     E*
     E*****************************************************************
     E*
     E/COPY ZSRSRC,ZFRPEDZ1
     E*
     E**   FOR REPORT AMOUNT EDIT SUBROUTINE
     E*
     E*****************************************************************
     E/EJECT
     I*****************************************************************
     I*
     IJOESD     E DSCHGCMD
     I                                        1 512 DET
     I*
     I** DATA STRUCTURE FOR RECORD LAYOUT
     I*
     I*RINT****** DS                                                      S01117
     I*
     I*                   DATA STRUCTURE FOR PAGE FULL LINE COUNT
     I*
     ISPOOL       DS                                                      S01117
     I                                       83  92 SFILE                 S01117
     I                                    B 123 1240SFNUM                 S01117
     I                                    B 367 3680LINE
     ISPOOLU      DS                                                      S01117
     I                                       83  92 SFILEU                S01117
     I                                    B 123 1240SFNUMU                S01117
     ICCLR        DS
     I*
     I**                  DATA STRUCTURES TO LOAD ARRAY WITH CHARGE/
     I**                                     COMMISSION LOWER RANGE LMT
     I*
     I                                    P   1  27 ALR
     I                                        1  27 ALRX
     I*
     ICCA         DS
     I*
     I**                  DATA STRUCTURE TO LOAD ARRAY WITH CHARGE
     I**                                    COMMISSION AMOUNT
     I*
     I                                    P   1  63 ACA
     I                                        1  63 ACAX
     I*****************************************************************
     I*
     I/COPY ZSRSRC,FFSTDPZ2
     I*
     I**   FOR CURRENCY DECIMAL PLACE SUBROUTINE
     I*
     I*****************************************************************
     I*
     I/COPY ZSRSRC,ZFRPEDZ2
     I*
     I**   FOR REPORT AMOUNT EDIT SUBROUTINE
     I*
     I*****************************************************************
     I*
     ILDA         DS                            256
     I*
     I**                  DATA STRUCTURE TO LOAD DB ERROR INFORMATION
     I**                                    TO LOCAL DATA AREA
     I*
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 177 DBASE                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
      **                                                                  CAC003
     ISDMMOD    E DSSDMMODPD                                              CAC003
     I** EXTERNAL DS FOR MODULES                                          CAC003
      **                                                                  S01117
     ISDBANK    E DSSDBANKPD                                              S01117
      **  EXTERNAL DS FOR BANK DETAILS                                    S01117
     I              BJURPT                          TITL                  S01117
     IDSFDY     E DSDSFDY                                                 S01117
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01117
      **                                                                  S01117
     IRUNDT       DS                                                      S01117
      **  DATA AREA RUNDAT                                                S01117
     I                                        1   7 RUNA                  S01117
     I                                    P   8  100RUND                  S01117
     I                                       11  11 SSF                   S01117
     I                                       12  12 DATF                  S01117
     I                                       13  13 MBIN                  S01117
     I*
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     C**
     C**   Receive Parameters
     C**
     C           *ENTRY    PLIST
     C                     PARM           SWS
     C                     PARM           SEQ              SEQUENCE NO    S01117
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*
     C*    MAIN BODY OF PROGRAM
     C*
     C                     EXSR START
     C                     EXSR REPORT
     C                     EXSR END
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*
     C*                   INDEX FOR SUB-ROUTINE
     C*
     C*****************************************************************
     C*
     C*                1.  START  -   INITIALISATION
     C*                2.  REPORT -   REPORT PROCESS & PRINTING
     C*                3.  ADLIN  -   DETAILED LINE PROCESS & PRINTING
     C*                4.  ACCESS -   READ NEXT VALID RECORD
     C*                5.  FFSTDP -   NUMBER OF DP'S FOR CURRENCY
     C*                6.  ZFRPED -   EDIT AMOUNT FIELDS
     C*                7.  CHECK  -   CHECK NUMBER OF VALID LINES/REC
     C*                8.  ZDATE2 -   CONVERT DAY NUMBER TO DATE
     C*                9.  END    -   END PROGRAM
     C*               10.  SWTS   -   RESET SWITCHES
     C*               11.  RCFSPL -   SPOOL BOTH P1 & AU REPORT           S01117
     C*               12.  PRTAU  -   PRINT AU REPORT                     S01117
     C*               13.  *PSSR  -   DATABASE ERROR HANDLING             S01117
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*
     C**   SUB-ROUTINE START - INTIALISATION & CHECKING FOR DB ERRORS
     C**                        CALLED BY - MAIN BODY
     C*                         CALLS     - SWTS
     C*****************************************************************
     C           START     BEGSR
     C*
     C* INITIALISING THE SWITCHES
     C                     MOVE SWS,1     *INU1
     C                     MOVE SWS,2     *INU2
     C                     MOVE SWS,3     *INU3
     C                     MOVE SWS,4     *INU4
     C                     MOVE SWS,5     *INU5
     C                     MOVE SWS,6     *INU6
     C                     MOVE SWS,7     *INU7
     C                     MOVE SWS,8     *INU8
     C*                                                                   S01117
     C                     Z-ADD*ZERO     COUNT   50                      S01117
     C*                                                                   S01117
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE *BLANK    DBPGM                           S01117
     C                     MOVE *BLANK    DBFILE                          S01117
     C                     MOVE *BLANK    DBKEY                           S01117
     C                     Z-ADD0         DBASE                           S01117
     C                     OUT  LDA                                       S01117
     C*                                                                   S01117
     C                     EXSR RCFSPL                     RCF            S01117
     C*                                                                   S01117
     C** IN THE DATAAREA RUNDAT                                           S01117
     C*                                                                   S01117
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           S01117
     C                     IN   RUNDT                                     S01117
     C*
      **                                                                  S01117
      **   ACCESS BANK DETAILS, FOR TITLE                                 S01117
     C                     CALL 'AOBANKR0'                                S01117
     C                     PARM '*MSG   ' @RTCD   7                       S01117
     C                     PARM '*FIRST ' @OPTN   7                       S01117
     C           SDBANK    PARM SDBANK    DSFDY                           S01117
     C*                                                                   S01117
     C           @RTCD     IFNE *BLANK                                    S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVEL'FF1130  'DBPGM                           S01117
     C                     MOVEL'SDBANKPD'DBFILE                          S01117
     C                     MOVEL@OPTN     DBKEY            ***************S01117
     C                     MOVEL@OPTN     DBOPTN  7        * DBERROR 001 *S01117
     C                     Z-ADD1         DBASE            ***************S01117
     C                     OUT  LDA                                       S01117
     C                     SETON                     U7U8LR               S01117
     C                     WRITEFMT4001                                   S01117
     C                     WRITEFMT9001                                   S01117
     C                     EXSR PRTAU                      PRINT AU       S01117
     C                     EXSR *PSSR                      DUMP           S01117
     C                     EXSR SWTS                                      S01117
     C                     RETRN                           EXIT           S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C** Check if Analytical Accounting and Switchable Feature            CAC003
     C** CAC003 are switched on.                                          CAC003
     C                     MOVE '0'       *IN20                           CAC003
     C                     CALL 'AOMMODR0'                                CAC003
     C                     PARM *BLANKS   @RTCD   7                       CAC003
     C                     PARM '*FIRST ' @OPTN   7                       CAC003
     C           SDMMOD    PARM SDMMOD    DSFDY                           CAC003
     C*                                                                   CAC003
     C           @RTCD     IFNE *BLANKS                                   CAC003
     C           *LOCK     IN   LDA                                       CAC003
     C                     MOVEL'FF1130  'DBPGM                           CAC003
     C                     MOVE 'SDMMODPD'DBFILE                          CAC003
     C                     MOVE *BLANKS   DBKEY            ***************CAC003
     C                     Z-ADD3         DBASE            * DBERROR 003 *CAC003
     C*                                                    ***************CAC003
     C                     OUT  LDA                                       CAC003
     C                     SETON                     U7U8LR               CAC003
     C                     WRITEFMT4001                                   CAC003
     C                     WRITEFMT9001                                   CAC003
     C                     EXSR PRTAU                      PRINT AU       CAC003
     C                     EXSR *PSSR                                     CAC003
     C                     EXSR SWTS                                      CAC003
     C                     RETRN                           EXIT           CAC003
     C                     END                                            CAC003
     C*                                                                   CAC003
     C                     CALL 'AOSARDR0'                                CAC003
     C                     PARM           @RTCD                           CAC003
     C                     PARM '*VERIFY' @OPTN                           CAC003
     C                     PARM 'CAC003'  @SARD   6                       CAC003
      *                                                                   CAC003
     C           @RTCD     IFEQ *BLANK                                    CAC003
     C           BGN0ST    ANDEQ'Y'                                       CAC003
     C                     MOVE '1'       *IN20                           CAC003
     C                     ENDIF                                          CAC003
     C*                                                                   CAC003
     C***********          READ TABTB10F                 40               S01117
     C***********                                                         S01117
     C*****CHECK*THAT*INSTALLATION*CONTROL*DATA*PRESENT*******************S01117
     C*****IF*NOT*THEN*DATABASE*ERROR*************************************S01117
     C********SET*UP*DATABASE*ERROR***************************************S01117
     C**N40******RECI      COMP 'D'                  4040  *** DBERR 01 ***01117
     C************IN40     IFEQ '1'                                       S01117
     C***********          SETON                     U7U8LR               S01117
     C************NAMVAR   DEFN           LDA                             S01117
     C************LOCK     IN   LDA                                       S01117
     C***********          MOVE 'FF1130  'DBPGM                           S01117
     C***********          MOVE *BLANKS   DBKEY                           S01117
     C***********          MOVE 'TABFF'   DBFILE                          S01117
     C***********          MOVE '    10'  DBKEY1 12                       S01117
     C***********          MOVEL'01    '  DBKEY1                          S01117
     C***********          MOVELDBKEY1    DBKEY                           S01117
     C***********          MOVE '01'      DBASE                           S01117
     C***********          OUT  LDA                                       S01117
     C*****PRINT*OUT HEADINGS WITHOUT INSTALLATION CONTROL                S01117
     C*****AND*THEN DATABASE ERROR MESSAGE                                S01117
     C***********          WRITEFMT4001                                   S01117
     C***********          WRITEFMT9001                                   S01117
     C***********          EXSR SWTS                                      S01117
     C***********          RETRN                                          S01117
     C***********          END                                            S01117
     C*
     C** OPEN CHGCM FILE IF U3 IS NOT ON
     C*
     C  NU3                OPEN CHGCM
     C*
     C*    DETERMINE DATE FORMAT
     C*
     C           DATF      COMP 'M'                      98
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*
     C**   SUB-ROUTINE REPORT - PROCESSING & PRINTING OUT REPORT
     C**                        CALLED BY - MAIN BODY
     C**                        CALLS S/R'S - ACCESS, FFSTDP, ZDATE2,
     C**                                 CHECK, ZFRPED, ADLIN & SWTS
     C*
     C*****************************************************************
     C           REPORT    BEGSR
     C***************************************************************
     C*
     C/COPY ZSRSRC,FFSTDPZ3
     C*
     I**   FOR CURRENCY DECIMAL PLACE SUBROUTINE
     C*
     C***************************************************************
     C*
     C**   PRINT OUT INITIAL HEADINGS
     C*
     C                     WRITEFMT4001
     C                     EXSR ACCESS
     C*
     C*    READ FILE AND CHECK FOR RECORD AND RECORD VALID
     C*    IF NO RECORD PRINTOUT NO CHARGES MESSAGE
     C*
     C           *IN41     IFEQ '1'
     C                     WRITEFMTP106
     C                     ELSE
     C           *IN41     DOWNE'1'
     C*
     C*    EXECUTE SUB-ROUTINE TO FIND NUMBER OF DECIMAL PLACES
     C*
     C                     MOVE CCY       FFCCY   3
     C                     EXSR FFSTDP
     C**
     C           *IN89     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE 'FF1130  'DBPGM
     C                     MOVE *BLANKS   DBKEY
     C                     MOVE 'TABFF'   DBFILE
     C                     MOVELFFCKY     DBKEY
     C***********          MOVE '02'      DBASE            *** DBERR 02 **S01117
     C                     Z-ADD2         DBASE            *** DBERR 02***S01117
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C*
     C*    PRINT OUT HEADINGS WITHOUT INSTALLATION CONTROL
     C*    AND THEN DATABASE ERROR MESSAGE
     C                     WRITEFMT9001
     C                     EXSR PRTAU                      AU RPT         S01117
     C                     EXSR *PSSR                      DUMP           S01117
     C                     EXSR SWTS
     C                     RETRN
     C                     END
     C*
     C*    DOW ROUTINE FOR RECORDS WITH SAME CURRENCY
     C*
     C           FFCCY     DOWEQCCY
     C           *IN41     ANDNE'1'
     C                     MOVE PLCD      ZDAYNO  50
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RLCD    7
     C*
     C** MOVE RANGES/AMOUNTS INTO WORK ARRAYS
     C*
     C                     MOVEADET,42    ALRX
     C                     MOVEADET,69    ACAX
     C*
     C*    EXECUTE SUB-ROUTINE TO CHECK NUMBER OF VALID ENTRIES
     C*    IN A RECORD
     C*
     C                     Z-ADD1         M       20
     C                     EXSR CHECK
     C*
     C*    CHECK TO SEE IF RECORD WILL FIT ON PAGE
     C*    IF NOT THEN PRINT OUT NEW PAGE HEADINGS
     C*
     C           M         ADD  LINE      LEN     20
     C           LEN       IFGT 55
     C                     WRITEFMT4001
     C                     END
     C*
     C*    WHILE RECORD EXISTS PROCESS FIELDS
     C*
     C           TTFI      IFEQ 'T'
     C                     MOVE 'TIERED'  RTTFI   6
     C                     END
     C           TTFI      IFEQ 'X'
     C                     MOVE 'THRESH'  RTTFI
     C                     END
     C           TTFI      IFEQ 'F'
     C                     MOVE ' FLAT '  RTTFI
     C                     END
     C           PCHTP     IFEQ 'A'
     C                     MOVE 'AMD'     RCHTP   3
     C                     END
     C           PCHTP     IFEQ 'I'
     C                     MOVE 'INS'     RCHTP
     C                     END
     C           PCHTP     IFEQ 'D'
     C                     MOVE 'DEL'     RCHTP
     C                     END
     C*
     C**  CHECK FOR ENTRY TYPE
     C*
     C                     MOVEL*BLANK    ENTT
     C           CHTP      IFEQ 'A'
     C           JOENTT    IFEQ 'UB'
     C                     MOVE 'OLD'     ENTT
     C                     END
     C           JOENTT    IFEQ 'UP'
     C                     MOVE 'NEW'     ENTT
     C                     END
     C                     END
     C*
     C*    MOVE NUMERIC VALUES TO FIELD WITH CORRECT NUMBER
     C*    OF DECIMAL PLACES
     C*
     C                     Z-ADDFFCDP     ZDECS
     C                     Z-ADDACA,1     ZFLD15
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    RCA01
     C*
     C*    IF A MAXIMUM CHARGE HAS BEEN ENTERED,
     C*    MOVE NUMERIC VALUES TO FIELD WITH CORRECT NUMBER
     C*    OF DECIMAL PLACES
     C*
     C                     MOVE *BLANKS   RMAXC                           E20461
     C           MAXC      IFNE 0
     C                     Z-ADDFFCDP     ZDECS
     C                     Z-ADDMAXC      ZFLD15
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    RMAXC
     C                     END
     C*
     C*    MOVE NUMERIC VALUES TO FIELD WITH CORRECT NUMBER
     C*    OF DECIMAL PLACES
     C*
     C                     Z-ADDFFCDP     ZDECS
     C                     Z-ADDMINC      ZFLD15
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    RMINC
     C*    PRINT OUT FIRST LINE AND EXECUTE SUB-ROUTINE TO PROCESS
     C*    ADDITIONAL LINES
     C*
     C                     WRITEFMT4002
     C                     EXSR ADLIN
     C*
     C*    READ NEXT RECORD
     C*
     C                     EXSR ACCESS
     C                     END
     C                     END
     C                     END
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C*****************************************************************
     C*
     C**   SUB-ROUTINE ADLIN - PROCESS & PRINT OUT ADDITIONAL LINES
     C**                        CALLED BY - REPORT
     C**                        CALLS SR'S - ZFRPED
     C*
     C*****************************************************************
     C           ADLIN     BEGSR
     C                     Z-ADD2         N       20
     C           N         DOWLEM
     C                     MOVE ALR,N     RLRN
     C*
     C*    MOVE NUMERIC VALUES TO FIELD WITH CORRECT NUMBER
     C*    OF DECIMAL PLACES
     C*
     C                     Z-ADDFFCDP     ZDECS
     C                     Z-ADDACA,N     ZFLD15
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    RCA01
     C*
     C*    PRINT OUT ADDITIONAL LINE DETAILS
     C*
     C                     WRITEFMT4003
     C           N         ADD  1         N
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C********************************************************************
     C*
     C**   SUB-ROUTINE ACCESS - READ NEXT VALID RECORD
     C**                        CALLED BY - REPORT
     C*
     C********************************************************************
     C           ACCESS    BEGSR
     C           *IN41     DOWEQ'0'
     C           *IN42     ANDEQ'0'
     C  NU3                READ CHGCM                    41
     C   U3                READ JRNCGD                   41
     C  N41                ADD  1         COUNT   50       AU COUNTER     S01117
     C* N41      RECI      COMP 'D'                      42               E15864
     C           RECI      IFEQ 'D'                                       E15864
     C           RECI      OREQ '*'                                       E15864
     C                     SETON                         42               E15864
     C                     END                                            E15864
     C*
     C                     END
     C*
     C**   MOVE LAST CHANGE DATE & TYPE TO SAFE FIELDS
     C*
     C*    ONLY IF NOT EOF                                                HK0000
     C           *IN41     IFEQ '0'                                       HK0000
     C                     Z-ADDLCD       PLCD    50
     C                     MOVE CHTP      PCHTP   1
     C                     END                                            HK0000
     C                     SETOF                     42
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C/COPY ZSRSRC,FFSTDPZ4
     C*
     C**   FFSTDP SR. TO DETERMINE NUMBER OF CURRENCY DECIMAL PLACES
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C/COPY ZSRSRC,ZFRPEDZ3
     C*
     C**   ZFRPED SR. TO EDIT MIDAS AMOUNT FIELDS FOR OUTPUT
     C**               TO EXTERNALLY DESCRIBED PRINTER FILE
     C*
     C********************************************************************
     C/EJECT
     C*****************************************************************
     C*
     C**   SUB-ROUTINE CHECK - CHECK NUMBER OF VALID LINES PER RECORD
     C**                        CALLED BY - REPORT
     C*
     C*****************************************************************
     C           CHECK     BEGSR
     C*
     C*    CHECK COUNTER LESS THAN 9 AND ARRAY ELEMENT NOT BLANK
     C*
     C           ACA,M     DOWNE0
     C           M         ANDLT9
     C           M         ADD  1         M
     C                     END
     C           ACA,9     IFEQ 0
     C           M         SUB  1         M
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*
     C/COPY ZSRSRC,ZDATE2Z2
     C*
     C/EJECT
     C*****************************************************************
     C*
     C**   SUB-ROUTINE TO PRINT OUT END OF REPORT FORMAT
     C**                        CALLED BY - MAIN BODY
     C**                        CALLS     - SWTS
     C*
     C*****************************************************************
     C           END       BEGSR
     C                     SETON                     LR
     C                     WRITEFMTP105
     C                     EXSR SWTS
     C                     EXSR PRTAU                                     S01117
     C                     RETRN
     C                     ENDSR
     C*
     C*****************************************************************
     C*
     C*   SWTS-SUBROUTINE TO RESET SWITCHES PARAMETER
     C*
     C*  CALLED  FROM  SR. END, REPORT, START
     C*
     C********************************************************************
     C*
     C           SWTS      BEGSR
     C                     MOVE *INU1     SWS,1
     C                     MOVE *INU2     SWS,2
     C                     MOVE *INU3     SWS,3
     C                     MOVE *INU4     SWS,4
     C                     MOVE *INU5     SWS,5
     C                     MOVE *INU6     SWS,6
     C                     MOVE *INU7     SWS,7
     C                     MOVE *INU8     SWS,8
     C                     ENDSR
     C*
     C****************************************************************
     C********************************************************************
     C/EJECT                                                              S01117
     C********************************************************************S01117
      **                                                                  S01117
      ** RCFSPL -- SUBROUTINE TO SPOOL BOTH P1 AND AU THROUGH RCF         S01117
      ** CALLED FROM: BEFORE END OF PROGRAM                               S01117
      ** CALLS: NOTHING                                                   S01117
      **                                                                  S01117
     C********************************************************************S01117
     C*                                                                   S01117
     C           RCFSPL    BEGSR                           ** RCFSPL **   S01117
     C*                                                                   S01117
     C**      ENSURE REPORT SPOOL FILE RECORDED BY RCF                    S01117
     C*                                                                   S01117
     C                     Z-ADDSFNUM     ZSNUM   60                      S01117
     C**                                                                  S01117
     C                     CALL 'ZSFILE'                                  S01117
     C                     PARM           SEQ     5                       S01117
     C                     PARM *BLANK    SENTY   3                       S01117
     C                     PARM           SFILE                           S01117
     C                     PARM           ZSNUM                           S01117
     C                     PARM           ZSERR   1                       S01117
     C*                                                                   S01117
     C           ZSERR     IFEQ 'Y'                                       S01117
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       S01117
     C                     SETON                     U7U8LR               S01117
     C                     RETRN                                          S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C**      ENSURE TOTAL SPOOL FILE RECORDED BY RCF                     S01117
     C*                                                                   S01117
     C                     Z-ADDSFNUMU    ZSNUMU  60                      S01117
     C**                                                                  S01117
     C                     CALL 'ZSFILE'                                  S01117
     C                     PARM           SEQ     5                       S01117
     C                     PARM *BLANK    SENTY   3                       S01117
     C                     PARM           SFILEU                          S01117
     C                     PARM           ZSNUMU                          S01117
     C                     PARM           ZSERR   1                       S01117
     C*                                                                   S01117
     C           ZSERR     IFEQ 'Y'                                       S01117
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       S01117
     C                     SETON                     U7U8LR               S01117
     C                     RETRN                                          S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C                     ENDSR                                          S01117
     C*                                                                   S01117
     C/EJECT                                                              S01117
     C********************************************************************S01117
      **   PRTAU -- SUBROUTINE TO PRINT AU REPORT                         S01117
      ** CALLS: NOTHING                                                   S01117
     C********************************************************************S01117
     C           PRTAU     BEGSR                                          S01117
     C*                                                                   S01117
     C           *INU3     IFEQ '1'                                       S01117
     C                     SETON                     30                   S01117
     C                     ELSE                                           S01117
     C                     SETOF                     30                   S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C                     WRITEFMTAU01                                   S01117
     C                     WRITEFMTAU02                                   S01117
     C*                                                                   S01117
     C           COUNT     IFEQ *ZERO                      NO DETL        S01117
     C           *INU7     ANDEQ'0'                                       S01117
     C                     WRITEFMTAU03                                   S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C           *INU7     IFEQ '1'                                       S01117
     C                     WRITEFMTAU04                                   S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C                     ENDSR                                          S01117
     C********************************************************************S01117
     C/EJECT
     C********************************************************************S01117
      **                                                                  S01117
      ** *PSSR --- SUBROUTINE TO HANDLE ABNORMAL ERROR CONDITIONS         S01117
      ** CALLED FROM: AFTER ABNORMAL OPERATION OCCURS                     S01117
      ** CALLS: NOTHING                                                   S01117
      **                                                                  S01117
     C********************************************************************S01117
     C*                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C*                                                                   S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C                     ENDSR                           ** *PSSR **    S01117
     C*                                                                   S01117
     C********************************************************************S01117
     C/EJECT                                                              S01117
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
