     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FF Account Keys List')
     F********************************************************************
     F*                                                                  *
     F*  Midas - Futures and Options Module
     F*                                                                  *
     F*   FF1180  -  ACCOUNT KEYS LIST                                   *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 FO0062/E2          Date 09Apr91               *
     F*                 S01117             Date 01FEB90               *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*   FO0062  -  DO NOT OUTPUT P1 REPORT IF FO DETAILS TO REPORT     *
     F*   S01117  -  MULTIBRANCHING                                      *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*     CREATION REQUIREMENTS                                        *
     F*                                                                  *
     F*     IGNDECERR(*NO)                                               *
     F*                                                                  *
     F********************************************************************
     F*
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*ABTB10*IF* E           K        DISK                               S01117
     F*
     FFACKY   IF  E           K        DISK                           UC
     FJRNCGD  IF  E           K        DISK                           U3
     F*
     F*F1180P1O   E           K        DISK                               FO0062
     FFF1180P1O   E           K        DISK                           UC  FO0062
     F***********                                   KINFDS PRINT          S01117
     F                                              KINFDS SPOOL          S01117
     FFF1180AUO   E           K        DISK                               S01117
     F                                              KINFDS SPOOLU         S01117
     F/COPY WNCPYSRC,FF1180F001
     F/SPACE 2
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*       49        ON IF PRODUCING COB AUDIT REPORT (U3 ON)
     F*       50        END OF FILE PF/JRNCGD
     F*       51        END OF FILE LF/FACKY
     F*       52        RECORDS WRITTEN TO REPORT
     F*     61-66       ZERO ENTRIES-ACCOUNT CODES 1-6
     F*     71-76       ZERO ENTRIES-NARRATIVE CODES 1-6
     F*
     F*     80-89     RESERVED FOR STANDARD FF SUBROUTINES
     F*     90-99     RESERVED FOR STANDARD  Z SUBROUTINES
     F*
     F*            U1 LIST TODAY'S DETAILS FROM LF/FACKY
     F*            U3 LIST ALL DETAILS FROM PF/JRNCGD
     F*            U7 )_ DATABASE ERROR
     F*            U8 )
     F*            LR LAST RECORD
     F/SPACE 2
     E                    CPY@    1   1 80
     E*
     E/COPY ZSRSRC,ZDATE2Z1
     E** SR.ARRAY FOR ZDATE2
     E*
     E                    SWS         8  1
     E** SWITCHES
     E*
     IJOESD     E DSFACKYD
     I*
     I** DATA STRUCTURE FOR RECORD LAYOUT
     I*
     I*RINT****** DS                                                      S01117
     I***DS*FOR*LINE NO. TO HANDLE OVERFLOW                               S01117
     I*                                                                   S01117
     I** FILE INFORMATION DATA STRUCTURE                                  S01117
     I*                                                                   S01117
     ISPOOL       DS                                                      S01117
     I                                       83  92 SFILE                 S01117
     I                                    B 123 1240SFNUM                 S01117
     I                                    B 367 3680CURLIN
     ISPOOLU      DS                                                      S01117
     I                                       83  92 SFILEU                S01117
     I                                    B 123 1240SFNUMU                S01117
     I*
     ILDA         DS                            256
     I*  LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 177 DBASE                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I*                                                                   S01117
     I** DATA AREA RUNDAT                                                 S01117
     I*                                                                   S01117
     IRUNDT       DS                                                      S01117
     I                                        1   7 RUNA                  S01117
     I                                    P   8  100RUND                  S01117
     I                                       11  11 SSF                   S01117
     I                                       12  12 DATF                  S01117
     I                                       13  13 MBIN                  S01117
      *                                                                   S01117
     ISDBANK    E DSSDBANKPD                                              S01117
      **  EXTERNAL DS FOR BANK DETAILS                                    S01117
     I              BJURPT                          TITL                  S01117
      *                                                                   S01117
     I@DSFDY    E DSDSFDY                                                 S01117
      **  FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE              S01117
      *                                                                   S01117
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     C**
     C**   Receive Parameters
     C**
     C           *ENTRY    PLIST
     C                     PARM           SWS
     C                     PARM           SEQ     5        SEQUENCE NO    S01117
     C/EJECT
     C*================================================================
     C*  P R O G R A M     I N I T I A L I S A T I O N
     C*================================================================
     C*
     C**   Reset LDA
     C**
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL*BLANKS   DBPGM                           S01117
     C                     MOVEL'FF1180  'DBPGM
     C                     MOVEL*BLANKS   DBKEY
     C                     MOVEL*BLANKS   DBFILE
     C                     MOVEL*ZEROS    DBASE
     C                     OUT  LDA
     C*                                                                   S01117
     C**   IN THE DATAAREA RUNDAT                                         S01117
     C*                                                                   S01117
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           S01117
     C                     IN   RUNDT                                     S01117
     C*                                                                   S01117
     C**   ZEROISE HASH COUNT                                             S01117
     C*                                                                   S01117
     C                     Z-ADD*ZERO     COUNT   50                      S01117
     C*
     C* INITIALISING THE SWITCHES
     C**
     C                     MOVE SWS,1     *INU1
     C                     MOVE SWS,2     *INU2
     C                     MOVE SWS,3     *INU3
     C                     MOVE SWS,4     *INU4
     C                     MOVE SWS,5     *INU5
     C                     MOVE SWS,6     *INU6
     C                     MOVE SWS,7     *INU7
     C                     MOVE SWS,8     *INU8
     C*
     C** Set indicator to enable entry type heading to be printed, for
     C*     COB audit report.
     C*
     C   U3                SETON                     49
     C*
     C*
     C***Access*ICD1*for*Titles,Dates*etc.********************************S01117
     C***********                                                         S01117
     C***********          READ TABTB10F                 99*              S01117
      *                                                                   S01117
      **   ACCESS BANK DETAILS                                            S01117
      *                                                                   S01117
     C                     CALL 'AOBANKR0'                                S01117
     C                     PARM '*MSG   ' @RTCD   7                       S01117
     C                     PARM '*FIRST ' @OPTN   7                       S01117
     C           SDBANK    PARM SDBANK    @DSFDY                          S01117
     C*                                                                   S01117
     C** Write Report Headings
     C*********************WRITEFMTP801                                   FO0062
     C                     WRITEFMTAU01                                   S01117
     C*                                                                   S01117
     C**N99******RECI      COMP 'D'                  9999  ON, IS DELETED S01117
     C************IN99     IFEQ '0'                                       S01117
     C*
     C** If live Record is found,check date format required.
     C*
     C           DATF      COMP 'M'                      98MMDDYY, 98 ON
     C***********          ELSE                                           S01117
     C           @RTCD     IFNE *BLANK                                    S01117
     C*
     C** If Record not found,Database Error 01
     C*
     C           *LOCK     IN   LDA
     C                     SETON                     U7U8  ***************
     C***********          MOVEL'01'      DBASE            **DB ERROR 01**S01117
     C***********          MOVEL'TB10 '   DBFILE           ***************S01117
     C***********          MOVEL*BLANKS   DBKEY                           S01117
     C                     Z-ADD1         DBASE            **DB ERROR 01**S01117
     C                     MOVEL'SDBANKPD'DBFILE           ***************S01117
     C                     MOVEL@OPTN     DBKEY                           S01117
     C                     MOVEL@OPTN     DBOPTN  7                       S01117
     C                     OUT  LDA
     C                     EXSR *PSSR                                     S01117
     C** Write Database Error details
     C*********************WRITEFMTP805                                   FO0062
     C                     WRITEFMTAU04                                   S01117
     C                     EXSR RCFSPL                                    S01117
     C                     SETON                     U7U8LR
     C                     EXSR SWTS
     C                     RETRN
     C                     END
     C/COPY WNCPYSRC,FF1180C001
     C*
     C/EJECT
     C*================================================================
     C*  P R O G R A M     M A I N
     C*================================================================
     C*
     C** There are 3 possible alternatives when calling this Program:-
     C**     1.U3 is on - Report all details from PF/JRNCGD
     C**     2.U1 is on - Report Today's details from LF/FACKY
     C**     3.Neither Switch on - Report all from LF/FACKY
     C*
     C           *INU3     CASEQ'1'       JOURN
     C           *INU1     CASEQ'1'       AKDAY
     C                     CAS            AKALL
     C                     END
     C/EJECT
     C*================================================================
     C*  P R O G R A M     C L O S E
     C*================================================================
     C*
     C** Write Report Footings
      *                                                                   FO0062
      ** ONLY WRITE THE REPORT FOOTER IF THERE HAVE BEEN ANY DETAILS      FO0062
      ** I.E. THE REPORT HAS BEEN OPENED                                  FO0062
      *                                                                   FO0062
     C           *IN52     IFEQ '1'                                       FO0062
     C                     WRITEFMTP803
     C                     END                                            FO0062
     C*                                                                   S01117
     C**    CALL RCF                                                      S01117
     C*                                                                   S01117
     C                     EXSR RCFSPL                                    S01117
     C*                                                                   S01117
     C                     SETON                     LR
     C                     EXSR SWTS
     C                     RETRN
     C/EJECT
     C********************************************************************
     C*
     C*                   INDEX TO SUB-ROUTINES
     C*                   ---------------------
     C*
     C*     1.S.R. JOURN   - PRINTS ALL DETAILS ON PF/JRNCGD
     C*     2.S.R. AKDAY   - PRINTS TODAY'S DETAILS ON LF/FACKY
     C*     3.S.R. AKALL   - PRINTS ALL DETAILS ON LF/FACKY
     C*     4.S.R. EZBAL   - EDITS CODE FIELDS FOR OUTPUT
     C*     5.S.R. ZDATE2  - CHANGES MIDAS DAY No. TO DATE
     C*     6.S.R. SWTS    - RESET SWITCHES
     C*     7.S.R. RCFSPL  - SPOOL REPORTS PRINTER FILE                   S01117
     C*     8.S.R. *PSSR   - HANDLES ABNORMAL ERROR CONDITIONS            S01117
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C*   S.R. JOURN - SUB-ROUTINE TO ACCESS JOURNAL CHANGES FILE
     C*                AND PRINT ALL DETAILS  IF U3 IS ON
     C*
     C*  CALLED ONCE FROM MAIN PROGRAM
     C*  CALLS SR. EZBAL
     C*
     C********************************************************************
     C*
     C           JOURN     BEGSR
     C                     READ JRNCGD                   50
     C*
     C** If no Records in File,End of file Indicator is on.
     C           *IN50     IFEQ '1'
     C*********************WRITEFMTP804                                   FO0062
     C                     WRITEFMTAU02                                   S01117
     C                     WRITEFMTAU03                                   S01117
     C***********          END                                            S01117
     C                     ELSE                                           S01117
      *                                                                   FO0062
      ** IF THERE ARE RECORDS IN THE FILE THEN THE P1 REPORT MUST BE      FO0062
      ** OPENED, AND THE TITLES WRITTEN.                                  FO0062
      *                                                                   FO0062
     C                     OPEN FF1180P1                                  FO0062
     C                     WRITEFMTP801                                   FO0062
     C                     SETON                     52                   FO0062
     C*
     C** Do while Records remain on file.
     C           *IN50     DOWNE'1'
     C                     ADD  1         COUNT                           S01117
     C** Write Details until Page is full.
     C           CURLIN    IFGE 59
     C*
     C** If Page is full,write Headings on next Page.
     C                     WRITEFMTP801
     C                     END
     C** Write only 'live' Records from Today.
     C           RECI      IFEQ 'D'
     C*
     C** Edit DEBIT/CREDIT Code fields for Output.
     C                     EXSR EZBAL
     C*
     C**  Check for last change type.
     C*
     C                     MOVE *BLANKS   TYPE    3
     C           CHTP      IFEQ 'I'
     C                     MOVE 'INS'     TYPE
     C                     END
     C           CHTP      IFEQ 'A'
     C                     MOVE 'AMD'     TYPE
     C                     END
     C           CHTP      IFEQ 'D'
     C                     MOVE 'DEL'     TYPE
     C                     END
     C*
     C                     MOVEL*BLANKS   ENTT
     C           JOENTT    IFEQ 'UB'
     C           CHTP      ANDEQ'A'
     C                     MOVE 'OLD'     ENTT
     C                     END
     C           JOENTT    IFEQ 'UP'
     C           CHTP      ANDEQ'A'
     C                     MOVE 'NEW'     ENTT
     C                     END
     C*
     C** Format date
     C                     MOVELLCD       ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    FDATE   7
     C*
     C                     WRITEFMTP802
     C/COPY WNCPYSRC,FF1180C002
     C                     END
     C** Read next Record.
     C                     READ JRNCGD                   50
     C                     END
     C*
     C                     WRITEFMTAU02                                   S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C                     ENDSR
     C/EJECT
     C********************************************************************
     C*
     C*   S.R. AKDAY - SUB-ROUTINE TO ACCESS LF/FACKY AND PRINT TODAY'S
     C*                DETAILS IF U1 IS ON
     C*
     C*  CALLED ONCE FROM MAIN PROGRAM
     C*  CALLS SR. EZBAL
     C*
     C********************************************************************
     C*
     C           AKDAY     BEGSR
     C                     OPEN FACKY
     C                     READ FACKY                    51
     C*
     C** Do while Records remain on file.
     C           *IN51     DOWNE'1'
     C                     ADD  1         COUNT                           S01117
     C** Write Details until Page is full.
     C           CURLIN    IFGE 59
     C           *IN52     ANDEQ'1'                                       FO0062
     C*
     C** If Page is full,write Headings on next Page.
     C                     WRITEFMTP801
     C                     END
     C** Write only 'live' Records from Today.
     C           RECI      IFEQ 'D'
     C           LCD       ANDEQRUND
     C*
     C** Edit DEBIT/CREDIT Code fields for Output.
     C                     EXSR EZBAL
     C*
     C**  Check for last change type.
     C*
     C                     MOVE *BLANKS   TYPE    3
     C           CHTP      IFEQ 'I'
     C                     MOVE 'INS'     TYPE
     C                     END
     C           CHTP      IFEQ 'A'
     C                     MOVE 'AMD'     TYPE
     C                     END
     C           CHTP      IFEQ 'D'
     C                     MOVE 'DEL'     TYPE
     C                     END
     C*
     C                     MOVE *BLANKS   ENTT    3
     C*
     C** Format date
     C                     MOVELLCD       ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    FDATE   7
     C*
     C*
     C*********************MOVE '1'       *IN52                           FO0062
      ** Open P1 report if not already open                               FO0062
     C           *IN52     IFEQ '0'                                       FO0062
     C                     OPEN FF1180P1                                  FO0062
     C                     WRITEFMTP801                                   FO0062
     C                     SETON                     52                   FO0062
     C                     END                                            FO0062
     C                     WRITEFMTP802
     C/COPY WNCPYSRC,FF1180C003
     C                     END
     C** Read next Record.
     C                     READ FACKY                    51
     C                     END
     C*                                                                   S01117
     C** OUTPUT AUDIT TOTALS                                              S01117
     C                     WRITEFMTAU02                                   S01117
     C*
     C** If no Details Today or File empty,Print no Details.
     C           *IN52     IFEQ '0'
     C*********************WRITEFMTP804                                   FO0062
     C                     WRITEFMTAU03                                   S01117
     C                     END
     C*
     C                     ENDSR
     C/EJECT
     C********************************************************************
     C*
     C*   S.R. AKALL - SUB-ROUTINE TO ACCESS LF/FACKY AND PRINT ALL
     C*                DETAILS IF U3 AND U1 ARE BOTH OFF
     C*
     C*  CALLED ONCE FROM MAIN PROGRAM
     C*  CALLS SR. EZBAL
     C*
     C********************************************************************
     C*
     C           AKALL     BEGSR
     C                     OPEN FACKY
     C                     READ FACKY                    51
     C*
     C** If no Records in File,End of file Indicator is on.
     C           *IN51     IFEQ '1'
     C*********************WRITEFMTP804                                   FO0062
     C                     WRITEFMTAU02                                   S01117
     C                     WRITEFMTAU03                                   S01117
     C***********          END                                            S01117
     C                     ELSE                                           S01117
      *                                                                   FO0062
      ** IF THERE ARE RECORDS IN THE FILE THEN THE P1 REPORT MUST BE      FO0062
      ** OPENED, AND THE TITLES WRITTEN.                                  FO0062
      *                                                                   FO0062
     C                     OPEN FF1180P1                                  FO0062
     C                     WRITEFMTP801                                   FO0062
     C                     SETON                     52                   FO0062
     C*
     C** Do while Records remain on file.
     C           *IN51     DOWNE'1'
     C                     ADD  1         COUNT                           S01117
     C** Write Details until Page is full.
     C           CURLIN    IFGE 59
     C*
     C** If Page is full,write Headings on next Page.
     C                     WRITEFMTP801
     C                     END
     C*
     C** Write only 'live' Details.
     C           RECI      IFEQ 'D'
     C** Edit DEBIT/CREDIT Code fields for Output.
     C                     EXSR EZBAL
     C*
     C**  Check for last change type.
     C*
     C                     MOVE *BLANKS   TYPE    3
     C           CHTP      IFEQ 'I'
     C                     MOVE 'INS'     TYPE
     C                     END
     C           CHTP      IFEQ 'A'
     C                     MOVE 'AMD'     TYPE
     C                     END
     C           CHTP      IFEQ 'D'
     C                     MOVE 'DEL'     TYPE
     C                     END
     C*
     C                     MOVE *BLANKS   ENTT    3
     C*
     C** Format date
     C                     MOVELLCD       ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    FDATE   7
     C*
     C                     WRITEFMTP802
     C/COPY WNCPYSRC,FF1180C004
     C                     END
     C*
     C                     READ FACKY                    51
     C                     END
     C*
     C                     WRITEFMTAU02                                   S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C                     ENDSR
     C/EJECT
     C********************************************************************
     C*
     C*   S.R. EZBAL - SUB-ROUTINE TO OUTPUT NON-ZERO ACCOUNT CODES
     C*                AND NARRATIVE CODES ONLY.
     C*
     C*  CALLED BY :-  S.R. JOURN
     C*                S.R. AKDAY
     C*                S.R. AKALL
     C*  CALLS SR.
     C*
     C********************************************************************
     C*
     C           EZBAL     BEGSR
     C*
     C** Set off all Output Indicators.
     C                     MOVEA'000000'  *IN,61
     C                     MOVEA'000000'  *IN,71
     C           ACD1      IFEQ *ZEROS
     C                     MOVE '1'       *IN61
     C                     END
     C           NRC1      IFEQ *ZEROS
     C                     MOVE '1'       *IN71
     C                     END
     C           ACD2      IFEQ *ZEROS
     C                     MOVE '1'       *IN62
     C                     END
     C           NRC2      IFEQ *ZEROS
     C                     MOVE '1'       *IN72
     C                     END
     C           ACD3      IFEQ *ZEROS
     C                     MOVE '1'       *IN63
     C                     END
     C           NRC3      IFEQ *ZEROS
     C                     MOVE '1'       *IN73
     C                     END
     C*
     C           ACD4      IFEQ *ZEROS
     C                     MOVE '1'       *IN64
     C                     END
     C           NRC4      IFEQ *ZEROS
     C                     MOVE '1'       *IN74
     C                     END
     C           ACD5      IFEQ *ZEROS
     C                     MOVE '1'       *IN65
     C                     END
     C           NRC5      IFEQ *ZEROS
     C                     MOVE '1'       *IN75
     C                     END
     C           ACD6      IFEQ *ZEROS
     C                     MOVE '1'       *IN66
     C                     END
     C           NRC6      IFEQ *ZEROS
     C                     MOVE '1'       *IN76
     C                     END
     C*
     C                     ENDSR
     C/EJECT
     C** ZDATE2 SUBROUTINE
     C/COPY ZSRSRC,ZDATE2Z2
     C*
     C/EJECT
     C****************************************************************
     C*
     C*   SWTS-SUBROUTINE TO RESET SWITCHES PARAMETER
     C*
     C*  CALLED  FROM  PROGRAM INIT & CLOSE
     C*
     C********************************************************************
     C*
     C           SWTS      BEGSR
     C                     MOVE *INU1     SWS,1
     C                     MOVE *INU2     SWS,2
     C                     MOVE *INU3     SWS,3
     C                     MOVE *INU4     SWS,4
     C                     MOVE *INU5     SWS,5
     C                     MOVE *INU6     SWS,6
     C                     MOVE *INU7     SWS,7
     C                     MOVE *INU8     SWS,8
     C                     ENDSR
     C*
     C********************************************************************
     C/EJECT                                                              S01117
     C********************************************************************S01117
      **                                                                  S01117
      ** RCFSPL -- SUBROUTINE TO SPOOL BOTH P1 AND AU THROUGH RCF         S01117
      ** CALLED FROM: BEFORE END OF PROGRAM                               S01117
      ** CALLS: NOTHING                                                   S01117
      **                                                                  S01117
     C********************************************************************S01117
     C*                                                                   S01117
     C           RCFSPL    BEGSR                           ** RCFSPL **   S01117
     C*                                                                   S01117
     C**      ENSURE REPORT SPOOL FILE RECORDED BY RCF                    S01117
     C*                                                                   S01117
     C           *IN52     IFEQ '1'                                       FO0062
     C                     Z-ADDSFNUM     ZSNUM   60                      S01117
     C**                                                                  S01117
     C                     CALL 'ZSFILE'                                  S01117
     C                     PARM           SEQ     5                       S01117
     C                     PARM *BLANK    SENTY   3                       S01117
     C                     PARM           SFILE                           S01117
     C                     PARM           ZSNUM                           S01117
     C                     PARM           ZSERR   1                       S01117
     C*                                                                   S01117
     C           ZSERR     IFEQ 'Y'                                       S01117
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       S01117
     C                     SETON                     U7U8LR               S01117
     C                     RETRN                                          S01117
     C                     END                                            S01117
     C                     END                                            FO0062
     C*                                                                   S01117
     C**      ENSURE TOTAL SPOOL FILE RECORDED BY RCF                     S01117
     C*                                                                   S01117
     C                     Z-ADDSFNUMU    ZSNUMU  60                      S01117
     C**                                                                  S01117
     C                     CALL 'ZSFILE'                                  S01117
     C                     PARM           SEQ     5                       S01117
     C                     PARM *BLANK    SENTY   3                       S01117
     C                     PARM           SFILEU                          S01117
     C                     PARM           ZSNUMU                          S01117
     C                     PARM           ZSERR   1                       S01117
     C*                                                                   S01117
     C           ZSERR     IFEQ 'Y'                                       S01117
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       S01117
     C                     SETON                     U7U8LR               S01117
     C                     RETRN                                          S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C                     ENDSR                                          S01117
     C*                                                                   S01117
     C********************************************************************S01117
     C/EJECT                                                              S01117
     C********************************************************************S01117
      **                                                                  S01117
      ** *PSSR --- SUBROUTINE TO HANDLE ABNORMAL ERROR CONDITIONS         S01117
      ** CALLED FROM: AFTER ABNORMAL OPERATION OCCURS                     S01117
      ** CALLS: NOTHING                                                   S01117
      **                                                                  S01117
     C********************************************************************S01117
     C*                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C*                                                                   S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C                     ENDSR                           ** *PSSR **    S01117
     C*                                                                   S01117
     C********************************************************************S01117
     C/COPY WNCPYSRC,FF1180C005
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
