     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FF Monthly Profit And Loss - Extract')
     F********************************************************************
     F*                                                                  *
     F*  Midas - Futures and Options Module
     F*                                                                  *
     F*   FF1240 - MONTHLY PROFIT AND LOSS - EXTRACT                     *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 Patch D ---------------------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 163444             Date 30Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CFF004             Date 02Oct96               *
     F*                  S01456             DATE 20FEB90                 *
     F*                  S01117             DATE 20FEB90                 *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*   163444 - Y2K Sort Order.                                       *
     F*   CFF004 - Increase in size of Price Fields,(Recompiled Only)    *
     F*   S01456 - RECOMPILED FOR SAR S01456                             *
     F*   S01117 - MULTIBRANCHING                                        *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*     CREATION REQUIREMENTS                                        *
     F*                                                                  *
     F*     IGNDECERR(*NO )                                              *
     F*                                                                  *
     F********************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FBOOKD   IF  E           K        DISK
     F*OSTNK1*IF* E           K        DISK                               S01117
     FPOSTNK5 IF  E           K        DISK                               S01117
     FFOPCK   IF  E           K        DISK                           UC  S01117
     FBOKPL   O   E                    DISK
     F*
     F/SPACE 2
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F********01*********READ POSTNK1*************************************S01117
     F*       01         READ POSTNK5                                     S01117
     F*       02         CHAIN BOOKD
     F*       03         CHAIN FOPCK                                      S01117
     F*
     F*     80-89       Reserved for Standard FF Subroutines
     F*
     F*     90-99       Reserved for Standard MIDAS Subroutines
     F*
     F*     U7+U8       Database Errors
     F*
     F/SPACE 2
     E                    CPY@    1   1 80
     I*
     ISDGELR    E DSSDGELRPD                                              S01117
      **  EXTERNAL DS FOR GENERAL LEDGER DETAILS                          S01117
      *                                                                   S01117
     I@DSFDY    E DSDSFDY                                                 S01117
      **  FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE              S01117
      *                                                                   S01117
     I*DA********UDS                            256                       S01117
     ILDA         DS                            256                       S01117
     I** LOCAL DATA AREA FOR PROGRAM NAME
     I***********                           134 177 DBLOT                 S01117
     I***********                           168 175 DBPGM                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     IPCKEY       DS                             10                       S01117
     I                                        1   3 KBRCA                 S01117
     I                                        4   5 KBOKC                 S01117
     I                                        6  10 KISTT                 S01117
     I*
      /EJECT
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *                                                                                       CGL029
     IDSSDY     E DSDSSDY                                                                     CGL029
      *                                                                                       CGL029
     C********************************************************************S01117
     C*                                                                  *S01117
     C**      K   E   Y        L   I   S   T   S                         *S01117
     C*                                                                  *S01117
     C********************************************************************S01117
     C*                                                                   S01117
     C**   LF/FOPCK                                                       S01117
     C*                                                                   S01117
     C           KFOPCK    KLIST                                          S01117
     C                     KFLD           KBRCA   3                       S01117
     C                     KFLD           KBOKC   2                       S01117
     C                     KFLD           KISTT   5                       S01117
     C*================================================================
     C*  P R O G R A M     S T A R T
     C*================================================================
     C*
      ** PERFORM SETUP OF STANDARD FIELDS
     C*
     C                     EXSR INIT
     C*
      ** MAIN PROCESS - PERFORM UNTIL CMD/3 OR ERROR
     C*
     C                     EXSR BOOKCD
     C*
      ** TERMINATE PROGRAM
     C*
     C                     SETON                         LR*
     C*
     C*================================================================
     C*  P R O G R A M     E N D
     C*================================================================
      /EJECT
      ********************************************************************
      ** INDEX OF SUBROUTINES
      **
      ** INIT              INITIALISATION OF DATA
      **
      ** BOOKCD            MAIN ROUTINE TO WRITE BOKPL RECORD
      **                                                                  S01117
      ** *PSSR             HANDLES ABNORMAL ERROR CONDITIONS              S01117
      **
      ********************************************************************
      /EJECT
      ********************************************************************
      **
      ** INIT S/R - TO INITIALISE STATIC FIELDS
      ** CALLED ONCE AT START OF PROGRAM FROM MAIN LINE
      ** CALLS NO OTHER ROUTINES
      **
      ********************************************************************
     C*
     C           INIT      BEGSR                           * INIT BEGSR *
     C*
      ***READ*THE*FIRST*RECORD*IN*FILE*POSTNK1****************************S01117
      ** READ THE FIRST RECORD IN FILE POSTNK5                            S01117
      ** AND PERFORM BOOKCD UNTIL EOF
     C*
     C***********          READ POSTNK1                  01               S01117
     C                     READ POSTNK5                  01               S01117
     C*
      ** PREPARE LDA
     C*
     C***********          MOVEL*BLANK    DBLOT                           S01117
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE *BLANK    DBFILE                          S01117
     C                     MOVE *BLANK    DBKEY                           S01117
     C                     MOVE *BLANK    DBPGM                           S01117
     C                     Z-ADD*ZERO     DBASE                           S01117
     C                     MOVEL'FF1240'  DBPGM
     C                     OUT  LDA                                       S01117
     C*                                                                   S01117
      **   ACCESS GENERAL LEDGER DETAILS                                  S01117
     C*                                                                   S01117
     C**********           CALL 'AOGELRR0'                                S01117              CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*MSG   ' @RTCD   7                       S01117
     C                     PARM '*FIRST ' @OPTN   7                       S01117
     C********** SDGELR    PARM SDGELR    @DSFDY                          S01117              CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
     C*                                                                   S01117
     C           @RTCD     IFNE *BLANK                                    S01117
     C                     SETON                     U7U8LR               S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVEL'SDGELRPD'DBFILE                          S01117
     C                     MOVEL@OPTN     DBKEY                           S01117
     C                     MOVEL@OPTN     DBOPTN  7         ***DBERR 01***S01117
     C                     Z-ADD1         DBASE                           S01117
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     RETRN                                          S01117
     C*                                                                   S01117
     C                     ELSE                                           S01117
     C*                                                                   S01117
     C**   OPEN LF/FOPCK IF PROFIT CENTRE USED INDICATOR = Y              S01117
     C*                                                                   S01117
     C           BKPRCU    IFEQ 'Y'                                       S01117
     C                     OPEN FOPCK                                     S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C                     END                                            S01117
     C*
     C           XTINIT    ENDSR                           ** XTINIT ESR**
     C*===================================================================
      /EJECT
      ********************************************************************
      **
      ** BOOKCD S/R TO PROCESS THE PROGRAM
      ** CALLED FROM MAIN PROCESSING. DOW EOF
      **
      ********************************************************************
     C*
     C           BOOKCD    BEGSR                           ** BOOKCD BSR**
     C*
      ** PROCESS IF NOT EOF WRITE BOKPL RECORD
     C*
     C           *IN01     DOWEQ'0'
     C*
     C           *IN01     DOWEQ'0'
     C           BOKC      ANDEQPBOKC
     C           BRCA      ANDEQPBRCA                                     S01117
     C           ISTT      ANDEQPISTT                                     S01117
     C*
     C                     MOVE ISCY      CCY
     C*
     C                     MOVE PBOKC     BOKC
     C                     MOVE PBOKD     BOKD
      *                                                                   163444
      * Setup Y2k Date Field                                              163444
      *                                                                   163444
     C           YRNO      IFLT 72                                        163444
     C           YRNO      ADD  2000      YRCC                            163444
     C                     ELSE                                           163444
     C           YRNO      ADD  1900      YRCC                            163444
     C                     ENDIF                                          163444
      *                                                                   163444
     C                     WRITEBOKPLF
     C*
     C           BOCC      IFNE *BLANKS
     C                     MOVE *BLANKS   BOKC
     C                     MOVE *BLANKS   BOKD
     C                     WRITEBOKPLF
     C                     END
     C*
     C***********          READ POSTNK1                  01               S01117
     C                     READ POSTNK5                  01               S01117
     C                     END
     C*                                                                   S01117
     C           *IN01     IFEQ '0'                                       S01117
     C*
      ** BOOK CODE CHANGES
     C*
     C           BOKC      IFNE PBOKC                                     S01117
     C                     MOVE BOKC      PBOKC   2
     C           BOKC      CHAINBOOKD                02
     C*                                                                   S01117
      ** DATABASE ERROR IF RECORD NOT FOUND                               S01117
     C*                                                                   S01117
     C           *IN02     IFEQ '1'                                       S01117
     C           RECI      ORNE 'D'                                       S01117
     C*                                                                   S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     Z-ADD2         DBASE                           S01117
     C                     MOVEL'BOOKD'   DBFILE           ***DBERR 02*** S01117
     C                     MOVELBOKC      DBKEY                           S01117
     C                     OUT  LDA                                       S01117
     C*                                                                   S01117
     C                     SETON                     U7U8LR               S01117
     C                     EXSR *PSSR                                     S01117
     C                     RETRN                                          S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C                     END                                            S01117
     C*                                                                   S01117
      ** BRANCH CODE OR BOOK CODE OR INSTRUMENT TYPE CHANGES,             S01117
      ** ACCESS PROFIT CENTRE IF PROFIT CENTRE USED INDICATOR = Y         S01117
     C*                                                                   S01117
     C                     MOVE BRCA      PBRCA   3                       S01117
     C                     MOVE ISTT      PISTT   5                       S01117
     C           BKPRCU    IFEQ 'Y'                                       S01117
     C                     MOVE BRCA      KBRCA   3                       S01117
     C                     MOVE BOKC      KBOKC   2                       S01117
     C                     MOVE ISTT      KISTT   5                       S01117
     C           KFOPCK    CHAINFOPCK                03                   S01117
     C*                                                                   S01117
      ** DATABASE ERROR IF RECORD NOT FOUND                               S01117
     C*                                                                   S01117
     C           *IN03     IFEQ '1'                                       S01117
     C           RECI      ORNE 'D'                                       S01117
     C*                                                                   S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     Z-ADD3         DBASE                           S01117
     C                     MOVEL'FOPCK'   DBFILE           ***DBERR 03*** S01117
     C                     MOVELPCKEY     DBKEY                           S01117
     C                     OUT  LDA                                       S01117
     C*                                                                   S01117
     C                     SETON                     U7U8LR               S01117
     C                     EXSR *PSSR                                     S01117
     C                     RETRN                                          S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C**   SET PROFIT CENTRE TO BLANK                                     S01117
     C**   IF PROFIT CENTRE USED INDICATOR NOT = Y                        S01117
     C*                                                                   S01117
     C                     ELSE                                           S01117
     C                     MOVE *BLANKS   PCBK                            S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C                     MOVE BOKD      PBOKD  30
     C*                                                                   S01117
     C                     END                                            S01117
     C*
     C                     END
     C*                                                                   S01117
     C**   CLOSE LF/FOPCK IF PROFIT CENTRE USED INDICATOR = Y             S01117
     C*                                                                   S01117
     C           BKPRCU    IFEQ 'Y'                                       S01117
     C                     CLOSEFOPCK                                     S01117
     C                     END                                            S01117
     C*
     C           XTBOOK    ENDSR                           ** XTBOOK ESR**
     C*
     C*===================================================================
      /EJECT                                                              S01117
     C********************************************************************S01117
      **                                                                  S01117
      ** *PSSR --- SUBROUTINE TO HANDLE ABNORMAL ERROR CONDITIONS         S01117
      ** CALLED FROM: AFTER ABNORMAL OPERATION OCCURS                     S01117
      ** CALLS: NOTHING                                                   S01117
      **                                                                  S01117
     C********************************************************************S01117
     C*                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C*                                                                   S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C                     ENDSR                           ** *PSSR **    S01117
     C*                                                                   S01117
     C********************************************************************S01117
**  CPY@
(c) Finastra International Limited 2001
