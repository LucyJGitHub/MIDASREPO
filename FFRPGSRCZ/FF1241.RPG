     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FF Monthly profit and loss - extract')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Futures and Options Module                           *
     F*                                                               *
     F*   FF1241 - MONTHLY PROFIT AND LOSS - EXTRACT                  *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
     F*   Created for S01456.                                         *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 Patch D ---------------------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *  Prev Amend No. 163444             Date 30Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CFF004             Date 02Oct96               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*   163444 - Y2K Sort Order.                                    *
     F*   CFF004 - Increase in size of Price Fields,(Recompiled Only) *
     F*   S01456 - RECOMPILED FO S01456                               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*     CREATION REQUIREMENTS                                     *
     F*                                                               *
     F*     IGNDECERR(*NO )                                           *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FBOOKD   IF  E           K        DISK
     FPOSTNK  IF  E           K        DISK
     FFOPCK   IF  E           K        DISK                           UC
     FBOKPL   O   E                    DISK
     F*
     F/SPACE 2
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*       01         READ POSTNK
     F*       02         CHAIN BOOKD
     F*       03         CHAIN FOPCK
     F*
     F*     80-89       Reserved for Standard FF Subroutines
     F*
     F*     90-99       Reserved for Standard MIDAS Subroutines
     F*
     F*     U7+U8       Database Errors
     F*
     F/SPACE 2
     E                    CPY@    1   1 80
     I*
     ISDGELR    E DSSDGELRPD
      **  EXTERNAL DS FOR GENERAL LEDGER DETAILS
      *
     I@DSFDY    E DSDSFDY
      **  FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
      *
     ILDA         DS                            256
     I** LOCAL DATA AREA FOR PROGRAM NAME
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     IPCKEY       DS                             10
     I                                        1   3 KBRCA
     I                                        4   5 KBOKC
     I                                        6  10 KISTT
     I*
      /EJECT
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *                                                                                       CGL029
     IDSSDY     E DSDSSDY                                                                     CGL029
      *                                                                                       CGL029
      ********************************************************************
      *                                                                  *
      **      K   E   Y        L   I   S   T   S                         *
      *                                                                  *
      ********************************************************************
      *
      **   LF/FOPCK
      *
     C           KFOPCK    KLIST
     C                     KFLD           KBRCA   3
     C                     KFLD           KBOKC   2
     C                     KFLD           KISTT   5
      *================================================================
      *  P R O G R A M     S T A R T
      *================================================================
      *
      ** PERFORM SETUP OF STANDARD FIELDS
      *
     C                     EXSR INIT
      *
      ** MAIN PROCESS - PERFORM UNTIL CMD/3 OR ERROR
      *
     C                     EXSR BOOKCD
      *
      ** TERMINATE PROGRAM
      *
     C                     SETON                         LR*
      *
      *===============================================================
      *  P R O G R A M     E N D
      *===============================================================
      /EJECT
      ****************************************************************
      ** INDEX OF SUBROUTINES
      **
      ** INIT              INITIALISATION OF DATA
      **
      ** BOOKCD            MAIN ROUTINE TO WRITE BOKPL RECORD
      **
      ** *PSSR             HANDLES ABNORMAL ERROR CONDITIONS
      **
      ****************************************************************
      /EJECT
      ****************************************************************
      **
      ** INIT S/R - TO INITIALISE STATIC FIELDS
      ** CALLED ONCE AT START OF PROGRAM FROM MAIN LINE
      ** CALLS NO OTHER ROUTINES
      **
      *******************************************************************
      *
     C           INIT      BEGSR                           * INIT BEGSR *
     C**
     C** INPUT PARAMETER :
     C           *ENTRY    PLIST
     C                     PARM           MKT     2
      *
      ** READ THE FIRST RECORD IN FILE POSTNK
      ** AND PERFORM BOOKCD UNTIL EOF
      *
     C                     READ POSTNK                   01
      *
      ** PREPARE LDA
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANK    DBFILE
     C                     MOVE *BLANK    DBKEY
     C                     MOVE *BLANK    DBPGM
     C                     Z-ADD*ZERO     DBASE
     C                     MOVEL'FF1241'  DBPGM
     C                     OUT  LDA
      *
      **   ACCESS GENERAL LEDGER DETAILS
      *
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C********** SDGELR    PARM SDGELR    @DSFDY                                              CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
     C           @RTCD     IFNE *BLANK
     C                     SETON                     U7U8LR
     C           *LOCK     IN   LDA
     C                     MOVEL'SDGELRPD'DBFILE
     C                     MOVEL@OPTN     DBKEY
     C                     MOVEL@OPTN     DBOPTN  7         ***DBERR 01**
     C                     Z-ADD1         DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     RETRN
      *
     C                     ELSE
      *
      **   OPEN LF/FOPCK IF PROFIT CENTRE USED INDICATOR = Y
      *
     C           BKPRCU    IFEQ 'Y'
     C                     OPEN FOPCK
     C                     END
      *
     C                     END
      *
     C           XTINIT    ENDSR                           ** XTINIT ESR*
      *==================================================================
      /EJECT
      *******************************************************************
      **
      ** BOOKCD S/R TO PROCESS THE PROGRAM
      ** CALLED FROM MAIN PROCESSING. DOW EOF
      **
      *******************************************************************
      *
     C           BOOKCD    BEGSR                           ** BOOKCD BSR*
      *
      ** PROCESS IF NOT EOF WRITE BOKPL RECORD
      *
     C           *IN01     DOWEQ'0'
      *
     C           *IN01     DOWEQ'0'
     C           BOKC      ANDEQPBOKC
     C           BRCA      ANDEQPBRCA
     C           ISTT      ANDEQPISTT
      *
     C                     MOVE ISCY      CCY
      *
     C                     MOVE PBOKC     BOKC
     C                     MOVE PBOKD     BOKD
      *                                                                   163444
      * Setup Y2k Date Field                                              163444
      *                                                                   163444
     C           YRNO      IFLT 72                                        163444
     C           YRNO      ADD  2000      YRCC                            163444
     C                     ELSE                                           163444
     C           YRNO      ADD  1900      YRCC                            163444
     C                     ENDIF                                          163444
      *                                                                   163444
     C                     WRITEBOKPLF
      *
     C           BOCC      IFNE *BLANKS
     C                     MOVE *BLANKS   BOKC
     C                     MOVE *BLANKS   BOKD
     C                     WRITEBOKPLF
     C                     END
      *
     C                     READ POSTNK                   01
     C                     END
      *
     C           *IN01     IFEQ '0'
      *
      ** BOOK CODE CHANGES
      *
     C           BOKC      IFNE PBOKC
     C                     MOVE BOKC      PBOKC   2
     C           BOKC      CHAINBOOKD                02
      *
      ** DATABASE ERROR IF RECORD NOT FOUND
      *
     C           *IN02     IFEQ '1'
     C           RECI      ORNE 'D'
      *
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE
     C                     MOVEL'BOOKD'   DBFILE           ***DBERR 02**
     C                     MOVELBOKC      DBKEY
     C                     OUT  LDA
      *
     C                     SETON                     U7U8LR
     C                     EXSR *PSSR
     C                     RETRN
     C                     END
      *
     C                     END
      *
      ** BRANCH CODE OR BOOK CODE OR INSTRUMENT TYPE CHANGES,
      ** ACCESS PROFIT CENTRE IF PROFIT CENTRE USED INDICATOR = Y
      *
     C                     MOVE BRCA      PBRCA   3
     C                     MOVE ISTT      PISTT   5
     C           BKPRCU    IFEQ 'Y'
     C                     MOVE BRCA      KBRCA   3
     C                     MOVE BOKC      KBOKC   2
     C                     MOVE ISTT      KISTT   5
     C           KFOPCK    CHAINFOPCK                03
      *
      ** DATABASE ERROR IF RECORD NOT FOUND
      *
     C           *IN03     IFEQ '1'
     C           RECI      ORNE 'D'
      *
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE
     C                     MOVEL'FOPCK'   DBFILE           ***DBERR 03**
     C                     MOVELPCKEY     DBKEY
     C                     OUT  LDA
      *
     C                     SETON                     U7U8LR
     C                     EXSR *PSSR
     C                     RETRN
     C                     END
      *
      **   SET PROFIT CENTRE TO BLANK
      **   IF PROFIT CENTRE USED INDICATOR NOT = Y
      *
     C                     ELSE
     C                     MOVE *BLANKS   PCBK
     C                     END
      *
     C                     MOVE BOKD      PBOKD  30
      *
     C                     END
      *
     C                     END
      *
      **   CLOSE LF/FOPCK IF PROFIT CENTRE USED INDICATOR = Y
      *
     C           BKPRCU    IFEQ 'Y'
     C                     CLOSEFOPCK
     C                     END
      *
     C           XTBOOK    ENDSR                           ** XTBOOK ESR*
      *
      *==================================================================
      /EJECT
      *******************************************************************
      **
      ** *PSSR --- SUBROUTINE TO HANDLE ABNORMAL ERROR CONDITIONS
      ** CALLED FROM: AFTER ABNORMAL OPERATION OCCURS
      ** CALLS: NOTHING
      **
      ********************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
     C                     ENDSR                           ** *PSSR **
      *
     C*******************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
