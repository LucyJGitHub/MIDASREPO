     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FF OTC pay/receive extract')
      ****************************************************************
      *                                                              *
      *  Midas - Dealing Module                                      *
      *                                                              *
      *  FF1705 - OTC Pay/Receive Extract                            *
      *                                                              *
      *  Function:  This program reads all OTC options. For those    *
      *             which have not yet produced any pay/receive      *
      *             reporting, the program FF1706 is called to write *
      *             the trade details (when required) to the OTC     *
      *             pay/receive extract file.                        *
      *                                                              *
      *  Called By: FFC1705 - OTC pay/receive reporting (COB).       *
      *                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CPK014             Date 04Oct01               *
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.01 Patch E ---------------------------------------*
      *                 170520             Date 19Nov99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                    CPK009             DATE 13Aug99           *
      *                    CFF003 *CREATE     DATE 14APR97           *
      *                                                              *
      ****************************************************************
      *                                                              *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CPK014 - Release 4 packaging.  Rename of timestamp field.    *
      *     170520  - Recompile over changed TRANSD file             *
      *     CPK009  - Recompile problems during DBA R3.00 packaging. *
      *     CFF003  - ENHANCED OTC PROCESSING - PHASE II             *
      *               UPGRADE FROM BLI LBL001                        *
      *                                                              *
      ****************************************************************
      * INDICATORS USED                                              *
      * ~~~~~~~~~~~~~~~                                              *
      * 87 - EOF for TRANSD                                          *
      * 88 - EOF for TRSET                                           *
      *                                                              *
      * U7 - Program Error                                           *
      * U8 - Database error                                          *
      * LR - Last record                                             *
      ****************************************************************
     F/EJECT
     FTRANSD  IF  E                    DISK
     F                                              KINFSR *PSSR
     FTRSET   IF  E           K        DISK
     F                                              KINFSR *PSSR
     FFF1705AUO   E             65     PRINTER
     F*
      /SPACE 3
     E* Array containing Copyright Statement
     E                    CPY@    1   1 80
     E*
      /SPACE 3
     I*
     I@TRANS    E DSTRANSD
     I              RECI                            RECIX
     I              TNBR                            TNBRX
     I* External DS for Transaction details
     I*
     I@TRSET    E DSTRSETD
     I              FRNT                            XXFRNT                                    CPK009
     I              AFRT                            XXAFRT                                    CPK009
     I              REPA                            XXREPA                                    CPK009
     I              TMST                            XXTMST                                    CPK014
     I* External DS for Transaction Settlement details
     I*
     ISDBANK    E DSSDBANKPD
     I* External DS for Bank details
     I*
     IDSFDY     E DSDSFDY
     I* First DS for access programs, short data structure
     I*
     ILDA       E DSLDA
     I* Data area giving installation control details
     I*
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     I/EJECT
     C****************************************************************
     C* MAIN PROCESSING                                              *
     C*                                                              *
     C* Calls     : INIT, PROCES, UPDAT                              *
     C****************************************************************
     C                     EXSR INIT
     C                     EXSR UPDAT
     C/EJECT
     C****************************************************************
     C* SR/INIT   : To perform all initialisation processing         *
     C*                                                              *
     C* Called by : Main Processing                                  *
     C* Calls     : *PSSR                                            *
     C****************************************************************
     C           INIT      BEGSR
     C*
     C** Set up copyright parameter
     C*
     C                     MOVEACPY@      ACT@   80
     C*
     C** Set up LDA
     C*
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'FF1705'  DBPGM
     C                     MOVE *BLANKS   DBASE
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           AGDFF     IFEQ 'M'
     C                     SETON                     98
     C                     END
     C*
     C** Call AOBANKR0 to access the bank's ICD
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG'    @RTCD   7
     C                     PARM '*FIRST'  @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C** Check for Data base error in file
     C*
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD01        DBASE            *************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 01*
     C                     MOVEL'*FIRST'  DBKEY            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C                     ENDSR
     C/EJECT
     C****************************************************************
     C* SR/UPDAT  : Updates pay/receive ind on OTC extract file by   *
     C*             calling FF1706 if required                       *
     C*                                                              *
     C* Called by : Main Processing                                  *
     C* Calls     : None                                             *
     C****************************************************************
     C           UPDAT     BEGSR
     C*
     C                     READ TRANSD                   87
     C*
     C** Do While not End of File
     C*
     C           *IN87     DOWEQ*OFF
     C*
     C*
     C** Check if record needs to be processed
     C*
     C           RECI      IFEQ 'D'
     C           TRTY      ANDNE'E'
     C           PYRC      ANDNE'Y'
     C           VALD      ANDGEBJRDNB
     C*
     C** Increment count of records read from TRANSD by one
     C*
     C           TOT       ADD  1         TOT     50
     C*
     C** Access transaction settlement details
     C*
     C           TNBR      CHAINTRSET                88
     C           *IN88     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD02        DBASE            *************
     C                     MOVEL'TRSET'   DBFILE           *DB ERROR 02*
     C                     MOVELTNBR      DBKEY            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C                     MOVE TNBR      TNBRX
     C*
     C** Call program FF1706
     C*
     C                     CALL 'FF1706'
     C                     PARM           @TRANS
     C                     PARM           @TRSET
     C                     PARM *BLANK    @RTCD
     C                     PARM *BLANK    @ERRF   8
     C                     PARM *BLANK    @ERRK  29
     C                     PARM 'C'       @MODE   1
     C*
     C** Check for Data base error in file
     C*
     C           @RTCD     IFEQ '*ERROR'
     C           *LOCK     IN   LDA
     C                     Z-ADD03        DBASE            *************
     C                     MOVEL@ERRF     DBFILE           *DB ERROR 03*
     C                     MOVEL@ERRK     DBKEY            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C** Check for return code of 'NO DEAL' or 'NO TRAD'
     C*
     C           @RTCD     IFEQ 'NO DEAL'
     C           @RTCD     OREQ 'NO TRAD'
     C                     WRITEFF1705AH
     C                     WRITEFF1705D2
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
     C*
     C** Increment the No. of records written by one, if return code
     C** equals 'WRITE'
     C*
     C           @RTCD     IFEQ 'WRITE'
     C           TOTW      ADD  1         TOTW    50
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     C                     READ TRANSD                   87
     C*
     C                     ENDDO
     C*
     C** Write the audit report
     C*
     C                     WRITEFF1705AH
     C                     WRITEFF1705D1
     C*
     C                     MOVE *ON       *INLR
     C                     RETRN
     C*
     C                     ENDSR
     C/EJECT
     C****************************************************************
     C* SR/*PSSR  : Program exception error routine                  *
     C*                                                              *
     C* Called by : INIT                                             *
     C* Calls     : None                                             *
     C****************************************************************
     C           *PSSR     BEGSR
     C*
     C** Write error details to error report
     C*
     C           @RUN      IFEQ *BLANK
     C                     MOVE *BLANK    @RUN    1
     C                     WRITEFF1705AH
     C                     WRITEFF1705AE
     C                     DUMP
     C*
     C** End program
     C*
     C                     SETON                     U7U8LR
     C                     ENDIF
     C                     RETRN
     C*
     C                     ENDSR
      *
** CPY@
(c) Misys International Banking Systems Ltd. 2001
