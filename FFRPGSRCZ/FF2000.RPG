     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FF Check if EOM and update market contr.')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Financial Futures and Options.                   *
     F*                                                               *
     F*  FF2000 - Check if EOM and update MKCTL file if YES.          *
     F*                                                               *
     F*  Function:  This program checks to see if EOM if so will      *
     F*             update  file MKCTL.                               *
     F*                                                               *
     F*  Called By: FFC1013 - Add Position changes to Customer/       *
     F*                       Broker Positions (EOC)                  *
     F*                                                               *
     F*  Also called by: FFC0820 - Add Position changes details for   *
     F*                            Customer/Broker Positions -        *
     F*                            (EOC IN COB)                       *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO. 069250        DATE 03MAY94                    *
     F*  PREV AMEND NO. 069255        DATE 11APR94                    *
     F*                 S01456        DATE 03DEC93                    *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  069250 - Remove dummy OVRDBF command from creation parms.    *
     F*  069255 - Define LDA and RUNDAT internally                    *
     F*  S01456 - Monthly P&L Enhancements.                           *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
/******:*OVRDBF*(File*in*program)*(file*on*system)******************:**   069250
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FMKCTL   UF  E           K        DISK
     F/EJECT
     E/COPY ZSRSRC,ZDATE2Z1
     E/EJECT
     E                    CPY@    1   1 80
     E*
     E** Array containing Copyright statement
     E*
      ***************** FIRST COMPILE TIME ARRAY *********************
      /SPACE 3
      *
     I***LDA*******E DSLDA                         256                    069255
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     ILDA         DS                            256                       069255
     I                                      134 141 DBFILE                069255
     I                                      142 170 DBKEY                 069255
     I                                      171 180 DBPGM                 069255
     I                                      181 1830DBASE                 069255
     I***RUNDAT****E DSRUNDAT                                             069255
     IRUNDAT      DS                                                      069255
     I                                        1   7 AGMRDT                069255
     I                                    P   8  100AGRDNB                069255
     I                                       11  11 AGSUC                 069255
     I                                       12  12 AGDFF                 069255
     I                                       13  13 AGMBIN                069255
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
     I                                      244 253 JOB
     I                                      244 246 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      BIS@   80
      *
      ** Read in Installation Data
      *
     C********** *NAMVAR   DEFN           RUNDAT                          069255
     C********** *NAMVAR   DEFN           LDA                             069255
     C**********           IN   RUNDAT                                    069255
      *
     C           *ENTRY    PLIST
     C                     PARM           MKT     2
      *
      ** TAKE MARKET CONTROL INFORMATIONS
      *
     C           MKT       CHAINMKCTL                50
      *
      *
      ** If record not found show that a database error has occurred.
      *
     C           *IN50     IFEQ '1'
     C********** *LOCK     IN   LDA                                       069255
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'MKCTLD'  DBFILE           *DB ERROR 001 *
     C                     MOVEL'MKT  '   DBKEY            ***************
     C**********           OUT  LDA                                       069255
     C                     EXSR *PSSR
     C                     END
      *
      ** CHECK SYSTEM DATE FORMAT, DDMMYY OR MMDDYY.
      *
     C           AGDFF     COMP 'M'                      98MMDDYY, 98 ON
      *
      **   CHANGE FORMAT OF BUSINESS DATE TO DD/MM/YY FORMAT
      *
     C                     Z-ADDBUSD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZMTH      MONTH   20
      *
      **   CHANGE FORMAT OF NEXT BUSINESS DATE TO DD/MM/YY FORMAT
      *
     C                     SETOF                     55
     C                     Z-ADDNXBDT     ZDAYNO
     C                     EXSR ZDATE2
     C           MONTH     COMP ZMTH                     55
     C           *IN55     IFNE '1'
     C                     MOVE 'Y'       EOMCHK
     C                     UPDATMKCTLDF
     C                     END
     C                     SETON                     LR
     C                     RETRN
     C/EJECT
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
     C********************************************************************
     C/COPY ZSRSRC,ZDATE2Z4
     C********************************************************************
**   ZYDY - YEARS IN DAYS CUMULATIVE IN FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
