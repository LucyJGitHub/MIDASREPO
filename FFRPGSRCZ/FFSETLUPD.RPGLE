     H DEBUG
     H COPYRIGHT('(c) Finastra International Limited 2002')
      *****************************************************************
/*STD *  RPGBASEMOD                                                   *
/*EXI *  TEXT('Midas FF Settlements database updater')                *
      *****************************************************************
      *                                                               *
      *  Midas - Financial Futures and Options module                 *
      *                                                               *
      *  FFSETLUPD - Settlements database update component            *
      *                                                               *
      *  Function: This module updates the back office settlements    *
      *            file.                                              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CAP208             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 256564             Date 17Sep08               *
      *                 247471             Date 01May07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 220744             Date 26May06               *
      *                 CSD027             Date 09Dec05               *
      *                 BG9042             Date 26Oct05               *
      *                 CGL029             Date 01Sep03               *
      *                 CGP001             Date 14Oct03               *
      *                 215528             Date 21Mar03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP166             Date 15Oct01               *
      *                 CSC011             Date 18Sep01               *
      *                 CAS002             Date 14Jan02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 190828             Date 09Jul01               *
      *                 CFF007             Date 13Feb01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CFF006             Date 28Nov00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP013             Date 07Sep99               *
      * Midas DBA 3.01 Patch E ---------------------------------------*
      *                 170520             Date 19Nov99               *
      * Midas DBA 3.00 -----------------Base--------------------------*
      *                 147775             Date 16Nov98               *
      *                 CAP004  *CREATE    Date 01Jun98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP208 - F&O Market Instrument API (Recompile)               *
      *  256564 - Recompile due to PF changes done by fix 256330      *
      *  247471 - Calculate Premium Amount based on Premium currency. *
      *  220744 - Premium from first transaction is being applied to  *
      *           the second transaction when input is done consecu-  *
      *           tively without exiting the menu.  Clear the work    *
      *           variable FFPREM after moving value to actual field  *
      *           PRMP in PF/TRSETD during inserts and amends.        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BG9042 - FFC0760 failed due to attempt to write duplicate    *
      *           record in FF0355. Applied 219148 & 221374.          *
      *           Delete the close-out transaction generated by the   *
      *           system (negative transaction number) if it exists,  *
      *           when a transaction is deleted.                      *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGP001 - Global processing.                                  *
      *  215528 - System is associating a wrong premium amount.       *
      *  CAS002 - Hedge Strategy/Linkage (Recompile)                  *
      *  CAP166 - API for Non-Currency OTC                            *
      *  CSC011 - 24x7 Midas Availability                             *
      *  190828 - No commision account keys were produced due to      *
      *           broker commision pattern not updated accordingly.   *
      *           Fix is to default 'A' as broker commision pattern   *
      *           if commision codes were entered.                    *
      *  CFF007 - Futures and Options Enhancement for Private Banking *
      *  CFF006 - Fractional Ticks Enhancement.  (Recompiled)         *
      *  CAP013 - Allow access by Midas transaction ID if not insert  *
      *  170520 - Recompile over changed TRANSD file                  *
      *  147775 - General FF API fixes - (Pre-release unnanotated)    *
      *  CAP004 - Conversion of Midas inputs to APIs.                 *
      *                                                               *
      *****************************************************************
 
      ** +--------------------------------------+
      ** ¦ F-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
 
      ** Back-office settlement details
     FTRSET     UF A E           K DISK    COMMIT INFSR(*pssr) USROPN
 
      ** Customer/broker instrument defaults
     FDEFLT     IF   E           K DISK
 
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ Automatically included D-specs       ¦
      ** ¦ ==============================       ¦
      ** +--------------------------------------+
      **
      **--------------------------------------------------------------------------------------------
      ** The following /COPY line includes (among others) the LDA layout
      ** and the copyright array definition:
     D/COPY ZACPYSRC,STD_D_SPEC
      **--------------------------------------------------------------------------------------------
 
      **--------------------------------------------------------------------------------------------
      ** The following /COPY includes the MM standard declares:
     D/COPY ZACPYSRC,STDDECLARE
      **--------------------------------------------------------------------------------------------
 
      **--------------------------------------------------------------------------------------------
      ** The following /COPY line includes all the defined fields in the
      ** Program Status Data Structures.  They have meaningful
      ** names, prefixed by 'PS'.
     D/COPY ZACPYSRC,PSDS
      **--------------------------------------------------------------------------------------------
 
      **--------------------------------------------------------------------------------------------
      ** The following /COPY line includes all the definitions of the error
      ** arrays, including the named constant giving the size of
      ** the arrays.  Note that it uses #MsgID and *MsgData, and so require
      ** STD_D_SPEC.
     D/COPY ZACPYSRC,FVAL_ARRAY
      **--------------------------------------------------------------------------------------------
 
      ** +--------------------------------------+
      ** ¦ End of automatically included D-specs¦
      ** ¦ =====================================¦
      ** +--------------------------------------+
 
      *****************************************************************
      /EJECT
      *****************************************************************
 
      ** +--------------------------------------+
      ** ¦ Manually included D-specs            ¦
      ** ¦ =========================            ¦
      ** +--------------------------------------+
 
      ** +--------------------------------------+
      ** ¦ Named constants                      ¦
      ** ¦ ===============                      ¦
      ** +--------------------------------------+
 
      ** +--------------------------------------+
      ** ¦ Arrays and Data Structures           ¦
      ** ¦ ==========================           ¦
      ** +--------------------------------------+
 
      ** +--- Named indicators -------------------------------------------
      ** ¦
      ** ¦ Map variable names to indicators to allow use of names instead
      ** ¦ of numbers; base the following data structure on a pointer to
      ** ¦ the indicator array.
     D Indicators      DS                  BASED(pIndicator)
      ** ¦
     D  NoRecFound            99     99
      ** ¦
      ** ¦
      ** ¦ Set the indicator array pointer
     D pIndicator      S               *   INZ(%Addr(*In))
      ** ¦
      ** +----------------------------------------------------------------
 
     D TRSET_DS      E DS                  EXTNAME(TRSETD)
      ** External data structure based on the settlements file to allow
      ** record copying.
 
     D FFVSETL       E DS                  EXTNAME(FFVSETLPD) PREFIX(NS)
      ** Externally described data structure for valid settlement
      ** instructions.
 
     D FFVTRAN       E DS                  EXTNAME(FFVTRANPD) PREFIX(TR)
      ** Externally described data structure for valid exchange-traded
      ** transactions.
 
     D InstType      E DS                  EXTNAME(INTYPD) PREFIX(IT)
      ** Data structure for instrument details
                                                                                              CSC011
      ** Header information to be passed to APLOGTRAN                                         CSC011
     D PHead         E DS                  EXTNAME(APHEADPD)                                  CSC011
                                                                                              CSC011
      ** Externally described DS for SAR details                                              CSC011
     D SCSARD        E DS                  EXTNAME(SCSARDPD)                                  CSC011
     D  SCA_ZONE     E                     EXTFLD(ZONE)                                       CGP001
                                                                                              CSC011
      ** DS for access programs - short data structure                                        CSC011
     D DSFDY         E DS                  EXTNAME(DSFDY)                                     CSC011
                                                                                              CSC011
      ** Data structure for data area SC24X7                                                  CSC011
     D SC24X7        E DS                  EXTNAME(SC24X7) DTAARA(SC24X7)                     CSC011
                                                                                              CSC011
      ** Data structure for data area SDSTAT                                                  CSC011
     D SDSTAT        E DS                  EXTNAME(SDSTAT) DTAARA(SDSTAT)                     CSC011
 
 
      ** +--------------------------------------+
      ** ¦ Declared variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+
 
      ** Multibranching indicator
     D BGMBIN          S              1A
 
 
      ** --- Start of key fields for DEFLT ---------------------------------------------------------
 
      ** Customer/Broker code
     D*KCBCD****       S              6S 0                                                    CSD027
     D KCBCD           S              6A                                                      CSD027
 
      ** Market
     D KMRKT           S              2A
 
      ** Instrument code
     D KISTC           S              3A
 
      ** --- End of key fields for DEFLT -----------------------------------------------------------
 
      ** Broker/Customer charge pattern
     D XBCPT           S              1A
     D XCCPT           S              1A
 
      ** Premium (returned from FFPMCL)
     D FFPREM          S             13P 0
 
      ** Price
     D FFPRIC          S                   LIKE(TRSTRP)
                                                                                              215528
      ** Strike Price                                                                         215528
     D FFSTRP          S                   LIKE(TRSTRP)                                       215528
 
      ** CFF007 enhancement                                                                   CFF007
     D CFF007          S              1                                                       CFF007
      ** Private Banking customer indicator                                                   CFF007
     D PPBBFLG         S              1                                                       CFF007
     D PPBCFLG         S              1                                                       CFF007
                                                                                              CSC011
      ** Define variable for switchable CSC011.                                               CSC011
     D CSC011          S              1A   INZ(*BLANKS)                                       CSC011
                                                                                              CSC011
      ** Define parameter to be passed to APLOGTRAN.                                          CSC011
     D TRANSDTL        S           5800A   INZ(*BLANKS)                                       CSC011
                                                                                              CSC011
      ** Define Timestamp variable                                                            CSC011
     D PTime           S             26A                                                      CSC011
                                                                                              CSC011
      ** Transaction number to be pass to APLOGTRAN.                                          CSC011
     D***PDealNo         S             18A   INZ(*BLANKS)                              CSC011 CGL029
     D PDealNo         S             24A   INZ(*BLANKS)                                       CGL029
                                                                                              CSC011
      ** Associated transaction number                                                        CSC011
     D***PADealNo        S             18A   INZ(*BLANKS)                              CSC011 CGL029
     D PADealNo        S             24A   INZ(*BLANKS)                                       CGL029
                                                                                              CSC011
      ** Define parameters for AOSARDR0                                                       CSC011
     D PRTCD           S              7A                                                      CSC011
     D POPTN           S              7A                                                      CSC011
     D PSARD           S              6A                                                      CSC011
                                                                                              CSC011
      ** Work field for alpha transaction number                                              CSC011
     D WDealNo         S              6A                                                      CSC011
 
      ** +--------------------------------------+
      ** ¦ End of D-specs                       ¦
      ** ¦ ==============                       ¦
      ** +--------------------------------------+
      *****************************************************************
      /EJECT
      *****************************************************************
 
     C     Start         TAG
      ** Note: the above tag is only there to force the first comments in
      ** the C-specs to appear after the D- or I-specs in compiled listing
 
      ** +--- Start of main processing -----------------------------------
      ** ¦
      ** ¦ Initial processing is performed automatically: the *inzsr is
      ** ¦ executed at program activation.
      ** ¦
      ** +----------------------------------------------------------------
 
      ** Perform one subroutine for inserts, another for amends
      ** and deletes.
     C                   SELECT
 
     C                   WHEN      TRCHTP = 'I'
     C                   EXSR      WTRSET
 
     C                   OTHER
     C                   EXSR      UTRSET
                                                                                              BG9042
     C     TRCHTP        IFEQ      'D'                                                        BG9042
     C                   Z-SUB     TRTNBR        TRTNBR                                       BG9042
     C                   MOVE      'Y'           CLOSOUT           1                          BG9042
     C                   EXSR      UTRSET                                                     BG9042
     C                   Z-SUB     TRTNBR        TRTNBR                                       BG9042
     C                   MOVE      *BLANK        CLOSOUT                                      BG9042
     C                   ENDIF                                                                BG9042
 
     C                   ENDSL
                                                                                              CSC011
      ** If CSC011 is on and current system is in support system, convert                     CSC011
      ** trade details and settlement details in file format to screen                        CSC011
      ** format to be written to API log file.                                                CSC011
                                                                                              CSC011
     C                   IF        (CSC011 = 'Y') AND (S1SUPP = LIBR)                         CSC011
                                                                                              CSC011
     C                   CLEAR                   ReturnCode                                   CSC011
     C                   EXSR      SRCONVERT                                                  CSC011
                                                                                              CSC011
      ** If there are no errors in conversion, proceed with writing to                        CSC011
      ** AP standard log file.                                                                CSC011
     C                   IF        ReturnCode = *BLANKS                                       CSC011
     C                   EXSR      SRAPILOG                                                   CSC011
     C                   ENDIF                                                                CSC011
                                                                                              CSC011
     C                   ENDIF                                                                CSC011
 
 
 
     C                   RETURN
 
      ********************************************************************
      /EJECT
      ********************************************************************
      **                                                                 *
      **   WTRSET SR. -  Write record to  LF/TRSET                       *
      **   ~~~~~~~~~                                                     *
      ********************************************************************
 
     C     WTRSET        BEGSR
 
      ** Start by copying the input settlement details to the file format
     C                   EVAL      TRSET_DS = FFVSETL
 
     C                   MOVE      'D'           RECI
 
     C                   MOVE      'N'           CMCB                                         CFF007
     C                   MOVE      'N'           CHCB                                         CFF007
     C                   MOVE      'N'           CMCC                                         CFF007
     C                   MOVE      'N'           CHCC                                         CFF007
                                                                                              CFF007
     C                   Z-ADD     0             OCSB
     C                   Z-ADD     0             OCDB
     C                   Z-ADD     0             CCSB
     C                   Z-ADD     0             CCDB
     C                   Z-ADD     0             CPSB
     C                   Z-ADD     0             CPDB
     C                   Z-ADD     0             OCSC
     C                   Z-ADD     0             OCDC
     C                   Z-ADD     0             CCSC
     C                   Z-ADD     0             CCDC
     C                   Z-ADD     0             CPSC
     C                   Z-ADD     0             CPDC
     C                   Z-ADD     0             NCCP
 
      **   Premiums Paid (- Options only)
     C     ITISPT        IFEQ      1
     C     ITISPT        OREQ      2
     C     ITISPT        OREQ      3
     C     ITISPT        OREQ      7
     C                   Z-ADD     0             PRMP
     C                   ELSE
 
      **   Use the strike price in the Premium calculation.
      **   for processing type 8
 
     C     ITISPT        IFEQ      8
     C                   Z-ADD     TRSTRP        FFPRIC
 
      **   for other types of options use the contract price
 
     C                   ELSE
     C                   Z-ADD     TRCOPR        FFPRIC
     C                   END
 
     C                   CALLB     'FFPMCL'
      ** Outputs
      ** -------
      **                              Premium (13,0P)
     C                   PARM                    FFPREM
      ** Inputs
      ** ------
      **                              Number of contracts (5,0P)
     C                   PARM                    TRNUCO
      **                              Price (15,8P)
     C                   PARM                    FFPRIC
      **                              Ticks denominator (3,0P)
     C                   PARM                    ITTKDM
      **                              Minimum price fluctuation (15,8P)
     C                   PARM                    ITMNPF
      **                              Tick Value (14,5P)
     C                   PARM                    ITTKVL
      **                              Instrument type (1,0S)
     C                   PARM                    ITISPT
      **                              Contract type (1,0S, from INTYPD)
     C                   PARM                    ITCNTT
      **                              Contingent amount (13,0P), from INTYPD)
     C                   PARM                    ITCTAM
      **                              Strike Price (15,8P)                                    215528
     C                   PARM      TRSTRP        FFSTRP                                       215528
 
 
      *                                                                                       247471
     C     ITISPT        IFEQ      5                                                          247471
     C     ITISCY        IFEQ      ITCTCY                                                     247471
     C     FFPREM        MULT      TRSTRP        FFPREM                                       247471
     C                   END                                                                  247471
     C     ITISCY        IFNE      ITCTCY                                                     247471
     C     ITISCY        ANDNE     ITOTHC                                                     247471
     C                   Z-ADD     0             FFPREM                                       247471
     C                   END                                                                  247471
     C                   END                                                                  247471
      *                                                                                       247471
 
 
      **   for processing type 8, multiply the amount calculated
      **   by the contract price.
 
     C     ITISPT        IFEQ      8
     C                   MULT      TRCOPR        FFPREM
     C                   END
 
     C                   Z-ADD     FFPREM        PRMP
     C                   Z-ADD     *ZERO         FFPREM                                       220744
     C                   END
 
      **   Broker  Settlement Instructions
 
     C**********         IF        TRBOCO <> 0                                                CSD027
     C                   IF        TRBOCO <> *BLANKS                                          CSD027
 
      **   - Determine Commission Pattern
     C                   EXSR      PBCPT
 
     C                   ELSE
     C                   MOVE      ' '           BCPT
     C                   Z-ADD     0             BCMC
     C                   Z-ADD     0             BCMS
     C                   Z-ADD     0             BCMA
     C                   Z-ADD     0             BCHC
     C                   Z-ADD     0             BCHA
     C                   Z-ADD     0             BMAR
     C                   Z-ADD     0             BSLT
     C                   MOVE      *BLANK        BSLA
     C                   MOVE      *BLANK        BFAO
     C                   MOVE      *BLANK        BCPN
     C                   MOVE      *BLANK        BSBR
     C                   MOVE      *BLANK        BBID
 
 
     C                   END
 
     C                   Z-ADD     0             BCHP
     C                   Z-ADD     0             BCMP
 
      **   Customer  Settlement Instructions
 
     C*****TRCUSC        IFNE      0                                                          CSD027
     C     TRCUSC        IFNE      *BLANKS                                                    CSD027
 
      **   - Determine Commission Pattern
     C                   EXSR      PCCPT
 
     C                   ELSE
     C                   MOVE      ' '           CCPT
     C                   Z-ADD     0             CCMC
     C                   Z-ADD     0             CCMS
     C                   Z-ADD     0             CCMA
     C                   Z-ADD     0             CCHC
     C                   Z-ADD     0             CCHA
     C                   Z-ADD     0             CMAR
     C                   Z-ADD     0             CSLT
     C                   MOVE      *BLANK        CSLA
     C                   MOVE      *BLANK        CFAO
     C                   MOVE      *BLANK        CCPN
     C                   MOVE      *BLANK        BRSC
     C                   MOVE      *BLANK        CBID
     C                   END
 
     C                   Z-ADD     0             CCHP
     C                   Z-ADD     0             CCMP
 
     C     CFF007        IFEQ      'Y'                                                        CFF007
                                                                                              CFF007
     C     PPBBFLG       IFEQ      'Y'                                                        CFF007
     C     BCMC          IFNE      *ZERO                                                      CFF007
     C     BCMA          ANDNE     *ZERO                                                      CFF007
     C                   MOVE      'Y'           CMCB                                         CFF007
     C                   ELSE                                                                 CFF007
     C                   MOVE      'N'           CMCB                                         CFF007
     C                   ENDIF                                                                CFF007
                                                                                              CFF007
     C     BCHC          IFNE      *ZERO                                                      CFF007
     C     BCHA          ANDNE     *ZERO                                                      CFF007
     C                   MOVE      'Y'           CHCB                                         CFF007
     C                   ELSE                                                                 CFF007
     C                   MOVE      'N'           CHCB                                         CFF007
     C                   ENDIF                                                                CFF007
     C                   ENDIF                                                                CFF007
                                                                                              CFF007
     C     PPBCFLG       IFEQ      'Y'                                                        CFF007
     C     CCMC          IFNE      *ZERO                                                      CFF007
     C     CCMA          ANDNE     *ZERO                                                      CFF007
     C                   MOVE      'Y'           CMCC                                         CFF007
     C                   ELSE                                                                 CFF007
     C                   MOVE      'N'           CMCC                                         CFF007
     C                   ENDIF                                                                CFF007
                                                                                              CFF007
     C     CCHC          IFNE      *ZERO                                                      CFF007
     C     CCHA          ANDNE     *ZERO                                                      CFF007
     C                   MOVE      'Y'           CHCC                                         CFF007
     C                   ELSE                                                                 CFF007
     C                   MOVE      'N'           CHCC                                         CFF007
     C                   ENDIF                                                                CFF007
     C                   ENDIF                                                                CFF007
                                                                                              CFF007
     C                   ENDIF                                                                CFF007
                                                                                              CFF007
                                                                                              CSC011
      ** If CSC011 is installed, supply a Front Office ID for the                             CSC011
      ** transaction even if it originated from SIN module.                                   CSC011
                                                                                              CSC011
     C                   IF        CSC011 = 'Y'                                               CSC011
     C                   IF        FRNT = *BLANKS                                             CSC011
     C                   MOVE      TNBR          WDealNo                                      CSC011
     C                   EVAL      FRNT = 'MD' + WDealNo                                      CSC011
     C                   ENDIF                                                                CSC011
     C                   ENDIF                                                                CSC011
                                                                                              CSC011
      **   Write new record
     C                   OPEN      TRSET
     C                   WRITE     TRSETDF
     C                   CLOSE     TRSET
 
     C                   ENDSR
 
      ********************************************************************
      /EJECT
      ********************************************************************
      **                                                                 *
      **   PCCPT SR. - Determine  Customer Commission Pattern            *
      **   ~~~~~~~~~                                                     *
      ********************************************************************
 
     C     PCCPT         BEGSR
      **
      ** Check if customer instrument defaults exist.
      **
     C                   MOVEL     TRCUSC        KCBCD
     C                   MOVEL     TRISTT        KMRKT
     C                   MOVE      TRISTT        KISTC
 
     C     KDEFLT        CHAIN     DEFLT                              71
     C  N71RECI          COMP      'D'                                7171
 
     C     *IN71         IFEQ      '1'
      *
      ** If they don't, use the customer commission pattern from INTYPD
      *
     C                   MOVE      ITCCPT        CCPT
     C                   ELSE
      *
      ** If they do, use the commission pattern if it is present,
      ** else (if not) use the customer commission pattern from INTYPD
      *
     C     CPAT          IFNE      ' '
     C                   MOVE      CPAT          CCPT
     C                   ELSE
     C                   MOVE      ITCCPT        CCPT
     C                   ENDIF
     C                   ENDIF
 
      **   If a commission amount entered, or commission amount = 'A'
 
     C                   IF        CCPT = 'A' OR CCMA <> 0
 
     C                   MOVE      'A'           CCPT
 
     C                   ELSE
 
      **   If commission codes not entered
 
     C     CCMC          IFEQ      0
     C     CCMS          ANDEQ     0
     C                   MOVE      ' '           CCPT
     C                   ELSE                                                                 190828
     C     CCPT          IFEQ      *BLANK                                                     190828
     C                   MOVE      'A'           CCPT                                         190828
     C                   END                                                                  190828
     C                   END
     C                   END
 
     C                   ENDSR
 
      ********************************************************************
      /EJECT
      ********************************************************************
      **                                                                 *
      **   PBCPT SR. - Determine  Broker Commission Pattern              *
      **   ~~~~~~~~~                                                     *
      ********************************************************************
 
     C     PBCPT         BEGSR
      **
      ** Check if broker instrument defaults exist.
      **
     C                   MOVEL     TRBOCO        KCBCD
     C                   MOVEL     TRISTT        KMRKT
     C                   MOVE      TRISTT        KISTC
 
     C     KDEFLT        CHAIN     DEFLT                              71
     C  N71RECI          COMP      'D'                                7171
 
     C     *IN71         IFEQ      '1'
      *
      ** If they don't, use the broker commission pattern from INTYPD
      *
     C                   MOVE      ITBCPT        BCPT
     C                   ELSE
      *
      ** If they do, use the commission pattern if it is present,
      ** else (if not) use the broker commission pattern from INTYPD
      *
     C     CPAT          IFNE      ' '
     C                   MOVE      CPAT          BCPT
     C                   ELSE
     C                   MOVE      ITBCPT        BCPT
     C                   ENDIF
     C                   ENDIF
 
      **   If a commission amount entered, or commission amount = 'A'
 
     C                   IF        BCPT = 'A' OR BCMA <> 0
 
     C                   MOVE      'A'           BCPT
 
     C                   ELSE
 
      **   If commission codes not entered
 
     C     BCMC          IFEQ      0
     C     BCMS          ANDEQ     0
     C                   MOVE      ' '           BCPT
     C                   ELSE                                                                 190828
     C     BCPT          IFEQ      *BLANK                                                     190828
     C                   MOVE      'A'           BCPT                                         190828
     C                   END                                                                  190828
     C                   END
     C                   END
 
     C                   ENDSR
 
      ********************************************************************
      /EJECT
      ********************************************************************
      **                                                                 *
      **   UTRSET SR. -  Update  LF/TRSET main                           *
      **   ~~~~~~~~~~                                                    *
      **                                                                 *
      ********************************************************************
 
     C     UTRSET        BEGSR
 
      **   Access LF/TRSET main
     C                   OPEN      TRSET
     C     TRTNBR        CHAIN     TRSET                              71
 
      **   - not found
     C     *IN71         IFEQ      '1'
     C     CLOSOUT       ANDNE     'Y'                                                        BG9042
     C                   EXSR      ErrorExit
     C                   END
      *                                                                                       BG9042
     C     *IN71         IFEQ      '1'                                                        BG9042
     C     CLOSOUT       ANDEQ     'Y'                                                        BG9042
     C                   CLOSE     TRSET                                                      BG9042
     C                   GOTO      ExitOnly                                                   BG9042
     C                   END                                                                  BG9042
 
      **   Delete
     C     TRCHTP        IFEQ      'D'
     C                   MOVE      '*'           RECI
                                                                                CAP013
      ** Front office transaction ID                                            CAP013
                                                                                CAP013
     C                   MOVEL     TRFRNT        FRNT                           CAP013
 
     C                   UPDATE    TRSETDF
     C                   CLOSE     TRSET
 
     C                   ELSE
 
      **   Amend
 
 
      **   - Margins (always amendable)
     C                   Z-ADD     NSBMAR        BMAR
     C                   Z-ADD     NSCMAR        CMAR
 
      **   - EOC processed ind = 'N'
 
     C                   IF        TRECPI = 'N'
 
      **   - No. contracts = No. open contracts
     C**********         IF        (TRNUCO = TRNOCO AND TRBOCO = 0)                           CSD027
     C                   IF        (TRNUCO = TRNOCO AND TRBOCO = *BLANKS)                     CSD027
 
      **     - Premiums Paid (- Options only)
     C     ITISPT        IFEQ      1
     C     ITISPT        OREQ      2
     C     ITISPT        OREQ      3
     C     ITISPT        OREQ      7
     C                   Z-ADD     0             PRMP
     C                   ELSE
 
      **   Use the strike price in the Premium calculation.
      **   for processing type 8
 
     C     ITISPT        IFEQ      8
     C                   Z-ADD     TRSTRP        FFPRIC
 
      **   for other types of options use the contract price
 
     C                   ELSE
     C                   Z-ADD     TRCOPR        FFPRIC
     C                   END
 
     C                   CALLB     'FFPMCL'
      ** Outputs
      ** -------
      **                              Premium (13,0P)
     C                   PARM                    FFPREM
      ** Inputs
      ** ------
      **                              Number of contracts (5,0P)
     C                   PARM                    TRNUCO
      **                              Price (15,8P)
     C                   PARM                    FFPRIC
      **                              Ticks denominator (3,0P)
     C                   PARM                    ITTKDM
      **                              Minimum price fluctuation (15,8P)
     C                   PARM                    ITMNPF
      **                              Tick Value (14,5P)
     C                   PARM                    ITTKVL
      **                              Instrument type (1,0P)
     C                   PARM                    ITISPT
      **                              Contract type (1,0P, from INTYPD)
     C                   PARM                    ITCNTT
      **                              Contingent amount (13,0S), from INTYPD)
     C                   PARM                    ITCTAM
      **                              Strike Price (15,8P)                                    215528
     C                   PARM      TRSTRP        FFSTRP                                       215528
 
      *                                                                                       247471
     C     ITISPT        IFEQ      5                                                          247471
     C     ITISCY        IFEQ      ITCTCY                                                     247471
     C     FFPREM        MULT      TRSTRP        FFPREM                                       247471
     C                   END                                                                  247471
     C     ITISCY        IFNE      ITCTCY                                                     247471
     C     ITISCY        ANDNE     ITOTHC                                                     247471
     C                   Z-ADD     0             FFPREM                                       247471
     C                   END                                                                  247471
     C                   END                                                                  247471
      *                                                                                       247471
 
      **   for processing type 8, multiply the amount calculated
      **   by the contract price.
 
     C     ITISPT        IFEQ      8
     C                   MULT      TRCOPR        FFPREM
     C                   END
 
     C                   Z-ADD     FFPREM        PRMP
     C                   Z-ADD     *ZERO         FFPREM                                       220744
     C                   END
 
     C                   END
 
      **     - Broker  Settlement Instructions
 
     C**********         IF        TRBOCO <> 0                                                CSD027
     C                   IF        TRBOCO <> *BLANKS                                          CSD027
 
     C                   MOVE      NSBCMC        BCMC
     C                   MOVE      NSBCMS        BCMS
     C                   Z-ADD     NSBCMA        BCMA
     C                   MOVE      NSBCHC        BCHC
     C                   Z-ADD     NSBCHA        BCHA
 
     C     CFF007        IFEQ      'Y'                                                        CFF007
                                                                                              CFF007
     C     PPBBFLG       IFEQ      'Y'                                                        CFF007
     C     BCMC          IFNE      *ZERO                                                      CFF007
     C     BCMA          ANDNE     *ZERO                                                      CFF007
     C                   MOVE      'Y'           CMCB                                         CFF007
     C                   ELSE                                                                 CFF007
     C                   MOVE      'N'           CMCB                                         CFF007
     C                   ENDIF                                                                CFF007
                                                                                              CFF007
     C     BCHC          IFNE      *ZERO                                                      CFF007
     C     BCHA          ANDNE     *ZERO                                                      CFF007
     C                   MOVE      'Y'           CHCB                                         CFF007
     C                   ELSE                                                                 CFF007
     C                   MOVE      'N'           CHCB                                         CFF007
     C                   ENDIF                                                                CFF007
     C                   ENDIF                                                                CFF007
                                                                                              CFF007
     C                   ENDIF                                                                CFF007
                                                                                              CFF007
      **       - Determine Commission Pattern
     C                   EXSR      PBCPT
 
     C                   MOVE      NSBSLT        BSLT
     C                   MOVE      NSBSLA        BSLA
     C                   MOVE      NSBSBR        BSBR
     C                   MOVE      NSBBID        BBID
     C                   MOVE      NSBFAO        BFAO
     C                   MOVE      NSBCPN        BCPN
 
     C                   END
 
      **     - Customer  Settlement Instructions
 
     C**********         IF        TRCUSC <> 0                                                CSD027
     C                   IF        TRCUSC <> *BLANKS                                          CSD027
 
     C                   MOVE      NSCCMC        CCMC
     C                   MOVE      NSCCMS        CCMS
     C                   Z-ADD     NSCCMA        CCMA
     C                   MOVE      NSCCHC        CCHC
     C                   Z-ADD     NSCCHA        CCHA
                                                                                              CFF007
     C     CFF007        IFEQ      'Y'                                                        CFF007
                                                                                              CFF007
     C     PPBCFLG       IFEQ      'Y'                                                        CFF007
     C     CCMC          IFNE      *ZERO                                                      CFF007
     C     CCMA          ANDNE     *ZERO                                                      CFF007
     C                   MOVE      'Y'           CMCC                                         CFF007
     C                   ELSE                                                                 CFF007
     C                   MOVE      'N'           CMCC                                         CFF007
     C                   ENDIF                                                                CFF007
                                                                                              CFF007
     C     CCHC          IFNE      *ZERO                                                      CFF007
     C     CCHA          ANDNE     *ZERO                                                      CFF007
     C                   MOVE      'Y'           CHCC                                         CFF007
     C                   ELSE                                                                 CFF007
     C                   MOVE      'N'           CHCC                                         CFF007
     C                   ENDIF                                                                CFF007
     C                   ENDIF                                                                CFF007
                                                                                              CFF007
     C                   ENDIF                                                                CFF007
 
      **       - Determine Commission Pattern
     C                   EXSR      PCCPT
 
     C                   MOVE      NSCSLT        CSLT
     C                   MOVE      NSCSLA        CSLA
     C                   MOVE      NSBRSC        BRSC
     C                   MOVE      NSCBID        CBID
     C                   MOVE      NSCFAO        CFAO
     C                   MOVE      NSCCPN        CCPN
 
     C                   END
 
     C                   END
                                                                                CAP013
      ** Front office transaction ID                                            CAP013
                                                                                CAP013
     C                   MOVEL     TRFRNT        FRNT                           CAP013
      **   Update record
     C                   UPDATE    TRSETDF
     C                   CLOSE     TRSET
 
     C                   END
     C     ExitOnly      TAG                                                                  BG9042
      *                                                                                       BG9042
 
     C                   ENDSR
 
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *                       CSC011
      *  SRCONVERT - Convert trade and settlement details to screen   *                       CSC011
      *              format before writing to API log file.           *                       CSC011
      *                                                               *                       CSC011
      *  Called by:  Main Processing                                  *                       CSC011
      *                                                               *                       CSC011
      *  Calls: FFTRANLOG - FF Trades Support Log                     *                       CSC011
      *                                                               *                       CSC011
      *****************************************************************                       CSC011
                                                                                              CSC011
     C     SRCONVERT     BEGSR                                                                CSC011
                                                                                              CSC011
      ** Call module to convert file format to screen format                                  CSC011
     C                   CALLB     'FFTRANLOG'                                                CSC011
     C                   PARM                    ReturnCode                                   CSC011
     C                   PARM                    TRCHTP                                       CSC011
     C                   PARM                    TRSET_DS                                     CSC011
     C                   PARM                    CFF007                                       CSC011
     C                   PARM                    TRANSDTL                                     CSC011
                                                                                              CSC011
      ** If error occurred in program, return with an error code.                             CSC011
     C                   IF        ReturnCode <> *BLANKS                                      CSC011
     C                   EVAL      DBASE = 2                                                  CSC011
     C                   EVAL      DBFILE = 'FFTRANLOG'                                       CSC011
     C                   EVAL      DBKEY = '*CALL'                                            CSC011
     C                   EXSR      *PSSR                                                      CSC011
     C                   ENDIF                                                                CSC011
                                                                                              CSC011
     C                   ENDSR                                                                CSC011
      *****************************************************************                       CSC011
      /EJECT                                                                                  CSC011
      *****************************************************************                       CSC011
      *                                                               *                       CSC011
      *  SRAPILOG - Write trade details to API standard log file.     *                       CSC011
      *                                                               *                       CSC011
      *  Called by: Main Processing                                   *                       CSC011
      *                                                               *                       CSC011
      *  Calls: APILOGTRAN - API Standard Log Module                  *                       CSC011
      *                                                               *                       CSC011
      *****************************************************************                       CSC011
                                                                                              CSC011
     C     SRAPILOG      BEGSR                                                                CSC011
                                                                                              CSC011
     C                   CLEAR                   PHead                                        CSC011
     C                   CLEAR                   PDealNo                                      CSC011
                                                                                              CSC011
      ** Determine if trade is a market trade or an OTC.                                      CSC011
     C                   IF        TRMTHN <> 0                                                CSC011
     C                   EVAL      APTGTTYPE = 'FFEXTR'                                       CSC011
     C                   ELSE                                                                 CSC011
                                                                                              CAP166
      ** If processing type is 5 use 'FFOTCC' otherwise use 'FFOTCO'                          CAP166
                                                                                              CAP166
     C                   IF        ITISPT = 5                                                 CAP166
     C                   EVAL      APTGTTYPE = 'FFOTCC'                                       CSC011
     C                   ELSE                                                                 CAP166
     C                   EVAL      APTGTTYPE = 'FFOTCO'                                       CAP166
     C                   ENDIF                                                                CAP166
                                                                                              CAP166
     C                   ENDIF                                                                CSC011
                                                                                              CSC011
      ** Fill up other neccessary information in the header to be used                        CSC011
      ** by APITRANLOG                                                                        CSC011
     C                   EVAL      APFOTRANID = FRNT                                          CSC011
     C                   EVAL      APFOASOCID = AFRT                                          CSC011
     C                   EVAL      APRPRLOCN = REPA                                           CSC011
     C                   EVAL      APUSERID = PSUser                                          CSC011
                                                                                              CSC011
      ** Initialise Trade reference                                                           CSC011
     C                   MOVEL     TNBR          PDealNo                                      CSC011
                                                                                              CSC011
      ** Call program that will write to API standard log file.                               CSC011
     C                   CALLB     'APLOGTRAN'                                                CSC011
     C                   PARM                    ReturnCode                                   CSC011
     C                   PARM                    PHead                                        CSC011
     C                   PARM                    TRANSDTL                                     CSC011
     C                   PARM      *BLANKS       PTime                                        CSC011
     C                   PARM                    PDealNo                                      CSC011
     C                   PARM      *BLANKS       PADealNo                                     CSC011
                                                                                              CSC011
      ** If error occurred, return with an error to the calling program                       CSC011
     C                   IF        ReturnCode <> *BLANKS                                      CSC011
     C                   EVAL      DBASE = 3                                                  CSC011
     C                   EVAL      DBFILE = 'APLOGTRAN'                                       CSC011
     C                   EVAL      DBKEY = '*CALL'                                            CSC011
     C                   EXSR      *pssr                                                      CSC011
     C                   ENDIF                                                                CSC011
                                                                                              CSC011
     C                   ENDSR                                                                CSC011
      *****************************************************************                       CSC011
      /EJECT                                                                                  CSC011
      *****************************************************************                       CSC011
 
      **--------------------------------------------------------------------------------------------
      ** The following /COPY contains the error handling subroutine,
      ** which sets a suitable return code and exits the module.
     C/COPY FFCPYSRC,ERROREXIT
      **--------------------------------------------------------------------------------------------
 
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *inzsr - Program Initialisation routine                       *
      *                                                               *
      * Called by: Implicitly on program activation                   *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     *inzsr        BEGSR
 
     C     *entry        PLIST
      ** Parameters returned to caller
      ** -----------------------------
      ** Return code (10A, returned to caller)
     C                   PARM                    ReturnCode
 
      ** Parameters received from caller
      ** -------------------------------
      ** The format containing the valid settlement instructions; broker
      ** and customer (data structure).
     C                   PARM                    FFVSETL
      ** The format containing the valid transaction (data structure)
     C                   PARM                    FFVTRAN
      ** The instrument details (data structure)
     C                   PARM                    InstType
      ** Multibranching indicator
     C                   PARM                    BGMBIN
      ** CFF007 enhancement                                                                   CFF007
     C                   PARM                    CFF007                                       CFF007
      ** PB customer indicator (Broker)                                                       CFF007
     C                   PARM                    PPBBFLG                                      CFF007
      ** PB customer indicator (Customer)                                                     CFF007
     C                   PARM                    PPBCFLG                                      CFF007
 
                                                                                              CSC011
      ** Check if Midas 24x7 Availability is installed.                                       CSC011
     C                   EVAL      CSC011 = 'N'                                               CSC011
     C                   CALLB     'AOSARDR0'                                                 CSC011
     C                   PARM      *BLANKS       PRTCD                                        CSC011
     C                   PARM      '*VERIFY'     POPTN                                        CSC011
     C                   PARM      'CSC011'      PSARD                                        CSC011
     C     SCSARD        PARM      SCSARD        DSFDY                                        CSC011
                                                                                              CSC011
      ** An NRF (No Record Found) return code is valid.                                       CSC011
      ** Issue database error only for error return codes.                                    CSC011
     C                   IF        PRTCD <> *BLANKS AND PRTCD <> '*NRF   '                    CSC011
     C                   EVAL      DBFILE = 'SCSARDPD'                                        CSC011
     C                   EVAL      DBKEY = 'CSC011'                                           CSC011
     C                   EVAL      DBASE = 1                                                  CSC011
     C                   EXSR      *PSSR                                                      CSC011
     C                   ENDIF                                                                CSC011
                                                                                              CSC011
     C                   IF        PRTCD = *BLANK                                             CSC011
     C                   EVAL      CSC011 = 'Y'                                               CSC011
     C                   IN        SC24X7                                                     CSC011
     C                   IN        SDSTAT                                                     CSC011
     C                   ELSE                                                                 CSC011
     C                   EVAL      CSC011 = 'N'                                               CSC011
     C                   ENDIF                                                                CSC011
                                                                                              CSC011
      ** -------------------------------------------------------------------------------------------
      ** Key lists
      ** ---------
 
      ** LF/DEFLT
     C     KDEFLT        KLIST
     C                   KFLD                    KCBCD
     C                   KFLD                    KMRKT
     C                   KFLD                    KISTC
 
 
 
      **--------------------------------------------------------------------------------------------
      ** The following /COPY sets the values of program, module and
      ** procedure names for database error processing.
     C/COPY ZACPYSRC,DBFIELDS
      **--------------------------------------------------------------------------------------------
 
     C                   ENDSR
 
      *****************************************************************
      /EJECT
      *****************************************************************
      **--------------------------------------------------------------------------------------------
      ** The following /COPY contains the standard program status
      ** subroutine, including a bound call to the DBERRCTL module.
     C/COPY ZACPYSRC,PSSR_ILENE
 
      ** Set the return code to indicate to the caller that an
      ** error returned; do NOT call DBERRCTL, as the caller will
      ** handle rollback processing.
     C                   EVAL      ReturnCode = 'Error'
     C                   RETURN
 
     C                   ENDSR
      **--------------------------------------------------------------------------------------------
      ********************************************************************
      /EJECT
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
