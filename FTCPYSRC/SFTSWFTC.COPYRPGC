      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  XXXXXXX - (/COPY Name)                                       *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SWIFT    : Validate SWIFT Address                            *
      *             Allowable characters are A-Z, 0-9                 *
      *                                                               *
      *  CALLED BY:        -                                          *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     C           SWIFT     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SWIFT '  @STK,Q
      *
     C                     MOVEA'00'      *IN,31
      *
      *  Test positions 1-8 for valid SWIFT Characters
      *
      *
      *  Check how many characters are alphanumeric
      *
     C                     MOVEL*BLANKS   I#FLD
     C                     MOVELCLSWTK    I#FLD  35
     C           ALPHAN    CHECKI#FLD     X#      30     90
      *
      * If the first 8 or 11 are ok proceed
      *
     C           *IN90     IFEQ '1'
     C           X#        ANDEQ9
     C           *IN90     OREQ '1'
     C           X#        ANDEQ12
     C           ' '       CHECKI#FLD:X#                 90
      *
      * Check for blanks if before 8 or 11 then assume address
      *
     C           *IN90     IFEQ '1'
     C           X#        ANDEQ11
     C                     MOVEL'1'       *IN31
     C                     MOVEL'Y'       ##CSID  1
     C                     GOTO EXSWFT
     C                     END
     C                     END
      *
      *  Invalid format - Not a S.W.I.F.T.
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'N'       ##CSID  1
     C                     GOTO EXSWFT
     C                     END
      *
      *  Call Access object to validate identifier
      *
     C                     MOVELI#FLD     W9BICC
     C           3         SUBSTI#FLD:9   W9BICB
      *
     C                     CALL 'ME1400'
     C                     PARM *BLANKS   W9RTN   7
     C                     PARM           W9BICC  8
     C                     PARM           W9BICB  3
     C                     PARM           SDCUST
     C                     PARM           SDCNST
     C                     PARM           MEBICD
     C                     PARM *BLANKS   W9CUST  1
     C                     PARM *BLANKS   W9CNST  1
     C                     PARM *BLANKS   W9BICD  1
      *
     C                     SELEC
      *
      *  Record not found on directory but found on others
      *  - BIC directory has data so error
      *
     C           W9RTN     WHEQ 'MIN0244'
     C           W9RTN     OREQ 'MIN0245'
     C           W9RTN     OREQ 'MIN0246'
     C                     MOVEL'Y'       ##CSID
     C                     MOVEL'1'       *IN31
      *
      *  Record not found - S.W.I.F.T does not exist
      *
     C           W9RTN     WHEQ 'Y2U0005'
     C                     MOVEL'Y'       ##CSID
     C                     MOVEL'1'       *IN32
      *
      *  Record found - BIC exists
      *
     C           W9RTN     WHEQ *BLANKS
      *
     C                     MOVEL'Y'       ##CSID
     C                     ENDSL
      *
      *  Unwind subroutine stack name
      *
     C           EXSWFT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
      *****************************************************************
      * End of /COPY XXXXXXX                                          *
      *****************************************************************
