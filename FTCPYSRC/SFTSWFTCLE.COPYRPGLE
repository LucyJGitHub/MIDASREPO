     C/EJECT
      *****************************************************************
      *                                                               *
      *  SWIFT    : Validate SWIFT Address                            *
      *             Allowable characters are A-Z, 0-9                 *
      *                                                               *
      *  CALLED BY:        -                                          *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *  Last Amend No. AR666005                 Date 14Feb23         *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * MIDAS DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. XXXXXXX                  DATEXXXXXXX          *
      *****************************************************************
      *---------------------------------------------------------------*
      *                                                               *
      *  AR666005 - Closed customer numbers are unchecked. Applied    *
      *             fix 229380.                                       *
      *                                                               *
      *****************************************************************
     C     SWIFT         BEGSR
 
     C                   Movea     '00'          *IN(31)
     C                   MOVE      'N'           WCUSTC            1                        AR666005
 
      *  Test positions 1-8 for valid SWIFT Characters
 
      *  Check how many characters are alphanumeric
     C                   Movel     *Blanks       FLD
     C                   Movel     CLSWTK        FLD              35
     C     ALPHAN        Check     FLD           X#                3 0    90
 
      * If the first 8 or 11 are ok proceed
     C                   If        *IN90 = *On and
     C                             X# = 9      or
     C                             *IN90 = *On and
     C                             X# = 12
     C     ' '           Check     FLD:X#                                 90
 
      * Check for blanks if before 8 or 11 then assume address
     C                   If        *IN90 = *On and
     C                             X# = 11
     C                   Eval      *IN31 = *On
     C                   Eval      ##CSID = 'Y'
     C                   Goto      EXSWFT
     C                   End
     C                   End
 
      *  Invalid format - Not a S.W.I.F.T.
     C                   If        *IN90 = *On
     C                   Eval      ##CSID = 'N'
     C                   Goto      EXSWFT
     C                   End
 
      *  Call Access object to validate identifier
     C                   Movel     FLD           W9BICC
     C     3             Subst     FLD:9         W9BICB
 
     C                   CALL      'ME1400'
     C                   Parm      *Blanks       W9RTN             7
     C                   Parm                    W9BICC            8
     C                   Parm                    W9BICB            3
     C                   Parm                    SDCUST
     C                   Parm                    SDCNST
     C                   Parm                    MEBICD
     C                   Parm      *Blanks       W9CUST            1
     C                   Parm      *Blanks       W9CNST            1
     C                   Parm      *Blanks       W9BICD            1
 
     C                   Select
 
      *  Record not found on directory but found on others
      *  - BIC directory has data so error
     C                   When      W9RTN = 'MIN0244' or
     C                             W9RTN = 'MIN0245' or
     C                             W9RTN = 'MIN0246'
     C                   Eval      *IN31 = *On
     C                   Eval      ##CSID = 'Y'
 
      *  Record not found - S.W.I.F.T does not exist
     C                   When      W9RTN = 'Y2U0005'
     C                   Eval      *IN32 = *On
     C                   Eval      ##CSID = 'Y'
 
      *  Record found - BIC exists
     C                   When      W9RTN = *Blanks
     C                   Eval      ##CSID = 'Y'
      *                                                                                     AR666005
     C     W9CUST        IFEQ      'Y'                                                      AR666005
     C     BBCLST        ANDEQ     'Y'                                                      AR666005
     C                   MOVEL     'Y'           WCUSTC                                     AR666005
     C                   ENDIF                                                              AR666005
 
     C                   End
 
     CSR   EXSWFT        ENDSR
