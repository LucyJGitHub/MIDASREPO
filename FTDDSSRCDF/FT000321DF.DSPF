      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Maintain Payment Charge definitions')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FT000321DF - Maintain Payment Charge Definitions        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. 245708             Date 16Mar07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CFT009 *C *CREATE  Date 06Jun00               *
      *                 xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  245708 - Subfile text not displayed.                         *
      *  CFT009   Funds Transfer Fees and Charges                     *
      *                                                               *
      *****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      PRINT(*LIBL/MPRINT)
      *
      *
     A*
     A          R SFL321S1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  34                                  SFLNXTCHG
     A            SFRRN1         5S 0H
     A            SFLSL1         1A  B 11  2
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  37                                  DSPATR(PR)
     A            PYCHCD    R        O 11  8REFFLD(DBFL)
     A                                      DSPATR(HI)
     A            CHDESC    R        O 11 17REFFLD(RPNA)
     A                                      DSPATR(HI)
     A*
     A          R CTL321C1                  SFLCTL(SFL321S1)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  30                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A                                      PAGEDOWN(33)
     A                                      CA03(03 'Exit')
     A                                      CA09(09 'ADD')
      *
     A            BJMRDT         7A  O  1 70
     A                                  2 70TIME
     A                                  1  2USER
     A            #PGM          10A  O  1 13
     A                                  9  2'Sel   Code     Description'
     A                                  5  2'A=Amend   D=Delete   E=Enquire'
     A                                      DSPATR(HI)
     A            #WRKST        10A  O  2  2
     A            POSCDE         5A  B 10  8CHANGE(36)
     A                                      DSPATR(HI)
     A                                  2 27'Payments Charge Definition'
     A                                      DSPATR(HI)
     A                                  8  8'Charge'
     A                                  7  8'Payment'
     A            SFPOS1         4S 0H      SFLRCDNBR
     A*
     A          R TRL321T1
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F3=Exit  F9=Add'
     A          R SFL321S2                  SFL
     A  24                                  SFLNXTCHG
     A            SFRRN2         5S 0H
     A            SCRAN1         2A  O 11  3
      * Start of change 245708
     A*********                        11  9'Manual'
     A            SCTXT1         6A    11  9TEXT('Manual')
      * End of change   245708
     A            SCCCY1         3A  O 11 16DSPATR(HI)
     A            SCRST1        14A  B 11 21
     A  02
     AO 06
     AO 29                                  DSPATR(PR)
     A  29                                  DSPATR(ND)
     A  56N29                               DSPATR(PC)
     A  56N29                               DSPATR(RI)
     A N56                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
     A            SCREN1        14A  B 11 36
     A  02
     AO 06
     AO 29                                  DSPATR(PR)
     A  29                                  DSPATR(ND)
     A  57N29                               DSPATR(PC)
     A  57N29                               DSPATR(RI)
     A N57                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
     A            SCRRT1        14A  B 11 51
     A  02
     AO 06                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A N58                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
     A            SCRPM1        14A  B 11 66
     A  02
     AO 06                                  DSPATR(PR)
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(RI)
     A N59                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
      * Start of change 245708
     A**********                       12 21'Minimum Charge'
     A            SCTXT2        14A    12 21TEXT('Minimum Charge')
      * End of change   245708
     A            SCMIC1        14A  B 12 36
     A  02
     AO 06                                  DSPATR(PR)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N60                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
      * Start of change 245708
     A**********                       12 51'Maximum Charge'
     A            SCTXT3        14A    12 51TEXT('Maximum Charge')
      * End of change   245708
     A            SCMAC1        14A  B 12 66
     A  02
     AO 06                                  DSPATR(PR)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
     A            SCRAN2         2A  O 14  3
      * Start of change 245708
     A**********                       14  9'Manual'
     A            SCTXT4         6A    14  9TEXT('Manual')
      * End of change   245708
     A            SCCCY2    R        B 14 16REFFLD(BCCY)
     A N80
     AO 02
     AO 06                                  DSPATR(PR)
     A N02N06 80                            DSPATR(UL)
     A  62 80                               DSPATR(PC)
     A  62 80                               DSPATR(RI)
     A N62                                  DSPATR(HI)
     A            SCRST2        14A  B 14 21
     A  29
     AO 02
     AO 06                                  DSPATR(PR)
     A  29                                  DSPATR(ND)
     A  63N29                               DSPATR(PC)
     A  63N29                               DSPATR(RI)
     A N63                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
     A            SCREN2        14A  B 14 36
     A  02
     AO 06
     AO 29                                  DSPATR(PR)
     A  29                                  DSPATR(ND)
     A  64N29                               DSPATR(PC)
     A  64N29                               DSPATR(RI)
     A N64                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
      * Start of change 245708
     A**********                       15 21'Minimum Charge'
     A            SCTXT5        14A    15 21TEXT('Minimum Charge')
      * End of change   245708
     A            SCMIC2        14A  B 15 36
     A  02
     AO 06                                  DSPATR(PR)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A N66                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
      * Start of change 245708
     A**********                       15 51'Maximum Charge'
     A            SCTXT6        14A    15 51TEXT('Maximum Charge')
      * End of change   245708
     A            SCMAC2        14A  B 15 66
     A  02
     AO 06                                  DSPATR(PR)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A N67                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
     A            SCRAN3         2A  O 17  3
      * Start of change 245708
     A**********                       17  9'STP'
     A            SCTXT7         3A    17  9TEXT('STP')
      * End of change   245708
     A            SCCCY3         3A  O 17 16DSPATR(HI)
     A            SCRST3        14A  B 17 21
     A  02
     AO 06
     AO 29                                  DSPATR(PR)
     A  29                                  DSPATR(ND)
     A  68N29                               DSPATR(PC)
     A  68N29                               DSPATR(RI)
     A N68                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
     A            SCREN3        14A  B 17 36
     A  02
     AO 06
     AO 29                                  DSPATR(PR)
     A  29                                  DSPATR(ND)
     A  69N29                               DSPATR(PC)
     A  69N29                               DSPATR(RI)
     A N69                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
     A            SCRRT3        14A  B 17 51
     A  02
     AO 06                                  DSPATR(PR)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A N70                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
     A            SCRPM3        14A  B 17 66
     A  02
     AO 06                                  DSPATR(PR)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A N71                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
      * Start of change 245708
     A**********                       18 21'Minimum Charge'
     A            SCTXT8        14A    18 21TEXT('Minimum Charge')
      * End of change   245708
     A            SCMIC3        14A  B 18 36
     A  02
     AO 06                                  DSPATR(PR)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A N72                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
      * Start of change 245708
     A**********                       18 51'Maximum Charge'
     A            SCTXT9        14A    18 51TEXT('Maximum Charge')
      * End of change   245708
     A            SCMAC3        14A  B 18 66
     A  02
     AO 06                                  DSPATR(PR)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A N73                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
     A            SCRAN4         2A  O 20  3
      * Start of change 245708
     A**********                       20  9'STP'
     A            SCTXTA         3A    20  9TEXT('STP')
      * End of change   245708
     A            SCCCY4    R        O 20 16REFFLD(BCCY)
     A                                      DSPATR(HI)
     A            SCRST4        14A  B 20 21
     A  02
     AO 06
     AO 29                                  DSPATR(PR)
     A  29                                  DSPATR(ND)
     A  75N29                               DSPATR(PC)
     A  75N29                               DSPATR(RI)
     A N75                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
     A            SCREN4        14A  B 20 36
     A  02
     AO 06
     AO 29                                  DSPATR(PR)
     A  29                                  DSPATR(ND)
     A  76N29                               DSPATR(PC)
     A  76N29                               DSPATR(RI)
     A N76                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
      * Start of change 245708
     A**********                       21 21'Minimum Charge'
     A            SCTXTB        14A    21 21TEXT('Minimum Charge')
      * End of change 245708
     A            SCMIC4        14A  B 21 36
     A  02
     AO 06                                  DSPATR(PR)
     A  78                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
     A N78                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
      * Start of change 245708
     A**********                       21 51'Maximum Charge'
     A            SCTXTC        14A    21 51TEXT('Maximum Charge')
      * End of change 245708
     A            SCMAC4        14A  B 21 66
     A  02
     AO 06                                  DSPATR(PR)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A N79                                  DSPATR(HI)
     A N02N06                               DSPATR(UL)
      *
     A          R CTL321C2                  SFLCTL(SFL321S2)
     A                                      SFLSIZ(0004)
     A                                      SFLPAG(0001)
     A                                      OVERLAY
     A  20                                  SFLDSP
     A N21                                  SFLDSPCTL
     A  21                                  SFLCLR
     A  22                                  SFLEND(*MORE)
     A                                      CA03(03 'Exit')
     A  04
     AO 09                                  CA05(05 'Refresh')
     A  07                                  CA10(10 'Confirm')
     A                                      CA12(12 'Previous')
      *
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  1  2USER
     A            #PGM          10A  O  1 13
     A            #WRKST        10A  O  2  2
     A                                  2 21'Payments Charge Definition Mainten-
     A                                      ance'
     A                                  4  2'Payment Charge Code'
     A  02
     AO 04
     AO 06        PYCHCD    R        O  4 23REFFLD(DBFL)
     A                                      DSPATR(HI)
     A  02
     AO 06        CHDESC    R        O  4 30REFFLD(RPNA)
     A                                      DSPATR(HI)
     A  09        SPYCCD    R        B  4 23REFFLD(DBFL)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A N51                                  DSPATR(HI)
     A  04
     AO 09        SCHDSC    R        B  4 30REFFLD(RPNA)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A N52                                  DSPATR(HI)
     A                                  6  2'Vat Liable'
     A N23                                  DSPATR(ND)
     A                                  7  2'Calculation Method'
     A                                  7 43'STP Calculation Method'
     AO 04 23
     AO 09 23     SCVTLB    R    1A  B  6 22REFFLD(CGAS)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A N53                                  DSPATR(HI)
     A  04
     AO 09        SCMCMT    R    1A  B  7 22REFFLD(CGAS)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A N54                                  DSPATR(HI)
     A  04
     AO 09        SCSCMT    R    1A  B  7 68REFFLD(CGAS)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A N55                                  DSPATR(HI)
     A  02 23
     AO 06 23     VATLIN    R        O  6 22REFFLD(CGAS)
     A                                      DSPATR(HI)
     A  02
     AO 06        CALCMD    R        O  7 22REFFLD(CGAS)
     A                                      DSPATR(HI)
     A  02
     AO 06        STPCMD    R        O  7 68REFFLD(CGAS)
     A                                      DSPATR(HI)
     A                                  9  2'Range  Manual Ccy   Range Start   -
     A                                        Range End     Range Rate   Range -
      *                                              Begin /184680/
     A                                      Pro Mile'
      *                                              End   /184680/
     A                                 10  2'       /STP'
     A            SFPOS2         4S 0H      SFLRCDNBR
     A                                  3  2'Action : '
     A            #ACT           1   O  3 12
     A                                      DSPATR(HI)
     A*
     A          R TRL321T2
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F3=Exit  F5=Refresh  F12=Previous -
     A                                       Rollup  Rolldown'
     A*
     A          R TRL321T3
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
      *                                              Begin /184680/
     A                                 23  2'F3=Exit  F12=Previous  Rollup  Rol-
     A                                      ldown'
     A*
     A          R TRL321T4
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F3=Exit  F10=Confirm  F12=Previous-
     A                                        Rollup  Rolldown'
     A          R TRL321T5
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F3=Exit  F5=Refresh  F12=Previous -
     A                                       Rollup  Rolldown'
      *
     A          R SFLMSG                       SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
      *
     A          R MSGCTL
     A                                      SFLCTL(SFLMSG)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(2)
     A                                      SFLDSPCTL SFLDSP
     A  36
     AON36                                  SFLEND
     A            #PGM                      SFLPGMQ
      *
     A          R FMT321
     A                                      CA03(03 'Exit')
     A                                      CA09(09 'Add')
     A                                      OVERLAY
     A                                  1  2USER
     A            #PGM          10A  O  1 13
     A            #WRKST        10A  O  2  2
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A                                  2 21'Payments Charge Definition Mainten-
     A                                      ance'
     A                                      DSPATR(HI)
     A                                 23  3'F3=Exit'
     A                                      DSPATR(HI)
     A                                 23 14'F9=Add'
     A                                      DSPATR(HI)
     A                                 10 31'No records found'
     A                                      DSPATR(HI)
