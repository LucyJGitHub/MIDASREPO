     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Maintain rate spread codes')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Funds Transfer Module                                *
     A*                                                               *
     A*  FT000322DF - MAINTAIN EXCHANGE RATE SPREAD CODES             *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
     A*  Last Amend No. CFT009  *CREATE    Date 08Jun00               *
     A*  Prev Amend No. xxxxxx             Date DdMmmYy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CFT009 - FT Fees and Charges                                 *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * FIRST SCREEN SUBFILE                                             *
      * -------------------------------------------------------------------*
      *
     A          R FT000322SA                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SFL
     A  95                                  SFLNXTCHG
     A            S1SEL          1A  B 10  3TEXT('Select option')
     A                                      DSPATR(UL)
     A N10                                  DSPATR(HI)
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A            S1SPRCOD       5A  O 10  9DSPATR(HI)
     A            S1XRSDSC      30A  O 10 19DSPATR(HI)
     A            S1PNTDCD       5A  H
      *
      * -------------------------------------------------------------------*
      * * FIRST SCREEN CONTROL                                             *
      * -------------------------------------------------------------------*
      *
     A          R FT000322CA                SFLCTL(FT000322SA)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF09
     A  92                                  SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND(*MORE)
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
     A            S1SFRN         4S 0H      SFLRCDNBR
     A            SUSER         10A  O  1  2OVRDTA
     A                                  1 13'FT000322'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            SWID          10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 25'Exchange Rate Spread +
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  2'A=Amend, D=Delete, E=Enquire'
     A                                  6  9'Exchange'
     A                                  7  9'Spread'
     A                                  8  2'Sel    Code      Description'
     A            S1POSNTO       5A  B  9  9
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
      * -------------------------------------------------------------------*
      * * FIRST SCREEN FOOTER                                              *
      * -------------------------------------------------------------------*
      *
     A          R FT000322FA                TEXT('Initial screen footer')
     A                                      CF03
     A                                      CF09
     A                                      BLINK
     A                                      OVERLAY
     A                                      ERASE(FT000322SA)
     A                                      PUTOVR
     A                                 23  2'F3=Main Menu'
     A                                 23 16'F9=Add'
      *                                                                ----*
      * -------------------------------------------------------------------*
      * * SECOND SCREEN SUBFILE                                            *
      * -------------------------------------------------------------------*
      *
     A          R FT000322SB                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SFL
     A  91                                  SFLNXTCHG
     A            H@IN74         1A  H
      *
     A            S2SEL          1A  B 12  2TEXT('Select')
     A N70
     A  71                                  DSPATR(UL)
     A N12                                  DSPATR(HI)
     A  12                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A  70
     AON71                                  DSPATR(PR)
     A            S2XRSCCY       3A  B 12  5TEXT('Currency')
     A N74N70                               DSPATR(UL)
     A N13                                  DSPATR(HI)
     A  13                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A  70
     AO 74                                  DSPATR(PR)
     A                                 12  9'Man'
     A            S2MANBPC      14A  B 12 13TEXT('Buy Spread % - Manual')
     A N70                                  DSPATR(UL)
     A N14                                  DSPATR(HI)
     A  14                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            S2MANBSP      14A  B 12 28TEXT('Buy Spread Points - Manual')
     A N70                                  DSPATR(UL)
     A N15                                  DSPATR(HI)
     A  15                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            S2MANBSN       1A  B 12 43TEXT('Buy Sign - Manual')
     A N70                                  DSPATR(UL)
     A N16                                  DSPATR(HI)
     A  16                                  DSPATR(RI)
     A  16                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            S2MANSPC      14A  B 12 47TEXT('Sell Spread % - Manual')
     A N70                                  DSPATR(UL)
     A N17                                  DSPATR(HI)
     A  17                                  DSPATR(RI)
     A  17                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            S2MANSSP      14A  B 12 62TEXT('Sell Spread Points - Manual')
     A N70                                  DSPATR(UL)
     A N18                                  DSPATR(HI)
     A  18                                  DSPATR(RI)
     A  18                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            S2MANSSN       1A  B 12 78TEXT('Sell Sign - Manual')
     A N70                                  DSPATR(UL)
     A N19                                  DSPATR(HI)
     A  19                                  DSPATR(RI)
     A  19                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
      *
     A                                 13  9'STP'
     A            S2STPBPC      14A  B 13 13TEXT('Buy Spread % - STP')
     A N70                                  DSPATR(UL)
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            S2STPBSP      14A  B 13 28TEXT('Buy Spread Points - STP')
     A N70                                  DSPATR(UL)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            S2STPBSN       1A  B 13 43TEXT('Buy Sign - STP')
     A N70                                  DSPATR(UL)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            S2STPSPC      14A  B 13 47TEXT('Sell Spread % - STP')
     A N70                                  DSPATR(UL)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            S2STPSSP      14A  B 13 62TEXT('Sell Spread Points - STP')
     A N70                                  DSPATR(UL)
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            S2STPSSN       1A  B 13 78TEXT('Sell Sign - STP')
     A N70                                  DSPATR(UL)
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A*
     A* -------------------------------------------------------------------*
     A* * SECOND SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R FT000322CB                SFLCTL(FT000322SB)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      TEXT('SECOND SCREEN CONTROL')
     A                                      CF03
     A  73                                  CF10
     A                                      CF12
     A  90                                  SFLDSP
     A                                      SFLDSPCTL
     A  89                                  SFLEND(*MORE)
     A  88                                  SFLCLR
     A N89                                  ROLLUP(87)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
     A            S2SFRN         4S 0H      SFLRCDNBR
     A            SUSER         10A  O  1  2OVRDTA
     A                                  1 13'FT000322'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            SWID          10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 25'Exchange Rate Spread Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Exchange Rate Spread Code'
     A                                  5  2'Exchange Rate Spread Description'
     A                                  7  2'Parent Spread Code'
     A            S2SPRDCD       5A  B  4 37
     A N70N72                               DSPATR(UL)
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
     A  70
     AO 72                                  DSPATR(PR)
     A                                      OVRDTA
     A            S2XRSDSC      30A  B  5 37
     A N70                                  DSPATR(UL)
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            S2PNTDCD       5A  B  7 37
     A N70                                  DSPATR(UL)
     A N11                                  DSPATR(HI)
     A  11                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A N30                              9  2'D=Delete'
     A                                 10 13'Buy Spread'
     A                                 10 28'Buy Spread'
     A                                 10 47'Sell Spread'
     A                                 10 62'Sell Spread'
     A  71                             11  1'Sel'
     A                                 11  5'Ccy'
     A                                 11 13'Percentage'
     A                                 11 28'Points'
     A                                 11 42'+/-'
     A                                 11 47'Percentage'
     A                                 11 62'Points'
     A                                 11 77'+/-'
     A                                  3  2'Action = '
     A            S2ACTION       1   O  3 12DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * SECOND SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R FT000322FB                TEXT('Second screen footer')
     A                                      CF03
     A  70                                  CF10
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      ERASE(FT000322SB)
     A                                      PUTOVR
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Previous'
     A                                 23 24'Rollup    Rolldown'
     A  73                             23 44'F10=Confirm'
      *                                                                ----*
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R FT000322M1                TEXT('Message Subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            S1SFKY                    SFLMSGKEY
     A            S1PGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R FT000322M0                TEXT('Msg Subfile Control')
     A                                      CF03
     A                                      SFLCTL(FT000322M1)
     A N94                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  93                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            S1PGMQ                    SFLPGMQ
