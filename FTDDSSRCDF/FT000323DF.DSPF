     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Maintain Payment Date Offset Codes')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Funds Transfer Module                                *
     A*                                                               *
     A*  DSPF/FT000323DF - Maintain Payment Date Offset Codes         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
     A*  Last Amend No. CFT009 *CREATE     Date 05Jun00               *
     A*  Prev Amend No. xxxxxx             Date ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CFT009 - Funds Transfer Fees and Charges                     *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      *
     A          R FT323S1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  34                                  SFLNXTCHG
     A            SFLRRN         5S 0H
     A            SFLSEL         1A  B  8  2
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A            PYDTOC    R        O  8  9REFFLD(DBFL)
     A                                      DSPATR(HI)
     A            PYDTOD    R        O  8 15REFFLD(BDES)
     A                                      DSPATR(HI)
     A*
     A          R FT323C1                   SFLCTL(FT323S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  30                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A                                      CA03(03 'Exit')
     A                                      CA09(09 'ADD')
     A                                      CHANGE(60)
      *
     A                                  2 24'Maintain Payment Date Offset Codes'
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O  1 70
     A                                  2 70TIME
     A                                  1  2USER
     A            #PGM          10A  O  1 13
     A                                  6  5'Sel Code  Description'
     A                                  4  5'A=Amend   D=Delete   E=Enquire'
     A                                      DSPATR(HI)
     A            #WRKST        10A  O  2  2
     A            POSCDE         5A  B  7  9
      *
     A          R FT323T1
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F3=Exit   F9=Add'
      *
     A          R SFLMSG                       SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
      *
     A          R MSGCTL
     A                                      SFLCTL(SFLMSG)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(99)
     A                                      SFLDSPCTL SFLDSP
     A  36
     AON36                                  SFLEND
     A            #PGM                      SFLPGMQ
     A*
     A          R FT323X1
     A                                      CA03(03 'Exit')
     A  44                                  CF10(10 'CONFIRM')
     A                                      CA12(12 'RETURN')
     A                                      OVERLAY
      *
     A                                  1  2USER
     A            #PGM          10A  O  1 13
     A            #WRKST        10A  O  2  2
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A                                  4  2'Action = '
     A            #ACT           1A  O  4 12DSPATR(HI)
     A                                  2 18'Funds Transfer Payment Date Offset-
     A                                       Maintenance'
     A                                      DSPATR(HI)
     A                                  7  3'Payment Date Offset Code . . . . .-
     A                                       . : '
     A                                  9  3'Description  . . . . . . . . . . .-
     A                                       . : '
     A                                 11  3'Local Start time . . . . . . . . .-
     A                                       . : '
     A                                 13  3'Local Cut-off time . . . . . . . .-
     A                                       . : '
     A                                 15  3'Before Cut-off settle date offset -
     A                                       . :'
     A                                 17  3'Before Cut-off payment date offset-
     A                                       . :'
     A                                 19  3'After Cut-off settle date offset .-
     A                                       . : '
     A                                 21  3'After Cut-off payment date offset -
     A                                       . : '
     A            WPYDTOC   R        B  7 44REFFLD(DBFL)
     A  49                                  DSPATR(PR)
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A N55                                  DSPATR(UL)
     A            WPYDTOD   R        B  9 44REFFLD(BDES)
     A  50                                  DSPATR(PR)
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A N56                                  DSPATR(UL)
     A            WLSTTIM        8   B 11 44
     A  50                                  DSPATR(PR)
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A N56                                  DSPATR(UL)
     A            WLCUTOF        8   B 13 44
     A  50                                  DSPATR(PR)
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A N56                                  DSPATR(UL)
     A            WBCOSDO        6   B 15 44
     A  50                                  DSPATR(PR)
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A N56                                  DSPATR(UL)
     A            WBCOPDO        6   B 17 44
     A  50                                  DSPATR(PR)
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A N56                                  DSPATR(UL)
     A            WACOSDO        6   B 19 44
     A  50                                  DSPATR(PR)
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A N56                                  DSPATR(UL)
     A            WACOPDO        6   B 21 44
     A  50                                  DSPATR(PR)
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A N56                                  DSPATR(UL)
     A                                 23  3'F3=Exit'
     A  44                             23 14'F10=Confirm'
     A                                 23 29'F12=Previous'
