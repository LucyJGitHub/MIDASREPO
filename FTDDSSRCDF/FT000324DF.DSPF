      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Maintain Transaction Types : Display')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FT000324DM - Maintain Transaction Types                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CFT009 *CREATE     Date 05Jun00               *
      *                 xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFT009 - Funds Transfer Fees and Charges                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
      *
      *
     A          R FT324S1                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  34                                  SFLNXTCHG
     A            SFLRRN         5S 0H
     A            SFLSEL         1   B  7  6
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A            TRTYPE    R        O  7 10REFFLD(CKEN)
     A                                      DSPATR(HI)
     A            TRNDSC    R        O  7 15REFFLD(RPNA)
     A                                      DSPATR(HI)
      *
     A            COREDL    R        H      REFFLD(CHSN)
     A            RCVINT    R        H      REFFLD(CHSN)
     A            PAYGEN    R        H      REFFLD(CHSN)
      *
     A          R FT324C1                   SFLCTL(FT324S1)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      OVERLAY
     A  30                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A                                      CA03(03 'Exit')
     A                                      CA09(09 'ADD')
      *
     A                                  2 25'Maintain Transaction Types'
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O  1 70
     A                                  2 70TIME
     A                                  1  2USER
     A            #PGM          10A  O  1 13
     A                                  6  5'Sel Type  Description'
     A                                  4  5'A=Amend   D=Delete  E=Enquire'
     A                                      DSPATR(HI)
     A            #WRKST        10A  O  2  2
     A*
     A          R FT324T1
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F3=Exit   F9=Add'
      *
     A          R SFLMSG                       SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
      *
     A          R MSGCTL
     A                                      SFLCTL(SFLMSG)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(2)
     A                                      SFLDSPCTL SFLDSP
     A  36
     AON36                                  SFLEND
     A            #PGM                      SFLPGMQ
      *
      *
     A          R FT324X1
     A                                      CA03(03 'Exit')
     A  44                                  CF10(10 'CONFIRM')
     A                                      CA12(12 'RETURN')
     A                                      OVERLAY
      *
     A                                  1  2USER
     A            #PGM          10A  O  1 13
     A            #WRKST        10A  O  2  2
     A                                  2 19'Funds Transfer Transaction Type Ma-
     A                                      intenance'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A                                  4  2'Action = '
     A            #ACT           1A  O  4 12DSPATR(HI)
     A                                  7  2'Transaction Type Code . . . . . . -
     A                                      . . . . . . :'
     A                                 10  2'Description . . . . . . . . . . . -
     A                                      . . . . . . :'
     A                                 13  2'Available for Receivers'''
     A                                 13 27'/ Intermediaries'''
     A                                 14 36'charges . . :'
     A                                 17  2'Payment Advice Generation . . . . -
     A                                      . . . . . . :'
     A            WTRTYPE   R        B  7 51REFFLD(CKEN)
     A  49                                  DSPATR(PR)
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A N51                                  DSPATR(UL)
     A            WTRNDSC   R        B 10 51REFFLD(RPNA)
     A  50                                  DSPATR(PR)
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A N52                                  DSPATR(UL)
     A            WRCVINT   R        B 14 51REFFLD(CHSN)
     A  50                                  DSPATR(PR)
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A N52                                  DSPATR(UL)
     A            WPAYGEN   R        B 17 51REFFLD(CHSN)
     A  50                                  DSPATR(PR)
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A N52                                  DSPATR(UL)
     A                                 23  2'F3=Exit'
     A  44                             23 12'F10=Confirm'
     A  44                             23 26'F12=Previous'
     A N44                             23 12'F12=Previous'
