      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Receivers and Intermediaries Charges')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FT000327DF - Maintain Receivers and Intermediary Chgs   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CFT009 *CREATE     Date 05Jun00               *
      *  Prev Amend No. xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFT009 - Funds Transfer Fees and Charges                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
      *
      *
     A*
     A          R FT327S1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  34                                  SFLNXTCHG
     A            SFLRRN         5S 0H
     A            SFLSEL         1A  B  8  3
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A            PTYID     R        O  8  7REFFLD(DCRA)
     A                                      DSPATR(HI)
     A            BBCNA1        35   O  8 22DSPATR(HI)
     A            CURCCY    R        O  8 61REFFLD(BCCY)
     A                                      DSPATR(HI)
     A            TRANTY    R        O  8 68REFFLD(CKEN)
     A                                      DSPATR(HI)
     A            CHGCD     R        O  8 74REFFLD(DBFL)
     A                                      DSPATR(HI)
     A*
     A          R FT327C1                   SFLCTL(FT327S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  30                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A                                      CA03(03 'Exit')
     A                                      CA09(09 'ADD')
     A                                      CHANGE(60)
      *
     A            BJMRDT         7A  O  1 73
     A                                  2 73TIME
     A                                  1  2USER
     A            #PGM          10A  O  1 13
     A                                  3  7'A=Amend   D=Delete   E=Enquire'
     A                                      DSPATR(HI)
     A            #WRKST        10A  O  2  2
     A            POSCDE        11A  B  7  7DSPATR(HI)
     A                                  2 25'Maintain Receiver''s and -
     A                                      Intermediaries'' Charges'
     A                                      DSPATR(HI)
     A                                  6  3'Sel Identifier     Description    -
     A                                                           Currency Type -
     A                                        Code'
     A                                  5  7'Party                             -
     A                                                                      Cha-
     A                                      rge'
      *
     A          R FT327T1
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F3=Exit   F9=Add'
      *
     A          R SFLMSG                       SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
      *
     A          R MSGCTL
     A                                      SFLCTL(SFLMSG)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(2)
     A                                      SFLDSPCTL SFLDSP
     A  36
     AON36                                  SFLEND
     A            #PGM                      SFLPGMQ
     A          R FT327X1
     A                                      CA03(03 'Exit')
     A  44                                  CF10(10 'CONFIRM')
     A                                      CA12(12 'RETURN')
     A                                      OVERLAY
      *
     A                                  1  2USER
     A            #PGM          10A  O  1 13
     A            #WRKST        10A  O  2  2
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A                                  4  2'Action = '
     A            #ACT           1A  O  4 12DSPATR(HI)
     A                                  2 19'Maintain Receiver''s and -
     A                                      Intermediaries'' Charges'
     A                                      DSPATR(HI)
     A                                  7  3'Party Identifier .  . . . :'
     A                                 10  3'Currency . . . . .  . . . :'
     A                                 13  3'Transaction Type .  . . . :'
     A                                 16  3'Charge Code  . . .  . . . :'
     A            WPTYID    R        B  7 32REFFLD(DCRA)
     A N45                                  DSPATR(HI)
     A  49                                  DSPATR(PR)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A N49                                  DSPATR(UL)
     A                                      CHANGE(61)
     A            WBBCNA1       35   O  7 44
     A                                      DSPATR(HI)
     A            WCURCCY   R        B 10 32REFFLD(BCCY)
     A  50                                  DSPATR(PR)
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A N50                                  DSPATR(UL)
     A            WTRANTY   R        B 13 32REFFLD(CKEN)
     A  50                                  DSPATR(PR)
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A N50                                  DSPATR(UL)
     A            WCHGCD    R        B 16 32REFFLD(DBFL)
     A  51                                  DSPATR(PR)
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A N51                                  DSPATR(UL)
     A                                 23  4'F3=Exit'
     A  44                             23 15'F10=Confirm'
     A  44                             23 29'F12=Previous'
     A N44N52                          23 15'F12=Previous'
     A N44 52                          23 15'F12=Reject'
     A            XPTYID    R        H      REFFLD(DCRA)
     A            XCURCCY   R        H      REFFLD(BCCY)
     A            XTRANTY   R        H      REFFLD(CKEN)
