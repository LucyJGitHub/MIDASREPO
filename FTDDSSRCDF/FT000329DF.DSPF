      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Maintain Notification Code Words')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FT000329DF - Maintain Notification Code Words           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CFT009 *CREATE     Date 05Jun00               *
      *  Prev Amend No. xxxxxx             Date xxxxxxx               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFT009 -  Funds Transfer Fees and Charges                    *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
      *
      *
     A          R FT329S1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  34                                  SFLNXTCHG
     A            SFLRRN         5S 0H
     A            SFLSEL         1A  B  8  6
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A            NCODEW    R        O  8 10REFFLD(ACWB)
     A                                      DSPATR(HI)
     A          R FT329C1                   SFLCTL(FT329S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CA03(03 'Exit')
     A                                      CA09(09 'ADD')
     A                                      CHANGE(60)
     A                                      OVERLAY
     A  30                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
      *
     A                                  1 31'Maintain Code Words'
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O  1 70
     A                                  2 70TIME
     A                                  1  2USER
     A            #PGM          10A  O  1 13
     A                                  6  5'Sel  Code Word'
     A                                  4  5'A=Amend   D=Delete  E=Enquire'
     A                                      DSPATR(HI)
     A            #WRKST        10A  O  2  2
     A            POSCDE        10A  B  7 10
     A*
     A          R FT329T1
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F3=Exit   F9=Add'
      *
     A          R SFLMSG                       SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
      *
     A          R MSGCTL
     A                                      SFLCTL(SFLMSG)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(2)
     A                                      SFLDSPCTL SFLDSP
     A  36
     AON36                                  SFLEND
     A            #PGM                      SFLPGMQ
      *
      *
     A          R FT329X1
      *
     A                                      CA03(03 'Exit')
     A  44                                  CF10(10 'CONFIRM')
     A                                      CA12(12 'RETURN')
     A                                      OVERLAY
      *
     A                                  1  2USER
     A            #PGM          10A  O  1 13
     A            #WRKST        10A  O  2  2
     A                                  2 24'Notification Code Word Maintenance'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A                                  4  2'Action = '
     A            #ACT           1A  O  4 12DSPATR(HI)
     A                                 13  2'Notification Code Word  . . . . . -
     A                                      . . . . . . :'
     A            WNCODEW   R        B 13 51REFFLD(ACWB)
     A  49                                  DSPATR(PR)
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A  46                                  DSPATR(UL)
     A                                 23  2'F3=Exit'
     A  44                             23 12'F10=Confirm'
     A                                 23 26'F12=Return'
