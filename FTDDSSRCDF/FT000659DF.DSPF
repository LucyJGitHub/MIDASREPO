      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Customer Notification Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FT000659DM - Payment Notification Advice Enquiry        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CFT009 *CREATE     Date 05Jun00               *
      *  Prev Amend No. xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFT009 - Funds Transfer Fees and Charges                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
      *
      *
     A*
     A          R FT659S1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  34                                  SFLNXTCHG
     A            SFLRRN         5S 0H
     A            SFLSEL         1A  B  8  2
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A            WBBCUST        6   O  8  4
     A                                      DSPATR(HI)
     A            WBBCNA1       35   O  8 11DSPATR(HI)
     A            WPREF     R        O  8 47REFFLD(PREF)
     A                                      DSPATR(HI)
     A            WAMT          14   O  8 63DSPATR(HI)
     A            WCCY      R        O  8 78REFFLD(SMCY)
     A                                      DSPATR(HI)
     A                                  9  4'Advice Method'
     A            WADVMTD   R        O  9 20REFFLD(ACCK) DSPATR(HI)
     A            WADVNOT   R        O  9 31REFFLD(DDES) DSPATR(HI)
     A            WTYPE          2   H
     A            WTRNSID       16   H
      *
     A          R FT659C1                   SFLCTL(FT659S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0007)
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'REFRESH')
     A                                      CHANGE(60)
     A                                      RTNCSRLOC(&@@CRC1 &@@CFC1 &@@CPC1)
     A                                      BLINK
     A                                      OVERLAY
     A                                      SFLCSRRRN(&@@RNS1)
     A  30N31                               SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
      *
     A            @@RNC1         4S 0H      SFLRCDNBR
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  1  2USER
     A            #PGM          10A  O  1 13
     A            #WRKST        10A  O  2  2
     A                                  1 27'Payment Advice Notification'
     A                                      DSPATR(HI)
     A                                  3  3'Position on customer : '
     A                                  3 35'Select on customer : '
     A            CUSPOS         6A  B  3 27
     A            CUSSEL         6A  B  3 57
     A                                  7  4'Customer'
     A                                  7 53'Reference'
     A                                  7 71'Amount'
     A                                  7 78'Ccy'
     A                                  5  4'A=Amend Payment   E=Enquire on Pay-
     A                                      ment   X=Action Payment'
     A                                      COLOR(BLU)
     A            @@CPC1         4S 0H
     A            @@CFC1        10A  H
     A            @@CRC1        10A  H
     A            @@RNS1         5S 0H
      *
     A          R BLK659
     A            BLK            1      2  2
      *
     A          R FT659T1
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F3=Exit   F5=Refresh'
      *
     A          R SFLMSG                       SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
      *
     A          R MSGCTL
     A                                      SFLCTL(SFLMSG)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(2)
     A                                      SFLDSPCTL SFLDSP
     A            #PGM                      SFLPGMQ
