     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Payment Charge Prompt : Display')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Funds Transfer Module                                *
     A*                                                               *
     A*  DSPF/FT000940DF - Select Payment Charge                      *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
     A*  Prev Amend No. CFT009 *CREATE     Date 05Jun00               *
     A*                 xxxxxx             Date ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  CFT009 - Funds Transfer Fees and Charges                     *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CF12(12 'Exit')
     A*
     A          R FT940S1                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  34                                  SFLNXTCHG
     A            SFLRRN         5S 0H
     A            SFLSEL         1A  B  7  6
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A            PYCHCD    R        O  7  9REFFLD(DBFL)
     A                                      DSPATR(HI)
     A            CHDESC    R        O  7 15REFFLD(RPNA)
     A                                      DSPATR(HI)
     A*
     A          R FT940C1                   SFLCTL(FT940S1)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      CHANGE(60)
     A                                      OVERLAY
     A  30                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
      *
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  1  2USER
     A            #PGM          10A  O  2  2
     A                                  6  5'Sel Code  Description'
     A                                  4  5'1=Select'
     A                                      DSPATR(HI)
     A                                  1 27'Select Payment Charge'
     A                                      DSPATR(HI)
     A                                  3 27'Position to : '
     A            POSCDE    R        B  3 42REFFLD(DBFL)
     A*
     A          R FT940T1
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F12=Return'
      *
     A          R SFLMSG                       SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
      *
     A          R MSGCTL
     A                                      SFLCTL(SFLMSG)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(2)
     A                                      SFLDSPCTL SFLDSP
     A            #PGM                      SFLPGMQ
