      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Payment Date Offset Codes PROMPT')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FT000960DF - Payment Date Offset Code                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CFT009 *CREATE     Date 05Jun00               *
      *  Prev Amend No. xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFT009 - Funds Transfer Fees and Charges                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CF12(12 'Exit')
      *
      *
     A*
     A          R FT960S1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  34                                  SFLNXTCHG
     A            SFLRRN         5S 0H
     A            SFLSEL         1A  B  7  6
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A            PYDTOC    R        O  7  9REFFLD(DBFL)
     A                                      DSPATR(HI)
     A            PYDTOD    R        O  7 16REFFLD(BDES)
     A                                      DSPATR(HI)
     A*
     A          R FT960C1                   SFLCTL(FT960S1)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      CHANGE(60)
     A                                      OVERLAY
     A  30                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
      *
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  1  2USER
     A            #PGM          10A  O  2  2
     A                                  6  5'Sel Code   Description'
     A                                  4  5'1=Select'
     A                                      DSPATR(HI)
     A                                  1 27'Payment Date Offset Prompt'
     A                                      DSPATR(HI)
     A                                  3 27'Position to : '
     A            POSCDE    R        B  3 42REFFLD(DBFL)
     A                                  5  9'Payment'
     A*
     A          R FT960T1
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F12=Return'
      *
     A          R SFLMSG                       SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
      *
     A          R MSGCTL
     A                                      SFLCTL(SFLMSG)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(2)
     A                                      SFLDSPCTL SFLDSP
     A            #PGM                      SFLPGMQ
