      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IBLC 2002 Outgoing Payments display')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FT006ADF - IBLC 2002 Outgoing Payments display          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A*
     A*----------------*
     A*  Input Screen  *
     A*----------------*
     A*
     A          R FT006AF0
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2005')
     A                                      KEEP
     A                                      OVERLAY
     A                                      CF03
     A  31                                  CF05
     A                                      CF12
     A            SUSER     R        O  1  2REFFLD(JOUSER)
     A            SJOB      R        O  1 13REFFLD(JOPGM)
     A            SRUNA     R        O  1 74REFFLD(RUNA)
     A                                  1 67'DATE '
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*
     A                                  3 32'BCL 2002 Reporting'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  2'Ordering Type . . . . . . . . :'
     A*
     A            #0ORTY         2A  B  5 34TEXT('Ordering type')
     A  31                                  DSPATR(UL)
     A N20N31                               DSPATR(HI)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
     A                                  6  2'Beneficiary Type  . . . . . . :'
     A*
     A            #0BETY         2A  B  6 34TEXT('Beneficiary type')
     A  31                                  DSPATR(UL)
     A N21N31                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
     A                                  7  2'IBLC Operation Code . . . . . :'
     A*
     A            #0OPCO         3A  B  7 34TEXT('IBLC Operation Code')
     A  31                                  DSPATR(UL)
     A N22N31                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
     A                                  8  2'Country Code  . . . . . . . . :'
     A*
     A            #0COCO         2A  B  8 34TEXT('Country Code')
     A  31                                  DSPATR(UL)
     A N23N31                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
     A                                  9  2'Identification Code . . . . . :'
     A*
     A            #0IDCO         2A  B  9 34TEXT('Identification Code')
     A  31                                  DSPATR(UL)
     A N24N31                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
     A                                 10  2'Identification Number . . . . :'
     A*
     A            #0IDEN         9A  B 10 34TEXT('Identification')
     A  31                                  DSPATR(UL)
     A N25N31                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
     A  31                             10  2'F3=Exit   F5=Refresh'
     A                                      COLOR(BLU)
     A N31                             23  1'F3=Exit F12=Previous'
     A                                      COLOR(BLU)
      *
      *------------------*
      *  Message Format  *
      *------------------*
      *
     A          R #MSGRCD                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *-------------------*
      *  Message Control  *
      *-------------------*
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(10)
     A                                      SFLPAG(01)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     AON55                                  SFLEND
     A            ##PGM                     SFLPGMQ
