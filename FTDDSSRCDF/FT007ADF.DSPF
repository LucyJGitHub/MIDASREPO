      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IBLC 2002 Cheques For Collection Display')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FT007ADF - IBLC Cheques for Collection Display          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
      *
      *----------------*
      *  Input Screen  *
      *----------------*
      *
     A          R FT007AF0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      KEEP
     A                                      OVERLAY
     A  31                                  CF05
     A                                      CF03
     A            SUSER     R        O  1  2REFFLD(JOUSER)
     A            SJOB      R        O  1 13REFFLD(JOPGM)
     A            SRUNA     R        O  1 74REFFLD(RUNA)
     A                                  1 67'DATE '
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  3 12'BCL 2002 Reporting'
     A                                      DSPATR(HI UL)
     A                                  5  2'Ordering Type . . . . . . . . :'
     A                                  6  2'Beneficiary Type  . . . . . . :'
     A                                  7  2'IBLC Operation Code . . . . . :'
     A                                  8  2'Country Code  . . . . . . . . :'
     A                                  9  2'Identification Code . . . . . :'
     A                                 10  2'Identification Number . . . . :'
      *
     A            #0ORTY         2A  B  5 34TEXT('Ordering Type')
     A  31                                  DSPATR(UL)
     A N20N31                               DSPATR(HI)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A            #0BETY         2A  B  6 34TEXT('Beneficiary Type')
     A  31                                  DSPATR(UL)
     A N21N31                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A            #0OPCO         3A  B  7 34TEXT('IBLC Operation Code')
     A  31                                  DSPATR(UL)
     A N22N31                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A            #0COCO         2A  B  8 34TEXT('Country Code')
     A  31                                  DSPATR(UL)
     A N23N31                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A            #0IDCO         2A  B  9 34TEXT('Identification Code')
     A  31                                  DSPATR(UL)
     A N24N31                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A            #0IDEN         9A  B 10 34TEXT('Identification Number')
     A  31                                  DSPATR(UL)
     A N25N31                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A N31                             23  5'F3=Exit   F5=Refresh'
     A  31                             23  2'Enter to continue ... '
      *
      *------------------*
      *  Message Format  *
      *------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *-------------------*
      *  Message Control  *
      *-------------------*
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(10)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  55
     AON55                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
