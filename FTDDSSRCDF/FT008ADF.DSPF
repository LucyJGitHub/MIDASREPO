      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IBLC 2002 Cheques to be Paid Display')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FT0080#L - IBLC Cheques to be Paid Display              *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001 *CREATE     Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      *----------------*
      *  Input Screen  *
      *----------------*
      *
     A          R FT0080X0
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A                                      KEEP
     A                                      OVERLAY
     A  31                                  CF05
     A                                      CF03
     A                                      CSRLOC(CSRROW CSRCOL)
      *
     A            CSRROW         3S 0H
     A            CSRCOL         3S 0H
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FT0080A '
     A                                  1 32'BCL 2002 Reporting'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  5  2'Ordering type . . . . . . . . :'
     A                                  6  2'Beneficiary type  . . . . . . :'
     A                                  7  2'IBLC Operation Code . . . . . :'
     A                                  8  2'Country Code  . . . . . . . . :'
     A                                  9  2'Identification code . . . . . :'
     A                                 10  2'Identification number . . . . :'
      *
     A            #0ORTY         2A  B  5 34TEXT('Ordering type')
     A  31                                  DSPATR(UL)
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A            #0BETY         2A  B  6 34TEXT('Beneficiary type')
     A  31                                  DSPATR(UL)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A            #0OPCO         3A  B  7 34TEXT('IBLC Operation Code')
     A  31                                  DSPATR(UL)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A            #0COCO         2A  B  8 34TEXT('Country Code')
     A  31                                  DSPATR(UL)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A            #0IDCO         2A  B  9 34TEXT('Identification Code')
     A  31                                  DSPATR(UL)
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A            #0IDEN         9A  B 10 34TEXT('Identification Number')
     A  31                                  DSPATR(UL)
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A  31                             23  2'F3=Exit   F5=Refresh'
     A                                      COLOR(BLU)
     A N31                             23  2'ENTER to continue ... '
     A                                      COLOR(BLU)
      *
      *------------------*
      *  Message Format  *
      *------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *-------------------*
      *  Message Control  *
      *-------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(02)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  55
     AON55                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
