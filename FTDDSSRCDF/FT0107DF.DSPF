      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Multiple Authorisation Program DSPF')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FUNDS TRANSFER MODULE                            *
     A*                                                               *
     A*  FT0107DF - MULTIPLE AUTHORISATION PROGRAM                    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01494             DATE 25MAY94               *
     A*  PREV AMEND NO. 061008             DATE 21SEP93               *
     A*                 S01435             DATE 22JUL93               *
     A*                 E39162             DATE 18JUN92               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     F*  S01494 - BLI Funds Transfer Enhancements                     *
     A*  061008 - Missing functionality for FT Cheques processing     *
     A*  S01435 - Incoming Message Management.                        *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
     A*
     A          R FT0107DS                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A*/COPY WNCPYSRC,FT0107DF01
     A*
     A            USRID         10A  O  1  2
     A                                  1 13'FT0107'
     A  30                              1 23'OUTGOING PAYMENTS AWAITING AUTHORI+
     A                                      SATION'
     A                                      DSPATR(UL HI)
     A  31                              1 23'INCOMING PAYMENTS AWAITING AUTHORI+
     A                                      SATION'
     A                                      DSPATR(UL HI)
     A  32                              1 23'CHEQUES TO BE COLLECTED AWAITING A+
     A                                      UTHORISATION'
     A                                      DSPATR(UL HI)
     A  33                              1 23'CHEQUES TO BE PAID AWAITING AUTHOR+
     A                                      ISATION'
     A                                      DSPATR(UL HI)
     A  34                              1 23'NOSTRO TRANSFERS AWAITING AUTHORIS+
     A                                      ATION'
     A                                      DSPATR(UL HI)
     A  35                              1 23'INCOMING PAYMENTS AWAITING LEVEL 2'
     A                                      DSPATR(UL HI)
     A  36                              1 23'OUTGOING PAYMENTS AWAITING LEVEL 2'
     A                                      DSPATR(UL HI)
     A  37                              1 23'CHEQUES TO BE PAID AWAITING LEVEL +
     A                                      2'
     A                                      DSPATR(UL HI)
     A  38                              1 23'OUTGOING PAYMENTS AWAITING COMPLET+
     A                                      ION'
                                            DSPATR(UL HI)
     A* Start S01435
     A  39                              1 23'INCOMING PAYMENTS AWAITING COMPLET+
     A                                      ION'
     A                                      DSPATR(UL HI)
     A* End   S01435
     A* Start 061008
     A  40                              1 23'CHEQUES FOR COLLECTION AWAITING CO+
     A                                      MPLETION'
     A                                      DSPATR(UL HI)
     A  42                              1 23'CHEQUES TO BE PAID AWAITING COMPLE+
     A                                      TION'
     A                                      DSPATR(UL HI)
     A* End   061008
     A********* Start of Change                            /S01494/
     A  43                              1 23'REGULAR PAYMENTS AWAITING AUTHORIS+
     A                                      ATION'
     A                                      DSPATR(UL HI)
     A  44                              1 23'REGULAR PAYMENTS AWAITING LEVEL 2'
     A                                      DSPATR(UL HI)
     A*         End of Change                              /S01494/
     A            BJMRDT         7A  O  1 72
     A            TIME           6Y 0O  2 71EDTWRD('  .  .  ')
     A            WSID          10A  O  2  2
     A                                  8 30'BRANCH CODE :'
     A                                      DSPATR(HI)
     A            SBRCA     R        B  8 46REFFLD(BRCA)
     A                                      DSPATR(UL)
     A  49                                  DSPATR(RI PC)
     A  50                             10 30'B/O         :'
     A                                      DSPATR(HI)
     A  50        SBKOR          1A  B 10 46
     A*/COPY WNCPYSRC,FT0107DF02
     A                                      DSPATR(UL)
     A  51                                  DSPATR(PC RI)
     A  41        ERR1          12A    21  2
     A                                      DSPATR(HI RI)
     A                                 23  2'Press F3 to End'
