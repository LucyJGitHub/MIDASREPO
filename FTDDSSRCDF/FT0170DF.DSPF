      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Cheques For Collection For Sender')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FUNDS TRANSFER MODULE
     A*                                                               *
     A*  FT0170DF  - CHEQUES FOR COLLECTION BY COLLECTING BANK        *
     A*              DISPLAY FILE .                                   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 104633             DATE 15JUL96               *
     A*                 003357             DATE 13MAY93               *
     A*                 S01117             DATE 30JUL91               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA02 - Remove use of LOCK keyword to enable WebFacing      *
      *           to process screens.                                 *
     A*  104633 - GUI Amendments to DSPF with non-standard footers    *
     A*  003357 - Program shows entries for Collecting Bank/value date*
     A*           before the date specified on input screen. Selection*
     A*           process picks up all transactions with a matching   *
     A*           Collecting Bank but as the screen field was         *
     A*           truncated from 35 to 20, the matching process was   *
     A*           not accurate. Need to extend screen field to full   *
     A*           length of 35.                                       *
     A*  S01117 - REWRITTEN FOR RELEASE 10                            *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
                                            INDARA
                                            CHGINPDFT
                                            PRINT(MPRINT)
                                            CA03(01)
                                            CA12(05)
     A  24N21                               CA08
     A  24N25                               ROLLUP(29)
 
                R FT0170F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
                                            SFL
        26                                  SFLNXTCHG
 
                  @DET           1A  B  7  5TEXT('Select payment')
       N50 31                               DSPATR(PC)
       N44                                  DSPATR(HI)
                                            DSPATR(UL)
        44                                  DSPATR(RI)
       N31                                  DSPATR(ND)
       N31                                  DSPATR(PR)
 
                  @SFLIN        58A  O  7  8TEXT('Subfile line of above fields')
                                            DSPATR(HI)
 
                  @SFAUT         3A  O  7 68TEXT('AUTHORITY INDICATOR')
                                            DSPATR(HI)
       N31                                  DSPATR(ND)
 
                  @HBRCA    R        H      REFFLD(BRCA)
                  @HORBR    R        H      REFFLD(ORBR)
 
                R FT0170F1                  TEXT('Screen header')
                                            PUTOVR
                                            OVERLAY
       N28                                  BLINK
      ** Start of CAAA02
      ******************                    LOCK
      ** End   of CAAA02
                                            SFLCTL(FT0170F0)
                                            SFLSIZ(0016)
                                            SFLPAG(0015)
        23                                  SFLCLR
        24                                  SFLDSP
        25                                  SFLEND
        30                                  SFLDSPCTL
       N24                                  ERASE(FT0170F0)
 
     A*  /* Start 003357 */
     A*  /* End   003357 */
     A*  /* Start 003357 */
     A*  /* End   003357 */
     A*
                  @SFREC         4  0H      TEXT('SFLRCDNBR field')
                                            SFLRCDNBR(CURSOR)
 
     A            USRID         10A  O  1  2
     A                                  1 13'FT0170'
     A                                  1 26'CHEQUES FOR COLLECTION BY-
     A                                       COLLECTING BANK'
     A                                      DSPATR(HI UL)
     A            BJMRDT         7A  O  1 72
     A            TIME           6Y 0O  2 71EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            WSID          10A  O  2  2
 
                                        3  4'Currency'
                  @SCCY          3A  B  3 15TEXT('Currency')
                                            OVRDTA
       N51                                  DSPATR(HI)
       N28                                  DSPATR(UL)
        28                                  DSPATR(PR)
        51                                  DSPATR(RI PC)
 
                                        3 19'Collecting Bank'
     A*  /* Start 003357 */
     A************@SDEST********20A  B  3 36TEXT('Destination')
     A**Start of change CGL029
     A**********  @SDEST        35A  B  3 36TEXT('Destination')                               CGL029
     A            @SDEST        41A  B  3 36TEXT('Destination')
     A**End of change CGL029
     A*  /* End   003357 */
                                            OVRDTA
       N52                                  DSPATR(HI)
       N28                                  DSPATR(UL)
        28                                  DSPATR(PR)
        52                                  DSPATR(RI PC)
 
                                        4  4'Authorised'
                  @SAUTH         1A  B  4 15TEXT('Authorised indicator')
                                            OVRDTA
       N53                                  DSPATR(HI)
       N28                                  DSPATR(UL)
        28                                  DSPATR(PR)
        53                                  DSPATR(RI PC)
 
                                        4 19'Start value date'
                  @SVALD         6A  B  4 36TEXT('Start value date')
                                            OVRDTA
       N54                                  DSPATR(HI)
       N28                                  DSPATR(UL)
        28                                  DSPATR(PR)
        54                                  DSPATR(RI PC)
 
     A*  /* Start 003357 */
     A**43******************            3 57'Branch'
     A**43********@BRCA*****R        B  3 64REFFLD(BRCA)
     A  43                              4 44'Branch'
     A  43        @BRCA     R        B  4 51REFFLD(BRCA)
     A*  /* End   003357 */
     A                                      OVRDTA
     A N55                                  DSPATR(HI)
     A N28                                  DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  55                                  DSPATR(RI PC)
     A*
     A*  /* Start 003357 */
     A**43*08***************            4 44'Originating/Booking'
     A**43*08*****@BORO*****     1A  B  4 66TEXT('B or O')
     A  43 08                           4 56'Originating/Booking'
     A  43 08     @BORO          1A  B  4 76TEXT('B or O')
     A*  /* End   003357 */
     A                                      OVRDTA
     A N56                                  DSPATR(HI)
     A N28                                  DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  56                                  DSPATR(RI PC)
                                        6  4'Sel'
                                            OVRATR
       N24                                  DSPATR(ND)
 
                                        6  8'Val date'
                                            OVRATR
       N24                                  DSPATR(ND)
 
                                        6 19'Payment ref'
                                            OVRATR
       N24                                  DSPATR(ND)
 
                                        6 34'Sub'
                                            OVRATR
       N24                                  DSPATR(ND)
 
                                        6 38'Ccy'
                                            OVRATR
       N24                                  DSPATR(ND)
 
                                        6 51'Amount'
                                            OVRATR
       N24                                  DSPATR(ND)
 
                                        6 62'Det'
                                            OVRATR
       N24                                  DSPATR(ND)
 
                                        6 68'Aut'
                                            OVRATR
       N24                                  DSPATR(ND)
 
                R FT0170F2                  TEXT('Screen footer')
      ** Start of CAAA02
      ******************                    LOCK
      ** End   of CAAA02
 
                  @ERR          76   O 22  3TEXT('Error codes')
        50                                  DSPATR(RI)
       N50                                  DSPATR(ND)
 
      ******                                           Start  /104633/
     A******                           23  2'F3-End, F12 for init+
      ******                                ial screen'
     A                                 23  2'F3=Exit   F12=Previous'
      ******                                            End   /104633/
     A N60N18N20
     AA 43
     AON60N18N43
     AO 20N60N18
      ******                                           Start  /104633/
     A*AN19                            23 33', F8 for next Collecting Bank'
     AAN19                             23 27'F8=Next Collecting Bank'
      ******                                            End   /104633/
     A N24
     AO 21                                  DSPATR(ND)
     A  60N43
     AO 20 60N19
      ******                                           Start  /104633/
     A*O 60N20 43                       23 33', F8 for next currency'
     AO 60N20 43                       23 27'F8=Next currency'
      ******                                            End   /104633/
     A N24
     AO 21                                  DSPATR(ND)
     A  18N20
      ******                                           Start  /104633/
     A*O 20 18                         23 33', F8 next currency/Collecting Bank'
     AO 20 18                          23 27'F8=Next currency/Collecting Bank'
      ******                                            End   /104633/
     A N24
     AO 21                                  DSPATR(ND)
      ******                                           Start  /104633/
     A* 20 19                          23 33', F8 for next Branch'
     A  20 19                          23 27'F8=Next Branch'
      ******                                            End   /104633/
     A N24
     AO 21                                  DSPATR(ND)
 
                                       24  2'Rollup/Rolldown for next/previous -
                                            screen'
       N24                                  DSPATR(ND)
 
