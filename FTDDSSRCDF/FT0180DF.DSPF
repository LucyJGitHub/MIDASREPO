      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Incoming Pay'ts Waiting Complet'n')
     A*****************************************************************
     A*                                                               *
     A*  Midas   Funds Transfer Module
     A*                                                               *
     A*  FT0180DF  - Incoming Payments Awaiting Completion            *
     A*              Display File .                                   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 S01435             DATE 22JUL93               *
     A*                                                               *
     A*****************************************************************
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAAA02 - Remove use of LOCK keyword to enable WebFacing      *
      *           to process screens.                                 *
     A*  S01435 - Incoming Message Management.                        *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A
     A                                      CA03(01)
     A  10                                  CA12(01)
     A
     A  24N25                               ROLLUP(29)
     A
     A          R FT0180F0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  26                                  SFLNXTCHG
     A
      **Start of change CGL029
     A**********  @DET           1A  B  5  2TEXT('Complete payment')                          CGL029
     A            @DET           1A  B  6  2TEXT('Complete payment')
      **End of change CGL029
     A N44                                  DSPATR(HI)
     A N28                                  DSPATR(UL PC)
     A  28                                  DSPATR(PR)
     A  44                                  DSPATR(RI)
     A  45                                  DSPATR(ND)
     A
     A
      **Start of change CGL029
     A**********  @SFLIN        73A  O  5  4TEXT('Subfile line of above fields')              CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A            @SFLN1        68A  O  6  4DSPATR(HI)
     A            @SFLN2        22A  O  7  6DSPATR(HI)
      **End of change CGL029
     A
     A            @HBRCA    R        H      REFFLD(BRCA)
     A            @HORBR    R        H      REFFLD(ORBR)
     A
     A          R FT0180F1                  TEXT('Screen header')
     A                                      PUTOVR
     A                                      OVERLAY
     A N28                                  BLINK
      ** Start of CAAA02
     A******************                    LOCK
      ** End   of CAAA02
     A                                      SFLCTL(FT0180F0)
      **Start of change CGL029
     A**********                            SFLSIZ(0018)                                      CGL029
     A**********                            SFLPAG(0017)                                      CGL029
     A                                      SFLSIZ(9)
     A                                      SFLPAG(8)
      **End of change CGL029
     A  23                                  SFLCLR
     A  24                                  SFLDSP
     A  25                                  SFLEND
     A  30                                  SFLDSPCTL
     A N24                                  ERASE(FT0180F0)
     A
     A
     A            @SFREC         4  0H      TEXT('SFLRCDNBR field')
     A                                      SFLRCDNBR(CURSOR)
     A
     A            @USR          10A  O  1  2
     A                                  1 13'FT0180'
     A                                  1 25'INCOMING PAYMENTS AWAIT+
     A                                      ING COMPLETION'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A
     A            SRUNA          7A  O  1 72
     A            TIME           6Y 0O  2 71EDTWRD('  .  .  ')
     A                                      OVRATR
     A            @JOB          10A  O  2  2
     A
     A  11 10                           3  4'Originating Branch'
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A
     A N11 10                           3  4'Booking Branch'
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A
     A  10        @BRCH     R        B  3 23OVRDTA  REFFLD(BRCA)
     A                                      DSPATR(HI)
     A                                      DSPATR(PR UL)
     A
     A  10        @BRNM         35A  B  3 27OVRDTA
     A                                      DSPATR(HI)
     A                                      DSPATR(PR)
     A N15                                  DSPATR(UL)
     A
     A N16                              4  1'Sel'
     A                                  4  6'Payment ref'
     A                                  4 23'Sender'
      **Start of change CGL029
     A**********                        4 34'Val date'                                        CGL029
     A**********                        4 43'Ccy'                                             CGL029
     A**********                        4 55'Amount'                                          CGL029
     A**********                        4 65'A/c with/ben'                                    CGL029
     A                                  4 42'Val date'
     A                                  4 51'Ccy'
     A                                  4 62'Amount'
     A                                  5  6'A/c with Beneficiary'
      **End of change CGL029
     A
     A          R FT0180F2                  TEXT('Screen footer')
     A                                      PUTOVR
      ** Start of CAAA02
     A******************                    LOCK
      ** End   of CAAA02
     A
     A            @ERR          76   O 22  3TEXT('Error codes')
     A                                      OVRDTA
     A  50                                  DSPATR(RI)
     A N50                                  DSPATR(ND)
     A
     A                                 24  2'F3 to End,'
     A  10                             24 13'F12 for previous screen'
     A                                       OVRATR
     A  24 10                          24 40'Rollup/Rolldown'
     A                                       OVRATR
     A                                 24 13'Rollup/Rolldown for next/previous -
     A                                      screen'
     A                                      OVRATR
     A  10
     AON24                                  DSPATR(ND)
