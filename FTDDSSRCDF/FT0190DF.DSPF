      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Cheques to be Paid Waiting Complet'n')
     A*****************************************************************
     A*                                                               *
     A*  Midas   Funds Transfer Module
     A*                                                               *
     A*  FT0190DF  - Cheques to be Paid Awaiting Completion           *
     A*              Display File.                                    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 068813             Date 26Feb96               *
     A*                 061008             DATE 21SEP93               *   S01117
     A*                                                               *
     A*****************************************************************
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA02 - Remove use of LOCK keyword to enable WebFacing      *
      *           to process screens.                                 *
     A*  068813 - Help text tags wrong                                *
     A*  061008 - Missing functionality for FT Cheques processing     *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      CA03(01)
     A  40                                  CA12(01)
     A
     A  24N25                               ROLLUP(29)
     A
     A*                                                    /*068813*/
     A*                                                    /*068813*/
     A
     A          R FT0190F0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  26                                  SFLNXTCHG
     A
     A            @DET           1A  B  5  2TEXT('Select payment')
     A N44                                  DSPATR(HI)
     A N28                                  DSPATR(UL PC)
     A  28                                  DSPATR(PR)
     A  44                                  DSPATR(RI)
     A  45                                  DSPATR(ND)
     A
     A            @SFLIN        76A  O  5  4
     A                                      DSPATR(HI)
     A            @HBRCA    R        H      REFFLD(BRCA)
     A            @HORBR    R        H      REFFLD(ORBR)
     A
     A          R FT0190F1                  TEXT('Screen header')
     A                                      PUTOVR
     A                                      OVERLAY
     A N28                                  BLINK
      ** Start of CAAA02
     A******************                    LOCK
      ** End   of CAAA02
     A                                      SFLCTL(FT0190F0)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A  23                                  SFLCLR
     A  24                                  SFLDSP
     A  25                                  SFLEND
     A  30                                  SFLDSPCTL
     A N24                                  ERASE(FT0190F0)
     A
     A*                                                    /*068813*/
     A*                                                    /*068813*/
     A
     A            @SFREC         4  0H      TEXT('SFLRCDNBR field')
     A                                      SFLRCDNBR(CURSOR)
     A
     A            @USR          10A  O  1  2
     A                                  1 13'FT0190'
     A                                  1 27'CHEQUES TO BE PAID AWAI+
     A                                      TING COMPLETION'
     A                                      DSPATR(HI UL)
     A            SRUNA          7A  O  1 72
     A            TIME           6Y 0O  2 71EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            @JOB          10A  O  2  2
     A
     A  40 41                           3  4'Originating branch'
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A
     A  40 42                           3  4'Booking branch'
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A
     A  40        @BRCH     R        B  3 23OVRDTA  REFFLD(BRCA)
     A                                      DSPATR(HI)
     A                                      DSPATR(PR UL)
     A
     A  40        @BRNM         35A  B  3 27OVRDTA
     A                                      DSPATR(HI)
     A                                      DSPATR(PR)
     A N43                                  DSPATR(UL)
     A
     A N16                              4  1'Sel'
     A                                  4  5'Payment ref'
     A                                  4 21'Remitting Bank'
     A                                  4 47'Val date'
     A                                  4 56'Ccy'
     A                                  4 67'Amount'
     A                                  4 77'Setl'
     A
     A          R FT0190F2                  TEXT('Screen footer')
     A                                      PUTOVR
      ** Start of CAAA02
     A******************                    LOCK
      ** End   of CAAA02
     A
     A            @ERR          76   O 22  3TEXT('Error codes')
     A                                      OVRDTA
     A  50                                  DSPATR(RI)
     A N50                                  DSPATR(ND)
     A
     A                                 24  2'F3 to End,'
     A  40                             24 13'F12 for previous screen'
     A                                       OVRATR
     A  24 40                          24 40'Rollup/Rolldown'
     A                                       OVRATR
     A                                 24 13'Rollup/Rolldown for next/previous -
     A                                      screen'
     A                                      OVRATR
     A  40
     AON24                                  DSPATR(ND)
