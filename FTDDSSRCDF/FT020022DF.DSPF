      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Element Data Enquiry')                        *
      *****************************************************************
      *                                                               *
      *  Midas - MX Element Data Enquiry                              *
      *                                                               *
      *  DSPF/FT20022DF - MX Data Element Enquiry                     *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. CSW122  *CREATE    Date 04Oct21               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW122 - SWIFT ISO 20022                                     *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      CA03
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R FT020022F1                TEXT('(c) Finastra International +
     A                                      Limited 2021')
     A            SUSER         10   O  1  2
     A            SPGM          10   O  1 13
     A            SRUNA          7   O  1124
     A            SWSID         10   O  2  2
     A                                  2 59'MX Element Data'
     A                                      DSPATR(HI)
     A                                  2123TIME
     A                                  4  6'Payment Reference'
     A  77                              4 70'UETR'
     A            SPREF         15A  O  4 25
     A                                      DSPATR(HI)
     A  77        SUETR         36A  O  4 75
     A                                      DSPATR(HI)
     A  77                              5 70'Message ID'
     A  77        SMSGID        36A  O  5 81
     A                                      DSPATR(HI)
      *
     A          R FT020022S0                SFL
     A            S0ELEM        60   O  8  6
     A            S0EVAL        60   O  8 70
     A                                      DSPATR(HI)
      *
     A          R FT020022C0                TEXT('Subfile control')
     A                                      SFLCTL(FT020022S0)
     A                                      SFLPAG(17) SFLSIZ(18)
     A                                      BLINK OVERLAY PUTOVR
     A  30                                  SFLDSPCTL
     A  31                                  SFLDSP
     A N30N31                               SFLCLR
     A  32                                  SFLEND
     A N32                                  ROLLUP(39)
      *
     A            C0SFRC         4  0H      SFLRCDNBR
     A                                  7  6'Element Name'
     A                                  7 70'Element Value'
      *
     A          R FT020022F2                TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 27  2'F3=Exit F12=Cancel'
      *
     A          R FT020022F3                TEXT('No Data')
     A                                      OVERLAY PUTOVR
     A                                 11 53'*** NO DATA TO DISPLAY ***'
     A                                      DSPATR(HI)
     A                                 27  2'F3=Exit F12=Cancel'
      *
     A          R FT020022M0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R FT020022C2                TEXT('Program messages')
     A                                      SFLCTL(FT020022M0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
