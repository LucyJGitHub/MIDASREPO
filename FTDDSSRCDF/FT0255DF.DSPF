      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Incoming by Sender Prompt Program')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FUNDS TRANSFER MODULE
     A*                                                               *
     A*  FT0255DF - INCOMING PAYMENTS BY SENDER PROMPT SCREEN         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 112563             DATE 28JAN97               *
     A*  PREV AMEND NO. 000000             DATE DDMMMYY               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      CA03
     A  12                                  CA10
     A                                      CA12
     A*
     A*
     A          R FT0255DS                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PUTOVR
     A            USRID         10A  O  1  2TEXT('User ID')
     A                                  1 13'FT0255'
     A                                  1 23'INCOMING PAYMENTS BY SENDER R+
     A                                      EPORT PROMPT'
     A                                      DSPATR(UL HI)
     A            SRUNA          7A  O  1 72TEXT('Todays Run date')
     A            TIME           6Y 0O  2 71EDTWRD('  .  .  ')
     A                                      OVRDTA
     A            WSID          10A  O  2  2TEXT('Workstation ID')
     A                                  6 29'SELECT OPTION - Enter Y'
     A                                      DSPATR(HI)
     A  11                                  DSPATR(ND)
     A                                 10 29'All Payments:'
     A                                      DSPATR(HI)
     A            SALLP          1A  B 10 53
     A                                      DSPATR(UL)
     A  11                                  DSPATR(PR)
     A  30                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 13 29'Authorised Only:'
     A                                      DSPATR(HI)
     A            SAUTH          1A  B 13 53
     A                                      DSPATR(UL)
     A  11                                  DSPATR(PR)
     A  31                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 16 29'Non-authorised Only:'
     A                                      DSPATR(HI)
     A            SNAUT          1A  B 16 53
     A                                      DSPATR(UL)
     A  11                                  DSPATR(PR)
     A  32                                  DSPATR(PC RI)
     A                                      OVRDTA
     A            ERCD          76A  O 21  2TEXT('Error codes')
     A                                      OVRDTA
     A  13                                  DSPATR(HI RI)
     A**                                             Begin /112563/
     A***********                      23  2'Press F3 to end,'
     A*N12 ******                      23 19'or F12 for initial screen'
     A* 12 ******                      23 19'F10 to delete,'
     A* 12 ******                      23 34'or F12 for initial screen'
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Initial screen'
     A  12                             23 31'F10=Delete'
     A**                                             End   /112563/
