      **************************************************************
/*STD *  DSPFBASE                                                  *
/*EXI *  TEXT('Midas FT Batch Authorisation Prompt')
      **************************************************************
      *                                                            *
      *   Midas - Funds Transfer Module                            *
      *                                                            *
      *   DSPF/FT0801DF - Batch Authorisation Prompt               *
      *                                                            *
      *  (c) Finastra International Limited 2001                      *
      *                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CFT003  *CREATE    Date 05May98               *
      *                                                            *
      *------------------------------------------------------------*
      *                                                            *
      *  MD046248 - Finastra Rebranding                               *
      *    CFT003 - FT Batch Authorisation                         *
      *             Based on FTH003DF original references          *
      *             HUB01B/IMP066                                  *
      *                                                            *
      **************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      CA03
      *
     A          R INITSCN                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      CA05
      *
     A            PGMUSR        10   O  1  2
     A                                  1 13'FT0801'
     A            RDAT           7   O  1 72
     A            PGMDEV        10   O  2  2
     A                                  1 27'FT PAYMENTS BATCH AUTHORISATION'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *
     A                                  4  7'Report/Authorise'
     A            SRPAUT         1   B  4 24
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(UL HI)
      *
     A                                  4 38'Auto-Gen Transaction (Y/N)'
     A            SAUTOG         1   B  4 65
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(UL HI)
      *
     A                                  6  7'Transaction reference number range'
     A                                  6 42'- from'
     A            SFMTRN        15   B  6 49
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A                                  7 44'to'
     A            STOTRN        15   B  7 49
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
      *
     A                                  9  7'Settle Value Date from'
     A            SSFMDT         6   B  9 31
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
      *
     A                                  9 41'Pay Value Date from'
     A            SPFMDT         6   B  9 62
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
      *
     A                                 10 26'to'
     A            SSTODT         6   B 10 31
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
      *
     A                                 10 56'to'
     A            SPTODT         6   B 10 62
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
      *
     A                                 12 13'Payment Type'
     A            SPYTYP         2   B 12 31
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
      *
     A                                 12 41'Payment Subtype'
     A            SPSTYP         2   B 12 62
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
      *
     A                                 13 13'Settle Currency'
     A            SSCURR         3   B 13 31
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
      *
     A                                 13 41'Pay Currency'
     A            SPCURR         3   B 13 62
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
      *
     A                                 14 13'Nostro Code'
     A            SNSCDE         5   B 14 31
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
      *
     A                                 15 13'Settlement Method'
     A            SSETMD         2   B 15 31
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
      *
     A                                 15 41'Settlement Period'
     A            SCPIND         1   B 15 62
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(UL HI)
     A                                 17 13'Operator'
     A            SOPER1        10   B 17 31
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(UL HI)
     A            SOPER2        10   B 17 47
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(UL HI)
     A            SOPER3        10   B 17 62
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(UL HI)
     A            SOPER4        10   B 18 31
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(UL HI)
     A            SOPER5        10   B 18 47
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(UL HI)
      *
     A                                 23  2'F3=Exit   F5=Refresh'
      *
     A          R CONFSCN                   OVERLAY
     A                                      CA06
     A                                      CA12
      *
     A            PGMUSR        10   O  1  2
     A                                  1 13'FT0801'
     A            RDAT           7   O  1 72
     A            PGMDEV        10   O  2  2
     A                                  1 27'FT PAYMENTS BATCH AUTHORISATION'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *
     A                                  4  7'Report/Authorise'
     A            SRPAUT         1   O  4 24
     A                                      DSPATR(HI)
      *
     A                                  4 38'Auto-Gen Transaction (Y/N)'
     A            SAUTOG         1   O  4 65
     A                                      DSPATR(HI)
      *
     A                                  6  7'Transaction reference number range'
     A                                  6 42'- from'
     A            SFMTRN        15   O  6 49
     A                                      DSPATR(HI)
     A                                  7 44'to'
     A            STOTRN        15   O  7 49
     A                                      DSPATR(HI)
      *
     A                                  9  7'Settle Value Date from'
     A            SSFMDT         6   O  9 31
     A                                      DSPATR(HI)
      *
     A                                  9 41'Pay Value Date from'
     A            SPFMDT         6   O  9 62
     A                                      DSPATR(HI)
      *
     A                                 10 26'to'
     A            SSTODT         6   O 10 31
     A                                      DSPATR(HI)
      *
     A                                 10 56'to'
     A            SPTODT         6   O 10 62
     A                                      DSPATR(HI)
      *
     A                                 12 13'Payment Type'
     A            SPYTYP         2   O 12 31
     A                                      DSPATR(HI)
      *
     A                                 12 41'Payment Subtype'
     A            SPSTYP         2   O 12 62
     A                                      DSPATR(HI)
      *
     A                                 13 13'Settle Currency'
     A            SSCURR         3   O 13 31
     A                                      DSPATR(HI)
      *
     A                                 13 41'Pay Currency'
     A            SPCURR         3   O 13 62
     A                                      DSPATR(HI)
      *
     A                                 14 13'Nostro Code'
     A            SNSCDE         5   O 14 31
     A                                      DSPATR(HI)
      *
     A                                 15 13'Settlement Method'
     A            SSETMD         2   O 15 31
     A                                      DSPATR(HI)
      *
     A                                 15 41'Settlement Period'
     A            SCPIND         1   O 15 62
     A                                      DSPATR(HI)
     A                                 17 13'Operator'
     A            SOPER1        10   O 17 31
     A                                      DSPATR(HI)
     A            SOPER2        10   O 17 47
     A                                      DSPATR(HI)
     A            SOPER3        10   O 17 62
     A                                      DSPATR(HI)
     A            SOPER4        10   O 18 31
     A                                      DSPATR(HI)
     A            SOPER5        10   O 18 47
     A                                      DSPATR(HI)
      *
     A                                 23  2'F3=Exit   F6=Confirm   +
     A                                      F12=Cancel'
      *
      **  Message subfile Record format
      *
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
      *
      ** Message Subfile Control Record format
      *
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
