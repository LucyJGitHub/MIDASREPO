      **************************************************************
/*STD *: DSPFBASE                                                   : *
/*EXI *: GENLVL(21)                                                 : *
/*EXI *  TEXT('MIDAS FT Batch Authorisation Prompt Window')
     A****************************************************************
      *                                                            *
      *   Midas - Funds Transfer Module                            *
      *                                                            *
      *   DSPF/FT0802DF - Batch Authorisation Prompt Window        *
      *                                                            *
      *  (c) Finastra International Limited 2001                      *
      *                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CFT003  *CREATE    Date 05May98               *
      *                                                            *
      *------------------------------------------------------------*
      *                                                            *
      *  MD046248 - Finastra Rebranding                               *
      *    CFT003 - FT Batch Authorisation                         *
      *             Based on FTH003#0 original references          *
      *             HUB01B/IMP066                                  *
      *                                                            *
      **************************************************************
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A
     A          R WINDOWF                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      KEEP
     A                                  8 25'.................................'
     A                                      OVRATR
     A                                      OVRDTA
     A                                  9 25':                               :'
     A                                      OVRATR
     A                                      OVRDTA
     A                                 10 25':    Number of transactions     :'
     A                                      OVRATR
     A                                      OVRDTA
     A                                 11 25':    included in this batch     :'
     A                                      OVRATR
     A                                      OVRDTA
     A                                 12 25':    authorisation:'
     A                                      OVRATR
     A                                      OVRDTA
     A            SNOTRN         5  0O 12 45OVRDTA
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A                                 12 51'      :'
     A                                      OVRATR
     A                                      OVRDTA
     A                                 13 25':                               :'
     A                                      OVRATR
     A                                      OVRDTA
     A                                 14 25':    Confirm :'
     A                                      OVRATR
     A                                      OVRDTA
     A            SCNFRM         1A  B 14 40OVRDTA
     A                                      OVRATR
     A N51                                  DSPATR(UL HI)
     A  51                                  DSPATR(RI PC)
     A                                 14 42' (Y/N)         :'
     A                                      OVRATR
     A                                      OVRDTA
     A                                 15 25':                               :'
     A                                      OVRATR
     A                                      OVRDTA
     A                                 16 25':...............................:'
     A                                      OVRATR
     A                                      OVRDTA
     A*=====================================================================
     A*  Format DUMMY is not used. It contains the ASSUME keyword          =
     A*   which must be in at least one format when the WINDOW             =
     A*   keyword is used.                                                 =
     A*=====================================================================
     A          R DUMMY                      TEXT('Dummy format.')
     A*
     A                                       OVERLAY PUTOVR ASSUME
     A            DUMMYF         1A  O  1 80 DSPATR(ND)
     A*=====================================================================
     A
     A          R #MSGRCD                   TEXT('PROGRAM MESSAGES')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A
     A          R #MSGCTL                   TEXT('PROGRAM MESSAGES')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  47
     AON47                                  SFLEND
     A            ##PGM                     SFLPGMQ
