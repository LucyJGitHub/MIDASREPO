      **************************************************************
/*STD *  DSPFBASE                                                  *
/*EXI *  TEXT('Midas FT De-select by user')
      **************************************************************
      *                                                            *
      *  Midas - Funds Transfer Module                             *
      *                                                            *
      *  DSPF/FT0808DF - Batch Authorisation De-Select a Batch     *
      *                  Prompt                                    *
      *                                                            *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CFT003  *CREATE  Date 24APR98              *
      *                                                            *
      *------------------------------------------------------------*
      *                                                            *
      *    CFT003 - FT Batch Authorisation                         *
      *             Based on FTH008DF original references          *
      *             HUB01B/IMP066                                  *
      *                                                            *
      **************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      CA03
     A  11                                  CA06
     A  11                                  CA12
      *
     A          R INITSCN                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            PGMUSR        10   O  1  2
     A                                  1 13'FT0808'
     A            RDAT           7   O  1 72
     A            PGMDEV        10   O  2  2
     A                                  1 27'EXCLUDE PAYMENTS FOR AUTHORISATION'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
     A          R PROMPT                    OVERLAY
     A                                  8 27'Authorising User Name :'
     A            SUSER         10   B  8 51
     A N10                                  DSPATR(HI UL)
     A  10N11                               DSPATR(RI PC)
     A  11                                  DSPATR(PR)
      *
     A          R FOOTER                    OVERLAY
     A N11                             23  2'F3=Exit'
     A  11                             23  2'F3=Exit   F6=Confirm   F12=Init+
     A                                       ial Screen'
     A          R MSGDSP                    OVERLAY
     A                                 12 27'Transactions previously selected '
     A                                 13 27'by this user will be excluded in '
     A                                 14 27'   the Authorisation process.'
      *-------------------------------------------------------------------------
      * Message Subfile Record format
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
      *-------------------------------------------------------------------------
      * Message Subfile Control Record format
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
