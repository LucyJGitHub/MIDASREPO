     A*****************************************************************
/*STDA*  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas FT BIC plus enquiry')                            *
     A*****************************************************************
     A*                                                               *
     A*  Midas   FUNDS TRANSFER MODULE                                *
     A*                                                               *
     A*  FT0900DF  - BIC Plus enquiry                                 *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  Last Amend No. MD035218           Date 29Jul15               *
      *  Prev Amend No. MD023143A          Date 06Nov13               *
      *                 MD023143           Date 30Oct13               *
      *                 MD021562           Date 22Aug13               *
      *                 CFT050             Date 03Jun13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
     A*                 CFT007  *CREATE    Date 25Oct99               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD035218 - Incorrect message on SWIFT/SWIFTRef 2014          *
      *             related screens (Recompile)                       *
      *  MD023143A - 1 = Select should be displayed in the Bank direc-*
      *             tory Plus Search Screen of CUAP (Reopen)          *
      *  MD023143 - 1 = Select should be displayed in the Bank direc- *
      *             tory Plus Search Screen of CUAP                   *
      *  MD021562 - Question mark character is present beside         *
      *             Institution label                                 *
     A*  CFT050 - SWIFTRef Directories upload                         *
     A*  CFT007 - BIC Database Plus Integration                       *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03
     A                                      CA12
     A                                      CF18
     A  24N25                               ROLLUP(29)
     A*=========================================================================
     A* SUBFILE CONTROLE
     A*=========================================================================
     A          R FT0900F0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A
     A            @0SEL          1   B 12  2TEXT('Select')
     A                                      DSPATR(HI UL)
     A                                      VALUES('A' 'B' 'C' 'D' +
      **Start of change MD023143
     A**********                                   'E' 'X' ' ')                             MD023143
     A                                             'E' 'X' ' ' '1')
      **End of change MD023143
     A            @0INST        50   O 12  5
     A                                      DSPATR(HI)
     A            @0NATI        15   O 12 56
     A                                      DSPATR(HI)
     A            @0LOCB        35   O 13  5
     A                                      DSPATR(HI)
      **Start of change CFT050
     A**********  @0BIC         11   O 13 42                                                  CFT050
     A            @0BIC         11   O 13 41
      **End of change CFT050
     A                                      DSPATR(HI)
      **Start of change CFT050
     A**********  @0CHIP         6   O 13 55                                                  CFT050
     A            @0CHIP         6   O 13 53
      **End of change CFT050
     A                                      DSPATR(HI)
      **Start of change CFT050
     A            @0LEIC        20   O 13 60
     A  75                                  DSPATR(HI)
     A N75                                  DSPATR(ND)
      **End of change CFT050
     A* HIDDEN FIELDS
     A            @0HBRC        70   H      TEXT('Branch')
     A            @0HLOC        90   H      TEXT('Location')
     A            @0HINS       105   H      TEXT('Institution')
     A            @0HCIT        35   H      TEXT('City')
     A            @0HCNT        70   H      TEXT('Country')
     A            @0HADD       140   H      TEXT('Address')
     A            @0HZIP        15   H      TEXT('ZIP')
     A            @0HPNB        35   H      TEXT('PO Box Number')
     A            @0HPZP        15   H      TEXT('PO Box ZIP')
     A            @0HPLC        90   H      TEXT('PO Box Location ')
      **Start of change CFT050
     A            @0HKEY        12   H      TEXT('Record Key      ')
      **End of change CFT050
     A
     A
     A*=========================================================================
     A* SUBFILE
     A*=========================================================================
     A          R FT0900F1                  SFLCTL(FT0900F0)
     A                                      TEXT('Screen header')
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                      SFLCSRRRN(&@SFPOS)
     A  24                                  SFLDSP
     A  30                                  SFLDSPCTL
     A  23                                  SFLCLR
     A  25                                  SFLEND
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A            @SFREC         4S 0H      TEXT('SFLRCDNBR field')
     A                                      SFLRCDNBR(CURSOR)
     A            @SFPOS         5S 0H
      **Start of change MD021562
     A**********                       10  2'?'                                             MD021562
      **End of change MD021562
     A                                 10  5'Institution'
     A                                 10 56'National Identifier'
     A            @LOCBR         8A  O 11  5
      **Start of change CFT050
     A**********                       11 42'BIC Code'                                        CFT050
     A**********                       11 55'CHIPS Identifier'                                CFT050
     A                                 11 41'BIC Code'
     A                                 11 53'CHIPS'
     A  75                             11 60'LEI Code'
      **End of change CFT050
     A*=========================================================================
     A* SELECTION SCREEN
     A*=========================================================================
     A*
     A          R FT0900F2
     A                                      OVERLAY
     A* Screen HEADER
     A* -----------
     A            ##USR         10A     1  2
     A                                      DSPATR(HI)
     A                                  1 13'FT0900'
     A                                      DSPATR(HI)
      **Start of change CFT050
     A**********                        1 30'BIC Database Plus Search'                        CFT050
     A**********                            DSPATR(HI UL)                                     CFT050
     A N75                              1 30'BIC Database Plus Search'
     A N75                                  DSPATR(HI UL)
     A  75                              1 29'BANK DIRECTORY Plus Search'
     A  75                                  DSPATR(HI UL)
      **End of change CFT050
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A            ##TIME         6Y 0O  2 71EDTWRD('  .  .  ')
     A                                      DSPATR(HI)
     A* Screen text
     A* -----------
     A                                  4  2'Select on: Search criteria'
     A                                  5  2'Or:        Institution    '
     A N40                              6  2'           Location'
     A  40                              6  2'           Branch'
     A                                  7  2'           S.W.I.F.T BIC'
      **Start of change CFT050
     A  75                              7 45'LEI  '
      **End of change CFT050
     A                                  8  2'           National Id'
     A                                  8 45'CHIPS'
     A            @2CRIV        35A  B  4 29TEXT('Search Criteria')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            @2INSV        35A  B  5 29TEXT('Institution')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            @2LBRV        35A  B  6 29TEXT('Location/Branch')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            @2BICV        11A  B  7 29TEXT('SWIFT BIC')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      **Start of change CFT050
     A  75        @2LEIC        20A  B  7 51TEXT('LEI Code')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      **End of change CFT050
     A            @2NATV        15A  B  8 29TEXT('National Id')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            @2CHPV         6A  B  8 51TEXT('CHIPS')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*=========================================================================
     A* BIC+ DETAILS
     A*=========================================================================
     A*
     A          R FT0900F3
     A                                      PUTOVR
     A                                      OVRATR
     A                                      OVRDTA
     A* Screen HEADER
     A* -----------
     A            ##USR         10A     1  2
     A                                      DSPATR(HI)
     A                                  1 13'FT0900'
     A                                      DSPATR(HI)
     A                                  1 30'BIC Database Plus Search'
     A                                      DSPATR(HI UL)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A            ##TIME         6Y 0O  2 71EDTWRD('  .  .  ')
     A                                      DSPATR(HI)
     A* Screen text
     A* -----------
     A                                  3  2'Institution'
     A                                  7  2'Branch'
     A                                  9  2'Address'
     A                                 13  2'City'
     A                                 13 48'ZIP'
     A                                 15  2'Country'
     A                                 17  2'S.W.I.F.T. BIC    '
     A                                 18  2'National bank code'
     A                                 19  2'CHIPS code        '
     A                                 21  2'PO Box'
     A                                 21 48'ZIP'
     A                                 22  2'Location'
     A                                 23  2'F3=Exit F12=Previous'
     A            @3INS1        70   O  4 11TEXT('Institution line 1')
     A                                      DSPATR(HI)
     A            @3INS2        35   O  5 11TEXT('Institution line 2')
     A                                      DSPATR(HI)
     A            @3BRCA        70   O  7 11TEXT('Branch line 1')
     A                                      DSPATR(HI)
     A            @3ADD1        35   O  9 11TEXT('Address line 1')
     A                                      DSPATR(HI)
     A            @3ADD2        35   O 10 11TEXT('Address line 2')
     A                                      DSPATR(HI)
     A            @3ADD3        35   O 11 11TEXT('Address line 3')
     A                                      DSPATR(HI)
     A            @3ADD4        35   O 12 11TEXT('Address line 4')
     A                                      DSPATR(HI)
     A            @3CITY        35   O 13 11TEXT('City')
     A                                      DSPATR(HI)
     A            @3ZIP1        15   O 13 53TEXT('ZIP Code')
     A                                      DSPATR(HI)
     A            @3CNTY        70   O 15 11TEXT('Country line 1')
     A                                      DSPATR(HI)
     A            @3BIC         11   O 17 21TEXT('BIC')
     A                                      DSPATR(HI)
     A            @3NATI        11   O 18 21TEXT('National bank code')
     A                                      DSPATR(HI)
     A            @3CHIP         6   O 19 21TEXT('CHIPS')
     A                                      DSPATR(HI)
     A            @3POBN        35   O 21 11TEXT('PO Box number')
     A                                      DSPATR(HI)
     A            @3PZIP        15   O 21 53TEXT('PO Box ZIP')
     A                                      DSPATR(HI)
     A            @3PLOC        70   O 22 11TEXT('PO Box Location line 1')
     A                                      DSPATR(HI)
     A*=========================================================================
     A* ' No record ' Format
     A*================================================================
     A*
     A          R FTNORECD
     A                                      PUTOVR
     A                                      OVRATR
     A                                      OVRDTA
     A* Screen text
     A* -----------
     A                                 15 21'No record matches the selection +
     A                                      criteria'
     A                                      DSPATR(HI)
     A*================================================================
     A* Sreen Footer
     A*================================================================
     A          R FTFOOTER                  TEXT('Screen footer')
     A                                      OVERLAY
     A*                                     OVRDTA
     A*                                     OVRATR
     A*                                     PUTOVR
     A*                                     PUTOVR
     A*                                     OVRATR
     A*                                     OVRDTA
     A
     A  41N42                          22  2'A=BIC B=Branch C=Clearing code D=A+
     A                                      ddress E=Enquire X=Default'
      **Start of change MD023143
     A**41*42***                       22  2'E=Enquiry '                                    MD023143
     A  41 42N76                       22  2'E=Enquiry '
      **Start of change MD023143A
     A**41*42*76                       22  2'E=Enquiry 1=Link'                             MD023143A
     A  41 42 76                       22  2'E=Enquiry 1=Select'
      **End of change MD023143A
      **End of change MD023143
     A N40                             23  2'F3=Exit F12=Previous F18=Branch'
     A  40                             23  2'F3=Exit F12=Previous F18=Location'
     A*=========================================================================
     A* Message record format
     A*================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A* Message control format
     A*================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  23                                  SFLCLR
     A*                                     PUTOVR
     A*                                     OVRDTA
     A*                                     OVRATR
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
