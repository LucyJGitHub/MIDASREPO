      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Manual repair of reformatted address')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Funds Transfer Module.                               *
     A*                                                               *
     A*   DSPF/FT0930DF - Manual repair of reformatted address        *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CFT007  *CREATE    Date 21Oct99               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CFT007 - BIC Database Plus Integration                       *
     A*                                                               *
     A*****************************************************************
      *================================================================
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Key screen.')
     A                                      HOME(30 'Reset.')
     A                                      PUTOVR
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'FT0930'
     A                                      DSPATR(HI)
     A                                  1 26'Constructed Address Adjustment'
     A                                      DSPATR(HI)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  2'Constructed address:'
     A                                      DSPATR(UL)
     A                                  5  2'Line        1:'
     A            #1ADD1        35   B  5 17TEXT('Address line 1')
     A                                      OVRDTA
     A                                      DSPATR(UL HI)
     A                                  6  2'Line        2:'
     A            #1ADD2        35   B  6 17TEXT('Address line 2')
     A                                      OVRDTA
     A                                      DSPATR(UL HI)
     A                                  7  2'Line        3:'
     A            #1ADD3        35   B  7 17TEXT('Address line 3')
     A                                      OVRDTA
     A                                      DSPATR(UL HI)
     A                                  8  2'Line        4:'
     A            #1ADD4        35   B  8 17TEXT('Address line 4')
     A                                      OVRDTA
     A                                      DSPATR(UL HI)
     A                                 10  2'S.W.I.F.T. database details:'
     A                                      DSPATR(UL)
     A                                 11  2'Institution 1:'
     A            #1INS1        35   O 11 17TEXT('Institution line 1')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 12  2'            2:'
     A            #1INS2        35   O 12 17TEXT('Institution line 2')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 13  2'            3:'
     A            #1INS3        35   O 13 17TEXT('Institution line 3')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 14  2'Street      1:'
     A            #1STR1        35   O 14 17TEXT('Street line 1')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 15  2'            2:'
     A            #1STR2        35   O 15 17TEXT('Street line 2')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 16  2'            3:'
     A            #1STR3        35   O 16 17TEXT('Street line 3')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 17  2'            4:'
     A            #1STR4        35   O 17 17TEXT('Street line 4')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 18  2'Town/State  1:'
     A            #1TWN1        20   O 18 17TEXT('Town/State  1')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 19  2'            2:'
     A            #1TWN2        35   O 19 17TEXT('Town/State  1')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 20  2'            3:'
     A            #1TWN3        35   O 20 17TEXT('Town/State  3')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 21  2'Postal/ZIP    '
     A            #1ZIP         15   O 21 17TEXT('Postal/ZIP   ')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit F12=Previous'
     A                                      DSPATR(HI) COLOR(BLU)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A
      *                                     SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
