      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Incoming payments margins info display')      *
      *****************************************************************
      *                                                               *
      *  Midas - API Funds Transfer Module                            *
      *                                                               *
      *  DSPF/FTIPAY5DF - Incoming Payments Margins Informations      *
      *                   Display.                                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER030             Date 09Jul08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CFT014  *CREATE    Date 25May00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER030 - Multicash German Feature                            *
      *  CFT014 - Straight-through processing Phase 2 MT103           *
      *           Messages Generation for FT.                         *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(23)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Message Subfile Format
      *
     A
     A          R FTIPAY5S1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Message subfile control
      *
     A          R FTIPAY5S0                 TEXT('Msg Subfile Control')
     A                                      CF03
     A  71                                  CF05
     A                                      CF12
     A                                      SFLCTL(FTIPAY5S1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      ** Main Display Screen
      *
     A          R FTIPAY5D1
     A                                      CF03
     A  71                                  CF05
     A                                      CF12
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      ** FX Margin Points
      ** Margin P/S Ind
      ** Margin Sign
      *
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'FTIPAY5'
     A                                  1 33'Incoming Payments Input'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  3  1'Action Code'
     A            DDACTN         1A  O  3 13TEXT('ACTION CODE')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  3 16'Payment Reference'
     A            DDPREF        15A  O  3 41OVRDTA
     A                                      DSPATR(HI)
     A                                  5 02'FX Margin Pts.'
      ** Start of Change CER030
     A N74                                  DSPATR(ND)
      ** End of Change CER030
     A                                      OVRATR
     A            DDFXMP         8A  B  5 18OVRDTA
      ** Start of Change CER030
     A N74                                  DSPATR(ND)
      ** End of Change CER030
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI)
      ** Start of Change CER030
     AON74
      ** End of Change CER030
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *
     A                                  6 02'Margin P/S Ind.'
      ** Start of Change CER030
     A N74                                  DSPATR(ND)
      ** End of Change CER030
     A                                      OVRATR
     A            DDMBSI         1A  B  6 18OVRDTA
      ** Start of Change CER030
     A N74                                  DSPATR(ND)
      ** End of Change CER030
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
      ** Start of Change CER030
     AON74
      ** End of Change CER030
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *
     A                                  7 02'Margin Sign'
      ** Start of Change CER030
     A N74                                  DSPATR(ND)
      ** End of Change CER030
     A                                      OVRATR
     A            DDMSIG         1A  B  7 18OVRDTA
      ** Start of Change CER030
     A N74                                  DSPATR(ND)
      ** End of Change CER030
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
      ** Start of Change CER030
     AON74
      ** End of Change CER030
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      ** Start of Change CER030
     A  73                              9 02'Transaction Type'
     A                                      OVRATR
     A            DDRTTY         5A  B  9 19OVRDTA
     A N73                                  DSPATR(ND)
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     AON73
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *
     A  72                             11  2'Extended Narrative +
     A                                      (Credit)'
     A                                      OVRATR
     A            DDEXT1        65A  B 12 11OVRDTA
     A N72                                  DSPATR(ND)
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     AON72
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *
     A            DDEXT2        65A  B 13 11OVRDTA
     A N72                                  DSPATR(ND)
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     AON72
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *
     A            DDEXT3        65A  B 14 11OVRDTA
     A N72                                  DSPATR(ND)
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     AON72
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *
     A            DDEXT4        65A  B 15 11OVRDTA
     A N72                                  DSPATR(ND)
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     AON72
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *
     A            DDEXT5        65A  B 16 11OVRDTA
     A N72                                  DSPATR(ND)
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     AON72
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *
     A            DDEXT6        65A  B 17 11OVRDTA
     A N72                                  DSPATR(ND)
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     AON72
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      ** End of Change CER030
      *
      ** Main Screen Footer
      *
     A          R FTIPAY5F1                 TEXT('Screen Footer')
     A                                      CF03
     A  71                                  CF05
     A                                      CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A            SCCMDTXT      79A    24  2OVRDTA
