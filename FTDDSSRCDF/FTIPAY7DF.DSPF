     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Incoming payments level 2 scrn 7 display')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FTIPAY7DF - Incoming Payments Level 2 Screen 7 Display  *
      *                                                               *
      *  (C) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD035776           Date 10Sep15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CSW209  *CREATE    Date 01Apr09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD035776 - User need to press twice on input screen          *
      *           - Remove unsupported keywords of HATS.              *
      *  CSW209 - SWIFT 2009 Changes                                  *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Details Screen
      *
     A          R FTIPAYD1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2009')
      *
     A                                      CF03
     A                                      CF12
     A  72                                  CF07
     A  73                                  CF08
     A  74                                  CF15
      **Start of change MD035776
     A**********                            FRCDTA                                          MD035776
      **End of change MD035776
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                      OVRDTA
     A                                  1 13'FTIPY2F7'
     A                                  1 25'Level 2'
     A                                  1 33'INCOMING PAYMENTS +
     A                                      MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
     A                                      OVRDTA
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  3  6'Enter Action code'
     A            DDACTN         1A  O  3 25OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  3 29'Payment reference'
     A            DDPREF        15A  O  3 48OVRDTA
     A                                      DSPATR(HI)
      *
     A            DDSTAT        10A  O  3 71OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  4  2'Network'
     A            DDNWRK         6A  B  4 18OVRDTA
     A  10                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N10                                  DSPATR(HI)
      *
     A                                  5  2'Message Type'
     A            DDMTPY         3A  B  5 18OVRDTA
     A  11                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N11                                  DSPATR(HI)
      *
     A                                  6  2'Validation Flag'
     A            DDVALF         8A  B  6 18OVRDTA
     A  12                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N12                                  DSPATR(HI)
      *
     A                                  7  2'Ordering Customer (Cover)'
     A            DDCOC1        35A  B  7 38OVRDTA
     A  13                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N13                                  DSPATR(HI)
     A            DDCOC2        35A  B  8  2OVRDTA
     A  14                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N14                                  DSPATR(HI)
     A            DDCOC3        35A  B  8 38OVRDTA
     A  15                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N15                                  DSPATR(HI)
     A            DDCOC4        35A  B  9  2OVRDTA
     A  16                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N16                                  DSPATR(HI)
     A            DDCOC5        35A  B  9 38OVRDTA
     A  17                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N17                                  DSPATR(HI)
      *
     A                                 10  2'Ordering Institution (Cover)'
     A            DDCOB1        35A  B 10 38OVRDTA
     A  18                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N18                                  DSPATR(HI)
     A            DDCOB2        35A  B 11  2OVRDTA
     A  19                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N19                                  DSPATR(HI)
     A            DDCOB3        35A  B 11 38OVRDTA
     A  20                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N20                                  DSPATR(HI)
     A            DDCOB4        35A  B 12  2OVRDTA
     A  21                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N21                                  DSPATR(HI)
     A            DDCOB5        35A  B 12 38OVRDTA
     A  22                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N22                                  DSPATR(HI)
      *
     A                                 13  2'Intermediary Institution (Cover)'
     A            DDCIB1        35A  B 13 38OVRDTA
     A  23                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N23                                  DSPATR(HI)
     A            DDCIB2        35A  B 14  2OVRDTA
     A  24                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N24                                  DSPATR(HI)
     A            DDCIB3        35A  B 14 38OVRDTA
     A  25                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N25                                  DSPATR(HI)
     A            DDCIB4        35A  B 15  2OVRDTA
     A  26                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N26                                  DSPATR(HI)
     A            DDCIB5        35A  B 15 38OVRDTA
     A  27                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N27                                  DSPATR(HI)
      *
     A                                 16  2'Account with Institution (Cover)'
     A            DDCAB1        35A  B 16 38OVRDTA
     A  28                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N28                                  DSPATR(HI)
     A            DDCAB2        35A  B 17  2OVRDTA
     A  29                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N29                                  DSPATR(HI)
     A            DDCAB3        35A  B 17 38OVRDTA
     A  30                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N30                                  DSPATR(HI)
     A            DDCAB4        35A  B 18  2OVRDTA
     A  31                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N31                                  DSPATR(HI)
     A            DDCAB5        35A  B 18 38OVRDTA
     A  32                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N32                                  DSPATR(HI)
      *
     A                                 19  2'Beneficiary Customer (Cover)'
     A            DDCBC1        35A  B 19 38OVRDTA
     A  33                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N33                                  DSPATR(HI)
     A            DDCBC2        35A  B 20  2OVRDTA
     A  34                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N34                                  DSPATR(HI)
     A            DDCBC3        35A  B 20 38OVRDTA
     A  35                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N35                                  DSPATR(HI)
     A            DDCBC4        35A  B 21  2OVRDTA
     A  36                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N36                                  DSPATR(HI)
     A            DDCBC5        35A  B 21 38OVRDTA
     A  37                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N37                                  DSPATR(HI)
      *
      ** Screen Footer
      *
     A          R FTIPAYF1                  TEXT('Screen Footer')
     A                                      CF03
     A                                      CF12
     A  72                                  CF07
     A  73                                  CF08
     A  74                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      CLRL(23)
      *
     A                                 23  2'F3=Exit  F7=Previous  F8=Next  +
     A                                       F12=Cancel  F15=Select'
      *
      ** Message Subfile Format
      *
     A          R FTIPAYS1                  TEXT('Message Subfile')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Message Subfile Control
      *
     A          R FTIPAYS0                  TEXT('MSG Subfile Control')
     A                                      CF03
     A                                      CF12
     A  72                                  CF07
     A  73                                  CF08
     A  74                                  CF15
     A                                      SFLCTL(FTIPAYS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
