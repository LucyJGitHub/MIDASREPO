      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas FT Incoming payments browse')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FTIPAYBDF - Incoming Payments Browse                    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CAP136  *CREATE    Date 15Nov99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP136 - Conversion of FT Incoming Payment inputs into       *
      *           modular structure to use as APIs.                   *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Select Screen Subfile for Midas Payment Reference
      *
     A          R FTIPAYS0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
      *
     A            DDOPTN         1A  B  7  2TEXT('Select Option')
     A                                      DSPATR(UL)
      *
     A            DDSTTS        10A  O  7  5TEXT('Payment Status')
      *
     A            DDPREF        15A  O  7 17TEXT('Payment Reference')
     A                                      DSPATR(HI)
      *
     A**Start of change CGL029
     A**********  DDSEND        17A  O  7 35TEXT('Sender')                                    CGL029
     A**End of change CGL029
      *
     A**Start of change CGL029
     A**********  DDBENF        17A  O  7 53TEXT('Beneficiary')                               CGL029
     A**End of change CGL029
      *
     A            DDSMCY         3A  O  7 71TEXT('Settlement Currency')
      *
     A            DDPCCY         3A  O  7 75TEXT('Payment Currency')
     A**Start of change CGL029
     A            DDSEND        24A  O  8 29TEXT('Sender')
     A            DDBENF        24A  O  8 54TEXT('Beneficiary')
     A**End of change CGL029
      *
      ** Select Screen Control for Midas Payment Reference
      *
     A          R FTIPAYC0                  TEXT('Select Screen Control')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLCTL(FTIPAYS0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A**Start of change CGL029
     A**********                            SFLSIZ(15)                                        CGL029
     A**********                            SFLPAG(14)                                        CGL029
     A                                      SFLSIZ(8)
     A                                      SFLPAG(7)
     A**End of change CGL029
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
     A            DDSFRN         4  0H      SFLRCDNBR
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                  1 13'FTIPYBF1'
     A                                  1 26'INCOMING PAYMENTS MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation Id')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5  1'Sel'
     A                                  5  5'Payment Sts'
     A                                  5 17'Payment Reference'
     A**Start of change CGL029
     A**********                        5 35'Sender'                                          CGL029
     A**********                        5 53'Beneficiary'                                     CGL029
     A**End of change CGL029
     A                                  5 71'Set'
     A                                  5 75'Pay'
     A**Start of change CGL029
     A                                  6 29'Sender'
     A                                  6 54'Beneficiary'
     A**End of change CGL029
     A                                  6 71'Ccy'
     A                                  6 75'Ccy'
      *
      ** Select Screen Subfile for Front Office Id
      *
     A          R FTIPAYS1                  TEXT('Select Screen Subfile')
     A                                      SFL
      *
     A            DDOPTN         1A  B  7  2TEXT('Select Option')
     A                                      DSPATR(UL)
      *
     A            DDSTTS        10A  O  7  5TEXT('Payment Status')
      *
     A            DDFRNT        20A  O  7 17TEXT('Front Office Trans Id')
     A                                      DSPATR(HI)
      *
     A**Start of change CGL029
     A**********  DDSEND        17A  O  7 38TEXT('Sender')                                    CGL029
     A**End of change CGL029
      *
     A**Start of change CGL029
     A**********  DDBENF        17A  O  7 56TEXT('Beneficiary')                               CGL029
     A**End of change CGL029
      *
     A            DDSMCY         3A  O  7 74TEXT('Settlement Currency')
      *
     A            DDPCCY         3A  O  7 78TEXT('Payment Currency')
     A**Start of change CGL029
     A            DDSEND        24A  O  8 29TEXT('Sender')
     A            DDBENF        24A  O  8 54TEXT('Beneficiary')
     A**End of change CGL029
      *
      ** Select Screen Control for Front Office Id
      *
     A          R FTIPAYC1                  TEXT('Select Screen Control')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLCTL(FTIPAYS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A**Start of change CGL029
     A**********                            SFLSIZ(15)                                        CGL029
     A**********                            SFLPAG(14)                                        CGL029
     A                                      SFLSIZ(8)
     A                                      SFLPAG(7)
     A**End of change CGL029
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
     A            DDSFRN         4  0H      SFLRCDNBR
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                  1 13'FTIPYBF2'
     A                                  1 26'INCOMING PAYMENTS MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation Id')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5  1'Sel'
     A                                  5  5'Payment Sts'
     A                                  5 17'Front Office Id'
     A**Start of change CGL029
     A**********                        5 38'Sender'                                          CGL029
     A**********                        5 56'Beneficiary'                                     CGL029
     A**End of change CGL029
     A                                  5 74'Set'
     A                                  5 78'Pay'
     A**Start of change CGL029
     A                                  6 29'Sender'
     A                                  6 54'Beneficiary'
     A**End of change CGL029
     A                                  6 74'Ccy'
     A                                  6 78'Ccy'
      *
      ** Select Screen Footer
      *
     A          R FTIPAYF1                  TEXT('Select Screen Footer')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit  F11=Toggle Front Office +
     A                                      Id  F12=Cancel'
