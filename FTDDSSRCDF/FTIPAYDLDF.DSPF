      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT IPAY - Display File')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FTIPAYDLDF - FT Incoming Payments Display - Extension   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      * -----------------------*
      *   MAIN DISPLAY SCREEN  *
      * -----------------------*
      *
     A          R FTPAYDF1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2005')
     A                                      KEEP
     A                                      OVERLAY
     A  35                                  CF05(05 'Reset')
     A                                      CF12(12 'Previous')
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CSRLOC(CSRROW CSRCOL)
      *
     A            CSRROW         3S 0H
     A            CSRCOL         3S 0H
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FTPAYDF1'
     A                                  1 32'BCL 2002 Reporting'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  5  2'Ordering type. . . . . . . . .:'
     A            LLLXORTY       2A  B  5 34
     A N50N20                               DSPATR(HI)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                  6  2'Beneficiary type . . . . . . .:'
     A            LLLXBETY       2A  B  6 34
     A N51N20                               DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                  7  2'IBLC operation code. . . . . .:'
     A            LLLXOPCO       3A  B  7 34
     A N52N20                               DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                  8  2'Country code . . . . . . . . .:'
     A            LLLXCOCO       2A  B  8 34
     A N53N20                               DSPATR(HI)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                  9  2'Identification code. . . . . .:'
     A            LLLXIDCO       2A  B  9 34
     A N54N20                               DSPATR(HI)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                 10  2'Identification number. . . . .:'
     A            LLLXIDEN       9A  B 10 34
     A N55N20                               DSPATR(HI)
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                 23  2'F3=Main Menu'
     A                                      COLOR(BLU)
     A  35                             23 17'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 23 30'F12=Previous'
     A                                      COLOR(BLU)
      *
      * --------------------------*
      *   Message subfile format  *
      * --------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * ---------------------------*
      *   Message subfile control  *
      * ---------------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(02)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
