      *****************************************************************
/*STD *  DSPFBASE
/*EXI *  TEXT('Midas FT Incoming payments lvl 1 init scrn display')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FTIPAYIDF - Incoming Payments Level 1 Initial Screen    *
      *                   Display                                     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CAP136  *CREATE    Date 15Nov99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP136 - Conversion of FT Incoming Payment inputs into       *
      *           modular structure to use as APIs.                   *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Details Screen
      *
     A          R FTIPAYD1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF15
     A  74                                  CF18
      *
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                      OVRDTA
     A                                  1 13'FTIPY1F1'
     A                                  1 25'Level 1'
     A                                  1 33'INCOMING PAYMENTS +
     A                                      MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
     A                                      OVRDTA
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  3  6'Enter Action code'
     A            DDACTN         1A  B  3 25OVRDTA
     A  10                                  DSPATR(RI)
     A  10
     AO 68                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N10                                  DSPATR(HI)
      *
     A                                  3 29'Payment reference'
     A            DDPREF        15A  B  3 48OVRDTA
     A  11                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N11                                  DSPATR(HI)
      *
     A                                  4  2'Booking branch'
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A            DDBRCA    R        B  4 17REFFLD(BRCA)
     A                                      OVRDTA
     A N80                                  DSPATR(ND)
     A  12                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A N80
     AO 70                                  DSPATR(PR)
     A N12                                  DSPATR(HI)
      *
     A                                  4 29'SWIFT reference'
     A            DDSWTN        28A  B  4 48OVRDTA
     A  13                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N13                                  DSPATR(HI)
      *
     A                                  5 29'Sequence number'
     A            DDSEQNO        2A  B  5 48OVRDTA
     A  14                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N14                                  DSPATR(HI)
      *
      ** Screen Footer
      *
     A          R FTIPAYF1                  TEXT('Screen Footer')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF15
     A  74                                  CF18
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      CLRL(23)
      *
     A  61                             23  2'F3=Exit  F7=Previous  F8=Next  +
     A                                       F12=Cancel  F15=Select'
     A  62                             23  2'F3=Exit  F12=Cancel'
     A  63                             23  2'F3=Exit  F12=Cancel  F18=Next +
     A                                       Payment'
      *
      ** Message Subfile Format
      *
     A          R FTIPAYS1                  TEXT('Message Subfile')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Message Subfile Control
      *
     A          R FTIPAYS0                  TEXT('MSG Subfile Control')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF15
     A  74                                  CF18
     A                                      SFLCTL(FTIPAYS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
