      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas FT Invalid incoming payments browse')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FTIPAYRDF - Invalid Incoming Payments Browse            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. 258227             Date 24Dec08               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CAP136  *CREATE    Date 15Nov99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  258227 - Text of comment is unreadable                       *
      *  CAP136 - Conversion of FT Incoming Payment inputs into       *
      *           modular structure to use as APIs.                   *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Select Screen Subfile
      *
     A          R FTIIPAYS0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
      *
     A            DDOPTN         1A  B  7  3TEXT('Select Option')
     A                                      DSPATR(UL)
      *
     A            DDACTN         1A  O  7 10TEXT('Action Code')
     A                                      DSPATR(HI)
      *
     A            DDFRNT        20A  O  7 15TEXT('Front Office Trans. Id')
     A                                      DSPATR(HI)
      *
     A            DDSEND        17A  O  7 36TEXT('Sender')
      *
     A            DDBENF        17A  O  7 54TEXT('Beneficiary')
      *
     A            DDSMCY         3A  O  7 72TEXT('Settlement Currency')
      *
     A            DDPCCY         3A  O  7 76TEXT('Payment Currency')
      *
     A            DDPREF        15A  H      TEXT('Payment Reference')
     A            DDBRCA         3A  H      TEXT('Booking Branch')
     A            DDSND1        35A  H      TEXT('Sender')
     A            DDBNC1        35A  H      TEXT('Beneficiary')
     A            DDSMAM        14A  H      TEXT('Settlement Amount')
     A            DDPYAM        14A  H      TEXT('Payment Amount')
     A            DDSLDT         6A  H      TEXT('Settlement Date')
     A            DDPYDT         6A  H      TEXT('Payment Date')
     A            D@TMST    R   26A  H      REFFLD(TIMESTMP)
      *
      ** Select Screen Control
      *
     A          R FTIIPAYC0                 TEXT('Select Screen Control')
     A                                      CF03
      *
     A                                      SFLCTL(FTIIPAYS0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
     A            DDSFRN         4  0H      SFLRCDNBR
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                  1 13'FTIPYRF1'
     A                                  1 28'Invalid Incoming Payments'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation Id')
     A                                  2 35'Maintenance'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5  1'Select'
     A                                  5  8'Action'
     A                                  6  9'Code'
     A                                  5 15'Front Office Id'
     A                                  5 36'Sender'
     A                                  5 54'Beneficiary'
     A                                  5 72'Set'
     A                                  5 76'Pay'
     A                                  6 72'Ccy'
     A                                  6 76'Ccy'
      *
      ** Select Screen Footer
      *
     A          R FTIIPAYF1                 TEXT('Select Screen Footer')
     A                                      CF03
      *
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CLRL(22)
      *
     A  61                             22  2'The last transaction was +
     A                                      not applied to the database.'
     A  61                                  DSPATR(HI)
      *
     A  62                             22  2'Payment Reference assigned +
     A                                      to previous insert'
     A  62                                  DSPATR(HI)
      *
     A            DDPREFI       15A  O 22 51TEXT('Newly Inserted Payment +
     A                                      Reference')
     A                                      OVRDTA
     A N62                                  DSPATR(ND)
     A                                      DSPATR(HI)
      *
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit'
      *
      ** End Message
      *
     A          R FTIIPAYEND                TEXT('End Message')
     A                                      CF03
      *
     A**Start of change 258227
     A**********                            BLINK                                             258227
     A**********                            OVERLAY                                           258227
     A**********                            PUTOVR                                            258227
     A**End of change 258227
      *
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                  1 13'FTIPYRF2'
     A                                  1 28'Invalid Incoming Payments'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation Id')
     A                                  2 35'Maintenance'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A**Start of change 258227
     A**********                            OVRDTA                                            258227
     A**End of change 258227
      *
     A N10                             11 26'NO INVALID INCOMING PAYMENTS +
     A                                      PRESENT'
     A N10                                  DSPATR(HI)
     A  10                             11 26'USER IS NOT AUTHORISED TO +
     A                                      BROWSE'
     A  10                                  DSPATR(HI)
     A                                 23  2'Press enter to end this function'
