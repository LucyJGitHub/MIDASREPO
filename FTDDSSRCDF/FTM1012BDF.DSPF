      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Header browse screen')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FTM1012BDF - Funds Transfer Browse Function Module      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CFT006  *CREATE    Date 27Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFT006 - Funds Transfer addition of MT101 and MT102 messages *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CA12(12)
     A                                      CF14(14)
     A                                      CF15(15)
      *=========================================================================
     A          R FTM1012BSF                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  30                                  SFLNXTCHG
     A            SFACTN         1A  B 13  2
     A N52                                  DSPATR(HI UL)
     A  52                                  DSPATR(RI PC)
     A            SFRFTID       15A  O 13  4
     A            SFSNDREF      16A  O 13 20
     A            SFSNDDST      20A  O 13 37
     A            SFORDCUS      20A  O 13 58
     A*=========================================================================
     A          R FTM1012BCT                SFLCTL(FTM1012BSF)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      OVERLAY
     A  26                                  SFLDSP
     A  27                                  SFLDSPCTL
     A  28                                  SFLCLR
     A  29                                  SFLEND(*MORE)
     A            PSUSER    R        O  1  2REFFLD(USER)
     A                                      DSPATR(HI)
     A                                  1 13'FTM1012BDF'
     A                                      DSPATR(HI)
     A N31                              1 25'Incoming Request for Transfers'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  31                              1 25'Outgoing Request for Transfers'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 67'DATE '
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 73REFFLD(RUNA)
     A                                      DSPATR(HI)
     A            PSJOBNAME R        O  2  2REFFLD(JOJOB)
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                      DSPATR(HI)
     A                                  2 73TIME
     A                                      DSPATR(HI)
     A                                  4  1'Action'
     A            SACTN          1A  B  4  8
     A N53                                  DSPATR(HI)
     A N53                                  DSPATR(UL)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A                                  4 14'Request for Transfer Id'
     A            SRFTID        15A  B  4 38
     A N54                                  DSPATR(HI)
     A N54                                  DSPATR(UL)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A                                  4 54'Trans. Ref'
     A            STRNREF       16A  B  4 65
     A N55                                  DSPATR(HI)
     A N55                                  DSPATR(UL)
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A                                  5 14'Swift Reference'
     A            SSWFREF       28A  B  5 30
     A N56                                  DSPATR(HI)
     A N56                                  DSPATR(UL)
     A  56                                  DSPATR(RI)
     A  56                                  DSPATR(PC)
     A                                  6  1'Position to:'
     A                                  6 14'Request for Transfer Id'
     A            SPRFTID       15A  B  6 38
     A N51                                  DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A                                  7  1'Select on  :'
     A  31                              7 14'Destination'
     A N31                              7 14'Sender     '
     A            SSSNDDST      35A  B  7 38
     A N50                                  DSPATR(HI)
     A N50                                  DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A                                  8 14'Ordering Customer'
     A            SSORDCUS      35A  B  8 38
     A N50                                  DSPATR(HI)
     A N50                                  DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A                                  9 14'Sender''s Reference'
     A            SSSNDREF      16A  B  9 38
     A N50                                  DSPATR(HI)
     A N50                                  DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
      *
     A                                 11  1'Sel'
     A                                      DSPATR(HI)
     A                                 11  5'Request for'
     A                                      DSPATR(HI)
     A                                 11 20'Sender''s'
     A                                      DSPATR(HI)
     A  31                             11 37'Destination'
     A                                      DSPATR(HI)
     A N31                             11 37'Sender'
     A                                      DSPATR(HI)
     A                                 11 58'Ordering'
     A                                      DSPATR(HI)
     A                                 12  5'Transfer Id'
     A                                      DSPATR(HI)
     A                                 12 20'Reference'
     A                                      DSPATR(HI)
     A                                 12 58'Customer'
     A                                      DSPATR(HI)
     A            REC            3S 0H      SFLRCDNBR
     A            SCNTXT        30   O  2 25DSPATR(HI)
     A*=========================================================================
     A          R CMDKEY
     A                                 23  1'F3=Exit'
     A                                      DSPATR(HI)
     A                                 23 10'F5=Refresh'
     A                                      DSPATR(HI)
     A                                 23 22'F12=Cancel'
     A                                      DSPATR(HI)
     A  31                             23 34'F14=Incoming RFT'
     A                                      DSPATR(HI)
     A N31                             23 34'F14=Outgoing RFT'
     A                                      DSPATR(HI)
     A                                 22  1'I=Insert'
     A                                      DSPATR(HI)
     A                                 22 11'C=Credit List'
     A                                      DSPATR(HI)
     A                                 22 26'A=Amend'
     A                                      DSPATR(HI)
     A                                 22 35'D=Delete'
     A                                      DSPATR(HI)
     A                                 22 45'E=Enquire'
     A                                      DSPATR(HI)
     A                                 22 56'X=Authorise'
     A                                      DSPATR(HI)
     A N32N33N34                       23 52'F15=Unauthorised'
     A N32N33N34                            DSPATR(HI)
     A  32                             23 52'F15=Authorised'
     A  32                                  DSPATR(HI)
     A  33                             23 52'F15=Part Authorised'
     A  33                                  DSPATR(HI)
     A  34                             23 52'F15=All'
     A  34                                  DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            PGMQ                      SFLPGMQ
