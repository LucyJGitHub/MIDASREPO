      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT RFT transaction details screen 1')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer  Module                               *
      *                                                               *
      *  DSPF/FTM1012DDF - RFT Transaction details screen 1           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. 188649             Date 09Feb01               *
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 178751             Date 09May00               *
      *                 CFT007             Date 16Feb00               *
      *                 CFT006  *CREATE    Date 23Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  188649 - ADD AUTHORISED DESCRIPTION TO SCREEN                *
      *  178751 - Auto generation of references                       *
      *  CFT007 - BIC Database Plus                                   *
      *  CFT006 - Swift messages MT101/MT102                          *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A*****                                          Begin /CFT007/
     A  82                                  CF04(04)
     A  82                                  CF16(16)
     A*****                                          End   /CFT007/
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R FTM1012DF1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A*****                                          Begin /CFT007/
     A                                      RTNCSRLOC(&CSRREC &CSRFLD &CSRPOS)
     A*****                                          End   /CFT007/
     A                                      OVERLAY
     A N72                                  CF05
     A N73                                  CF10
     A                                      CF11
     A                                      CF12
      *
     A****                                           Begin /CFT007/
     A            CSRPOS         4S 0H
     A            CSRFLD        10A  H
     A            CSRREC        10A  H
     A****                                           End   /CFT007/
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FTM1012DF1'
     A N78                              1 23'Incoming Request for Transfer Main-
     A                                      tenance'
     A                                      DSPATR(UL HI)
     A  78                              1 23'Outgoing Request for Transfer Main-
     A                                      tenance'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            STIME          8A  O  2 73
     A                                  1 25'Request for Transfer Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 25'Transaction Details'
     A                                  2 46'Mode'
     A            SACTN          1A  O  2 52DSPATR(HI)
     A                                  4  2'Request for Transfer Id'
     A                                  5  2'Customer Reference'
     A            SRFTHDR       15A  B  4 27
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(RI)
     A  29                                  DSPATR(PC)
     A  74                                  DSPATR(PR)
     A N74                                  DSPATR(UL)
     A            SCUSTRF       16A  O  5 27DSPATR(HI)
      *
      *                                              Begin /178751/
     A            STSEQNO        3A  O  4 43DSPATR(HI)
     A**********************************4 45'Sender''s Ref.'
     A                                  4 48'Sender''s Ref.'
      *                                              End   /178751/
      *
     A                                  5 45'Transaction Ref.'
     A            STRNSID       16A  B  5 62
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A            SSNDREF       16A  O  4 62DSPATR(HI)
     A                                  7  2'Beneficiary'
     A            SBNA35        35A  B  7 40
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SBNB35        35A  B  8  2
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SBNC35        35A  B  8 40
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SBND35        35A  B  9  2
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SBNE35        35A  B  9 40
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 10  2'Account with institution'
     A            SACA11        11A  B 10 40
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SACA35        35A  B 11  2
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SACB35        35A  B 11 40
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SACC35        35A  B 12  2
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SACD35        35A  B 12 40
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SACE35        35A  B 13  2
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 14  2'Intermediary'
     A            SINSTA        11A  B 14 40
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SINST1        35A  B 15  2
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SINST2        35A  B 15 40
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SINST3        35A  B 16  2
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SINST4        35A  B 16 40
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SINST5        35A  B 17  2
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 18  2'Instructing Party'
     A            SINPTY        35   B 18 40
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 19  2'Ordering Customer'
     A            SOCUS1        35   B 19 40
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SOCUS2        35   B 20  2
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SOCUS3        35   B 20 40
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SOCUS4        35   B 21  2
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SOCUS5        35   B 21 40
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SASI35        35   B 22 26
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 22  2'A/C Servicing Inst.'
     A            SACC11        11   B 22 64
     A N55                                  DSPATR(HI)
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
      *
      ** Hidden fields - used to update 'Type' fields
     A            SBENTTP        1A  H
     A            SAWITTP        1A  H
     A            SINTTTP        1A  H
     A            SACSITP        1A  H
     A            SDCUSTP        1A  H
      *
     A                                 23  2'F3=Exit'
     A****                                           Begin /CFT007/
     A                                 23 10'F4=BIC Prompt'
     A  76                                  DSPATR(ND)
     A****                                           End   /CFT007/
     A                                 23 24'F5=Refresh'
     A  72                                  DSPATR(ND)
     A N73                             23 35'F10=Confirm'
     A****                                           Begin /CFT007/
     A N76                             23 35'F16=BIC Search'
     A****                                           End   /CFT007/
     A                                 23 50'F11=Next Screen'
     A                                 23 66'F12=Cancel'
      **************************************            START 188649
     A            SAIND         20   O  3 52DSPATR(HI)
      **************************************            END   188649
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PSPROCNAME                SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  77
     AON77                                  SFLEND
     A            PSPROCNAME                SFLPGMQ
