      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT RFT transaction details display screen 2')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer  Module                               *
      *                                                               *
      *  DSPF/FTM1013DDF - RFT Transaction details screen 2           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. 188649             Date 09Feb01               *
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 177802             Date 14Apr00               *
      *                 CFT007             Date 21MAR00               *
      *                 CFT006  *CREATE    Date 23Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  188649 - ADD AUTHORISED DESCRIPTION TO SCREEN                *
      *  177802 - Amend F12 text                                      *
      *  CFT007 - BIC DATABASE PLUS                                   *
      *  CFT006 - Swift messages MT101/MT102                          *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A****                                          Begin  /CFT007/
     A  82                                  CF04(04)
     A  82                                  CF16(16)
     A****                                          End    /CFT007/
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R FTM1013DF1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      OVERLAY
     A N72                                  CF05
     A N73                                  CF10
     A                                      CF11
     A                                      CF12
     A****                                          Begin  /CFT007/
     A                                      RTNCSRLOC(&CSRREC &CSRFLD &CSRPOS)
     A****                                          End    /CFT007/
      *
     A****                                          Begin  /CFT007/
     A            CSRPOS         4S 0H
     A            CSRFLD        10A  H
     A            CSRREC        10A  H
     A****                                          End    /CFT007/
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FTM1013DF1'
     A                                  1 67'DATE '
     A N78                              1 23'Incoming Request for Transfer Main-
     A                                      tenance'
     A                                      DSPATR(UL HI)
     A  78                              1 23'Outgoing Request for Transfer Main-
     A                                      tenance'
     A                                      DSPATR(UL HI)
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            STIME          8A  O  2 73
     A                                  1 25'Request for Transfer Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 25'Transaction Details'
     A                                  2 46'Mode'
     A            SACTN          1A  O  2 52DSPATR(HI)
     A                                  4  2'Request for Transfer id'
     A                                  5  2'Customer Reference'
     A            SRFTHDR       15A  O  4 27DSPATR(HI)
     A            SCUSTRF       16A  O  5 27DSPATR(HI)
     A                                  4 45'Sender''s Ref.'
     A                                  5 45'Transaction Ref.'
     A            STRNSID       16A  O  5 62DSPATR(HI)
     A            SSNDREF       16A  O  4 62DSPATR(HI)
     A            SFXDREF       16A  B  8 55
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            STRNAMT       16A  B  9 22
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            STRNCCY        3A  B  9 55
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SXRATE        14A  B 10 24
     A  70                                  DSPATR(ND)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  71
     AO 70                                  DSPATR(PR)
     A N71N70                               DSPATR(UL)
     A            SXRATEO        1A  B 10 55
     A  70                                  DSPATR(ND)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  71
     AO 70                                  DSPATR(PR)
     A N71N70                               DSPATR(UL)
     A            SPAYAMT       16A  B 11 22
     A  70                                  DSPATR(ND)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A  71
     AO 70                                  DSPATR(PR)
     A N71N70                               DSPATR(UL)
     A            SPAYCCY        3A  B 11 55
     A  70                                  DSPATR(ND)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A  71
     AO 70                                  DSPATR(PR)
     A N71N70                               DSPATR(UL)
     A            SCRDVDT        6   B 11 66
     A  70                                  DSPATR(ND)
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A  71
     AO 70                                  DSPATR(PR)
     A N71N70                               DSPATR(UL)
     A            STOOAMT       16A  B 12 22
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            STOCCY         3A  B 12 55
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SCHGACC       35A  B 14 22
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            STDCHGS        3A  B 15 22
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SRCVCHG       14A  B 16 22
     A  70                                  DSPATR(ND)
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A  71
     AO 70                                  DSPATR(PR)
     A N71N70                               DSPATR(UL)
     A            SRCHCCY        3A  B 16 59
     A  70                                  DSPATR(ND)
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A  71
     AO 70                                  DSPATR(PR)
     A N71N70                               DSPATR(UL)
     A            SRCTCHG       14A  O 17 22DSPATR(HI)
     A  70                                  DSPATR(ND)
     A            SRCTCCY        3A  O 17 59DSPATR(HI)
     A  70                                  DSPATR(ND)
     A            SRCPCHG       14A  O 18 22DSPATR(HI)
     A  70                                  DSPATR(ND)
     A            SRCPCCY        3A  O 18 59DSPATR(HI)
     A  70                                  DSPATR(ND)
     A                                 11 61'Date'
     A  70                                  DSPATR(ND)
     A                                 10  2'Exchange Rate'
     A  70                                  DSPATR(ND)
     A                                 11  2'Payment Amount'
     A  70                                  DSPATR(ND)
     A                                 12  2'Original Amount'
     A                                 15  2'Details of Charges'
     A                                 16  2'Receiver''s Charges'
     A  70                                  DSPATR(ND)
     A                                  9  2'Transfer Amount'
     A                                  9 40'Currency'
     A                                 11 40'Currency'
     A  70                                  DSPATR(ND)
     A                                 12 40'Currency'
     A                                 14  2'Charges Account'
     A                                 16 40'Currency'
     A  70                                  DSPATR(ND)
     A                                 17 40'Transfer Currency'
     A  70                                  DSPATR(ND)
     A                                 18 40'Payment Currency'
     A  70                                  DSPATR(ND)
     A                                  8 40'FX Deal Ref'
     A                                 10 40'Override (Y/N)'
     A  70                                  DSPATR(ND)
      *
      *
------*-------------------------------------------------------------------------
      *  COMMAND KEYS AVAILABLE DISPLAYED ON LINE 23.
------*-------------------------------------------------------------------------
     A                                 23  2'F3=Exit'
     A                                 23 24'F5=Refresh'
     A  72                                  DSPATR(ND)
     A N73                             23 35'F10=Confirm'
     A                                 23 50'F11=Next Screen'
      *****************                              Start 177802
     A*****************                23 66'F12=Cancel'
     A                                 23 66'F12=Previous'
      *****************                              End   177802
      **************************************            START 188649
     A            SAIND         20   O  3 52DSPATR(HI)
      **************************************            END   188649
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PSPROCNAME                SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  77
     AON77                                  SFLEND
     A            PSPROCNAME                SFLPGMQ
