      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT RFT transaction details display screen 3')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfert Module                               *
      *                                                               *
      *  DSPF/FTM1014DDF - RFT Transaction details screen 3           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. 192765             Date 01May01               *
      *  Prev Amend No. 188649             Date 09Feb01               *
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 177802             Date 14Apr00               *
      *                 CFT007             Date 21MAR00               *
      *                 CFT006  *CREATE    Date 23Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *  192765 - Field Reference File MFRFL not used                 *
      *  188649 - Include authorised text on screen                   *
      *  177802 - Amend F12 text                                      *
      *  CFT007 - BIC DATABASE PLUS                                   *
      *  CFT006 - Swift messages MT101/MT102                          *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      CA03
     A****                                          Begin  /CFT007/
     A  82                                  CF04(04)
     A  82                                  CF16(16)
     A****                                          End    /CFT007/
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *****************************************************************
     A          R FTM1014DF1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      OVERLAY
     A N72                                  CF05
     A N73                                  CF10
     A                                      CF11
     A                                      CF12
     A****                                          Begin  /CFT007/
     A                                      RTNCSRLOC(&CSRREC &CSRFLD &CSRPOS)
     A****                                          End    /CFT007/
      *****************************************************************
      *
     A****                                          Begin  /CFT007/
     A            CSRPOS         4S 0H
     A            CSRFLD        10A  H
     A            CSRREC        10A  H
     A****                                          End    /CFT007/
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FTM1014DF1'
     A                                  1 67'DATE '
     A N78                              1 23'Incoming Request for Transfer Main-
     A                                      tenance'
     A                                      DSPATR(UL HI)
     A  78                              1 23'Outgoing Request for Transfer Main-
     A                                      tenance'
     A                                      DSPATR(UL HI)
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            STIME          8A  O  2 73
     A                                  1 25'Request for Transfer Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 25'Transaction Details'
     A                                  2 46'Mode'
     A            SACTN          1A  O  2 52DSPATR(HI)
     A                                  4  2'Request for Transfer id'
     A                                  5  2'Customer Reference'
     A            SRFTHDR       15A  O  4 27DSPATR(HI)
     A            SCUSTRF       16A  O  5 27DSPATR(HI)
     A                                  4 45'Sender''s Ref.'
     A                                  5 45'Transaction Ref.'
     A            STRNSID       16A  O  5 62DSPATR(HI)
     A            SSNDREF       16A  O  4 62DSPATR(HI)
     A            SGOPFLG        1   B  7 16
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A  71
     AO 70                                  DSPATR(PR)
     A N71N70                               DSPATR(UL)
     A            SINSCA4        4   B  9  2
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SINSA31       31   B  9  8
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SINSCB4        4   B 10  2
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SINSB31       31   B 10  8
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SINSCC4        4   B 11  2
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SINSCD4        4   B 12  2
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SINSCE4        4   B 13  2
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            STRMA35       35   B 16  2
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            STRMB35       35   B 16 39
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            STRMC35       35   B 17  2
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            STRMD35       35   B 17 39
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            STRGA35       35   B 19 39
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            STRGB35       35   B 20  2
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            STRGC35       35   B 20 39
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
      *
     A                                  7  2'Generate O/P'
     A  70                                  DSPATR(ND)
     A                                  8  2'Instructions'
     A                                 15  2'Remittance Information'
     A                                 19  2'Regulatory Reporting'
      *
      *-------------------------------------------------------------------------
     A                                 23  2'F3=Exit'
     A                                 23 10'F5=Refresh'
     A  72                                  DSPATR(ND)
     A                                 23 23'F10=Confirm'
     A  73                                  DSPATR(ND)
     A  74                             23 36'F11=User Details'
      ***********************                     Start 177802
     A***********************          23 54'F12=Cancel'
     A                                 23 54'F12=Previous'
      ***********************                     End   177802
      **************************************            START 188649
     A            SAIND         20   O  3 52DSPATR(HI)
      **************************************            END   188649
     A*=========================================================================
     A          R FTM1014DF2
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A                                      CF12
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FTM1014DF2'
     A                                  1 67'DATE '
     A N78                              1 23'Incoming Request for Transfer Main-
     A                                      tenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  78                              1 23'Outgoing Request for Transfer Main-
     A                                      tenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            STIME          8A  O  2 73
     A                                  1 25'Request for Transfer Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 25'Transaction Details'
     A                                  2 46'Mode'
     A            SACTN          1A  O  2 52DSPATR(HI)
     A                                  4  2'Request for Transfer id'
     A                                  5  2'Customer Reference'
     A            SRFTHDR       15A  O  4 27DSPATR(HI)
     A            SCUSTRF       16A  O  5 27DSPATR(HI)
     A                                  4 45'Sender''s Reference'
     A                                  5 45'Transaction Ref.'
     A            STRNSID       16A  O  5 63DSPATR(HI)
     A            SSNDREF       16A  O  4 63DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 10'F5=Refresh'
     A  72                                  DSPATR(ND)
     A                                 23 23'F10=Confirm'
     A  73                                  DSPATR(ND)
     A                                 23 54'F12=Cancel'
     A                                  9  8'Input User'
     A                                 10  8'Input Date'
     A                                 11  8'Input Time'
     A                                 13  8'Update User'
     A                                 14  8'Update Date'
     A                                 15  8'Update Time'
     A*                                                                 /* Start 192765*/
     A************PINUSER_D*R        O  9 21REFFLD(FT101DSD0/PINUSER_D *LIBL/FT-
     A***********************               101DSPD)
     A************PINDATE_D*R        O 10 21REFFLD(FT101DSD0/PINDATE_D *LIBL/FT-
     A***********************               101DSPD)
     A************PINTIME_D*R        O 11 21REFFLD(FT101DSD0/PINTIME_D *LIBL/FT-
     A***********************               101DSPD)
     A************PUPDUSR_D*R        O 13 21REFFLD(FT101DSD0/PUPDUSR_D *LIBL/FT-
     A***********************               101DSPD)
     A************PUPDDTE_D*R        O 14 21REFFLD(FT101DSD0/PUPDDTE_D *LIBL/FT-
     A***********************               101DSPD)
     A************PUPDTIM_D*R        O 15 21REFFLD(FT101DSD0/PUPDTIM_D *LIBL/FT-
     A***********************               101DSPD)
     A            PINUSER_D     10A  O  9 21
     A            PINDATE_D      6A  O 10 21
     A            PINTIME_D      6A  O 11 21
     A            PUPDUSR_D     10A  O 13 21
     A            PUPDDTE_D      6A  O 14 21
     A            PUPDTIM_D      6A  O 15 21
     A*                                                                 /*  End  192765*/
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PSPROCNAME                SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  77
     AON77                                  SFLEND
     A            PSPROCNAME                SFLPGMQ
