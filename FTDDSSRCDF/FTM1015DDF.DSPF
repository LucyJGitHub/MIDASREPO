      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT RFT general details screen')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  FTM1015DDF - FUNDS TRANSFER REQUEST FOR TRANSFER/MT101 AND   *
      *              MT102 GENERAL DETAILS SCREEN                     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 221761             Date 01Apr04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 192765             Date 01May01              *
      *                 188649             Date 01FEB01              *
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CFT007             Date 21MAR00               *
      *                 CFT006  *CREATE    Date 21SEP99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  221761 - Additional field SRSEQNO.                           *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  192765 - Field Reference File MFRFL not used                 *
      *  188649 - ADD AUTHORISED DESCRIPTION TO SCREEN                *
      *  CFT007 - BIC DATABASE PLUS                                   *
      *  CFT006 - MT101 MESSAGES                                      *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CF03(03)
     A****                                        Start of /CFT007/
     A  82                                  CF04(04)
     A  82                                  CF16(16)
     A****                                          End of /CFT007/
     A  27                                  CF11(11)
     A                                      CF12(12)
     A N15                                  CF05(05)
     A N16                                  CF10(10)
     A*
     A          R FTM1015DD1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A****                                        Start of /CFT007/
     A                                      RTNCSRLOC(&CSRREC &CSRFLD &CSRPOS)
     A            CSRPOS         4S 0H
     A            CSRFLD        10A  H
     A            CSRREC        10A  H
     A****                                          End of /CFT007/
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FTM1015DF1'
     A N20                              1 24'Incoming Request for Transfer Main-
     A                                      tenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  20                              1 24'Outgoing Request for Transfer Main-
     A                                      tenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date'
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 25'General Details'
     A                                  2 46'Mode'
     A            SACTN          1A  O  2 52DSPATR(HI)
     A                                  2 67'Time'
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
     A                                      TEXT('Current Time')
     A                                  4  2'Request for Transfer ID'
     A            SRFTID        15A  O  4 27DSPATR(HI)
     A                                  4 44'Sender''s Reference'
     A            SSNDREF       16A  B  4 63
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(PC)
     A  26                                  DSPATR(PR)
     A N26                                  DSPATR(UL)
     A                                  5  2'Customer Reference'
     A            SCUSTRF       16A  B  5 23
     A  29                                  DSPATR(RI)
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A**Start of change 221761
     A  13                              5 44'Sender''s Ref. Routing Seq.'                     221761
     A  13        SRSEQNO        3A  O  5 72TEXT('Sender''s Routing Seq.')                    221761
     A                                      DSPATR(HI)                                        221761
     A**End of change 221761
     A                                  6  2'Request for Transfer Type'
     A            SPTYP          2A  O  6 29DSPATR(HI)
     A                                  6 36'Sub Type'
     A            SPYST          2A  B  6 46
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A                                  7  2'Booking Branch'
     A            SBRCA          3A  B  7 18
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A                                  7 23'Originating Branch'
     A            SORBR          3A  B  7 43
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A                                  7 48'Book Code'
     A            SBOKC          2A  B  7 58
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A                                  7 62'Profit Centre'
     A            SPRFC          4A  B  7 77
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A                                  8  2'Swift Priority'
     A            SSWPRIO        1A  B  8 18
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A                                  9  2'Instructing Party'
     A            SHINPTY       35A  B  9 22
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A                                 10  2'Ordering Customer'
     A            SHOCUS1       35A  B 10 38
     A  38                                  DSPATR(RI)
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A            SHOCUS2       35A  B 11  2
     A  39                                  DSPATR(RI)
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A            SHOCUS3       35A  B 11 38
     A  40                                  DSPATR(RI)
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A            SHOCUS4       35A  B 12  2
     A  41                                  DSPATR(RI)
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A            SHOCUS5       35A  B 12 38
     A  42                                  DSPATR(RI)
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A                                 14  2'Destination'
     A            SRFTDST       35A  B 14 32
     A  43                                  DSPATR(RI)
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A                                 15  2'Account Servicing Institution'
     A            SHACSV1       35A  B 15 32
     A  44                                  DSPATR(RI)
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A            SHACSV2       11A  B 15 68
     A  45                                  DSPATR(RI)
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A                                 17  2'Debit Date'
     A            SDBTVDT        6A  B 17 15
     A  46                                  DSPATR(RI)
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A                                 17 26'Requested Execution Date'
     A            SEXDATE        6A  B 17 52
     A  47                                  DSPATR(RI)
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A                                 19  2'Handling Charge'
     A            SHANDCG       13A  B 19 19
     A  48                                  DSPATR(RI)
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A                                 19 34'Currency'
      **Start of change CGL029
     A**********  SHANDCY        3A  B 19 46                                                  CGL029
     A            SHANDCY        3A  B 19 44
      **End of change CGL029
     A  49                                  DSPATR(RI)
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
      **Start of change CGL029
     A**********                       19 50'Account'                                         CGL029
     A**********  SHANDAC       18A  B 19 58                                                  CGL029
     A                                 19 49'Account'
     A            SHANDAC       24A  B 19 57
      **End of change CGL029
     A  50                                  DSPATR(RI)
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A*********************************21  2'Sending Institution'
     A                                 21  2'Sender'
     A            SSINST        11A  B 21 23
     A  51                                  DSPATR(RI)
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A                                 22  2'Authorisation'
     A            SAUTHTX       35A  B 22 23
     A  52                                  DSPATR(RI)
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(PC)
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A            SCUSTTP        1A  H
     A            SASITTP        1A  H
     A            SSTID         12A  H
      **************************************            START 188649
     A            SAIND         20   O  3 52DSPATR(HI)
      **************************************            END   188649
     A*=========================================================================
     A          R FTM1015DD2
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FTM1015DF2'
     A N20                              1 24'Incoming Request for Transfer Main-
     A                                      tenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  20                              1 24'Outgoing Request for Transfer Main-
     A                                      tenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date'
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 25'General Details'
     A                                  2 46'Mode'
     A            SACTN          1A  O  2 52DSPATR(HI)
     A                                  2 67'Time'
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
     A                                      TEXT('Current Time')
     A                                  4  2'Request for Transfer ID'
     A            SRFTID        15A  O  4 27DSPATR(HI)
     A                                  4 44'Sender''s Reference'
     A            SSNDREF       16A  O  4 63DSPATR(HI)
     A                                  5  2'Customer Reference'
     A            SCUSTRF       16A  O  5 23DSPATR(HI)
     A                                  6  2'Request for Transfer Type'
     A            SPTYP          2A  O  6 29DSPATR(HI)
     A                                  6 36'Sub Type'
     A            SPYST          2A  O  6 46
     A                                  9  2'Input User'
     A*                                                                    /*Start 192765*/
     A************SINUSER   R        O  9 14REFFLD(FT101HD0/INUSER *LIBL/FT101H-
     A************                          PD)
     A            SINUSER   R        O  9 14REFFLD(RGUD)
     A*                                                                    /* End  192765*/
     A                                  9 25'Date'
     A*                                                                    /*Start 192765*/
     A************SINDATE   R        O  9 30REFFLD(FT101HD0/INDATE *LIBL/FT101H-
     A************                          PD)
     A            SINDATE   R        O  9 30REFFLD(LASD)
     A*                                                                    /* End  192765*/
     A                                  9 37'Time'
     A*                                                                    /*Start 192765*/
     A************SINTIME   R        O  9 42REFFLD(FT101HD0/INTIME *LIBL/FT101H-
     A************                          PD)
     A            SINTIME   R        O  9 42REFFLD(LATM)
     A*                                                                    /* End  192765*/
     A                                 11  2'Update'
     A                                 11  9'User'
     A*                                                                    /*Start 192765*/
     A************SUPDUSR   R        O 11 14REFFLD(FT101HD0/UPDUSR *LIBL/FT101H-
     A************                          PD)
     A            SUPDUSR   R        O 11 14REFFLD(RGUD)
     A*                                                                    /* End  192765*/
     A                                 11 25'Date'
     A*                                                                    /*Start 192765*/
     A************SUPDDTE   R        O 11 30REFFLD(FT101HD0/UPDDTE *LIBL/FT101H-
     A************                          PD)
     A            SUPDDTE   R        O 11 30REFFLD(LASD)
     A*                                                                    /* End  192765*/
     A                                 11 37'Time'
     A*                                                                    /*Start 192765*/
     A************SUPDTIM   R        O 11 42REFFLD(FT101HD0/UPDTIM *LIBL/FT101H-
     A************                          PD)
     A************SAUP1     R        O 13 26REFFLD(FT101HD0/AUP1 *LIBL/FT101HPD)
     A************SAUP2     R        O 14 26REFFLD(FT101HD0/AUP2 *LIBL/FT101HPD)
     A            SUPDTIM   R        O 11 42REFFLD(LATM)
     A            SAUP1     R        O 13 26REFFLD(RGUD)
     A            SAUP2     R        O 14 26REFFLD(RGUD)
     A*                                                                    /*End   192765*/
     A                                 13  3'First Authorising User'
     A                                 14  2'Second Authorising User'
     A                                 16  2'Authorised Indicator'
     A*                                                                    /*Start 192765*/
     A************SAUIN     R        O 16 35REFFLD(FT101HD0/AUIN *LIBL/FT101HPD)
     A************SBAUTH    R        O 18 32REFFLD(FT101HD0/BAUTH *LIBL/FT101HP-
     A************                          D)
     A            SAUIN     R        O 16 35REFFLD(CRLF)
     A            SBAUTH         4   O 18 32
     A*                                                                    /*End   192765*/
     A                                 18  2'Batch Authorised Number'
     A                                 20  2'Batch Authorising User'
     A*                                                                    /*Start 192765*/
     A************SBATUSR   R        O 20 26REFFLD(FT101HD0/BATUSR *LIBL/FT101H-
     A************                          PD)
     A            SBATUSR   R        O 20 26REFFLD(RGUD)
     A*                                                                    /*End   192765*/
     A                                 16 37'Dual Authorisation Required '
     A*                                                                    /*Start 192765*/
     A************SRDUAL    R        O 16 66REFFLD(FT101HD0/RDUAL  *LIBL/FT101H-
     A************                          PD)
     A            SRDUAL    R        O 16 66REFFLD(CRLF)
     A*                                                                    /*End   192765*/
     A*=========================================================================
     A          R FTM1015DFF                TEXT('SCREEN FOOTER')
      *
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                      DSPATR(HI)
     A****                                        Start of /CFT007/
     A                                 23 10'F4=BIC Prompt'
     A N76                                  DSPATR(HI)
     A  76                                  DSPATR(ND)
     A****                                          End of /CFT007/
     A                                 23 24'F5=Refresh'
     A  15                                  DSPATR(ND)
     A N15                                  DSPATR(HI)
     A                                 23 35'F10=Confirm'
     A  16                                  DSPATR(ND)
     A N16                                  DSPATR(HI)
     A  27                             23 47'F11=User Details'
     A  27                                  DSPATR(HI)
     A****                                        Start of /CFT007/
     A N76                             23 47'F16=BIC Search  '
     A N76                                  DSPATR(HI)
     A****                                          End of /CFT007/
     A                                 23 64'F12=Return'
     A                                      DSPATR(HI)
      *
     A*=========================================================================
      * -------------------------------------------------------------------*
      *   ERROR MESSAGE SUBFILE CONTROL                                    *
      * -------------------------------------------------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
