      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT CCT credit detail browse')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  FTM1021BDF - CUSTOMER CREDIT TRANSFER CREDIT DETAILS BROWSE  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. 178751             Date 09May00               *
      *                 178696             Date 08May00               *
      *                 177802             Date 14Apr00               *
      *                 CFT006  *CREATE    Date 07Dec99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  178751 - Auto generation of references                       *
      *  178696 - Amend Footer to show F12/F3                         *
      *  177802 - Amend F12 text to be Previous                       *
      *  CFT006 - FT MT101/MT102 development.                         *
      *                                                               *
      *****************************************************************
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
     A                                      INDARA
      *
     A          R FTM1021BS1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SFL
     A  95                                  SFLNXTCHG
     A            DDSEL          1A  B 12  2TEXT('Select option')
     A                                      DSPATR(UL)
     A*****************                         START/178751/
     A*N14*****                             DSPATR(HI)
     A**14*****                             DSPATR(RI)
     A**14*****                             DSPATR(PC)
     A N09                                  DSPATR(HI)
     A  09                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A            DDTSEQNO       3A  O 12  5DSPATR(HI)
     A******      DDTRNSID      16A  O 12  5DSPATR(HI)
     A            DDTRNSID      16A  O 12  9DSPATR(HI)
     A******      DDCCTCCY       3A  O 12 24DSPATR(HI)
     A            DDCCTCCY       3A  O 12 26DSPATR(HI)
     A******      DDCCTAMT      15A  O 12 29DSPATR(HI)
     A            DDCCTAMT      15A  O 12 31DSPATR(HI)
     A******      DDBENF1       35A  O 12 46DSPATR(HI)
     A            DDBENF1       33A  O 12 48DSPATR(HI)
     A*****************                           END/178751/
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN CONTROL                                            *
      * -------------------------------------------------------------------*
      *
     A          R FTM1021BS0                SFLCTL(FTM1021BS1)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A N81                                  CF09
     A                                      CF12
     A  92                                  SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND(*MORE)
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'FTM1021BF1'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 23'Customer Credit Transfer +
     A                                      Transaction List'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  2'Action'
     A            DDACTN         1A  B  4 10
     A                                      DSPATR(UL)
     A N10                                  DSPATR(HI)
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  4 15'Customer Credit Transfer +
     A                                      Id'
     A            DDCCTID       15A  O  4 44
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  5 15'Transaction Reference'
     A            DDTRNSIDX     16A  B  5 44
     A                                      DSPATR(UL)
     A N11                                  DSPATR(HI)
     A  11                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  6 15'SWIFT Reference'
     A            DDSWREF       28A  B  6 44
     A                                      DSPATR(UL)
     A N12                                  DSPATR(HI)
     A  12                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  6 73'Seq'
     A            DDSWSEQ        2A  B  6 77
     A                                      DSPATR(UL)
     A N13                                  DSPATR(HI)
     A  13                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A*****************                         START/178751/
     A                                  8  5'Crd'
     A*******                           8  5'Transaction'
     A                                  8  9'Transaction'
     A                                  9  1'Sel'
     A                                  9  5'Seq'
     A*******                           9  5'Reference'
     A                                  9  9'Reference'
     A*******                           9 24'Ccy'
     A                                  9 26'Ccy'
     A*******                           9 29'Amount'
     A                                  9 31'Amount'
     A*******                           9 46'Beneficiary'
     A                                  9 48'Beneficiary'
     A            DDSEQSEL       3A  B 10  5
     A                                      DSPATR(UL)
     A*******                               DSPATR(HI)
     A N14                                  DSPATR(HI)
     A  14                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A                                      OVRDTA
     A*******     DDTRNSEL      16A  B 10  5
     A            DDTRNSEL      16A  B 10  9
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A*******     DDBENFSEL     35A  B 10 46
     A            DDBENFSEL     33A  B 10 48
     A                                      DSPATR(UL)
     A*******                               DSPATR(HI)
     A N14                                  DSPATR(HI)
     A  14                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A                                      OVRDTA
     A*****************                           END/178751/
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R FTM1021BF1                TEXT('Select screen footer')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      ERASE(FTM1021BS1)
     A                                      PUTOVR
     A                                 22  2'E=Enquire'
     A                                 22 14'A=Amend'
     A  81                                  DSPATR(ND)
     A                                 22 24'D=Delete'
     A  81                                  DSPATR(ND)
     A                                 22 35'R=Reassign'
     A  81                                  DSPATR(ND)
      *********************************           Start 178696
     A******                           23  2'F3=Exit   F5=Refresh   +
      *********************************           Start 177802
     A*********************************     F9=Add   F12=Cancel'
     A******                                F9=Add   F12=Previous'
      *********************************           End   177802
     A**81***                               DSPATR(ND)
     A******                           23  2'F3=Exit   F5=Refresh   +
      *********************************           Start 177802
     A*********************************     F9=Add   F12=Cancel'
     A******                                F9=Add   F12=Previous'
      *********************************           End   177802
     A*N81***                               DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
     A  81                                  DSPATR(ND)
     A                                 23 25'F9=Add'
     A  81                                  DSPATR(ND)
     A                                 23 34'F12=Previous'
      *********************************           END   178696
      *                                                                ----*
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R FTM1021BM1                TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R FTM1021BM0                TEXT('MSG SUBFILE CONTROL')
     A                                      CF03
     A                                      SFLCTL(FTM1021BM1)
     A N94                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  93                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
