      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT CCT detail display screen')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FTM1022DDF - Customer Credit Transfer Maintenance       *
      *                    Credit Details                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CSW207             Date 07Jun07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. 188649             Date 09Feb01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 184060             Date 22Sep00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 178751             Date 09May00               *
      *                 CFT007             Date 21MAR00               *
      *                 CFT006  *CREATE    Date 22Nov99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW207 - Swift 2007 Changes                                  *
      *  188649 - Add authorised text to screen                       *
      *  184060 - No transaction type code in detail screen           *
      *  178751 - Auto generation of references                       *
      *  CFT007 - BIC DATABASE PLUS                                   *
      *  CFT006 - Swift messages MT101/MT102                          *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
     A*
     A          R FTM1022DF1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A****                                          Begin  /CFT007/
     A  82                                  CF04(04)
     A  82                                  CF16(16)
     A****                                          End    /CFT007/
     A N72                                  CF05
     A N73                                  CF10
     A                                      CF11
     A                                      CF12
     A****                                          Begin  /CFT007/
     A                                      RTNCSRLOC(&CSRREC &CSRFLD &CSRPOS)
     A            CSRPOS         4S 0H
     A            CSRFLD        10A  H
     A            CSRREC        10A  H
     A****                                          End    /CFT007/
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FTM1022DF1'
     A N78                              1 25'Incoming Customer Credit Transfer'
     A                                      DSPATR(UL HI)
     A  78                              1 25'Outgoing Customer Credit Transfer'
     A                                      DSPATR(UL HI)
     A                                  1 67'Date'
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 26'Transaction Details'
     A                                  2 46'Mode'
     A            SACTN          1A  O  2 52DSPATR(HI)
     A                                  2 67'Time'
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
     A                                      TEXT('Current Time')
     A                                  4  2'Customer Credit Transfer ID'
     A            SCCTID        15A  B  4 30
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(RI)
     A  29                                  DSPATR(PC)
     A  74                                  DSPATR(PR)
     A N74                                  DSPATR(UL)
      *
      *                                              Begin /178751/
     A            STSEQNO        3A  O  4 46DSPATR(HI)
     A**********************************4 47'File Reference'
     A                                  4 52'File Ref.'
      *                                              End   /178751/
      *
     A            SFILREF       16A  O  4 63DSPATR(HI)
     A                                  5  2'Transaction Type'
     A            SCCTTYP        2A  O  5 30DSPATR(HI)
     A                                  5 40'Transaction Reference'
     A            STRNSID       16A  B  5 63
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *                                              Begin /184060/
     A                                  6  2'Transaction Type Code'
     A            SCCTTYC        3A  B  6 30
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
      *                                              End   /184060/
     A                                  7  2'Beneficiary'
     A            SBENF1        35A  B  7 40
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SBENF2        35A  B  8  2
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SBENF3        35A  B  8 40
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SBENF4        35A  B  9  2
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SBENF5        35A  B  9 40
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 11  2'Account with Institution'
     A            SACCI35       35A  B 11 28
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SACCI11       11A  B 11 64
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 12  2'Ordering Institution'
     A            SOINST        35A  B 12 28
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SOINS1        11A  B 12 64
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 13  2'Ordering Customer'
     A            SORDCU1       35A  B 14  2
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SORDCU2       35A  B 14 38
     A  41                                  DSPATR(RI)
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SORDCU3       35A  B 15  2
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SORDCU4       35A  B 15 38
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
      ** Start of Changes CSW207
     A            SORDCU5       35A  B 16  2
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A N84
     AO 71                                  DSPATR(PR)
     A N84                                  DSPATR(ND)
     A  84
     AAN71                                  DSPATR(UL)
      ** End of Changes CSW207
     A                                 17  2'Details of Charges'
     A            SCCYCHG        3A  B 17 22
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 18  2'Transfer Amount'
     A            SCCTAMT       15A  B 18 22
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 18 41'Currency'
     A            SCCTCCY        3A  B 18 50
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 18 54'Credit Date'
     A            SCRDVDT        6A  B 18 67
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 19  2'Exchange Rate'
     A            SXRATE        14A  B 19 22
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 19 41'Exchange Rate Override'
     A            SXRATEO        1A  B 19 65
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SORIGAM       15A  O 20 22
     A  28                                  DSPATR(ND)
     A            SORIGCY        3A  O 20 50
     A  28                                  DSPATR(ND)
     A                                 21  2'Payment Amount'
     A            SPAYAMT       15A  B 21 22
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 21 41'Currency'
     A            SPAYCCY        3A  B 21 50
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 23  2'F3=Exit'
     A****                                         Begin   /CFT007/
     A N76                             23 10'F4=BIC Prompt'
     A****                                         End     /CFT007/
     A                                 23 10'F5=Refresh'
     A  72                                  DSPATR(ND)
     A N73                             23 35'F10=Confirm'
     A****                                         Begin   /CFT007/
     A N76                             23 35'F16=BIC Search'
     A****                                         End     /CFT007/
     A                                 23 50'F11=Next Screen'
     A                                 23 66'F12=Cancel'
      ************************************************ Start 188649
     A            SAIND         20   O  3 52DSPATR(HI)
      ************************************************ End   188649
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PSPROCNAME                SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  77
     AON77                                  SFLEND
     A            PSPROCNAME                SFLPGMQ
