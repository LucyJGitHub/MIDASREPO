      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT CCT header display screen')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FTM1025DDF- Customer Credit Transfer Maintenance        *
      *                   Debit Details                               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CSW207             Date 07Jun07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. 188649             Date 09Feb01               *
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 177802             Date 14Apr00               *
      *                 CFT007             Date 21MAR00               *
      *                 CFT006  *CREATE    Date 21Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW207 - Swift 2007 Changes                                  *
      *  188649 - Show authorised text on screen                      *
      *  177802 - Amend F12 = Previous                                *
      *  CFT007 - BIC DATABASE PLUS                                   *
      *  CFT006 - Swift messages MT101/MT102                          *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
     A*
     A          R FTM1025DF1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A****                                          Begin  /CFT007/
     A  82                                  CF04(04)
     A  82                                  CF16(16)
     A****                                          End    /CFT007/
     A N72                                  CF05
     A N73                                  CF10
     A                                      CF11
     A                                      CF12
     A****                                          Begin  /CFT007/
     A                                      RTNCSRLOC(&CSRREC &CSRFLD &CSRPOS)
     A            CSRPOS         4S 0H
     A            CSRFLD        10A  H
     A            CSRREC        10A  H
     A****                                          End    /CFT007/
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FTM1025DF1'
     A N78                              1 25'Incoming Customer Credit Transfer'
     A                                      DSPATR(UL HI)
     A  78                              1 25'Outgoing Customer Credit Transfer'
     A                                      DSPATR(UL HI)
     A                                  1 67'Date'
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 26'Debit Details'
     A                                  2 46'Mode'
     A            SACTN          1A  O  2 52DSPATR(HI)
     A                                  2 67'Time'
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
     A                                      TEXT('Current Time')
     A                                  4  2'Customer Credit Transfer ID'
                  SCCTID        15A  O  4 30DSPATR(HI)
     A                                  5  2'File Reference'
     A            SFILREF       16A  B  5 17
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A  28                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                  5 38'Transfer Type'
     A            STRNTYP        2A  O  5 52DSPATR(HI)
     A                                  5 56'Transfer Sub Type'
     A            STRNSTP        2A  B  5 75
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                  7  2'Booking Branch'
     A            SBRCA          3A  B  7 18
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                  7 23'Originating Branch'
     A            SORBR          3A  B  7 43
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                  7 48'Book Code'
     A            SBOKC          2A  B  7 58
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                  7 62'Profit Centre'
     A            SPRFC          4A  B  7 77
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                  8  2'Swift Priority'
     A            SSWPRIO        1A  B  8 18
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                  9  2'Ordering Institution'
     A            SOINST        35A  B  9 23
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SOINS1        11A  B  9 59
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 10  2'Ordering Customer'
     A** Start of change CSW207
     A**********  STOCUS1       35A  B 11  2                                                  CSW207
     A            STOCUS1       35A  B 10 38
     A** End of change CSW207
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A** Start of change CSW207
     A**********  STOCUS2       35A  B 11 38                                                  CSW207
     A            STOCUS2       35A  B 11  2
     A** End of change CSW207
     A  39                                  DSPATR(RI)
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A** Start of change CSW207
     A**********  STOCUS3       35A  B 12  2                                                  CSW207
     A            STOCUS3       35A  B 11 38
     A** End of change CSW207
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A** Start of change CSW207
     A**********  STOCUS4       35A  B 12 38                                                  CSW207
     A            STOCUS4       35A  B 12  2
     A** End of change CSW207
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A** Start of change CSW207
     A            STOCUS5       35A  B 12 38
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A** End of change CSW207
     A                                 13  2'Sending Institution'
     A            SSINST        35A  B 13 27
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 15  2'Destination'
     A            SCCTDST       35A  B 15 27
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 16  2'Sender''s Correspondent'
     A            SSNDCOR       35A  B 16 27
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 17  2'Receiver''s Correspondent'
     A            SRCVCOR       35A  B 17 27
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 19  2'Debit Date'
     A            SDBTVDT        6A  B 19 13
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 20  2'Transfer Amount'
     A            SCCTAMT       16A  B 20 27
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 20 45'Currency'
     A            SCCTCCY        3A  B 20 54
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 20 59'Transfer Date'
     A            STTVDT         6A  B 20 73
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 21  2'Exchange Rate'
     A            SCCTRT        14A  B 21 27
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 22  2'Sum of Amounts'
     A            SSUMAMT       16A  O 22 27
     A                                 23  2'F3=Exit'
     A****                                          Begin  /CFT007/
     A N76                             23 10'F4=BIC Prompt'
     A****                                          End    /CFT007/
     A                                 23 24'F5=Refresh'
     A  72                                  DSPATR(ND)
     A N73                             23 35'F10=Confirm'
     A****                                          Begin  /CFT007/
     A N76                             23 35'F16=BIC Search'
     A****                                          End    /CFT007/
     A                                 23 50'F11=Next Screen'
      *******************************         Start 177802
     A*******************************  23 66'F12=Cancel'
     A                                 23 66'F12=Previous'
      *******************************         End   177802
      ************************************************ Start 188649
     A            SAIND         20   O  3 53DSPATR(HI)
      ************************************************ End   188649
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PSPROCNAME                SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  77
     AON77                                  SFLEND
     A            PSPROCNAME                SFLPGMQ
