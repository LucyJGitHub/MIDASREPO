/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT CCT header display screen')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FTM1026DDF - Customer Credit Transfer Maintenance       *
      *                    Debit Details                              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CSW209             Date 01Apr09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 192765             Date 01May01               *
      *                 188649             Date 09Feb01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 184417             Date 02Oct00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 177802             Date 14Apr00               *
      *                 CFT007             Date 21MAR00               *
      *                 CFT006  *CREATE    Date 21SEP99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW209 - SWIFT 2009 Changes                                  *
      *  192765 - Field Reference File MFRFL not used                 *
      *  188649 - Show authorised text on screen                      *
      *  184417 - Transcaction Type Code incorrectly labeled.         *
      *  177802 - Amend F12 text                                      *
      *  CFT007 - BIC DATABASE PLUS                                   *
      *  CFT006 - Swift messages MT101/MT102                          *
      *                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*
     A          R FTM1026DF1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A****                                          Begin  /CFT007/
     A  82                                  CF04(04)
     A  82                                  CF16(16)
     A****                                          End    /CFT007/
     A N72                                  CF05
     A N73                                  CF10
     A                                      CF11
     A                                      CF12
     A****                                          Begin  /CFT007/
     A                                      RTNCSRLOC(&CSRREC &CSRFLD &CSRPOS)
     A            CSRPOS         4S 0H
     A            CSRFLD        10A  H
     A            CSRREC        10A  H
     A****                                          End    /CFT007/
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FTM1026DF1'
     A N78                              1 25'Incoming Customer Credit Transfer'
     A                                      DSPATR(UL HI)
     A  78                              1 25'Outgoing Customer Credit Transfer'
     A                                      DSPATR(UL HI)
     A                                  1 67'Date'
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 26'Debit Details'
     A                                  2 46'Mode'
     A            SACTN          1A  O  2 52DSPATR(HI)
     A                                  2 67'Time'
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
     A                                      TEXT('Current Time')
     A                                  4  2'Customer Credit Transfer ID'
                  SCCTID        15A  O  4 30DSPATR(HI)
     A                                  5  2'File Reference'
     A            SFILREF       16A  O  5 17DSPATR(HI)
     A                                  5 38'Transfer Type'
     A            SCCTTYP        2A  O  5 52DSPATR(HI)
     A                                  5 56'Transfer Sub Type'
     A            SCCTTST        2A  O  5 75DSPATR(HI)
     A                                  7  2'Details of Charges'
     A            SCCTCHG        3A  B  7 22
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SSUMSCG       14A  O  8 28
     A  28                                  DSPATR(ND)
     A            SSUMSCY        3A  O  8 54
     A  28                                  DSPATR(ND)
     A                                  9  2'Sum of Receiver''s Charges'
     A            SSUMRCH       14A  O  9 28
     A                                  9 44'Currency'
     A            SSUMRCY        3A  O  9 54
     A*                                                  /*Start 184417*/
     A*********************************10  2'Transfer Type'
     A                                 10  2'Transaction Type Code'
     A************STRNSTP********3A  B 10 17
     A            STRNSTP        3A  B 10 25
     A*                                                  /*End   184417*/
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 11  2'Regulatory Reporting'
     A            SPREGP1       35A  B 11 39
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SPREGP2       35A  B 12  2
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SPREGP3       35A  B 12 39
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 14  2'Bank Operation Code'
     A            SCCTBOC       16A  B 14 28
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                 15  2'Additional Information'
     A            SADIF1        35A  B 16  2
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SADIF2        35A  B 16 39
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SADIF3        35A  B 17  2
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SADIF4        35A  B 17 39
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SADIF5        35A  B 18  2
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            SADIF6        35A  B 18 39
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
      **Start of change CSW209
     A N78 79                          20  2'Network'
     A N78 79     SNTWRK         6A  B 20 10
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A N78 79                          20 19'Message Type'
     A N78 79     SMSGTY         3A  B 20 32
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A N78 79                          20 39'Validation Flag'
     A N78 79     SVLFLG         8A  B 20 55
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  78 79                          20  2'Validation Flag (Main)'
     A  78 79     SCOMVF         8A  O 20 25
     A  78 79                          20 39'Validation Flag (Cover)'
     A  78 79     SCOCVF         8A  O 20 63
      **End of change CSW209
      *-------------------------------------------------------------------------
     A                                 23  2'F3=Exit'
     A                                 23 10'F5=Refresh'
     A  72                                  DSPATR(ND)
     A                                 23 23'F10=Confirm'
     A  73                                  DSPATR(ND)
     A  74                             23 36'F11=User Details'
      *****************************               Start 177802
     A*****************************    23 54'F12=Cancel'
     A                                 23 54'F12=Previous'
      *****************************               End   177802
      ************************************************ Start 188649
     A            SAIND         20   O  3 53DSPATR(HI)
      ************************************************ End   188649
     A*=========================================================================
     A          R FTM1026DF2
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      CF12
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FTM1026DF2'
     A N78                              1 25'Incoming Customer Credit Transfer'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  78                              1 25'Outgoing Customer Credit Transfer'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date'
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 26'Debit Details'
     A                                  2 46'Mode'
     A            SACTN          1A  O  2 52DSPATR(HI)
     A                                  2 67'Time'
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
     A                                      TEXT('Current Time')
     A                                  4  2'Customer Credit Transfer ID'
     A            SCCTID        15A  O  4 30DSPATR(HI)
     A                                  5  2'File Reference'
     A            SFILREF       16A  O  5 17DSPATR(HI)
     A                                  5 38'Transfer Type'
     A            SCCTTYP        2A  O  5 52DSPATR(HI)
     A                                  5 56'Transfer Sub Type'
     A            SCCTTST        2A  O  5 75DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 10'F5=Refresh'
     A  72                                  DSPATR(ND)
     A                                 23 23'F10=Confirm'
     A  73                                  DSPATR(ND)
     A                                 23 54'F12=Cancel'
     A                                  8  2'Input User'
     A*                                                                    /*Start 192765*/
     A************SINUSER   R        O  8 14REFFLD(FT102HD0/INUSER *LIBL/FT102H-
     A************                          PD)
     A            SINUSER   R        O  8 14REFFLD(RGUD)
     A*                                                                    /* End  192765*/
     A                                  8 25'Date'
     A*                                                                    /*Start 192765*/
     A************SINDATE   R        O  8 30REFFLD(FT102HD0/INDATE *LIBL/FT102H-
     A************                          PD)
     A            SINDATE   R        O  8 30REFFLD(LASD)
     A*                                                                    /* End  192765*/
     A                                  8 37'Time'
     A*                                                                    /*Start 192765*/
     A************SINTIME   R        O  8 42REFFLD(FT102HD0/INTIME *LIBL/FT102H-
     A************                          PD)
     A            SINTIME   R        O  8 42REFFLD(LATM)
     A*                                                                    /* End  192765*/
     A                                 10  2'Update'
     A                                 10  9'User'
     A*                                                                    /*Start 192765*/
     A************SUPDUSR   R        O 10 14REFFLD(FT102HD0/UPDUSR *LIBL/FT102H-
     A************                          PD)
     A            SUPDUSR   R        O 10 14REFFLD(RGUD)
     A*                                                                    /* End  192765*/
     A                                 10 25'Date'
     A*                                                                    /*Start 192765*/
     A************SUPDDTE   R        O 10 30REFFLD(FT102HD0/UPDDTE *LIBL/FT102H-
     A************                          PD)
     A            SUPDDTE   R        O 10 30REFFLD(LASD)
     A*                                                                    /* End  192765*/
     A                                 10 37'Time'
     A*                                                                    /*Start 192765*/
     A************SUPDTIM   R        O 10 42REFFLD(FT102HD0/UPDTIM *LIBL/FT102H-
     A************                          PD)
     A************SAUP1     R        O 12 26REFFLD(FT102HD0/AUP1 *LIBL/FT102HPD)
     A************SAUP2     R        O 13 26REFFLD(FT102HD0/AUP2 *LIBL/FT102HPD)
     A            SUPDTIM   R        O 10 42REFFLD(LATM)
     A            SAUP1     R        O 12 26REFFLD(RGUD)
     A            SAUP2     R        O 13 26REFFLD(RGUD)
     A*                                                                    /* End  192765*/
     A                                 12  3'First Authorising User'
     A                                 13  2'Second Authorising User'
     A                                 15  2'Authorised Indicator'
     A*                                                                    /*Start 192765*/
     A************SAUIN     R        O 15 35REFFLD(FT102HD0/AUIN *LIBL/FT102HPD)
     A************SBAUTH    R        O 17 32REFFLD(FT102HD0/BAUTH *LIBL/FT102HP-
     A************                          D)
     A            SAUIN     R        O 15 35REFFLD(CRLF)
     A            SBAUTH         4   O 17 32
     A*                                                                    /* End  192765*/
     A                                 17  2'Batch Authorised Number'
     A                                 19  2'Batch Authorising User'
     A*                                                                    /*Start 192765*/
     A************SBATUSR   R        O 19 26REFFLD(FT102HD0/BATUSR *LIBL/FT102H-
     A************                          PD)
     A            SBATUSR   R        O 19 26REFFLD(RGUD)
     A*                                                                    /* End  192765*/
     A                                 15 37'Dual Authorisation Required'
     A*                                                                    /*Start 192765*/
     A************SCDUAL    R        O 15 65REFFLD(FT102HD0/CDUAL *LIBL/FT102HP-
     A************                          D)
     A            SCDUAL    R        O 15 65REFFLD(CRLF)
     A*                                                                    /* End  192765*/
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PSPROCNAME                SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  77
     AON77                                  SFLEND
     A            PSPROCNAME                SFLPGMQ
