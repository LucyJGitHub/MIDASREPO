      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Outgoing Payments Margins Info Display')      *
      *****************************************************************
      *                                                               *
      *  Midas - API Funds Transfer Module                            *
      *                                                               *
      *  DSPF/FTOPAY5DF - Outgoing Payments Margins Informations      *
      *                   Display.                                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER030             Date 09Jul08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CFT014  *CREATE    Date 25May00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER030 - Multicash German Feature                            *
      *  CFT014 - Straight-through processing Phase 2 MT103           *
      *           Messages Generation for FT.                         *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(23)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Message Subfile Format
      *
     A
     A          R FTOPAY5S1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Message subfile control
      *
     A          R FTOPAY5S0                 TEXT('Msg Subfile Control')
     A                                      CF03
     A  71                                  CF05
     A                                      CF12
     A                                      SFLCTL(FTOPAY5S1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      ** Main Display Screen
      *
     A          R FTOPAY5D1
     A                                      CF03
     A  71                                  CF05
     A                                      CF12
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      ** FX Margin Pts
      ** FX P/S Ind
      ** Margin Sign
      *
      *
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'FTOPAY5'
     A                                  1 33'Outgoing Payments Input'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  3  1'Action Code'
     A            SACTION        1A  O  3 13TEXT('ACTION CODE')
     A N10                                  DSPATR(HI)
     A                                      OVRDTA
     A                                  3 16'Payment Reference'
     A            SPREF         15A  O  3 41
     A N11                                  DSPATR(HI)
     A                                      OVRDTA
     A            DDSTAT        10A  O  3 64DSPATR(HI)
     A                                      OVRDTA
     A                                  5 02'FX Margin Pts'
      ** Start of Change CER030
     A N75                                  DSPATR(ND)
      ** End of Change CER030
     A                                      OVRATR
     A            SFXMP          8A  B  5 16
      ** Start of Change CER030
     A N75                                  DSPATR(ND)
      ** End of Change CER030
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
      ** Start of Change CER030
     AON75
      ** End of Change CER030
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  6 02'FX P/S Ind'
      ** Start of Change CER030
     A N75                                  DSPATR(ND)
      ** End of Change CER030
     A                                      OVRATR
     A            SMBSI          1A  B  6 16
      ** Start of Change CER030
     A N75                                  DSPATR(ND)
      ** End of Change CER030
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
      ** Start of Change CER030
     AON75
      ** End of Change CER030
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  7 02'Margin Sign'
      ** Start of Change CER030
     A N75                                  DSPATR(ND)
      ** End of Change CER030
     A                                      OVRATR
     A            SMSIG          1A  B  7 16
      ** Start of Change CER030
     A N75                                  DSPATR(ND)
      ** End of Change CER030
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
      ** Start of Change CER030
     AON75
      ** End of Change CER030
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      ** Start of change CER030
     A  72                              8 02'Transaction Type'
     A                                      OVRATR
     A            SRTTY          5A  B  8 19
     A N72                                  DSPATR(ND)
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     AON72
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A  73                              9  2'Extended Narrative +
     A                                      (Debit)'
     A                                      OVRATR
     A            SEXT1         65A  B 10 11
     A N73                                  DSPATR(ND)
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     AON73
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A            SEXT2         65A  B 11 11
     A N73                                  DSPATR(ND)
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     AON73
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A            SEXT3         65A  B 12 11
     A N73                                  DSPATR(ND)
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     AON73
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A            SEXT4         65A  B 13 11
     A N73                                  DSPATR(ND)
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     AON73
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A            SEXT5         65A  B 14 11
     A N73                                  DSPATR(ND)
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     AON73
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A            SEXT6         65A  B 15 11
     A N73                                  DSPATR(ND)
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     AON73
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A  74                             16  2'Extended Narrative +
     A                                      (Credit)'
     A                                      OVRATR
     A            SEXC1         65A  B 17 11
     A N74                                  DSPATR(ND)
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     AON74
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A            SEXC2         65A  B 18 11
     A N74                                  DSPATR(ND)
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     AON74
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SEXC3         65A  B 19 11
     A N74                                  DSPATR(ND)
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     AON74
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A            SEXC4         65A  B 20 11
     A N74                                  DSPATR(ND)
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     AON74
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A            SEXC5         65A  B 21 11
     A N74                                  DSPATR(ND)
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     AON74
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A            SEXC6         65A  B 22 11
     A N74                                  DSPATR(ND)
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     AON74
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
      ** End Of Change CER030
      *
      ** Main Screen Footer
      *
     A          R FTOPAY5F1                 TEXT('Screen Footer')
     A                                      CF03
     A  71                                  CF05
     A                                      CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A            SCCMDTXT      79A    24  2OVRDTA
