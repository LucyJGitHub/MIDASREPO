      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas FT Outgoing payments maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - API Funds Transfer Module                            *
      *                                                               *
      *  FTOPAY6DF - Outgoing Payments Initial Insert Screen          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. 217769             Date 09Jun03               *
      *                 CFT018  *CREATE    Date 29Nov02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  217769 - OPSS - Can not Exit if invalid action entered       *
      *  CFT018 - Standard Settlement Instructions for                *
      *           Outgoing Payments                                   *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A          R FTOPAYS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2002')
     A                                      SFL
     A  84                                  SFLNXTCHG
      **Start of change CGL029
     A**********  SOPTION        1   B  8  7TEXT('SELECT OPTION')                             CGL029
     A            SOPTION        1   B  8  4TEXT('SELECT OPTION')
      **End of change CGL029
      * Start of change 217769
     A**********                            VALUES('I' ' ')                                   217769
      * End of change 217769
     A N85                                  DSPATR(UL HI)
     A  85                                  DSPATR(RI)
      **Start of change CGL029
     A**********  SDRCY          3A  O  8 11TEXT('Debit Ccy')                                 CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A**********  SDRAC         15A  O  8 18TEXT('Debit Account')                             CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A            SDRCY          3A  O  8  8TEXT('Debit Ccy')
     A                                      DSPATR(HI)
     A            SDRAC         21A  O  8 13TEXT('Debit Account')
     A                                      DSPATR(HI)
      **End of change CGL029
     A            SREF1         10A  O  8 36TEXT('Reference I')
     A                                      DSPATR(HI)
     A            SREF2         10A  O  8 49TEXT('Reference II')
     A                                      DSPATR(HI)
     A            SCRCY          3A  O  8 63TEXT('Pay Ccy')
     A                                      DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * Subfile Control Format                                           *
      * -------------------------------------------------------------------*
     A          R FTOPAYS0                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF09
     A                                      CF15
     A                                      SFLCTL(FTOPAYS1)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  89                                  SFLEND
     A  87                                  SFLCLR
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A N89                                  ROLLUP(88)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSREC         4  0H      SFLRCDNBR
      *
     A                                  1  2'FTOPAY6'
     A                                  1 29'Outgoing Payments Input'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            DDMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 35'Templates'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
      *
      **Start of change CGL029
     A**********                        4 11'Debit'                                           CGL029
     A**********                        4 18'Debit'                                           CGL029
     A                                  4  8'Dr'
     A                                  4 13'Dr'
      **End of change CGL029
     A                                  4 63'Pay'
      *
      **Start of change CGL029
     A**********                        5  6'Sel'                                             CGL029
     A**********                        5 11'Ccy'                                             CGL029
     A**********                        5 18'Account'                                         CGL029
     A                                  5  3'Sel'
     A                                  5  8'Ccy'
     A                                  5 13'Account'
      **End of change CGL029
     A                                  5 36'Reference I'
     A                                  5 49'Reference II'
     A                                  5 63'Ccy'
      *
      ** Input fields to position sub-file
      **Start of change CGL029
     A**********  S1DRCY         3A  B  6 11TEXT('Debit Ccy')                                 CGL029
     A**********                            DSPATR(UL HI)                                     CGL029
     A**********  S1DRAC        15A  B  6 18TEXT('Debit Account')                             CGL029
     A**********                            DSPATR(UL HI)                                     CGL029
     A            S1DRCY         3A  B  6  8TEXT('Debit Ccy')
     A                                      DSPATR(UL HI)
     A            S1DRAC        21A  B  6 13TEXT('Debit Account')
     A                                      DSPATR(UL HI)
      **End of change CGL029
     A            S1REF1        10A  B  6 36TEXT('Reference I')
     A                                      DSPATR(UL HI)
     A            S1REF2        10A  B  6 49TEXT('Reference II')
     A                                      DSPATR(UL HI)
     A            S1CRCY         3A  B  6 63TEXT('Pay Ccy')
     A                                      DSPATR(UL HI)
      *
      * -------------------------------------------------------------------*
      *   SCREEN FOOTER                                                    *
      * -------------------------------------------------------------------*
     A          R FTOPAYF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF09
     A                                      CF15
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A                                 22  2'I=Insert Outgoing Payment'
     A                                 23  2'F3=Exit'
     A                                 23 12'F15=Existing payments'
     A                                 23 36'F9=Insert Outgoing Payment +
     A                                      without Template'
      *
      *--------------------------------------------------------------------*
      *  Message Subfile Format                                            *
      *--------------------------------------------------------------- ----*
     A          R FTOPAYM1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      *--------------------------------------------------------------- ----*
      *  Message Subfile Control Format                                    *
      *--------------------------------------------------------------- ----*
     A          R FTOPAYM0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF09
     A                                      CF15
     A                                      SFLCTL(FTOPAYM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      BLINK
     A            DDPGMQ                    SFLPGMQ
