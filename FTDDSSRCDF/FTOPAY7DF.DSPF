/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas FT Outgoing Payments Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - API Funds Transfer Module                            *
      *                                                               *
      *  FTOPAY7DF - Outgoing Payments Initial Insert Screen          *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD035776           Date 10Sep15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      *                 CFT018  *CREATE    Date 29Nov02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD035776 - User need to press twice on input screen          *
      *           - Remove unsupported keywords of HATS.              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CFT018 - Standard Settlement Instructions for                *
      *           Outgoing Payments                                   *
      *                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R FTOPAY7D1
     A                                      CF03
     A  68                                  CF06
     A                                      CF12
      **Start of change MD035776
     A**********                            FRCDTA                                          MD035776
      **End of change MD035776
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                  1  2'FTOPAY7'
     A                                  1 32'Outgoing Payments Input'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            DDMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
      *
      *
     A                                  4  2'Debit Account'
      **Start of change CGL029
     A**********  SDRAC         15A  B  4 20                                                  CGL029
     A            SDRAC         21A  B  4 17
      **End of change CGL029
     A  11                                  DSPATR(RI)
     A N11                                  DSPATR(HI)
     A  11N69                               DSPATR(PC)
     A  68
     AO 69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
      **Start of change CGL029
     A**69******  SDRNM         20A  O  5 20DSPATR(HI)                                        CGL029
     A  69        SDRNM         20A  O  5 17DSPATR(HI)
      **End of change CGL029
      *
     A                                  4 41'Credit Account'
      **Start of change CGL029
     A**********  SCRAC         15A  B  4 59                                                  CGL029
     A            SCRAC         21A  B  4 57
      **End of change CGL029
     A  12                                  DSPATR(RI)
     A N12                                  DSPATR(HI)
     A  12N69                               DSPATR(PC)
     A  68
     AO 69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
      **Start of change CGL029
     A**69******  SCRNM         20A  O  5 59DSPATR(HI)                                        CGL029
     A  69        SCRNM         20A  O  5 57DSPATR(HI)
      **End of change CGL029
      *
     A                                  7  2'Settle Ccy'
     A            SDRCY          3A  B  7 20
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(HI)
     A  13N69                               DSPATR(PC)
     A  68
     AO 69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  7 41'Pay Ccy'
     A            SCRCY          3A  B  7 59
     A  14                                  DSPATR(RI)
     A N14                                  DSPATR(HI)
     A  14N69                               DSPATR(PC)
     A  68
     AO 69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  8  2'Settle Amount'
     A            SSMAM         14A  B  8 20
     A  15                                  DSPATR(RI)
     A N15                                  DSPATR(HI)
     A  15                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  8 41'Pay Amount'
     A            SPYAM         14A  B  8 59
     A  16                                  DSPATR(RI)
     A N16                                  DSPATR(HI)
     A  16                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  9  2'Settle Value Date'
     A            SPYDT          6A  B  9 20
     A  17                                  DSPATR(RI)
     A N17                                  DSPATR(HI)
     A  17                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  9 41'Pay Value Date'
     A            SPYVD          6A  B  9 59
     A  18                                  DSPATR(RI)
     A N18                                  DSPATR(HI)
     A  18                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 11  2'Rate'
     A            SRATE         14A  B 11 20
     A  19                                  DSPATR(RI)
     A N19                                  DSPATR(HI)
     A  19                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 11 41'Override'
     A            SOVRD          1A  B 11 59
     A  20                                  DSPATR(RI)
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 13  2'Details of Payment'
     A            SDTP1         35A  B 14  3
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A            SDTP2         35A  B 14 41
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A            SDTP3         35A  B 15  3
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A            SDTP4         35A  B 15 41
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 17  2'Bank to Bank Info'
     A            SBBI1         35A  B 18  3
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A            SBBI2         35A  B 18 41
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A            SBBI3         35A  B 19  3
     A  27                                  DSPATR(RI)
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A            SBBI4         35A  B 19 41
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A            SBBI5         35A  B 20  3
     A  29                                  DSPATR(RI)
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A            SBBI6         35A  B 20 41
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A* -------------------------------------------------------------------*
     A* * SCREEN FOOTER                                                    *
     A* -------------------------------------------------------------------*
     A          R FTOPAY7F1                 TEXT('SCREEN FOOTER')
     A                                      CF03
     A  68                                  CF06
     A                                      CF12
     A                                 23  2'F3=Exit  F12=Cancel'
     A  68                             23 35'F6=Insert Payment'
     A                                      DSPATR(HI)
     A  68                             23 54'Enter=Payment Details'
     A                                      DSPATR(HI)
      *
      *--------------------------------------------------------------------*
      *  Message Subfile Format                                            *
      *--------------------------------------------------------------- ----*
     A          R FTOPAY7S1                 TEXT('(c) Finastra International +
     A                                      Limited 2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      *--------------------------------------------------------------- ----*
      *  Message Subfile Control Format                                    *
      *--------------------------------------------------------------- ----*
     A          R FTOPAY7S0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A  68                                  CF06
     A                                      CF12
     A                                      SFLCTL(FTOPAY7S1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
