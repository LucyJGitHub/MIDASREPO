/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas FT Outgoing payments browse')
     A*****************************************************************
     A*                                                               *
      *  Midas - API Funds Transfer Module                            *
     A*                                                               *
      *  FTOPAYBDF - Outgoing Payments Browse                         *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD054957           Date 11Dec19               *
      *  Prev Amend No. 259456             Date 29Jan13               *
      *                 ERZ123             Date 29Jan13               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 185023             Date 13Oct00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP031  *CREATE    Date 20May99               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD054957 - RBRU SP21 Compatibility Patch (Recompile)         *
      *  259456 - ERZ123 : Add dummy record format                    *
      *           Amended hook: FTH00617                              *
      *  ERZ123 - FT Select Payment on Beneficiary Line 1             *
      *           Added hook: FTH00617                                *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  185023 - Replace DBA2 help tag references with DBA3 ones.    *
     A*  CAP031 - Conversion of FT inputs into modular structure to   *
     A*           use as APIs.                                        *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
      **Start of change 185023
      **End of change 185023
     A          R FTOPAYS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1   B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDPREF        15A  O  8  4TEXT('Payment reference')
     A                                      DSPATR(HI)
 
     A**Start of change CGL029
     A**********  DDDEST        14A  O  8 20TEXT('Destination')                               CGL029
     A            DDDEST        24A  O  8 20TEXT('Destination')
     A**End of change CGL029
 
     A**Start of change CGL029
     A**********  DDVALD         7A  O  8 35TEXT('Value date')                                CGL029
     A            DDVALD         7A  O  8 50TEXT('Value date')
     A**End of change CGL029
 
     A**Start of change CGL029
     A**********  DDCCY          3A  O  8 43TEXT('Currency')                                  CGL029
     A            DDCCY          3A  O  8 58TEXT('Currency')
     A**End of change CGL029
 
     A**Start of change CGL029
     A**********  DDAMNT        17A  O  8 47TEXT('Amount')                                    CGL029
     A            DDAMNT        17A  O  8 62TEXT('Amount')
     A**End of change CGL029
 
     A**Start of change CGL029
     A**********  DDORBC        12A  O  8 65TEXT('Ordering bank/customer')                    CGL029
      **********                                                                              CGL029
     A**********  DDSETL         2A  O  8 78TEXT('Settlement method')                         CGL029
     A            DDORBC        21A  O  9  6TEXT('Ordering bank/customer')
     A            DDSETL         2A  O  9 29TEXT('Settlement method')
     A**End of change CGL029
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R FTOPAYS0                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLCTL(FTOPAYS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
      **Start of change CGL029
     A**********                            SFLSIZ(15)                                        CGL029
     A**********                            SFLPAG(14)                                        CGL029
     A                                      SFLSIZ(8)
     A                                      SFLPAG(7)
      **End of change CGL029
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      **Start of change 185023
      **End of change 185023
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'FTOPAYB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 28'FT OUTGOING PAYMENTS INPUT'
     A                                  4 37'ENQUIRY'
     A                                  6  1'Sel'
     A                                  6  5'Payment ref'
     A                                  6 21'Destination'
     A**Start of change CGL029
     A**********                        6 35'Val date'                                        CGL029
     A**********                        6 44'Ccy'                                             CGL029
     A**********                        6 55'Amount'                                          CGL029
     A**********                        6 65'Ord bnk/cus'                                     CGL029
     A**********                        6 77'Setl'                                            CGL029
     A                                  6 50'Val date'
     A                                  6 59'Ccy'
     A                                  6 70'Amount'
     A                                  7  7'Ord Bank/Customer'
     A                                  7 28'Setl'
      **End of change CGL029
 
     A          R FTOPAYS3                  TEXT('SELECT SCREEN SUBFILE')
     A                                      SFL
     A            DDOPT          1   B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFODN        20A     8  4TEXT('FRONT OFFICE ID.')
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  DDDEST        14A  O  8 25TEXT('Destination')                               CGL029
     A**End of change CGL029
 
     A            DDVALD         7A  O  8 40TEXT('Value date')
 
     A            DDCCY          3A  O  8 48TEXT('Currency')
 
     A            DDAMNT        17A  O  8 52TEXT('Amount')
 
     A            DDSETL         2A  O  8 70TEXT('Settlement method')
     A**Start of change CGL029
     A            DDDEST        24A  O  9 25TEXT('Destination')
     A**End of change CGL029
 
     A            DDPREF        15A  H      TEXT('Payment reference')
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R FTOPAYS2                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLCTL(FTOPAYS3)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A**Start of change CGL029
     A**********                            SFLSIZ(15)                                        CGL029
     A**********                            SFLPAG(14)                                        CGL029
     A                                      SFLSIZ(8)
     A                                      SFLPAG(7)
     A**End of change CGL029
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      **Start of change 185023
      **End of change 185023
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'FTOPAYB2'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 28'FT - OUTGOING PAYMENTS'
     A                                  4 37'ENQUIRY'
     A                                  6  2'Select'
     A                                  6  9'Front Office'
     A                                  6 25'Destination'
     A                                  6 40'Val date'
     A                                  6 49'Ccy'
     A                                  6 60'Amount'
     A                                  6 70'Setl'
     A*
     A*/COPY WNCPYSRC,FTH00617                                                                ERZ123
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R FTOPAYF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'