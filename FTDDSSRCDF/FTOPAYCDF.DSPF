     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Outgoing Payments Cover Message screen 2')
      *****************************************************************
      *                                                               *
      *  Midas - API Funds Transfer Module                            *
      *                                                               *
      *  DSPF/FTOPAYCDF - Outgoing Payments Cover Message screen 2    *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD035776           Date 10Sep15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CSW209  *CREATE    Date 01Apr09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD035776 - User need to press twice on input screen          *
      *           - Remove unsupported keywords of HATS.              *
      *  CSW209 - SWIFT 2009 Changes                                  *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(23)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R FTOPAYCS1                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2009')
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Message subfile control
      *
     A          R FTOPAYCS0                 TEXT('Msg Subfile Control')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(FTOPAYCS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      ** Main Display Screen
      *
     A          R FTOPAYCD1
     A                                      CF03
     A                                      CF12
      **Start of change MD035776
     A**********                            FRCDTA                                          MD035776
      **End of change MD035776
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'FTOPAYC'
     A                                  1 33'Outgoing Payments Input'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  3  2'Action Code'
     A                                  3 16'Payment Reference'
     A            SPREF         15A  O  3 41
     A N11                                  DSPATR(HI)
     A                                      OVRDTA
     A            SACTION        1A  O  3 14
     A N10                                  DSPATR(HI)
     A                                      OVRDTA
     A                                  9  2'Sender to Receiver Information (Co-
     A                                      ver B)'
     A                                  5  2'Remittance Information (Cover)'
     A            SCDP1         35A  B  6  2
     A N12                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  12                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SCDP3         35A  B  7  2
     A N14                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  14                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SCDP2         35A  B  6 38
     A N13                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  13                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SCDP4         35A  B  7 38
     A N15                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  15                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SCBB1         35A  B 10  2
     A N16                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  16                                  DSPATR(RI)
     A  16                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SCBB3         35A  B 11  2
     A N18                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  18                                  DSPATR(RI)
     A  18                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SCBB5         35A  B 12  2
     A N20                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SCBB2         35A  B 10 38
     A N17                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  17                                  DSPATR(RI)
     A  17                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SCBB4         35A  B 11 38
     A N19                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  19                                  DSPATR(RI)
     A  19                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SCBB6         35A  B 12 38
     A N21                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14  2'Instructed Currency/Amount (Cover)'
     A            SCICY          3A  B 14 38
     A N22                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SCIAM         15A  B 14 42
     A N23                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16  2'Sender to Receiver Information (Co-
     A                                      ver A)'
     A            SOCBB1        35A  B 17  2
     A N24                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SOCBB3        35A  B 18  2
     A N26                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SOCBB5        35A  B 19  2
     A N28                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  28                                  DSPATR(RI)
     A  28                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SOCBB2        35A  B 17 38
     A N25                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SOCBB4        35A  B 18 38
     A N27                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SOCBB6        35A  B 19 38
     A N29                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  29                                  DSPATR(RI)
     A  29                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
      ** Main Screen Footer
      *
     A          R FTOPAYCF1                 TEXT('Screen Footer')
     A                                      CF03
     A                                      CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A            SCCMDTXT      79A  O 24  2OVRDTA
