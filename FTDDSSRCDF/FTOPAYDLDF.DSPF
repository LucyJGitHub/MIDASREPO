      *****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT OPAY - Display File')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FTOPAYDLDF- FT Outgoing Payments Extension              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
      *
      **-----------------------*
      **  MAIN DISPLAY SCREEN  *
      **-----------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
      *
      **---------------------------*
      **  Message subfile control  *
      **---------------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(02)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ(10)
     A          R SCREENF
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2005')
     A                                      CF03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A                                      CF12(12 'Previous')
     A                                      PUTOVR
     A            ##PGM         10A  H
     A                                  1 32'BCL 2002 Reporting'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A                                  5  6'Ordering type. . . . . . . . .:'
     A            LLLXORTY       2A  B  5 38
     A N50N20                               DSPATR(HI)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                  6  6'Beneficiary type . . . . . . .:'
     A            LLLXBETY       2A  B  6 38
     A N51N20                               DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                  7  6'IBLC operation code. . . . . .:'
     A            LLLXOPCO       3A  B  7 38
     A N52N20                               DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                  8  6'Country code . . . . . . . . .:'
     A            LLLXCOCO       2A  B  8 38
     A N53N20                               DSPATR(HI)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                  9  6'Identification code. . . . . .:'
     A            LLLXIDCO       2A  B  9 38
     A N54N20                               DSPATR(HI)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                 10  6'Identification number. . . . .:'
     A            LLLXIDEN       9A  B 10 38
     A N55N20                               DSPATR(HI)
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                 23  6'F3=Main Menu'
     A                                      COLOR(BLU)
     A  35                             23 21'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 23 34'F12=Previous'
     A                                      COLOR(BLU)
     A            SRUNA          7A  O  1 72
     A            TIME           6Y 0O  2 71EDTWRD('  .  .  ')
     A            USRID         10A  O  1  2
     A            WID           10A  O  2  2
     A            SJOB          10A  O  1 13
     A                                  1 65'DATE'
     A                                  2 65'TIME'
