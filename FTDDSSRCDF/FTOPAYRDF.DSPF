/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas FT Invalid Outgoing Payments Browse')
     A*****************************************************************
     A*                                                               *
      *  Midas - API Funds Transfer Module                            *
     A*                                                               *
     A*  FTOPAYRDF - INVALID OUTGOING PAYMENTS BROWSE                 *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.03 ----------------------------------------*
      *  Last Amend No. 207404             Date 09Jan03               *
      * Midas Release 4.01.02 ----------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CAP014             Date 08Dec99               *
      *                 CAP031  *CREATE    Date 08Jun99               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  207404 - Display msg with allocated payment reference, also  *
      *           for last payment                                    *
     A*  CAP014 - Repair function enhancements                        *
     A*  CAP031 - Conversion of Midas inputs to modular API structure *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R FTIOPAYS1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1   B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            SACTION   R           8  6REFFLD(ACTO)
     A                                      DSPATR(HI)
     A            SFOTRANID R     A     8  9REFFLD(FOTRNID)
     A                                      DSPATR(HI)
 
     A            DEST1         14A  O  8 30TEXT('Destination')
 
     A            SPYVD          6A  O  8 45TEXT('Value date')
 
     A            SPCCY          3A  O  8 53TEXT('Currency')
 
     A            SPYAM         14A  O  8 57TEXT('Amount')
 
     A            SSTTP          2A  O  8 75TEXT('Settlement method')
 
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
     A            SPREF     R     A  H      REFFLD(PREF)
      * Start CAP014
     A            DDBRSN    R        H      REFFLD(BRSN)
      * End CAP014
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R FTIOPAYS0                 TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(FTIOPAYS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'FTOPAYRB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'FT - INVALID OUTGOING PAYMENTS MAI+
     A                                      NTENANCE'
     A                                  4 37'ENQUIRY'
     A                                  6  1'Sel'
     A                                  6  5'Act'
     A                                  6  9'Front Office ID'
     A                                  6 31'Destination'
     A                                  6 44'Val date'
     A                                  6 53'Pay'
     A                                  6 60'Amount'
     A                                  6 74'Setl'
     A*
      * Start CAP014
     A**********************************7  1'A,D'
      * End CAP014
     A                                  7 53'Ccy'
     A                                  7 74'Meth'
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R FTIOPAYF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
     A*
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A*
     A* -------------------------------------------------------------------*
     A* * END MESSAGE                                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R FTIOPAYEND                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
     A                                  1  2'FTOPAYRB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'FT - INVALID OUTGOING PAYMENTS MAI+
     A                                      NTENANCE'
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
      * Start of change 207404
     A            ERRORMSG      56     24 24
     A                                      DSPATR(HI)
      * End of change 207404
