      *****************************************************************
/*S*D ***DSPFDFRWN*****************************************************                     MD037021
/*STD *  DSPFBASE                                                     *                     MD037021
/*EXI *  TEXT('Midas FT Outgoing Payments SSIs Primary Screen')       *
      *****************************************************************
      *                                                               *
      *  Midas - API Funds Transfer Module                            *
      *                                                               *
      *  FTOPSI1DF - Outgoing Payments SSI's Primary Screen           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. MD037021           Date 27Jan16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG14716           Date 24Aug07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      *                 CFT018  *CREATE    Date 11Nov02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD037021 - After several enter made in Outgoing Payments     *
      *             Template, it returns to main menu                 *
      *  BUG14716 - CFT032 pre-req missing CFT018 OPSI Outgoing       *
      *             Payments Template                                 *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CFT018 - Standard Settlement Instructions for                *
      *           Outgoing Payments                                   *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      REF(MFRFL)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
     A          R FTOPSI1S1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R FTOPSI1S0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  73                                  CF10
     A                                      SFLCTL(FTOPSI1S1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R FTOPSI1D1
     A                                      CF03
     A                                      CF12
     A  73                                  CF10
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                  1  2'FTOPSI1'
     A                                  1 27'Outgoing Payments +
     A                                      Template'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            DDMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
      *
      *
     A                                  4  2'Action'
     A            SACTION        1A  O  4 10DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  4 13'Debit Ccy'
     A            SDRCY          3A  O  4 24DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  4 29'Debit Account'
      **Start of change CGL029
     A**********  SDRAC         15A  O  4 43DSPATR(HI)                                        CGL029
     A            SDRAC         21A  O  4 43DSPATR(HI)
      **End of change CGL029
     A                                      OVRDTA
      *
      **Start of change CGL029
     A**********  DDSTAT        10A  O  4 64DSPATR(HI)                                        CGL029
     A            DDSTAT        10A  O  4 66DSPATR(HI)
      **End of change CGL029
     A                                      OVRDTA
      *
     A                                  5 13'Reference I'
     A            SREF1         10A  O  5 25DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  5 37'Reference II'
     A            SREF2         10A  O  5 50DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  5 62'Pay Ccy'
     A            SCRCY          3A  O  5 70DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  6 13'Description'
     A            SDESC         30A  O  6 25DSPATR(HI)
     A                                      OVRDTA
      *
      *
     A                                  8  2'Payment type'
     A            SPYTP          2A  B  8 15DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      DFT('OP')
     A                                      OVRDTA
      *
     A                                  8 19'Sub-type'
     A            SPYST          2A  B  8 28
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  8 32'Settlement type'
     A            SSTMT          2A  B  8 48
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  8 53'SWIFT priority'
     A            SSWPC          1A  B  8 69
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  9 32'Booking branch'
     A  80                                  DSPATR(ND)
     A            SBRCA     R        B  9 47REFFLD(BRCA)
     A  80                                  DSPATR(ND)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A N20                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  9 53'Orig branch'
     A  80 81                               DSPATR(ND)
     A            SORBR     R        B  9 65REFFLD(ORBR)
     A  80 81                               DSPATR(ND)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A N21                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 10  2'Destination'
     A            SDST1         35A  B 11  2
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SDST2         35A  B 11 45
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SDST3         35A  B 12  2
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SDST4         35A  B 12 45
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A**Start of BUG14716
     A*N87******                       14 45'MT100 Customer'                                BUG14716
     A**87******                       14 45'MT103/M100 Customer'                           BUG14716
     A**********  SODMT         10A  B 14 70                                                BUG14716
     A                                 17 31'MT103 Account'
     A            SODMT         34A  B 17 45
     A**End of BUG14716
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 14  2'Ordering customer'
     A**Start of BUG14716
     A**********  SORC1         35A  B 15  2                                                BUG14716
     A            SORC1         35A  B 14 45
     A**End of BUG14716
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A**Start of BUG14716
     A**********  SORC2         35A  B 15 45                                                BUG14716
     A            SORC2         35A  B 15  2
     A**End of BUG14716
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A**Start of BUG14716
     A**********  SORC3         35A  B 16  2                                                BUG14716
     A            SORC3         35A  B 15 45
     A**End of BUG14716
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A**Start of BUG14716
     A**********  SORC4         35A  B 16 45                                                BUG14716
     A            SORC4         35A  B 16  2
     A**End of BUG14716
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A**Start of BUG14716
     A            SORC5         35A  B 16 45
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A**End of BUG14716
      *
     A                                 18  2'Ord Bank'
      **Start of change CGL029
     A**********  SORBK         12A  B 18 12                                                  CGL029
     A            SORBK         18A  B 18 12
      **End of change CGL029
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      **Start of change CGL029
     A**********  SORBB     R        B 18 25REFFLD(ORBB)                                      CGL029
     A            SORBB     R        B 18 31REFFLD(ORBB)
      **End of change CGL029
     A  80                                  DSPATR(ND)
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
      **Start of change CGL029
     A**********                       18 31'Sender''s correspondent'                         CGL029
     A                                 18 37'Sender''s corresp'
      **End of change CGL029
     A            SSNCO         10A  B 18 55
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 18 67'MT202 ind'
     A            SSNMT          1A  B 18 78
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A  88                             19  2'Date offset code'
     A            SOFCD          5A  B 19 19
     A                                      OVRDTA
     A N88                                  DSPATR(ND)
     A N88
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N28                                  DSPATR(HI)
     A  28 88                               DSPATR(PC)
     A  28                                  DSPATR(RI)
      *
     A  88                             19 26'Spread set'
     A            SPDSET         5A  B 19 37
     A                                      OVRDTA
     A N88                                  DSPATR(ND)
     A N88
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N53                                  DSPATR(HI)
     A  53 88                               DSPATR(PC)
     A  53                                  DSPATR(RI)
      *
     A                                 19 55'Add/deduct charges ind'
     A            SADDC          1A  B 19 78
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 20  2'Book Code'
     A            SBOKC     R        B 20 12REFFLD(BOKC MFRFL)
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 20 15'Profit Centre'
     A  82                                  DSPATR(ND)
     A            SPRFC     R        B 20 29REFFLD(PRFC MFRFL)
     A  82                                  DSPATR(ND)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A*
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R FTOPSI1F1                 TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  73                                  CF10
     A                                      BLINK
     A                                 23  2'F3=Exit  F12=Previous Screen'
     A  73                             23 50'F10=Delete'
      *
