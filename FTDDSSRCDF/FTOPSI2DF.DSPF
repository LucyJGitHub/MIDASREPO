      *****************************************************************
/*S*D ***DSPFDFRWN*****************************************************                     MD037021
/*STD *  DSPFBASE                                                     *                     MD037021
/*EXI *  TEXT('Midas FT Outgoing Payments SSIs Secondary Screen')     *
      *****************************************************************
      *                                                               *
      *  Midas - API Funds Transfer Module                            *
      *                                                               *
      *  FTOPSI2DF - Outgoing Payments SSI's Secondary Screen         *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD037021           Date 27Jan16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 BUG14716           Date 24Aug07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CFT018  *CREATE    Date 04Nov02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD037021 - After several enter made in Outgoing Payments     *
      *             Template, it returns to main menu                 *
      *  BUG14716 - CFT032 pre-req missing CFT018 OPSI Outgoing       *
      *             Payments Template (Recompile)                     *
      *  CFT018 - Standard Settlement Instructions for                *
      *           Outgoing Payments                                   *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      REF(MFRFL)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
     A          R FTOPSI2S1                 TEXT('(c) Finastra International +
     A                                      Limited 2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R FTOPSI2S0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  73                                  CF10
     A                                      SFLCTL(FTOPSI2S1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R FTOPSI2D1
     A                                      CF03
     A                                      CF12
     A  73                                  CF10
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                  1  2'FTOPSI2'
     A                                  1 27'Outgoing Payments +
     A                                      Template'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            DDMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
      *
     A                                  4  2'Bank operation code'
     A N87                                  DSPATR(ND)
     A            SBOCD          4A  B  4 33
     A N87                                  DSPATR(ND)
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A N87
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  4 45'Transaction type code'
     A N87                                  DSPATR(ND)
     A            STTCD          3A  B  4 77
     A N87                                  DSPATR(ND)
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A N87
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  5  2'Receiver''s correspondent'
     A            SRCO1         35A  B  5 45
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SRCO2         35A  B  6  2
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SRCO3         35A  B  6 45
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SRCO4         35A  B  7  2
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SRCO5         35A  B  7 45
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  8  2'Third reimbursement institution'
     A N87                                  DSPATR(ND)
     A            STRI1         35A  B  8 45
     A N87                                  DSPATR(ND)
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A N87
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            STRI2         35A  B  9  2
     A N87                                  DSPATR(ND)
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A N87
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            STRI3         35A  B  9 45
     A N87                                  DSPATR(ND)
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A N87
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            STRI4         35A  B 10  2
     A N87                                  DSPATR(ND)
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A N87
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            STRI5         35A  B 10 45
     A N87                                  DSPATR(ND)
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A N87
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 11  2'Intermediary bank'
     A            SINB1         35A  B 11 45
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SINB2         35A  B 12  2
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SINB3         35A  B 12 45
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SINB4         35A  B 13  2
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SINB5         35A  B 13 45
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 14  2'Account with bank'
     A            SACB1         35A  B 14 45
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SACB2         35A  B 15  2
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SACB3         35A  B 15 45
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SACB4         35A  B 16  2
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SACB5         35A  B 16 45
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 17  2'Beneficiary'
     A            SBNC1         35A  B 17 45
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SBNC2         35A  B 18  2
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SBNC3         35A  B 18 45
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SBNC4         35A  B 19  2
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SBNC5         35A  B 19 45
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R FTOPSI2F1                 TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                 23  2'F3=Exit  F12=Previous Screen'
      *
