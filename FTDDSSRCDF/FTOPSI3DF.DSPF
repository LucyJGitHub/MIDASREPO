      *****************************************************************
/*S*D ***DSPFDFRWN*****************************************************                     MD037021
/*STD *  DSPFBASE                                                     *                     MD037021
/*EXI *  TEXT('Midas FT Outgoing Payments SSIs Third Screen')         *
      *****************************************************************
      *                                                               *
      *  Midas - API Funds Transfer Module                            *
      *                                                               *
      *  FTOPSI3DF - Outgoing Payments SSI's Thirds Screen            *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD037021           Date 27Jan16               *
      *                 CFT120             Date 28Sep12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 BUG14716           Date 24Aug07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      *                 CFT018  *CREATE    Date 11Nov02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD037021 - After several enter made in Outgoing Payments     *
      *             Template, it returns to main menu                 *
      *  CFT120 - FT IN/OP - Charges to DR of Account Currency        *
      *  BUG14716 - CFT032 pre-req missing CFT018 OPSI Outgoing       *
      *             Payments Template (Recompile)                     *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CFT018 - Standard Settlement Instructions for                *
      *           Outgoing Payments                                   *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      REF(MFRFL)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
     A          R FTOPSI3S1                 TEXT('(c) Finastra International +
     A                                      Limited 2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R FTOPSI3S0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(FTOPSI3S1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R FTOPSI3D1
     A                                      CF03
     A                                      CF12
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                  1  2'FTOPSI3'
     A                                  1 27'Outgoing Payments +
     A                                      Template'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            DDMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
      *
      **  Details of Payment
     A                                  4  2'Details of payment'
     A            SDTP1         35A  B  5  2
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SDTP2         35A  B  5 45
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SDTP3         35A  B  6  2
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SDTP4         35A  B  6 45
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
      **  Bank to Bank Information
     A                                  7  2'Bank to bank information'
     A            SBBI1         35A  B  8  2
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SBBI2         35A  B  8 45
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SBBI3         35A  B  9  2
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SBBI4         35A  B  9 45
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SBBI5         35A  B 10  2
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SBBI6         35A  B 10 45
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
      **  Central Bank Ref
     A                                 12  2'Central bank ref'
     A            SDNBC         11A  B 12 26
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
      **  Transfer Commission Tariff                                   '
     A N88                             12 45'Transfer commission tariff'
     A            STRCT          1A  B 12 72
     A  88                                  DSPATR(ND)
     A  23                                  DSPATR(RI)
     A  23N88                               DSPATR(PC)
     A N23                                  DSPATR(HI)
     A  88
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
      **  CHAPS Code
     A                                 13 45'CHAPS code'
     A            SCHAP          6A  B 13 57
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
      **   Instruction Code
     A                                 15  2'Instruction code'
     A N87                                  DSPATR(ND)
     A            SICD1         35A  B 15 45
     A N87                                  DSPATR(ND)
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A N87
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SICD2         35A  B 16  2
     A N87                                  DSPATR(ND)
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A N87
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SICD3         35A  B 16 45
     A N87                                  DSPATR(ND)
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A N87
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SICD4         35A  B 17  2
     A N87                                  DSPATR(ND)
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI)
     A N87
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SICD5         35A  B 17 45
     A N87                                  DSPATR(ND)
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A N87
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
      **  Regulatory Reporting
     A                                 18  2'Regulatory reporting'
     A N87                                  DSPATR(ND)
     A            SRRP1         35A  B 18 45
     A N87                                  DSPATR(ND)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A N87
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SRRP2         35A  B 19  2
     A N87                                  DSPATR(ND)
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A N87
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            SRRP3         35A  B 19 45
     A N87                                  DSPATR(ND)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A N87
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
      **  Charges to the Debit of A/c
     A                                 21 02'Charges to the debit of a/c'
     A**Start of change CGL029
     A**********  SCDRO         12A  B 21 30                                                  CGL029
     A            SCDRO         18A  B 21 30
     A**End of change CGL029
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      **Start of change CFT120
     A  59        SCDRC          3A  B 21 49TEXT('Charges to DR of Ccy')
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      **End of change CFT120
     A**Start of change CGL029
     A**********  SCDBR          3A  B 21 43                                                  CGL029
      **Start of change CFT120
     A**********  SCDBR          3A  B 21 49                                                  CFT120
     A            SCDBR          3A  B 21 53
      **End of change CFT120
     A**End of change CGL029
     A  80                                  DSPATR(ND)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
      **  Details of Charges
      **Start of change CFT120
     A**********                       21 56'Details of charges'                              CFT120
     A                                 21 57'Details of charges'
      **End of change CFT120
     A            SDTCH          3A  B 21 76
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
      *
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R FTOPSI3F1                 TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                 23  2'F3=Exit  F12=Previous Screen'
      *
