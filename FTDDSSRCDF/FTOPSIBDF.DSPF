      *****************************************************************
/*S*D ***DSPFDFRWN*****************************************************                     MD037021
/*STD *  DSPFBASE                                                     *                     MD037021
/*EXI *  TEXT('Midas FT Outgoing payments SSIs browse screen')        *
      *****************************************************************
      *                                                               *
      *  Midas - API Funds Transfer Module                            *
      *                                                               *
      *  FTOPSIBDF - Outgoing Payments SSI's Browse Screen            *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD037021           Date 27Jan16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      *                 217768             Date 09Jun03               *
      *                 CFT018  *CREATE    Date 29Nov02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD037021 - After several enter made in Outgoing Payments     *
      *             Template, it returns to main menu                 *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  217768 - OPSI - Can not Exit if invalid action entered       *
      *  CFT018 - Standard Settlement Instructions for                *
      *           Outgoing Payments                                   *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A          R FTOPSIS1                  TEXT('(c) Finastra International +
     A                                      Limited 2002')
     A                                      SFL
     A  84                                  SFLNXTCHG
     A            SOPTION        1   B  8  2TEXT('SELECT OPTION')
      * Start of change 217768
     A*********                             VALUES('E' 'A' 'X' 'D' 'P' ' ')                   217768
      * End of change 217768
     A N85                                  DSPATR(UL HI)
     A  85                                  DSPATR(RI PC)
      **Start of change CGL029
     A**********  SDRCY          3A  O  8  6TEXT('Debit Ccy')                                 CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A**********  SDRAC         15A  O  8 13TEXT('Debit Account')                             CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A            SDRCY          3A  O  8  5TEXT('Debit Ccy')
     A                                      DSPATR(HI)
     A            SDRAC         21A  O  8  9TEXT('Debit Account')
     A                                      DSPATR(HI)
      **End of change CGL029
     A            SREF1         10A  O  8 31TEXT('Reference I')
     A                                      DSPATR(HI)
     A            SREF2         10A  O  8 44TEXT('Reference II')
     A                                      DSPATR(HI)
     A            SCRCY          3A  O  8 58TEXT('Pay Ccy')
     A                                      DSPATR(HI)
     A            SSTAT         11A  O  8 64TEXT('Status')
     A                                      DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * Subfile Control Format                                           *
      * -------------------------------------------------------------------*
     A          R FTOPSIS0                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      SFLCTL(FTOPSIS1)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  89                                  SFLEND
     A  87                                  SFLCLR
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A N89                                  ROLLUP(88)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSREC         4  0H      SFLRCDNBR
      *
     A                                  1  2'FTOPSIB'
     A                                  1 27'Outgoing Payments +
     A                                      Template'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            DDMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
      *
      **Start of change CGL029
     A**********                        4  6'Debit'                                           CGL029
     A**********                        4 13'Debit'                                           CGL029
     A                                  4  5'Dr'
     A                                  4  9'Dr'
      **End of change CGL029
     A                                  4 58'Pay'
     A                                  4 64'Status'
      *
     A                                  5  1'Sel'
      **Start of change CGL029
     A**********                        5  6'Ccy'                                             CGL029
     A**********                        5 13'Account'                                         CGL029
     A                                  5  5'Ccy'
     A                                  5  9'Account'
      **End of change CGL029
     A                                  5 31'Reference I'
     A                                  5 44'Reference II'
     A                                  5 58'Ccy'
     A                                  5 64'(A,C,D,R)'
      *
      ** Input fields to position sub-file
      **Start of change CGL029
     A**********  S1DRCY         3A  B  6  6TEXT('Debit Ccy')                                 CGL029
     A**********                            DSPATR(UL HI)                                     CGL029
     A**********  S1DRAC        15A  B  6 13TEXT('Debit Account')                             CGL029
     A**********                            DSPATR(UL HI)                                     CGL029
     A            S1DRCY         3A  B  6  5TEXT('Debit Ccy')
     A                                      DSPATR(UL HI)
     A            S1DRAC        21A  B  6  9TEXT('Debit Account')
     A                                      DSPATR(UL HI)
      **End of change CGL029
     A            S1REF1        10A  B  6 31TEXT('Reference I')
     A                                      DSPATR(UL HI)
     A            S1REF2        10A  B  6 44TEXT('Reference II')
     A                                      DSPATR(UL HI)
     A            S1CRCY         3A  B  6 58TEXT('Pay Ccy')
     A                                      DSPATR(UL HI)
     A            S1STAT         1A  B  6 64TEXT('Status')
      * Start of change 217768
     A**********                            VALUES('A' 'C' 'D' 'R' ' ')                       217768
      * End of change 217768
     A                                      DSPATR(UL HI)
      *
      * -------------------------------------------------------------------*
      *   SCREEN FOOTER                                                    *
      * -------------------------------------------------------------------*
     A          R FTOPSIF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A                                 22  2'A=Amend  E=Enquire  D=Delete  +
     A                                       X=Authorise  P=Print'
     A                                 23  2'F3=Exit   F5=Refresh    +
     A                                      F9=Insert'
      *
      *--------------------------------------------------------------- ----*
      *  Message Subfile Format                                            *
      *--------------------------------------------------------------- ----*
     A          R FTOPSIM1                  TEXT('(c) Finastra International +
     A                                      Limited 2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      *--------------------------------------------------------------- ----*
      *  Message Subfile Control Format                                    *
      *--------------------------------------------------------------- ----*
     A          R FTOPSIM0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      SFLCTL(FTOPSIM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      BLINK
     A            DDPGMQ                    SFLPGMQ
