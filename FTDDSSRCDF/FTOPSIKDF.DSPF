      *********************************************************************
/*STD *  DSPFBASE                                                         *
/*EXI *  TEXT('Midas FT Outgoing Payments SSI's Key Insert Screen')
      *********************************************************************
      *                                                                   *
      *  Midas - API Funds Transfer Module                                *
      *                                                                   *
      *  FTOPSIKDF - Outgoing Payments SSI's Key Insert Screen            *
      *                                                                   *
      *  (c) Finastra International Limited 2002                          *
      *                                                                   *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CFT018  *CREATE    Date 04Nov02               *
      * Midas Release 4 --------------- Base -----------------------------*
      *                                                                   *
      *-------------------------------------------------------------------*
      *                                                                   *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CFT018 - Standard Settlement Instructions for                    *
      *           Outgoing Payments                                       *
      *                                                                   *
      *********************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
     A          R FTOPSIKS1                 TEXT('(c) Finastra International +
     A                                      Limited 2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R FTOPSIKS0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  73                                  CF10
     A                                      SFLCTL(FTOPSIKS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R FTOPSIKD1
     A                                      CF03
     A                                      CF12
     A  73                                  CF10
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                  1  2'FTOPSIK'
     A                                  1 27'Outgoing Payments +
     A                                      Template'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            DDMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
      *
      *
     A                                  4  2'Action'
     A            SACTION        1A  O  4 20DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  6  2'Debit Ccy'
     A            SDRCY          3A  B  6 20
     A  11                                  DSPATR(RI)
     A N11                                  DSPATR(HI)
     A  11N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  8  2'Debit Account'
      **Start of change CGL029
     A**********  SDRAC         15A  B  8 20                                                  CGL029
     A            SDRAC         21A  B  8 20
      **End of change CGL029
     A  12                                  DSPATR(RI)
     A N12                                  DSPATR(HI)
     A  12N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 10  2'Reference I'
     A            SREF1         10A  B 10 20
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(HI)
     A  13N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 12  2'Reference II'
     A            SREF2         10A  B 12 20
     A  14                                  DSPATR(RI)
     A N14                                  DSPATR(HI)
     A  14N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 14  2'Pay Ccy'
     A            SCRCY          3A  B 14 20
     A  15                                  DSPATR(RI)
     A N15                                  DSPATR(HI)
     A  15N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 16  2'Description'
     A            SDESC         30A  B 16 20
     A  16                                  DSPATR(RI)
     A N16                                  DSPATR(HI)
     A  16N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
      *
      *
     A*
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R FTOPSIKF1                 TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  73                                  CF10
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                 24  2'F3=Exit  F12=Previous Screen'
      *
