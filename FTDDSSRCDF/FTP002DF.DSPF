      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas  FT STP Charges Maintenance')
      *****************************************************************
      *                                                               *
      *   Midas - Funds Transfer Module                               *
      *                                                               *
      *   DSPF/FTP002DF - Midas FT STP Charges Maintenance            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *   Last Amend No. MD031937           Date 13Jan15              *
      *   Prev Amend No. AR856737           Date 13Sep11              *
      *                  Djb003           Date 18Mar2005              *
      *                  ESL038 *Create   Date 20Ocy04                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *    MD031937 - Upgrade STP enhancements to BF Midas 2.1        *
      *    AR856737 - Upgrade STP enhancements to Midas Plus level    *
      *    Djb003 - Add send mt191 advices                            *
      *    EEE065 - FT Payment Routing                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
     A                                      CF12(12 'CF12 Previous')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CCCY')
     A                                      SETOF(32 'Invalid: #1AMNT')
     A                                      SETOF(33 'Invalid: #1REA1')
      *.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'FTP002F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  4  2'Action Code . :'
     A            #PACTC         1      4 20TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Payment Reference...............:'
     A            #1PREF        15A     5 36TEXT('Payment Reference')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N25                                  OVRATR
     A                                  6  2'Field 20 Transaction Reference..:'
     A            #1F20R        16A     6 36TEXT('Field 20 Trans Ref.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N25                                  OVRATR
      *
     A                                  8  2'Charges..............Currency...:'
     A            #1CCCY         3A  B  8 36TEXT('Currency')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31N79                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A  79                                  DSPATR(PR)
     A N25                                  OVRATR
     A                                  9  2'.....................Amount.....:'
     A            #1AMNT        15A  B  9 36TEXT('Currency')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32N79                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A  79                                  DSPATR(PR)
     A N25                                  OVRATR
     A                                 10  2'Reasons for Charge..............:'
     A            #1REA1        35A  B 10 36TEXT('Reason for Charge 1')
     A N25                                  OVRDTA
     A                                      CHECK(LC)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33N79                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A  79                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1REA2        35A  B 11 36TEXT('Reason for Charge 2')
     A N25                                  OVRDTA
     A                                      CHECK(LC)
     A N79                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  79                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1REA3        35A  B 12 36TEXT('Reason for Charge 3')
     A N25                                  OVRDTA
     A                                      CHECK(LC)
     A N79                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  79                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1REA4        35A  B 13 36TEXT('Reason for Charge 4')
     A N25                                  OVRDTA
     A                                      CHECK(LC)
     A N79                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  79                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1REA5        35A  B 14 36TEXT('Reason for Charge 5')
     A N25                                  OVRDTA
     A                                      CHECK(LC)
     A N79                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  79                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1REA6        35A  B 15 36TEXT('Reason for Charge 6')
     A N25                                  OVRDTA
     A                                      CHECK(LC)
     A N79                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  79                                  DSPATR(PR)
     A N25                                  OVRATR
      *
     A                                 17  2'MT199 Reference.................:'
     A            #1M19R        16A    17 36TEXT('MT199 Reference')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N25                                  OVRATR
     A                                 18  2'Date Sent.......................:'
     A            #1DATS         7A    18 36TEXT('Date Sent')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N25                                  OVRATR
      *
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
