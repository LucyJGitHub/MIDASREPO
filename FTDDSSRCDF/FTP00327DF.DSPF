      *****************************************************************
/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  DSPF/FTP00327DF - Maintain Receivers and Intermediary Chgs   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. MD31937            Date 13Jan15               *
      *  Last Amend No. AR856737           Date 13Sep11               *
      *  Last Amend No. ESL038             Date 04Oct2004             *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CFT009 *CREATE     Date 05Jun00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD31937  - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  ESL038 - ING STP Development                                 *
      *  CFT009 - Funds Transfer Fees and Charges                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      INDARA
      *
      *
     A*
     A          R FT327S1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFL
     A  34                                  SFLNXTCHG
     A            SFLRRN         5S 0H
     A            SFLSEL         1A  B  8  2
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A            PTYID     R        O  8  6REFFLD(DCRA)
     A                                      DSPATR(HI)
     A            BBCNA1        35   O  8 18DSPATR(HI)
     A            RIBICC         8   O  8 54DSPATR(HI)
     A            RIBICB         3   O  8 63DSPATR(HI)
     A            CURCCY    R        O  8 67REFFLD(BCCY)
     A                                      DSPATR(HI)
     A            TRANTY    R        O  8 71REFFLD(CKEN)
     A                                      DSPATR(HI)
     A            CHGCD     R        O  8 75REFFLD(DBFL)
     A                                      DSPATR(HI)
     A*
     A          R FT327C1                   SFLCTL(FT327S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  30                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A                                      CA03(03 'Exit')
     A                                      CA09(09 'ADD')
     A                                      CHANGE(60)
      *
     A            BJMRDT         7A  O  1 73
     A                                  2 73TIME
     A                                  1  2USER
     A            #PGM          10A  O  1 13
     A                                  3  7'A=Amend   D=Delete   E=Enquire'
     A                                      DSPATR(HI)
     A            #WRKST        10A  O  2  2
     A            POSCDE        11A  B  7  6DSPATR(HI)
     A                                  2 25'Maintain Receiver''s and -
     A                                      Intermediaries'' Charges'
     A                                      DSPATR(HI)
     A                                  6  2'Sel Identifier   Description      -
     A                                                        Beneficiary Ccy T-
     A                                      upe Code'
     A                                  5  6'Party                             -
     A                                                                        C-
     A                                      harge'
      *
     A          R FT327T1
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F3=Exit   F9=Add'
      *
     A          R SFLMSG                       SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
      *
     A          R MSGCTL
     A                                      SFLCTL(SFLMSG)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(2)
     A                                      SFLDSPCTL SFLDSP
     A  36
     AON36                                  SFLEND
     A            #PGM                      SFLPGMQ
     A          R FT327X1
     A                                      CA03(03 'Exit')
     A  44                                  CF10(10 'CONFIRM')
     A                                      CA12(12 'RETURN')
     A                                      OVERLAY
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
      *
     A                                  1  2USER
     A            #PGM          10A  O  1 13
     A            #WRKST        10A  O  2  2
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A                                  4  2'Action = '
     A            #ACT           1A  O  4 12DSPATR(HI)
     A                                  2 19'Maintain Receiver''s and -
     A                                      Intermediaries'' Charges'
     A                                      DSPATR(HI)
     A                                  7  3'Party Identifier .  . . . :'
     A                                  9  3'Currency . . . . .  . . . :'
     A                                 11  3'Transaction Type .  . . . :'
     A                                 13  3'Beneficiary Institution . :'
     A                                 15  3'Charge Code  . . .  . . . :'
     A            WPTYID    R        B  7 32REFFLD(DCRA)
     A N45                                  DSPATR(HI)
     A  49                                  DSPATR(PR)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A N49                                  DSPATR(UL)
     A                                      CHANGE(61)
     A            WBBCNA1       35   O  7 44
     A                                      DSPATR(HI)
     A            WCURCCY   R        B  9 32REFFLD(BCCY)
     A  50                                  DSPATR(PR)
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A N50                                  DSPATR(UL)
     A            WTRANTY   R        B 11 32REFFLD(CKEN)
     A  50                                  DSPATR(PR)
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A N50                                  DSPATR(UL)
     A            WBENBIC        8   B 13 32TEXT('Beneficiary Institution')
     A  50                                  DSPATR(PR)
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A N50                                  DSPATR(UL)
     A            WBENBIB        3   B 13 41TEXT('Beneficiary Institution')
     A  50                                  DSPATR(PR)
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A N50                                  DSPATR(UL)
     A            WCHGCD    R        B 15 32REFFLD(DBFL)
     A  51                                  DSPATR(PR)
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A N51                                  DSPATR(UL)
     A                                 17  3'OUR Charge code  .  . . . :'
     A            WOURCHG        5   B 17 32TEXT('OUR Charge code')
     A  51                                  DSPATR(PR)
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A N51                                  DSPATR(UL)
     A                                 19  3'STP Fail BEN/SHA code . . :'
     A            WSTPCHG        5   B 19 32TEXT('STP Fail Charge code')
     A  51                                  DSPATR(PR)
     A N55                                  DSPATR(HI)
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A N51                                  DSPATR(UL)
     A                                 15 41'71G hidden Costs Abroad . :'
     A            WCHGHID        1   B 15 69TEXT('Hidden Charge')
     A  51                                  DSPATR(PR)
     A N56                                  DSPATR(HI)
     A  56                                  DSPATR(RI)
     A  56                                  DSPATR(PC)
     A N51                                  DSPATR(UL)
     A                                 17 41'Regulatory Compliant Check:'
     A            WRCCGIN        1   B 17 69TEXT('Reg. Comp. Check')
     A  51                                  DSPATR(PR)
     A N57                                  DSPATR(HI)
     A  57                                  DSPATR(RI)
     A  57                                  DSPATR(PC)
     A N51                                  DSPATR(UL)
     A                                 19 41'Regulatory Compliant Charge'
     A            WRCCGCD        5   B 19 69TEXT('Reg. Comp. Charge')
     A  51                                  DSPATR(PR)
     A N58                                  DSPATR(HI)
     A  58                                  DSPATR(RI)
     A  58                                  DSPATR(PC)
     A N51                                  DSPATR(UL)
     A                                 23  4'F3=Exit'
     A  44                             23 15'F10=Confirm'
     A  44                             23 29'F12=Previous'
     A N44N52                          23 15'F12=Previous'
     A N44 52                          23 15'F12=Reject'
     A            XPTYID    R        H      REFFLD(DCRA)
     A            XCURCCY   R        H      REFFLD(BCCY)
     A            XTRANTY   R        H      REFFLD(CKEN)
     A            XBENBIC        8   H      TEXT('Beneficiary Institution')
     A            XBENBIB        3   H      TEXT('Beneficiary Institution')
