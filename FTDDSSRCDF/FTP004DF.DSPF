      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Payment Charges Maintenance Screen')
      *****************************************************************
      *                                                               *
      *   Midas - Funds Transfer Module                               *
      *                                                               *
      *   DSPF/FTP004DF - FT Payment Charges Maintenance Screen       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *   Last Amend No. MD031937         Date 13Jan15                *
      *   Prev Amend No. AR856737         Date 13Sep11                *
      *                  Djb003           Date 18Mar2005              *
      *                  ESL038 *Create   Date 16Oct04                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *   AR856737 - Upgrade STP enhancements to Midas Plus level     *
      *   Djb003 - Add send mt191 advices                             *
      *   ESL038 - FT STP Enhancements                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
      *---------------------------------------------------------------------
     A            #1RECI         1   H      TEXT('Record Identifier')
      *---------------------------------------------------------------------
      * Audit information
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
      *---------------------------------------------------------------------
      * Direct account in Midas
     A            #1DES1        35   H      TEXT('Description 1')
     A            #1DES2        35   H      TEXT('Description 2')
     A            #1DES3        35   H      TEXT('Description 3')
      *---------------------------------------------------------------------
      *
      *---------------------------------------------------------------------
      * Charges Reasons
     A            #1REA1        35   H      TEXT('Charges Reason 1')
     A            #1REA2        35   H      TEXT('Charges Reason 2')
     A            #1REA3        35   H      TEXT('Charges Reason 3')
     A            #1REA4        35   H      TEXT('Charges Reason 4')
     A            #1REA5        35   H      TEXT('Charges Reason 5')
     A            #1REA6        35   H      TEXT('Charges Reason 6')
      *
      *---------------------------------------------------------------------
     A            #1SEL          1   B  9  2
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A            #1PREF        15      9  4TEXT('Payment Reference')
     A                                      DSPATR(HI)
     A            #1F20R        16      9 20TEXT('Field 20 Tran Ref')
     A                                      DSPATR(HI)
     A            #1CCCY         3      9 37TEXT('Charges Currency')
     A                                      DSPATR(HI)
     A            #1AMNT        15      9 41TEXT('Charge Amount')
     A                                      DSPATR(HI)
     A            #1M19R        16      9 57TEXT('MT199 Reference')
     A                                      DSPATR(HI)
     A            #1DATS         6      9 74TEXT('Date Sent')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
      *
     A N80 81 82                            SFLEND(*MORE)
      *
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2PREF')
     A                                      SETOF(32 'Invalid: #2CCCY')
     A                                      SETOF(33 'Invalid: #2DATS')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'FTP004F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
      *---------------------------------------------------------------*
      *---------------------------------------------------------------*
     A                                  4  2'Position on.:'
     A                                  4 16'Payment Reference..:'
     A            #2PREF        15   B  4 38TEXT('Payment Refernce')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5  2'Select on...:'
     A                                  5 16'Currency...:'
     A            #2CCCY         3   B  5 29TEXT('Charge Ccy')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5 33'Date Sent..:'
     A            #2DATS         6   B  5 46TEXT('Date Sent')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33N70                               DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A  81                              7  2'? Transaction     Field 20 Trans. -
     A                                       Charges             MT199 Referenc-
     A                                      e  Date'
     A  81                              8  4'Reference       Reference       -
     A                                       Ccy Amount                        -
     A                                         Sent  '
     A                                      DSPATR(UL)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
