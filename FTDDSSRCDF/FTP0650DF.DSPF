      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT STP Correspondent Pay Via Maintenance')
      *****************************************************************
      *                                                               *
      *   Midas - Funds Transfer Module                               *
      *                                                               *
      *   DSPF/FTP0650DF - STP Correspondent Pay Via Maintenance      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *   Last Amend No. MD031937         Date 13Jan15                *
      *   Prev Amend No. AR856737         Date 13Sep11                *
      *                  PNL015           Date 30Nov2005              *
      *                  PNL014           Date 07Dec2005              *
      *                  EEE065           Date 25Nov2004              *
      *                  EEE065  *CREATE  Date 30Mar2004              *
      * Midas Release 4.01------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   MD031937 - Upgrade STP enhancements to BF Midas 2.1         *
      *   AR856737 - Upgrade STP enhancements to Midas Plus level     *
      *   PNL015 - Redirection to head office BIC                     *
      *   PNL014 - Time Sensitive Routing                             *
      *   EEE065 - Allow Foreign BICs                                 *
      *   EEE065 - FT Payment Routing                                 *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
      *---------------------------------------------------------------------
     A            #1RECI         1   H      TEXT('Record Identifier')
      *---------------------------------------------------------------------
      * Audit information
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
      *---------------------------------------------------------------------
      * Correspondent Payment Re-Route
     A            #1BICC         8   H      TEXT('BIC (Bank, Country, & Loc')
     A            #1BICB         3   H      TEXT('BIC (Branch Code)')
     A            #1PCCY         3   H      TEXT('Payment Currency')
      *---------------------------------------------------------------------
      * Correspondent Payment Pay Route
     A            #1RCAU         1   H      TEXT('Receive a/c Used')
     A            #1RCAC        35   H      TEXT('Receive a/c Quoted')
     A            #1C53C         8   H      TEXT('Senders Correspondent BIC')
     A            #1C53B         3   H      TEXT('Senders Correspondent BIC Br')
     A            #1C53A        10   H      TEXT('Senders Correspondent A/c')
     A            #1C53R         1   H      TEXT('Cover Required')
     A            #1C54C         8   H      TEXT('Receivers Correspondent BIC')
     A            #1C54B         3   H      TEXT('Receivers Corr BIC Br')
     A            #1C56C         8   H      TEXT('Intermediary BIC')
     A            #1C56B         3   H      TEXT('Intermediary BIC Br')
     A            #1SCHR        13S 8H      TEXT('Standard - Charge %')
     A            #1SCHL        15S 0H      TEXT('Standard - Min Fee')
     A            #1SCHH        15S 0H      TEXT('Standard - Max Fee')
     A            #1SCHC         5   H      TEXT('Standard - Charge code')
     A            #1DES1        35   H      TEXT('Description 1')
     A            #1DES2        35   H      TEXT('Description 2')
     A            #1DES3        35   H      TEXT('Description 3')
     A            #1PNBR         7S 0H      TEXT('Preferential Number Routed')
     A            #1PBNB         7S 0H      TEXT('Preferential Number Rout BEN')
     A            #1PSNB         7S 0H      TEXT('Preferential Number Rout SHA')
     A            #1PONB         7S 0H      TEXT('Preferential Number Rout OUR')
     A            #1PRNB         7S 0H      TEXT('Preferential LVP Nbr Routed')
     A            #1PLNB         7S 0H      TEXT('Preferential LVP Nbr Routed')
      *---------------------------------------------------------------------
     A            #1BENR         1   H      TEXT('Route for BEN')
     A            #1BENL        15S 0H      TEXT('BEN Amount - Min')
     A            #1BENH        15S 0H      TEXT('BEN Amount - Max')
     A            #1SHAR         1   H      TEXT('Route for SHA')
     A            #1SHAL        15S 0H      TEXT('SHA Amount - Min')
     A            #1SHAH        15S 0H      TEXT('SHA Amount - Max')
     A            #1OURR         1   H      TEXT('Route for OUR')
     A            #1OURL        15S 0H      TEXT('OUR Amount - Min')
     A            #1OURH        15S 0H      TEXT('OUR Amount - Max')
      *
      *---------------------------------------------------------------------
      *
     A            #1FBIC         1   H      TEXT('Foreign Bank')
      * PNL015 Start
     A            #1RHOB         1   H      TEXT('Route via Head Office BIC')
      * PNL015 End
      *---------------------------------------------------------------------
      * Cover Replacement Information
     A            #1R910         1   H      TEXT('Replace 202C with 910')
     A            #1STMT         2   H      TEXT('Settlement Method')
      *
      *---------------------------------------------------------------------
      * Active Dates
      *
     A            #1STAT         1   H      TEXT('Status' )
     A            #1STDT         5S 0H      TEXT('Start Date')
     A            #1ENDT         5S 0H      TEXT('End Date')
      * PNL014 Start
      *---------------------------------------------------------------------
      * Time Sensitive Routing
     A            #1TSDO         5   H      TEXT('TSP Date Offset')
     A            #1TSCT         4S 0H      TEXT('TSP Cut Off Time')
     A            #1TSCD         2S 0H      TEXT('TSP Cut Off Day Offset')
     A            #1TSCK         1   H      TEXT('TSP Cut Off Check')
     A            #1TSHC         3   H      TEXT('TSP Cut Off Holiday Cal. Ccy')
     A            #1TSHL         3   H      TEXT('TSP Cut Off Holiday Cal. Loc')
     A            #1TSFC         1   H      TEXT('TSP Fail Condition')
     A            #1TSIS        10   H      TEXT('TSP Iteration Flags')
     A            #1RREF        25   H      TEXT('Route Reference')
      *
      *---------------------------------------------------------------------
      * PNL014 End
      *---------------------------------------------------------------------
     A            #1SEL          1   B 10  2
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A            #1CBIC         8     10  4TEXT('Pay Through BIC')
     A                                      DSPATR(HI)
     A            #1CBIB         3     10 13TEXT('Pay Through BIC branch')
     A                                      DSPATR(HI)
     A            #1SERP         1     10 20TEXT('Serial Pay')
     A                                      DSPATR(HI)
     A            #1SREG         1     10 24TEXT('Serial REG')
     A                                      DSPATR(HI)
     A            #1SOUR         1     10 28TEXT('Serial OUR')
     A                                      DSPATR(HI)
     A            #1SLVP         1     10 32TEXT('Serial LVP')
     A                                      DSPATR(HI)
     A            #1PCOR         1     10 36TEXT('Preferential Correspondent')
     A                                      DSPATR(HI)
     A            #1PPCT         2  0  10 38TEXT('Preferential Percentage')
     A                                      EDTWRD('  ')
     A                                      DSPATR(HI)
     A            #1PBCR         1     10 41TEXT('Preferential Correspond BEN')
     A                                      DSPATR(HI)
     A            #1PBPT         2  0  10 43TEXT('Preferential Percentage BEN')
     A                                      EDTWRD('  ')
     A                                      DSPATR(HI)
     A            #1PSCR         1     10 46TEXT('Preferential Correspond SHA')
     A                                      DSPATR(HI)
     A            #1PSPT         2  0  10 48TEXT('Preferential Percentage SHA')
     A                                      EDTWRD('  ')
     A                                      DSPATR(HI)
     A            #1POCR         1     10 51TEXT('Preferential Correspond OUR')
     A                                      DSPATR(HI)
     A            #1POPT         2  0  10 53TEXT('Preferential Percentage OUR')
     A                                      EDTWRD('  ')
     A                                      DSPATR(HI)
     A            #1PREG         1     10 57TEXT('Preferential Regulated Cor')
     A                                      DSPATR(HI)
     A            #1PRPC         2  0  10 59TEXT('Preferential Regulated %')
     A                                      EDTWRD('  ')
     A                                      DSPATR(HI)
     A            #1PLVP         1     10 63TEXT('Preferential LVP Cor')
     A                                      DSPATR(HI)
     A            #1PLPC         2  0  10 65TEXT('Preferential LVP %')
     A                                      EDTWRD('  ')
     A                                      DSPATR(HI)
      * PNL014 Start
     A            #1RTSQ         3     10 68TEXT('Routing Sequence')
     A                                      DSPATR(HI)
     A            #1TSIS8        8     10 73TEXT('TSP Iteration Flag')
     A                                      DSPATR(HI)
      * PNL014 End
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(11) SFLSIZ(12)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
      *
     A N80 81 82                            SFLEND(*MORE)
      *
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2BICC')
     A                                      SETOF(32 'Invalid: #2BICB')
     A                                      SETOF(33 'Invalid: #2103P')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'FTP0650F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
      *---------------------------------------------------------------*
     A            #2BICC         8      3 21TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2BICB         3      3 31TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2PCCY         3      3 35TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2INS1        35      3 39TEXT('Institution 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *---------------------------------------------------------------*
     A                                  5  2'Position on.:'
     A                                  5 16'BIC Identifier .:'
     A            #2CBIC         8   B  5 35TEXT('BIC Identifier')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  6  2'Select on...:'
     A                                  6 16'BIC Branch.:'
     A            #2CBIB         3   B  6 29TEXT('BIC Branch')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32N70                               DSPATR(UL HI)
     A N25                                  OVRATR
      *
      * PNL014 Start
      *******
     A**81***                           8  2'? BIC Identifier <---Serial---->  -
     A*******                               <-------Preferential---------->    -
     A*******                                        '
     A*******                               DSPATR(HI) COLOR(WHT)
     A**81***                           9  4'               Pay Reg Our LVP  St-
     A*******                               d  Ben  Sha  Our   Reg   LVP       -
     A*******                                      '
     A  81                              8  2'? BIC Identifier <---Serial---->  -
     A                                      <-------Preferential----------> Rou-
     A                                      te  Iter '
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9  4'               Pay Reg Our LVP  St-
     A                                      d  Ben  Sha  Our   Reg   LVP  Seq. -
     A                                         ?   '
      * PNL014 End
     A                                      DSPATR(HI) COLOR(WHT)
     A                                      DSPATR(UL)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
