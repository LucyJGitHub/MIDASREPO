      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT STP Payment Corr. ReRoute Maintenance')
      *****************************************************************
      *                                                               *
      *   Midas - Funds Transfer Module                               *
      *                                                               *
      *   DSPF/FTP0660DF - FT STP Payment Corr. ReRoute Maintenance   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *   Last Amend No. MD031937         Date 13Jan15                *
      *   Prev Amend No. AR856737         Date 13Sep11                *
      *                  PNL014           Date 07Dec2005              *
      *                  EEE065  *CREATE  Date 05Apr2004              *
      * Midas Release 4.01------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *    MD031937 - Upgrade STP enhancements to BF Midas 2.1        *
      *    AR856737 - Upgrade STP enhancements to Midas Plus level    *
      *    PNL014 - Time Sensitive Routing                            *
      *    EEE065 - FT Payment Routing                                *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(31 'Invalid: #1CBIC')
     A                                      SETOF(32 'Invalid: #1CBIB')
     A                                      SETOF(99 'Global error flag')
      *----------------------------------------------------------------
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'FTP0660F1'
     A N25                                  OVRATR
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code  . . . . :'
     A            #PACTC         1A  O  4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'BIC identifier . . . :'
     A            #1BICC         8A     5 27TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1BICB         3A     5 37TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1INS1        30      5 43TEXT('BIC Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Pay currency . . . . :'
     A            #1PCCY         3A     6 27TEXT('Payment Currency')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Re Route BIC . . . . :'
     A            #1RBIC         8A  B  7 27TEXT('Re Route Through BIC')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1RBIB         3A  B  7 37TEXT('Re Route Through BIC branch')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1INT1        30      7 43TEXT('BIC Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #RCDDTL1
      *
     A                                      TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Previous')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1COVR')
     A                                      SETOF(32 'Invalid: #1RPLC')
     A                                      SETOF(33 'Invalid: #1DES*')
     A                                      SETOF(34 'Invalid: #1BENR')
     A                                      SETOF(35 'Invalid: #1BENL')
     A                                      SETOF(36 'Invalid: #1BENH')
     A                                      SETOF(37 'Invalid: #1SHAR')
     A                                      SETOF(38 'Invalid: #1SHAL')
     A                                      SETOF(49 'Invalid: #1SHAH')
     A                                      SETOF(40 'Invalid: #1OURR')
     A                                      SETOF(41 'Invalid: #1OURL')
     A                                      SETOF(42 'Invalid: #1OURH')
     A                                      SETOF(43 'Invalid: #1STAT')
     A                                      SETOF(44 'Invalid: #1STDT')
     A                                      SETOF(45 'Invalid: #1ENDT')
     A                                      SETOF(46 'Invalid: #1CFLG')
      *-------------------------------------------------------------------------
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'FTP0660F2'
     A N25                                  OVRATR
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *---------------------------------------------------------------------
     A            #1RECI         1A  H      TEXT('Record Identifier')
     A            #1AJOB        10A  H      TEXT('Job name')
     A            #1AACT         1A  H      TEXT('Action Type')
     A            #1RDNB         5S 0H      TEXT('Run day number')
     A            #1STDT         5S 0H      TEXT('Start Date')
     A            #1ENDT         5S 0H      TEXT('End Date')
     A            #1BENL        15S 0H      TEXT('BEN Amount - Min')
     A            #1BENH        15S 0H      TEXT('BEN Amount - Max')
     A            #1SHAL        15S 0H      TEXT('SHA Amount - Min')
     A            #1SHAH        15S 0H      TEXT('SHA Amount - Max')
     A            #1OURL        15S 0H      TEXT('OUR Amount - Min')
     A            #1OURH        15S 0H      TEXT('OUR Amount - Max')
      * PNL014 Start
      *---------------------------------------------------------------------
      * Time Sensitive Routing
     A            #1RTSQ         3   H      TEXT('Routing Sequence')
     A            #1TSDO         5   H      TEXT('TSP Date Offset')
     A            #1TSCT         4S 0H      TEXT('TSP Cut Off Time')
     A            #1TSCD         2S 0H      TEXT('TSP Cut Off Day Offset')
     A            #1TSCK         1   H      TEXT('TSP Cut Off Check')
     A            #1TSHC         3   H      TEXT('TSP Cut Off Holiday Cal. Ccy')
     A            #1TSHL         3   H      TEXT('TSP Cut Off Holiday Cal. Loc')
     A            #1TSFC         1   H      TEXT('TSP Fail Condition')
     A            #1TSIS        10   H      TEXT('TSP Iteration Flags')
     A            #1RREF        25   H      TEXT('Route Reference')
      *
      *---------------------------------------------------------------------
      * PNL014 End
      *---------------------------------------------------------------------
     A                                  4  2'Action Code  . . . . :'
     A            #PACTC         1A  O  4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'BIC identifier . . . :'
     A            #1BICC         8A  O  5 27TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1BICB         3A  O  5 37TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1INS1        30      5 43TEXT('BIC Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Pay currency . . . . :'
     A            #1PCCY         3A     6 27TEXT('Payment Currency')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Re Route BIC . . . . :'
     A            #1RBIC         8A     7 27TEXT('Re Route Through BIC')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1RBIB         3A     7 37TEXT('Re Route Through BIC branch')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1INT1        30      7 43TEXT('BIC Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'As cover. . . . . . . .'
     A            #1COVR         1   B  9 26TEXT('As Cover (Y/N)')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N75N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  9 30'Cover Flag value .'
     A            #1CFLG         1   B  9 49TEXT('Cover Flag')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A N75N46                               DSPATR(UL)
     A N46                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 10  2'Replace . . . . . . . .'
     A            #1RPLC         1   B 10 26TEXT('Replace (Y/N)')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N75N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 12  2'Apply for . . . . .BEN.'
     A                                 11 30'  Minimum Value '
     A                                      DSPATR(UL)
     A                                 11 50'  Maximum Value '
     A                                      DSPATR(UL)
     A            #1BENR         1   B 12 26TEXT('Reroute for BEN')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N75N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1BENLA       16   B 12 30TEXT('BEN Amount - Min')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N75N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1BENHA       16   B 12 50TEXT('BEN Amount - Max')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N75N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 13 21'SHA'
     A            #1SHAR         1   B 13 26TEXT('Reroute for SHA')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N75N37                               DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1SHALA       16   B 13 30TEXT('SHA Amount - Min')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A N75N38                               DSPATR(UL)
     A N38                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1SHAHA       16   B 13 50TEXT('SHA Amount - Max')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A N75N39                               DSPATR(UL)
     A N39                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 14 21'OUR'
     A            #1OURR         1   B 14 26TEXT('Reroute for OUR')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N75N40                               DSPATR(UL)
     A N40                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1OURLA       16   B 14 30TEXT('OUR Amount - Min')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N75N41                               DSPATR(UL)
     A N41                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1OURHA       16   B 14 50TEXT('OUR Amount - Max')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N75N42                               DSPATR(UL)
     A N42                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 15  2'Description . . . . . .'
     A            #1DES1        35   B 15 26TEXT('Description 1')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N75N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1DES2        35   B 16 26TEXT('Description 2')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N75N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1DES3        35   B 17 26TEXT('Description 3')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N75N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
      *---------------------------------------------------------------------
      * Active Dates
      *
     A                                 18  2'Status. . . . . . . . .'
     A            #1STAT         1   B 18 26TEXT('Status' )
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N75N43                               DSPATR(UL)
     A N43                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 18 28'Start date'
     A            #1STDTA        6   B 18 39TEXT('Start Date')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A N75N44                               DSPATR(UL)
     A N44                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 18 47'End date'
     A            #1ENDTA        6   B 18 56TEXT('End Date')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A N75N45                               DSPATR(UL)
     A N45                                  DSPATR(HI)
     A N25                                  OVRATR
      *
      *=========================================================================
      * Audit Information
     A                                 19  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL)
     A N25                                  OVRATR
     A                                 20  2'User name :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10A  O 20 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 25'Action Date :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7A  O 20 39TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 47'Action Time :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6Y 0O 20 61TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
