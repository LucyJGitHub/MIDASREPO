      *****************************************************************
/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *   Midas - Funds Transfer Module                               *
      *                                                               *
      *   DSPF/FTP0690DF - STP Payment Correspondent Maintenance      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *   Last Amend No. MD31937          Date 13Jan15                *
      *   Last Amend No. AR856737         Date 13Sep11                *
      *   Last Amend No. PNL014           Date 07Dec2005              *
      *   Prev Amend No. EEE065           Date 30Mar2004              *
      * Midas Release 4.01------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *    MD31937  - Upgrade STP enhancements to BF Midas 2.1        *
      *   AR856737 - Upgrade STP enhancements to Midas Plus level     *
      *   PNL014 - Time Sensitive Routing                             *
      *   EEE065 - FT Payment Routing                                 *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
      *---------------------------------------------------------------------
     A            #1RECI         1   H      TEXT('Record Identifier')
      *---------------------------------------------------------------------
      * Audit information
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A            #1CTRY         2   H      TEXT('Country Location')
     A            #1PCOR         1   H      TEXT('Preferential Correspondent')
     A            #1PNBR         7S 0H      TEXT('Preferential Number Routed')
     A            #1S71A         7   H      TEXT('Std Quotation of 71A')
     A            #1DACR        34   H      TEXT('Direct Account reference')
      *---------------------------------------------------------------------
      * Direct account in Midas
     A            #1DBRC         3A  H      TEXT('Direct Account Branch')
     A            #1DCST         6A  H      TEXT('Direct Account Customer')
     A            #1DCCY         3A  H      TEXT('Direct Account Currency')
     A            #1DACD         4A  H      TEXT('Direct Account Account code')
     A            #1DASQ         2A  H      TEXT('Direct Account Account Seq.')
      *
      *---------------------------------------------------------------------
     A            #1LVPA         1   H      TEXT('Low Value Payment Agreement')
     A            #1LVPT        15S 0H      TEXT('Low Value Threshold')
     A            #1Q71A         7   H      TEXT('Quotation of 71A')
     A            #1PLNB         7S 0H      TEXT('Preferential LVP Nbr Routed')
     A            #1SCHR        13S 8H      TEXT('Standard - Charge %')
     A            #1SCHL        15S 0H      TEXT('Standard - Min Fee')
     A            #1SCHH        15S 0H      TEXT('Standard - Max Fee')
     A            #1SCHC         5   H      TEXT('Standard - Charge code')
     A            #1LCHR        13S 8H      TEXT('LVPA - Charge %')
     A            #1LCHL        15S 0H      TEXT('LVPA - Min Fee')
     A            #1LCHH        15S 0H      TEXT('LVPA - Max Fee')
     A            #1LCHC         5   H      TEXT('LVPA - Charge code')
     A            #1REGP         1   H      TEXT('Regulated Payment Agreement')
     A            #1REGT        15S 0H      TEXT('Regulated Threshold')
     A            #1R71A         7   H      TEXT('Reg Quotation of 71A')
     A            #1PRNB         7S 0H      TEXT('Preferential Reg Nbr Routed')
     A            #1DES1        35   H      TEXT('Description 1')
     A            #1DES2        35   H      TEXT('Description 2')
     A            #1DES3        35   H      TEXT('Description 3')
      *---------------------------------------------------------------------
     A            #1IDAB         1   H      TEXT('Inhibit Direct A/c BEN')
     A            #1IDAS         1   H      TEXT('Inhibit Direct A/c SHA')
     A            #1IDAO         1   H      TEXT('Inhibit Direct A/c OUR')
     A            #1SPCR         1   H      TEXT('Split Preferential Cor')
     A            #1SPTH        15S 0H      TEXT('Split Threshold')
     A            #1SPBN         7S 0H      TEXT('Split Pref BEN Number Routed')
     A            #1SPSN         7S 0H      TEXT('Split Pref SHA Number Routed')
     A            #1SPON         7S 0H      TEXT('Split Pref OUR Number Routed')
     A            #1BPAL        15S 0H      TEXT('BEN Payment Amount - Min')
     A            #1BPAH        15S 0H      TEXT('BEN Payment Amount - Max')
     A            #1SPAL        15S 0H      TEXT('SHA Payment Amount - Min')
     A            #1SPAH        15S 0H      TEXT('SHA Payment Amount - Max')
     A            #1OPAL        15S 0H      TEXT('OUR Payment Amount - Min')
     A            #1OPAH        15S 0H      TEXT('OUR Payment Amount - Max')
     A            #1STMT         2   H      TEXT('Settlement Method')
     A            #1PVAS        12   H      TEXT('Payment Value Added Service')
     A            #1OTHR        15S 0H      TEXT('Override Threshold Amount')
     A            #1OSTM         2   H      TEXT('Override Settlement Method')
     A            #1OVAS        12   H      TEXT('Override Value Added Service')
     A            #1O71A         7   H      TEXT('Override Quotation of 71A')
     A            #1OACR        34   H      TEXT('Override Account reference')
      *---------------------------------------------------------------------
      * Direct account in Midas
     A            #1OBRC         3A  H      TEXT('Override Account Branch')
     A            #1OCST         6A  H      TEXT('Override Account Customer')
     A            #1OCCY         3A  H      TEXT('Override Account Currency')
     A            #1OACD         4A  H      TEXT('Override Account Account cde')
     A            #1OASQ         2A  H      TEXT('Override Account Account Seq')
      *
      *---------------------------------------------------------------------
      * Cover Replacement Information
     A            #1R910         1   H      TEXT('Replace 202C with 910')
      *
      *---------------------------------------------------------------------
      * Active Dates
      *
     A            #1STAT         1   H      TEXT('Status' )
     A            #1STDT         5S 0H      TEXT('Start Date')
     A            #1ENDT         5S 0H      TEXT('End Date')
      * PNL014 Start
      *---------------------------------------------------------------------
      * Time Sensitive Routing
     A            #1TSDO         5   H      TEXT('TSP Date Offset')
     A            #1TSCT         4S 0H      TEXT('TSP Cut Off Time')
     A            #1TSCD         2S 0H      TEXT('TSP Cut Off Day Offset')
     A            #1TSCK         1   H      TEXT('TSP Cut Off Check')
     A            #1TSHC         3   H      TEXT('TSP Cut Off Holiday Cal. Ccy')
     A            #1TSHL         3   H      TEXT('TSP Cut Off Holiday Cal. Loc')
     A            #1TSFC         1   H      TEXT('TSP Fail Condition')
     A            #1TSIS        10   H      TEXT('TSP Iteration Flags')
     A            #1RREF        25   H      TEXT('Route Reference')
      *
      *---------------------------------------------------------------------
      * PNL014 End
      *---------------------------------------------------------------------
     A            #1SEL          1   B  9  2
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A            #1BICC         8      9  4TEXT('BIC (Bank, Country, & Loc')
     A                                      DSPATR(HI)
     A            #1BICB         3      9 13TEXT('BIC (Branch Code)')
     A                                      DSPATR(HI)
     A            #1PCCY         3      9 20TEXT('Payment Currency')
     A                                      DSPATR(HI)
     A            #1CTYP        10      9 25TEXT('Correspondent Type')
     A                                      DSPATR(HI)
     A            #1INS1        35      9 38TEXT('BIC Institution Name')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
      *
     A N80 81 82                            SFLEND(*MORE)
      *
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2BICC')
     A                                      SETOF(32 'Invalid: #2BICB')
     A                                      SETOF(33 'Invalid: #2PCCY')
     A                                      SETOF(34 'Invalid: #2CTYP')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'FTP0690F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
      *---------------------------------------------------------------*
      *---------------------------------------------------------------*
     A                                  4  2'Position on.:'
     A                                  4 16'BIC Identifier .:'
     A            #2BICC         8   B  4 35TEXT('BIC Identifier')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5  2'Select on...:'
     A                                  5 16'BIC Branch.:'
     A            #2BICB         3   B  5 29TEXT('BIC Branch')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5 33'Currency  .:'
     A            #2PCCY         3   B  5 46TEXT('Pay Currency')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5 50'Type.:'
     A            #2CTYP        10   B  5 57TEXT('Correspondent Type')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34N70                               DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A**81*****                         7  2'? BIC Identifier  Ccy   Type      -
     A*********                               Institution Name                 -
     A*********                                      '
     A  81                              7  2' ?  BIC Identifier  Ccy  Type     -             MD31937
     A                                             Institution Name            -             MD31937
     A                                               '                                       MD31937
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8  4'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(HI) COLOR(WHT)
     A                                      DSPATR(UL)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
