      *****************************************************************
/*STDA*  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas FT BIC Plus Enquiry (STP)')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  FTP0716DF - Midas FT BIC Plus Enquiry (STP)                  *
      *                                                               *
      *  (C) Copyright Misys International Banking Systems 2004       *
      *                                                               *
      *  Last Amend No. MD031937         Date 13Jan15                 *
      *  Prev Amend No. AR856737         Date 13Sep11                 *
      *                 EEE065  *Create    Date 19May2004             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *    MD031937 - Upgrade STP enhancements to BF Midas 2.1        *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  EEE065 - FT Payment Routing                                  *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03
     A                                      CA12
     A                                      CF18
     A  24N25                               ROLLUP(29)
     A*=========================================================================
     A* SUBFILE CONTROLE
     A*=========================================================================
     A          R FTP716F0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A
     A            @0SEL          1   B 12  2TEXT('Select')
     A                                      DSPATR(HI UL)
     A                                      VALUES('A' 'B' 'C' 'D' +
     A                                             'E' 'X' ' ')
     A            @0INST        50   O 12  5
     A                                      DSPATR(HI)
     A            @0NATI        15   O 12 56
     A                                      DSPATR(HI)
     A            @0LOCB        35   O 13  5
     A                                      DSPATR(HI)
     A            @0BIC         11   O 13 42
     A                                      DSPATR(HI)
     A            @0CHIP         6   O 13 55
     A                                      DSPATR(HI)
     A* HIDDEN FIELDS
     A            @0HBRC        70   H      TEXT('Branch')
     A            @0HLOC        90   H      TEXT('Location')
     A            @0HINS       105   H      TEXT('Institution')
     A            @0HCIT        35   H      TEXT('City')
     A            @0HCNT        70   H      TEXT('Country')
     A            @0HADD       140   H      TEXT('Address')
     A            @0HZIP        15   H      TEXT('ZIP')
     A            @0HPNB        35   H      TEXT('PO Box Number')
     A            @0HPZP        15   H      TEXT('PO Box ZIP')
     A            @0HPLC        90   H      TEXT('PO Box Location ')
     A
     A
     A*=========================================================================
     A* SUBFILE
     A*=========================================================================
     A          R FTP716F1                  SFLCTL(FTP716F0)
     A                                      TEXT('Screen header')
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                      SFLCSRRRN(&@SFPOS)
     A  24                                  SFLDSP
     A  30                                  SFLDSPCTL
     A  23                                  SFLCLR
     A  25                                  SFLEND
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A            @SFREC         4S 0H      TEXT('SFLRCDNBR field')
     A                                      SFLRCDNBR(CURSOR)
     A            @SFPOS         5S 0H
     A                                 10  2'?'
     A                                 10  5'Institution'
     A                                 10 56'National Identifier'
     A            @LOCBR         8A  O 11  5
     A                                 11 42'BIC Code'
     A                                 11 55'CHIPS Identifier'
     A*=========================================================================
     A* SELECTION SCREEN
     A*=========================================================================
     A*
     A          R FTP716F2
     A                                      OVERLAY
     A* Screen HEADER
     A* -----------
     A            ##USR         10A     1  2
     A                                      DSPATR(HI)
     A                                  1 13'FTP0716'
     A                                      DSPATR(HI)
     A                                  1 30'BIC Database Plus Search'
     A                                      DSPATR(HI UL)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A            ##TIME         6Y 0O  2 71EDTWRD('  .  .  ')
     A                                      DSPATR(HI)
     A* Screen text
     A* -----------
     A                                  4  2'Select on: Search criteria'
     A                                  5  2'Or:        Institution    '
     A N40                              6  2'           Location'
     A  40                              6  2'           Branch'
     A                                  7  2'           S.W.I.F.T BIC'
     A                                  8  2'           National Id'
     A                                  8 45'CHIPS'
     A            @2CRIV        35A  B  4 29TEXT('Search Criteria')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            @2INSV        35A  B  5 29TEXT('Institution')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            @2LBRV        35A  B  6 29TEXT('Location/Branch')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            @2BICV        11A  B  7 29TEXT('SWIFT BIC')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            @2NATV        15A  B  8 29TEXT('National Id')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            @2CHPV         6A  B  8 51TEXT('CHIPS')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*=========================================================================
     A* BIC+ DETAILS
     A*=========================================================================
     A*
     A          R FTP716F3
     A                                      PUTOVR
     A                                      OVRATR
     A                                      OVRDTA
     A* Screen HEADER
     A* -----------
     A            ##USR         10A     1  2
     A                                      DSPATR(HI)
     A                                  1 13'FTP0716'
     A                                      DSPATR(HI)
     A                                  1 30'BIC Database Plus Search'
     A                                      DSPATR(HI UL)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A            ##TIME         6Y 0O  2 71EDTWRD('  .  .  ')
     A                                      DSPATR(HI)
     A* Screen text
     A* -----------
     A                                  3  2'Institution'
     A                                  7  2'Branch'
     A                                  9  2'Address'
     A                                 13  2'City'
     A                                 13 48'ZIP'
     A                                 15  2'Country'
     A                                 17  2'S.W.I.F.T. BIC    '
     A                                 18  2'National bank code'
     A                                 19  2'CHIPS code        '
     A                                 21  2'PO Box'
     A                                 21 48'ZIP'
     A                                 22  2'Location'
     A                                 23  2'F3=Exit F12=Previous'
     A            @3INS1        70   O  4 11TEXT('Institution line 1')
     A                                      DSPATR(HI)
     A            @3INS2        35   O  5 11TEXT('Institution line 2')
     A                                      DSPATR(HI)
     A            @3BRCA        70   O  7 11TEXT('Branch line 1')
     A                                      DSPATR(HI)
     A            @3ADD1        35   O  9 11TEXT('Address line 1')
     A                                      DSPATR(HI)
     A            @3ADD2        35   O 10 11TEXT('Address line 2')
     A                                      DSPATR(HI)
     A            @3ADD3        35   O 11 11TEXT('Address line 3')
     A                                      DSPATR(HI)
     A            @3ADD4        35   O 12 11TEXT('Address line 4')
     A                                      DSPATR(HI)
     A            @3CITY        35   O 13 11TEXT('City')
     A                                      DSPATR(HI)
     A            @3ZIP1        15   O 13 53TEXT('ZIP Code')
     A                                      DSPATR(HI)
     A            @3CNTY        70   O 15 11TEXT('Country line 1')
     A                                      DSPATR(HI)
     A            @3BIC         11   O 17 21TEXT('BIC')
     A                                      DSPATR(HI)
     A            @3NATI        11   O 18 21TEXT('National bank code')
     A                                      DSPATR(HI)
     A            @3CHIP         6   O 19 21TEXT('CHIPS')
     A                                      DSPATR(HI)
     A            @3POBN        35   O 21 11TEXT('PO Box number')
     A                                      DSPATR(HI)
     A            @3PZIP        15   O 21 53TEXT('PO Box ZIP')
     A                                      DSPATR(HI)
     A            @3PLOC        70   O 22 11TEXT('PO Box Location line 1')
     A                                      DSPATR(HI)
     A*=========================================================================
     A* ' No record ' Format
     A*================================================================
     A*
     A          R FTNORECD
     A                                      PUTOVR
     A                                      OVRATR
     A                                      OVRDTA
     A* Screen text
     A* -----------
     A                                 15 21'No record matches the selection +
     A                                      criteria'
     A                                      DSPATR(HI)
     A*================================================================
     A* Sreen Footer
     A*================================================================
     A          R FTFOOTER                  TEXT('Screen footer')
     A                                      OVERLAY
     A*                                     OVRDTA
     A*                                     OVRATR
     A*                                     PUTOVR
     A*                                     PUTOVR
     A*                                     OVRATR
     A*                                     OVRDTA
     A
     A  41N42                          22  2'A=BIC B=Branch C=Clearing code D=A+
     A                                      ddress E=Enquire X=Default'
     A  41 42                          22  2'E=Enquiry '
     A N40                             23  2'F3=Exit F12=Previous F18=Branch'
     A  40                             23  2'F3=Exit F12=Previous F18=Location'
     A*=========================================================================
     A* Message record format
     A*================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A* Message control format
     A*================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  23                                  SFLCLR
     A*                                     PUTOVR
     A*                                     OVRDTA
     A*                                     OVRATR
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
