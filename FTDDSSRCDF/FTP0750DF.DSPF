      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT STP Requirements Maintenance Values')
      *****************************************************************
      *                                                               *
      *   Midas - Funds Transfer Module                               *
      *                                                               *
      *   DSPF/FTP0750DF - Midas FT STP Requirements Maintenance Values
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *   Last Amend No. MD031937         Date 13Jan15                *
      *   Prev Amend No. AR856737         Date 13Sep11                *
      *                  EEE065  *CREATE  Date 19Mar2004              *
      * Midas Release 4.01------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *    MD031937 - Upgrade STP enhancements to BF Midas 2.1        *
      *    AR856737 - Upgrade STP enhancements to Midas Plus level    *
      *    EEE065 - FT Payment Routing                                *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(31 'Invalid: #1MTAG')
     A                                      SETOF(32 'Invalid: #1TSEQ')
     A                                      SETOF(99 'Global error flag')
      *----------------------------------------------------------------
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'FTP0750F1'
     A N25                                  OVRATR
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code  . . . . :'
     A            #PACTC         1A  O  4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'BIC identifier . . . :'
     A            #1BICC         8A  O  5 27TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1BICB         3A  O  5 37TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1INS1        30      5 43TEXT('BIC Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'LVP Agreement. . . . :'
     A            #1LVPA         1A     7 27TEXT('LVP Agreement')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'Regulated Payment  . :'
     A            #1REGP         1A     9 27TEXT('Regulated Pay')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11  2'Message Tag  . . . . :'
     A            #1MTAG         5A  B 11 27TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 13  2'Sequence . . . . . . :'
     A            #1TSEQ         3A  B 13 27TEXT('Sequence')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #RCDDTL1
      *
     A                                      TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Previous')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1TEST')
     A                                      SETOF(32 'Invalid: #1TSTD')
     A                                      SETOF(33 'Invalid: #1DER1')
     A                                      SETOF(34 'Invalid: #1DER2')
     A                                      SETOF(35 'Invalid: #1DER3')
      *-------------------------------------------------------------------------
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'FTP0750F2'
     A N25                                  OVRATR
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *---------------------------------------------------------------------
     A            #1RECI         1A  H      TEXT('Record Identifier')
     A            #1AJOB        10A  H      TEXT('Job name')
     A            #1AACT         1A  H      TEXT('Action Type')
     A            #1RDNB         5S 0H      TEXT('Run day number')
      *---------------------------------------------------------------------
     A                                  4  2'Action Code  . . . . :'
     A            #PACTC         1A  O  4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'BIC identifier . . . :'
     A            #1BICC         8A  O  5 27TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1BICB         3A  O  5 37TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1INS1        30      5 43TEXT('BIC Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'LVP Agreement. . . . :'
     A            #1LVPA         1A     7 27TEXT('LVP Agreement')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'Regulated Payment  . :'
     A            #1REGP         1A     9 27TEXT('Regulated Pay')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11  2'Message Tag  . . . . :'
     A            #1MTAG         5A  O 11 27TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 13  2'Sequence . . . . . . :'
     A            #1TSEQ         3A  O 13 27TEXT('Sequence')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 15  2'Test. . . . . . . . .:'
     A            #1TEST        10A  B 15 26TEXT('Test')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N75N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 17  2'Test Data . . . . . .:'
     A            #1TSTD        30A  B 17 26TEXT('Test Data')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N75N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 13 37'Notes :'
     A            #1DER1        35   B 13 45TEXT('Description 1')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N75N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1DER2        35   B 14 45TEXT('Description 2')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N75N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1DER3        35   B 15 45TEXT('Description 3')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N75N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
      *
      *=========================================================================
      * Audit Information
     A                                 19  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL)
     A N25                                  OVRATR
     A                                 20  2'User name :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10A  O 20 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 25'Action Date :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7A  O 20 39TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 47'Action Time :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6Y 0O 20 61TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
