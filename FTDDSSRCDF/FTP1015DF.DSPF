      *****************************************************************
/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *   Midas - Funds Transfer Module                               *
      *                                                               *
      *   DSPF/FTP1015DF - Midas FT Bank Correspondent relationship   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *   Last Amend No. MD31937          Date 13Jan15                *
      *   Last Amend No. AR856737         Date 13Sep11                *
      *   Last Amend No. EEE065           Date 16Nov2004              *
      *   Prev Amend No. EEE065           Date 16Nov2004              *
      * Midas Release 4.01------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *    MD31937  - Upgrade STP enhancements to BF Midas 2.1        *
      *    AR856737 - Upgrade STP enhancements to Midas Plus level    *
      *    ESL038 - ING FT STP Development                            *
      *    EEE065 - FT Payment Routing                                *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(31 'Invalid: #1BIC*')
     A                                      SETOF(99 'Global error flag')
      *----------------------------------------------------------------
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'FTP1025F1'
     A N25                                  OVRATR
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code  . . . . :'
     A            #PACTC         1A  O  4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'BIC identifier . . . :'
     A            #1BICC         8A  B  5 27TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1BICB         3A  B  5 37TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1INS1        30      5 43TEXT('BIC Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #RCDDTL1
      *
     A                                      TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Previous')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1ORST')
     A                                      SETOF(32 'Invalid: #1OMAN')
     A                                      SETOF(33 'Invalid: #1OMTH')
     A                                      SETOF(34 'Invalid: #1PSTI')
     A                                      SETOF(35 'Invalid: #1FREQ')
     A                                      SETOF(36 'Invalid: #1PDNB')
     A                                      SETOF(37 'Invalid: #1NSTD')
     A                                      SETOF(38 'Invalid: #1ABRC')
     A                                      SETOF(39 'Invalid: #1ACST')
     A                                      SETOF(40 'Invalid: #1ACST')
     A                                      SETOF(41 'Invalid: #1ACST')
     A                                      SETOF(42 'Invalid: #1DES*')
      *-------------------------------------------------------------------------
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'FTP1025F2'
     A N25                                  OVRATR
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *---------------------------------------------------------------------
     A            #1PDNB         2S 0H      TEXT('Periodic Day Nbr')
     A            #1LSTD         5S 0H      TEXT('Last Statement Date')
     A            #1NSTD         5S 0H      TEXT('Next Statement Date')
      *---------------------------------------------------------------------
     A            #1INS2        35   H      TEXT('Institution Name 2')
     A            #1INS3        35   H      TEXT('Institution Name 3')
      *---------------------------------------------------------------------
     A            #1RECI         1A  H      TEXT('Record Identifier')
     A            #1AJOB        10A  H      TEXT('Job name')
     A            #1AACT         1A  H      TEXT('Action Type')
     A            #1RDNB         5S 0H      TEXT('Run day number')
      *---------------------------------------------------------------------
     A                                  4  2'Action Code  . . . . :'
     A            #PACTC         1A  O  4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'BIC identifier . . . :'
     A            #1BICC         8A     5 27TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1BICB         3A     5 37TEXT('BIC Identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1INS1        30      5 43TEXT('BIC Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 07  2'OUR Restricted. . . .:'
     A            #1ORST         1A  B 07 26TEXT('OUR Restricted')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N75N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 08  2'OUR Manual . . . . ..:'
     A            #1OMAN         1A  B 08 26TEXT('OUR Manual')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N75N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 09  2'OUR Monthly. . . . ..:'
     A            #1OMTH         1A  B 09 26TEXT('OUR Month')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N75N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 11  2'Periodic Statement ..:'
     A            #1PSTI         1A  B 11 26TEXT('Periodic Statement')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N75N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 11 29'Frequency . .:'
     A            #1FREQ         1A  B 11 44TEXT('Period Frequency')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N75N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 11 51'Day Number.:'
     A            #1PDNBA        2A  B 11 64TEXT('Periodic Day Nbr')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N75N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 12 29'Next Statement Date.:'
     A            #1NSTDA        6A  B 12 51TEXT('Next Statement Date')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N75N37                               DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 13 29'Last Statement Date.:'
     A            #1LSTDA        6A  O 13 51TEXT('Last Statement Date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14 29'Account branch......:'
     A            #1ABRC         3A  B 14 51TEXT('Accounting Branch')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A N75N38                               DSPATR(UL)
     A N38                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 14 55'Customer:'
     A            #1ACST         6A  B 14 65TEXT('Accounting Customer')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A N75N39                               DSPATR(UL)
     A N39                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 15 29'Account code . .....:'
     A            #1ACCD         4A  B 15 51TEXT('Accounting A/c/ code')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N75N40                               DSPATR(UL)
     A N40                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 15 55'Sequence:'
     A            #1ACSQ         2A  B 15 65TEXT('Accounting A/c Seq')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N75N41                               DSPATR(UL)
     A N41                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 16  2'Description . . . . .:'
     A            #1DES1        30A  B 16 26TEXT('Description 1')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N75N42                               DSPATR(UL)
     A N42                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1DES2        30A  B 17 26TEXT('Description 2')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N75N42                               DSPATR(UL)
     A N42                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1DES3        30A  B 18 26TEXT('Description 3')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N75N42                               DSPATR(UL)
     A N42                                  DSPATR(HI)
     A N25                                  OVRATR
      *
      *=========================================================================
      * Audit Information
     A                                 19  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL)
     A N25                                  OVRATR
     A                                 20  2'User name :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10A  O 20 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 25'Action Date :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7A  O 20 39TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 47'Action Time :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6Y 0O 20 61TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
