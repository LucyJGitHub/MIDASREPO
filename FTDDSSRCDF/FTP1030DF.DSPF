      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Segment Customer Group X-Ref')
      *****************************************************************
      *                                                               *
      *   Midas - Funds Transfer Module                               *
      *                                                               *
      *   DSPF/FTP1030DF - Segment Customer Group X-Ref               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *   Last Amend No. MD031937         Date 13Jan15                *
      *   Prev Amend No. AR856737         Date 13Sep11                *
      *                  EEE065  *CREATE  Date 16Nov2004              *
      * Midas Release 4.01------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   MD031937 - Upgrade STP enhancements to BF Midas 2.1         *
      *   AR856737 - Upgrade STP enhancements to Midas Plus level     *
      *   EEE065 - FT Payment Routing                                 *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
      *---------------------------------------------------------------------
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1DES2        30   H      TEXT('Decription 2')
     A            #1DES3        30   H      TEXT('Decription 3')
      *---------------------------------------------------------------------
      * Audit information
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
      *---------------------------------------------------------------------
     A            #1SEL          1   B  9  2
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A            #1BSEG         3      9  6TEXT('Beneficiary Segment')
     A                                      DSPATR(HI)
     A            #1SNBR         1      9 20TEXT('Set Number')
     A                                      DSPATR(HI)
     A            #1GPCD         4      9 26TEXT('Group Code')
     A                                      DSPATR(HI)
     A            #1DES1        35      9 33TEXT('Description 1')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
      *
     A N80 81 82                            SFLEND(*MORE)
      *
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2BSEG')
     A                                      SETOF(32 'Invalid: #2SNBR')
     A                                      SETOF(33 'Invalid: #2GPCD')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'FTP1030F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
      *---------------------------------------------------------------*
     A                                  4  2'Position on.:'
     A                                  4 16'Segment.........:'
     A            #2BSEG         3   B  4 34TEXT('Segment')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5  2'Select on...:'
     A                                  5 16'Set Number......:'
     A            #2SNBR         1   B  5 34TEXT('Set Number')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5 36'Group code...:'
     A            #2GPCD         4   B  5 51TEXT('Group Code')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33N70                               DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A  81                              7  2'? Segment        Set    Group  Des-
     A                                      cription                           -
     A                                               '
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8  4'              Number              -
     A                                                                         -
     A                                             '
     A                                      DSPATR(HI) COLOR(WHT)
     A                                      DSPATR(UL)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
