      *****************************************************************
/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *   Midas - Funds Transfer Module                               *
      *                                                               *
      *   DSPF/FTP1095DF - Midas FT PCAT Extension Information        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *   Last Amend No. MD31937          Date 13Jan15                *
      *   Last Amend No. AR856737         Date 13Sep11                *
      *   Last Amend No. EEE065           Date 16Nov2004              *
      * Midas Release 4.01------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *    MD31937  - Upgrade STP enhancements to BF Midas 2.1        *
      *    AR856737 - Upgrade STP enhancements to Midas Plus level    *
      *    EEE065 - FT Payment Routing                                *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(31 'Invalid: #1IBAN')
     A                                      SETOF(99 'Global error flag')
      *----------------------------------------------------------------
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'FTP1095F1'
     A N25                                  OVRATR
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code  . . . . :'
     A            #PACTC         1A  O  4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'IBAN .  . . . .  . . :'
     A            #1IBAN        34A  B  5 27TEXT('Country')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #RCDDTL1
      *
     A                                      TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Previous')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1SNBR')
     A                                      SETOF(32 'Invalid: #1GPCD')
     A                                      SETOF(33 'Invalid: #1DES*')
     A                                      SETOF(34 'Invalid: #1DES*')
     A                                      SETOF(35 'Invalid: #1DES*')
     A                                      SETOF(36 'Invalid: #1DES*')
     A                                      SETOF(37 'Invalid: #1DES*')
     A                                      SETOF(38 'Invalid: #1DES*')
     A                                      SETOF(39 'Invalid: #1DES*')
     A                                      SETOF(40 'Invalid: #1DES*')
     A                                      SETOF(41 'Invalid: #1DES*')
     A                                      SETOF(42 'Invalid: #1DES*')
     A                                      SETOF(43 'Invalid: #1DES*')
     A                                      SETOF(44 'Invalid: #1DES*')
     A                                      SETOF(45 'Invalid: #1DES*')
     A                                      SETOF(46 'Invalid: #1DES*')
     A                                      SETOF(47 'Invalid: #1DES*')
     A                                      SETOF(48 'Invalid: #1DES*')
     A                                      SETOF(49 'Invalid: #1DES*')
     A                                      SETOF(50 'Invalid: #1DES*')
     A                                      SETOF(51 'Invalid: #1DES*')
      *-------------------------------------------------------------------------
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'FTP1095F2'
     A N25                                  OVRATR
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *---------------------------------------------------------------------
      *---------------------------------------------------------------------
      * Incoming Payment Control Data
     A            #1COMA        15S 3H      TEXT('Incoming Commission Amount')
     A            #1FXLA        15S 3H      TEXT('Inc FX Rate Limit Amount')
     A            #1ICUT         4S 0H      TEXT('Incoming Cut Off Time')
      *
      *---------------------------------------------------------------------
      * Outgoing Payment Control Data
     A            #1OCMA        15S 3H      TEXT('Outgoing Commission Amount')
     A            #1OXLA        15S 3H      TEXT('Out FX Rate Limit Amount')
     A            #1OCUT         4S 0H      TEXT('Outgoing Cut Off Time')
      *
      *---------------------------------------------------------------------
      * Other Information
      *
      *---------------------------------------------------------------------
      * Account Status
      *---------------------------------------------------------------------
     A            #1AJOB        10A  H      TEXT('Job name')
     A            #1AACT         1A  H      TEXT('Action Type')
     A            #1RDNB         5S 0H      TEXT('Run day number')
      *---------------------------------------------------------------------
     A                                  4  2'Action Code  . . . . :'
     A            #PACTC         1A  O  4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'IBAN .  . . . .  . . :'
     A            #1IBAN        34A  O  5 27TEXT('IBAN')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7 45'Special Routing . :'
     A            #1SPRT         1A  B  7 65TEXT('Special Routing Ind.')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N75N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  8  2'Default Account . . .:'
     A            #1DACC         1A  B  8 26TEXT('Default Account Indicator')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N75N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  8 45'Pensioner . . . . :'
     A            #1PENR         1A  B  8 65TEXT('Pensioner')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N75N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  9  2'Account Status  . . .:'
     A            #1ASTS         1A  B  9 26TEXT('Account Status')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N75N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  9 45'Midas Customer. . :'
     A            #1CUST         6A  B  9 65TEXT('Midas Customer')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N75N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 11  2'                          Incoming-
     A                                             Outgoing                   '
     A                                 12  2'Commission Ind/Amt. .:'
     A            #1COMI         1A  B 12 26TEXT('Inc Commission Indicator')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N75N37                               DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1COMAA       11A  B 12 28TEXT('Incoming Commission Amount')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A N75N38                               DSPATR(UL)
     A N38                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1OCMI         1A  B 12 41TEXT('Out Commission Indicator')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A N75N39                               DSPATR(UL)
     A N39                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1OCMAA       11A  B 12 43TEXT('Outgoing Commission Amount')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N75N40                               DSPATR(UL)
     A N40                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 13  2'FX Rate Ind/Amt.  . .:'
     A            #1FXRI         1A  B 13 26TEXT('Inc FX Rate Indicator')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N75N41                               DSPATR(UL)
     A N41                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1FXLAA       11A  B 13 28TEXT('Incoming FX Limit Amount')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N75N42                               DSPATR(UL)
     A N42                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1OXRI         1A  B 13 41TEXT('Out FX Rate Indicator')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N75N43                               DSPATR(UL)
     A N43                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1OXLAA       11A  B 13 43TEXT('Outgoing FX Limit Amount')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A N75N44                               DSPATR(UL)
     A N44                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 14  2'Date Offset or Time .:'
     A            #1IDOC         5A  B 14 26TEXT('Incoming Date Offset code')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A N75N45                               DSPATR(UL)
     A N45                                  DSPATR(HI)
     A N25                                  OVRATR
      *
     A            #1ICUTA        4A  B 14 35TEXT('Incoming Cut Off Time')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A N75N46                               DSPATR(UL)
     A N46                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1ODOC         5A  B 14 43TEXT('Outgoing Date Offset code')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A N75N47                               DSPATR(UL)
     A N47                                  DSPATR(HI)
     A N25                                  OVRATR
      *
     A            #1OCUTA        4A  B 14 52TEXT('Outgoing Cut Off Time')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A N75N48                               DSPATR(UL)
     A N48                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 15  2'FX Spread set . . . .:'
     A            #1IFXS         5A  B 15 26TEXT('Incoming FX Spread Set')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A N75N49                               DSPATR(UL)
     A N49                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1OFXS         5A  B 15 43TEXT('Outgoing FX Spread Set')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A N75N50                               DSPATR(UL)
     A N50                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 16  2'Trans in Msg Ccy. . .:'
     A            #1IMCY         1A  B 16 26TEXT('Incoming in Msg Ccy')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A N75N51                               DSPATR(UL)
     A N51                                  DSPATR(HI)
     A N25                                  OVRATR
      *
      *=========================================================================
      * Audit Information
     A                                 19  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL)
     A N25                                  OVRATR
     A                                 20  2'User name :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10A  O 20 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 25'Action Date :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7A  O 20 39TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 47'Action Time :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6Y 0O 20 61TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
