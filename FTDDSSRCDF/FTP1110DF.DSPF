      *****************************************************************
/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *   Midas - Funds Transfer Module                               *
      *                                                               *
      *   DSPF/FTP1110DF - Midas FT Currency Cut-Off                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *   Last Amend No. MD31937          Date 13Jan15                *
      *   Last Amend No. AR856737         Date 13Sep11                *
      *   Last Amend No. EEE065           Date 16Nov2004              *
      * Midas Release 4.01------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   MD31937  - Upgrade STP enhancements to BF Midas 2.1         *
      *   AR856737 - Upgrade STP enhancements to Midas Plus level     *
      *   EEE065 - FT Payment Routing                                 *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
      *---------------------------------------------------------------------
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1DES2        30   H      TEXT('Decription 2')
     A            #1DES3        30   H      TEXT('Decription 3')
      *---------------------------------------------------------------------
      * Audit information
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
      *---------------------------------------------------------------------
     A            #1ICUT         4S 0H      TEXT('Incoming Cut Off Time')
      *---------------------------------------------------------------------
     A            #1SEL          1   B  9  2
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A            #1CYCD         3      9  5TEXT('Currency Code       (key)')
     A                                      DSPATR(HI)
     A*****       #1BSEG         3      9 12TEXT('Customer Segment')
     A            #1GPCD         4      9 11TEXT('Customer Group Code')
     A                                      DSPATR(HI)
     A            #1NWRK         6      9 18TEXT('Network')
     A                                      DSPATR(HI)
     A            #1MTPY         3      9 26TEXT('Message type')
     A                                      DSPATR(HI)
     A            #1IDOC         5      9 32TEXT('Incoming Date Offset code')
     A                                      DSPATR(HI)
     A            #1ICUTA        4      9 40TEXT('Incoming Cut Off Time')
     A                                      DSPATR(HI)
     A            #1DES1        30      9 46TEXT('Decription 1')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
      *
     A N80 81 82                            SFLEND(*MORE)
      *
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2CYCD')
     A*****                                 SETOF(32 'Invalid: #2BSEG')                       STP499
     A                                      SETOF(32 'Invalid: #2GPCD')                       STP499
     A                                      SETOF(33 'Invalid: #2NWRK')
     A                                      SETOF(34 'Invalid: #2MTPY')
     A                                      SETOF(35 'Invalid: #2IDOC')
     A                                      SETOF(36 'Invalid: #2ICUT')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'FTP1110F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
      *---------------------------------------------------------------*
     A                                  4  2'Position on.:'
     A                                  4 16'Currency . . ...:'
     A            #2CYCD         3   B  4 34TEXT('Currency Code       (key)')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5  2'Select on...:'
     A*****                             5 16'Segment.........:'                               STP499
     A*****       #2BSEG         3   B  5 34TEXT('Customer Segment')                          STP499
     A                                  5 16'Group Code......:'                               STP499
     A            #2GPCD         3   B  5 34TEXT('Customer Group Code')                       STP499
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5 38'Network....:'
     A            #2NWRK         6   B  5 51TEXT('Network')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5 58'Msg Type..:'
     A            #2MTPY         3   B  5 70TEXT('Message type')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  6 16'Date offset.....:'
     A            #2IDOC         5   B  6 34TEXT('Incoming Date Offset code')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  6 40'Cut Off..:'
     A            #2ICUTA        4   B  6 51TEXT('Incoming Cut Off Time')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36N70                               DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A**81*                             7  2'? Ccy   Segment Network Msg   Date-              STP499
     A**81***                           7  2'? Ccy    Group  Network Msg   Date-              STP499
     A*****                                     Cut   Description              -
     A*****                                          '
     A  81                              7  2'?   Ccy   Group   Network  Msg   D-             MD31937
     A                                      ate      Cut   Description         -             MD31937
     A                                               '                                       MD31937
     A
     A                                      DSPATR(HI) COLOR(WHT)
     A**81*                             8  4'                      Type Offset -              STP499
     A**81***                           8  4'       Code           Type Offset -              STP499
     A*******                                 Off                              -
     A*******                                      '                                         MD31937
     A  81                              8  4'       Code            Type   Offs-             MD31937
     A                                      et    Off                          -             MD31937
     A                                             '                                         MD31937
     A                                      DSPATR(HI) COLOR(WHT)
     A                                      DSPATR(UL)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
