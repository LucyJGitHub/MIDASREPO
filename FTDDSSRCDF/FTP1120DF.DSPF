      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT STP Payment Correspondent General Routes')
      *****************************************************************
      *                                                               *
      *   Midas - Funds Transfer Module                               *
      *                                                               *
      *   DSPF/FTP1120DF - STP Payment Correspondent General Routes   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *   Last Amend No. MD031937         Date 13Jan15                *
      *   Prev Amend No. AR856737         Date 13Sep11                *
      *                  PNL015           Date 30Nov2005              *
      *                  EEE065           Date 25Nov2004              *
      * Midas Release 4.01------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   MD031937 - Upgrade STP enhancements to BF Midas 2.1         *
      *   AR856737 - Upgrade STP enhancements to Midas Plus level     *
      *   PNL015 - Redirection to head office BIC                     *
      *   EEE065 - FT Payment Routing                                 *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
      *---------------------------------------------------------------------
      *
     A            #1RECI         1   H      TEXT('Record Identifier')
      *
      *---------------------------------------------------------------------
      * Correspondent General Pay Route
      *
     A            #1RCAU         1   H      TEXT('Receive a/c Used')
     A            #1RCAC        35   H      TEXT('Receive a/c Quoted')
      *
      *---------------------------------------------------------------------
      * Main BIC details
      *
     A            #1CLRC         8   H      TEXT('Clearer Correspondent BIC')
     A            #1CLRB         3   H      TEXT('Clearer Correspondent BIC Br')
     A            #1UI56         1   H      TEXT('Use in Field 56')
     A* PNL015 start
     A            #1EX54         1   H      TEXT('Exclude from 54')
     A            #1HOBC         1   H      TEXT('Head Office BIC')
     A* PNL015 End
      *
      *---------------------------------------------------------------------
      *
     A            #1CREG         3   H      TEXT('Clearing Region')
      *
     A            #1CTY1         2   H      TEXT('Clearing Country 1')
     A            #1CTY2         2   H      TEXT('Clearing Country 2')
     A            #1CTY3         2   H      TEXT('Clearing Country 3')
     A            #1CTY4         2   H      TEXT('Clearing Country 4')
     A            #1CTY5         2   H      TEXT('Clearing Country 5')
      *
     A            #1CLID        10   H      TEXT('Clearing System Id.')
      *
      *---------------------------------------------------------------------
      *
      *---------------------------------------------------------------------
     A            #1BENR         1   H      TEXT('Route for BEN')
     A            #1BENL        15S 0H      TEXT('BEN Amount - Min')
     A            #1BENH        15S 0H      TEXT('BEN Amount - Max')
     A            #1SHAR         1   H      TEXT('Route for SHA')
     A            #1SHAL        15S 0H      TEXT('SHA Amount - Min')
     A            #1SHAH        15S 0H      TEXT('SHA Amount - Max')
     A            #1OURR         1   H      TEXT('Route for OUR')
     A            #1OURL        15S 0H      TEXT('OUR Amount - Min')
     A            #1OURH        15S 0H      TEXT('OUR Amount - Max')
      *---------------------------------------------------------------------
     A            #1DES2        35   H      TEXT('Description 2')
     A            #1DES3        35   H      TEXT('Description 3')
      *
      *---------------------------------------------------------------------
      * Active Dates
      *
     A            #1STAT         1   H      TEXT('Status' )
     A            #1STDT         5S 0H      TEXT('Start Date')
     A            #1ENDT         5S 0H      TEXT('End Date')
      *
      *---------------------------------------------------------------------
      * Audit information
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
      *---------------------------------------------------------------------
     A            #1SEL          1   B  9  2
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A            #1BICC         8      9  4TEXT('BIC (Bank, Country, & Loc')
     A                                      DSPATR(HI)
     A            #1BICB         3      9 13TEXT('BIC (Branch Code)')
     A                                      DSPATR(HI)
     A            #1PCCY         3      9 20TEXT('Payment Currency')
     A                                      DSPATR(HI)
     A            #1CBIC         8      9 27TEXT('Pay Through BIC')
     A                                      DSPATR(HI)
     A            #1CBIB         3      9 36TEXT('Pay Through BIC branch')
     A                                      DSPATR(HI)
     A            #1PROT         1      9 41TEXT('Preferential Route')
     A                                      DSPATR(HI)
     A            #1DES1        35      9 45TEXT('Description 1')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
      *
     A N80 81 82                            SFLEND(*MORE)
      *
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2BICC')
     A                                      SETOF(32 'Invalid: #2BICB')
     A                                      SETOF(33 'Invalid: #2PCCY')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'FTP1120F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
      *---------------------------------------------------------------*
      *---------------------------------------------------------------*
     A                                  4  2'Position on.:'
     A                                  4 16'BIC Identifier .:'
     A            #2BICC         8   B  4 35TEXT('BIC Identifier')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5  2'Select on...:'
     A                                  5 16'BIC Branch......:'
     A            #2BICB         3   B  5 35TEXT('BIC Branch')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5 39'Currency.:'
     A            #2PCCY         3   B  5 50TEXT('BIC Branch')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33N70                               DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A  81                              7  2'? BIC Identifier  Ccy    Pay Thru -
     A                                      BIC Pref Description               -
     A                                               '
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8  4'                                  -
     A                                         ?                               -
     A                                             '
     A                                      DSPATR(HI) COLOR(WHT)
     A                                      DSPATR(UL)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
