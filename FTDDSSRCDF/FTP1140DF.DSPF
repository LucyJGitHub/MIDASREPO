      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT SWIFT Authentication Keys Maintenance')
      *****************************************************************
      *                                                               *
      *   Midas - Funds Transfer Module                               *
      *                                                               *
      *   DSPF/FTP1140DF - SWIFT Authentication Keys Maintenance      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      *   Last Amend No. MD031937         Date 13Jan15                *
      *   Prev Amend No. AR856737         Date 13Sep11                *
      *                  ESL038           Date 22Feb2005              *
      * Midas Release 4.01------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   MD031937 - Upgrade STP enhancements to BF Midas 2.1         *
      *   AR856737 - Upgrade STP enhancements to Midas Plus level     *
      *   ESL038 - SWIFT Authentication Keys Maintenance              *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
      *---------------------------------------------------------------------
      *
     A            #1RECI         1   H      TEXT('Record Identifier')
      *
      *---------------------------------------------------------------------
      * SWIFT Authentication Keys
      *
     A            #1AKEY         1   H      TEXT('0=Has key, 5=No key')
      *
      *---------------------------------------------------------------------
      * Active Dates
      *
     A            #1STDT         5S 0H      TEXT('Start Date')
     A            #1ENDT         5S 0H      TEXT('End Date')
      *
      *---------------------------------------------------------------------
      * Audit information
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
      *---------------------------------------------------------------------
     A            #1SEL          1   B  9  2
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A            #1OKEY         8      9  4TEXT('Bank SWIFT ID')
     A                                      DSPATR(HI)
     A            #1CKEY         8      9 13TEXT('Branch id')
     A                                      DSPATR(HI)
     A            #1AKEYD       20      9 22TEXT('Key Status')
     A                                      DSPATR(HI)
     A            #1STAT         1      9 45TEXT('Status' )
     A                                      DSPATR(HI)
     A            #1STDTA        6      9 51TEXT('Start Date')
     A                                      DSPATR(HI)
     A            #1ENDTA        6      9 60TEXT('End Date')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
      *
     A N80 81 82                            SFLEND(*MORE)
      *
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2BICC')
     A                                      SETOF(32 'Invalid: #2BICB')
     A                                      SETOF(33 'Invalid: #2PCCY')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'FTP1140F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
      *---------------------------------------------------------------*
      *---------------------------------------------------------------*
     A                                  4  2'Position on.:'
     A                                  4 16'BIC Identifier .:'
     A            #2OKEY         8   B  4 35TEXT('BIC Identifier')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  4 44'Counterparty:'
     A            #2CKEY1        8   B  4 58TEXT('BIC Identifier')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5  2'Select on...:'
     A                                  5 16'Counterparty....:'
     A            #2CKEY2        8   B  5 35TEXT('BIC Branch')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32N70                               DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A  81                              7  2'? Bank BIC Counter-  Key Status   -
     A                                             Record  Activity Dates      -
     A                                               '
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8  4'          Party                   -
     A                                           Status  Start     End         -
     A                                             '
     A                                      DSPATR(HI) COLOR(WHT)
     A                                      DSPATR(UL)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
