      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT SWIFT Authentication Keys Maint.')
      *****************************************************************
      *                                                               *
      *   Midas - Funds Transfer Module                               *
      *                                                               *
      *   DSPF/FTP1045DF - Midas FT SWIFT Authentication Keys Maint.  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      *   Last Amend No. MD031937         Date 13Jan15                *
      *   Prev Amend No. AR856737         Date 13Sep11                *
      *                  ESL038           Date 22Feb2005              *
      * Midas Release 4.01------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   MD031937 - Upgrade STP enhancements to BF Midas 2.1         *
      *   AR856737 - Upgrade STP enhancements to Midas Plus level     *
      *   ESL038 - SWIFT Authentication Keys Maintenance              *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(31 'Invalid: #1OKEY')
     A                                      SETOF(32 'Invalid: #1CKEY')
     A                                      SETOF(99 'Global error flag')
      *----------------------------------------------------------------
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'FTP1145F1'
     A N25                                  OVRATR
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code  . . . . :'
     A            #PACTC         1A  O  4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Bank SWIFT Id. . . . :'
     A            #1OKEY         8A  B  6 27TEXT('Bank SWIFT ID')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  8  2'Counterparty . . . . :'
     A            #1CKEY         8A  B  8 27TEXT('Counterparty')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #RCDDTL1
      *
     A                                      TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Previous')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1AKEY')
     A                                      SETOF(40 'Invalid: #1STAT')
     A                                      SETOF(41 'Invalid: #1STDT')
     A                                      SETOF(42 'Invalid: #1ENDT')
      *-------------------------------------------------------------------------
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'FTP1145F2'
     A N25                                  OVRATR
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *---------------------------------------------------------------------
     A            #1RECI         1A  H      TEXT('Record Identifier')
     A            #1AJOB        10A  H      TEXT('Job name')
     A            #1AACT         1A  H      TEXT('Action Type')
     A            #1RDNB         5S 0H      TEXT('Run day number')
     A            #1STDT         5S 0H      TEXT('Start Date')
     A            #1ENDT         5S 0H      TEXT('End Date')
      *---------------------------------------------------------------------
     A                                  4  2'Action Code  . . . . :'
     A            #PACTC         1A  O  4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Bank SWIFT Id. . . . :'
     A            #1OKEY         8A  O  6 27TEXT('Bank SWIFT ID')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Counterparty . . . . :'
     A            #1CKEY         8A  B  8 27TEXT('Counterparty')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)                                       MD31937
     A                                      DSPATR(HI)
     A                                 11  2'Authentication Status:'
     A            #1AKEY         1A  B 11 26TEXT('0=Has key, 5=No key')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N75N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1AKEYD       20     11 28TEXT('Key Status')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *---------------------------------------------------------------------
      * Active Dates
      *
     A                                 14  2'Status. . . . . . . . .'
     A            #1STAT         1   B 14 26TEXT('Status' )
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N75N40                               DSPATR(UL)
     A N40                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 14 28'Start date'
     A            #1STDTA        6   B 14 39TEXT('Start Date')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N75N41                               DSPATR(UL)
     A N41                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 14 47'End date'
     A            #1ENDTA        6   B 14 56TEXT('End Date')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N75N42                               DSPATR(UL)
     A N42                                  DSPATR(HI)
     A N25                                  OVRATR
      *
      *=========================================================================
      * Audit Information
     A                                 19  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL)
     A N25                                  OVRATR
     A                                 20  2'User name :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10A  O 20 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 25'Action Date :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7A  O 20 39TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 47'Action Time :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6Y 0O 20 61TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
