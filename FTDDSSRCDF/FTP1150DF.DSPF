      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT STP Payment Customer Specific Routes')
      *****************************************************************
      *                                                               *
      *   Midas - Funds Transfer Module                               *
      *                                                               *
      *   DSPF/FTP1150DF - STP Payment Customer Specific Routes       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      *   Last Amend No. MD031937         Date 13Jan15                *
      *   Prev Amend No. AR856737         Date 13Sep11                *
      *                  EEE065           Date 01Feb2005              *
      * Midas Release 4.01------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   MD031937 - Upgrade STP enhancements to BF Midas 2.1         *
      *   AR856737 - Upgrade STP enhancements to Midas Plus level     *
      *   EEE065 - FT Payment Routing                                 *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
      *---------------------------------------------------------------------
      *
     A            #1RECI         1   H      TEXT('Record Identifier')
      *
      *---------------------------------------------------------------------
      *
      *---------------------------------------------------------------------
      * Correspondent Payment Pay Route
     A            #1CBIC         8   H      TEXT('Pay Through BIC')
     A            #1CBIB         3   H      TEXT('Pay Through BIC branch')
     A            #1CDST        18   H      TEXT('Destination Correspondent')
     A            #1RCAU         1   H      TEXT('Receive a/c Used')
     A            #1RCAC        35   H      TEXT('Receive a/c Quoted')
      *
      *---------------------------------------------------------------------
     A            #1C53C         8   H      TEXT('Senders Correspondent BIC')
     A            #1C53B         3   H      TEXT('Senders Correspondent BIC Br')
     A            #1C53A        10   H      TEXT('Senders Correspondent A/c')
     A            #1C53R         1   H      TEXT('Cover Required')
     A            #1C54C         8   H      TEXT('Receivers Correspondent BIC')
     A            #1C54B         3   H      TEXT('Receivers Corr BIC Br')
     A            #1C56C         8   H      TEXT('Intermediary BIC')
     A            #1C56B         3   H      TEXT('Intermediary BIC Br')
     A            #1SCHR        13S 8H      TEXT('Standard - Charge %')
     A            #1SCHL        15S 0H      TEXT('Standard - Min Fee')
     A            #1SCHH        15S 0H      TEXT('Standard - Max Fee')
     A            #1SCHC         5   H      TEXT('Standard - Charge code')
     A            #1DES1        35   H      TEXT('Description 1')
     A            #1DES2        35   H      TEXT('Description 2')
     A            #1DES3        35   H      TEXT('Description 3')
      *---------------------------------------------------------------------
     A            #1BENR         1   H      TEXT('Route for BEN')
     A            #1BENL        15S 0H      TEXT('BEN Amount - Min')
     A            #1BENH        15S 0H      TEXT('BEN Amount - Max')
     A            #1SHAR         1   H      TEXT('Route for SHA')
     A            #1SHAL        15S 0H      TEXT('SHA Amount - Min')
     A            #1SHAH        15S 0H      TEXT('SHA Amount - Max')
     A            #1OURR         1   H      TEXT('Route for OUR')
     A            #1OURL        15S 0H      TEXT('OUR Amount - Min')
     A            #1OURH        15S 0H      TEXT('OUR Amount - Max')
      *
      *---------------------------------------------------------------------
      * Cover Replacement Information
     A            #1R910         1   H      TEXT('Replace 202C with 910')
     A            #1STMT         2   H      TEXT('Settlement Method')
     A            #1MROF         1   H      TEXT('Manual Route On fail')
     A            #1C57C         8   H      TEXT('A/c with Inst BIC')
     A            #1C57B         3   H      TEXT('A/c with Inst BIC')
     A            #1C701        35   H      TEXT('Details of Payment 1')
     A            #1C702        35   H      TEXT('Details of Payment 2')
     A            #1C703        35   H      TEXT('Details of Payment 3')
     A            #1C704        35   H      TEXT('Details of Payment 4')
     A            #1OCCY         3   H      TEXT('Override Currency')
      *
     A            #1C721        35   H      TEXT('Sender to Receiver 1')
     A            #1C722        35   H      TEXT('Sender to Receiver 2')
     A            #1C723        35   H      TEXT('Sender to Receiver 3')
     A            #1C724        35   H      TEXT('Sender to Receiver 4')
     A            #1C725        35   H      TEXT('Sender to Receiver 5')
     A            #1C726        35   H      TEXT('Sender to Receiver 6')
      *
      *---------------------------------------------------------------------
      * Active Dates
      *
     A            #1STAT         1   H      TEXT('Status' )
     A            #1STDT         5S 0H      TEXT('Start Date')
     A            #1ENDT         5S 0H      TEXT('End Date')
      *
      *---------------------------------------------------------------------
      * Audit information
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
      *---------------------------------------------------------------------
     A            #1SEL          1   B  9  2
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A            #1IDKS        12      9  4TEXT('IDKS cust id for customer')
     A                                      DSPATR(HI)
     A            #1IBAN        34      9 17TEXT('IBAN Account number')
     A                                      DSPATR(HI)
     A            #1PCCY         3      9 52TEXT('Payment Currency')
     A                                      DSPATR(HI)
     A            #1REF1        11      9 56TEXT('Route Reference 1')
     A                                      DSPATR(HI)
     A            #1REF2        12      9 68TEXT('Payment Reference 2')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
      *
     A N80 81 82                            SFLEND(*MORE)
      *
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2IDKS')
     A                                      SETOF(32 'Invalid: #2IBAN')
     A                                      SETOF(33 'Invalid: #2PCCY')
     A                                      SETOF(34 'Invalid: #2REF1')
     A                                      SETOF(35 'Invalid: #2REF2')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'FTP1150F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
      *---------------------------------------------------------------*
      *---------------------------------------------------------------*
     A                                  4  2'Position on.:'
     A                                  4 16'Customer id/Iban'
     A            #2IDKS        12   B  4 33TEXT('IDKS cust id for customer')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2IBAN        34   B  4 46TEXT('IBAN Account number')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5  2'Select on...:'
     A                                  5 16'Currency........:'
     A            #2PCCY         3   B  5 35TEXT('Currency')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  6 43'Route Ref.:'
     A            #2REF1        11   B  6 56TEXT('Route Reference 1')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34N70                               DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2REF2        12   B  6 68TEXT('Payment Reference 2')
     A  70                                  DSPATR(PR)
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35N70                               DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A  81                              8  2'? Customer id  IBAN Reference     -
     A                                                      Ccy Route Reference-
     A                                      -->      '
     A                                      DSPATR(HI) COLOR(WHT)
     A**81                              8  4'                                  -
     A**                                                                       -
     A**                                           '
     A**                                    DSPATR(HI) COLOR(WHT)
     A                                      DSPATR(UL)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
