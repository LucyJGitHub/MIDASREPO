      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Local Clearing Systems Maintenance')
      *****************************************************************
      *                                                               *
      *   Midas - Funds Transfer Module                               *
      *                                                               *
      *   DSPF/FTP1165DF - Midas FT Local Clearing Systems Maintenance*
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *   Last Amend No. MD031937         Date 13Jan15                *
      *   Prev Amend No. AR856737         Date 13Sep11                *
      *                  EEE065           Date 04Apr2005              *
      * Midas Release 4.01------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   MD031937 - Upgrade STP enhancements to BF Midas 2.1         *
      *    AR856737 - Upgrade STP enhancements to Midas Plus level    *
      *    EEE065 - FT Payment Routing                                *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(31 'Invalid: #1CLID')
     A                                      SETOF(32 'Invalid: #1CYCD')
     A                                      SETOF(99 'Global error flag')
      *----------------------------------------------------------------
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'FTP1165F1'
     A N25                                  OVRATR
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code..........:'
     A            #PACTC         1A  O  4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Clearing System Id...:'
     A            #1CLID        10A  B  5 27TEXT('Clearing System Id.')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  6  2'Clearing Currency....:'
     A            #1CYCD         3A  B  6 27TEXT('Clearing Currency')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #RCDDTL1
      *
     A                                      TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Previous')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1DESC')
     A                                      SETOF(32 'Invalid: #1SETA')
     A                                      SETOF(33 'Invalid: #1LLIM')
     A                                      SETOF(34 'Invalid: #1ULIM')
     A                                      SETOF(35 'Invalid: #1INCT')
     A                                      SETOF(36 'Invalid: #1AUCT')
     A                                      SETOF(37 'Invalid: #1TNOT')
      *-------------------------------------------------------------------------
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'FTP1165F2'
     A N25                                  OVRATR
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *---------------------------------------------------------------------
     A                                  4  2'Action Code..........:'
     A            #PACTC         1A  O  4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Claering System Id...:'
     A            #1CLID        10A     5 27TEXT('Clearing System Id.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Claering Currency....:'
     A            #1CYCD         3A     6 27TEXT('Clearing Currency')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Description..........:'
     A            #1DESC        35A  B  8 27TEXT('Description')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N75N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  9  2'Settlement Account...:'
     A            #1SETA        10A  B  9 27TEXT('Settlement Account')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N75N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 10  2'Lower Limit Amount...:'
     A            #1LLIM        14A  B 10 27TEXT('Lower Limit Amount')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N75N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 11  2'Upper Limit Amount...:'
     A            #1ULIM        14A  B 11 27TEXT('Upper Limit Amount')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N75N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 12  2'Input Cut-Off Time...:'
     A            #1INCT         4A  B 12 27TEXT('Input Cut-Off Time')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N75N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 13  2'Auth. Cut-Off Time...:'
     A            #1AUCT         4A  B 13 27TEXT('Auth. Cut-Off Time')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N75N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 14  2'Notice Days..........:'
     A            #1TNOT         6A  B 14 27TEXT('Notice Days')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N75N37                               DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
      *
      *=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
