      *****************************************************************
/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  FTP780DF - Insufficient Funds Check window                   *
      *                                                               *
      *  (C) Copyright Misys International Banking Systems Ltd 2003   *
      *                                                               *
      *   Last Amend No. MD31937          Date 13Jan15                *
      *  Last Amend No. AR856737         Date 13Sep11                 *
      *  Last Amend No. PNL012             Date 16Sep05               *
      *  Prev Amend No. ESL038             Date 04Oct2004             *
      *                 ERN054 *Create     Date 15Jul2003             *
      *                                                               *
      *****************************************************************
      *                                                               *
      *    MD31937  - Upgrade STP enhancements to BF Midas 2.1        *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  PNL012 - Account Payments Authorisation Processing           *
      *  ESL038 - ING STP Development                                 *
      *  ERN054 - Agent Message changes information                   *
      *                                                               *
      *****************************************************************
     A                                      PRINT
     A                                      INDARA
     A**********                            CA03(03 'Exit')
     A                                      CF05(05 'Recalculate')
     A**********                            CF12(12 'Previous')
     A  78                                  CF10(10 'Override')
     A          R FTP780W1                  TEXT('(C) Misys IBS +
     A                                      Ltd. 2003')
     A                                      WINDOW(&SWROW &SWCOL 14 50)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA                                            PNL012
     A            ##PGM         10A  H
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
      *----------------------------------------------------------------
      *
     A                                  2  1'Balance Check Information'
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  3  1'----- Account ------'
     A                                      OVRATR
     A            #1ACCT        22   O  4  1TEXT('Account')
     A                                      DSPATR(HI)
     A                                      OVRATR
     A            #1ACNM        20   O  4 25TEXT('Customer Name')
     A                                      DSPATR(HI)
     A                                      OVRATR
      *
     A                                  5  1'Current Balance --->'
     A                                      OVRATR
     A            #1CLBA        22   O  5 25TEXT('Current Balance')
     A                                      DSPATR(HI)
     A                                      OVRATR
     A            #1CLBS         2   O  5 48TEXT('Current Balance Sign')
     A                                      DSPATR(HI)
     A                                      OVRATR
      *
     A                                  6  1'Unauthorised payments'
     A                                      OVRATR
     A            #1UNPA        22   O  6 25TEXT('Unauthorised Payment Amounts')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRATR
     A            #1UNPS         2   O  6 48TEXT('Unauthorised Payment Sign')
     A                                      DSPATR(HI)
     A                                      OVRATR
      *
     A                                  7  1'Clean Balance ----->'
     A                                      OVRATR
     A            #1CLNA        22   O  7 25TEXT('Clean Balance')
     A                                      DSPATR(HI)
     A                                      OVRATR
     A            #1CLNS         2   O  7 48TEXT('Clean Balance sign')
     A                                      DSPATR(HI)
     A                                      OVRATR
      *
     A                                  8  1'Authorising amount   '
     A                                      OVRATR
     A            #1AUPA        22   O  8 25TEXT('Authorised Payment Amount')
     A                                      DSPATR(HI)
     A                                      OVRATR
     A            #1AUPS         2   O  8 48TEXT('Authorised Payment Sign')
     A                                      DSPATR(HI)
     A                                      OVRATR
      *
     A                                  9  1'Overdraft line      '
     A                                      OVRATR
     A            #1OVDA        22   O  9 25TEXT('Overdraft line')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRATR
     A            #1OVDS         2   O  9 48TEXT('Overdraft line sign')
     A                                      DSPATR(HI)
     A                                      OVRATR
      *
     A                                 10  1'Available Balance ->'
     A                                      OVRATR
     A            #1AVBA        22   O 10 25TEXT('Availble balance')
     A                                      DSPATR(HI)
     A                                      OVRATR
     A            #1AVBS         2   O 10 48TEXT('Availble balance sign')
     A                                      DSPATR(HI)
     A                                      OVRATR
      *
     A  78                             12  1'F5=Recalculate   F10=Overr-
     A                                      ide Balance Check       '
     A N78                             12  1'ENTER to continue    F5=Re-
     A                                      calculate               '
      *
     A*=========================================================================
      *
     A          R FTP780W2                  TEXT('(C) Misys IBS +
     A                                      Ltd. 2003')
     A                                      WINDOW(&SWROW &SWCOL 14 50)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA                                            PNL012
     A            ##PGM         10A  H
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
      *----------------------------------------------------------------
      *
     A                                  2  1'FX Rate verification'
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  3  1'----- Account ------'
     A                                      OVRATR
     A            #1ACCT        22   O  4  1TEXT('Account')
     A                                      DSPATR(HI)
     A                                      OVRATR
     A            #1ACNM        20   O  4 25TEXT('Customer Name')
     A                                      DSPATR(HI)
     A                                      OVRATR
      *
     A                                  5  1'Settlement Amount-->'
     A                                      OVRATR
     A            #1SMCY         3   O  5 25TEXT('Settle Currency')
     A                                      DSPATR(HI)
     A                                      OVRATR
     A            #1SMAM        22   O  5 29TEXT('Settlement Amount')
     A                                      DSPATR(HI)
     A                                      OVRATR
      *
     A                                  6  1'Rate'
     A                                      OVRATR
     A            #1RATE        22   O  6 29TEXT('Exchange Rate')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRATR
      *
     A                                  7  1'Pay Amount -------->'
     A                                      OVRATR
     A            #1PCCY         3   O  7 25TEXT('Clean Balance')
     A                                      DSPATR(HI)
     A                                      OVRATR
     A            #1PYAM        22   O  7 29TEXT('Payment Amount')
     A                                      DSPATR(HI)
     A                                      OVRATR
      *
     A                                  9  1'Expected Amount     '
     A                                      OVRATR
     A            #1EXCY         3   O  9 25TEXT('Expected Currency')
     A                                      DSPATR(HI)
     A                                      OVRATR
     A            #1EXAM        22   O  9 29TEXT('Expected Amount')
     A                                      DSPATR(HI)
     A                                      OVRATR
      *
     A                                 10  1'Expected Rate ----->'
     A                                      OVRATR
     A            #1EXRT        22   O 10 29TEXT('Expected rate')
     A                                      DSPATR(HI)
     A                                      OVRATR
      *
     A  78                             12  1'F5=Recalculate   F10=Overr-
     A                                      ide Rate Check          '
     A N78                             12  1'ENTER to continue    F5=Re-
     A                                      calculate               '
      *
      *
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(13)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A                                      WINDOW(FTP780W1)
     A            ##PGM                     SFLPGMQ
