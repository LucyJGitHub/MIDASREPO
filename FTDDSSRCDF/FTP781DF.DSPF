      **************************************************************
/*STD *  DSPFBASE                                                  *
      **************************************************************
      *                                                            *
      *  Midas - Funds Transfer Module                             *
      *                                                            *
      *  DSPF/FTP781DF - Midas FT Payment Routes List - Selection  *
      *                  Criteria                                  *
      *                                                            *
      *  (c) Misys International Banking Systems Ltd. 2004         *
      *                                                            *
      *   Last Amend No. MD31937          Date 13Jan15                *
      *    Last Amend No. AR856737         Date 13Sep11            *
      *    Last Amend No. EEE065  *CREATE    Date 17Jun2004        *
      *    Prev Amend No. xxxxxx             Date ddmmmyyyy        *
      *                                                            *
      **************************************************************
      *                                                            *
      *   MD31937  - Upgrade STP enhancements to BF Midas 2.1         *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level   *
      *  EEE065 - FT Payment Routing                               *
      *                                                            *
      **************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* COMMAND KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #DATE1')
     A                                      SETOF(32 'Invalid: #DATE2')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'FTP781DF'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N41                              2 31'Payment Routes Report'
     A                                      DSPATR(HI)
     A  41                              2 26'Overriden Payment Routes Report'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  6 27'From date :'
     A            #DATE1         6   B  6 42TEXT('From date')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A                                  7 28'To date :'
     A            #DATE2         6   B  7 42TEXT('To date')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit      F5=Refresh'
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
