      **************************************************************
/*STD *  DSPFBASE                                                  *
/*EXI *  TEXT('Midas FT Edit STP Queue (1scrn)')
      **************************************************************
      *                                                            *
      *  Midas - Funds Transfer Module                             *
      *                                                            *
      *   DSPF/FTP850DF - Edit STP Queue            Edit rd(1scrn) *
      *                                                            *
      *  (c) Misys International Banking Systems Ltd. 2003         *
      *                                                            *
      *  Last Amend No. MD031937         Date 13Jan15              *
      *  Prev Amend No. AR856737         Date 13Sep11              *
      *                 ESL038             Date 01Oct2004          *
      * Midas Release 4 --------------- Base ----------------------*
      *                                                            *
      **************************************************************
      *                                                            *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1       *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level   *
      *  ESL038  -  ING STP Development                            *
      *                                                            *
      *================================================================
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(31 'Invalid: #1STPQ')
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'FTP890F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code . . . :'
     A            #PACTC         1      4 24TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'STP Queue name. . .'
     A            #1STPQ        20   B  6 24TEXT('STP Queue name')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Previous')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(32 'Invalid: #1DESC')
     A                                      SETOF(33 'Invalid: #1RSTO')
     A                                      SETOF(34 'Invalid: #1STPO')
     A                                      SETOF(35 'Invalid: #1RSTD')
     A                                      SETOF(36 'Invalid: #1STPO')
     A                                      SETOF(37 'Invalid: #1RSTP')
     A                                      SETOF(38 'Invalid: #1STPT')
     A                                      SETOF(39 'Invalid: #1STPA')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'FTP890F2'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  2'Action Code  . . . . :'
     A            #PACTC         1      4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'STP Queue name. . .'
     A            #1STPQ        20      6 24TEXT('STP Queue name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Description . . . .'
     A            #1DESC        30   B  8 24TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N79N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 10  2'Reset Queue overnight'
     A            #1RSTO         1   B 10 24TEXT('Reset Queue overnight')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N79N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 10 26'Move to queue. .'
     A            #1STPO        20   B 10 43TEXT('Overnight STP Queue Name')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N79N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 11  2'Reset Queue on demand'
     A            #1RSTD         1   B 11 24TEXT('Reset Queue on demand')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N79N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 11 26'Move to queue. .'
     A            #1STPD        20   B 11 43TEXT('Demand STP Queue Name')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N79N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 12  2'Allow STP on reset. .'
     A            #1RSTP         1   B 12 24TEXT('Allow STP on reset')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N79N37                               DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 14  2'When transaction created-
     A                                      , move to queue.'
     A            #1STPT        20   B 14 43TEXT('Transaction STP Queue Name')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A N79N38                               DSPATR(UL)
     A N38                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 15  2'        on authorisation-
     A                                      , move to queue.'
     A            #1STPA        20   B 15 43TEXT('Transaction STP Queue Name')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A N79N39                               DSPATR(UL)
     A N39                                  DSPATR(HI)
     A N25                                  OVRATR
      *---------------------------------------------------------------------------------------------
     A                                 19  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL)
     A N25                                  OVRATR
     A                                 20  2'User name :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10     20 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 25'Action Date :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7     20 39TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 47'Action Time :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6  0  20 61TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
