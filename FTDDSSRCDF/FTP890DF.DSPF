      **************************************************************
/*STD *  DSPFBASE                                                  *
/*EXI *  TEXT('Midas FT Edit Ext   Customer A/c')
      **************************************************************
      *                                                            *
      *  Midas - Funds Transfer Module                             *
      *                                                            *
      *   DSPF/FTP890DF - Edit Ext   Customer A/c   Edit rd(1scrn) *
      *                                                            *
      *  (c) Misys International Banking Systems Ltd. 2003         *
      *                                                            *
      *  Last Amend No. MD031937         Date 13Jan15              *
      *  Prev Amend No. AR856737         Date 13Sep11              *
      *                 E30004           Date 25Feb2004            *
      *                 EEE058           Date 10Jun2003            *
      *                 EEE058  *CREATE  Date 25Feb2003            *
      * Midas Release 4 --------------- Base ----------------------*
      *                                                            *
      **************************************************************
      *                                                            *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1       *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level   *
      *  E30004  -  Include status in display                      *
      *  EEE058  -  Add default account indicator                  *
      *  EEE058  -  Incoming STP                                   *
      *                                                            *
      *================================================================
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(31 'Invalid: #1IBAN')
     A                                      SETOF(32 'Invalid: #1RNBR')
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'FTP890F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code . . . :'
     A            #PACTC         1      4 24TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'IBAN  . . . . . . .'
     A            #1IBAN        34   B  6 24TEXT('IBAN Account number')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  7  2'Retail Nbr  . . . .'
     A            #1RNBR        34   B  7 24TEXT('External System A/c Number')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Previous')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(33 'Invalid: #1BEN*')
     A                                      SETOF(34 'Invalid: #1RIND')
     A                                      SETOF(35 'Invalid: #1BTYP')
     A                                      SETOF(36 'Invalid: #1BSEG')
     A                                      SETOF(37 'Invalid: #1BBRC')
     A                                      SETOF(38 'Invalid: #1BEXT')
     A                                      SETOF(39 'Invalid: #1BCCY')
     A                                      SETOF(40 'Invalid: #1RGON')
     A                                      SETOF(41 'Invalid: #1PESL')
     A                                      SETOF(42 'Invalid: #1IDKS')
     A                                      SETOF(43 'Invalid: #1GRPO')
     A                                      SETOF(44 'Invalid: #1COMI')
     A                                      SETOF(45 'Invalid: #1COMA')
     A                                      SETOF(46 'Invalid: #1FXRI')
     A                                      SETOF(47 'Invalid: #1FXLA')
     A                                      SETOF(48 'Invalid: #1PENR')
     A* Start EEE058
     A                                      SETOF(49 'Invalid: #1DACC')
     A* End EEE058
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'FTP890F2'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  2'Action Code  . . . . :'
     A            #PACTC         1      4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'IBAN  . . . . . . .'
     A            #1IBAN        34      6 24TEXT('IBAN Account number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Retail Nbr  . . . .'
     A            #1RNBR        34      7 24TEXT('External System A/c Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Beneficiary . . . .'
     A            #1BEN1        35   B  8 24TEXT('Beneficiary Name 1')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N79N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1BEN2        35   B  9 24TEXT('Beneficiary Name 1')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N79N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1BEN3        35   B 10 24TEXT('Beneficiary Name 1')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N79N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 11  2'Resident (R/N). . .'
     A            #1RIND         1   B 11 24TEXT('Resident/Non/resident')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N79N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 12  2'Beneficiary (I/G) .'
     A            #1BTYP         1   B 12 24TEXT('Beneficiary Type')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N79N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 13  2'Beneficiary Segment'
     A            #1BSEG         3   B 13 24TEXT('Beneficiary Segment')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N79N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 13 28'Beneficiary Branch'
     A            #1BBRC         8   B 13 47TEXT('Beneficiary Branch.')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N79N37                               DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 13 56'RABAN Branch'
     A            #1BEXT         3   B 13 69TEXT('RABAN Branch External')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A N79N38                               DSPATR(UL)
     A N38                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 14  2'A/c Currency  . . .'
     A            #1BCCY         3   B 14 24TEXT('Beneficiary A/c Currency')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A N79N39                               DSPATR(UL)
     A N39                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 15  2'REGON . . . . . . .'
     A            #1RGON        14   B 15 24TEXT('REGON for beneficiary')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  40                                  DSPATR(RI PC)
     A N79N40                               DSPATR(UL)
     A N40                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 16  2'PESEL . . . . . . .'
     A            #1PESL        11   B 16 24TEXT('PESEL for beneficiary')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  41                                  DSPATR(RI PC)
     A N79N41                               DSPATR(UL)
     A N41                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 17  2'IDKS  . . . . . . .'
     A            #1IDKS        12   B 17 24TEXT('IDKS cust id for beneficiary')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  42                                  DSPATR(RI PC)
     A N79N42                               DSPATR(UL)
     A N42                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 18  2'GRPO  . . . . . . .'
     A            #1GRPO         4   B 18 24TEXT('GRPO code beneficiary')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  43                                  DSPATR(RI PC)
     A N79N43                               DSPATR(UL)
     A N43                                  DSPATR(HI)
     A N25                                  OVRATR
     A* Start EEE058
     A  60                             12 45'Default Account . .'
     A  60        #1DACC         1   B 12 65TEXT('Pensioner indicator')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  49                                  DSPATR(RI PC)
     A N79N49                               DSPATR(UL)
     A N49                                  DSPATR(HI)
     A N25                                  OVRATR
     A* End EEE058
     A  60                             14 45'Pensioner . . . . .'
     A  60        #1PENR         1   B 14 65TEXT('Pensioner indicator')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  48                                  DSPATR(RI PC)
     A N79N48                               DSPATR(UL)
     A N48                                  DSPATR(HI)
     A N25                                  OVRATR
     A  60                             15 45'Commission ind. . .'
     A  60        #1COMI         1   B 15 65TEXT('Commssion indicator')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  44                                  DSPATR(RI PC)
     A N79N44                               DSPATR(UL)
     A N44                                  DSPATR(HI)
     A N25                                  OVRATR
     A  60                             16 45'           Amount .'
     A  60        #1COMA        16   B 16 65TEXT('Commssion Amount')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  45                                  DSPATR(RI PC)
     A N79N45                               DSPATR(UL)
     A N45                                  DSPATR(HI)
     A N25                                  OVRATR
     A  60                             17 45'FX Rate Indicator .'
     A  60        #1FXRI         1   B 17 65TEXT('FX Rate Indicator')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  46                                  DSPATR(RI PC)
     A N79N46                               DSPATR(UL)
     A N46                                  DSPATR(HI)
     A N25                                  OVRATR
     A  60                             18 45'     Limit Amount .'
     A  60        #1FXLA        16   B 18 65TEXT('FX Rate Limit Amount')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  47                                  DSPATR(RI PC)
     A N79N47                               DSPATR(UL)
     A N47                                  DSPATR(HI)
     A N25                                  OVRATR
     A  60                             19  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL)
     A N25                                  OVRATR
     A  60                             20  2'User name :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A  60        #1AUSR        10     20 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A  60                             20 25'Action Date :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A  60        #1ARDT         7     20 39TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A  60                             20 47'Action Time :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A  60        #1ATIM         6  0  20 61TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
