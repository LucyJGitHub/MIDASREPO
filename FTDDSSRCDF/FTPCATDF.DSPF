      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FT Outgoing Payments Prompt PCAT Information')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  FTPCATDF - Outgoing Payments Prompt PCAT Information         *
      *                                                               *
      *  (C) Copyright Misys International Banking Systems Ltd 2004   *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 13Sep11               *
      *                 ESL038 *Create     Date 25Nov04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  ESL038 - Funds Transfer STP Enhancements                     *
      *                                                               *
      *****************************************************************
     A                                      PRINT
     A                                      INDARA
     A                                      CA03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A                                      CF12(12 'Previous')
     A                                      CF16(16 'PCAT Enq')
      *
     A          R FTPCATD0                  TEXT('(C) Misys IBS +
     A                                      Ltd. 2004')
     A                                      WINDOW(5 11  5 30)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##PGM         10A  H
      *
     A                                  2  1'Enter PCAT Details (Y/N):'
     A                                      OVRATR
     A            SDSPY          1A  B  2 27OVRDTA
     A N55                                  DSPATR(HI)
     A  55                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
      *
     A          R FTPCATD1                  TEXT('(C) Misys IBS +
     A                                      Ltd. 2004')
     A********                              WINDOW(5 11 16 50)
     A                                      WINDOW(5 11 17 50)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A            ##PGM         10A  H
      *----------------------------------------------------------------
      *
     A                                  2  1'PCAT IBAN   '
     A                                      OVRATR
     A            SPCIB         35A  B  2 15OVRDTA
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  3  1'     Account'
     A                                      OVRATR
     A            SPCAT         35A  B  3 15OVRDTA
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A            SASTD         11A     4  1OVRDTA
     A                                      DSPATR(HI)
     A                                      COLOR(RED)
     A            SCUS1         35A     4 15OVRDTA
     A                                      DSPATR(HI)
     A            SCUS2         35A     5 15OVRDTA
     A                                      DSPATR(HI)
     A            SCUS3         35A     6 15OVRDTA
     A                                      DSPATR(HI)
     A            SCUS4         35A     7 15OVRDTA
     A                                      DSPATR(HI)
     A            SCUS5         35A     8 15OVRDTA
     A                                      DSPATR(HI)
     A                                  9  1'Resident (R/N). . .'
     A            SRIND          1A     9 23TEXT('Resident/Non/resident')
     A                                      DSPATR(HI)
     A                                  9 30'Pensioner Account'
     A            SPENR          1A     9 48TEXT('Pensioner Account')
     A                                      DSPATR(HI)
     A                                 10  1'Customer Type'
     A            SBTYP          1A    10 17TEXT('Customer Type')
     A                                      DSPATR(HI)
     A                                 11  1'Segment'
     A            SBSEG          3A    11  9OVRDTA
     A                                      DSPATR(HI)
     A                                 11 18'A/c ccy'
     A            SBCCY          3A    11 26OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 11 30'Foreign Pension'
     A            SPENF          1A  B 11 46OVRDTA
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                 12 30'Transaction Type'
     A N21                                  DSPATR(ND)
     A            STTYP          4A  B 12 47TEXT('Transaction Type')
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI PC)
     A  20
     AO 21                                  DSPATR(PR)
     A N21                                  DSPATR(ND)
     A N20                                  DSPATR(UL)
     A                                 12  1'Commission'
     A            SCOMI          1A    12 12OVRDTA
     A                                      DSPATR(HI)
     A            SCOMN         10A    12 14OVRDTA
     A                                      DSPATR(HI)
     A                                 13  1'FX rate'
     A            SFXRI          1A    13 12OVRDTA
     A                                      DSPATR(HI)
     A            SFXRN         10A    13 14OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 14  1'Trans In Msg Ccy'
     A            SIMCY          1A    14 18OVRDTA
     A                                      DSPATR(HI)
     A                                 14 21'Special Routing'
     A            SSPRT          1A    14 37OVRDTA
     A                                      DSPATR(HI)
      *
     A**********                       15  1'F3=Exit         F12=Cancel-
     A                                 16  1'F3=Exit         F12=Cancel-
     A                                              F16=PCAT Enquiry'
      *
      *
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
