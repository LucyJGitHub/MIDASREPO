      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas FT RFT header invalid')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PF/FTI101HPD - RFT Header Invalid                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Last Amend No. CFT006  *CREATE    Date 06Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFT006 - Funds Transfer/Swift Messages MT101                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R FTI101HD0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            RFTID     R               REFFLD(ACID)
     A                                      TEXT('RFT Id')
     A                                      COLHDG('RFT Id')
     A            STID      R               TEXT('Sender Id')
     A                                      COLHDG('Sender Id')
     A            SNDREF    R               REFFLD(SWCR)
     A                                      TEXT('Senders Reference')
     A                                      COLHDG('Senders Reference')
     A            PYTP      R               TEXT('Transfer Type')
     A                                      COLHDG('Transfer Type')
     A            PYST      R               TEXT('Transfer Sub-Type')
     A                                      COLHDG('Transfer Sub-Type')
     A            BRCA      R               TEXT('Booking Branch')
     A                                      COLHDG('Booking Branch')
     A            ORBR      R
     A            BOKC      R
     A            PRFC      R
     A            CUSTRF    R               REFFLD(CPRF)
     A                                      TEXT('Customer Reference')
     A                                      COLHDG('Customer +
     A                                      Reference')
     A            HINPTY    R               REFFLD(IPYL)
     A                                      TEXT('Instructing Party')
     A                                      COLHDG('Instructing Party')
     A            HOCUS1    R               REFFLD(POBA)
     A                                      TEXT('Ordering Customer 1')
     A                                      COLHDG('Ordering Customer +
     A                                      1')
     A            HOCUS2    R               REFFLD(POBA)
     A                                      TEXT('Ordering Customer 2')
     A                                      COLHDG('Ordering Customer +
     A                                      2')
     A            HOCUS3    R               REFFLD(POBA)
     A                                      TEXT('Ordering Customer 3')
     A                                      COLHDG('Ordering Customer +
     A                                      3')
     A            HOCUS4    R               REFFLD(POBA)
     A                                      TEXT('Ordering Customer 4')
     A                                      COLHDG('Ordering Customer +
     A                                      4')
     A            HOCUS5    R               REFFLD(POBA)
     A                                      TEXT('Ordering Customer 5')
     A                                      COLHDG('Ordering Customer +
     A                                      5')
     A            CUSTTP    R               REFFLD(TYPG)
     A                                      TEXT('Ordering Customer +
     A                                      Type')
     A                                      COLHDG('Ordering ' +
     A                                      'Customer Type')
     A            HACSV1    R               REFFLD(AWIL)
     A                                      TEXT('Account Servicing +
     A                                      Institution 1')
     A                                      COLHDG('Account ' +
     A                                      'Servicing Inst. 1')
     A            HACSV2    R               REFFLD(SWID)
     A                                      TEXT('Account Servicing +
     A                                      Institution 2')
     A                                      COLHDG('Account ' +
     A                                      'Servicing Inst. 2')
     A            SINST     R               REFFLD(SWID)
     A                                      TEXT('Sending Institution')
     A                                      COLHDG('Sending +
     A                                      Institution')
     A            EXDATE    R               REFFLD(CHAP)
     A                                      TEXT('Request Execution +
     A                                      Date')
     A                                      COLHDG('Request ' +
     A                                      'Execution Date')
     A            DBTVDT    R               REFFLD(CHAP)
     A                                      TEXT('Debit Value Date')
     A                                      COLHDG('Debit Value Date')
     A            AUTHTX    R               REFFLD(DAA1)
     A                                      TEXT('Authorisation')
     A                                      COLHDG('Authorisation')
     A            HANDCG    R               REFFLD(VDCR)
     A                                      TEXT('Handling Charge')
     A                                      COLHDG('Handling Charge')
     A            HANDCY    R               REFFLD(XCCY)
     A                                      TEXT('Handling Ccy')
     A                                      COLHDG('Handling Ccy')
     A            HANDAC    R               REFFLD(DAA1)
     A                                      TEXT('Charges to the +
     A                                      Debit of')
     A                                      COLHDG('Charges to the ' +
     A                                      'Debit of')
     A            SWPRIO    R               REFFLD(MCIS)
     A                                      TEXT('Swift Priority')
     A                                      COLHDG('Swift Priority')
     A            BCQSPT    R               REFFLD(FACV)
     A                                      TEXT('Base Ccy Equiv +
     A                                      Spot Rate')
     A                                      COLHDG('Base Ccy Equiv ' +
     A                                      'Spot Rate')
     A            INUSER    R               REFFLD(RGUD)
     A                                      TEXT('Input User')
     A                                      COLHDG('Input User')
     A            INDATE    R               REFFLD(LASD)
     A                                      TEXT('Input Date')
     A                                      COLHDG('Input Date')
     A            INTIME    R               REFFLD(LATM)
     A                                      TEXT('Input Time')
     A                                      COLHDG('Input Time')
     A            UPDUSR    R               REFFLD(RGUD)
     A                                      TEXT('Update User')
     A                                      COLHDG('Update User')
     A            UPDDTE    R               REFFLD(LASD)
     A                                      TEXT('Update Date')
     A                                      COLHDG('Update Date')
     A            UPDTIM    R               REFFLD(LATM)
     A                                      TEXT('Update Time')
     A                                      COLHDG('Update Time')
     A            AUP1      R               REFFLD(RGUD)
     A                                      TEXT('Authorising User 1')
     A                                      COLHDG('Authorising User +
     A                                      1')
     A            AUP2      R               REFFLD(RGUD)
     A                                      TEXT('Authorising User 2')
     A                                      COLHDG('Authorising User +
     A                                      2')
     A            AUIN      R               REFFLD(CRLF)
     A                                      TEXT('Authorised +
     A                                      Indicator')
     A                                      COLHDG('Authorised +
     A                                      Indicator')
     A            BAUTH          4          TEXT('BATCH Authorised +
     A                                      Number')
     A                                      COLHDG('BATCH Authorised' +
     A                                      'Number')
     A            ADVCRT    R               REFFLD(CRLF)
     A                                      TEXT('Advice Created')
     A                                      COLHDG('Advice Created')
     A            TOTDBT    R               REFFLD(M5DLAM)
     A                                      TEXT('Total Debit +
     A                                      Transaction')
     A                                      COLHDG('Total Debit ' +
     A                                      'Transaction')
     A            FRNT      R               REFFLD(FOTRNID)
     A            AFRT      R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                      Office Transaction ID)')
     A                                      COLHDG('Associated ' +
     A                                      'Front Office ' +
     A                                      'Transaction ID')
     A            REPA      R               REFFLD(RPRLOC)
     A                                      TEXT('Transaction Repair +
     A                                      Location')
     A                                      COLHDG('Transaction ' +
     A                                      'Repair Location')
     A            TMST      R               REFFLD(TIMESTMP)
      *
