      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas FT CCT header invalid')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PF/FTI102HPD - CCT Header Invalid                            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CFT008  *CREATE    Date 06Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFT008 - Funds Transfer/Swift Messages MT102                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R FTI102HD0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            CCTID     R               REFFLD(ACID)
     A                                      TEXT('CCT Id')
     A                                      COLHDG('CCT Id')
     A            FILREF    R               REFFLD(ACOR)
     A                                      TEXT('File Reference')
     A                                      COLHDG('File Reference')
     A            CCTTYP    R               REFFLD(PYTP)
     A                                      TEXT('Transfer Type')
     A                                      COLHDG('Transfer Type')
     A            CCTTST    R               REFFLD(PYST)
     A                                      TEXT('Transfer Sub-Type')
     A                                      COLHDG('Transfer Sub-Type')
     A            BRCA      R               TEXT('Booking Branch')
     A                                      COLHDG('Booking Branch')
     A            ORBR      R
     A            BOKC      R
     A            PRFC      R
     A            CCTBOC    R               REFFLD(ACOR)
     A                                      TEXT('Bank Operation Code')
     A                                      COLHDG('Bank Operation +
     A                                      Code')
     A            SINST     R               REFFLD(AWIL)
     A                                      TEXT('Sending Institution')
     A                                      COLHDG('Sending +
     A                                      Institution')
     A            CCTDST    R               REFFLD(AWIL)
     A                                      TEXT('Destination')
     A                                      COLHDG('Destination')
     A            TOCUS1    R               REFFLD(POBA)
     A                                      TEXT('Ordering Customer 1')
     A                                      COLHDG('Ordering Customer +
     A                                      1')
     A            TOCUS2    R               REFFLD(POBA)
     A                                      TEXT('Ordering Customer 2')
     A                                      COLHDG('Ordering Customer +
     A                                      2')
     A            TOCUS3    R               REFFLD(POBA)
     A                                      TEXT('Ordering Customer 3')
     A                                      COLHDG('Ordering Customer +
     A                                      3')
     A            TOCUS4    R               REFFLD(POBA)
     A                                      TEXT('Ordering Customer 4')
     A                                      COLHDG('Ordering Customer +
     A                                      4')
     A            TOCUS5    R               REFFLD(POBA)
     A                                      TEXT('Ordering Customer 5')
     A                                      COLHDG('Ordering Customer +
     A                                      5')
     A            OINST3    R               REFFLD(POBA)
     A                                      TEXT('Ordering +
     A                                      Institution')
     A                                      COLHDG('Ordering +
     A                                      Institution')
     A            OINST1    R               REFFLD(SWID)
     A                                      TEXT('Ordering +
     A                                      Institution')
     A                                      COLHDG('Ordering +
     A                                      Institution')
     A            TRNSTP    R               REFFLD(SWRF)
     A                                      TEXT('Transaction Type +
     A                                      Code')
     A                                      COLHDG('Transaction ' +
     A                                      'Type Code')
     A            REGRP1    R               REFFLD(CABN)
     A                                      TEXT('Regulatory +
     A                                      Reporting')
     A                                      COLHDG('Regulatory +
     A                                      Reporting')
     A            REGRP2    R               REFFLD(CABN)
     A                                      TEXT('Regulatory +
     A                                      Reporting')
     A                                      COLHDG('Regulatory +
     A                                      Reporting')
     A            REGRP3    R               REFFLD(CABN)
     A                                      TEXT('Regulatory +
     A                                      Reporting')
     A                                      COLHDG('Regulatory +
     A                                      Reporting')
     A            CCTCHG    R               REFFLD(CHINL)
     A                                      TEXT('Charges')
     A                                      COLHDG('Charges')
     A            CCTRTE    R               REFFLD(EXRATE)
     A                                      TEXT('Exchange Rate')
     A                                      COLHDG('Exchange Rate')
     A            CCTAMT    R               REFFLD(M5DLAM)
     A                                      TEXT('Transfer Amount')
     A                                      COLHDG('Transfer Amount')
     A            CCTCCY    R               REFFLD(BCCY)
     A                                      TEXT('Transfer Currency')
     A                                      COLHDG('Transfer Currency')
     A            TTVDTE    R               REFFLD(CHAP)
     A                                      TEXT('Total Transfer +
     A                                      Value Date')
     A                                      COLHDG('Total Transfer ' +
     A                                      'Value Date')
     A            DBTVDT    R               REFFLD(CHAP)
     A                                      TEXT('Debit Value Date')
     A                                      COLHDG('Debit Value Date')
     A            SUMAMT    R               REFFLD(COLH)
     A                                      TEXT('Sum of Amounts')
     A                                      COLHDG('Sum of Amounts')
     A            SUMSCG    R               REFFLD(EXRATE)
     A                                      TEXT('Sum of Senders +
     A                                      Charges')
     A                                      COLHDG('Sum of Senders ' +
     A                                      'Charges')
     A            SUMRCH    R               REFFLD(EXRATE)
     A                                      TEXT('Sum of Receivers +
     A                                      Charges')
     A                                      COLHDG('Sum of ' +
     A                                      'Receivers Charges')
     A            SNDCOR    R               REFFLD(RCRA)
     A                                      TEXT('Senders +
     A                                      Correspondent')
     A                                      COLHDG('Senders ' +
     A                                      'Correspondent')
     A            RCVCOR    R               REFFLD(RCRA)
     A                                      TEXT('Receivers +
     A                                      Correspondent')
     A                                      COLHDG('Receivers ' +
     A                                      'Correspondent')
     A            ADNLI1    R               REFFLD(FIN1)
     A                                      TEXT('Additional +
     A                                      Information')
     A                                      COLHDG('Additional ' +
     A                                      'Information')
     A            ADNLI2    R               REFFLD(FIN1)
     A                                      TEXT('Additional +
     A                                      Information')
     A                                      COLHDG('Additional ' +
     A                                      'Information')
     A            ADNLI3    R               REFFLD(FIN1)
     A                                      TEXT('Additional +
     A                                      Information')
     A                                      COLHDG('Additional ' +
     A                                      'Information')
     A            ADNLI4    R               REFFLD(FIN1)
     A                                      TEXT('Additional +
     A                                      Information')
     A                                      COLHDG('Additional ' +
     A                                      'Information')
     A            ADNLI5    R               REFFLD(FIN1)
     A                                      TEXT('Additional +
     A                                      Information')
     A                                      COLHDG('Additional ' +
     A                                      'Information')
     A            ADNLI6    R               REFFLD(FIN1)
     A                                      TEXT('Additional +
     A                                      Information')
     A                                      COLHDG('Additional ' +
     A                                      'Information')
     A            SWPRIO    R               REFFLD(MCIS)
     A                                      TEXT('Swift Priority')
     A                                      COLHDG('Swift Priority')
     A            BCQSPT    R               REFFLD(FACV)
     A                                      TEXT('Base Ccy Equiv +
     A                                      Spot Rate')
     A                                      COLHDG('Base Ccy Equiv ' +
     A                                      'Spot Rate')
     A            INUSER    R               REFFLD(RGUD)
     A                                      TEXT('Input User')
     A                                      COLHDG('Input User')
     A            INDATE    R               REFFLD(LASD)
     A                                      TEXT('Input Date')
     A                                      COLHDG('Input Date')
     A            INTIME    R               REFFLD(LATM)
     A                                      TEXT('Input Time')
     A                                      COLHDG('Input Time')
     A            UPDUSR    R               REFFLD(RGUD)
     A                                      TEXT('Update User')
     A                                      COLHDG('Update User')
     A            UPDDTE    R               REFFLD(LASD)
     A                                      TEXT('Update Date')
     A                                      COLHDG('Update Date')
     A            UPDTIM    R               REFFLD(LATM)
     A                                      TEXT('Update Time')
     A                                      COLHDG('Update Time')
     A            AUP1      R               REFFLD(RGUD)
     A                                      TEXT('Authorising User 1')
     A                                      COLHDG('Authorising User +
     A                                      1')
     A            AUP2      R               REFFLD(RGUD)
     A                                      TEXT('Authorising User 2')
     A                                      COLHDG('Authorising User +
     A                                      2')
     A            AUIN      R               REFFLD(CRLF)
     A                                      TEXT('Authorised +
     A                                      Indicator')
     A                                      COLHDG('Authorised +
     A                                      Indicator')
     A            AKDRIP    R               REFFLD(CRLF)
     A                                      TEXT('Debit Input Date +
     A                                      Account Key')
     A                                      COLHDG('Debit Input ' +
     A                                      'Date Account Key')
     A            AKDRDR    R               REFFLD(CRLF)
     A                                      TEXT('Debit Debit Date +
     A                                      Account Key')
     A                                      COLHDG('Debit Debit ' +
     A                                      'Date Account Key')
     A            ADVCRT    R               REFFLD(CRLF)
     A                                      TEXT('Advice Created')
     A                                      COLHDG('Advice Created')
     A            CCTTDB    R               REFFLD(M5DLAM)
     A                                      TEXT('Total Debit')
     A                                      COLHDG('Total Debit')
     A            FRNT      R               REFFLD(FOTRNID)
     A            AFRT      R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                      Office Transaction ID)')
     A                                      COLHDG('Associated ' +
     A                                      'Front Office ' +
     A                                      'Transaction ID')
     A            REPA      R               REFFLD(RPRLOC)
     A                                      TEXT('Transaction Repair +
     A                                      Location')
     A                                      COLHDG('Transaction ' +
     A                                      'Repair Location')
     A            TMST      R               REFFLD(TIMESTMP)
      *
