      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_RU                                                        *
/*EXI *  TEXT('Midas FT Payment Route')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PF/FTMROTP0 - Midas FT Payment Route                         *
      *                                                               *
      *  (C) Copyright Misys IBS Ltd. 2004                            *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 10NOV2011             *
      *                 PNL014             DATE 07Dec2005             *
      *                 ESL044             Date 02Mar2005             *
      *                 EEE065   *CREATE   Date 09Apr2004             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 -  Upgrade STP enhancement to Midas Plus level.     *
      *           -  MODMAC and MODMAO increased to 24 to accommodate *
      *              10 digits account code                           *
      *  PNL014 - Time Sensitive Routing                              *
      *  ESL044 - Add Details of Payment                              *
      *  EEE065 - FT Payment Routing                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R @MROTP0                   TEXT('(C) Misys IBS  +
     A                                      . 2004')
      *---------------------------------------------------------------------
     A            MOROUT        11          TEXT('Route Reference')
     A                                      COLHDG('Route' +
     A                                             'Reference')
     A            MOMSGR    R               TEXT('Msg Unique Reference')
     A                                      COLHDG('Msg Unique' +
     A                                             'Reference')
     A                                      REFFLD(MSGR)
     A            MOKPRT    R               TEXT('Part Number (Key)')
     A                                      COLHDG('Part Number' +
     A                                             '(Key)')
     A                                      REFFLD(KPRT)
     A            MOOPRF    R               TEXT('Outgoing Payment Ref.')
     A                                      COLHDG('Outgoing' +
     A                                             'Payment Ref.')
     A                                      REFFLD(PREF)
     A            MORECI         1A         TEXT('Record Identifier')
     A                                      COLHDG('Record' +
     A                                             'Identifier')
     A            MOSMCY         3A         TEXT('Settlement Ccy')
     A                                      COLHDG('Settlement' +
     A                                             'Currency')
     A            MOSMAM        15S 0       TEXT('Settlement amount')
     A                                      COLHDG('Settlement' +
     A                                             'Amount')
     A            MOFXSP         5A         TEXT('FX Spread')
     A                                      COLHDG('FX Spread')
     A            MORATE        13S 8       TEXT('FX Rate')
     A                                      COLHDG('FX Spread')
     A            MOPCCY         3A         TEXT('Pay Ccy')
     A                                      COLHDG('Pay' +
     A                                             'Currency')
     A            MOPYAM        15S 0       TEXT('Payment amount')
     A                                      COLHDG('Payment' +
     A                                             'Amount')
     A            MOPTHR        11A         TEXT('Pay through')
     A                                      COLHDG('Pay Through' +
     A                                             'Bic')
     A            MOBENB        11A         TEXT('Beneficiary BIC')
     A                                      COLHDG('Beneficiary' +
     A                                             'Bic')
     A            MOBENC         2A         TEXT('Beneficiary Country')
     A                                      COLHDG('Beneficiary' +
     A                                             'Country')
     A            MOSNCR        11A         TEXT('Sender Corres')
     A                                      COLHDG('Sender''s' +
     A                                             'Correspondent')
     A            MOSNCO        11A         TEXT('Sender Corres Override')
     A                                      COLHDG('Sender''s' +
     A                                             'Correspondent' +
     A                                             'Override')
     A            MORCCR        11A         TEXT('Receiver Corres')
     A                                      COLHDG('Receiver''s' +
     A                                             'Correspondent')
     A            MORCCO        11A         TEXT('Receiver Corres Override')
     A                                      COLHDG('Receiver''s' +
     A                                             'Correspondent' +
     A                                             'Override')
     A            MODSTR        11A         TEXT('Destination')
     A                                      COLHDG('Destinatiion' +
     A                                             'Correspondent')
     A            MODSTO        11A         TEXT('Destination Override')
     A                                      COLHDG('Destinatiion' +
     A                                             'Correspondent' +
     A                                             'Override')
     A            MOINTR        11A         TEXT('Intermediary')
     A                                      COLHDG('Intermediary' +
     A                                             'Correspondent')
     A            MOINTO        11A         TEXT('Intermediary')
     A                                      COLHDG('Intermediary' +
     A                                             'Correspondent' +
     A                                             'Override')
     A            MOACBR        11A         TEXT('A/c with Inst')
     A                                      COLHDG('A/c with Inst' +
     A                                             'Correspondent')
     A            MOACBO        11A         TEXT('A/c with Inst')
     A                                      COLHDG('A/c with Inst' +
     A                                             'Correspondent' +
     A                                             'Override')
     A*****       MODMAC        18A         TEXT('Destination A/c')                         AR856737
     A            MODMAC        24A         TEXT('Destination A/c')                         AR856737
     A                                      COLHDG('Destination A/c')
     A*****       MODMAO        18A         TEXT('Destination A/c')                         AR856737
     A            MODMAO        24A         TEXT('Destination A/c')                         AR856737
     A                                      COLHDG('Destination A/c' +
     A                                             'Override')
     A            MOCVRR         1A         TEXT('Cover required')
     A                                      COLHDG('Cover required')
     A            MOCVRO         1A         TEXT('Cover required')
     A                                      COLHDG('Cover required' +
     A                                             'Override')
     A            MOCTHR        10A         TEXT('Cover Through')
     A                                      COLHDG('Cover Through' +
     A                                             'Correspondent')
     A            MOCTHO        10A         TEXT('Cover Through')
     A                                      COLHDG('Cover Through' +
     A                                             'Correspondent' +
     A                                             'Override')
     A            MODTCH         3A         TEXT('Details of Charge')
     A                                      COLHDG('Details of' +
     A                                             '  Charge' +
     A                                             'Override')
     A            MOBRCA         3A         TEXT('Branch')
     A                                      COLHDG('Branch')
     A            MOSETT         2A         TEXT('Settlement Method')
     A                                      COLHDG('Settlement' +
     A                                             '  Method')
     A            MOBEND        35A         TEXT('Beneficiary Line')
     A                                      COLHDG('Beneficiary' +
     A                                             '   Line')
     A            MOSNCD        35A         TEXT('Sender Correspond. Line')
     A                                      COLHDG('Sender Correspond.' +
     A                                             '      Line')
     A            MORCCD        35A         TEXT('Receiver Correspond. Line')
     A                                      COLHDG('Receiver Correspond.' +
     A                                             '      Line')
     A            MOINTD        35A         TEXT('Intermediary Line')
     A                                      COLHDG('Intermediary' +
     A                                             '   Line')
     A            MOACBD        35A         TEXT('A/c with Inst Line')
     A                                      COLHDG('A/c with Inst Line')
      * STP / LVP / Regulated Flags
     A            MOSTPF         1A         TEXT('STP Flag')
     A                                      COLHDG('STP Flag')
     A            MOLVPF         1A         TEXT('LVP Flag')
     A                                      COLHDG('LVP Flag')
     A            MOREGF         1A         TEXT('Regulated Msg Flag')
     A                                      COLHDG('Regulated Msg Flag')
      * Non routing fields
     A            MOBNKC         4A         TEXT('Bank code')
     A                                      COLHDG('Bank code')
     A            MOINSC       140A         TEXT('Instruction code')
     A                                      COLHDG('Instruction code')
     A            MOTTYP         3A         TEXT('Transaction Type')
     A                                      COLHDG('Transaction Type')
     A            MOORDC       175A         TEXT('Ordering Customer')
     A                                      COLHDG('Ordering Customer')
     A            MOORDB       175A         TEXT('Ordering Bank')
     A                                      COLHDG('Ordering Bank')
     A            MOBENF       175A         TEXT('Beneficiary Customer')
     A                                      COLHDG('Beneficiary Customer')
     A            MOOCHG        35A         TEXT('Our charge to send')
     A                                      COLHDG('Our charge to send')
     A            MOBTBI       210A         TEXT('Bank to Bank Information')
     A                                      COLHDG('Bank to Bank Info')
      *---------------------------------------------------------------------
      * Charges
     A            MOSTDC         5A         TEXT('Standard Charge code')
     A                                      COLHDG('Standard' +
     A                                             'Charge' +
     A                                             'Code')
     A            MOSTDA        15S 0       TEXT('Standard Charge Amount')
     A                                      COLHDG('Standard' +
     A                                             'Charge' +
     A                                             'Amount')
     A            MOLVPC         5A         TEXT('LVP Charge code')
     A                                      COLHDG('LVP' +
     A                                             'Charge' +
     A                                             'Code')
     A            MOLVPA        15S 0       TEXT('LVP Charge Amount')
     A                                      COLHDG('LVP' +
     A                                             'Charge' +
     A                                             'Amount')
     A            MOHDNC         1A         TEXT('Hidden Charges')
     A                                      COLHDG('Hidden' +
     A                                             'Charges')
     A            MOPVAS        12          TEXT('Payment Value Added Service')
     A                                      COLHDG('Payment Value' +
     A                                             'Added Service')
     A            MOR910         1          TEXT('Replace 202C with 910')
     A                                      COLHDG('Replace 202C' +
     A                                             'with 910')
     A            MOR91O         1          TEXT('Replace 202C with 910 Overde')
     A                                      COLHDG('Replace 202C' +
     A                                             'with 910' +
     A                                             'Override')
      *
      *---------------------------------------------------------------------
      * Audit information
     A            MOAJOB    R               TEXT('Job name')
     A                                      COLHDG('Job name')
     A                                      REFFLD(JOJOB)
     A            MOAUSR        10          TEXT('User name')
     A                                      COLHDG('User name')
     A            MOATIM         6  0       TEXT('Action Time')
     A                                      COLHDG('Action' +
     A                                             'Time')
     A            MOARDT         7          TEXT('Action Date')
     A                                      COLHDG('Action' +
     A                                             'Date')
     A            MOAACT         1          TEXT('Action Type')
     A                                      COLHDG('Action' +
     A                                             'Type')
     A            MORDNB         5  0       TEXT('Run day number')
     A                                      COLHDG('Run day' +
     A                                             'number')
      *
      *---------------------------------------------------------------------
      *
     A            MODTP        140A         TEXT('Details of Payment')
     A                                      COLHDG('Details of Payment')
      *
      * PNL014 Start
      *---------------------------------------------------------------------
      * Time Sensitive Routing
     A            MOVDAT         8          TEXT('Value Date')
     A                                      COLHDG('Value' +
     A                                             'Date')
     A            MOCDAT         8          TEXT('Calculated Value Date')
     A                                      COLHDG('Calculated' +
     A                                             'Value' +
     A                                             'Date')
     A            MORREF        25          TEXT('Route Reference')
     A                                      COLHDG('Route' +
     A                                             'Reference')
      *---------------------------------------------------------------------
      * PNL014 End
      *
      *****************************************************************
