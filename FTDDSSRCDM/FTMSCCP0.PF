      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_RU                                                        *
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PF/FTMSCCP0 - Midas FT Payment Correspondent                 *
      *                                                               *
      *  (C) Copyright Misys IBS Ltd. 2004                            *
      *                                                               *
      *  Last Amend No. MD31937            Date 13Jan15               *
      *  Last Amend No. AR856737           Date 13Sep11               *
      *  Last Amend No. PNL014             DATE 07Dec2005             *
      *  Prev Amend No. EEE065             Date 01Jan2004             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD31937  - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  PNL014 - Time Sensitive Routing                              *
      *  EEE065 - FT Payment Routing                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R @MSCCP0                   TEXT('(C) Misys IBS  +
     A                                      . 2004')
      *---------------------------------------------------------------------
      *
     A            MCBICC         8          TEXT('BIC (Bank, Country, & Loc')
     A                                      COLHDG('BIC Code' +
     A                                             'Bank, Country, ' +
     A                                             'Location')
     A            MCBICB         3          TEXT('BIC (Branch Code)')
     A                                      COLHDG('BIC' +
     A                                             'Branch' +
     A                                             'Code')
     A            MCPCCY         3          TEXT('Payment Currency')
     A                                      COLHDG('Payment' +
     A                                             'Currency')
      *
     A            MCRECI         1          TEXT('Record Identifier')
     A                                      COLHDG('Record' +
     A                                             'Indentifier')
      *
     A            MCCTYP        10          TEXT('Correspondent Type')
     A                                      COLHDG('Correspondent' +
     A                                             'Type')
     A            MCCTRY         2          TEXT('Country Location')
     A                                      COLHDG('Country' +
     A                                             'Location')
     A            MCPCOR         1          TEXT('Preferential Correspondent')
     A                                      COLHDG('Preferential' +
     A                                             'Correspondent')
     A            MCPNBR         7S 0       TEXT('Preferential Number Routed')
     A                                      COLHDG('Preferential' +
     A                                             'Number Routed')
     A            MCS71A         7          TEXT('Std Quotation of 71A')
     A                                      COLHDG('Std Quotation' +
     A                                             'Of 71A')
     A            MCDACR        34          TEXT('Direct Account reference')
     A                                      COLHDG('Direct Account' +
     A                                             'Reference')
      *---------------------------------------------------------------------
      * Direct account in Midas
     A            MCDBRC         3A         TEXT('Direct Account Branch')
     A                                      COLHDG('Direct ' +
     A                                             'Account' +
     A                                             'Branch ')
     A            MCDCST         6A         TEXT('Direct Account Customer')
     A                                      COLHDG('Direct ' +
     A                                             'Account' +
     A                                             'Customer')
     A            MCDCCY         3A         TEXT('Direct Account Currency')
     A                                      COLHDG('Direct ' +
     A                                             'Account' +
     A                                             'Currency')
     A            MCDACD         4A         TEXT('Direct Account Account code')
     A                                      COLHDG('Direct ' +
     A                                             'Account' +
     A                                             'Account code')
     A            MCDASQ         2A         TEXT('Direct Account Account Seq.')
     A                                      COLHDG('Direct ' +
     A                                             'Account' +
     A                                             'Account Seq. ')
      *
      *---------------------------------------------------------------------
     A            MCLVPA         1          TEXT('Low Value Payment Agreement')
     A                                      COLHDG('Low Value' +
     A                                             'Pay Agreement')
     A            MCLVPT        15S 0       TEXT('Low Value Threshold')
     A                                      COLHDG('Low Value' +
     A                                             'Threshold')
     A            MCQ71A         7          TEXT('Quotation of 71A')
     A                                      COLHDG('Quotation' +
     A                                             'Of 71A')
     A            MCPLNB         7S 0       TEXT('Preferential LVP Nbr Routed')
     A                                      COLHDG('Preferential LVP' +
     A                                             'Number Routed')
     A            MCSCHR        13S 8       TEXT('Standard - Charge %')
     A                                      COLHDG('Standard' +
     A                                             'Charge %')
     A            MCSCHL        15S 0       TEXT('Standard - Min Fee')
     A                                      COLHDG('Standard' +
     A                                             'Min Fee')
     A            MCSCHH        15S 0       TEXT('Standard - Max Fee')
     A                                      COLHDG('Standard' +
     A                                             'Max Fee')
     A            MCSCHC         5          TEXT('Standard - Charge code')
     A                                      COLHDG('Standard' +
     A                                             'Charge Code')
     A            MCLCHR        13S 8       TEXT('LVPA - Charge %')
     A                                      COLHDG('LVPA' +
     A                                             'Charge %')
     A            MCLCHL        15S 0       TEXT('LVPA - Min Fee')
     A                                      COLHDG('LVPA' +
     A                                             'Min Fee')
     A            MCLCHH        15S 0       TEXT('LVPA - Max Fee')
     A                                      COLHDG('LVPA' +
     A                                             'Max Fee')
     A            MCLCHC         5          TEXT('LVPA - Charge code')
     A                                      COLHDG('LVPA' +
     A                                             'Charge Code')
      *---------------------------------------------------------------------
     A            MCREGP         1          TEXT('Regulated Payment Agreement')
     A                                      COLHDG('Regulated' +
     A                                             'Pay Agreement')
     A            MCREGT        15S 0       TEXT('Regulated Threshold')
     A                                      COLHDG('Regulated' +
     A                                             'Threshold')
     A            MCR71A         7          TEXT('Reg Quotation of 71A')
     A                                      COLHDG('Reg Quotation' +
     A                                             'Of 71A')
     A            MCPRNB         7S 0       TEXT('Preferential Reg Nbr Routed')
     A                                      COLHDG('Preferential Reg' +
     A                                             'Number Routed')
      *---------------------------------------------------------------------
     A            MCDES1        35          TEXT('Description 1')
     A                                      COLHDG('Description 1')
     A            MCDES2        35          TEXT('Description 2')
     A                                      COLHDG('Description 2')
     A            MCDES3        35          TEXT('Description 3')
     A                                      COLHDG('Description 3')
      *---------------------------------------------------------------------
     A            MCIDAB         1          TEXT('Inhibit Direct A/c BEN')
     A                                      COLHDG('Inhibit Direct' +
     A                                             'A/c BEN')
     A            MCIDAS         1          TEXT('Inhibit Direct A/c SHA')
     A                                      COLHDG('Inhibit Direct' +
     A                                             'A/c SHA')
     A            MCIDAO         1          TEXT('Inhibit Direct A/c OUR')
     A                                      COLHDG('Inhibit Direct' +
     A                                             'A/c OUR')
     A            MCSPCR         1          TEXT('Split Preferential Cor')
     A                                      COLHDG('Split Preferential' +
     A                                             'Correspondent')
     A            MCSPTH        15S 0       TEXT('Split Threshold')
     A                                      COLHDG('Split' +
     A                                             'Threshold')
     A            MCSPBN         7S 0       TEXT('Split Pref BEN Number Routed')
     A                                      COLHDG('Split Pref BEN' +
     A                                             'Number Routed')
     A            MCSPSN         7S 0       TEXT('Split Pref SHA Number Routed')
     A                                      COLHDG('Split Pref SHA' +
     A                                             'Number Routed')
     A            MCSPON         7S 0       TEXT('Split Pref OUR Number Routed')
     A                                      COLHDG('Split Pref OUR' +
     A                                             'Number Routed')
     A            MCBPAL        15S 0       TEXT('BEN Payment Amount - Min')
     A                                      COLHDG('BEN Payment Amount' +
     A                                             'Min')
     A            MCBPAH        15S 0       TEXT('BEN Payment Amount - Max')
     A                                      COLHDG('BEN Payment Amount' +
     A                                             'Max')
     A            MCSPAL        15S 0       TEXT('SHA Payment Amount - Min')
     A                                      COLHDG('SHA Payment Amount' +
     A                                             'Min')
     A            MCSPAH        15S 0       TEXT('SHA Payment Amount - Max')
     A                                      COLHDG('SHA Payment Amount' +
     A                                             'Max')
     A            MCOPAL        15S 0       TEXT('OUR Payment Amount - Min')
     A                                      COLHDG('OUR Payment Amount' +
     A                                             'Min')
     A            MCOPAH        15S 0       TEXT('OUR Payment Amount - Max')
     A                                      COLHDG('OUR Payment Amount' +
     A                                             'Max')
     A            MCSTMT         2          TEXT('Settlement Method')
     A                                      COLHDG('Settlement' +
     A                                             'Method')
     A            MCPVAS        12          TEXT('Payment Value Added Service')
     A                                      COLHDG('Payment Value' +
     A                                             'Added Service')
     A            MCOTHR        15S 0       TEXT('Override Threshold Amount')
     A                                      COLHDG('Override Threshold' +
     A                                             'Amount')
     A            MCOSTM         2          TEXT('Override Settlement Method')
     A                                      COLHDG('Override Settlement' +
     A                                             'Method')
     A            MCOVAS        12          TEXT('Override Value Added Service')
     A                                      COLHDG('Override Value' +
     A                                             'Added Service')
     A            MCO71A         7          TEXT('Override Quotation of 71A')
     A                                      COLHDG('Override Quotation' +
     A                                             'Of 71A')
     A            MCOACR        34          TEXT('Override Account reference')
     A                                      COLHDG('Override Account' +
     A                                             'Reference')
      *---------------------------------------------------------------------
      * Direct account in Midas
     A            MCOBRC         3A         TEXT('Override Account Branch')
     A                                      COLHDG('Override ' +
     A                                             'Account' +
     A                                             'Branch ')
     A            MCOCST         6A         TEXT('Override Account Customer')
     A                                      COLHDG('Override ' +
     A                                             'Account' +
     A                                             'Customer')
     A            MCOCCY         3A         TEXT('Override Account Currency')
     A                                      COLHDG('Override' +
     A                                             'Account' +
     A                                             'Currency')
     A            MCOACD         4A         TEXT('Override Account Account cde')
     A                                      COLHDG('Override ' +
     A                                             'Account' +
     A                                             'Account code')
     A            MCOASQ         2A         TEXT('Override Account Account Seq')
     A                                      COLHDG('Override ' +
     A                                             'Account' +
     A                                             'Account Seq. ')
      *
      *---------------------------------------------------------------------
      * Cover Replacement Information
     A            MCR910         1          TEXT('Replace 202C with 910')
     A                                      COLHDG('Replace 202C' +
     A                                             'with 910')
      *
      *---------------------------------------------------------------------
      * Active Dates
      *
     A            MCSTAT         1          TEXT('Status' )
     A                                      COLHDG('Status')
     A            MCSTDT         5S 0       TEXT('Start Date')
     A                                      COLHDG('Start' +
     A                                             'Date')
     A            MCENDT         5S 0       TEXT('End Date')
     A                                      COLHDG('End' +
     A                                             'Date')
      *
      *---------------------------------------------------------------------
      * Audit information
     A            MCAJOB    R               TEXT('Job name')
     A                                      COLHDG('Job name')
     A                                      REFFLD(JOJOB)
     A            MCAUSR        10          TEXT('User name')
     A                                      COLHDG('User name')
     A            MCATIM         6  0       TEXT('Action Time')
     A                                      COLHDG('Action' +
     A                                             'Time')
     A            MCARDT         7          TEXT('Action Date')
     A                                      COLHDG('Action' +
     A                                             'Date')
     A            MCAACT         1          TEXT('Action Type')
     A                                      COLHDG('Action' +
     A                                             'Type')
     A            MCRDNB         5  0       TEXT('Run day number')
     A                                      COLHDG('Run day' +
     A                                             'number')
      *
      * PNL014 Start
      *---------------------------------------------------------------------
      * Time Sensitive Routing
     A            MCTSDO         5          TEXT('TSP Date Offset')
     A                                      COLHDG('TSP Date' +
     A                                             'Offset')
     A            MCTSCT         4S 0       TEXT('TSP Cut Off Time')
     A                                      COLHDG('TSP Cut' +
     A                                             'Off Time')
     A            MCTSCD         2S 0       TEXT('TSP Cut Off Day Offset')
     A                                      COLHDG('TSP Cut' +
     A                                             'Off Day' +
     A                                             'Offset')
     A            MCTSCK         1          TEXT('TSP Cut Off Check')
     A                                      COLHDG('TSP Cut' +
     A                                             'Off' +
     A                                             'Check')
     A            MCTSHC         3          TEXT('TSP Cut Off Holiday Cal. Ccy')
     A                                      COLHDG('TSP Cut' +
     A                                             'Off Holiday' +
     A                                             'Calendar Currency')
     A            MCTSHL         3          TEXT('TSP Cut Off Holiday Cal. Loc')
     A                                      COLHDG('TSP Cut' +
     A                                             'Off Holiday' +
     A                                             'Calendar Location')
     A            MCTSFC         1          TEXT('TSP Fail Condition')
     A                                      COLHDG('TSP Fail' +
     A                                             'Condition')
     A            MCTSIS        10          TEXT('TSP Iteration Sequence')
     A                                      COLHDG('TSP Iteration' +
     A                                             'Sequence')
     A            MCRREF        25          TEXT('Route Reference')
     A                                      COLHDG('Route' +
     A                                             'Reference')
      *
      * PNL014 End
      *
      *---------------------------------------------------------------------
      *****************************************************************
