      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_RU                                                        *
/*EXI *  TEXT('Midas FT Payment Correspondent Pay Routes')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PF/FTMSCPP0 - Midas FT Payment Correspondent Pay Routes      *
      *                                                               *
      *  (C) Copyright Misys IBS Ltd. 2004                            *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 13Sep11               *
      *                 PNL015             Date 30Nov2005             *
      *                 PNL014             Dare 07Dec2005             *
      *                 EEE065 *Amend      Date 25Nov2004             *
      *                 EEE065             Date 01Jan2004             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  PNL015 - Redirection to head office BIC                      *
      *  PNL014 - Time Sensitive Routing                              *
      *  EEE065 - Allow Foreign BICs                                  *
      *  EEE065 - FT Payment Routing                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R @MSCPP0                   TEXT('(C) Misys IBS  +
     A                                      . 2004')
      *---------------------------------------------------------------------
      *
     A            MPBICC         8          TEXT('BIC (Bank, Country, & Loc')
     A                                      COLHDG('BIC Code' +
     A                                             'Bank, Country, ' +
     A                                             'Location')
     A            MPBICB         3          TEXT('BIC (Branch Code)')
     A                                      COLHDG('BIC' +
     A                                             'Branch' +
     A                                             'Code')
     A            MPPCCY         3          TEXT('Payment Currency')
     A                                      COLHDG('Payment' +
     A                                             'Currency')
      *
     A            MPRECI         1          TEXT('Record Identifier')
     A                                      COLHDG('Record' +
     A                                             'Indentifier')
      *
      *---------------------------------------------------------------------
      * Correspondent Payment Pay Route
     A            MPCBIC         8          TEXT('Pay Through BIC')
     A                                      COLHDG('Pay Through ' +
     A                                             'BIC')
     A            MPCBIB         3          TEXT('Pay Through BIC branch')
     A                                      COLHDG('Pay Through ' +
     A                                             'BIC Branch')
      *
     A            MPRCAU         1          TEXT('Receive a/c Used')
     A                                      COLHDG('Receive a/c ' +
     A                                             'Used')
     A            MPRCAC        35          TEXT('Receive a/c Quoted')
     A                                      COLHDG('Receive a/c ' +
     A                                             'Quoted')
      *
      *---------------------------------------------------------------------
     A            MPC53C         8          TEXT('Senders Correspondent BIC')
     A                                      COLHDG('Senders' +
     A                                             'Correspondent BIC')
     A            MPC53B         3          TEXT('Senders Correspondent BIC Br')
     A                                      COLHDG('Senders' +
     A                                             'Correspondent BIC Br')
     A            MPC53A        10          TEXT('Senders Correspondent A/c')
     A                                      COLHDG('Senders' +
     A                                             'Correspondent A/c')
     A            MPC53R         1          TEXT('Cover Required')
     A                                      COLHDG('Cover' +
     A                                             'Required')
     A            MPSERP         1          TEXT('Serial Payment')
     A                                      COLHDG('Serial' +
     A                                             'Payment')
     A            MPSLVP         1          TEXT('Serial LVP')
     A                                      COLHDG('Serial' +
     A                                             'LVP')
     A            MPSREG         1          TEXT('Serial REG')
     A                                      COLHDG('Serial' +
     A                                             'REG')
     A            MPSOUR         1          TEXT('Serial OUR')
     A                                      COLHDG('Serial' +
     A                                             'OUR')
     A            MPC54C         8          TEXT('Receivers Correspondent BIC')
     A                                      COLHDG('Receivers' +
     A                                             'Correspondent BIC')
     A            MPC54B         3          TEXT('Receivers Corr BIC Br')
     A                                      COLHDG('Receivers' +
     A                                             'Corr BIC Br')
     A            MPC56C         8          TEXT('Intermediary BIC')
     A                                      COLHDG('Intermediary' +
     A                                             'BIC')
     A            MPC56B         3          TEXT('Intermediary BIC Br')
     A                                      COLHDG('Intermediary' +
     A                                             'BIC Br')
     A            MPSCHR        13S 8       TEXT('Standard - Charge %')
     A                                      COLHDG('Standard' +
     A                                             'Charge %')
     A            MPSCHL        15S 0       TEXT('Standard - Min Fee')
     A                                      COLHDG('Standard' +
     A                                             'Min Fee')
     A            MPSCHH        15S 0       TEXT('Standard - Max Fee')
     A                                      COLHDG('Standard' +
     A                                             'Max Fee')
     A            MPSCHC         5          TEXT('Standard - Charge code')
     A                                      COLHDG('Standard' +
     A                                             'Charge Code')
     A            MPDES1        35          TEXT('Description 1')
     A                                      COLHDG('Description 1')
     A            MPDES2        35          TEXT('Description 2')
     A                                      COLHDG('Description 2')
     A            MPDES3        35          TEXT('Description 3')
     A                                      COLHDG('Description 3')
     A            MPPCOR         1          TEXT('Preferential Correspondent')
     A                                      COLHDG('Preferential' +
     A                                             'Correspondent')
     A            MPPPCT         2S 0       TEXT('Preferential Percentage')
     A                                      COLHDG('Preferential' +
     A                                             'Percentage')
     A            MPPNBR         7S 0       TEXT('Preferential Number Routed')
     A                                      COLHDG('Preferential' +
     A                                             'Number Routed')
     A            MPPBCR         1          TEXT('Preferential Correspond BEN')
     A                                      COLHDG('Preferential' +
     A                                             'Correspondent BEN')
     A            MPPBPT         2S 0       TEXT('Preferential Percentage BEN')
     A                                      COLHDG('Preferential' +
     A                                             'Percentage BEN')
     A            MPPBNB         7S 0       TEXT('Preferential Number Rout BEN')
     A                                      COLHDG('Preferential' +
     A                                             'Number Routed BEN')
     A            MPPSCR         1          TEXT('Preferential Correspond SHA')
     A                                      COLHDG('Preferential' +
     A                                             'Correspondent SHA')
     A            MPPSPT         2S 0       TEXT('Preferential Percentage SHA')
     A                                      COLHDG('Preferential' +
     A                                             'Percentage SHA')
     A            MPPSNB         7S 0       TEXT('Preferential Number Rout SHA')
     A                                      COLHDG('Preferential' +
     A                                             'Number Routed SHA')
     A            MPPOCR         1          TEXT('Preferential Correspond OUR')
     A                                      COLHDG('Preferential' +
     A                                             'Correspondent OUR')
     A            MPPOPT         2S 0       TEXT('Preferential Percentage OUR')
     A                                      COLHDG('Preferential' +
     A                                             'Percentage OUR')
     A            MPPONB         7S 0       TEXT('Preferential Number Rout OUR')
     A                                      COLHDG('Preferential' +
     A                                             'Number Routed OUR')
     A            MPPREG         1          TEXT('Preferential Regulated Cor')
     A                                      COLHDG('Preferential Reg' +
     A                                             'Correspondent')
     A            MPPRPC         2S 0       TEXT('Preferential Regulated %')
     A                                      COLHDG('Preferential' +
     A                                             'Regulated %')
     A            MPPRNB         7S 0       TEXT('Preferential Reg Nbr Routed')
     A                                      COLHDG('Preferential Reg' +
     A                                             'Number Routed')
     A            MPPLVP         1          TEXT('Preferential LVP Cor')
     A                                      COLHDG('Preferential LVP' +
     A                                             'Correspondent')
     A            MPPLPC         2S 0       TEXT('Preferential LVP %')
     A                                      COLHDG('Preferential' +
     A                                             'LVP %')
     A            MPPLNB         7S 0       TEXT('Preferential LVP Nbr Routed')
     A                                      COLHDG('Preferential LVP' +
     A                                             'Number Routed')
      *---------------------------------------------------------------------
     A            MPBENR         1          TEXT('Route for BEN')
     A                                      COLHDG('Reroute' +
     A                                             'For BEN')
     A            MPBENL        15S 0       TEXT('BEN Amount - Min')
     A                                      COLHDG('BEN Amount' +
     A                                             'Min')
     A            MPBENH        15S 0       TEXT('BEN Amount - Max')
     A                                      COLHDG('BEN Amount' +
     A                                             'Max')
     A            MPSHAR         1          TEXT('Route for SHA')
     A                                      COLHDG('Reroute' +
     A                                             'For SHA')
     A            MPSHAL        15S 0       TEXT('SHA Amount - Min')
     A                                      COLHDG('SHA Amount' +
     A                                             'Min')
     A            MPSHAH        15S 0       TEXT('SHA Amount - Max')
     A                                      COLHDG('SHA Amount' +
     A                                             'Max')
     A            MPOURR         1          TEXT('Route for OUR')
     A                                      COLHDG('Reroute' +
     A                                             'For OUR')
     A            MPOURL        15S 0       TEXT('OUR Amount - Min')
     A                                      COLHDG('OUR Amount' +
     A                                             'Min')
     A            MPOURH        15S 0       TEXT('OUR Amount - Max')
     A                                      COLHDG('OUR Amount' +
     A                                             'Max')
      *
      *---------------------------------------------------------------------
      * Cover Replacement Information
     A            MPR910         1          TEXT('Replace 202C with 910')
     A                                      COLHDG('Replace 202C' +
     A                                             'with 910')
     A            MPSTMT         2          TEXT('Settlement Method')
     A                                      COLHDG('Settlement' +
     A                                             'Method')
      *
      *---------------------------------------------------------------------
      * Active Dates
      *
     A            MPSTAT         1          TEXT('Status' )
     A                                      COLHDG('Status')
     A            MPSTDT         5S 0       TEXT('Start Date')
     A                                      COLHDG('Start' +
     A                                             'Date')
     A            MPENDT         5S 0       TEXT('End Date')
     A                                      COLHDG('End' +
     A                                             'Date')
      *
      *---------------------------------------------------------------------
      * Audit information
     A            MPAJOB    R               TEXT('Job name')
     A                                      COLHDG('Job name')
     A                                      REFFLD(JOJOB)
     A            MPAUSR        10          TEXT('User name')
     A                                      COLHDG('User name')
     A            MPATIM         6  0       TEXT('Action Time')
     A                                      COLHDG('Action' +
     A                                             'Time')
     A            MPARDT         7          TEXT('Action Date')
     A                                      COLHDG('Action' +
     A                                             'Date')
     A            MPAACT         1          TEXT('Action Type')
     A                                      COLHDG('Action' +
     A                                             'Type')
     A            MPRDNB         5  0       TEXT('Run day number')
     A                                      COLHDG('Run day' +
     A                                             'number')
      *
      *---------------------------------------------------------------------
     A            MPFBIC         1          TEXT('Foreign Banks?')
     A                                      COLHDG('Foreign' +
     A                                             'Banks?')
      *****************************************************************
      * PNL015 Start
     A            MPRHOB         1          TEXT('Route via Head Office BIC')
     A                                      COLHDG('Route via' +
     A                                             'Head Office' +
     A                                             'BIC')
      * PNL015 End
      * PNL014 Start
      *---------------------------------------------------------------------
      * Time Sensitive Routing
     A            MPRTSQ         3          TEXT('Routing Sequence')
     A                                      COLHDG('Routing' +
     A                                             'Sequence')
     A            MPTSDO         5          TEXT('TSP Date Offset')
     A                                      COLHDG('TSP Date' +
     A                                             'Offset')
     A            MPTSCT         4S 0       TEXT('TSP Cut Off Time')
     A                                      COLHDG('TSP Cut' +
     A                                             'Off Time')
     A            MPTSCD         2S 0       TEXT('TSP Cut Off Day Offset')
     A                                      COLHDG('TSP Cut' +
     A                                             'Off Day' +
     A                                             'Offset')
     A            MPTSCK         1          TEXT('TSP Cut Off Check')
     A                                      COLHDG('TSP Cut' +
     A                                             'Off' +
     A                                             'Check')
     A            MPTSHC         3          TEXT('TSP Cut Off Holiday Cal. Ccy')
     A                                      COLHDG('TSP Cut' +
     A                                             'Off Holiday' +
     A                                             'Calendar Currency')
     A            MPTSHL         3          TEXT('TSP Cut Off Holiday Cal. Loc')
     A                                      COLHDG('TSP Cut' +
     A                                             'Off Holiday' +
     A                                             'Calendar Location')
     A            MPTSFC         1          TEXT('TSP Fail Condition')
     A                                      COLHDG('TSP Fail' +
     A                                             'Condition')
     A            MPTSIS        10          TEXT('TSP Iteration Sequence')
     A                                      COLHDG('TSP Iteration' +
     A                                             'Sequence')
     A            MPRREF        25          TEXT('Route Reference')
     A                                      COLHDG('Route' +
     A                                             'Reference')
      *
      * PNL014 End
      *---------------------------------------------------------------------
