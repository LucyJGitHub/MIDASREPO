      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_RU                                                        *
/*EXI *  TEXT('Midas FT Payment Correspondent General Routes')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PF/FTMSGRP0 - Midas FT Payment Correspondent General Routes  *
      *                                                               *
      *  (C) Copyright Misys IBS Ltd. 2005                            *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 13Sep11               *
      *                 PNL015             Date 30NOV2005             *
      *                 EEE065   *CREATE   Date 11Jan2005             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  PNL015 - Redirection to head office BIC                      *
      *  EEE065 - General Routes                                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R @MSGRP0                   TEXT('(C) Misys IBS  +
     A                                      . 2005')
      *---------------------------------------------------------------------
      *
     A            MGBICC         8          TEXT('BIC (Bank, Country, & Loc')
     A                                      COLHDG('BIC Code' +
     A                                             'Bank, Country, ' +
     A                                             'Location')
     A            MGBICB         3          TEXT('BIC (Branch Code)')
     A                                      COLHDG('BIC' +
     A                                             'Branch' +
     A                                             'Code')
     A            MGPCCY         3          TEXT('Payment Currency')
     A                                      COLHDG('Payment' +
     A                                             'Currency')
      *
     A            MGRECI         1          TEXT('Record Identifier')
     A                                      COLHDG('Record' +
     A                                             'Indentifier')
      *
      *---------------------------------------------------------------------
      * Correspondent General Pay Route
     A            MGCBIC         8          TEXT('Pay Through BIC')
     A                                      COLHDG('Pay Through ' +
     A                                             'BIC')
     A            MGCBIB         3          TEXT('Pay Through BIC branch')
     A                                      COLHDG('Pay Through ' +
     A                                             'BIC Branch')
      *
     A            MGRCAU         1          TEXT('Receive a/c Used')
     A                                      COLHDG('Receive a/c ' +
     A                                             'Used')
     A            MGRCAC        35          TEXT('Receive a/c Quoted')
     A                                      COLHDG('Receive a/c ' +
     A                                             'Quoted')
      *
      *---------------------------------------------------------------------
      * Main BIC details
      *
     A            MGCLRC         8          TEXT('Clearer Correspondent BIC')
     A                                      COLHDG('Clearer' +
     A                                             'Correspondent BIC')
     A            MGCLRB         3          TEXT('Clearer Correspondent BIC Br')
     A                                      COLHDG('Clearer' +
     A                                             'Correspondent BIC Br')
     A            MGUI56         1          TEXT('Use in Field 56')
     A                                      COLHDG('Use in' +
     A                                             'Field 56')
      *
      *---------------------------------------------------------------------
      *
     A            MGCREG         3          TEXT('Clearing Region')
     A                                      COLHDG('Clearing' 'Region')
      *
     A            MGCTY1         2          TEXT('Clearing Country 1')
     A                                      COLHDG('Clearing' 'Country 1')
     A            MGCTY2         2          TEXT('Clearing Country 2')
     A                                      COLHDG('Clearing' 'Country 2')
     A            MGCTY3         2          TEXT('Clearing Country 3')
     A                                      COLHDG('Clearing' 'Country 3')
     A            MGCTY4         2          TEXT('Clearing Country 4')
     A                                      COLHDG('Clearing' 'Country 4')
     A            MGCTY5         2          TEXT('Clearing Country 5')
     A                                      COLHDG('Clearing' 'Country 5')
      *
     A            MGCLID        10          TEXT('Clearing System Id.')
     A                                      COLHDG('Clearing' 'System Id.')
      *
      *---------------------------------------------------------------------
     A            MGPROT         1          TEXT('Preferential Route')
     A                                      COLHDG('Preferential' +
     A                                             'Route')
      *
      *---------------------------------------------------------------------
     A            MGBENR         1          TEXT('Route for BEN')
     A                                      COLHDG('Reroute' +
     A                                             'For BEN')
     A            MGBENL        15S 0       TEXT('BEN Amount - Min')
     A                                      COLHDG('BEN Amount' +
     A                                             'Min')
     A            MGBENH        15S 0       TEXT('BEN Amount - Max')
     A                                      COLHDG('BEN Amount' +
     A                                             'Max')
     A            MGSHAR         1          TEXT('Route for SHA')
     A                                      COLHDG('Reroute' +
     A                                             'For SHA')
     A            MGSHAL        15S 0       TEXT('SHA Amount - Min')
     A                                      COLHDG('SHA Amount' +
     A                                             'Min')
     A            MGSHAH        15S 0       TEXT('SHA Amount - Max')
     A                                      COLHDG('SHA Amount' +
     A                                             'Max')
     A            MGOURR         1          TEXT('Route for OUR')
     A                                      COLHDG('Reroute' +
     A                                             'For OUR')
     A            MGOURL        15S 0       TEXT('OUR Amount - Min')
     A                                      COLHDG('OUR Amount' +
     A                                             'Min')
     A            MGOURH        15S 0       TEXT('OUR Amount - Max')
     A                                      COLHDG('OUR Amount' +
     A                                             'Max')
      *
      *---------------------------------------------------------------------
     A            MGDES1        35          TEXT('Description 1')
     A                                      COLHDG('Description 1')
     A            MGDES2        35          TEXT('Description 2')
     A                                      COLHDG('Description 2')
     A            MGDES3        35          TEXT('Description 3')
     A                                      COLHDG('Description 3')
      *
      *---------------------------------------------------------------------
      * Active Dates
      *
     A            MGSTAT         1          TEXT('Status' )
     A                                      COLHDG('Status')
     A            MGSTDT         5S 0       TEXT('Start Date')
     A                                      COLHDG('Start' +
     A                                             'Date')
     A            MGENDT         5S 0       TEXT('End Date')
     A                                      COLHDG('End' +
     A                                             'Date')
      *
      *---------------------------------------------------------------------
      * Audit information
     A            MGAJOB    R               TEXT('Job name')
     A                                      COLHDG('Job name')
     A                                      REFFLD(JOJOB)
     A            MGAUSR        10          TEXT('User name')
     A                                      COLHDG('User name')
     A            MGATIM         6  0       TEXT('Action Time')
     A                                      COLHDG('Action' +
     A                                             'Time')
     A            MGARDT         7          TEXT('Action Date')
     A                                      COLHDG('Action' +
     A                                             'Date')
     A            MGAACT         1          TEXT('Action Type')
     A                                      COLHDG('Action' +
     A                                             'Type')
     A            MGRDNB         5  0       TEXT('Run day number')
     A                                      COLHDG('Run day' +
     A                                             'number')
      *
      *****************************************************************
      * PNL015 Start
     A            MGEX54         1          TEXT('Exclude from 54')
     A                                      COLHDG('Exclude' +
     A                                             'from' +
     A                                             '54')
     A            MGHOBC         1          TEXT('Head Office BIC')
     A                                      COLHDG('Head' +
     A                                             'Office' +
     A                                             'BIC')
      * PNL015 End
