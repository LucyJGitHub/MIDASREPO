      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_RU                                                        *
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PF/FTPICDP0 - Midas FT Payment Route ICD                     *
      *                                                               *
      *  (C) Copyright Misys IBS Ltd. 2004                            *
      *                                                               *
      *  Last Amend No. MD31937            Date 13Jan15               *
      *  Last Amend No. AR856737           Date 13Sep11               *
      *  Last Amend No. PNL014             DATE 07Dec2005             *
      *  Prev Amend No. EEE065             Date 02Jun2004             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD31937  - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  PNL014 - Time Sensitive Routing                              *
      *  EEE065 - FT Payment Routing                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R @PICDP0                   TEXT('(C) Misys IBS  +
     A                                      . 2004')
      *---------------------------------------------------------------------
     A            ICKEY         10A         TEXT('ICD Key')
     A                                      COLHDG('ICD' +
     A                                             'Key')
     A            ICRECI         1A         TEXT('Record Identifier')
     A                                      COLHDG('Record' +
     A                                             'Identifier')
     A            ICRROV         1A         TEXT('Re-Route Pre-Procees')
     A                                      COLHDG('Re-Route' +
     A                                             'Pre-Process')
     A            ICDBRC         3A         TEXT('Default Branch')
     A                                      COLHDG('Default' +
     A                                             'Branch')
     A            ICOURD         1A         TEXT('OUR Direct only')
     A                                      COLHDG('OUR Direct' +
     A                                             'Only')
     A            ICOURL         1A         TEXT('OUR Least Cost Route')
     A                                      COLHDG('OUR Least' +
     A                                             'Cost Route')
     A            ICSHAL         1A         TEXT('SHA Least Cost Route')
     A                                      COLHDG('SHA Least' +
     A                                             'Cost Route')
     A            ICBENL         1A         TEXT('BEN Least Cost Route')
     A                                      COLHDG('BEN Least' +
     A                                             'Cost Route')
     A            ICSTPC         1A         TEXT('STP Message Check')
     A                                      COLHDG('STP Message' +
     A                                             'Check')
     A            ICMACP         1A         TEXT('Multiple Account Processing')
     A                                      COLHDG('Multiple Account' +
     A                                             'Processing')
     A            ICSVAP         1A         TEXT('SWIFT Value Added Processing')
     A                                      COLHDG('SWIFT Value Added' +
     A                                             'Processing')
      *
      *---------------------------------------------------------------------
      * Audit information
     A            ICAJOB    R               TEXT('Job name')
     A                                      COLHDG('Job name')
     A                                      REFFLD(JOJOB)
     A            ICAUSR        10          TEXT('User name')
     A                                      COLHDG('User name')
     A            ICATIM         6  0       TEXT('Action Time')
     A                                      COLHDG('Action' +
     A                                             'Time')
     A            ICARDT         7          TEXT('Action Date')
     A                                      COLHDG('Action' +
     A                                             'Date')
     A            ICAACT         1          TEXT('Action Type')
     A                                      COLHDG('Action' +
     A                                             'Type')
     A            ICRDNB         5  0       TEXT('Run day number')
     A                                      COLHDG('Run day' +
     A                                             'number')
      * PNL014 Start
      *---------------------------------------------------------------------
      * Time Sensitive Routing
     A            ICTSRT         1          TEXT('Time Sensitive Route Active')
     A                                      COLHDG('Time Sensitive ' +
     A                                             'Route' +
     A                                             'Active')
     A            ICTSBS         1          TEXT('Time Sensitive Base Date')
     A                                      COLHDG('Time Sensitive ' +
     A                                             'Base' +
     A                                             'Date')
     A            ICTSNI         2S 0       TEXT('Time Sensitive Iter Nbr')
     A                                      COLHDG('Time Sensitive ' +
     A                                             'Iterations' +
     A                                             'Number')
     A            ICTSDI        10          TEXT('Time Sensitive Day Increment')
     A                                      COLHDG('Time Sensitive ' +
     A                                             'Day' +
     A                                             'Increment')
     A            ICTSDS        10          TEXT('Time Sensitive Day Defaults')
     A                                      COLHDG('Time Sensitive ' +
     A                                             'Day' +
     A                                             'Defaults')
      *
      * PNL014 End
      *---------------------------------------------------------------------
      *
      *****************************************************************
