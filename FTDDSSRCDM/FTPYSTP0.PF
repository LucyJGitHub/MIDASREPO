      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_RU                                                        *
/*EXI *  TEXT('Midas FT Payment Sub/type defaulting')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PF/FTPYSTP0 - Payment Sub/type defaulting                    *
      *                                                               *
      *  (C) Copyright Misys IBS Ltd. 2003                            *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 13Sep11               *
      *                 EEE058   *Create   Date 12Feb2003             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  EEE058 - Incominng STP                                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R @PYSTP0                   TEXT('(C) Misys IBS  +
     A                                      . 2003')
      *---------------------------------------------------------------------
     A            PYPYTP         2          TEXT('Payment type')
     A                                      COLHDG('Payment' +
     A                                             'Type')
     A            PYBTYP         1          TEXT('Beneficiary Type')
     A                                      COLHDG('Beneficiary' +
     A                                             'Type')
     A            PYFXIN         1          TEXT('FX Transaction')
     A                                      COLHDG('FX' +
     A                                             'Transaction')
     A            PYF71A         3          TEXT('Charges option')
     A                                      COLHDG('Charges' +
     A                                             'Option')
     A            PYF71G         1          TEXT('Our Charges')
     A                                      COLHDG('Our' +
     A                                             'Charges')
     A            PYPCHR         2          TEXT('Payment Character')
     A                                      COLHDG('Payment' +
     A                                             'Character')
     A            PYDRAC         1          TEXT('Debit Account Type')
     A                                      COLHDG('Debit' +
     A                                             'Account' +
     A                                             'Type')
     A            PYSTMT         2          TEXT('Settlement Type')
     A                                      COLHDG('Settlement' +
     A                                             'Type')
     A            PYPYST         2          TEXT('Payment subtype')
     A                                      COLHDG('Payment' +
     A                                             'SubType')
      *
     A            PYRECI         1          TEXT('Record Identifier')
     A                                      COLHDG('Record' +
     A                                             'Indentifier')
      *
      *---------------------------------------------------------------------
     A            PYDES1        35          TEXT('Description 1')
     A                                      COLHDG('Description 1')
     A            PYDES2        35          TEXT('Description 2')
     A                                      COLHDG('Description 2')
     A            PYDES3        35          TEXT('Description 3')
     A                                      COLHDG('Description 3')
      *
      *
      *---------------------------------------------------------------------
      * Audit information
     A            PYAJOB    R               TEXT('Job name')
     A                                      COLHDG('Job name')
     A                                      REFFLD(JOJOB)
     A            PYAUSR        10          TEXT('User name')
     A                                      COLHDG('User name')
     A            PYATIM         6  0       TEXT('Action Time')
     A                                      COLHDG('Action' +
     A                                             'Time')
     A            PYARDT         7          TEXT('Action Date')
     A                                      COLHDG('Action' +
     A                                             'Date')
     A            PYAACT         1          TEXT('Action Type')
     A                                      COLHDG('Action' +
     A                                             'Type')
     A            PYRDNB         5  0       TEXT('Run day number')
     A                                      COLHDG('Run day' +
     A                                             'number')
      *
     A*=====================================================================
