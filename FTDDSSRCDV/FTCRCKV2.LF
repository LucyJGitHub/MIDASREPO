      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMSN                                                      *
/*EXI *  TEXT('Midas FT Credit Check File by DR Account')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  FTCRCKV2 - Midas FT Midas FT Credit Check File               *
      *             by DR Account Reference                           *
      *                                                               *
      *  (C) Copyright Misys IBS Ltd. 2004                            *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 13Sep11               *
      *                 PNL012             Date 16Sep05               *
      *                 ESL038   *Create   Date 09Dec2004             *
      *                                                               *
      *---------------------------------------------------------------*
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  PNL012 - Account Payments Authorisation Processing           *
      *  ESL038 - ING STP Development                                 *
      *                                                               *
      *****************************************************************
      *
     A          R @CRCKV2                   TEXT('(C) Misys IBS  +
     A                                      . 2004')
     A                                      PFILE(FTCRCKP0)
      *---------------------------------------------------------------------
     A            CRRECI                    TEXT('Record Identifier')
     A            CRPREF                    TEXT('Payment Reference')
      *  Debit Account
     A            CRBRCA                    TEXT('DR Branch')
     A            CRCNUM                    TEXT('DR Customer')
     A            CRCCY                     TEXT('DR Currency')
     A            CRACOD                    TEXT('DR Account code')
     A            CRACSQ                    TEXT('DR Account Sequence')
      *
      *---------------------------------------------------------------------
      * Balances on Authorisation
     A            CRSMAM                    TEXT('Settlement Amount')
     A            CRCLBL                    TEXT('Cleared Balance')
     A            CRODLN                    TEXT('Overdraft Line')
     A            CRAVBL                    TEXT('Available Balance')
      *
      *---------------------------------------------------------------------
      * Credit Check
     A            CRDRCK                    TEXT('Debit Check')
     A            CRCPRF                    TEXT('Credit Trans Ref')
     A            CROVRT                    TEXT('Override Taken')
      *---------------------------------------------------------------------
      *  Credit Account
     A            CRCBRC                    TEXT('CR Branch')
     A            CRCCUS                    TEXT('CR Customer')
     A            CRCCCY                    TEXT('CR Currency')
     A            CRCACD                    TEXT('CR Account code')
     A            CRCASQ                    TEXT('CR Account Sequence')
      *---------------------------------------------------------------------
      *  Debit check complete
     A            CRDCKC                    TEXT('DR Check Complete')
      *....................
      * Key fields Debit Account
     A          K CRBRCA
     A          K CRCNUM
     A          K CRCCY
     A          K CRACOD
     A          K CRACSQ
     A          K CRSMAM
      *....................
      * PNL012 - START
     A*****     S CRDRCK                    VALUES(' ')
     A          S CRDCKC                    VALUES(' ')
      * PNL012 - END
     A*=====================================================================
