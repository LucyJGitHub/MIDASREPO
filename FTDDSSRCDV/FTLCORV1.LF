      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PF/FTLCORV1 - Midas FT Loro Correspondent Relationships      *
      *                                                               *
      *  (C) Copyright Misys IBS Ltd. 2003                            *
      *                                                               *
      *  Last Amend No. MD31937            Date 13Jan15               *
      *  Last Amend No. AR856737           Date 13Sep11               *
      *  Last Amend No. EEE058   *Create   Date 05Mar2003             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD31937  - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  EEE058 - Incoming STP                                        *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
      *
     A          R @LCORV1                   TEXT('(C) Misys IBS  +
     A                                      . 2003')
     A                                      PFILE(FTLCORP0)
      *---------------------------------------------------------------------
     A            BDBICC                    TEXT('BIC (Bank, Country, & Loc')
     A            BDBICB                    TEXT('BIC (Branch Code)')
     A            BDLCCY                    TEXT('Loro A/c ccy')
     A            BDM191                    TEXT('OUR Mt191 Ind.')
     A            BDM900                    TEXT('OUR Mt900 ind.')
     A            BDINS1                    TEXT('Institution Name 1')
     A            BDINS2                    TEXT('Institution Name 2')
     A            BDINS3                    TEXT('Institution Name 3')
     A            BDRECI                    TEXT('Record Identifier')
      *
      *---------------------------------------------------------------------
     A            BDDES1                    TEXT('Description 1')
     A            BDDES2                    TEXT('Description 2')
     A            BDDES3                    TEXT('Description 3')
      *
      *---------------------------------------------------------------------
      * Audit information
     A            BDAJOB                    TEXT('Job name')
     A            BDAUSR                    TEXT('User name')
     A            BDATIM                    TEXT('Action Time')
     A            BDARDT                    TEXT('Action Date')
     A            BDAACT                    TEXT('Action Type')
     A            BDRDNB                    TEXT('Run day number')
      *....................
      * Key fields
     A          K BDBICC
     A          K BDBICB
     A          K BDLCCY
     A*.....................................................................
     A* SELECT: Live
     A* Field    : Record Identifier
     A* Condition: Live
     A          S BDRECI                    COMP(EQ 'D')
      *
     A*=====================================================================
