      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas FT Payment Correspondent')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  FTMSCCV1 - Midas FT Payment Correspondent                    **
      *                                                               *
      *  (C) Copyright Misys IBS Ltd. 2004                            *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 13Sep11               *
      *                 PNL014             Date 07Dec2005             *
      *                 EEE065             Date 01Jan2004             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  PNL014 - Time Sensitive Routing                              *
      *  EEE065 - FT Payment Routing                                  *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
      *
     A          R @MSCCV1                   TEXT('(C) Misys IBS  +
     A                                      . 2004')
     A                                      PFILE(FTMSCCP0)
      *---------------------------------------------------------------------
      *
     A            MCBICC                    TEXT('BIC (Bank, Country, & Loc')
     A            MCBICB                    TEXT('BIC (Branch Code)')
     A            MCPCCY                    TEXT('Payment Currency')
      *
     A            MCRECI                    TEXT('Record Identifier')
      *
     A            MCCTYP                    TEXT('Correspondent Type')
     A            MCCTRY                    TEXT('Country Location')
     A            MCPCOR                    TEXT('Preferential Correspondent')
     A            MCPNBR                    TEXT('Preferential Number Routed')
     A            MCS71A                    TEXT('Std Quotation of 71A')
     A            MCDACR                    TEXT('Direct Account reference')
      *---------------------------------------------------------------------
      * Direct account in Midas
     A            MCDBRC                    TEXT('Direct Account Branch')
     A            MCDCST                    TEXT('Direct Account Customer')
     A            MCDCCY                    TEXT('Direct Account Currency')
     A            MCDACD                    TEXT('Direct Account Account code')
     A            MCDASQ                    TEXT('Direct Account Account Seq.')
      *
      *---------------------------------------------------------------------
     A            MCLVPA                    TEXT('Low Value Payment Agreement')
     A            MCLVPT                    TEXT('Low Value Threshold')
     A            MCQ71A                    TEXT('Quotation of 71A')
     A            MCPLNB                    TEXT('Preferential LVP Nbr Routed')
     A            MCSCHR                    TEXT('Standard - Charge %')
     A            MCSCHL                    TEXT('Standard - Min Fee')
     A            MCSCHH                    TEXT('Standard - Max Fee')
     A            MCSCHC                    TEXT('Standard - Charge code')
     A            MCLCHR                    TEXT('LVPA - Charge %')
     A            MCLCHL                    TEXT('LVPA - Min Fee')
     A            MCLCHH                    TEXT('LVPA - Max Fee')
     A            MCLCHC                    TEXT('LVPA - Charge code')
      *---------------------------------------------------------------------
     A            MCREGP                    TEXT('Regulated Payment Agreement')
     A            MCREGT                    TEXT('Regulated Threshold')
     A            MCR71A                    TEXT('Reg Quotation of 71A')
     A            MCPRNB                    TEXT('Preferential Reg Nbr Routed')
     A            MCDES1                    TEXT('Description 1')
     A            MCDES2                    TEXT('Description 2')
     A            MCDES3                    TEXT('Description 3')
      *---------------------------------------------------------------------
     A            MCIDAB                    TEXT('Inhibit Direct A/c BEN')
     A            MCIDAS                    TEXT('Inhibit Direct A/c SHA')
     A            MCIDAO                    TEXT('Inhibit Direct A/c OUR')
     A            MCSPCR                    TEXT('Split Preferential Cor')
     A            MCSPTH                    TEXT('Split Threshold')
     A            MCSPBN                    TEXT('Split Pref BEN Number Routed')
     A            MCSPSN                    TEXT('Split Pref SHA Number Routed')
     A            MCSPON                    TEXT('Split Pref OUR Number Routed')
     A            MCBPAL                    TEXT('BEN Payment Amount - Min')
     A            MCBPAH                    TEXT('BEN Payment Amount - Max')
     A            MCSPAL                    TEXT('SHA Payment Amount - Min')
     A            MCSPAH                    TEXT('SHA Payment Amount - Max')
     A            MCOPAL                    TEXT('OUR Payment Amount - Min')
     A            MCOPAH                    TEXT('OUR Payment Amount - Max')
     A            MCSTMT                    TEXT('Settlement Method')
     A            MCPVAS                    TEXT('Payment Value Added Service')
     A            MCOTHR                    TEXT('Override Threshold Amount')
     A            MCOSTM                    TEXT('Override Settlement Method')
     A            MCOVAS                    TEXT('Override Value Added Service')
     A            MCO71A                    TEXT('Override Quotation of 71A')
     A            MCOACR                    TEXT('Override Account reference')
      *---------------------------------------------------------------------
      * Direct account in Midas
     A            MCOBRC                    TEXT('Override Account Branch')
     A            MCOCST                    TEXT('Override Account Customer')
     A            MCOCCY                    TEXT('Override Account Currency')
     A            MCOACD                    TEXT('Override Account Account cde')
     A            MCOASQ                    TEXT('Override Account Account Seq')
      *
      *---------------------------------------------------------------------
      * Cover Replacement Information
     A            MCR910                    TEXT('Replace 202C with 910')
      *
      *---------------------------------------------------------------------
      * Active Dates
      *
     A            MCSTAT                    TEXT('Status' )
     A            MCSTDT                    TEXT('Start Date')
     A            MCENDT                    TEXT('End Date')
      *
      *---------------------------------------------------------------------
      * Audit information
     A            MCAJOB                    TEXT('Job name')
     A            MCAUSR                    TEXT('User name')
     A            MCATIM                    TEXT('Action Time')
     A            MCARDT                    TEXT('Action Date')
     A            MCAACT                    TEXT('Action Type')
     A            MCRDNB                    TEXT('Run day number')
      * PNL014 Start
      *---------------------------------------------------------------------
      * Time Sensitive Routing
     A            MCTSDO                    TEXT('TSP Date Offset')
     A            MCTSCT                    TEXT('TSP Cut Off Time')
     A            MCTSCD                    TEXT('TSP Cut Off Day Offset')
     A            MCTSCK                    TEXT('TSP Cut Off Check')
     A            MCTSHC                    TEXT('TSP Cut Off Holiday Cal. Ccy')
     A            MCTSHL                    TEXT('TSP Cut Off Holiday Cal. Loc')
     A            MCTSFC                    TEXT('TSP Fail Condition')
     A            MCTSIS                    TEXT('TSP Iteration Sequence')
     A            MCRREF                    TEXT('Route Reference')
      *
      * PNL014 End
      *---------------------------------------------------------------------
      *....................
      * Key fields
     A          K MCBICC
     A          K MCBICB
     A          K MCPCCY
     A*.....................................................................
     A* SELECT: Live
     A* Field    : Record Identifier
     A* Condition: Live
     A          S MCRECI                    COMP(EQ 'D')
      *
      *****************************************************************
