      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas FT Payment Correspondent Pay Routes')            *
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  LF/FTMSCPV2 - Midas FT Payment Correspondent Pay Routes      *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD058190           Date 25May21               *
      *  Prev Amend No. MD031937           Date 13Jan15               *
      *                 AR856737           Date 13Sep11               *
      *                 PNL014 *Create     Date 07Dec2005             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058190 - FT0300 Compile Error Due to Missing Files.        *
      *             (Recompile)                                       *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  PNL014 - Time Sensitive Routing                              *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
      *
     A          R @MSCPV2                   TEXT('(c) Finastra International +
     A                                      Limited 2004')
     A                                      PFILE(FTMSCPP0)
      *---------------------------------------------------------------------
      *
     A            MPBICC                    TEXT('BIC (Bank, Country, & Loc')
     A            MPBICB                    TEXT('BIC (Branch Code)')
     A            MPPCCY                    TEXT('Payment Currency')
      *
     A            MPRECI                    TEXT('Record Identifier')
      *
      *---------------------------------------------------------------------
      * Correspondent Payment Pay Route
     A            MPCBIC                    TEXT('Pay Through BIC')
     A            MPCBIB                    TEXT('Pay Through BIC branch')
     A            MPRCAU                    TEXT('Receive a/c Used')
     A            MPRCAC                    TEXT('Receive a/c Quoted')
      *---------------------------------------------------------------------
     A            MPC53C                    TEXT('Senders Correspondent BIC')
     A            MPC53B                    TEXT('Senders Correspondent BIC Br')
     A            MPC53A                    TEXT('Senders Correspondent A/c')
     A            MPC53R                    TEXT('Cover Required')
     A            MPSERP                    TEXT('Serial Payment')
     A            MPSLVP                    TEXT('Serial LVP')
     A            MPSREG                    TEXT('Serial REG')
     A            MPSOUR                    TEXT('Serial OUR')
     A            MPC54C                    TEXT('Receivers Correspondent BIC')
     A            MPC54B                    TEXT('Receivers Corr BIC Br')
     A            MPC56C                    TEXT('Intermediary BIC')
     A            MPC56B                    TEXT('Intermediary BIC Br')
     A            MPSCHR                    TEXT('Standard - Charge %')
     A            MPSCHL                    TEXT('Standard - Min Fee')
     A            MPSCHH                    TEXT('Standard - Max Fee')
     A            MPSCHC                    TEXT('Standard - Charge code')
     A            MPDES1                    TEXT('Description 1')
     A            MPDES2                    TEXT('Description 2')
     A            MPDES3                    TEXT('Description 3')
     A            MPPCOR                    TEXT('Preferential Correspondent')
     A            MPPPCT                    TEXT('Preferential Percentage')
     A            MPPNBR                    TEXT('Preferential Number Routed')
     A            MPPBCR                    TEXT('Preferential Correspond BEN')
     A            MPPBPT                    TEXT('Preferential Percentage BEN')
     A            MPPBNB                    TEXT('Preferential Number Rout BEN')
     A            MPPSCR                    TEXT('Preferential Correspond SHA')
     A            MPPSPT                    TEXT('Preferential Percentage SHA')
     A            MPPSNB                    TEXT('Preferential Number Rout SHA')
     A            MPPOCR                    TEXT('Preferential Correspond OUR')
     A            MPPOPT                    TEXT('Preferential Percentage OUR')
     A            MPPONB                    TEXT('Preferential Number Rout OUR')
     A            MPPREG                    TEXT('Preferential Regulated Cor')
     A            MPPRPC                    TEXT('Preferential Regulated %')
     A            MPPRNB                    TEXT('Preferential Reg Nbr Routed')
     A            MPPLVP                    TEXT('Preferential LVP Cor')
     A            MPPLPC                    TEXT('Preferential LVP %')
     A            MPPLNB                    TEXT('Preferential LVP Nbr Routed')
      *---------------------------------------------------------------------
     A            MPBENR                    TEXT('Route for BEN')
     A            MPBENL                    TEXT('BEN Amount - Min')
     A            MPBENH                    TEXT('BEN Amount - Max')
     A            MPSHAR                    TEXT('Route for SHA')
     A            MPSHAL                    TEXT('SHA Amount - Min')
     A            MPSHAH                    TEXT('SHA Amount - Max')
     A            MPOURR                    TEXT('Route for OUR')
     A            MPOURL                    TEXT('OUR Amount - Min')
     A            MPOURH                    TEXT('OUR Amount - Max')
      *
      *---------------------------------------------------------------------
      * Cover Replacement Information
     A            MPR910                    TEXT('Replace 202C with 910')
     A            MPSTMT                    TEXT('Settlement Method')
      *---------------------------------------------------------------------
      * Active Dates
      *
     A            MPSTAT                    TEXT('Statu' )
     A            MPSTDT                    TEXT('Start Date')
     A            MPENDT                    TEXT('End Date')
      *---------------------------------------------------------------------
      * Audit information
     A            MPAJOB                    TEXT('Job name')
     A            MPAUSR                    TEXT('User name')
     A            MPATIM                    TEXT('Action Time')
     A            MPARDT                    TEXT('Action Date')
     A            MPAACT                    TEXT('Action Type')
     A            MPRDNB                    TEXT('Run day number')
      *
      *---------------------------------------------------------------------
      *
     A            MPFBIC         1          TEXT('Foreign Banks?')
      *
     A            MPRHOB                    TEXT('Route via Head Office BIC')
      *
      *---------------------------------------------------------------------
      * Time Sensitive Routing
     A            MPRTSQ                    TEXT('Routing Sequence')
     A            MPTSDO                    TEXT('TSP Date Offset')
     A            MPTSCT                    TEXT('TSP Cut Off Time')
     A            MPTSCD                    TEXT('TSP Cut Off Day Offset')
     A            MPTSCK                    TEXT('TSP Cut Off Check')
     A            MPTSHC                    TEXT('TSP Cut Off Holiday Cal. Ccy')
     A            MPTSHL                    TEXT('TSP Cut Off Holiday Cal. Loc')
     A            MPTSFC                    TEXT('TSP Fail Condition')
     A            MPTSIS                    TEXT('TSP Iteration Sequence')
     A            MPRREF                    TEXT('Route Reference')
      *
      *---------------------------------------------------------------------
      *
      *---------------------------------------------------------------------
      *
      *....................
      * Key fields
     A          K MPBICC
     A          K MPBICB
     A          K MPPCCY
     A          K MPRTSQ
     A          K MPCBIC
     A          K MPCBIB
     A*.....................................................................
     A* SELECT: Live
     A* Field    : Record Identifier
     A* Condition: Live
     A          S MPRECI                    COMP(EQ 'D')
      *
      *---------------------------------------------------------------------
      *
