      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas FT Payment Correspondent General Routes')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  FTMSGRV1 - Midas FT Payment Correspondent General Routes     *
      *                                                               *
      *  (C) Copyright Misys IBS Ltd. 2005                            *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 13Sep11               *
      *                 PNL015             Date 30Nov2005             *
      *                 EEE065 *Create     Date 11Jan2005             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  PNL015 - Redirection to head office BIC                      *
      *  EEE065 - General Routes                                      *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
      *
     A          R @MSGRV1                   TEXT('(C) Misys IBS  +
     A                                      . 2005')
     A                                      PFILE(FTMSGRP0)
      *---------------------------------------------------------------------
     A            MGBICC                    TEXT('BIC (Bank, Country, & Loc')
     A            MGBICB                    TEXT('BIC (Branch Code)')
     A            MGPCCY                    TEXT('Payment Currency')
      *
     A            MGRECI                    TEXT('Record Identifier')
      *
      *---------------------------------------------------------------------
      * Correspondent General Pay Route
     A            MGCBIC                    TEXT('Pay Through BIC')
     A            MGCBIB                    TEXT('Pay Through BIC branch')
      *
     A            MGRCAU                    TEXT('Receive a/c Used')
     A            MGRCAC                    TEXT('Receive a/c Quoted')
      *
      *---------------------------------------------------------------------
      * Main BIC details
      *
     A            MGCLRC                    TEXT('Clearer Correspondent BIC')
     A            MGCLRB                    TEXT('Clearer Correspondent BIC Br')
     A            MGUI56                    TEXT('Use in Field 56')
      *
      *---------------------------------------------------------------------
      *
     A            MGCREG                    TEXT('Clearing Region')
      *
     A            MGCTY1                    TEXT('Clearing Country 1')
     A            MGCTY2                    TEXT('Clearing Country 2')
     A            MGCTY3                    TEXT('Clearing Country 3')
     A            MGCTY4                    TEXT('Clearing Country 4')
     A            MGCTY5                    TEXT('Clearing Country 5')
      *
     A            MGCLID                    TEXT('Clearing System Id.')
      *
      *---------------------------------------------------------------------
     A            MGPROT                    TEXT('Preferential Route')
      *
      *---------------------------------------------------------------------
     A            MGBENR                    TEXT('Route for BEN')
     A            MGBENL                    TEXT('BEN Amount - Min')
     A            MGBENH                    TEXT('BEN Amount - Max')
     A            MGSHAR                    TEXT('Route for SHA')
     A            MGSHAL                    TEXT('SHA Amount - Min')
     A            MGSHAH                    TEXT('SHA Amount - Max')
     A            MGOURR                    TEXT('Route for OUR')
     A            MGOURL                    TEXT('OUR Amount - Min')
     A            MGOURH                    TEXT('OUR Amount - Max')
      *---------------------------------------------------------------------
     A            MGDES1                    TEXT('Description 1')
     A            MGDES2                    TEXT('Description 2')
     A            MGDES3                    TEXT('Description 3')
      *
      *---------------------------------------------------------------------
      * Active Dates
      *
     A            MGSTAT                    TEXT('Status' )
     A            MGSTDT                    TEXT('Start Date')
     A            MGENDT                    TEXT('End Date')
      *
      *---------------------------------------------------------------------
      * Audit information
     A            MGAJOB                    TEXT('Job name')
     A            MGAUSR                    TEXT('User name')
     A            MGATIM                    TEXT('Action Time')
     A            MGARDT                    TEXT('Action Date')
     A            MGAACT                    TEXT('Action Type')
     A            MGRDNB                    TEXT('Run day number')
      * PNL015 Start
     A            MGEX54                    TEXT('Exclude from 54')
     A            MGHOBC                    TEXT('Head Office BIC')
      * PNL015 End
      *....................
      * Key fields
     A          K MGBICC
     A          K MGBICB
     A          K MGPCCY
     A          K MGCBIC
     A          K MGCBIB
     A*.....................................................................
     A* SELECT: Live
     A* Field    : Record Identifier
     A* Condition: Live
     A          S MGRECI                    COMP(EQ 'D')
      *
     A*=====================================================================
