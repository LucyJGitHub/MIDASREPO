      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas FT SWIFT Correspondent STP Req.')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  FTMSTPV1 - Midas FT SWIFT Correspondent STP Req.             *
      *                                                               *
      *  (C) Copyright Misys IBS Ltd. 2004                            *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 13Sep11               *
      *                 EEE065             Date 01Jan2004             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  EEE065 - FT Payment Routing                                  *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
      *
     A          R @MSTPV1                   TEXT('(C) Misys IBS  +
     A                                      . 2004')
     A                                      PFILE(FTMSTPP0)
      *---------------------------------------------------------------------
     A            MSBICC                    TEXT('BIC (Bank, Country, & Loc')
     A            MSBICB                    TEXT('BIC (Branch Code)')
     A            MSLVPA                    TEXT('Low Value Payment Agreement')
     A            MSREGP                    TEXT('Regulated Payment Agreement')
      *
     A            MSRECI                    TEXT('Record Identifier')
     A            MS103P                    TEXT('SWIFT 103 + Req.')
     A            MSDES1                    TEXT('Description 1')
     A            MSDES2                    TEXT('Description 2')
     A            MSDES3                    TEXT('Description 3')
      *---------------------------------------------------------------------
      * Audit information
     A            MSAJOB                    TEXT('Job name')
     A            MSAUSR                    TEXT('User name')
     A            MSATIM                    TEXT('Action Time')
     A            MSARDT                    TEXT('Action Date')
     A            MSAACT                    TEXT('Action Type')
     A            MSRDNB                    TEXT('Run day number')
      *....................
      * Key fields
     A          K MSBICC
     A          K MSBICB
     A          K MSLVPA
     A          K MSREGP
     A*.....................................................................
     A* SELECT: Live
     A* Field    : Record Identifier
     A* Condition: Live
     A          S MSRECI                    COMP(EQ 'D')
     A*=====================================================================
