      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas FT SWIFT Keys')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  FTSWKYV1 - FT SWIFT Keys                                     *
      *                                                               *
      *  (C) Copyright Misys IBS Ltd. 2004                            *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 13Sep11               *
      *                 ESL038             Date 22Feb2005             *
      *                 EEE065   *CREATE   Date 01Jan2004             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  ESL038 - ING STP Development                                 *
      *  EEE065 - FT SWIFT Key                                        *
      *                                                               *
      *****************************************************************
     A          R @SWKYV1                   TEXT('(C) Misys IBS  +
     A                                      . 2004')
     A                                      PFILE(FTSWKYP0)
      *---------------------------------------------------------------------
     A            SWOKEY                    TEXT('Bank SWIFT ID')
     A                                      RENAME(BANKID)
     A            SWCKEY                    TEXT('Branch id')
     A                                      RENAME(BANKKEY)
     A            SWAKEY                    TEXT('0=Has key, 5=No key')
     A                                      RENAME(KEYFLAG)
      * ESL038 Start
     A            SWRECI                    TEXT('Record Identifier')
      *
      *---------------------------------------------------------------------
      * Active Dates
      *
     A            SWSTAT         1          TEXT('Status' )
     A                                      COLHDG('Status')
     A            SWSTDT         5S 0       TEXT('Start Date')
     A                                      COLHDG('Start' +
     A                                             'Date')
     A            SWENDT         5S 0       TEXT('End Date')
     A                                      COLHDG('End' +
     A                                             'Date')
      *---------------------------------------------------------------------
      * Audit information
     A            SWAJOB                    TEXT('Job name')
     A            SWAUSR                    TEXT('User name')
     A            SWATIM                    TEXT('Action Time')
     A            SWARDT                    TEXT('Action Date')
     A            SWAACT                    TEXT('Action Type')
     A            SWRDNB                    TEXT('Run day number')
      *---------------------------------------------------------------------
      * ESL038 End
      *....................
      * Key fields
     A          K SWOKEY
     A          K SWCKEY
      * ESL038 Start
     A*.....................................................................
     A* SELECT: Live
     A* Field    : Record Identifier
     A* Condition: Live
     A          S SWRECI                    COMP(NE '*')
      *
      * ESL038 End
     A*=====================================================================
