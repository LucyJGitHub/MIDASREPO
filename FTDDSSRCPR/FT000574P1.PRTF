      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas FT Exchange rate spread : list')
/*EXT *  PRTF01                                                       *
      *****************************************************************
      *                                                               *
      *  Midas - FUNDS TRANSFER MODULE                                *
      *                                                               *
      *  PRTF/FT000574P1  Exchange Rate Spread List                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend  No. CFT009 *CREATE    Date  25May00              *
      *  Prev Amend  No. xxxxxx            DATE  ddmmmyy              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *     CFT009 - Funds Transfer Fees and Charges                  *
      *                                                               *
      *****************************************************************
      *
      **
      *
     A                                      REF(*LIBL/MFRFL)
      *
      ** Header Report Format
      *
     A          R HDR574                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
     A                                  1  6'REFERENCE FT000574P1'
     A            BJURPT    R           1 37REFFLD(URPT)
     A                                  1101'DATE'
     A            BJMRDT    R           1106REFFLD(MRDT)
     A                                  1119'PAGE'
     A            PAGENO         3  0   1124EDTCDE(Z)
     A                                  2101'TIME'
     A                                  2106TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 46'EXCHANGE RATE SPREAD : LIST'
     A                                  4 46'---------------------------'
     A                                  6  3'Exchange Rate Spread Code :'
     A            WSPRCOD   R           6 31REFFLD(DBFL)
     A                                  6 37'Description'
     A            WXRSDSC   R           6 49REFFLD(RPNA)
     A                                  7  3'Parent Spread Code  . . . :'
     A            WPNTDCD   R           7 31REFFLD(DBFL)
     A            WPXRSDSC  R           7 49REFFLD(RPNA)
      *
      ** Report Titles (Line 1)
      *
     A          R DTL574A                   SPACEB(2)
     A                                     2'Currency'
     A                                    14'Type'
     A                                    30'Buy Spread'
     A                                    52'Buy Spread'
     A                                    70'Sign'
     A                                    86'Sell Spread'
     A                                   109'Sell Spread'
     A                                   127'Sign'
      *
      ** Report Titles (Line 2)
      *
     A          R DTL574B                   SPACEB(1) SPACEA(1)
     A                                     4'Code'
     A                                    30'Percentage'
     A                                    56'Points'
     A                                    87'Percentage'
     A                                   114'Points'
      *
      ** Manual Exchange Rate Spread Items
      *
     A          R MAN574                    SPACEB(1)
     A            WXRSCCY   R              4REFFLD(BCCY)
     A                                    12'Manual'
     A            WMANBPC       16        24
     A            WMANBSP       16        46
     A            WMANBSN   R             71REFFLD(CGAS)
     A            WMANSPC       16        81
     A            WMANSSP       16       104
     A            WMANSSN   R            128REFFLD(CGAS)
      *
      ** STP Exchange Rate Spread Items
      *
     A          R STP574                    SPACEB(1)
     A                                    15'STP'
     A            WSTPBPC       16        24
     A            WSTPBSP       16        46
     A            WSTPBSN   R             71REFFLD(CGAS)
     A            WSTPSPC       16        81
     A            WSTPSSP       16       104
     A            WSTPSSN   R            128REFFLD(CGAS)
      *
      ** Trailer Record (End of Report)
      *
     A          R NODETL                    SPACEB(4)
     A                                    30'*** No Details To Report ***'
     A          R TRL574                    SPACEB(2)
     A                                    56'*** END OF REPORT ***'
