     A*****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas FT Payment Charge Defaults : List')
/*EXT *  PRTF01                                                       *
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PRTF/FT000575P1 - Payment Charge Defaults : List             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CFT009 *CREATE     Date 05Jun00               *
      *  Prev Amend No. xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFT009   Funds Transfer Fees and Charges                     *
      *                                                               *
      *****************************************************************
     A                                      REF(*LIBL/MFRFL)
     A*
      *
      **  Main header details
      *
     A          R HDR575                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
     A                                  1  6'REFERENCE FT000575P1'
     A            BJURPT    R           1 37REFFLD(URPT)
     A                                  1101'DATE'
     A            BJMRDT    R           1106REFFLD(MRDT)
     A                                  1119'PAGE'
     A            PAGNUM         3S 0   1124EDTCDE(Z)
     A                                  2101'TIME'
     A                                  2106TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 60'PAYMENT CHARGE DEFAULT LIST'
     A                                  4 60'---------------------------'
     A                                  6  1'Branch :'
     A            WBRANCH   R           6 10REFFLD(BCCY)
     A                                  6 14'Customer  :'
     A            WCUSTOM   R           6 26REFFLD(ACCK)
     A                                  6 33'Transaction'
     A                                  6 45'Type :'
     A            WTRNTYP   R           6 52REFFLD(CKEN)
     A                                  6 56'Transaction Sub-Type :'
     A            WTRSBTY   R           6 80REFFLD(CKEN)
     A                                  7  1'Customer Group Code    :'
     A            WCSGRCD   R           7 26REFFLD(ATST)
     A                                  7 33'Currency          :'
     A            WCURRCY   R           7 52REFFLD(BCCY)
     A                                  8  1'Description            :'
     A            WPDESCR   R           8 26REFFLD(BNC1)
      *
      * Customer Header 1
      *
     A          R CUSH1                     SPACEB(1)
     A                                     2'Customer'
      *
      * Customer Header 2
      *
     A          R CUSH2                     SPACEB(1)
     A                                     2'Number  Shortname   Name          -
     A                                                             Address'
      *
      * Customer Details Detail
      *
     A          R CUSDET                    SPACEB(1)
     A            BBCUST         6         2
     A            BBCSSN        10        10
     A            BBCNA1        35        22
     A            BBCNA2        35        59
      *
      * Charge Header 1
      *
     A          R CHGH1                     SPACEB(2)
     A                                     1'Payment Date Offset    :'
     A                                    40'Swift Standard Charge  :'
     A                                    78'Swift Priority         :'
      *
      * Charge Detail 1
      *
     A          R CHGD1
     A            WPDOFCD   R             27REFFLD(DBFL)
     A            WSWSTCH   R             65REFFLD(DBFL)
     A            WSWPRCH   R            103REFFLD(DBFL)
      *
      * Charge Header 2
      *
     A          R CHGH2                     SPACEB(2)
     A                                     1'Miscellaneous charge   :'
     A                                    40'Correspondents Charge  :'
     A                                    78'Cheque Charge          :'
      *
      * Charge Detail 2
      *
     A          R CHGD2
     A            WMISCHG   R             27REFFLD(DBFL)
     A            WCORCHG   R             65REFFLD(DBFL)
     A            WCHCHRG   R            103REFFLD(DBFL)
      *
      * Charge Header 3
      *
     A          R CHGH3                     SPACEB(2)
     A                                     1'Miscellaneous charge 1 :'
     A                                    40'FX Change Commission   :'
     A                                    78'Telex Charge           :'
      *
      * Charge Detail 3
      *
     A          R CHGD3
     A            WMISCH1   R             27REFFLD(DBFL)
     A            WFXCHCC   R             65REFFLD(DBFL)
     A            WTXCHCD   R            103REFFLD(DBFL)
      *
      * Charge Header 4
      *
     A          R CHGH4                     SPACEB(2)
     A                                     1'Miscellaneous charge 2 :'
     A                                    40'Costs abroad charge    :'
     A                                    78'Advice Charge          :'
      *
      * Charge Detail 4
      *
     A          R CHGD4
     A            WMISCH2   R             27REFFLD(DBFL)
     A            WCABCHC   R             65REFFLD(DBFL)
     A            WADVCHG   R            103REFFLD(DBFL)
      *
      * Charge Header 5
      *
     A          R CHGH5                     SPACEB(2)
     A                                     1'Miscellaneous charge 3 :'
     A                                    40'Transfer Commission    :'
     A                                    78'Telephone              :'
      *
      * Charge Detail 5
      *
     A          R CHGD5
     A            WMISCH3   R             27REFFLD(DBFL)
     A            WTRCMCH   R             65REFFLD(DBFL)
     A            WTELCHG   R            103REFFLD(DBFL)
      *
      * Charge Header 6
      *
     A          R CHGH6                     SPACEB(2)
     A                                     1'Miscellaneous charge 4 :'
     A                                    40'Collection Charge      :'
     A                                    78'FX spread set          :'
      *
      * Charge Detail 6
      *
     A          R CHGD6
     A            WMISCH4   R             27REFFLD(DBFL)
     A            WCOLCHG   R             65REFFLD(DBFL)
     A            WFXSPST   R            103REFFLD(DBFL)
      *
      * Charge Header 7
      *
     A          R CHGH7                     SPACEB(2)
     A                                     1'Miscellaneous charge 5 :'
     A                                    40'Deductions Charge      :'
      *
      * Charge Detail 7
      *
     A          R CHGD7
     A            WMISCH5   R             27REFFLD(DBFL)
     A            WDEDCHG   R             65REFFLD(DBFL)
      *
      * Report Trailer
      *
     A          R TRAILPY                   SPACEB(3)
     A                                    51'***  END OF REPORT  ***'
     A          R NODETL                    SPACEB(4)
     A                                    30'*** No Details To Report ***'
