      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas FT Payment Date Offset Code Report')
/*EXT *  PRTF01                                                       *
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PRTF/FT000576P1 - Payment Date Offset Codes                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CFT009  *CREATE    Date 07Jun00               *
      *                 XXXXXX             Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFT009 - Funds Transfer Fees and Charges                     *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE FT000576P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'PAYMENT DATE OFFSET CODE +
     A                                       : LIST'
      *
     A                                      SPACEA(1)
     A                                    46'-------------------------+
     A                                       ------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     6'Offset'
     A                                    13'Description'
     A                                    46'Local'
     A                                    58'Local'
     A                                    69'Before Cut-off'
     A                                    85'Before Cut-off'
     A                                   102'After Cut-off'
     A                                   119'After Cut-off'
     A                                      SPACEA(1)
     A                                     6'Code'
     A                                    44'Start Time'
     A                                    55'Cut-off Time'
     A                                    68'Set. Date Offset'
     A                                    85'Pay Date Offset'
     A                                   101'Set. Date Offset'
     A                                   118'Pay Date Offset'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            POFCDE         5A        6
     A            PDESCP        30A       13
     A            PLSTTM         8        45
     A            PLCOTM         8        57
     A            PBCOSO         6  0     72EDTCDE(Z)
     A            PBCOPO         6  0     89EDTCDE(Z)
     A            PACOSO         6  0    105EDTCDE(Z)
     A            PACOPO         6  0    122EDTCDE(Z)
      *
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  No Details     *
      *-------------------*
      *
     A          R NODETL                    SPACEB(4)
     A                                    30'*** No Details To Report ***'
