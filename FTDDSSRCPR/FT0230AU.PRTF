      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FT NON-AUTHORISED PAYMENTS REPORT')
     A*****************************************************************
     A*                                                               *
     F*  Midas - FUNDS TRANSFER MODULE
     A*                                                               *
     A*         PRTF/FT0230AU NON-AUTHORISED PAYMENTS - RUN CONTROLS  *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01117             Date 19Aug91               *
     A*                                                               *
     A*****************************************************************
      *  MD046248 - Finastra Rebranding                               *
     A*     S01117 - SOURCE BASED ON AND REPLACING FT0230P2 FOR REL10 *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R FT230AUH                  SKIPB(3)
     A
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A
     A                                     6'REFERENCE FT0230AU'
     A            TITL      R             37
     A                                   100'DATE'
     A            RUNA      R            105
     A                                   119'PAGE'
     A            PPAGE     R            124EDTCDE(4)  REFFLD(PAGE)
     A                                      SPACEA(1)
     A*
     A                                    46'NON-AUTHORISED PAYMENTS'
     A                                      SPACEB(1)
     A                                    71'- RUN CONTROLS'
     A                                    46'-----------------------'
     A                                      SPACEB(1)
     A                                    69'--'
     A                                    71'--------------'
     A*
     A  10N13N14                           6'BOOKING BRANCH'
     A                                      SPACEB(2)
     A  10N13 14                           6'ORIGINATING BRANCH'
     A                                      SPACEB(2)
     A  10N13     SBRA      R        O    26REFFLD(BRCA)
     A  10N13     PBRNM     R        O    +2REFFLD(BRNM)
     A*
     A                                    91'CALCULATED'
     A                                      SKIPB(11)
     A                                     5'FILE'
     A                                      SPACEB(2)
     A                                    82'NUMBER'
     A          R FT230AUD                  SPACEB(3)
     A                                     5'NON-AUTHORISED'
     A    N14                             27'- LF/NONAUBB'
     A  10 14                             27'- LF/NONAUOB'
     A                                    43'INPUT'
     A            RECCNT         5  0     82EDTCDE(1)
     A*
     A          R AUNREC                    SKIPB(51)
     A                                    53'** NO DETAILS TO REPORT **'
     A*
     A          R ENDAU                     SKIPB(53)
     A                                    56'*** END OF REPORT ***'
     A*
     A          R AUDBER                    SPACEB(3)
     A                                    17'PROGRAM'
     A            PPGM          10        25
     A                                    36'TERMINATED AT POINT'
     A            PDBASE         3  0     56
     A                                    59'- DATABASE IN ERROR - FILE'
     A            PFILE          8        86
     A                                    95'KEY'
     A            PKEY          29        99
     A*
