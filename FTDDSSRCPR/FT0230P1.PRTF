      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FT Non-Authorised Payments Report')
     A*****************************************************************
     A*                                                               *
     F*  Midas - FUNDS TRANSFER MODULE
     A*                                                               *
     A*         PRTF/FT0230P1 NON-AUTHORISED PAYMENTS                 *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 056803             Date 16Jun93               *
      *                 E32877             Date 23May91               *
     A*                 S01117     DATE 11JAN91                       *
     A*                 E23055     DATE 02AUG90                       *
     A*                                                               *
     A*****************************************************************
      *  MD046248 - Finastra Rebranding                               *
     A*  056803 - Continuation of fix 034111                          *
     A*           If payment MUST be authorised prior to COB then flag*
     A*           with '**'. i.e. when value date is < or = rundate.  *
     A*  E32877 - REPORT SHOWING MISLEADING INFORMATION               *
     A*  S01117 - RELEASE 10 MULTIBRANCHING CHANGES                   *
     A*  E23055 - PREVENT  OVERLAP OF REPORT FIELDS                   *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*                                                    /S01117/
     A                                      REF(MFRFL)
     A*                                                    /S01117/
     A          R FT0230H                   SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A
     A*                                                    /S01117/
     A*************************************6'REFERENCE FT0230'
     A                                     6'REFERENCE FT0230P1'
     A*                                                    /S01117/
     A            TITL          53        37
     A                                   100'DATE'
     A            RUNA           7       105
     A                                   119'PAGE'
     A            PAGE           5  0    124EDTCDE(4)
     A                                      SPACEA(1)
     A*
     A                                    53'NON-AUTHORISED PAYMENTS'
     A                                      SPACEB(1)
     A                                    53'-----------------------'
     A                                      SPACEB(1)
     A*
     A                                     6'REFERENCE'
     A                                      SPACEB(1)
     A                                    23'CURRENCY'
     A                                    47'AMOUNT'
     A*
     A                                    59'CURRENCY'
     A                                    83'AMOUNT'
     A                                      SPACEA(1)
     A*
     A*                                                    /S01117/
     A          R P1BRCH                    SPACEB(2)
     A  10N14                             38'BOOKING BRANCH'
     A  10 14                             38'ORIGINATING BRANCH'
     A  10        SBRA      R        O    59REFFLD(BRCA)
     A  10        PBRNM     R        O    +2REFFLD(BRNM)
     A  10N14                             38'--------------'
     A  10N14                               SPACEB(1)
     A  10 14                             38'------------------'
     A  10 14                               SPACEB(1)
     A*                                                    /S01117/
     A          R FT0230D1
     A                                     1' '
     A                                      SPACEB(2)
     A*
     A  50                                 3'OUTGOING PAYMENTS      '
     A  50                                40'SETTLE AMOUNT'
     A  50                                79'PAY AMOUNT'
     A*                                                    /S01117/
     A  50 10 14                         100'BOOKING BRANCH'
     A  50
     AA 10 11N14                         100'ORIGINATING BRANCH'
     A*                                                    /S01117/
     A  51                                 3'INCOMING PAYMENTS      '
     A  51                                43'PAY AMOUNT'
     A  51                                76'SETTLE AMOUNT'
     A*                                                    /S01117/
     A  51 10 14                         100'BOOKING BRANCH'
     A  51
     AA 10 11N14                         100'ORIGINATING BRANCH'
     A*                                                    /S01117/
     A  52                                 3'CHEQUES TO BE PAID     '
     A  52                                37'TOTAL REMITTANCE'
     A  52                                76'CHEQUE AMOUNT'
     A*                                                    /S01117/
     A  52 10 14                         100'BOOKING BRANCH'
     A  52
     AA 10 11N14                         100'ORIGINATING BRANCH'
     A*                                                    /S01117/
     A  53                                 3'CHEQUES TO BE COLLECTED'
     A  53                                41'TOTAL AMOUNT'
     A  53                                76'CREDIT AMOUNT'
     A*                                                    /S01117/
     A  53 10 14                         100'BOOKING BRANCH'
     A  53
     AA 10 11N14                         100'ORIGINATING BRANCH'
     A*                                                    /S01117/
     A  54                                 3'NOSTRO TRANSFERS       '
     A  54                                38'TRANSFER AMOUNT'
     A*                                                    /E23055/
     A                                    80' '
     A*                                                    /E23055/
     A*                                                    /S01117/
     A  54 10 14                         100'BOOKING BRANCH'
     A  54
     AA 10 11N14                         100'ORIGINATING BRANCH'
     A**************************************SPACEA(1)
     A*                                                    /S01117/
     A          R FT0230D2
     A                                     1' '
     A                                      SPACEB(1)
     A*
     A* Start 056803
     A            AUTFLG         2         3
     A* End   056803
     A            PPREF         15         6
     A            PCURR1         3        25
     A  30        PAMT1A        13  0     36EDTCDE(1)
     A  31        PAMT1B        13  1     36EDTWRD('   ,   ,   , 0 . ')
     A  32        PAMT1C        13  2     36EDTWRD('  ,   ,   , 0 .  ')
     A  33        PAMT1D        13  3     36EDTWRD(' ,   ,   , 0 .   ')
     A*
     A            PCURR2         3        61
     A  40        PAMT2A        13  0     72EDTCDE(1)
     A  41        PAMT2B        13  1     72EDTWRD('   ,   ,   , 0 . ')
     A  42        PAMT2C        13  2     72EDTWRD('  ,   ,   , 0 .  ')
     A  43        PAMT2D        13  3     72EDTWRD(' ,   ,   , 0 .   ')
     A*                                                    /S01117/
     A  10        SBRO      R             95REFFLD(ORBR)
     A  10        PBRNO     R        O   100REFFLD(BRNM)
     A*                                                    /S01117/
     A*
     A          R FT0230D3
     A                                     1' '
     A                                      SPACEB(1)
     A*
     A*
     A* Start 056803
     A            AUTFLG         2         3
     A* End   056803
     A            PPREF         15         6
     A            PCURR1         3        25
     A  30        PAMT1A        13  0     36EDTCDE(1)
     A  31        PAMT1B        13  1     36EDTWRD('   ,   ,   , 0 . ')
     A  32        PAMT1C        13  2     36EDTWRD('  ,   ,   , 0 .  ')
     A  33        PAMT1D        13  3     31EDTWRD(' ,   ,   , 0 .   ')
     A*                                                    /S01117/
     A  10        SBRO      R        O    95REFFLD(ORBR)
     A  10        PBRNO     R        O   100REFFLD(BRNM)
     A*                                                    /S01117/
     A*
     A*                                                    /* START OF E32877 */
     A          R FT0230D4
     A                                     1' '
     A                                      SPACEB(1)
     A*
     A* Start 056803
     A            AUTFLG         2         3
     A* End   056803
     A            PCURR2         3        61
     A  40        PAMT2A        13  0     72EDTCDE(1)
     A  41        PAMT2B        13  1     72EDTWRD('   ,   ,   , 0 . ')
     A  42        PAMT2C        13  2     72EDTWRD('  ,   ,   , 0 .  ')
     A  43        PAMT2D        13  3     72EDTWRD(' ,   ,   , 0 .   ')
     A*                                                      /* END OF E32877 */
     A*                                                    /S01117/
     A  10        SBRO      R        O    95REFFLD(ORBR)
     A  10        PBRNO     R        O   100REFFLD(BRNM)
     A*                                                    /S01117/
     A          R ENDLIN
     A*                                                    /S01117/
     A************************************56'*** END OF REPORT ***'
     A N10
     AO 13                                56'*** END OF REPORT ***'
     A                                      SPACEB(2)
     A  10N13                             50'*** END OF REPORT FOR BRANCH ***'
     A                                      SPACEB(2)
     A*                                                    /S01117/
     A*
     A*                                                    /S01117/
     A**********R PNOREC
     A************************************53'*** NO DETAILS TO +
     A************************************    REPORT ***'
     A************************************  SPACEB(3)
     A*                                                    /S01117/
