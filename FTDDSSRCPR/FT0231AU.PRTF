      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FT Unauth req for transfer/cust cr transfer')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PRTF/FT0231AU - Unauthorised Request for Transfer/Customer   *
      *                  Credit Transfer - Audit                      *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. BUG25160A *CREATE  Date 18Aug09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG25160A - Apply fix 229655                                 *
      *  229655 - Include RFT and CCT in FTC0230 RICY option UNPR     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE FT0231AU'
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                      SPACEA(1)
      *
     A            RUSER     R              6REFFLD(JUSR)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  51 52                             38'Unauthorised Request for +
     A                                      Transfer/Customer Credit +
     A                                      Transfer - AUDIT'
     A                                      SPACEA(1)
     A  51 52                             38'-------------------------+
     A                                      -------------------------+
     A                                      ----------------'
     A  51 52                               SPACEA(2)
      *
     A  51N52                             47'Unauthorised Request +
     A                                      for Transfer - AUDIT'
     A  51N52                               SPACEA(1)
     A  51N52                             47'---------------------+
     A                                      --------------------'
     A  51N52                               SPACEA(2)
      *
     A N51 52                             50'Unauthorised Customer +
     A                                      Credit Transfer'
     A N51 52                               SPACEA(1)
     A N51 52                             50'----------------------+
     A                                      ---------------'
     A N51 52                               SPACEA(2)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A  51                                 6'No. of Unauthorised +
     A                                       Request for Transfer +
     A                                       Printed'
     A  51        RCNT1          5  0     60EDTCDE(3)
     A  51                                  SPACEA(2)
     A  52                                 6'No. of Unauthorised +
     A                                       Customer Credit Transfer +
     A                                       Printed'
     A  52        RCNT2          5  0     60EDTCDE(3)
     A  52                                  SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
