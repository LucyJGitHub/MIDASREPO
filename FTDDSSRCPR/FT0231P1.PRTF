      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FT Unauth req for transfer/cust cr transfer')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PRTF/FT0231P1 - Unauthorised Request for Transfer/Customer   *
      *                  Credit Transfer                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. BUG25160A *CREATE  Date 18Aug09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG25160A - Apply fix 229655                                 *
      *  229655 - Overhaul FT0231 to produce RFT/CCT report           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'FT0231P1'
     A            BJMRDT    R            101REFFLD(MRDT)
     A                                      SPACEA(1)
      *
     A            RUSER     R              6REFFLD(JUSR)
     A                                   101TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  51N10                             50'Unauthorised Request +
     A                                      for Transfer'
     A  51N10                               SPACEA(1)
     A  51N10                             50'---------------------+
     A                                      ------------'
     A  51N10                               SPACEA(1)
      *
     A  10 52                             50'Unauthorised Customer +
     A                                      Credit Transfer'
     A  10 52                               SPACEA(1)
     A  10 52                             50'----------------------+
     A                                      ---------------'
     A  10 52                               SPACEA(1)
      *
      ***  BRANCH HEADING
      ***  --------------
      *
     A          R HEADP1A
     A N21                                76'Originating Branch:'
     A  21                                76'    Booking Branch:'
     A            RBRCA          3        96
     A            RBDESC        30       100
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R HEADP2
     A N10                                 6'Senders'
     A  10                                 6'File'
     A                                    35'Trans'
     A                                    68'Payment'
     A                                      SPACEA(1)
      *
     A                                     6'Reference'
     A                                    26'Type'
     A                                    36'Ccy'
     A                                    43'Transaction Amount'
     A                                    70'Ccy'
     A                                    81'Payment Amount'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A            RSNDRF        15         6
     A                                      SPACEB(1)
     A            RTYPE          2        27
     A                                      SPACEA(1)
      *
     A          R DETAIL2
     A            RASTK          2         3
     A                                     6'Trans Ref:'
     A            RTREF         16        17
     A            RTCCY          3        36
     A            RTAMT         25        42
     A            RPCCY          3        70
     A            RPAMT         25        77
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A N20                                51'***  END OF REPORT  ***'
     A N20                                  SPACEB(3)
     A  20                                48'** END OF REPORT FOR BRANCH **'
     A  20                                  SPACEB(3)
