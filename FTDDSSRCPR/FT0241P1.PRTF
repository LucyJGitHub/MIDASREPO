      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FT OUTGOING PAYMENTS BY DEST.REPORT')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Funds Transfer Module                            *
     A*                                                               *
     A*  PRTF/FT0241P1 - Outgoing Payments By Destination Report      *
     A*                      (Selection By Originating Branch)        *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG8717            Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01494             Date 25May94               *
     A*                                                               *
     A*---------------------------------------------------------------*
      *                                                               *
      *  BUG8717- Replace Underline keyword with hardcoded line.      *
     A*  S01494 - BLI Funds Transfer Enhancements                     *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R P1HEAD                    SKIPB(2)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE '
     A*********              Start of Change               /S01494/
     A*********                           16'FT0241P1'
     A N25                                16'FT0241P1'
     A  25                                16'FT0243P1'
     A*                      End of Change                 /S01494/
     A            TITL      R             37
     A                                   100'DATE'
     A            RUNA      R            105
     A                                   119'PAGE'
     A            PAGE1     R            124EDTCDE(4)  REFFLD(PAGE)
      *
     A*********              Start of Change               /S01494/
      ** Start of Change  BUG8717
     A*********                           41'OUTGOING PAYMENTS BY -
     A*N25*****                           41'OUTGOING PAYMENTS BY -
     A*********                             DESTINATION' UNDERLINE
     A*********                             SPACEB(2)
     A**25*****                           42'REGULAR PAYMENTS BY -
     A*                      End of Change                 /S01494/
     A*********                             DESTINATION' UNDERLINE
     A*********                             SPACEB(2)
     A N25                                41'OUTGOING PAYMENTS BY DESTINATION'
     A                                      SPACEB(2)
     A  25                                42'REGULAR  PAYMENTS BY DESTINATION'
     A                                      SPACEB(2)
      ** End of Change  BUG8717
     A  40                                80'** ALL **'
     A  41                                80'** AUTHORISED TODAY **'
     A  42                                80'** NOT AUTHORISED **'
      ** Start of Change  BUG8717
     A                                    42'--------------------------------'
     A                                      SPACEB(1)
      ** End of Change  BUG8717
      *
      * Origianting Branch Name heading
     A          R P1BRHD
     A                                      SPACEB(2)
     A                                     8'ORIGINATING BRANCH'
     A            SBRA      R        O    +2REFFLD(BRCA)
     A            SRNM      R        O    +3REFFLD(BRNM)
      *
      ** Sub-heading.  (Currency)
     A          R P1SUBH                    SPACEB(2)
     A            PCCY      R        O     8
     A            CCNM          14   O    +1
      *
      ** Column-heading.
     A          R P1COLH                    SPACEB(1)
     A                                    39'Value'
     A                                      SPACEA(1)
     A                                     1'Destination'
     A                                    39'Date'
     A                                    48'Beneficiary'
     A                                    86'Payment'
     A                                    +1'Ref'
     A                                   102'Payment'
     A                                    +1'amount'
     A                                   122'Authorised'
      *
      ** Detail
     A          R P1DETL                    SPACEB(2)
     A            DFDEST    R              1REFFLD(DST1)
     A            SLDT3          7        39
     A            DFBENF    R             48REFFLD(BNC1)
     A            PREF      R             86
     A  30        PYAM0         13  0    102EDTCDE(J)
     A  31        PYAM1         13  1    102EDTCDE(J)
     A  32        PYAM2         13  2    102EDTCDE(J)
     A  33        PYAM3         13  3    102EDTCDE(J)
     A            AUIN      R            126
      *
      ** Sub- Totals .
     A          R P1TOT                     SPACEB(2)
     A                                    77'Total for'
     A            TOTNAM        11        +1
     A  30        TPYAM0        15  0    100EDTCDE(J)
     A  31        TPYAM1        15  1    100EDTCDE(J)
     A  32        TPYAM2        15  2    100EDTCDE(J)
     A  33        TPYAM3        15  3    100EDTCDE(J)
      *
      ** End of report .
     A          R P1END                     SPACEB(2)
     A  11                                56'** END OF REPORT **'
     A N11                                50'** END OF REPORT FOR'
     A N11                                +1'BRANCH **'
      *
