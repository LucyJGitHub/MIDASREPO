      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FT OUTGOING PAYMENTS AUDIT AND LIST')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Funds Transfer Module                            *
     A*                                                               *
     A*  PRTF/FT0500AU - Payment Charges Control Report               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01508             DATE 27JUN94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01508 - BLI Extended Payment Charges                        *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R AUHEAD                    SKIPB(2)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A
     A                                     6'REFERENCE FT0500AU'
     A            TITL      R             37
     A                                   100'DATE'
     A            RUNA      R            105
     A                                   119'PAGE'
     A            PAGE      R            124EDTCDE(4)
     A*
     A                                    54'PAYMENT CHARGES'
     A                                      SPACEB(2)
     A                                   + 1'- RUN CONTROLS'
     A*
     A                                    54'~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~~'
     A                                      SPACEB(1)
     A*
     A  10N11                              3'BRANCH'
     A                                      SPACEB(2)
     A  10N11     PYBR      R        O    11REFFLD(BRCA)
     A  10N11N12  PBRNM     R        O    +2REFFLD(BRNM)
     A*
     A
     A          R AUCNTL                    SKIPB(11)
     A*
     A                                     6'FILE'
     A                                      SPACEB(2)
     A                                    58'NUMBER'
     A*
     A                                     6'PAYMENT CHARGES'
     A                                      SKIPB(17)
     A*********              Start of Change               /S01508/
     A*********                           27'- LF/PYCHGDB'
     A N15                                27'- LF/PYCHGL1'
     A  15                                27'- LF/PYCHGL2'
     A*                      End of Change                 /S01508/
     A                                    43'INPUT'
     A            AUNORE         5S 0     57EDTCDE(3)
     A*
     A          R AUDBER                    SKIPB(30)
     A                                    21'PROGRAM FT0500 TERMINATED'
     A                                   + 1'AT POINT'
     A            DBASE          3  0    + 1
     A                                   + 1'- DATABASE IN ERROR -'
     A                                   + 1'FILE'
     A            DBFILE         8       + 1
     A                                   + 1'KEY'
     A            DBKEY         29       + 1
     A*
     A          R AUNODT                    SPACEB(3)
     A                                    54'** NO DETAILS TO REPORT **'
     A*
     A          R AUEOR                     SKIPB(37)
     A                                    56'*** END OF REPORT ***'
     A*
