      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FT UNAUTHORISED OUTGOING PAYMENTS REPORT')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Funds Transfer Module                            *
     A*                                                               *
     A*  PRTF/FT0515AU - Outgoing Payments Control Report             *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND  NO. S01494            DATE 25MAY94               *
     A*  PREV AMEND  NO. S01117            DATE 13FEB90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01494 - BLI Funds Transfer Enhancements                     *
     A*  S01117 - New file replacing FT0515P2                         *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A*
     A          R AUHEAD                    SKIPB(2)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*********              Start of Change               /S01494/
     A*********                            6'REFERENCE FT0515AU'
     A N27                                 6'REFERENCE FT0515AU'
     A  27                                 6'REFERENCE FT0516AU'
     A*                      End of Change                 /S01494/
     A*
     A            AUTITL    R             37REFFLD(TITL)
     A                                   100'DATE'
     A            AUDATE    R            105REFFLD(RUNA)
     A                                   119'PAGE'
     A            PPAGE     R            124EDTCDE(4)  REFFLD(PAGE)
     A*
     A*********              Start of Change               /S01494/
     A*********                           44'OUTGOING PAYMENTS -
     A*********                             NON-AUTHORISED ITEMS'
     A N27                                44'OUTGOING PAYMENTS -
     A                                      NON-AUTHORISED ITEMS'
     A                                      SPACEB(2)
     A  27                                44'REGULAR PAYMENTS  -
     A                                      NON-AUTHORISED ITEMS'
     A*                      End of Change                 /S01494/
     A                                      SPACEB(2)
     A                                   + 1'- RUN CONTROLS'
     A                                    44'-------------------'
     A                                      SPACEB(1)
     A                                    63'-------------------'
     A                                    82'---------------'
     A*
     A  22N25N26                           6'BOOKING BRANCH'
     A                                      SPACEB(2)
     A  22N25 26                           6'ORIGINATING BRANCH'
     A                                      SPACEB(2)
     A  22N25     SBRA      R        O    26REFFLD(BRCA)
     A  22N25     PBRNM     R        O    +2REFFLD(BRNM)
     A*
     A          R AUCNTL                    SKIPB(11)
     A                                    91'CALCULATED'
     A*
     A                                     6'FILE'
     A                                      SPACEB(2)
     A                                    82'NUMBER'
     A                                   103'VALUE'
     A*********              Start of Change               /S01494/
     A*********
     A*********                            6'OUTGOING PAYMENTS'
     A*********                             SKIPB(17)
     A****N26**                           27'- LF/NOTPYBB'
     A**22*26**                           27'- LF/NOTPYOB'
     A N27                                 6'OUTGOING PAYMENTS'
     A                                      SKIPB(17)
     A  27                                 6'REGULAR PAYMENTS'
     A                                      SKIPB(17)
     A    N26N27                          27'- LF/NOTPYBB'
     A  22 26N27                          27'- LF/NOTPYOB'
     A    N26 27                          27'- LF/NRPPYBB'
     A  22 26 27                          27'- LF/NRPPYOB'
     A*                      End of Change                 /S01494/
     A                                    43'INPUT'
     A            AUCLNO         5S 0     83EDTCDE(3)
     A            AUCLWN        15S 0     90EDTCDE(4)
     A            AUCLDC         3S 0    105
     A*
     A          R AUDBER                    SKIPB(30)
     A                                    21'PROGRAM FT0515 TERMINATED'
     A                                   + 1'AT POINT'
     A            DBASE          3       + 1
     A                                   + 1'- DATABASE IN ERROR -'
     A                                   + 1'FILE'
     A            DBFILE         8       + 1
     A                                   + 1'KEY'
     A            DBKEY         29       + 1
     A*
     A          R AUNODT                    SPACEB(3)
     A                                    54'** NO DETAILS TO REPORT **'
     A*
     A          R AUEOR                     SKIPB(37)
     A                                    56'*** END OF REPORT ***'
     A*
