      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FT INCOMING PAYMENTS AUDIT AND LIST')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FUNDS TRANSFER MODULE
     A*                                                               *
     A*         PRTF/FT0520AU INCOMING PAYMENTS CONTROL REPORT        *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01117             Date 19Oct90               *
     A*                                                               *
     A*****************************************************************
      *  MD046248 - Finastra Rebranding                               *
     A*     S01117 - NEW SOURCE DERIVED FROM FT0520P2                 *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R AUHEAD                    SKIPB(2)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                     6'REFERENCE FT0520AU'
     A            AUTITL    R             37REFFLD(TITL)
     A                                   100'DATE'
     A            AUDATE    R            105REFFLD(RUNA)
     A                                   119'PAGE'
     A            PAGE           5  0    124EDTCDE(4)
     A*
     A                                    54'INCOMING PAYMENTS'
     A                                      SPACEB(2)
     A                                   + 1'- RUN CONTROLS'
     A*
     A                                    54'~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~~~'
     A                                      SPACEB(1)
     A*
     A N11
     AA 10N12N14                           3'BOOKING BRANCH'
     A                                      SPACEB(2)
     A N11
     AA 10N12 14                           3'ORIGINATING BRANCH'
     A                                      SPACEB(2)
     A  10N12N11  SBRA      R        O    23REFFLD(BRCA)
     A  10N12N11  PBRNM     R        O    +2REFFLD(BRNM)
     A*
     A          R AUCNTL                    SKIPB(11)
     A                                    63'TRAILER'
     A                                    91'CALCULATED'
     A*
     A                                     6'FILE'
     A                                      SPACEB(2)
     A                                    53'NUMBER'
     A                                    74'VALUE'
     A                                    82'NUMBER'
     A                                   103'VALUE'
     A*
     A                                     6'INCOMING PAYMENTS'
     A                                      SKIPB(17)
     A  14                                27'- LF/INPAYOB'
     A N14                                27'- LF/INPAYBB'
     A                                    43'INPUT'
     A            AUNORE         5S 0     54EDTCDE(3)
     A            AUHRWN        15S 0     61EDTCDE(4)
     A            AUHRDC         3S 0     76
     A            AUCLNO         5S 0     83EDTCDE(3)
     A            AUCLWN        15S 0     90EDTCDE(4)
     A            AUCLDC         3S 0    105
     A            AUDIFF        18       112
     A*
     A          R AUDBER                    SKIPB(30)
     A                                    21'PROGRAM FT0520 TERMINATED'
     A                                   + 1'AT POINT'
     A            AUDBAS         3       + 1
     A                                   + 1'- DATABASE IN ERROR -'
     A                                   + 1'FILE'
     A            AUDBFL         8       + 1
     A                                   + 1'KEY'
     A            AUDBKY        29       + 1
     A*
     A          R AUNODT                    SPACEB(3)
     A                                    54'** NO DETAILS TO REPORT **'
     A*
     A          R AUEOR                     SKIPB(37)
     A                                    56'*** END OF REPORT ***'
     A*
