      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FT UNAUTHORISED INCOMING PAYMENTS REPORT')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FUNDS TRANSFER MODULE
     A*                                                               *
     A*         PRTF/FT0525AU INCOMING PAYMENTS CONTROL REPORT        *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*      COMPANY CONFIDENTIAL                                     *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01117             Date 08/11/90              *
     A*                                                               *
     A*****************************************************************
      *  MD046248 - Finastra Rebranding                               *
     A*   S01117 - NEW FILE REPLACING FT0525P2                        *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A*
     A          R AUHEAD                    SKIPB(2)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                     6'REFERENCE FT0525AU'
     A*
     A            AUTITL    R             37REFFLD(TITL)
     A                                   100'DATE'
     A            AUDATE    R            105REFFLD(RUNA)
     A                                   119'PAGE'
     A            PPAGE     R            124EDTCDE(4)  REFFLD(PAGE)
     A*
     A                                    44'INCOMING PAYMENTS -
     A                                      NON-AUTHORISED ITEMS'
     A                                      SPACEB(2)
     A                                   + 1'- RUN CONTROLS'
     A                                    44'-------------------'
     A                                      SPACEB(1)
     A                                    63'-------------------'
     A                                    82'---------------'
     A*
     A  22N25N26                           6'BOOKING BRANCH'
     A                                      SPACEB(2)
     A  22N25 26                           6'ORIGINATING BRANCH'
     A                                      SPACEB(2)
     A  22N25     SBRA      R        O    26REFFLD(BRCA)
     A  22N25     PBRNM     R        O    +2REFFLD(BRNM)
     A*
     A          R AUCNTL                    SKIPB(11)
     A                                    63'TRAILER'
     A                                    91'CALCULATED'
     A*
     A                                      SPACEB(2)
     A                                   103'VALUE'
     A*
     A                                     6'INCOMING PAYMENTS'
     A                                      SKIPB(17)
     A    N26                             27'- LF/NINPYBB'
     A  22 26                             27'- LF/NINPYOB'
     A                                    43'INPUT'
     A            AUCLNO         5S 0     83EDTCDE(3)
     A            AUCLWN        15S 0     90EDTCDE(4)
     A            AUCLDC         3S 0    105
     A            AUDIFF        18       112
     A*
     A          R AUDBER                    SKIPB(30)
     A                                    21'PROGRAM FT0525 TERMINATED'
     A                                   + 1'AT POINT'
     A            DBASE          3       + 1
     A                                   + 1'- DATABASE IN ERROR -'
     A                                   + 1'FILE'
     A            DBFILE         8       + 1
     A                                   + 1'KEY'
     A            DBKEY         29       + 1
     A*
     A          R AUNODT                    SPACEB(3)
     A                                    54'** NO DETAILS TO REPORT **'
     A*
     A          R AUEOR                     SKIPB(37)
     A                                    56'*** END OF REPORT ***'
     A*
