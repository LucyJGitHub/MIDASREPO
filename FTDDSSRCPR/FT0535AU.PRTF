      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FT UNAUTHORISED NOSTRO TRANSFERS REPORT')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FUNDS TRANSFER MODULE
     A*                                                               *
     A*         PRTF/FT0535AU AUDIT/FULL LIST OF 'NTRAN'              *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     LAST AMEND  NO. S01117     DATE  03/01/91                 *
     A*                                                               *
     A*****************************************************************
     A*     S01117 - NEW SOURCE DERIVED FROM FT0535P2                 *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R AUHEAD                    SKIPB(2)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE FT0535AU'
     A            AUTITL    R             37REFFLD(TITL)
     A                                   100'DATE'
     A            AUDATE    R            105REFFLD(RUNA)
     A                                   119'PAGE'
     A            PAGE           5  0    124EDTCDE(4)
     A*
     A                                    40'UNAUTHORISED'
     A                                      SPACEB(2)
     A                                    53'NOSTRO TRANSFERS -'
     A                                   + 1'RUN CONTROLS'
     A*
     A                                    40'------------+
     A                                       ------------------+
     A                                       --------------'
     A                                      SPACEB(1)
     A*
     A  21N22N23                           6'BOOKING BRANCH'
     A                                      SPACEB(2)
     A  21N22 23                           6'ORIGINATING BRANCH'
     A                                      SPACEB(2)
     A  21N22     SBRA      R        O    26REFFLD(BRCA)
     A  21N22     PBRNM     R        O    30REFFLD(BRNM)
     A*
     A          R AUCNTL                    SKIPB(11)
     A                                    91'CALCULATED'
     A*
     A                                     6'FILE'
     A                                      SPACEB(2)
     A                                    82'NUMBER'
     A                                   103'VALUE'
     A*
     A                                     6'NOSTRO TRANSFERS -'
     A                                      SKIPB(17)
     A    N23                             27'- LF/NNTRNBB'
     A     23                             27'- LF/NNTRNOB'
     A                                    43'INPUT'
     A            AUCLNO         5S 0     83EDTCDE(3)
     A            AUCLWN        15S 0     90EDTCDE(4)
     A            AUCLDC         3S 0    105
     A*
     A          R AUDBER                    SKIPB(30)
     A                                    21'PROGRAM FT0535 TERMINATED'
     A                                   + 1'AT POINT'
     A            AUDBAS         3       + 1
     A                                   + 1'- DATABASE IN ERROR -'
     A                                   + 1'FILE'
     A            AUDBFL         8       + 1
     A                                   + 1'KEY'
     A            AUDBKY        29       + 1
     A*
     A          R AUEOR                     SKIPB(37)
     A
     A    N77                             53'*** NO DETAILS TO'
     A    N77                             70' REPORT ***'
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(2)
     A*
