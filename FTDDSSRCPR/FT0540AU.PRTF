      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FT CHEQUES FOR COLLECTION AUDIT AND LIST')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FUNDS TRANSFER MODULE
     A*                                                               *
     A*         PRTF/FT0540AU CHEQUES FOR COLLECTION                  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     LAST AMEND  NO. S01117     DATE  DDMMMYY                  *
     A*                                                               *
     A*****************************************************************
     A*     S01117 - NEW SOURCE DERIVED FROM FT0540P2                 *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R AUHEDT
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                  3  3'REFERENCE FT0540AU'
     A            TITL      R           3 37
     A                                  3100'DATE'
     A            RUNA      R           3105
     A                                  3119'PAGE'
     A            PAGE           5  0   3124EDTCDE(4)
     A*
     A                                  5 51'CHEQUES FOR COLLECTION -'
     A                                  5 76'RUN CONTROLS'
     A*
     A                                  6 51'-------------------------'
     A                                  6 76'------------'
     A*
     A N11
     AA 10N12N14                        9  6'BOOKING BRANCH'
     A N11
     AA 10N12 14                        9  6'ORIGINATING BRANCH'
     A  10N12N11  SBRA      R        O  9 26REFFLD(BRCA)
     A  10N12N11  PBRNM     R        O  9 30REFFLD(BRNM)
     A*
     A                                 11 63'TRAILER'
     A                                 11 91'CALCULATED'
     A*
     A                                 13  6'FILE'
     A                                 13 53'NUMBER'
     A                                 13 74'VALUE   NUMBER'
     A                                 13103'VALUE'
     A*
     A                                 17  6'CHEQUE HEADERS'
     A                                 17 27'- PF/CQCODDD    INPUT'
     A            TNDB           5  0  17 54EDTCDE(3)
     A            TIDB          15  0  17 61EDTCDE(4)
     A            TDDB           3  0  17 76
     A            TNDB#          5  0  17 83EDTCDE(3)
     A            TIDB#         15  0  17 90EDTCDE(4)
     A            TDDB#          3  0  17105
     A            DIFFDB        16A    17112
     A*
     A                                 20  6'CHEQUE DETAILS'
     A                                 20 27'- PF/CQCOCDD    INPUT'
     A            TNCR           5  0  20 54EDTCDE(3)
     A            TICR          15  0  20 61EDTCDE(4)
     A            TDCR           3  0  20 76
     A            TNCR#          5  0  20 83EDTCDE(3)
     A            TICR#         15  0  20 90EDTCDE(4)
     A            TDCR#          3  0  20105
     A            DIFFCR        16A    20112
     A    N77                          35 53'*** NO DETAILS TO +
     A                                        REPORT ***'
     A*
     A                                 37 56'*** END OF REPORT ***'
     A*
     A          R AUDBER
     A                                 42 21'PROGRAM FT0540 TERMINATED'
     A                                   + 1'AT POINT'
     A            AUDBAS         3       + 1
     A                                   + 1'- DATABASE IN ERROR -'
     A                                   + 1'FILE'
     A            AUDBFL         8       + 1
     A                                   + 1'KEY'
     A            AUDBKY        29       + 1
     A*
