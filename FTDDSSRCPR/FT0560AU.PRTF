      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FT Debit and credit advices input cycle')
     A*****************************************************************
     A*                                                               *
     F*  Midas - FUNDS TRANSFER MODULE
     A*                                                               *
     A*       PRTF/FT0560AU DR/CR ADVICE GENERATION - RUN CONTROLS    *
     A*                       (INPUT CYCLE)                           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CFT006             Date 26/10/99              *
      *                                    Date 00/00/00              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CFT006 - MT101/MT102 enhancements (print Debit and Credit    *
     A*           advices for RFT and CCT)                            *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R AUHEAD                    SKIPB(2)
     A
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A
     A                                     6'REFERENCE FT0560AU'
     A            TITL      R             37
     A                                   100'DATE'
     A            RUNA      R            105
     A                                   119'PAGE'
     A            PAGE      R            124EDTCDE(4)
     A*
     A                                    46'DR/CR ADVICES'
     A                                      SPACEB(2)
     A                                   + 1'- GENERATION'
     A                                   + 1'- RUN CONTROLS'
     A*
     A                                    46'~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~~~'
     A                                      SPACEB(1)
     A  28N31N29                           6'BOOKING BRANCH'
     A                                      SPACEB(2)
     A  28N31 29                           6'ORIGINATING BRANCH'
     A                                      SPACEB(2)
     A  28N31     SBRA      R        O    26REFFLD(BRCA)
     A  28N31     PBRNM     R        O    +2REFFLD(BRNM)
     A*
     A          R AUCNTL                    SKIPB(13)
     A*
     A                                     6'FILE'
     A                                      SPACEB(1)
     A    N29                             12'ADVGENB'
     A  28 29                             12'ADVGENO'
     A                                    82'NUMBER'
     A                                      SPACEB(1)
     A*
     A                                     6'CHEQUES FOR COLLECTION'
     A                                      SKIPB(19)
     A                                    29'- LF/CQCOCDDF'
     A                                    46'INPUT'
     A            WCQCOR         5S 0     83EDTCDE(3)
     A*
     A                                     6'CHEQUES TO BE PAID'
     A                                      SPACEB(2)
     A                                    29'- LF/CQPADDDF'
     A                                    46'INPUT'
     A            WCQPAR         5S 0     83EDTCDE(3)
     A*
     A                                     6'INCOMING PAYMENTS'
     A                                      SPACEB(2)
     A                                    29'- LF/INPAYDDF'
     A                                    46'INPUT'
     A            WINPAR         5S 0     83EDTCDE(3)
     A*
     A                                     6'OUTGOING PAYMENTS'
     A                                      SPACEB(2)
     A                                    29'- LF/OTPAYDDF'
     A                                    46'INPUT'
     A            WOTPAR         5S 0     83EDTCDE(3)
     A*
     A*********              Start of Change               /CFT006/
     A*
     A  36                                 6'RFT (RI) CREDIT ADVICE'
     A                                      SPACEB(2)
     A  36                                29'- LF/FT101HL3'
     A  36                                46'INPUT'
     A  36        WRFTPI         5S 0     83EDTCDE(3)
     A*
     A  36                                 6'RFT (RI) DEBIT ADVICE'
     A                                      SPACEB(2)
     A  36                                29'- LF/FT101HL3'
     A  36                                46'INPUT'
     A  36        WRFTPO         5S 0     83EDTCDE(3)
     A*
     A  26                                 6'CCT (BI) CREDIT ADVICE'
     A                                      SPACEB(2)
     A  26                                29'- LF/FT102HL3'
     A  26                                46'INPUT'
     A  26        WCCTPI         5S 0     83EDTCDE(3)
     A*
     A  26                                 6'CCT (BI) DEBIT ADVICE'
     A                                      SPACEB(2)
     A  26                                29'- LF/FT102HL3'
     A  26                                46'INPUT'
     A  26        WCCTPO         5S 0     83EDTCDE(3)
     A*
     A*********              End of Change                 /CFT006/
     A*
     A                                     6'ADVICES GENERATED'
     A                                      SPACEB(3)
     A                                    46'OUTPUT'
     A            WTOTAW         5S 0     83EDTCDE(3)
     A*
     A          R AUDBER                    SKIPB(32)
     A                                    21'PROGRAM FT0560 TERMINATED'
     A                                   + 1'AT POINT'
     A            AUDBAS         3  0    + 1
     A                                   + 1'- DATABASE IN ERROR -'
     A                                   + 1'FILE'
     A            AUDBFL         8       + 1
     A                                   + 1'KEY'
     A            AUDBKY        29       + 1
     A*
     A          R PREFHED                   SPACEB(2)
     A
     A  28 29                              5'ORIGINATING'
     A  28N29                              5'  BOOKING  '
     A  28                                32'STARTING'
     A  28                                54'ENDING'
     A  28                                 5'  BRANCH   '
     A                                      SPACEB(1)
     A  28                                32'  PREF  '
     A  28                                54' PREF '
     A          R PREFHED2                  SKIPB(2)
     A
     A  28 29                              5'ORIGINATING'
     A  28N29                              5'  BOOKING  '
     A  28                                32'STARTING'
     A  28                                54'ENDING'
     A  28                                 5'  BRANCH   '
     A                                      SPACEB(1)
     A  28                                32'  PREF  '
     A  28                                54' PREF '
     A          R PREFDET                   SPACEB(2)
     A
     A            BRCAAU    R              8REFFLD(BRCA)
     A            STPRFA    R             29REFFLD(PREF)
     A            EDPRFA    R             50REFFLD(PREF)
     A          R AUEOR                     SPACEB(2)
     A                                    56'*** END OF REPORT ***'
     A*
     A*
