      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FT Auth req for transfer/cust credit transfr')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PRTF/FT0572P1 - Authorised Request for Transfer/Customer     *
      *                  Credit Transfer                              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. BUG24708           Date 31Jul09               *
      *                 CSW209             Date 01Apr09               *
      *                 229420             Date 07Sep04               *
      *                 226942             DATE 27May04               *
      *                 221652             DATE 20Jan04               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSW201             Date 02May01               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CFT006  *CREATE    Date 13Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG24708 - Customer Reference field is misaligned.           *
      *  CSW209 - SWIFT 2009 Changes                                  *
      *  229420 - Branch 'ALL' request doesn't work.                  *
      *  226942 - Make RFT/CCT Authorised/Unauthorised reports        *
      *           available in the system.                            *
      *  221652 - Make report request available in the system.        *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSW201 - SWIFT 2001 Standards Update                         *
      *  CFT006 - Funds Transfer/Swift Messages MT101/MT102           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'FT0572P1'
      **Start of Change 221652
     A*N10******                          50'Authorised Request +                             221652
     A**********                            for Transfer'                                     221652
     A N10                                42'Authorised Request +
     A                                      for Transfer - Booking +
     A                                      Branch'
      **End of Change 221652
     A**Start of Change 226942
     A**10******                          50'Authorised Customer +                            226942
     A**********                            Credit Transfer'                                  226942
     A  10                                42'Authorised Customer +
     A                                      Credit Transfer - Booking +
     A                                      Branch'
     A**End of Change 226942
     A            BJMRDT    R            101REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A            RUSER     R              6REFFLD(JUSR)
      **Start of Change 221652
     A*N10******                          50'-------------------+                             221652
     A**********                            ------------'                                     221652
     A N10                                42'-------------------+
     A                                      -----------------------+
     A                                      ------'
      **End of Change 221652
     A**Start of Change 226942
     A**10******                          50'--------------------+                            226942
     A**********                            ---------------'                                  226942
     A  10                                42'--------------------+
     A                                      --------------------------+
     A                                      ------'
     A**End of Change 226942
     A                                   101TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R HEADP2
     A N10                                24'Senders'
     A  10                                24'File'
     A                                    47'Sub'
     A                                    53'Booking'
     A                                    63'Orig'
     A                                    71'Book'
     A                                    77'Profit'
     A  10                                91'Bank'
      **Start of Change BUG24708
     A N10                                86'Customer'
      **End of Change BUG24708
      **Start of Change CSW209
     A**10******                            SPACEA(1)                                         CSW209
     A  10N18                               SPACEA(1)
     A  16 18                            104'Network'
     A  16 18                            113'Message'
     A  16 18                            122'Validation'
     A  16 18                               SPACEA(1)
     A N16 18                            103'Validation'
     A N16 18                            117'Validation'
     A N16 18                               SPACEA(1)
      **End of Change CSW209
      **Start of Change BUG24708
     A*N10******                          86'Customer'                                      BUG24708
     A*N10******                            SPACEA(1)                                       BUG24708
      **End of Change BUG24708
      *
     A N10                                 6'RFT ID'
     A  10                                 6'CCT ID'
     A                                    24'Reference'
     A                                    41'Type'
     A                                    47'Type'
     A                                    53'Branch'
     A                                    62'Branch'
     A                                    71'Code'
     A                                    77'Center'
     A  10                                86'Operation Code'
      **Start of Change BUG24708
     A N10                                86'Reference'
      **End of Change BUG24708
      **Start of Change CSW209
     A**10********                          SPACEA(1)                                         CSW209
     A  10N18                               SPACEA(1)
     A  16 18                            114'Type'
     A  16 18                            124'Flag'
     A  16 18                               SPACEA(1)
     A N16 18                            103'Flag (Main)'
     A N16 18                            117'Flag (Cover)'
     A N16 18                               SPACEA(1)
      **End of Change CSW209
     **Start of Change BUG24708
     A*N10******                          86'Reference'                                     BUG24708
     A*N10******                            SPACEA(1)                                       BUG24708
      **End of Change BUG24708
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A            RTRID     R              6REFFLD(ACID)
     A            RSNDRF    R             24REFFLD(SWCR)
     A            RTYPE     R             42REFFLD(PYTP)
     A            RSTYP     R             48REFFLD(PYTP)
     A            RBBRC     R             54REFFLD(BRCA)
     A            ROBRC     R             63REFFLD(BRCA)
     A            RBOKC     R             72REFFLD(BOKC)
     A            RPRFC     R             78REFFLD(PRFC)
     A  10        RBOCD     R             86REFFLD(CPRF)
      **Start of Change BUG24708
     A N10        RCSRF     R             86REFFLD(CPRF)
      **End of Change BUG24708
      **Start of Change CSW209
     A**10******                            SPACEA(2)                                         CSW209
     A  10N18                               SPACEA(2)
     A  16 18     RANWRK         6A      104
     A  16 18     RAMTPY         3A      114
     A  16 18     RAVALF         8A      124
     A  16 18                               SPACEA(2)
     A N16 18     RAOMVF         8A      106
     A N16 18     RAOCVF         8A      119
     A N16 18                               SPACEA(2)
      **End of Change CSW209
      **Start of Change BUG24708
     A*N10******  RCSRF     R             86REFFLD(CPRF)                                    BUG24708
     A*N10******                            SPACEA(2)                                       BUG24708
      **End of Change BUG24708
      *
     A          R DETAIL2
      *
     A N10                                 6'Instructing Party'
     A N10        RINPY     R             24REFFLD(IPYL)
     A N10                                  SPACEA(2)
      *
     A  10                                 6'Destination'
     A  10        RDEST     R             24REFFLD(IPYL)
     A  10                                  SPACEA(2)
      *
     A                                     6'Ordering Customer'
     A            ROCS1     R             24REFFLD(POBA)
     A            ROCS2     R             61REFFLD(POBA)
     A            ROCS3     R             98REFFLD(POBA)
     A                                      SPACEA(1)
      *
     A            ROCS4     R             24REFFLD(POBA)
     A            ROCS5     R             61REFFLD(POBA)
     A                                      SPACEA(2)
      *
     A N10                                 6'Accnt Serv Inst'
     A N10        RAT35     R             24REFFLD(AWIL)
     A N10        RAT11     R             61REFFLD(SWID)
     A N10                                  SPACEA(2)
      *
     A  10                                 6'Ordering Inst'
     A  10        ROI35     R             24REFFLD(AWIL)
     A  10        ROI11     R             61REFFLD(SWID)
     A  10                                  SPACEA(2)
      *
     A  10                                 6'Reg. Reporting'
     A  10        RRGR1     R             24REFFLD(POBA)
     A  10        RRGR2     R             61REFFLD(POBA)
     A  10        RRGR3     R             98REFFLD(POBA)
     A  10                                  SPACEA(1)
      *
     A                                     6'Sending Inst'
     A            RSINT     R             24REFFLD(AWIL)
     A                                    61'Swift Priority'
     A            RSWPR     R             77REFFLD(MCIS)
     A                                      SPACEA(2)
      *
     A  10                                 6'Transaction Type Code'
     A  10        RTTCD     R             29REFFLD(PRTY)
     A N10                                 6'Execution Date'
     A N10        REXDT          6S       24EDTWRD('  /  /  ')
     A                                    44'Debit Value Date'
     A            RDRVD          6S       63EDTWRD('  /  /  ')
     A  10                                75'Total Transfer Value Date'
     A  10        RTTVD          6S      103EDTWRD('  /  /  ')
     A  10                                  SPACEA(2)
     A N10                                75'Authorisation'
     A N10        RAUTH     R             90REFFLD(AWIL)
     A N10                                  SPACEA(2)
      *
     A N10                                 6'Handling Charge / Ccy / +
     A                                       Account'
     A N10        RHDCH     R             44REFFLD(TYST)
     A N10                                58'/'
     A N10        RCCCY     R             60REFFLD(BCCY)
     A N10                                64'/'
     A**Start of change CGL029
     A*N10******  RACNT     R             66REFFLD(BASE)                                      CGL029
     A N10        RACNT         24        66
     A**End of change CGL029
     A N10                                  SPACEA(1)
      *
     A  10                                 6'Transfer Ccy / Amount'
     A  10        RTSCY     R             29REFFLD(BCCY)
     A  10                                33'/'
     A  10        RTSAM     R             35REFFLD(PREF)
     A  10                                52'Charges'
     A  10        RCHG      R             62REFFLD(BCCY)
     A  10                                68'Exchange Rate'
     A  10        REXRT     R             84REFFLD(FKTT)
     A  10                                  SPACEA(2)
      *
     A  10                                 6'Sum of Amounts'
     A  10        RSAMTS    R             24REFFLD(PREF)
     A  10                                45'Sum of Senders Charges'
     A  10        RSDCHG    R             70REFFLD(PREF)
     A  10                                90'Sum of Receivers Charges'
     A  10        RRCCHG    R            115REFFLD(PREF)
     A  10                                  SPACEA(2)
      *
     A  10                                 6'Senders Correspondent'
     A  10        RSCOR     R             29REFFLD(RCRA)
     A  10                                70'Receivers Correspondent'
     A  10        RRCOR     R             95REFFLD(RCRA)
     A  10                                  SPACEA(2)
      *
     A  10                                 6'Additional Info'
     A  10        RADIN1    R             24REFFLD(FIN1)
     A  10        RADIN2    R             61REFFLD(FIN1)
     A  10        RADIN3    R             98REFFLD(FIN1)
     A  10                                  SPACEA(1)
     A  10        RADIN4    R             24REFFLD(FIN1)
     A  10        RADIN5    R             61REFFLD(FIN1)
     A  10        RADIN6    R             98REFFLD(FIN1)
     A  10                                  SPACEA(2)
      *
     A                                     6'*************************+
     A                                      **************************+
     A                                      **************************+
     A                                      **************************+
     A                                      ****************'
     A                                      SPACEA(2)
      *
     A          R DETAIL3
      *
     A                                     6'Trans Ref'
     A            RTRNRF    R             17REFFLD(ACOR)
     A  10                                40'Transaction Type Code'
     A  10        RTYCD     R             63REFFLD(PRTY)
     A  10                                  SPACEA(2)
     A N10                                40'FX Deal Ref'
     A N10        RFXDRF    R             52REFFLD(SWCR)
     A N10                                  SPACEA(2)
      *
     A N10                                 6'Instructing Party'
     A N10        RDIPY     R             24REFFLD(IPYL)
     A N10                                  SPACEA(2)
      *
     A N10                                 6'Instructions'
     A N10        RINS1     R             24REFFLD(TCDE)
     A N10        RINS2     R             30REFFLD(TCDE)
     A N10        RINS3     R             36REFFLD(TCDE)
     A N10        RINS4     R             42REFFLD(TCDE)
     A N10        RINS5     R             48REFFLD(TCDE)
     A N10        RINS6     R             54REFFLD(DDTA)
     A N10        RINS7     R             87REFFLD(DDTA)
     A N10                                  SPACEA(2)
      *
     A                                     6'Transaction Ccy / Amount'
     A            RTRCY     R             32REFFLD(BCCY)
     A                                    36'/'
     A            RTRAM     R             38REFFLD(PREF)
     A                                    57'Payment Ccy / Amount'
     A            RPYCY     R             78REFFLD(BCCY)
     A                                    82'/'
     A            RPYAM     R             84REFFLD(PREF)
     A                                      SPACEA(2)
      *
     A                                     6'Credit Value Date'
     A            RCRVD          6S       24EDTWRD('  /  /  ')
     A                                    35'Outgoing Payment Flag'
     A            ROTFL     R             58REFFLD(ACBT)
     A                                      SPACEA(2)
      *
     A                                     6'Ordering Customer'
     A            RDOC1     R             24REFFLD(POBA)
     A            RDOC2     R             61REFFLD(POBA)
     A            RDOC3     R             98REFFLD(POBA)
     A                                      SPACEA(1)
      *
     A            RDOC4     R             24REFFLD(POBA)
     A            RDOC5     R             61REFFLD(POBA)
     A                                      SPACEA(2)
      *
     A  10                                 6'Ordering Institution'
     A  10        RDOI1     R             27REFFLD(POBA)
     A  10        RDOI2     R             65REFFLD(SWID)
     A  10                                  SPACEA(2)
      *
     A N10                                 6'Accnt Serv Inst'
     A N10        RDAT3     R             24REFFLD(AWIL)
     A N10        RDAT1     R             61REFFLD(SWID)
     A N10                                  SPACEA(2)
      *
     A N10                                 6'Intermediary'
     A N10        RINM1     R             24REFFLD(INB1)
     A N10        RINM2     R             61REFFLD(INB1)
     A N10        RINM3     R             98REFFLD(INB1)
     A N10                                  SPACEA(1)
     A N10        RINM4     R             24REFFLD(INB1)
     A N10        RINM5     R             61REFFLD(INB1)
     A N10        RINM6     R             98REFFLD(INB1)
     A N10                                  SPACEA(2)
      *
     A                                     6'Accnt w/ Inst'
     A            RAWI1     R             24REFFLD(INB1)
     A            RAWI2     R             61REFFLD(INB1)
     A  10                                  SPACEA(2)
     A N10        RAWI3     R             98REFFLD(INB1)
     A N10                                  SPACEA(1)
     A N10        RAWI4     R             24REFFLD(INB1)
     A N10        RAWI5     R             61REFFLD(INB1)
     A N10        RAW11     R             98REFFLD(AWIL)
     A N10                                  SPACEA(2)
      *
     A                                     6'Beneficiary'
     A            RBEN1     R             24REFFLD(INB1)
     A            RBEN2     R             61REFFLD(INB1)
     A            RBEN3     R             98REFFLD(INB1)
     A                                      SPACEA(1)
     A            RBEN4     R             24REFFLD(INB1)
     A            RBEN5     R             61REFFLD(INB1)
     A                                      SPACEA(2)
      *
     A                                     6'Remittance Info'
     A            RREM1     R             24REFFLD(INB1)
     A            RREM2     R             61REFFLD(INB1)
     A                                      SPACEA(1)
     A            RREM3     R             24REFFLD(INB1)
     A            RREM4     R             61REFFLD(INB1)
     A                                      SPACEA(2)
      *
     A                                     6'Regulatory Rept'
     A            RREG1     R             24REFFLD(INB1)
     A            RREG2     R             61REFFLD(INB1)
     A            RREG3     R             98REFFLD(INB1)
     A                                      SPACEA(2)
      *
     A N10                                 6'Orig Ccy / Amount'
     A N10        RORCY     R             24REFFLD(BCCY)
     A N10                                28'/'
     A N10        RORAM     R             30REFFLD(PREF)
     A N10                                  SPACEA(2)
      *
     A                                     6'Detail of Charges'
     A            RCHRG     R             25REFFLD(CHINL)
     A                                    40'Receivers Charges Ccy'
     A            RCCY      R             63REFFLD(BCCY)
     A                                    70'Receivers Charges'
     A            RCHAM     R             88REFFLD(TYST)
     A                                      SPACEA(2)
      *
     A N10                                 6'Receivers Charges Pay Ccy'
     A N10        RPCHCY    R             33REFFLD(BCCY)
     A N10                                40'Receivers Charges Pay Amt'
     A N10        RPCHAM    R             70REFFLD(TYST)
     A N10                                  SPACEA(2)
      *
     A N10                                 6'Receivers Charges Tfr Ccy'
     A N10        RTCHCY    R             33REFFLD(BCCY)
     A N10                                40'Receivers Charges Tfr Amt'
     A N10        RTCHAM    R             70REFFLD(TYST)
     A N10                                  SPACEA(2)
      *
     A N10                                 6'Charge Account'
     A N10        RCHAT     R             21REFFLD(CABN)
     A N10                                  SPACEA(2)
      *
     A  10                                 6'Receivers Charges in Pay +
     A                                       Ccy'
     A  10        RPCCCY    R             36REFFLD(TYST)
     A  10                                55'Receivers Charges in Tfr +
     A                                       Ccy'
     A  10        RTCCCY    R             85REFFLD(TYST)
     A  10                                  SPACEA(2)
      *
     A  10                                 6'Senders Charges Ccy'
     A  10        RSCCY     R             28REFFLD(BCCY)
     A  10                                35'Senders Charges'
     A  10        RSCHAM    R             52REFFLD(TYST)
     A  10                                  SPACEA(2)
      *
     A  10                                 6'Sender Charges in Pay +
     A                                       Ccy'
     A  10        RPSCCY    R             33REFFLD(TYST)
     A  10                                55'Senders Charges in Tfr +
     A                                       Ccy'
     A  10        RTSCCY    R             83REFFLD(TYST)
     A  10                                  SPACEA(2)
      *
     A**Start of Change CSW201
     A  10 11 12                           6'Senders Charges Ccy 2'
     A  10 11 12  RSCCY2    R             28REFFLD(BCCY)
     A  10 11 12                          35'Senders Charges 2'
     A  10 11 12  RSCHA2    R             52REFFLD(TYST)
     A  10 11 12                            SPACEA(2)
      *
     A  10 11 12                           6'Sender Charges in Pay +
     A                                       Ccy 2'
     A  10 11 12  RPSCY2    R             33REFFLD(TYST)
     A  10 11 12                          55'Senders Charges in Tfr +
     A                                       Ccy 2'
     A  10 11 12  RTSCY2    R             83REFFLD(TYST)
     A  10 11 12                            SPACEA(2)
      *
     A  10 11 13                           6'Senders Charges Ccy 3'
     A  10 11 13  RSCCY3    R             28REFFLD(BCCY)
     A  10 11 13                          35'Senders Charges 3'
     A  10 11 13  RSCHA3    R             52REFFLD(TYST)
     A  10 11 13                            SPACEA(2)
      *
     A  10 11 13                           6'Sender Charges in Pay +
     A                                       Ccy 3'
     A  10 11 13  RPSCY3    R             33REFFLD(TYST)
     A  10 11 13                          55'Senders Charges in Tfr +
     A                                       Ccy 3'
     A  10 11 13  RTSCY3    R             83REFFLD(TYST)
     A  10 11 13                            SPACEA(2)
      *
     A  10 11 14                           6'Senders Charges Ccy 4'
     A  10 11 14  RSCCY4    R             28REFFLD(BCCY)
     A  10 11 14                          35'Senders Charges 4'
     A  10 11 14  RSCHA4    R             52REFFLD(TYST)
     A  10 11 14                            SPACEA(2)
      *
     A  10 11 14                           6'Sender Charges in Pay +
     A                                       Ccy 4'
     A  10 11 14  RPSCY4    R             33REFFLD(TYST)
     A  10 11 14                          55'Senders Charges in Tfr +
     A                                       Ccy 4'
     A  10 11 14  RTSCY4    R             83REFFLD(TYST)
     A  10 11 14                            SPACEA(2)
      *
     A  10 11 15                           6'Senders Charges Ccy 5'
     A  10 11 15  RSCCY5    R             28REFFLD(BCCY)
     A  10 11 15                          35'Senders Charges 5'
     A  10 11 15  RSCHA5    R             52REFFLD(TYST)
     A  10 11 15                            SPACEA(2)
      *
     A  10 11 15                           6'Sender Charges in Pay +
     A                                       Ccy 5'
     A  10 11 15  RPSCY5    R             33REFFLD(TYST)
     A  10 11 15                          55'Senders Charges in Tfr +
     A                                       Ccy 5'
     A  10 11 15  RTSCY5    R             83REFFLD(TYST)
     A  10 11 15                            SPACEA(2)
      *
     A**End of Change CSW201
     A                                     6'Exchange Rate'
     A            RRATE     R             21REFFLD(FKTT)
     A                                    40'Exchange Rate Override Ind'
     A            RRTIN     R             68REFFLD(ADDC)
     A                                    75'Cheque Number'
     A            RCQNO     R             90REFFLD(CHID)
     A                                       SPACEA(3)
      *
     A          R DETAIL4
      *
     A N10                                 6'RFT ID'
     A  10                                 6'CCT ID'
     A            RTID      R             24REFFLD(ACID)
     A                                      SPACEA(2)
      *
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A**Start of Change 229420
     A***********                         51'***  END OF REPORT  ***'
     A***********                           SPACEB(3)
     A N20                                51'***  END OF REPORT  ***'
     A N20                                  SPACEB(3)
     A  20                                48'** END OF REPORT FOR BRANCH **'
     A  20                                  SPACEB(3)
     A**End of Change 229420
