      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FT Cheque printing input cycle')
     A*****************************************************************
     A*                                                               *
     F*  Midas - FUNDS TRANSFER MODULE
     A*                                                               *
     A*         PRTF/FT0580AU CHEQUES FOR PRINTING INPUT CYCLE        *
     A*                    FILE CONTROLS                              *
      *  (c) Finastra International Limited 2001                      *
     A*     COMPANY CONFIDENTIAL.                                     *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CFT006             Date 01Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01117             Date 15Apr91               *
     A*                                                               *
     A*****************************************************************
      *  MD046248 - Finastra Rebranding                               *
      *  CFT006 - Funds Transfer addition of MT101 and MT102 messages *
     A*     S01117 - NEW SOURCE DERIVED FROM FT0580P2                 *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R FT580AUF                  SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                     6'REFERENCE FT0580AU'
     A            TITL      R             37
     A                                   100'DATE'
     A            RUNA      R            105
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                    50' ' SPACEB(2)
     A                                    51'CHEQUE PRINTING - RUN'
     A                                    73'CONTROL REPORT'
     A                                    50' ' SPACEB(1)
     A                                    51'----------------------'
     A                                    73'--------------' SPACEA(2)
     A                                    91'CALCULATED' SPACEB(3)
     A                                     6'FILE' SPACEB(2)
     A                                    82'NUMBER'
     A                                   103'VALUE' SPACEA(1)
     A                                     6'CHEQUES DUE' SPACEB(3)
     A                                    43'INPUT'
     A            CQINVI        15  0     90EDTCDE(Z)
     A            CQINVD         3  0    105SPACEA(1)
     A                                    27'- LF/CQDUEBB'
     A                                    43'INPUT'
     A            CQINN          5  0     83EDTCDE(Z)
     A  51                                27'- LF/CQDUEOB'
     A  51                                  SPACEB(1)
     A  51                                43'INPUT'
     A  51        CQINNO         5  0     83EDTCDE(Z)
     A*                                                    /CFT006/
     A  72                                27'- RFT/FT102HJ0'
     A  72                                  SPACEB(1)
     A  72                                43'INPUT'
     A  72        CQRCIN         5  0     83EDTCDE(Z)
     A  72 51                             27'- RFT/FT102HJ1'
     A  72 51                               SPACEB(1)
     A  72 51                             43'INPUT'
     A  72 51     CQRCIO         5  0     83EDTCDE(Z)
     A*                                                    /CFT006/
     A                                     6'CHEQUES PRINTED' SPACEB(3)
     A                                    43'OUTPUT'
     A            CQPTN          5  0     83EDTCDE(Z)
     A                                     6'CHEQUE REGISTER' SPACEB(3)
     A                                    27'- PF/CQREGDD'
     A                                    43'OUTPUT'
     A            CQRGN          5  0     83EDTCDE(Z)
     A            CQRGVI        15  0     90EDTCDE(Z)
     A            CQRGVD         3  0    105
     A  88                                 1' ' SPACEB(2)
     A  88                                21'PROGRAM FT0580 TERMINATED'
     A  88                               + 1'AT POINT'
     A  88        DBASE          3  0    + 1
     A  88                               + 1'- DATABASE IN ERROR - FILE'
     A  88        DBFILE         8       + 1
     A  88                               + 1'KEY'
     A  88        DBKEY         29       + 1
     A                                    55'*** END OF REPORT ***'
     A                                      SKIPB(37)
