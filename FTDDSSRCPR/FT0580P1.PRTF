      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60J                                                    *
/*EXI *  TEXT('Midas FT Cheque Printing I/C')
/*ADD *  CHGPRTF FILE(FT0580P1) SCHEDULE(*IMMED)                      *
     A*****************************************************************
     A*                                                               *
     F*  Midas - FUNDS TRANSFER MODULE
     A*                                                               *
     A*         PRTF/FT0580P1 CHEQUES FOR PRINTING INPUT CYCLE        *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     LAST AMEND  NO. 035605     DATE  07MAY93                  *
     A*     PREV AMEND  NO. S01117     DATE  15APR91                  *
     A*                                                               *
     A*****************************************************************
     A*     035605 - CHANGE CREATION PARAMETER 'SCHEDULE' TO *IMMED   *
     A*     S01117 - RELEASE 10 CHANGES                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R FT580PF1                  SKIPB(4)
     A*                                                    /S01117/
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                                    /S01117/
     A                                     5'ORDERING CUSTOMER'
     A            ORCA1         35         5SPACEB(1)
     A            ORCA2         35         5SPACEB(1)
     A            ORCA3         35         5SPACEB(1)
     A            ORCA4         35         5SPACEB(1)
     A            ORCA5         35         5SPACEB(1)
     A                                    42'DATE' SPACEB(1)
     A            RUNA      R             49
     A                                    42'REFERENCE NO.' SPACEB(1)
     A            PREF          15        60
     A                                     5'REGISTERED' SPACEB(1)
     A            BNCA1         35         5SPACEB(1)
     A            BNCA2         35         5SPACEB(1)
     A                                    42'C H E Q U E   R E M I T T A N C E'
     A            BNCA3         35         5SPACEB(1)
     A            BNCA4         35         5SPACEB(1)
     A            BNCA5         35         5SPACEB(1)
     A                                    42'DETAILS OF PAYMENT' SPACEB(2)
     A            DTP1          35        42SPACEB(1)
     A            DTP2          35        42SPACEB(1)
     A            DTP3          35        42SPACEB(1)
     A            DTP4          35        42SPACEB(1) SPACEA(1)
     A            PREFA         15A       60SPACEB(3)
     A            RCBA1         35         5SPACEB(2)
     A            RCBA2         35         5SPACEB(1)
     A            RCBA3         35         5SPACEB(1)
     A            CCY       R             42
     A  27                                60'**,***,***,***.**'
     A  30        CQAM          13  0     60EDTWRD(' ,   ,   ,   , * ')
     A  31        CQAM1         13  1     60EDTWRD('   ,   ,   , * . ')
     A  32        CQAM2         13  2     60EDTWRD('  ,   ,   , * .  ')
     A  33        CQAM3         13  3     60EDTWRD(' ,   ,   , * .   ')
     A            RCBA4         35         5SPACEB(1)
     A            CCNM      R             42
     A            RCBA5         35         5SPACEB(1)
     A            NM3A          35        42SPACEB(1)
     A            NM2A          35        42SPACEB(1)
     A            NM1A          35        42SPACEB(1)
     A            OTDAT          7        55SPACEB(3)
     A            BNCA1B        35         5SPACEB(1)
     A            BNCA2B        35         5SPACEB(1)
     A            BNCA3B        35         5SPACEB(1)
     A            BNCA4B        35         5SPACEB(1)
     A            BNCA5B        35         5SPACEB(1)
     A*
     A          R FT580PF2                  SKIPB(4)
     A            BICNA1        35         5
     A            BICNA2        35         5SPACEB(1)
     A            BICNA3        35         5SPACEB(1)
     A            BICNA4        35         5SPACEB(1)
     A                                     5'REGISTERED' SPACEB(2)
     A                                    42'DATE'
     A            RUNA      R             49
     A            RBKA1         35         5SPACEB(1)
     A                                    42'REFERENCE NO.'
     A            PREF          15        60
     A            RBKA2         35         5SPACEB(1)
     A            RBKA3         35         5SPACEB(1)
     A            RBKA4         35         5SPACEB(1)
     A                                    42'C H E Q U E   R E M I T T A N C E'
     A            RBKA5         35         5SPACEB(1)
     A                                     5'DETAILS OF CHARGES' SPACEB(1)
     A                                    42'DETAILS OF AMOUNTS ADDED'
     A            DCH1          35         5SPACEB(1)
     A            DAA1          35        42
     A            DCH2          35         5SPACEB(1)
     A            DAA2          35        42
     A            DCH3          35         5SPACEB(1)
     A            DAA3          35        42
     A            DCH4          35         5SPACEB(1)
     A            DAA4          35        42
     A            DCH5          35         5SPACEB(1)
     A            DAA5          35        42SPACEA(3)
     A            PREFA         15        60SPACEB(3)
     A            SNBK1         35         5SPACEB(2)
     A            SNBK2         35         5SPACEB(1)
     A            SNBK3         35         5SPACEB(1)
     A            CCY       R             42
     A  27                                60'**,***,***,***.**'
     A  30        CQAM          13  0     60EDTWRD(' ,   ,   ,   , * ')
     A  31        CQAM1         13  1     60EDTWRD('   ,   ,   , * . ')
     A  32        CQAM2         13  2     60EDTWRD('  ,   ,   , * .  ')
     A  33        CQAM3         13  3     60EDTWRD(' ,   ,   , * .   ')
     A            SNBK4         35         5SPACEB(1)
     A            CCNM      R             42
     A            SNBK5         35         5SPACEB(1)
     A            NM3A          35        42SPACEB(1)
     A            NM2A          35        42SPACEB(1)
     A            NM1A          35        42SPACEB(1)
     A            OTDAT          7        55SPACEB(2)
     A            RBKA1B        35         5SPACEB(2)
     A            RBKA2B        35         5SPACEB(1)
     A            RBKA3B        35         5SPACEB(1)
     A            RBKA4B        35         5SPACEB(1)
     A            RBKA5B        35         5SPACEB(1)
