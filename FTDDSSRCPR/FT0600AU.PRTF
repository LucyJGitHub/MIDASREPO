      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FT UNCLEARED CHEQUES AUDIT REPORT')
     A*****************************************************************
     A*                                                               *
     F*  Midas - FUNDS TRANSFER MODULE
     A*                                                               *
     A*    PRTF/FT0600AU UNCLEARED CHEQUES LIST - RUN CONTROLS        *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    LAST AMEND  NO.  S01117            DATE  25MAR91           *
     A*                                                               *
     A*****************************************************************
     A*     S01117 - NEW SOURCE DERIVED FROM FT0600P2                 *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R AUHEAD                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE FT0600AU'
     A            TITL      R             37
     A                                   100'DATE'
     A            RUNA      R            105
     A                                   119'PAGE'
     A            PAGE      R            124EDTCDE(4)
     A*
     A                                    51'UNCLEARED CHEQUES -'
     A                                      SPACEB(2)
     A                                    +1'RUN CONTROLS'
     A*
     A                                    51'~~~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~'
     A                                      SPACEB(1)
     A*
     A  30N32N33                           6'BOOKING BRANCH'
     A                                      SPACEB(2)
     A  30N32 33                           6'ORIGINATING BRANCH'
     A                                      SPACEB(2)
     A  30N32     ABRA      R        O    26REFFLD(BRCA)
     A  30N32     ABRNM     R        O    +2REFFLD(BRNM)
     A*
     A                                    91'CALCULATED'
     A                                      SKIPB(11)
     A*
     A                                     6'FILE'
     A                                      SPACEB(2)
     A                                    82'NUMBER'
     A                                   103'VALUE'
     A*
     A                                     6'UNCLEARED CHEQUES'
     A                                      SKIPB(17)
     A    N33                             27'LF/UNCHQBB'
     A  30 33                             27'LF/UNCHQOB'
     A                                    42'INPUT'
     A            UCOUNT         5S 0     83EDTCDE(3)
     A            UAMT          15S 0     93EDTCDE(3)
     A*
     A N20                                56'NO DETAILS TO REPORT'
     A N20                                  SKIPB(35)
     A                                    56'*** END OF REPORT ***'
     A                                      SKIPB(37)
     A*
     A  78                                21'PROGRAM FT0600 TERMINATED'
     A  78                                  SKIPB(42)
     A  78                               + 1'AT POINT'
     A  78        DBERR#         3  0    + 1
     A  78                               + 1'- DATABASE IN ERROR - FILE'
     A  78        DBFNAM         8       + 1
     A  78                               + 1'KEY'
     A  78        DBERKY        29       + 1
