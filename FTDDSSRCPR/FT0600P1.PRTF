      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FT UNCLEARED CHEQUES REPORT')
     A*****************************************************************
     A*                                                               *
     F*  Midas - FUNDS TRANSFER MODULE
     A*                                                               *
     A*   PRTF/FT0600P1  UNCLEARED CHEQUES LIST                       *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*     COMPANY CONFIDENTIAL.                                     *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01117             Date 26Mar91               *
     A*                                                               *
     A*****************************************************************
      *  MD046248 - Finastra Rebranding                               *
     A*     S01117 - RELEASE 10 MULTIBRANCHING CHANGES                *
     A*****************************************************************
     A*
     A********************************
     A*  REPORT HEADING SECTION.     *
     A********************************
     A*
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R P1HEAD                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*                                                    /S01117/
     A*************************************6'REFERENCE FT0600'
     A                                     6'REFERENCE FT0600P1'
     A*                                                    /S01117/
     A            TITL      R             37
     A                                   100'DATE'
     A            RUNA      R            105
     A                                   119'PAGE'
     A*                                                    /S01117/
     A************PPAGE**********4S*0****124EDTCDE(4)
     A            PAGE           4  0    124EDTCDE(4)
     A*                                                    /S01117/
     A*
     A                                    53'LIST OF UNCLEARED'
     A                                      SPACEB(2)
     A                                    +1'CHEQUES'
     A*
     A                                    53'~~~~~~~~~~~~~~~~~~~~~~~~~'
     A                                      SPACEB(1)
     A*                                                    /S01117/
     A  30N33                              2'BOOKING BRANCH'
     A  30N33                               SPACEB(1)
     A  30 33                              2'ORIGINATING BRANCH'
     A  30 33                               SPACEB(1)
     A  30        SBRA      R        O    23REFFLD(BRCA)
     A  30        PBRNM     R        O    +2REFFLD(BRNM)
     A  30N33                              2'--------------'
     A  30N33                               SPACEB(1)
     A  30 33                              2'------------------'
     A  30 33                               SPACEB(1)
     A*                                                    /S01117/
     A*
     A                                     3'CHEQUE'
     A                                      SPACEB(2)
     A                                    15'TRANSACTION'
     A                                    34'TYPE'
     A                                    76'VALUE'
     A*
     A                                     3'NUMBER'
     A                                      SPACEB(1)
     A                                    16'REFERENCE'
     A                                    32'SUB-TYPE'
     A                                    43'CURRENCY'
     A                                    59'AMOUNT'
     A                                    76'DATE'
     A                                   105'PAYEE'
     A                                      SPACEA(1)
     A*
     A********************************
     A*  REPORT DETAILS SECTION.     *
     A********************************
     A*
     A          R P1DETL                    SPACEB(1)
     A*                                                    /S01117/
     A  30 33                              2'BOOKING BRANCH'
     A  30 33                               SPACEB(1)
     A  30 31N33                           2'ORIGINATING BRANCH'
     A  30 31N33                            SPACEB(1)
     A  30        SBRO      R             23REFFLD(BRCA)
     A  30        PBRNO     R        O    +2REFFLD(BRNM)
     A*                                                    /S01117/
     A            CQNM      R              2
     A*                                                    /S01117/
     A  30 33
     AO 30 31N33                            SPACEB(1)
     A*                                                    /S01117/
     A            PREF          15        13
     A            PYTP      R             33
     A                                    36'-'
     A            PYST      R             38
     A            CQCY      R             46
     A  01        CQAM          13S 0     53EDTCDE(J)
     A  02        CQAM1         13S 1     53EDTCDE(J)
     A  03        CQAM2         13S 2     53EDTCDE(J)
     A  04        CQAM3         13S 3     53EDTCDE(J)
     A            ZADATE         7        75
     A            PYE1      R             91
     A*                                                    /S01117/
     A          R P1ENDR
     A N30
     AO 32                                56'*** END OF REPORT ***'
     A                                      SPACEB(2)
     A  30N32                             50'*** END OF REPORT FOR BRANCH ***'
     A                                      SPACEB(2)
     A*                                                    /S01117/
     A*
