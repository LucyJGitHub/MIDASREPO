      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FT OVERDUE CHEQUES REPORT')
     A*****************************************************************
     A*                                                               *
     F*  Midas - FUNDS TRANSFER MODULE
     A*                                                               *
     A*    PRTF/FT0605P1 OVERDUE CHEQUES LIST                         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*     COMPANY CONFIDENTIAL.                                     *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    LAST AMEND  NO. S01117             DATE  04APR91           *
     A*                                                               *
     A*****************************************************************
     A*     S01117 - RELEASE 10 MULTIBRANCHING CHANGES                *
     A*****************************************************************
     A*
     A********************************
     A*  REPORT HEADING SECTION.     *
     A********************************
     A*
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R P1HEAD                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                                    /S01117/
     A*************************************6'REFERENCE FT0605'
     A                                     6'REFERENCE FT0605P1'
     A*                                                    /S01117/
     A            TITL      R             37REFFLD(TITL MFRFL)
     A                                   100'DATE'
     A            RUNA      R            105REFFLD(RUNA MFRFL)
     A                                   119'PAGE'
     A*                                                    /S01117/
     A************PPAGE**********4S*0****124EDTCDE(4)
     A            PAGE           4  0    124EDTCDE(4)
     A*                                                    /S01117/
     A*
     A                                    53'LIST OF OVERDUE'
     A                                      SPACEB(2)
     A                                    +1'CHEQUES'
     A*
     A                                    53'~~~~~~~~~~~~~~~~~~~~~~~'
     A                                      SPACEB(1)
     A*                                                    /S01117/
     A  35N38                              2'BOOKING BRANCH'
     A  35N38                               SPACEB(1)
     A  35 38                              2'ORIGINATING BRANCH'
     A  35 38                               SPACEB(1)
     A  35        SBRA      R        O    23REFFLD(BRCA)
     A  35        PBRNM     R        O    +2REFFLD(BRNM)
     A  35N38                              2'--------------'
     A  35N38                               SPACEB(1)
     A  35 38                              2'------------------'
     A  35 38                               SPACEB(1)
     A*                                                    /S01117/
     A*
     A                                     3'CHEQUE'
     A                                      SPACEB(2)
     A                                    15'TRANSACTION'
     A                                    34'TYPE'
     A                                    76'VALUE'
     A*
     A                                     3'NUMBER'
     A                                      SPACEB(1)
     A                                    16'REFERENCE'
     A                                    32'SUB-TYPE'
     A                                    43'CURRENCY'
     A                                    59'AMOUNT'
     A                                    76'DATE'
     A                                   105'PAYEE'
     A                                      SPACEA(1)
     A*
     A********************************
     A*  REPORT DETAILS SECTION.     *
     A********************************
     A*
     A          R P1DETL                    SPACEB(1)
     A*                                                    /S01117/
     A  35 38                              2'BOOKING BRANCH'
     A  35 38                               SPACEB(1)
     A  35 36N38                           2'ORIGINATING BRANCH'
     A  35 36N38                            SPACEB(1)
     A  35        SBRO      R             23REFFLD(BRCA)
     A  35        PBRNO     R        O    +2REFFLD(BRNM)
     A*                                                    /S01117/
     A            CQNM      R              2
     A*                                                    /S01117/
     A  35 38
     AO 35 36N38                            SPACEB(1)
     A*                                                    /S01117/
     A            PREF          15        13
     A            PYTP      R             33
     A                                    36'-'
     A            PYST      R             38
     A            CQCY      R             46
     A  01        CQAM          13S 0     53EDTCDE(J)
     A  02        CQAM1         13S 1     53EDTCDE(J)
     A  03        CQAM2         13S 2     53EDTCDE(J)
     A  04        CQAM3         13S 3     53EDTCDE(J)
     A            ZADATE         7        75
     A            PYE1      R             91
     A*                                                    /S01117/
     A          R P1ENDR
     A N35
     AO 37                                56'*** END OF REPORT ***'
     A                                      SPACEB(2)
     A  35N37                             50'*** END OF REPORT FOR BRANCH ***'
     A                                      SPACEB(2)
     A*                                                    /S01117/
     A*
