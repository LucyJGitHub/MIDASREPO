      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FT BANK TRNSFERS VIA SWIFT MT202 NON-I/C')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FUNDS TRANSFER MODULE                                *
     A*                                                               *
     A*         PRTF/FT0646P1 BANK TRANSFERS SWIFT REPORT             *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*      COMPANY CONFIDENTIAL                                     *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. BUG24462           Date 19Jun09               *
      *                 CSW209             Date 01Apr09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 S01408             Date  04Nov96              *
     A*                     S01310     DATE  08AUG91                  *
     A*                     E29845     DATE  04DEC90                  *
     A*                     S01117     DATE  09OCT90                  *
     A*                                                               *
     A*****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG24462 - Ordering customer cover value in FTC0645P1 is     *
      *             missing                                           *
      *  CSW209 - SWIFT 2009 Changes                                  *
     A*     S01408 - Addition of core hook FT0646P104                 *
     A*              Addition of core hook FT0646P103                 *
     A*              Addition of core hook FT0646P102                 *
     A*              Addition of core hook FT0646P101                 *
     A*     S01310 - SWIFT RATIONALISATION                            *
     A*     E29845 - ADJUSTMENT OF HEADER                             *
     A*     S01117 - RELEASE 10 MULTIBRANCHING CHANGES                *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*/COPY WNCPYSRC,FT0646P105
     A          R P1H1                      SKIPB(3)
     A*                                                               /S01117/
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A*************************************6'REFERENCE FT0646'
     A                                     6'REFERENCE FT0646P1'
     A*                                                               /S01117/
     A            STITL         53        37
     A                                   100'DATE'
     A            RUNA           7       105
     A                                   119'PAGE'
     A            PAGEP1         4S 0    124EDTCDE(Z)
     A*
     A*                                                               /E29845/
     A************************************45'FUNDS TRANSFER GENERATED   SWIFT +
     A************************************   MESSAGES'
     A                                    45'FUNDS TRANSFER GENERATED SWIFT +
     A                                       MESSAGES'
     A*                                                               /E29845/
     A                                      SPACEB(2)
     A  13                               100'TIME'
     A  13        TIME           6  0    105EDTWRD('  :  :  ')
     A  13                               119'EXCEPTION'
     A*
     A                                    44'~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~'
     A                                      SPACEB(1)
     A*
     A                                    19'PAYMENT MESSAGE FOR'
     A                                      SPACEB(3)
     A*                                                             /S01310/
     A************MXTREF********15        39TEXT('PAYMNT MSG FOR')
     A            MTRN          15        39TEXT('PAYMNT MSG FOR')
     A*                                                             /S01310/
     A                                      SPACEA(1)
     A*
     A            M1OTID        12        41TEXT(' OUR TID ')
     A                                      SPACEB(2)
     A*
      *                                               Start /S01408/
     A*/COPY WNCPYSRC,FT0646P101
      *                                                 End /S01408/
      ** Start of Change CSW209
     A*********                           41'202'                                             CSW209
     A            M1MTPY         3        41TEXT('Message Type')
      ** End of Change CSW209
     A                                      SPACEB(1)
     A*                                                    /S01117/
      *                                               Start /S01408/
     A*/COPY WNCPYSRC,FT0646P102
      *                                                 End /S01408/
     A************M1MSGP*********2********45
     A            M1MSGP         1        45
     A*                                                    /S01117/
     A*
     A*/COPY WNCPYSRC,FT0646P107
     A            M1RADD        12        41TEXT('RECIPIENT ADDR')
     A                                      SPACEB(1)
     A*
      ** Start of Change CSW209
     A  29                                25'BLOCK 3 DATA    :119:'
     A  29                                  SPACEB(1)
     A  29        M1BLK3        10        46TEXT('BLOCK 3 DATA')
      ** End of Change CSW209
     A*/COPY WNCPYSRC,FT0646P108
     A                                    34'TRN    :20:'
     A                                      SPACEB(2)
     A*/COPY WNCPYSRC,FT0646P109
     A*                                                             /S01310/
     A            M1TREF        16        45TEXT('TRANSFER      ')
     A            TRNA          16        45TEXT('TRANSFER      ')
     A*                                                             /S01310/
     A                                    20'RELATED REFERENCE    :21:'
     A                                      SPACEB(1)
     A            M1RREF        16        45TEXT('RELATED REFERENCE')
     A*
     A                                    11'VALUE DATE/CURRENCY/AMOUNT'
     A                                      SPACEB(1)
     A                                    41':32A:'
     A            M1VDAT         6        46TEXT('VALUE DATE')
     A            M1RCCY         3        52TEXT('CURRENCY')
     A            M1AMNT        15        55TEXT('AMOUNT')
     A*
     A                                    24'ORDERING BANK    :52'
     A                                      SPACEB(1)
     A            M1OBTG         1        44TEXT('ORDERING BANK TAG TYPE')
     A                                    45':'
     A            M1OBK1        37        46TEXT('ORDERING BANK 1')
     A            M1OBK2        35        46TEXT('ORDERING BANK 2')
     A                                      SPACEB(1)
     A            M2OBK3        35        46TEXT('ORDERING BANK 3')
     A                                      SPACEB(1)
     A            M2OBK4        35        46TEXT('ORDERING BANK 4')
     A                                      SPACEB(1)
     A            M2OBK5        35        46TEXT('ORDERING BANK 5')
     A                                      SPACEB(1)
     A*
     A                                    15'SENDER''S CORRESPONDENT    :53'
     A                                      SPACEB(1)
     A            M2SCTT         1        44TEXT('SENDERS"S CORRESPONDENT TAG')
     A                                    45':'
     A            M2SCR1        37        46TEXT('SENDERS"S CORRESPONDENT 1')
     A            M2SCR2        35        46TEXT('SENDERS"S CORRESPONDENT 2')
     A                                      SPACEB(1)
     A            M2SCR3        35        46TEXT('SENDERS"S CORRESPONDENT 3')
     A                                      SPACEB(1)
     A            M3SCR4        35        46TEXT('SENDERS"S CORRESPONDENT 4')
     A                                      SPACEB(1)
     A            M3SCR5        35        46TEXT('SENDERS"S CORRESPONDENT 5')
     A                                      SPACEB(1)
     A*
      *                                               Start /S01408/
     A*/COPY WNCPYSRC,FT0646P103
      *                                                 End /S01408/
     A                                    13'RECEIVER''S CORRESPONDENT    :54'
     A                                      SPACEB(1)
     A            M3RCTT         1        44TEXT('RECEIVERS"S CORRESPONDNT TAG')
     A                                    45':'
     A            M3RCR1        37        46TEXT('RECEIVERS"S CORRESPONDENT 1')
     A            M3RCR2        35        46TEXT('RECEIVERS"S CORRESPONDENT 2')
     A                                      SPACEB(1)
     A            M3RCR3        35        46TEXT('RECEIVERS"S CORRESPONDENT 3')
     A                                      SPACEB(1)
     A            M3RCR4        35        46TEXT('RECEIVERS"S CORRESPONDENT 4')
     A                                      SPACEB(1)
     A            M4RCR5        35        46TEXT('RECEIVERS"S CORRESPONDENT 5')
     A                                      SPACEB(1)
     A*
      *                                               Start /S01408/
     A*/COPY WNCPYSRC,FT0646P104
      *                                                 End /S01408/
     A                                    20'INTERMEDIARY BANK    :56'
     A                                      SPACEB(1)
     A            P1IBKT         1        44TEXT('INTERMEDIARY BANK TAG TYPE')
     A                                    45':'
     A            P1IBK1        37        46TEXT('INTERMEDIARY BANK 1')
     A            P1IBK2        35        46TEXT('INTERMEDIARY BANK 2')
     A                                      SPACEB(1)
     A            P1IBK3        35        46TEXT('INTERMEDIARY BANK 3')
     A                                      SPACEB(1)
     A            P1IBK4        35        46TEXT('INTERMEDIARY BANK 4')
     A                                      SPACEB(1)
     A            P1IBK5        35        46TEXT('INTERMEDIARY BANK 5')
     A                                      SPACEB(1)
     A*
     A                                    18'''ACCOUNT WITH'' BANK    :57'
     A                                      SPACEB(1)
     A            M5ABTT         1        44TEXT('ACCOUNT WITH BANK TAG TYPE')
     A                                    45':'
     A            M5AWB1        37        46TEXT('ACCOUNT WITH BANK 1')
     A            M5AWB2        35        46TEXT('ACCOUNT WITH BANK 2')
     A                                      SPACEB(1)
     A            M5AWB3        35        46TEXT('ACCOUNT WITH BANK 3')
     A                                      SPACEB(1)
     A            M5AWB4        35        46TEXT('ACCOUNT WITH BANK 4')
     A                                      SPACEB(1)
     A            M5AWB5        35        46TEXT('ACCOUNT WITH BANK 5')
     A                                      SPACEB(1)
     A*
     A                                    20'BENEFICIARY BANK     :58'
     A                                      SPACEB(1)
     A            M5BBTT         1        44TEXT('BENEFICIARY BANK TAG TYPE')
     A                                    45':'
     A            M5BBK1        37        46TEXT('BENEFICIARY BANK 1')
     A            M6BBK2        35        46TEXT('BENEFICIARY BANK 2')
     A                                      SPACEB(1)
     A            M6BBK3        35        46TEXT('BENEFICIARY BANK 3')
     A                                      SPACEB(1)
     A            M6BBK4        35        46TEXT('BENEFICIARY BANK 4')
     A                                      SPACEB(1)
     A            M6BBK5        35        46TEXT('BENEFICIARY BANK 5')
     A                                      SPACEB(1)
     A*
     A                                    13'BANK TO BANK INFORMATION    :72:'
     A                                      SPACEB(1)
     A            M6BTB1        35        45TEXT('BANK TO BANK 1')
     A            M6BTB2        35        45TEXT('BANK TO BANK 2')
     A                                      SPACEB(1)
     A            M7BTB3        35        45TEXT('BANK TO BANK 3')
     A                                      SPACEB(1)
     A            M7BTB4        35        45TEXT('BANK TO BANK 4')
     A                                      SPACEB(1)
     A            M7BTB5        35        45TEXT('BANK TO BANK 5')
     A                                      SPACEB(1)
     A            M7BTB6        35        45TEXT('BANK TO BANK 6')
     A                                      SPACEB(1)
     A*
      ** Start of Change CSW209
     A  29                                14'ORDERING CUSTOMER (Cover)  :50'
     A  29                                  SPACEB(1)
     A  29        M1COCT         1        44TEXT('ORDERING CUST (Cover) TYPE')
     A  29                                45':'
     A  29        M1COC1        35        46TEXT('ORDERING CUST (Cover) 1')
     A  29        M1COC2        35        46TEXT('ORDERING CUST (Cover) 2')
     A  29                                  SPACEB(1)
     A  29        M1COC3        35        46TEXT('ORDERING CUST (Cover) 3')
     A  29                                  SPACEB(1)
     A  29        M1COC4        35        46TEXT('ORDERING CUST (Cover) 4')
     A  29                                  SPACEB(1)
     A  29        M1COC5        35        46TEXT('ORDERING CUST (Cover) 5')
     A  29                                  SPACEB(1)
      *
     A  29                                11'ORDERING INSTITUTION (Cover)  :52'
     A  29                                  SPACEB(1)
     A  29        M1COBT         1        44TEXT('ORDERING INST (Cover) TYPE')
     A  29                                45':'
     A  29        M1COB1        35        46TEXT('ORDERING INSTITUTION Cover 1')
     A  29        M1COB2        35        46TEXT('ORDERING INSTITUTION Cover 2')
     A  29                                  SPACEB(1)
     A  29        M1COB3        35        46TEXT('ORDERING INSTITUTION Cover 3')
     A  29                                  SPACEB(1)
     A  29        M1COB4        35        46TEXT('ORDERING INSTITUTION Cover 4')
     A  29                                  SPACEB(1)
     A  29        M1COB5        35        46TEXT('ORDERING INSTITUTION Cover 5')
     A  29                                  SPACEB(1)
      *
     A  29                                 8'INTERMEDIARY INSTITUTION (Cover) +
     A                                      :56'
     A  29                                  SKIPB(1)
     A  29                                  SPACEB(1)
     A  29        M1CIBT         1        44TEXT('INTERMEDIARY INST Cover TYPE')
     A  29                                45':'
     A  29        M1CIB1        35        46TEXT('INTERMEDIARY INST Cover 1')
     A  29        M1CIB2        35        46TEXT('INTERMEDIARY INST Cover 2')
     A  29                                  SPACEB(1)
     A  29        M1CIB3        35        46TEXT('INTERMEDIARY INST Cover 3')
     A  29                                  SPACEB(1)
     A  29        M1CIB4        35        46TEXT('INTERMEDIARY INST Cover 4')
     A  29                                  SPACEB(1)
     A  29        M1CIB5        35        46TEXT('INTERMEDIARY INST Cover 5')
     A  29                                  SPACEB(1)
      *
     A  29                                 8'ACCOUNT WITH INSTITUTION (Cover) +
     A                                      :57'
     A  29                                  SPACEB(1)
     A  29        M1CABT         1        44TEXT('ACCOUNT WITH INST Cover Type')
     A  29                                45':'
     A  29        M1CAB1        35        46TEXT('ACCOUNT WITH INST Cover 1')
     A  29        M1CAB2        35        46TEXT('ACCOUNT WITH INST Cover 2')
     A  29                                  SPACEB(1)
     A  29        M1CAB3        35        46TEXT('ACCOUNT WITH INST Cover 3')
     A  29                                  SPACEB(1)
     A  29        M1CAB4        35        46TEXT('ACCOUNT WITH INST Cover 4')
     A  29                                  SPACEB(1)
     A  29        M1CAB5        35        46TEXT('ACCOUNT WITH INST Cover 5')
     A  29                                  SPACEB(1)
      *
     A  29                                13'BENEFICIARY CUSTOMER (Cover)'
     A  29                                  SPACEB(1)
      ** Start of Change BUG24462
     A**29******                          41':59'                                           BUG24462
     A  29 27                             41':59'
      ** End of Change BUG24462
     A  29 27     M1CBCT         1        44
     A  29 27                             45':'
     A  29N27                             41':59:'
     A  29        M1CBC1        35        46TEXT('BENEFICIARY CUSTOMER Cover 1')
     A  29        M1CBC2        35        46TEXT('BENEFICIARY CUSTOMER Cover 2')
     A  29                                  SPACEB(1)
     A  29        M1CBC3        35        46TEXT('BENEFICIARY CUSTOMER Cover 3')
     A  29                                  SPACEB(1)
     A  29        M1CBC4        35        46TEXT('BENEFICIARY CUSTOMER Cover 4')
     A  29                                  SPACEB(1)
     A  29        M1CBC5        35        46TEXT('BENEFICIARY CUSTOMER Cover 5')
     A  29                                  SPACEB(1)
      *
     A  29                                12'REMITANCE INFORMATION (Cover) :70:'
     A  29                                  SPACEB(1)
     A  29        M1CDP1        35        46TEXT('REMITANCE INFO (Cover) 1')
     A  29        M1CDP2        35        46TEXT('REMITANCE INFO (Cover) 2')
     A  29                                  SPACEB(1)
     A  29        M1CDP3        35        46TEXT('REMITANCE INFO (Cover) 3')
     A  29                                  SPACEB(1)
     A  29        M1CDP4        35        46TEXT('REMITANCE INFO (Cover) 4')
     A  29                                  SPACEB(1)
      *
     A  29                                10'SENDER TO RECEIVER INFO (Cover) +
     A                                       :72:'
     A  29                                  SPACEB(1)
     A  29        M1CBB1        35        46TEXT('SNDR TO RCVER INFO Cover 1')
     A  29        M1CBB2        35        46TEXT('SNDR TO RCVER INFO Cover 2')
     A  29                                  SPACEB(1)
     A  29        M1CBB3        35        46TEXT('SNDR TO RCVER INFO Cover 3')
     A  29                                  SPACEB(1)
     A  29        M1CBB4        35        46TEXT('SNDR TO RCVER INFO Cover 4')
     A  29                                  SPACEB(1)
     A  29        M1CBB5        35        46TEXT('SNDR TO RCVER INFO Cover 5')
     A  29                                  SPACEB(1)
     A  29        M1CBB6        35        46TEXT('SNDR TO RCVER INFO Cover 6')
     A  29                                  SPACEB(1)
      *
     A  29                                11'INSTRUCTED CCY/AMOUNT (Cover) +
     A                                       :33B:'
     A  29                                  SPACEB(1)
     A  29        M1R33B        18        46TEXT('INSTRUCTED CCY/AMOUNT Cover')
      ** End of Change CSW209
     A*/COPY WNCPYSRC,FT0646P106
