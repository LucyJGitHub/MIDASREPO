      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FT Request for transfer SWIFT MT101 i/c')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PRTF/FT0657P1 - Request for Transfer SWIFT Report            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CSW201             Date 02May01               *
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CFT006  *CREATE    Date 13Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW201 - SWIFT 2001 Standards Update                         *
      *  CFT006 - Funds Transfer/Swift Messages MT101/MT102           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R P1H1                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'FT0657P1'
      *
     A            STITL         53        37
     A                                   100'DATE'
     A            RUNA           7       105
     A                                   119'PAGE'
     A            PAGEP1         4S 0    124EDTCDE(Z)
      *
     A                                    46'FUNDS TRANSFER GENERATED SWIFT +
     A                                       MESSAGES'
      *
     A                                      SPACEB(2)
     A  13                               100'TIME'
     A  13        TIME           6  0    105EDTWRD('  :  :  ')
     A  13                               119'EXCEPTION'
      *
     A                                    46'~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~'
     A                                      SPACEB(1)
      *
     A                                    14'REQUEST FOR TRANSFER +
     A                                      FOR'
     A                                      SPACEB(3)
      *
     A            RRFTID        15        41
      *
     A            ROTID         12        41
     A                                      SPACEB(2)
      *
     A                                    41'101'
     A                                      SPACEB(1)
     A            RSWPC          1        46
      *
     A            RADDR         12        41
     A                                      SPACEB(1)
      *
     A                                    19'SENDER''S REFERENCE    :20:'
     A                                      SPACEB(2)
     A            TRNA          18        46
      *
     A                                     9'CUSTOMER SPECIFIED +
     A                                      REFERENCE'
     A                                      SPACEB(1)
     A                                    41':21R:'
     A            RCUSTR        16        46
 
      ** Start of change CSW201
     A  27                                18'MESSAGE INDEX/TOTAL'
     A  27                                   SPACEB(1)
     A  27                                41':28D:'
     A  27        RMSGIT        11        46
      *
     A**********                          20'INSTRUCTING PARTY    :50L:'                      CSW201
     A                                    20'INSTRUCTING PARTY    :50a:'
      ** End of change CSW201
 
     A                                      SPACEB(1)
     A            RINPTY        35        46
      *
      ** Start of change CSW201
     A**********                          20'ORDERING CUSTOMER    :50H:'                      CSW201
     A                                    20'ORDERING CUSTOMER    :50a:'
      ** End of change CSW201
 
     A                                      SPACEB(1)
     A            RORDC1        35        46
     A            RORDC2        35        46SPACEB(1)
     A            RORDC3        35        46SPACEB(1)
     A            RORDC4        35        46SPACEB(1)
     A            RORDC5        35        46SPACEB(1)
      *
     A                                     8'ACCOUNT SERVICING +
     A                                      INSTITUTION    :52a:'
     A                                      SPACEB(1)
     A            RACSV1        35        46
     A            RACSV2        11        46SPACEB(1)
      *
     A                                    18'SENDING INSTITUTION    :51A:'
     A                                      SPACEB(1)
     A            RSINST        35        46
      *
     A                                    13'REQUESTED EXECUTION DATE'
     A                                      SPACEB(1)
     A                                    41':30:'
     A            REXDTE         6        46
      *
     A                                    24'AUTHORISATION    :25:'
     A                                      SPACEB(1)
     A            RAUTHX        35        46
      *
      *--------------------------------*
      ***  Report Detail Record Format *
      *--------------------------------*
      *
     A          R P1D1
      *
     A                                      SKIPB(1)
     A                                     6'FT0657P1'
      *
     A            STITL         53        37
     A                                   100'DATE'
     A            RUNA           7       105
     A                                   119'PAGE'
     A            PAGEP1         4S 0    124EDTCDE(Z)
      *
     A                                    46'FUNDS TRANSFER GENERATED SWIFT +
     A                                       MESSAGES'
      *
     A                                      SPACEB(2)
     A  13                               100'TIME'
     A  13        TIME           6  0    105EDTWRD('  :  :  ')
     A  13                               119'EXCEPTION'
      *
     A                                    46'~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~'
     A                                      SPACEB(1)
      *
     A                                    14'REQUEST FOR TRANSFER +
     A                                      FOR'
     A                                      SPACEB(3)
      *
     A            RRFTID        15        41
      *
     A            ROTID         12        41
     A                                      SPACEB(2)
      *
     A                                    41'101'
     A                                      SPACEB(1)
     A            RSWPC          1        46
      *
     A            RADDR         12        41
     A                                      SPACEB(1)
      *
     A                                    16'TRANSACTION REFERENCE'
     A                                      SPACEB(2)
     A                                    41':21:'
     A            RTRNRF        16        46
      *
     A                                    19'F/X DEAL REFERENCE'
     A                                      SPACEB(1)
     A                                    41':21F:'
     A            RFXDLR        16        46
      *
     A                                    21'INSTRUCTION CODE'
     A                                      SPACEB(1)
     A                                    41':23E:'
     A            RINSC1         4        46
     A            RINSC2         4        46SPACEB(1)
     A            RINSC3         4        46SPACEB(1)
     A            RINSC4         4        46SPACEB(1)
     A            RINSC5         4        46SPACEB(1)
     A            RINSC6        31        46SPACEB(1)
     A            RINSC7        31        46SPACEB(1)
      *
     A                                    10'CURRENCY/TRANSACTION +
     A                                      AMOUNT'
     A                                      SPACEB(1)
     A                                    41':32B:'
     A            R32B          18        46
      *
      ** Start of change CSW201
     A**********                          20'INSTRUCTING PARTY    :50L:'                      CSW201
     A                                    20'INSTRUCTING PARTY    :50a:'
      ** End of change CSW201
 
     A                                      SPACEB(1)
     A            RDINPY        35        46
      *
      ** Start of change CSW201
     A**********                          20'ORDERING CUSTOMER    :50H:'                      CSW201
     A                                    20'ORDERING CUSTOMER    :50a:'
      ** End of change CSW201
 
     A                                      SPACEB(1)
     A            RDORC1        35        46
     A            RDORC2        35        46SPACEB(1)
     A            RDORC3        35        46SPACEB(1)
     A            RDORC4        35        46SPACEB(1)
     A            RDORC5        35        46SPACEB(1)
      *
     A                                     8'ACCOUNT SERVICING +
     A                                      INSTITUTION    :52a:'
     A                                      SPACEB(1)
     A            RDACS1        35        46
     A            RDACS2        11        46SPACEB(1)
      *
     A                                    25'INTERMEDIARY    :56a:'
     A                                      SPACEB(1)
     A            RINT1         35        46
     A            RINT2         35        46SPACEB(1)
     A            RINT3         35        46SPACEB(1)
     A            RINT4         35        46SPACEB(1)
     A            RINT5         35        46SPACEB(1)
     A            RINT6         11        46SPACEB(1)
      *
     A                                    13'ACCOUNT WITH INSTITUTION'
     A                                      SPACEB(1)
     A                                    41':57a:'
     A            RACI1         35        46
     A            RACI2         35        46SPACEB(1)
     A            RACI3         35        46SPACEB(1)
     A            RACI4         35        46SPACEB(1)
     A            RACI5         35        46SPACEB(1)
     A            RACI6         11        46SPACEB(1)
      *
      ** Start of change CSW201
 
     A**********                          26'BENEFICIARY    :59:'                             CSW201
     A                                    26'BENEFICIARY    :59a:'
      ** End of change CSW201
 
     A                                      SPACEB(1)
 
     A            RBEN1         35        46
     A            RBEN2         35        46SPACEB(1)
     A            RBEN3         35        46SPACEB(1)
     A            RBEN4         35        46SPACEB(1)
     A            RBEN5         35        46SPACEB(1)
      *
     A                                    15'REMITTANCE INFORMATION'
     A                                      SPACEB(1)
     A                                    41':70:'
     A            RREM1         35        46
     A            RREM2         35        46SPACEB(1)
     A            RREM3         35        46SPACEB(1)
     A            RREM4         35        46SPACEB(1)
      *
     A                                    17'REGULATORY REPORTING'
     A                                      SPACEB(1)
     A                                    41':77B:'
     A            RREG1         35        46
     A            RREG2         35        46SPACEB(1)
     A            RREG3         35        46SPACEB(1)
      *
     A                                     5'CURRENCY/ORIGINAL +
     A                                      ORDERED AMOUNT'
     A                                      SPACEB(1)
     A                                    41':33B:'
     A            R33B          18        46
      *
     A                                    19'DETAILS OF CHARGES'
     A                                      SPACEB(1)
     A                                    41':71A:'
     A            RDETCH         3        46
      *
     A                                    22'CHARGES ACCOUNT    :25A:'
     A                                      SPACEB(1)
     A            RCHRAC        35        46
      *
     A                                    24'EXCHANGE RATE    :36:'
     A                                      SPACEB(1)
     A            RXRATE        12        46
      *
