      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FT Customer credit transfer SWIFT MT102 i/c')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PRTF/FT0658P1 - Customer Credit Transfer SWIFT Report        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CSW207             Date 17Jun07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSW201             Date 02May01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 184060             Date 22Sep00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CFT006  *CREATE    Date 13Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW207 - SWIFT 2007 Changes                                  *
      *  CSW201 - SWIFT 2001 Standards Update                         *
      *  184060 - No transaction type code in detail screen           *
      *  CFT006 - Funds Transfer/Swift Messages MT101/MT102           *
      *                                                               *
      *****************************************************************
     A**************************************REF(MFRFL)
     A                                      REF(FT102DPD)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R P1H1                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'FT0658P1'
      *
     A            STITL         53        37
     A                                   100'DATE'
     A            RUNA           7       105
     A                                   119'PAGE'
     A            PAGEP1         4S 0    124EDTCDE(Z)
      *
     A                                    45'CUSTOMER CREDIT TRANSFER SWIFT +
     A                                       MESSAGES'
      *
     A                                      SPACEB(2)
     A  13                               100'TIME'
     A  13        TIME           6  0    105EDTWRD('  :  :  ')
     A  13                               119'EXCEPTION'
      *
     A                                    45'~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~'
     A                                      SPACEB(1)
      *
     A                                    12'CUSTOMER CREDIT TRANSFER +
     A                                      FOR'
     A                                      SPACEB(3)
      *
     A            RCCTID        15        42
      *
     A            ROTID         12        41
     A                                      SPACEB(2)
      *
     A                                    41'102'
     A                                      SPACEB(1)
     A            RSWPC          1        45
      *
     A            RADDR         12        41
     A                                      SPACEB(1)
      *
     A                                    34'TRN    :20:'
     A                                      SPACEB(2)
     A            TRNA          16        46
      *
     A                                    18'BANK OPERATION CODE'
     A
     A                                      SPACEB(1)
     A                                    41':23:'
     A            RCCTBOC        6        46
      *
     A                                    18'SENDING INSTITUTION'
     A                                      SPACEB(1)
     A                                    41':51A:'
     A            RSINST        35        46
      *
      ** Start of change CSW201
     A**********                          20'ORDERING CUSTOMER    :50: '                      CSW201
     A                                    20'ORDERING CUSTOMER    :50a:'
      ** End of change CSW201
 
     A                                      SPACEB(1)
     A            RORDC1        35        46
     A            RORDC2        35        46SPACEB(1)
     A            RORDC3        35        46SPACEB(1)
     A            RORDC4        35        46SPACEB(1)
 
      ** Start of change CSW201
     A            RORDC5        35        46SPACEB(1)
      ** End of change CSW201
      *
     A                                    17'ORDERING INSTITUTION'
     A                                      SPACEB(1)
     A                                    41':52'
     A            RORINS         1        44
     A                                    45':'
     A            ROINS1        35        46
     A            ROINS2        35        46SPACEB(1)
      *
     A                                    16'TRANSACTION TYPE CODE'
     A                                    41':26T:'
     A            RTRNTP         3        46SPACEB(1)
     A                                    17'REGULATORY REPORTING'
     A                                    41':77B:'
     A            RREGR1        35        46
     A            RREGR2        35        46SPACEB(1)
     A            RREGR3        35        46SPACEB(1)
      *
     A                                    19'DETAILS OF CHARGES'
     A                                      SPACEB(1)
     A                                    41':71A:'
     A            RDCHG          3        46
      *
     A                                    24'EXCHANGE RATE'
     A                                      SPACEB(1)
     A                                    41':36:'
     A            REXRAT        12        46
      * ===============================================================
     A          R P1D1                      SKIPB(3)
      *
     A                                     6'FT0658P1'
      *
     A            STITL         53        37
     A                                   100'DATE'
     A            RUNA           7       105
     A                                   119'PAGE'
     A            PAGEP1         4S 0    124EDTCDE(Z)
      *
     A                                    45'CUSTOMER CREDIT TRANSFER SWIFT +
     A                                       MESSAGES'
      *
     A                                      SPACEB(2)
     A  13                               100'TIME'
     A  13        TIME           6  0    105EDTWRD('  :  :  ')
     A  13                               119'EXCEPTION'
      *
     A                                    45'~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~'
     A                                      SPACEB(1)
      *
     A                                    12'CUSTOMER CREDIT TRANSFER +
     A                                      FOR'
     A                                      SPACEB(3)
      *
     A            TRNSID        16A       42
      * Transaction Reference
     A************                        16'TRANSACTION REFERENCE'
     A*                                     SPACEB(1)
     A*                                   41':21:'
     A*           PFILREF       16A       46
      * Transaction Amount
     A                                    19'TRANSACTION AMOUNT'
     A                                      SPACEB(1)
     A                                    41':32B:'
     A            CCTAMT        15  0     46
      * Ordering Customer
     A                                    20'ORDERING CUSTOMER'
     A                                      SPACEB(1)
 
      ** Start of change CSW201
     A**********                          41':50:'                                            CSW201
     A                                    41':50a:'
      ** End of change CSW201
 
     A            ORDCU1        35A       46
     A            ORDCU2        35A       46SPACEB(1)
     A            ORDCU3        35A       46SPACEB(1)
     A            ORDCU4        35A       46SPACEB(1)
     A**Start of Change CSW207
     A            ORDCU5        35A       46SPACEB(1)
     A**End of Change CSW207
      * Ordering Institution
     A                                    17'ORDERING INSTITUTION'
     A                                      SPACEB(1)
     A                                    41':52a:'
     A            ORDI35        35A       46
     A            ORDI11        11A       46SPACEB(1)
      * Account With Institution
     A                                    13'ACCOUNT WITH INSTITUTION'
     A                                      SPACEB(1)
     A                                    41':57a:'
     A            ACCI35        35A       46
     A            ACCI11        11A       46SPACEB(1)
      * Beneficiary
     A                                    26'BENEFICIARY'
     A                                      SPACEB(1)
 
      ** Start of change CSW201
     A**********                          41':59:'                                            CSW201
     A                                    41':59a:'
      ** End of change CSW201
 
     A            BENF1         35A       46
     A            BENF2         35A       46SPACEB(1)
     A            BENF3         35A       46SPACEB(1)
     A            BENF4         35A       46SPACEB(1)
     A            BENF5         35A       46SPACEB(1)
      * Remittance Information
     A                                    15'REMITTANCE INFORMATION'
     A                                      SPACEB(1)
     A                                    41':70:'
     A            REMI1         35A       46
     A            REMI2         35A       46SPACEB(1)
     A            REMI3         35A       46SPACEB(1)
     A            REMI4         35A       46SPACEB(1)
      * Transaction Type Code
     A                                    16'TRANSACTION TYPE CODE'
     A                                      SPACEB(1)
     A                                    41':26T:'
      *                                              Begin /184060/
     A************CCTTYC         2A       46
     A            CCTTYC         3A       46
      *                                              End   /184060/
      * Regulatory Reporting
     A                                    17'REGULATORY REPORTING'
     A                                      SPACEB(1)
     A                                    41':77B:'
     A            REGRP1        35A       46
     A            REGRP2        35A       46SPACEB(1)
     A            REGRP3        35A       46SPACEB(1)
      * Original Ordered Amount
     A                                    14'ORIGINAL ORDERED AMOUNT'
     A                                      SPACEB(1)
     A                                    41':33B:'
     A            ORIGAM        15  0     46
      * Details of Charges
     A                                    19'DETAILS OF CHARGES'
     A                                      SPACEB(1)
     A                                    41':71A:'
     A            CCYCHG         3A       46
      * Senders Charges / Amount
 
      ** Start of change CSW201
     A**********                          17'SENDERS CHARGES/AMOUNT'                          CSW201
     A**********                            SPACEB(1)                                         CSW201
     A N29                                17'SENDERS CHARGES/AMOUNT'
     A N29                                  SPACEB(1)
     A  29                                17'SENDERS CHARGES/CURRENCY'
     A  29                                  SPACEB(1)
      ** End of change CSW201
 
     A                                    41':71F:'
     A            SNDCHG        13  0     46
 
      ** Start of change CSW201
     A  29 30     SNDCCY         3A       59
     A  29 31     SDCHG2        13  0     46
     A                                      SPACEB(1)
     A  29 31     SDCCY2         3A       59
     A  29 32     SDCHG3        13  0     46
     A                                      SPACEB(1)
     A  29 32     SDCCY3         3A       59
     A  29 33     SDCHG4        13  0     46
     A                                      SPACEB(1)
     A  29 33     SDCCY4         3A       59
     A  29 34     SDCHG5        13  0     46
     A                                      SPACEB(1)
     A  29 34     SDCCY5         3A       59
      ** End of change CSW201
 
      * Receivers Charges / Amount
     A                                    15'RECEIVERS CHARGES/AMOUNT'
     A                                      SPACEB(1)
     A                                    41':71G:'
     A            RCVCHG        13  0     46
      * Exchange Rate
     A                                    26'EXCHANGE RATE'
     A                                      SPACEB(1)
     A                                    41':36:'
     A            XRATE         13  8     46
      *
      * ===============================================================
