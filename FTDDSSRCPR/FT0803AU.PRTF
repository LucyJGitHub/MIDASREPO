      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FT Batch Authorisation Audit Report')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PRTF/FT0803AU - Batch Authorisation Audit Report             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CFT003  *CREATE    Date 06May98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CFT003 - FT Batch Authorisation                              *
      *           Based on FTH004AU original references CFT078        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      **
      **   *************
      **   REPORT HEADER
      **   *************
      **
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     5'REFERENCE FT0803AU'
     A            TITL      R             40
     A                                   109'DATE'
     A            RUNA      R            114
     A                                   123'PAGE'
     A                                   128PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                    50'Batch Authorisation Audi+
     A                                       t Report'
     A                                   109'TIME'
     A                                   114TIME
     A                                      EDTWRD('  :  :  ')
      **
      **   *************
      **   AUDIT DETAILS
      **   *************
      **
     A          R DETLAU                    TEXT('Audit Details')
     A                                      SPACEB(3)
     A                                    60'RUN TOTALS'
     A                                      SPACEA(1)
     A                                    60'----------'
     A                                      SPACEA(3)
     A  30                                20'No. of Transactions +
     A                                       Reported'
     A N30                                20'No. of Transactions +
     A                                       Authorised'
     A            HNOTR          4  0     95EDTCDE(3)
     A                                      SPACEA(2)
     A                                    20'No. of Transactions with +
     A                                       Exceptional Conditions'
     A            HNOEX          4  0     95EDTCDE(3)
     A                                      SPACEA(2)
     A                                    20'Total No. of Transactions +
     A                                       processed'
     A            HTOTN          5  0     94EDTCDE(3)
      **   **********
      **   NO DETAILS
      **   **********
      **
     A          R NODETAU                   SPACEB(2)
     A                                    52'*** NO DETAILS TO REPORT'
     A                                    77' ***'
      **
      **   ********************
      **   AUDIT DATABASE ERROR
      **   ********************
      **
     A          R ERRORAU
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8        30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
