      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FT Autogen transactions detail report')          *
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PRTF/FT0803P1 - Auto Generated Transactions                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER030             Date 09Jul08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CFT006             Date 01Nov99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CFT003  *CREATE    Date 06May98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER030 - Multicash German Feature                            *
      *  CFT006 - Funds Transfer addition of MT101 and MT102 messages *
      *  CFT003 - FT Batch Authorisation                              *
      *           Based on FTH004P1 original references CFT078        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      **
      **   *************
      **   REPORT HEADER
      **   *************
      **
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     5'REFERENCE FT0803P1'
     A            TITL      R             49
     A                                   109'DATE'
     A            RUNA      R            114
     A                                   123'PAGE'
     A                                   128PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     5'BRANCH'
     A            BRCA      R             12TEXT('Branch Code')
     A            BRNM      R             16TEXT('Branch Name')
     A                                    48'Automatically-Generated +
     A                                       Transactions'
     A                                   109'TIME'
     A                                   114TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                     5'AUTHORISED BY:'
     A            WSUSR         10A       20TEXT('Work Station User')
     A                                    31'(                     )'
     A N30                               109'** Authorise Mode **'
     A  30                               109'** Report Mode **'
      **
      **   *************
      **   COLUMN HEADER - Page Break, Value date & Settlement method
      **   *************
      **
     A          R COLHED                    TEXT('Column Heading')
     A                                      SPACEB(2)
     A                                     2'-------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -----'
     A                                      SPACEA(1)
     A                                     5'VALUE DATE :'
     A            SLDTD1    R             18EDTWRD('  /  /  ')
     A                                    32'SETTLEMENT METHOD :'
     A            STMT      R             52TEXT('Settlement Method')
     A                                      SPACEA(1)
     A                                     2'-------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -----'
     A*                                              Start /CFT006/
      **
      **   *************
      **   COLUMN HEADER - Page Break, Transfer type
      **   *************
      **
     A          R COLHD2                    TEXT('Col Heading RFT,CCT')
     A                                      SPACEB(2)
     A                                     2'-------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -----'
     A                                      SPACEA(1)
     A                                     5'TRANSFER TYPE:'
     A            TTYPE          2        20
     A                                      SPACEA(1)
     A                                     2'-------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -----'
     A*                                                End /CFT006/
      ** **********************************
      ** Column Header for Incoming Message
      ** **********************************
     A          R HEADI                     TEXT('Header for Incoming')
     A                                      SPACEB(2)
     A                                     5'SENDER'
     A*                                                    /CFT003/
     A************************************25'REFERENCE'
     A                                    27'REFERENCE'
     A************************************35'   O.S.N.   '
     A                                    43'O.S.N.'
     A************************************47'CCY'
     A                                    50'CCY'
     A*                                                    /CFT003/
     A                                    65'AMOUNT'
     A                                    87'BENEFICIARY'
     A                                   111'BANK TO BANK INFO'
     A                                      SPACEA(1)
     A                                     2'-------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -----'
      ** **********************************
      ** Column Header for Outgoing Message
      ** **********************************
     A          R HEADO                     TEXT('Header for OUTGOING')
     A                                      SPACEB(2)
     A*                                                    /CFT003/
     A*************************************5'DESTINATION'
     A                                     2'DESTINATION'
     A************************************27'SENDER'
     A                                    22'SENDER'
     A************************************49'REFERENCE'
     A                                    43'REFERENCE'
     A************************************62'O.S.N.'
     A                                    59'O.S.N.'
     A************************************70'CCY'
     A                                    71'CCY'
     A*                                                    /CFT003/
     A                                    87'AMOUNT'
     A                                    96'REL. REFERENCE'
     A                                   112'BENEFICIARY'
     A                                      SPACEA(1)
     A                                     2'-------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -----'
     A*                                              Start /CFT006/
      ** **********************************
      ** Column Header for RFT & CCT
      ** **********************************
     A          R HEADRC                    TEXT('Header for RFT/CCT')
     A                                      SPACEB(2)
     A                                     2'REFERENCE'
     A  71                                20'SENDER'
     A N71                                20'DESTINATION'
     A                                    56'DEBIT DATE'
     A                                    71'CCY'
     A                                    82'AMOUNT'
     A                                    95'BENEFICIARY'
     A                                      SPACEA(1)
     A                                     2'-------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -----'
     A*                                                End /CFT006/
      **
      **   *************
      **   REPORT DETAIL - detail format for INCOMING payment
      **   *************
      **
     A          R DETP1I
     A                                      SPACEB(1)
     A            SSNDR         20         5TEXT('Sender')
     A            PREF      R             27TEXT('Payment Reference')
     A*                                                    /CFT003/
     A************SOSN           6        35TEXT('OSN Number')
     A            SOSN           6        43TEXT('OSN Number')
     A************SMCY      R             47TEXT('CCY')
     A            SMCY      R             50TEXT('CCY')
     A************HSMAM         22        52TEXT('Settle Amount')
     A            HSMAM         22        54TEXT('Settle Amount')
     A*                                                    /CFT003/
     A            SBENE         20        87TEXT('Beneficiary')
     A            SBKTB         20       111TEXT('Bank to Bank Info')
      **
      **   *************
      **   REPORT DETAIL - detail format for OUTGOING payment             IMP039
      **   *************
      **
     A          R DETP1O
     A                                      SPACEB(1)
     A            SDEST         20         2TEXT('Destination')
     A            SSNDR         20        22TEXT('Sender')
     A            PREF      R             43TEXT('Payment Reference')
     A            DNBC      R             59EDTCDE(4)
     A            SMCY      R             71TEXT('CCY')
     A            HSMAM         22        74TEXT('Settle Amount')
     A            RLPR      R             96TEXT('Related reference')
     A            SBENE         20       112TEXT('Beneficiary')
     A
     A*                                              Start /CFT006/
      **
      **   *************
      **   REPORT DETAIL - detail format for RFT/CCT                      IMP039
      **   *************
      **
     A          R DTP1RC
     A                                      SPACEB(1)
     A            PREFE         15         2TEXT('Reference')
     A            PSEND         35        20TEXT('Sender')
     A            PDDAT          7        57TEXT('Debit Date')
     A            PCCY           3        71TEXT('CCY')
     A            PCAMT         15        77TEXT('Amount')
     A            PBENEF        35        95TEXT('Beneficiary')
     A*                                                End /CFT006/
     A
     A
     A
      **Start of change CER030
      *
      ** Transaction Type
      *
     A          R DETTRN                    SPACEB(1)
     A                                     2'-------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       ----'
     A                                      SPACEA(1)
     A  17                                 2'TRANSACTION TYPE:'
     A  17        P1RTTY         5        20
      *
      **   REPORT DETAIL - Extended Narrative details for both Incoming
      **                    and Outgoing details
      *
     A          R DETEXND                   SPACEB(1)
      *
     A                                     2'-------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       ----'
     A                                      SPACEA(1)
      *
     A  15                                 2'EXTENDED NARRATIVE+
     A                                      (DEBIT):'
     A                                      SPACEA(1)
      *
     A                                     2'-------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       ----'
     A                                      SPACEA(1)
     A  15        P1EXD1        65         2
     A                                      SPACEA(1)
     A  15        P1EXD2        65         2
     A                                      SPACEA(1)
     A  15        P1EXD3        65         2
     A                                      SPACEA(1)
     A  15        P1EXD4        65         2
     A                                      SPACEA(1)
     A  15        P1EXD5        65         2
     A                                      SPACEA(1)
     A  15        P1EXD6        65         2
     A                                      SPACEA(2)
      *
     A          R DETEXNC                   SPACEB(1)
     A                                     2'-------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       ----'
     A                                      SPACEA(1)
     A  16                                 2'EXTENDED NARRATIVE+
                                            (CREDIT):'
     A                                      SPACEA(1)
     A
     A                                     2'-------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       ----'
     A                                      SPACEA(1)
     A  16        P1EXC1        65         2
     A                                      SPACEA(1)
     A  16        P1EXC2        65         2
     A                                      SPACEA(1)
     A  16        P1EXC3        65         2
     A                                      SPACEA(1)
     A  16        P1EXC4        65         2
     A                                      SPACEA(1)
     A  16        P1EXC5        65         2
     A                                      SPACEA(1)
     A  16        P1EXC6        65         2
     A                                      SPACEA(2)
      **End of change CER030
      **
      **   *************
      **   FOOTER DETAILS- Amount Totals
      **   *************
      **
     A          R FOOTP1                    TEXT('Footer Details')
     A                                      SPACEB(1)
     A                                     2'-------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       ----'
     A                                      SPACEA(1)
     A                                    34'TOTAL AMOUNT :'
     A            SHTOTL        22        50
     A                                    74'NUMBER OF ITEMS : ('
     A            SNMBR          3  0     93EDTCDE(Z)
     A                                    96')'
      **
      **   *************
      **   END OF REPORT
      **   *************
      **
     A          R ENDP1                     TEXT('End of Report')
     A                                      SPACEB(3)
     A                                    55'*** END OF REPORT ***'
