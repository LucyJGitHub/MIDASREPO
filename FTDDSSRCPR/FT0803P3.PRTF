      *****************************************************************
/*STD *  PRTFBASE                                                     *
/********PR_STD60                                                     *                     MD041081
/*EXS *  PR_STD66                                                     *                     MD041081
/*EXI *  TEXT('Midas FT Payments Exception Report')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PRTF/FT0803P3 - Payments Exception Report                    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD041081           Date 06Sep16               *
      *                 CER059             Date 19Jul10               *
      *                 CER030             Date 09Jul08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CSW209             Date 01Apr09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      *                 CSW203             Date 26May03               *
      *                 CSD015             Date 14Oct02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CFT014             Date 25May00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CFT003  *CREATE    Date 06May98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD041081 - FTC0803 job fails with CPF5004.                   *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER030 - Multicash German Feature                            *
      *  CSW209 - SWIFT 2009 Changes                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSW203 - SWIFT 2003 Standards Update                         *
      *  CSD015 - Midas Compliance Watch - Watch List Checking        *
      *  CFT014 - Straight-through Processing Phase 2 MT103           *
      *           Messages Generation for FT.                         *
      *  CFT003 - FT Batch Authorisation                              *
      *           Based on FTH004P3 original references CFT078        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      **
      **   *************
      **   REPORT HEADER
      **   *************
      **
     A          R HEADP3                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     2'REFERENCE FT0803P3'
     A            TITL      R             23
     A                                    80'PAGE'
     A                                    90PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     2'BRANCH'
     A            BRCA      R              9TEXT('Branch Code')
     A            BOKC      R             13TEXT('Book Code')
     A            PRFC      R             16TEXT('Profit Center')
     A                                    27'Batch Authorisation +
     A                                       Exception Report'
     A                                    80'DATE'
     A            RUNA      R             87
     A                                      SPACEA(1)
     A N30                                 2'** Authorise Mode **'
     A  30                                 2'** Report Mode **'
     A                                    80'TIME'
     A                                    86TIME
     A                                      EDTWRD('  :  :  ')
      **
      **   *************
      **   REPORT DETAIL
      **   *************
      **
     A          R DETP3
     A                                      SPACEB(2)
     A                                     2'AUTH BY:'
     A            WSUSR         10        12TEXT('Work Station User')
     A                                    23'(                     )'
     A                                      SPACEA(2)
     A                                     2'INPUT'
     A                                    14'TY/ST'
     A                                    21'PAYMENT REF'
     A                                    38'AUTO'
     A                                    44'RELATED REF'
     A                                    61'OVR'
     A                                    66'O.S.N'
     A                                    79'SW PTY'
     A                                      SPACEA(1)
     A            INUSR2    R              2
     A            PYTP      R             14
     A                                    16'/'
     A            PYST      R             17
     A            PREF      R             21
     A            HAUTO          1        40
     A            RLPR      R             44
     A            OVRD      R             62
     A            HOSN          12        66
     A            SWPTY          1        81
     A                                      SPACEA(2)
     A                                     2'S.VAL:'
     A            SVAL           7         9TEXT('Settle Value date')
     A                                    35'MT CVR:'
     A            SNMT      R             44TEXT('SEND MT202')
     A                                    63'P.VAL:'
     A            PVAL           7        70TEXT('Settle Value date')
      *                                              Begin /CFT014/
     A N83                                  SPACEA(1)
     A  83                                96'O. RATE:'
     A  83 84     POEXR     R            106REFFLD(RATE)
     A                                      EDTCDE(J)
     A  83                                  SPACEA(1)
     A***********                           SPACEA(1)
      *                                              End   /CFT014/
     A                                     2'S.AMT:'
     A            HSMAM         22         9
     A                                    35'RATE:'
     A            RATE      R             41EDTCDE(J)
     A                                    63'P.AMT:'
     A            HPYAM         22        70
      *                                              Begin /CFT014/
     A N83N82                               SPACEA(1)
     A  82
     AO 83                                96'I. AMT :'
     A  82
     AO 83        PIAMT         22       106
      *                                              End   /CFT014/
     A                                      SPACEA(1)
     A                                     2'S.CCY:'
     A            SMCY      R              9TEXT('Settle Currency')
     A                                    35'TRANS TYPE:'
     A            CHAP      R             47EDTCDE(4)
     A                                    63'P.CCY:'
     A            PCCY      R             70TEXT('Pay Currency')
      *                                              Begin /CFT014/
     A N83N82                               SPACEA(2)
     A  82
     AO 83                                96'I. CCY :'
     A  82
     AO 83        PICCY          3       106
      *                                              End   /CFT014/
      ** Start of Change CSW209
     A**********                            SPACEA(2)                                         CSW209
     A                                      SPACEA(1)
      ** End of Change CSW209
     A                                     2'CHARGES:'
     A            ADDC      R             11TEXT('Add/Deduct Indicator')
     A                                    15'TARIFF:'
     A            TRCT      R             23TEXT('Transfer Comm. Tariff')
     A                                    26'CHG A/C:'
     A**Start of change CGL029
     A**********  HCDRO         12        35TEXT('Charges to DB of AC')                       CGL029
     A**********  HCHCY          3        48TEXT('Charges Currency')                          CGL029
     A            HCDRO         18        35TEXT('Charges to DB of AC')
     A            HCHCY          3        54TEXT('Charges Currency')
     A**End of change CGL029
      *                                              Begin /CFT014/
     A N83                                  SPACEA(2)
      **Start of change CGL029
     A**83******                          55'BANK OPERATION CODE:'                            CGL029
     A**83******  PBOCD          4        77                                                  CGL029
     A**83******                          86'TRANS. TYPE CODE:'                               CGL029
     A**83******  PTTCD          3       105                                                  CGL029
     A  83                                61'BANK OPERATION CODE:'
     A  83        PBOCD          4        83
     A  83                                92'TRANS. TYPE CODE:'
     A  83        PTTCD          3       111
      **End of change CGL029
      *                                              End   /CFT014/
      ** Start of Change CSW209
     A**********                            SPACEA(2)                                         CSW209
     A                                      SPACEA(1)
      ** End of Change CSW209
     A N29                                 7'SW PTY CHG'
     A  29                                 8'PHONE CHG'
     A                                    20'TSFR COMM'
     A                                    34'CHQ CHG'
     A N29                                45'COST ABD'
     A  29                                46'ADV CHG'
     A                                    58'TLX CHG'
     A                                    69'MISC CHG'
     A                                      SPACEA(1)
     A            HCCCY          3         2TEXT('Charges Currency')
     A N29        HSPCH         12         6TEXT('Swift priority charge')
     A  29        HTLCH         12         6TEXT('Telephone charge')
     A            HTRCM         12        18TEXT('Transfer commission')
     A            HCQCH         12        30TEXT('Cheque charge')
     A N29        HCSTA         12        42TEXT('Costs abroad')
     A  29        HADCH         12        42TEXT('Advice charge')
     A            HTXCH         12        54TEXT('Telex charge')
     A            HMSCH         12        66TEXT('Miscellaneous charge')
     A                                      SPACEA(1)
     A            HDCCY          3         2TEXT('Add/Deduct Currency')
     A N29        HDSPC         12         6
     A  29        HDTLC         12         6
     A            HDTRC         12        18
     A            HDCQC         12        30
     A N29        HDCST         12        42
     A  29        HDADC         12        42
     A            HDTXC         12        54
     A            HDMSC         12        66
      *                                              Begin /CFT014/
     A***********                           SPACEA(2)
     A                                      SPACEA(1)
      *                                              End   /CFT014/
     A                                     6'CHARGES TOTAL :'
     A            HCHCT          3        22TEXT('Charges Currency')
     A            HCHTL         16        26
      *                                              Begin /CFT014/
     A N83                                  SPACEA(1)
     A  83                                60'SENDER''S CHARGES   :'
     A  83        PSCC1          3        83
     A  83        PSCH1         12        87
     A  83        PSCC2          3       101
     A  83        PSCH2         12       105
      *                                              End   /CFT014/
     A                                      SPACEA(1)
     A  54                                 6'TOTAL DR AMT  :'
     A  54        HDRC1          3        22TEXT('Add/Deduct Currency')
     A  54        HDRT1         22        26
      *                                              Begin /CFT014/
     A N83                                  SPACEA(1)
     A  83        PSCC3          3        65
     A  83        PSCH3         12        69
     A  83        PSCC4          3        83
     A  83        PSCH4         12        87
     A  83        PSCC5          3       101
     A  83        PSCH5         12       105
     A***********                           SPACEA(2)
     A                                      SPACEA(1)
      *                                              End   /CFT014/
     A N54                                 6'NET AMT       :'
     A N54        HDRC2          3        22TEXT('Add/Deduct Currency')
     A N54        HDRT2         22        26
      *                                              Begin /CFT014/
     A N83                                  SPACEA(1)
     A  83                                60'RECEIVERS''S CHARGES:'
     A  83        PRCCY          3        83
     A  83        PRCRM         12        87
     A***********                           SPACEA(2)
     A                                      SPACEA(1)
      *                                              End   /CFT014/
     A  51                                 2'REGULAR PAYMENT:'
     A  51        RPAY      R             19TEXT('Regular Pay')
     A  51                                23'FREQ:'
     A  51        PAYF      R             29TEXT('Payment Frequency')
     A  51                                32'DAY:'
     A  51        DYIM      R             37EDTCDE(4)
     A  51                                41'NEXT DATE:'
     A  51        HNXTD          7        52TEXT('Next Pay date')
     A  51                                61'FINAL DATE:'
     A  51        HFIND          7        73TEXT('Final Pay date')
     A                                      SPACEA(2)
     A N29                                 2'DESTINATION   :'
     A  29                                 2'SENDER        :'
     A**Start of change CGL029
     A**********  HDDST         15        18                                                  CGL029
     A**********  HNDS1         35        36                                                  CGL029
     A**********  HNDS2         35        73                                                  CGL029
     A            HDDST         24        18
     A            HNDS1         35        45
     A            HNDS2         35        82
     A**End of change CGL029
     A                                      SPACEA(1)
     A                                     2'ORDER CUST :50:'
     A**Start of change CGL029
     A**********  HDORC         15        18                                                  CGL029
     A**********  HNOC1         35        36                                                  CGL029
     A            HDORC         21        18
     A            HNOC1         35        42
     A**End of change CGL029
     A                                      SPACEA(1)
     A            HCORC          6         9
     A            HFORC          1        16
     A**Start of change CGL029
     A**********  HNOC2         35        36                                                  CGL029
     A            HNOC2         35        42
     A**End of change CGL029
     A                                      SPACEA(1)
      **Start of Change CSW203
     A**********                           2'MT100 CUST    :'                                 CSW203
     A N83N85                              2'MT100 CUST    :'
     A  83 85                              2'MT103 CUST    :'
     A  83N85                              2'MT103/MT100 CUST    :'
     A**********  HDMTC         15        18                                                  CSW203
     A            HDMTC         15        24
     A**********  HNMT1         35        36                                                  CSW203
     A            HNMT1         35        42
     A**********  HNMT2         35        73                                                  CSW203
     A            HNMT2         35        79
      **End of Change CSW203
     A                                      SPACEA(1)
     A                                     2'ORDER BANK :52:'
     A**Start of change CGL029
     A**********  HDORB         15        18                                                  CGL029
     A**********  HNOB1         35        36                                                  CGL029
     A**********  HNOB2         35        73                                                  CGL029
     A            HDORB         21        18
     A            HNOB1         35        42
     A            HNOB2         35        79
     A**End of change CGL029
     A                                      SPACEA(1)
     A                                     2'SNDR CORR  :53:'
     A            HDSNC         15        18
     A            HNSC1         35        36
     A            HNSC2         35        73
     A                                      SPACEA(1)
     A                                     2'RCVR CORR  :54:'
     A            HDRCO         15        18
     A            HNRC1         35        36
     A                                      SPACEA(1)
     A            HCRCO          6         9
     A            HFRCO          1        16
     A            HNRC2         35        36
      *                                              Begin /CFT014/
     A                                      SPACEA(1)
     A  83                                 2'3RD RMBRSMT:55:'
     A  83        HDTRM         15        18
     A  83        HNTR1         35        36
     A                                      SPACEA(1)
     A  83        HCTRM          6         9
     A  83        HFTRM          1        16
     A  83        HNTR2         35        36
      *                                              End   /CFT014/
     A                                      SPACEA(1)
     A                                     2'INTER BANK :56:'
     A            HDINB         15        18
     A            HNIB1         35        36
     A            HNIB2         35        73
     A                                      SPACEA(1)
     A            HCINB          6         9
     A            HFINB          1        16
     A            HNIB3         35        36
     A            HNIB4         35        73
     A                                      SPACEA(1)
     A            HNIB5         35        36
     A                                      SPACEA(1)
     A                                     2'AC WTIH BK :57:'
     A**Start of change CGL029
     A**********  HDACB         15        18                                                  CGL029
     A**********  HNAB1         35        36                                                  CGL029
     A**********  HNAB2         35        73                                                  CGL029
     A            HDACB         21        18
     A            HNAB1         35        42
     A            HNAB2         35        79
     A**End of change CGL029
     A                                      SPACEA(1)
     A            HCACB          6         9
     A            HFACB          1        16
     A**Start of change CGL029
     A**********  HNAB3         35        36                                                  CGL029
     A**********  HNAB4         35        73                                                  CGL029
     A            HNAB3         35        42
     A            HNAB4         35        79
     A**End of change CGL029
     A                                      SPACEA(1)
     A**Start of change CGL029
     A**********  HNAB5         35        36                                                  CGL029
     A            HNAB5         35        42
     A**End of change CGL029
     A                                      SPACEA(1)
     A                                     2'BENEFICIARY   :'
     A**Start of change CGL029
     A**********  HDBNC         15        18                                                  CGL029
     A**********  HNBC1         35        36                                                  CGL029
     A**********  HNBC2         35        73                                                  CGL029
     A            HDBNC         21        18
     A            HNBC1         35        42
     A            HNBC2         35        79
     A**End of change CGL029
     A                                      SPACEA(1)
     A            HCBNC          6         9
     A            HFBNC          1        16
     A**Start of change CGL029
     A**********  HNBC3         35        36                                                  CGL029
     A**********  HNBC4         35        73                                                  CGL029
     A            HNBC3         35        42
     A            HNBC4         35        79
     A**End of change CGL029
     A                                      SPACEA(1)
     A**Start of change CGL029
     A**********  HNBC5         35        36                                                  CGL029
     A            HNBC5         35        42
     A**End of change CGL029
     A                                      SPACEA(1)
     A                                     2'DETAIL :70:'
     A            DTP1      R             14
     A            DTP2      R             50
     A                                      SPACEA(1)
     A            DTP3      R             14
     A            DTP4      R             50
     A                                      SPACEA(1)
     A                                     2'CHG    :71:'
     A            DTCH      R             14
     A                                      SPACEA(1)
     A                                     2'BK INF :72:'
     A            BBI1      R             14
     A            BBI2      R             50
     A                                      SPACEA(1)
     A            BBI3      R             14
     A            BBI4      R             50
     A                                      SPACEA(1)
     A            BBI5      R             14
     A            BBI6      R             50
      *                                              Begin /CFT014/
     A***********                           SPACEA(2)
     A                                      SPACEA(1)
     A  83                                 2'INS CDE:23:'
     A  83        PICD1         35        14
     A  83        PICD2         35        50
     A                                      SPACEA(1)
     A  83        PICD3         35        14
     A  83        PICD4         35        50
     A                                      SPACEA(1)
     A  83        PICD5         35        14
     A                                      SPACEA(1)
     A  83                                 2'RG RPTG:77:'
     A  83        PRRP1         35        14
     A  83        PRRP2         35        50
     A                                      SPACEA(1)
     A  83        PRRP3         35        14
     A                                      SPACEA(1)
      *                                              End   /CFT014/
      **Start of change CER030
     A                                      SPACEB(1)
     A  17                                 2'TRANSACTION TYPE:'
     A  17        P3RTTY         5        20
     A                                      SPACEA(1)
      *
     A  15                                 2'EXTENDED NARRATIVE+
     A                                      (DEBIT):'
     A                                      SPACEA(1)
     A  15        P3EXD1        65        14
     A                                      SPACEA(1)
     A  15        P3EXD2        65        14
     A                                      SPACEA(1)
     A  15        P3EXD3        65        14
     A                                      SPACEA(1)
     A  15        P3EXD4        65        14
     A                                      SPACEA(1)
     A  15        P3EXD5        65        14
     A                                      SPACEA(1)
     A  15        P3EXD6        65        14
     A                                      SPACEA(1)
      *
     A  16                                 2'EXTENDED NARRATIVE+
     A                                      (CREDIT):'
     A                                      SPACEA(1)
     A  16        P3EXC1        65        14
     A                                      SPACEA(1)
     A  16        P3EXC2        65        14
     A                                      SPACEA(1)
     A  16        P3EXC3        65        14
     A                                      SPACEA(1)
     A  16        P3EXC4        65        14
     A                                      SPACEA(1)
     A  16        P3EXC5        65        14
     A                                      SPACEA(1)
     A  16        P3EXC6        65        14
     A                                      SPACEA(2)
      **End of change CER030
      ** Start of Change CSW209
     A  14 29                              2'NETWORK :'
     A  14 29     P2NWRK         6        12
     A  14 29                             27'MESSAGE TYPE :'
     A  14 29     P2MTPY         3        43
     A  14 29                             53'VALIDATION FLAG : '
     A  14 29     P2VALF         3        71
     A  14 29                               SPACEA(1)
     A  14N29                              2'INCOMING NETWORK :'
     A  14N29     P1NWRK         6        21
     A  14N29                             50'INCOMING MESSAGE TYPE :'
     A  14N29     P1MTPY         3        74
     A  14N29                             95'INCOMING VALIDATION FLAG :'
     A  14N29     P1VALF         8       122
     A  14N29                               SPACEA(1)
     A  14N29                              2'OUTGOING VALIDATION FLAG (MAIN) :'
     A  14N29     P1OMVF         8        36
     A  14N29                             50'OUTGOING VALIDATION FLAG (COVER) :'
     A  14N29     P1OCVF         8        85
     A  14N29                               SPACEA(2)
     A  14N29                              2'SENDER TO RECEIVER INFO :72: '
     A  14N29     PSOCB1        35        31
     A  14N29     PSOCB2        35        68
     A  14N29                               SPACEA(1)
     A  14N29                              2'(COVER A)'
     A  14N29     PSOCB3        35        31
     A  14N29     PSOCB4        35        68
     A  14N29                               SPACEA(1)
     A  14N29     PSOCB5        35        31
     A  14N29     PSOCB6        35        68
     A  14                                  SPACEA(1)
      ** End of Change CSW209
     A                                     2'EXCEPTIONAL CONDITION:'
     A  31                                26'*** BACK-VALUED PAYMENT ***'
     A                                      SPACEA(1)
     A N31 32                             26'*** INSUFFICIENT FUNDS ***'
     A                                      SPACEA(1)
     A N31N32 33                          26'*** AMOUNT ABOVE THRESHOLD +
     A                                       LIMIT ***'
     A                                      SPACEA(1)
     A  31 32                             26'*** INSUFFICIENT FUNDS ***'
     A                                      SPACEA(1)
     A**Start of Change CSD015
     A  46                                26'*** ITEM UNDER REVIEW BY   +
     A                                       COMPLIANCE OFFICER ***'
     A                                      SPACEA(1)
     A**End of Change CSD015
     A  31N32 33
     AON31 32 33                          26'*** AMOUNT ABOVE THRESHOLD +
     A                                       LIMIT ***'
     A                                      SPACEA(1)
     A  31 32 33                          26'*** AMOUNT ABOVE THRESHOLD +
     A                                       LIMIT ***'
     A N30                                 2'INSUFFICIENT FUND +
     A                                      ACCOUNT DETAILS'
     A                                      SKIPB(3)
     A                                      SPACEA(2)
     A N30                                 2'ACCOUNT NUMBER :'
     A**Start of change CGL029
     A*N30******  HACNT         22        19                                                  CGL029
     A N30        HACNT         28        19
     A**End of change CGL029
     A                                      SPACEA(2)
     A N30                                 2'YESTERDAY''S CLOSING BALANCE  :'
     A N30        LDBL      R             32EDTCDE(A)
     A                                      SPACEA(1)
     A N30                                 2'SHADOW CLEARED BALANCE       :'
     A N30        CLBL      R             32EDTCDE(A)
     A                                      SPACEA(1)
     A N30                                 2'DEBIT TOTAL                  :'
     A N30        DITM      R             34EDTCDE(A)
     A                                      SPACEA(1)
     A N30                                 2'CREDIT TOTAL                 :'
     A N30        CITM      R             34EDTCDE(A)
     A                                      SPACEA(1)
     A N30                                 2'OVERDRAFT LIMIT              :'
     A N30        ODLN      R             37EDTCDE(A)
     A                                      SPACEA(1)
     A N30                                 2'EXPIRY DATE                  :'
     A N30        HEXDT          7        32
      ** Start of Change CSW209
     A          R PKCOVM                    SKIPB(2)
     A                                     2'REFERENCE FT0803P3'
     A            TITL      R             23
     A                                    80'PAGE'
     A                                    90PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     2'BRANCH'
     A            BRCA      R              9TEXT('Branch Code')
     A            BOKC      R             13TEXT('Book Code')
     A            PRFC      R             16TEXT('Profit Center')
     A                                    27'Batch Authorisation +
     A                                       Exception Report'
     A                                    80'DATE'
     A            RUNA      R             87
     A                                      SPACEA(1)
     A N30                                 2'** Authorise Mode **'
     A  30                                 2'** Report Mode **'
     A                                    80'TIME'
     A                                    86TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
     A                                     2'AUTH BY:'
     A            WSUSR         10        12TEXT('Work Station User')
     A                                    23'(                     )'
     A                                      SPACEA(1)
     A                                     2'INPUT'
     A                                    14'TY/ST'
     A                                    21'PAYMENT REF'
     A                                    38'AUTO'
     A                                    44'RELATED REF'
     A                                    61'OVR'
     A                                    66'O.S.N'
     A                                    79'SW PTY'
     A                                      SPACEA(1)
     A            PTUSER        10         2
     A            PTTYPE         2        14
     A                                    16'/'
     A            PTSUBT         2        17
     A            PTPREF        15        21
     A            HAUTO          1        40
     A            RLPR      R             44
     A            OVRD      R             62
     A            HOSN          12        66
     A            SWPTY          1        81
     A                                      SPACEA(1)
     A                                     2'COVER'
     A                                      SPACEA(1)
     A                                     5'ORDERING CUSTOMER'
     A                                    33':50:'
     A            PSCOC1        35        40
     A            PSCOC2        35        78
     A                                      SPACEA(1)
     A            PSCOC3        35        40
     A            PSCOC4        35        78
     A                                      SPACEA(1)
     A            PSCOC5        35        40
     A                                      SPACEA(1)
     A                                     5'ORDERING INSTITUTION'
     A                                    33':52:'
     A            PSCOB1        35        40
     A            PSCOB2        35        78
     A                                      SPACEA(1)
     A            PSCOB3        35        40
     A            PSCOB4        35        78
     A                                      SPACEA(1)
     A            PSCOB5        35        40
     A                                      SPACEA(1)
     A                                     5'INTERMEDIARY INSTITUTION'
     A                                    33':56:'
     A            PSCIB1        35        40
     A            PSCIB2        35        78
     A                                      SPACEA(1)
     A            PSCIB3        35        40
     A            PSCIB4        35        78
     A                                      SPACEA(1)
     A            PSCIB5        35        40
     A                                      SPACEA(1)
     A                                     5'ACCOUNT WITH INSTITUTION'
     A                                    33':57:'
     A            PSCAB1        35        40
     A            PSCAB2        35        78
     A                                      SPACEA(1)
     A            PSCAB3        35        40
     A            PSCAB4        35        78
     A                                      SPACEA(1)
     A            PSCAB5        35        40
     A                                      SPACEA(1)
     A                                     5'BENEFICIARY CUSTOMER'
     A                                    33':59:'
     A            PSCBC1        35        40
     A            PSCBC2        35        78
     A                                      SPACEA(1)
     A            PSCBC3        35        40
     A            PSCBC4        35        78
     A                                      SPACEA(1)
     A            PSCBC5        35        40
     A                                      SPACEA(1)
     A                                     5'REMITTANCE INFORMATION'
     A                                    33':70:'
     A            PSCDP1        35        40
     A            PSCDP2        35        78
     A                                      SPACEA(1)
     A            PSCDP3        35        40
     A            PSCDP4        35        78
     A                                      SPACEA(1)
     A                                     5'SENDER TO RECEIVER INFO'
     A                                    33':72:'
     A            PSCBB1        35        40
     A            PSCBB2        35        78
     A                                      SPACEA(1)
     A            PSCBB3        35        40
     A            PSCBB4        35        78
     A                                      SPACEA(1)
     A            PSCBB5        35        40
     A            PSCBB6        35        78
     A                                      SPACEA(1)
     A                                     5'INSTRUCTED CCY/AMOUNT'
     A                                    33':33B:'
     A            PSCICY         3        40
     A                                    44'/'
     A            PSCIAM        16        45
      ** End of Change CSW209
      **   *************
      **   END OF REPORT
      **   *************
      **
     A          R ENDP3                     TEXT('End of Report')
     A                                      SPACEB(3)
     A                                    35'*** END OF REPORT ***'
