      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas FT MT910 Bank Transfers SWIFT')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Funds Trnasfer Module                                *
      *                                                               *
      *  PRTF/FT162002P1 - MT910 Bank Transfers Via SWIFT             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2017            *
      *                                                               *
      *  Last Amend No. CFT162  *Create    Date 05Sep17               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFT162 - MT900 and MT910 Message Generation                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
      *
     A          R HEADP1                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2017')
     A                                     6'REFERENCE FT162002P1'
     A            STITL         53        37
     A                                   100'DATE'
     A            SRUNA          7       105
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
      *
     A                                    45'FUNDS TRANSFER GENERATED SWIFT +
     A                                       MESSAGES'
     A                                      SPACEB(2)
     A  13                               100'TIME'
     A  13                               105TIME EDTWRD('  :  :  ')
     A  13                               119'EXCEPTION'
      *
     A                                    44'~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~'
     A                                      SPACEB(1)
      *
     A                                    12'CONFIRMATION OF CREDIT FOR'
     A                                      SPACEB(3)
     A            P1PREF        15        39TEXT('MIDAS Transaction Reference')
     A  41                                61'(RECOVERY)'
     A                                      SPACEA(1)
      *
     A            P1NWSN        20        41TEXT('Network Senders Address')
     A                                      SPACEB(2)
      *
     A                                    41'910'
     A                                      SPACEB(1)
     A            MPRY           1        45TEXT('SWIFT II Message Priority')
      *
     A            P1NWDS        20        41TEXT('Network Destination Address')
     A                                      SPACEB(1)
      *
     A                                    34'TRN    :20:'
     A                                      SPACEB(2)
     A            P1REF         16        45TEXT('Transaction Reference')
      *
     A                                    20'RELATED REFERENCE    :21:'
     A                                      SPACEB(1)
     A            P1RREF        16        45TEXT('Related Reference')
      *
     A                                    15'ACCOUNT IDENTIFICATION    :25:'
     A                                      SPACEB(1)
     A            P1ACCT        35        45TEXT('Account Identification')
      *
     A                                    11'VALUE DATE/CURRENCY/AMOUNT'
     A                                      SPACEB(1)
     A                                    41':32A:'
     A            P1VDAT         6        46TEXT('Value Date')
     A            P1RCCY         3        52TEXT('Currency')
     A            P1AMNT        15        55TEXT('Amount')
      *
     A                                    24'ORDERING BANK    :52'
     A                                      SPACEB(1)
     A            P1ORBT         1        44TEXT('Ordering Bank Tag Type')
     A                                    45':'
     A            P1ORB1        37        46TEXT('Ordering Bank 1')
     A            P1ORB2        35        46TEXT('Ordering Bank 2')
     A                                      SPACEB(1)
     A            P1ORB3        35        46TEXT('Ordering Bank 3')
     A                                      SPACEB(1)
     A            P1ORB4        35        46TEXT('Ordering Bank 4')
     A                                      SPACEB(1)
     A            P1ORB5        35        46TEXT('Ordering Bank 5')
     A                                      SPACEB(1)
      *
     A                                    20'INTERMEDIARY BANK    :56'
     A                                      SPACEB(1)
     A            P1INBT         1        44TEXT('Intermediary Bank Tag Type')
     A                                    45':'
     A            P1INB1        37        46TEXT('Intermediary Bank 1')
     A            P1INB2        35        46TEXT('Intermediary Bank 2')
     A                                      SPACEB(1)
     A            P1INB3        35        46TEXT('Intermediary Bank 3')
     A                                      SPACEB(1)
     A            P1INB4        35        46TEXT('Intermediary Bank 4')
     A                                      SPACEB(1)
     A            P1INB5        35        46TEXT('Intermediary Bank 5')
     A                                      SPACEB(1)
      *
     A                                    13'BANK TO BANK INFORMATION    :72:'
     A                                      SPACEB(1)
     A            P1BTB1        35        45TEXT('Bank to Bank 1')
     A            P1BTB2        35        45TEXT('Bank to Bank 2')
     A                                      SPACEB(1)
     A            P1BTB3        35        45TEXT('Bank to Bank 3')
     A                                      SPACEB(1)
     A            P1BTB4        35        45TEXT('Bank to Bank 4')
     A                                      SPACEB(1)
     A            P1BTB5        35        45TEXT('Bank to Bank 5')
     A                                      SPACEB(1)
     A            P1BTB6        35        45TEXT('Bank to Bank 6')
     A                                      SPACEB(1)
      *
