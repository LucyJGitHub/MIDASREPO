      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas FT MT900 Bank Transfers TELEX')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Funds Trnasfer Module                                *
      *                                                               *
      *  PRTF/FT162003P2 - MT900 Bank Transfers via Telex             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2017            *
      *                                                               *
      *  Last Amend No. CFT162  *Create    Date 05Sep17               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFT162 - MT900 and MT910 Message Generation                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2017')
      *
     A                                     6'REFERENCE FT162003P2'
     A            STITL         53        37
     A                                   100'DATE'
     A            SRUNA          7       105
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
      *
     A                                    51'CONFIRMATION OF DEBIT  MESSAGES'
     A                                      SPACEB(2)
     A  13                               100'TIME'
     A  13                               105TIME EDTWRD('  :  :  ')
     A  13                               119'EXCEPTION'
      *
     A                                    51'-------------------------------'
     A                                      SPACEB(1)
      *
     A  14                                 6'CURRENCY'
     A  14                                  SPACEB(2)
     A  14        P2CCY         14        16TEXT('CURRENCY')
      *
     A  15                                 6'VALUE'
     A  15                                  SPACEB(2)
     A  15        P2VAL          7        16TEXT('VALUE')
      *
     A                                     6'DESTINATION' SPACEB(2)
     A            P2DST1        35        19TEXT('Destination 1')
     A                                    71'REFERENCE'
     A            P2PREF        15        82TEXT('MIDAS Transaction Reference')
      *
     A            P2DST2        35        19TEXT('Destination 2')
     A                                      SPACEB(1)
     A                                    63'RELATED REFERENCE'
     A            P2RREF        16        82TEXT('RELATED REFERENCE')
      *
     A            P2DST3        35        19TEXT('Destination 3')
     A                                      SPACEB(1)
     A                                    71'AMOUNT'
     A  30        P2AMT0        13  0     82EDTCDE(1)
     A  31        P2AMT1        13  1     82EDTWRD('   ,   ,   , 0 . ')
     A  32        P2AMT2        13  2     82EDTWRD('  ,   ,   , 0 .  ')
     A  33        P2AMT3        13  3     82EDTWRD(' ,   ,   , 0 .   ')
      *
     A            P2DST4        35        19TEXT('Destination 4')
     A                                      SPACEB(1)
      *
     A            P2DST5        35        19TEXT('Destination 5')
     A                                      SPACEB(1)
     A                                     6'ACCOUNT IDENTIFICATION'
     A                                      SPACEB(2)
     A            P2IDCU         6        29TEXT('ACCOUNT ID CUSD')
     A                                    35'/'
     A            P2IDCY         3        36TEXT('ACCOUNT ID CCY ')
     A                                    39'/'
     A            P2IDAC        10        40TEXT('ACCOUNT ID ACOD')
     A                                    50'/'
     A            P2IDAS         2        51TEXT('ACCOUNT ID ACSQ')
