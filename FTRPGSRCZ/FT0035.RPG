     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FT Get Customer Number for Default Proc.')       *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Funds Transfer Module                            *
     F*                                                               *
     F*  FT0035 - FT Get Customer Number for Default Processing       *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CRE075             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER047             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 BUG15201           Date 30Oct07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CFT032             Date 11Sep06               *
      *                 CSD031             Date 10Apr06               *
      *                 221478             Date 06Jun06               *
      *                 223500             Date 06Jun06               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CSC022             Date 24Feb04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 198654             Date 27Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CFT014             Date 25May00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01506  *CREATE    Date 27Jun94               *
     F*                                                               *
     F*---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE075 - Effective Date for Retail Accounts (Recompile)      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *           (Recompile)                                         *
      *  BUG15201 - Encounter Database Errors (Recompile)             *
      *  CFT032 - Account Line in Field 50X in MT103                  *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  221478 - I#PTY2 field contents incorrectly checked.          *
      *  223500 - DB error at 6, file AOIBANR4, key *CALL due to      *
      *           wrong value of P@IBAN field that is passed to       *
      *           access object AOIBANR4 which is used to access the  *
      *           accounts file to get corresponding customer number  *
      *           of the IBAN.  Apply core fix 212997.                *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*  198654 - DB error as program doesn't cater for IBANs.        *
      *  CFT014 - Straight-through Processing Phase 2 MT103           *
      *           Messages Generation for FT. Recompile only.         *
     F*  S01506 - BLI Payment Charges Enhancements                    *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*                                                               *
     F*  50    First cycle                                            *
     F*  90    General work indicator                                 *
     F*                                                               *
     F*  U7/U8 Error Ocurred                                          *
     F*  LR    End program                                            *
     F*                                                               *
     F*****************************************************************
     F/COPY WNCPYSRC,FT0035FFPG
      *
      *  /Copy point for File Definition
      *
     F/EJECT
     E                    CPY@    1   1 80
      *
      *  Copyright table
      *
     E/COPY WNCPYSRC,FT0035EEPG
      *
      *  /Copy point for Arrays
      *
     E/COPY FTCPYSRC,SRERRE
      *
      *  end of Copysource for error processing
      *
      ** Array of Commitment Job Names                                                        CSC022
     E                    CMTJOB     10 10                                                    CSC022
     E/EJECT
     I/COPY WNCPYSRC,FT0035IIPG
      *
      *  /Copy point for Input specifications
      *
     ICPYR@#      DS
      *
      *  Data structure for compilation  - Copyright insertion
      *
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
     IW0DATA      DS                            256
      *
      * Calling parameter data structure
      *
     I                                        1   1 I#PT1
     I                                        2  36 I#PTY1
     I                                       37  37 I#PT2
     I                                       38  72 I#PTY2
     I                                       73  75 I#CCY
     I                                       76  81 O#CUST
     IDSMTR       DS
      *
      *  Define fields for message transalation
      *
     I                                        1 132 #MSDTA
     I                                       15  30 #MPREF
     I                                    P   1   20#MPART
     I                                      133 264 #MSTX1
     I#MSTX2      DS
     I                                        1 256 #MSTXA
     I                                      257 512 #MSTXB
     IACCNT     E DSACCNTAB
      *
      *  Define Account Details
      *
     IMEBICD    E DSMEBICDPD
      *
      *  Define BIC Directory
      *
     ISDBANK    E DSSDBANKPD
      *
      *  Define Bank Details
      *
     ISDCUST    E DSSDCUSTPD
      *
      *  Define Customer Details
      *
     ISDCNST    E DSSDCNSTPD
      *
      *  Define Counterparty Nostros
      *
     ISDNOST    E DSSDNOSTPD
      *
      *  Define Nostro Details
      *
     IRUNDTA    E DSRUNDAT
      *
      *  Define rundat data area
      *
     IDSFDY     E DSDSFDY
      *
      * Data Structures used by Access Programs
      *
     IDSSDY     E DSDSSDY
      *
      * Data Structures used by Access Programs
      *
     I/COPY FTCPYSRC,SFTADDRI
      *
      * Address data structure
      *
     I/COPY FTCPYSRC,SRERRI
      *
      *  End of Program Error Processing copysource member
      *
     I              '000000000000'        C         ZEROS                                     221478
      *                                                                                       221478
     ISCCMT       DS                            256                                           CSC022
     I                                        1   30CMTNUM                                    CSC022
     I                                        4 103 CMTARR                                    CSC022
     I/EJECT
      *****************************************************************
      *                 M A I N L I N E
      *****************************************************************
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *  Define entry parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           W0RTN   7
     C                     PARM           W0DATA
     C                     PARM           W0MSG 256
      *
      * Initialise program
      *
     C           *IN50     IFEQ '0'
     C                     EXSR SRINIT
     C                     END
      *
      *  Set up primary reference, and put into address structure
      *
     C           I#PTY2    IFEQ *BLANKS
     C           I#PTY2    OREQ ZEROS                                                         221478
     C                     MOVELI#PTY1    ADDRSS
     C                     MOVE I#PT1     W0TYPE  1
     C                     ELSE
     C                     MOVELI#PTY2    ADDRSS
     C                     MOVE I#PT2     W0TYPE
     C                     END
     C                     MOVE I#CCY     W0CCY   3
      *
      *  Initialise output fields to blanks
      *
     C                     MOVE *BLANKS   O#CUST
     C                     MOVE *BLANKS   W0MSG
      *
      *  Execute party type check processing
      *
     C           W0TYPE    CASEQ'A'       SRADDR           Address
     C           W0TYPE    CASEQ'C'       SRCNUM           Customer nbr
     C           W0TYPE    CASEQ'F'       SRFNOS           Full Nostro
     C           W0TYPE    CASEQ'G'       SRGLAC           GL Account
     C           W0TYPE    CASEQ'N'       SRNOSN           Nostro Nbr
     C           W0TYPE    CASEQ'P'       SRPTAC           Partial A/c
     C           W0TYPE    CASEQ'R'       SRRETL           Retail A/c
     C           W0TYPE    CASEQ'S'       SRBIC            SWIFT BIC id
     C           W0TYPE    CASEQ'I'       SRIBAN           IBAN                               198654
     C                     CAS            SRACT
     C                     ENDCS
      *
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      *  Return to calling program
      *
     C                     RETRN
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRCNUM   : Format Customer                                   *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRCNUM    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRCNUM'  @STK,Q
      *
      *  /Copy point for Format Customer
      *
     C/COPY WNCPYSRC,FT0035CCP1
      *
      *  Move the customer number in ADDRSS to the output customer number
      *
     C                     MOVELADCNUM    O#CUST
      *
      *  Unwind subroutine stack name
      *
     C           EXCNUM    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRFNOS   : Format Full Nostro                                *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRFNOS    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRFNOS'  @STK,Q
      *
      *  /Copy point for Format Nostro
      *
     C/COPY WNCPYSRC,FT0035CCP2
      *
      *  Get Nostro details
      *
     C                     MOVELADFCCY    @CYCD
     C                     MOVELADFNNB    @NONB
      *
     C                     CALL 'AONOSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM *BLANKS   @CUST   6
     C                     PARM           @CYCD   3
     C**********           PARM *BLANKS   @ACCN   4                                           CGL029
     C                     PARM *BLANKS   @ACCN  10                                           CGL029
     C                     PARM *BLANKS   @ACSN   2
     C                     PARM           @NONB   2
     C                     PARM *BLANKS   @BRCD   3
     C                     PARM *BLANKS   @CSSN  10
     C                     PARM *BLANKS   @PNOI   1
     C           SDNOST    PARM *BLANKS   DSFDY
      *
      *  If nostro is found then output the customer number
      *
     C           @RTCD     IFEQ *BLANKS
     C                     MOVE A7CUST    O#CUST
     C                     ELSE
      *
      *  Else, get warning information and perform DB error processing
      *
     C                     MOVEL*BLANKS   #MSDTA
     C                     MOVELADFNOS    #MSDTA
      *
     C                     CALL 'MEC1150'              9090
     C                     PARM 'FTM2344 '#MSGID  7
     C                     PARM 'FTUSRMSG'#MSGF  10
     C                     PARM           #MSDTA
     C                     PARM *BLANKS   #MSTX1
     C                     PARM *BLANKS   #MSTX2
     C*
     C                     MOVEL#MSTX1    W0MSG
     C*
     C                     MOVEL'AONOSTR0'W0FILE           ***************
     C                     MOVELADFNOS    W0KEY            * DB ERROR 02 *
     C                     Z-ADD2         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     MOVELW0MSG     W0PREF
     C                     EXSR SRERR
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXFNOS    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRGLAC   : Format General Ledger Account                     *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRGLAC    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRGLAC'  @STK,Q
      *
      *  /Copy point for Format General Ledger Account
      *
     C/COPY WNCPYSRC,FT0035CCP3
      *
      *  Move the customer number in ADDRSS to the output customer number
      *
     C                     MOVELADGCNM    O#CUST
      *
      *  Unwind subroutine stack name
      *
     C           EXGLAC    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRNOSN   : Format Nostro Number                              *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRNOSN    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRNOSN'  @STK,Q
      *
      *  /Copy point for Format Nostro number
      *
     C/COPY WNCPYSRC,FT0035CCP4
      *
      *  Get Nostro details
      *
     C                     MOVELW0CCY     @CYCD
     C                     MOVELADNOSN    @NONB
      *
     C                     CALL 'AONOSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM *BLANKS   @CUST   6
     C                     PARM           @CYCD   3
     C**********           PARM *BLANKS   @ACCN   4                                           CGL029
     C                     PARM *BLANKS   @ACCN                                               CGL029
     C                     PARM *BLANKS   @ACSN   2
     C                     PARM           @NONB   2
     C                     PARM *BLANKS   @BRCD   3
     C                     PARM *BLANKS   @CSSN  10
     C                     PARM *BLANKS   @PNOI   1
     C           SDNOST    PARM *BLANKS   DSFDY
      *
      *  If nostro is found then output the customer number
      *
     C           @RTCD     IFEQ *BLANKS
     C                     MOVELA7CUST    O#CUST
     C                     ELSE
      *
      *  Else, get warning information and perform DB error processing
      *
     C                     MOVEL*BLANKS   #MSDTA
     C           W0CCY     CAT  ADNOSN    #MSDTA    P
      *
     C                     CALL 'MEC1150'              9090
     C                     PARM 'FTM2344 '#MSGID  7
     C                     PARM 'FTUSRMSG'#MSGF  10
     C                     PARM           #MSDTA
     C                     PARM *BLANKS   #MSTX1
     C                     PARM *BLANKS   #MSTX2
      *
     C                     MOVEL#MSTX1    W0MSG
      *
     C                     MOVEL'AONOSTR0'W0FILE           ***************
     C           W0CCY     CAT  ADNOSN    W0KEY     P      * DB ERROR 03 *
     C                     Z-ADD3         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     MOVELW0MSG     W0PREF
     C                     EXSR SRERR
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXNOSN    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRPTAC   : Format Partial Account                            *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRPTAC    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRPTAC'  @STK,Q
      *
      *  /Copy point for Format Partial Account
      *
     C/COPY WNCPYSRC,FT0035CCP5
      *
      *  Move the customer number in ADDRSS to the output customer number
      *
     C                     MOVELADPCNM    O#CUST
      *                                                                                       CFT032
     C           ADSLSH    IFEQ '/'                                                           CFT032
     C                     MOVELADPCN2    O#CUST                                              CFT032
     C                     ENDIF                                                              CFT032
      *
      *  Unwind subroutine stack name
      *
     C           EXPTAC    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRRETL   : Format Retail Account Number                      *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRRETL    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRRETL'  @STK,Q
      *
      *  /Copy point for Format Retail Account
      *
     C/COPY WNCPYSRC,FT0035CCP6
     C                     MOVELADACNO    @RETL                                               CFT032
     C           ADSLSH    IFEQ '/'                                                           CFT032
     C                     MOVELADACN2    @RETL                                               CFT032
     C                     ENDIF                                                              CFT032
      *
      *  Get Account Details
      *
     C                     CALL 'AOACCTR0'             9090
     C                     PARM *BLANKS   @RTCD   7        B:Return code
     C                     PARM '*KEY   ' @OPTN   7        I:Option
     C**********           PARM ADACNO    @RETL  10        I:Retail key                       CFT032
     C                     PARM           @RETL  10        I:Retail key                       CFT032
     C                     PARM *BLANKS   @CNUM   6        I:Key field 1
     C                     PARM *BLANKS   @CUCD   3        I:Key field 2
     C**********           PARM *BLANKS   @ACCD   4        I:Key field 3                      CGL029
     C                     PARM *BLANKS   @ACCD  10                                           CGL029
     C                     PARM *BLANKS   @ACSQ   2        I:Key field 4
     C                     PARM *BLANKS   @BRCA   3        I:Key field 5
     C           ACCNT     PARM           DSSDY            O:OUTPUT
      *
      *  If no error, then output the customer number
      *
     C           *IN90     IFEQ '0'
     C           @RTCD     ANDEQ*BLANKS
     C                     MOVELCNUM      O#CUST
     C                     ELSE
      *
      *  Else, get warning information and perform DB error processing
      *
     C                     MOVEL*BLANKS   #MSDTA
     C                     MOVELADCHK1    #MSDTA
      *
     C                     CALL 'MEC1150'              9090
     C                     PARM 'FTM2346 '#MSGID  7
     C                     PARM 'FTUSRMSG'#MSGF  10
     C                     PARM           #MSDTA
     C                     PARM *BLANKS   #MSTX1
     C                     PARM *BLANKS   #MSTX2
      *
     C                     MOVEL#MSTX1    W0MSG
      *
     C                     MOVEL'AOACCTR0'W0FILE           ***************
     C**********           MOVELADACNO    W0KEY            * DB ERROR 04 *                    CFT032
     C                     MOVEL@RETL     W0KEY                                               CFT032
     C                     Z-ADD4         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     MOVELW0MSG     W0PREF
     C                     EXSR SRERR
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXRETL    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRBIC    : Format BIC Identifier                             *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRBIC     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRBIC '  @STK,Q
      *
      *  /Copy point for Format BIC Identifier
      *
     C/COPY WNCPYSRC,FT0035CCP7
      *
      *  If BIC access BIC directory
      *
     C           ADSLSH    IFNE '/'
     C                     MOVELADCHK1    W9BICC
     C           3         SUBSTADCHK1:9  W9BICB  3
     C                     ELSE
     C                     MOVELADCHK2    W9BICC
     C           3         SUBSTADCHK2:9  W9BICB  3
     C                     END
      *
     C                     CALL 'ME1400'               9090
     C                     PARM *BLANKS   W9RTN   7
     C                     PARM           W9BICC  8
     C                     PARM           W9BICB  3
     C                     PARM           SDCUST
     C                     PARM           SDCNST
     C                     PARM           MEBICD
     C                     PARM *BLANKS   W9CUST  1
     C                     PARM *BLANKS   W9CNST  1
     C                     PARM *BLANKS   W9BICD  1
      *
     C           *IN90     IFEQ '1'
     C           W9RTN     OREQ 'MIN0244'
     C           W9RTN     OREQ 'MIN0245'
     C           W9RTN     OREQ 'MIN0246'
     C                     MOVEL'ME1400  'W0FILE           ***************
     C                     MOVEL'*CALL   'W0KEY            * DB ERROR 05 *
     C                     Z-ADD5         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      *  If W9CUST =  'Y' then output the customer number
      *
     C           W9CUST    IFEQ 'Y'
     C                     MOVELBBCUST    O#CUST
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXBIC     TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRADDR   : Format Address                                    *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRADDR    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRADDR'  @STK,Q
      *
      *  /Copy point for Format Address
      *
     C/COPY WNCPYSRC,FT0035CCP8
      *
      *  Unwind subroutine stack name
      *
     C           EXADDR    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *
      *  File and Program Error Processing
      *
     C/COPY FTCPYSRC,SRERRC
     C/EJECT                                                                                  198654
      *****************************************************************                       198654
      *                                                               *                       198654
      *  SRIBAN   : Format IBAN                                       *                       198654
      *                                                               *                       198654
      *  CALLED BY: Main processing                                   *                       198654
      *                                                               *                       198654
      *****************************************************************                       198654
     CSR         SRIBAN    BEGSR                                                              198654
      *                                                                                       198654
      *  Set up subroutine stack name                                                         198654
      *                                                                                       198654
     C                     ADD  1         Q       50                                          198654
     C                     MOVEL'SRIBAN'  @STK,Q                                              198654
      *                                                                                       198654
      *  If IBAN access Accounts file                                                         198654
      *                                                                                       198654
     C           ADSLSH    IFNE '/'                                                           198654
     C                     MOVELADCHK1    P@IBAN 34                                           198654
     C                     ELSE                                                               198654
     C**********           MOVELADEXSW    P@IBAN                                       198654 223500
     C                     MOVE ADCHK1    P@IBAN                                              223500
     C                     END                                                                198654
      *                                                                                       198654
     C                     CALL 'AOIBANR4'                                                    198654
     C                     PARM *BLANKS   @RTCD                                               198654
     C                     PARM '*KEY   ' @OPTN                                               198654
     C                     PARM           P@IBAN                                              198654
     C           ACCNT     PARM           DSSDY                                               198654
      *                                                                                       198654
      *  If error, then do database error processing                                          198654
      *                                                                                       198654
     C           *IN90     IFEQ '1'                                                           198654
     C           @RTCD     ORNE *BLANKS                                                       198654
     C                     MOVEL'AOIBANR4'W0FILE           ***************                    198654
     C                     MOVEL'*CALL   'W0KEY            * DB ERROR 06 *                    198654
     C                     Z-ADD6         W0ERNB           ***************                    198654
     C                     MOVEL'MEM5003' W0MSGD                                              198654
     C                     MOVEL'MIDAS  ' W0MSGF                                              198654
     C                     EXSR SRERR                                                         198654
     C                     ELSE                                                               198654
      *                                                                                       198654
      *  If no error, then output the customer number                                         198654
      *                                                                                       198654
     C                     MOVELCNUM      O#CUST                                              198654
     C                     END                                                                198654
      *                                                                                       198654
      *  Unwind subroutine stack name                                                         198654
      *                                                                                       198654
     C           EXIBAN    TAG                                                                198654
     C                     MOVEA*BLANKS   @STK,Q                                              198654
     C                     SUB  1         Q                                                   198654
      *                                                                                       198654
     CSR                   ENDSR                                                              198654
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRACT    : Undefined action - error                          *
      *                                                               *
      *  CALLED BY: Main Processing                                   *
      *                                                               *
      *  CALLS    : SRERR   - report error and close down program     *
      *                                                               *
      *****************************************************************
     CSR         SRACT     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRACT '  @STK,Q
      *
      *  Database error undefined action
      *
     C                     MOVEL'*TYPE   'W0FILE           ***************
     C                     MOVELW0TYPE    W0KEY            * DB ERROR 15 *
     C                     Z-ADD15        W0ERNB           ***************
     C                     MOVEL'MEM5006' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
      *
      *  Unwind subroutine stack name
      *
     C           EXACT     TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT   : Initialise and define fields                      *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRINIT    BEGSR
      *
      *  Set up copyright statement
      *
     C                     MOVEACPY@      BIS@   80
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
      *
      *  Indicate that set up is complete
      *
     C                     SETON                     50
      *                                                                                       CSC022
      ** Check if CSC022 is switched *ON.                                                     CSC022
      *                                                                                       CSC022
     C                     CALL 'AOSARDR0'                                                    CSC022
     C                     PARM *BLANKS   @RTCD   7                                           CSC022
     C                     PARM '*VERIFY' @OPTN   7                                           CSC022
     C                     PARM 'CSC022'  @SARD   6                                           CSC022
      *                                                                                       CSC022
     C           @RTCD     IFEQ *BLANKS                                                       CSC022
     C                     MOVE 'Y'       CSC022  1                                           CSC022
     C           *NAMVAR   DEFN SCCMTJOB  SCCMT                                               CSC022
     C                     IN   SCCMT                                                         CSC022
     C                     Z-ADD1         C       30                                          CSC022
     C                     MOVEL*BLANKS   WCMTSK  1                                           CSC022
     C                     MOVEACMTARR    CMTJOB                                              CSC022
     C           CMTNUM    IFGT 0                                                             CSC022
     C           C         DOWLECMTNUM                                                        CSC022
     C           ##JOB     IFEQ CMTJOB,C                                                      CSC022
     C                     MOVEL'Y'       WCMTSK                                              CSC022
     C                     ENDIF                                                              CSC022
     C                     ADD  1         C                                                   CSC022
     C                     ENDDO                                                              CSC022
     C                     ENDIF                                                              CSC022
     C                     ELSE                                                               CSC022
     C                     MOVE 'N'       CSC022                                              CSC022
     C                     ENDIF                                                              CSC022
      *
      *  Get Rundate information
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDTA
     C                     IN   RUNDTA
     C                     MOVE AGMRDT    WUMRDT  7        Midas Run date
     C                     MOVE AGRDNB    WURDNB  50       Run day number
     C                     MOVE AGSUC     WUSUC   1        Set up complete
     C                     MOVE AGDFF     WUDFF   1        Date Format
     C                     MOVE AGMBIN    WUMBIN  1        Multi Branched
      *
      *  Extract bank details
      *
     C                     CALL 'AOBANKR0'             9090
     C                     PARM *BLANKS   @RTCD   7        B:Return code
     C                     PARM '*FIRST ' @OPTN   7        I:Option
     C           SDBANK    PARM *BLANKS   DSFDY
      *
      *  If return with an error (LR seton in called program) then
      *  process for database error.
      *
     C           *IN90     IFEQ '1'
     C           @RTCD     OREQ '*ERROR*'
     C                     MOVEL'AOBANKR0'W0FILE
     C                     MOVEL'*CALL'   W0KEY            ***************
     C                     Z-ADD01        W0ERNB           * DB ERROR 01 *
     C                     MOVEL'MEM5003' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXINIT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
     C/COPY WNCPYSRC,FT0035CCPG
      *
      *  /Copy point for Calculations
      *
     C/EJECT
      *
      *  File and Program Error Processing
      *
     O/COPY WNCPYSRC,FT0035OOPG
      *
      *  /Copy point for Outputs
      *
     O/EJECT
**  CPY@ table
(c) Finastra International Limited 2001
