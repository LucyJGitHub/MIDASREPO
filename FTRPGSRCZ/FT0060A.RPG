     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IBLC 2002 Outgoing Payments')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  RPG/FT0060A - Outgoing/Incoming Payment Authorisation        *
      *                                                               *
      *  (phase(s)) Input Cycle                                       *
      *                                                               *
      *  Called By: FT0060  - FT PAYMENT                              *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG11527           Date 21Jun06               *
      *                 CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG11527 - Display error message                             *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *
     FFTOPX1L0IF  E           K        DISK
      ***  IBLC 2002 Outgoing Payments Extended Index
      *
     FFTIPX1L0IF  E           K        DISK
      ***  IBLC 2002 Incoming Payments Extended Index
      *
     FILICDRPDIF  E                    DISK
      ***  IBLC 2002 ICD modules
      *
     FFT006ADFCF  E                    WORKSTN
     F            FT006AF0                          KRENAMESCREEN
      *
     E                    CPY@    1   1 80
      *-------------------------------------------------------------------------
     IPSDS       SDS
      * Get program name from PSDS
     I                                     *PROGRAM ##PGM
     I                                        1  10 PGM
     I                                      244 253 JOB
     I                                      244 246 WSID
     I                                      254 263 USER
      /EJECT
      *-------------------------------------------------------------------------
     IDLDA        DS                            256
      * Data structure for data-base processing
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I            DS
     I                                        1  15 KPREF
     I                                       12  13 TESTOI
     IRUNDAT    E DSRUNDAT
      ***  Rundat Data area
      *
     ISDMMOD    E DSSDMMODPD
      ***  Midas Modules Deatils
      *
     IDSSDY     E DSDSSDY
      ***  DS for access programs, long data structure
      *
     IDSFDY     E DSDSFDY
      ***  DS for access programs, short data structure
      *
     ISDBANK    E DSSDBANKPD
      ***  External data structures for Bank Details
      /EJECT
      *-------------------------------------------------------------------------
      *
      ** Get the data structure passed from calling program
      *
      /COPY QWINDSRC,FT0060DTA
      *
     ISCSARD    E DSSCSARDPD
     IPDATA       DS                           1024
      *
     I                                        7  21 PREFX
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * Main processing                                               *
      *                                                               *
      *****************************************************************
      *
      ** Execute initial routine
      *
     C                     EXSR SRINIT
      *
     C                     EXSR SRENQ
      *
      ** Execute routine to setup return code and exit program
      *
     C                     EXSR SRRTRN
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * SRENQ - Routine to handle 'ENQUIRY' action                    *
      *                                                               *
      *****************************************************************
      *
     C           SRENQ     BEGSR
      *
      ** Set indicators on for 'ENQUIRY' mode to protect fields
      *
     C                     MOVE '1'       *IN10
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record not found,
      ** set DB error indicator, setup message, display screen, exit
      *
     C           *IN89     IFEQ '*ON'
      *
     C                     ELSE
      *
      ** Record found, set file fields to screen fields
      *
     C                     EXSR SRFTOS
      *
      ** Display and handle screen
      *
     C                     EXSR SRSCRN
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SRSMSG - Routine to send messages to message subfile.         *
      *                                                               *
      *****************************************************************
      *
     C           SRSMSG    BEGSR
      *
     C           ZAMSGF    IFEQ *BLANKS
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     ENDIF
     C                     CALL 'SNDERMSG'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZAMSID  7
     C                     PARM           ZAMSGF 10
     C                     PARM           ZAMSDA132
     C                     PARM           ZAMSTP  7
      *
      ** Clear all fields for default mechanism next time.
      *
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZAMSID
     C                     MOVEL*BLANK    ZAMSDA
     C                     MOVEL*BLANK    ZAMSTP
     C                     MOVEL*BLANK    ZAMSGF
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SRCMSG - Routine to clear program's message queue.            *
      *                                                               *
      *****************************************************************
      *
     C           SRCMSG    BEGSR
      *
     C                     CALL 'CLRERMSG'
     C                     PARM ##PGM     ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SRREC - Routine to access file via retrieve index             *
      *                                                               *
      *****************************************************************
      *
     C           SRREC     BEGSR
      *
     C           TESTOI    IFEQ 'OP'
     C           KPREF     CHAINFTOPX1L0             89
     C                     ENDIF
      *
     C           TESTOI    IFEQ 'IN'
     C           KPREF     CHAINFTIPX1L0             89
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SRRTRN - Routine to set up return code for calling program    *
      *                                                               *
      *****************************************************************
      *
     C           SRRTRN    BEGSR
      *
      ** DBF update error
      *
     C           *IN69     IFEQ '1'
     C                     MOVE 'Y2U0004' WWRTRN
     C                     ELSE
      *
      ** F12 pressed
      *
     C           *INKL     IFEQ '1'
     C                     MOVE 'USR0790' WWRTRN
     C                     ELSE
      *
      ** F3 pressed
      *
     C           *INKC     IFEQ '1'
     C                     MOVE 'Y2U9999' WWRTRN
     C                     ELSE
      *
      ** No errors
      *
     C                     MOVE *BLANKS   WWRTRN
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Exit program
      *
     C                     MOVE '1'       *INLR
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SRDBER - Routine to handle database errors                    *
      *                                                               *
      *****************************************************************
      *
     C           SRDBER    BEGSR
      *
      ** Update data area LDA
      *
     C           *NAMVAR   DEFN LDA       DLDA
     C           *LOCK     IN   DLDA
     C                     MOVEL##PGM     DBPGM
     C                     MOVE WWBFIL    DBFILE
     C                     MOVE WWBKEY    DBKEY
     C                     MOVE WWBASE    DBASE
     C                     OUT  DLDA
      *
      ** Set on data-base error indicators
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *IN69
      *
      ** Dump program
      *
     C                     DUMP
      *
      ** Call standard DB error handler
      *
     C                     CALL 'DBERRCTL'             81
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SRSCRN - Routine to handle screen and validation              *
      *                                                               *
      *****************************************************************
      *
     C           SRSCRN    BEGSR
      *
      ** Display messages
      *
     C                     WRITE#MSGCTL
      *
      ** Display main screen
      *
     C                     EXFMTSCREEN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SRFTOS - Routine to move file fields to screen fields         *
      *                                                               *
      *****************************************************************
      *
     C           SRFTOS    BEGSR
      *
     C           *IN12     IFEQ '1'
      *
     C           TESTOI    IFEQ 'OP'
     C                     MOVELOPORTY    #0ORTY
     C                     MOVELOPBETY    #0BETY
     C                     MOVELOPOPCO    #0OPCO
     C                     MOVELOPCOCO    #0COCO
     C                     MOVELOPIDCO    #0IDCO
     C                     MOVELOPIDEN    #0IDEN
     C                     ENDIF
      *
     C           TESTOI    IFEQ 'IN'
     C                     MOVELINORTY    #0ORTY
     C                     MOVELINBETY    #0BETY
     C                     MOVELINOPCO    #0OPCO
     C                     MOVELINCOCO    #0COCO
     C                     MOVELINIDCO    #0IDCO
     C                     MOVELINIDEN    #0IDEN
     C                     ENDIF
      *
     C           #0ORTY    IFEQ *ZEROS
     C                     MOVE *BLANKS   #0ORTY
     C                     ENDIF
      *
     C           #0BETY    IFEQ *ZEROS
     C                     MOVE *BLANKS   #0BETY
     C                     ENDIF
      *
     C           #0OPCO    IFEQ *ZEROS
     C                     MOVE *BLANKS   #0OPCO
     C                     ENDIF
      *
     C           #0COCO    IFEQ *ZEROS
     C                     MOVE '0'       #0COCO
     C                     ENDIF
      *
     C           #0IDCO    IFEQ *ZEROS
     C                     MOVE *BLANKS   #0IDCO
     C                     ENDIF
      *
     C           #0IDEN    IFEQ *ZEROS
     C                     MOVE *BLANKS   #0IDEN
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR                                                         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     DUMP
      *
      ** Call standard DB error handler
      *
     C                     CALL 'DBERRCTL'
     C                     SETON                     U7U8LR
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      * SRINIT - Routine to handle initial processing                 *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Key list to access nostro details
      *
     C           KKEY1     KLIST
     C                     KFLD           KCYCD   3
     C                     KFLD           KNONB   2
      *
      ** Get parameters from calling program
      *
     C           *ENTRY    PLIST
     C                     PARM           ACTION  1
     C                     PARM           PDATA
     C                     PARM           WWRTRN  7
      *
      ** The following parameter is needed from PTF16 on.
      ** Every PTF level below should omit this parameter
      *
     C                     PARM           SPARE 256
      *
      ** Setup key values using transaction data passed from caller
      *
     C                     MOVE PREFX     KPREF
      *
      ** Redefine data-base error fields for program
      *
     C           *LIKE     DEFN DBFILE    WWBFIL
     C           *LIKE     DEFN DBKEY     WWBKEY
     C           *LIKE     DEFN DBASE     WWBASE
      *
      ** Setup file value used in database error during access to
      ** retrieval index
      *
     C           *LIKE     DEFN DBFILE    WWEXTF
     C           TESTOI    IFEQ 'OP'
     C                     MOVEL'FTOPX1L0'WWEXTF
     C                     ENDIF
      *
     C           TESTOI    IFEQ 'IN'
     C                     MOVEL'FTIPX1L0'WWEXTF
     C                     ENDIF
      *
      ** Access to bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   DLDA
     C                     Z-ADD1         DBASE
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVEL'*FIRST'  DBKEY     P
     C                     MOVELPGM       DBPGM     P
     C                     OUT  DLDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Check system data format (DDMMYY or MMDDYY):
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
     C                     MOVELJOB       SWSID     P
     C                     MOVELPGM       SJOB      P
     C                     MOVELUSER      SUSER     P
     C                     MOVE BJMRDT    SRUNA
      *
      ** Initialise non-display indicators
      *
     C                     MOVEL*OFF      *IN12
      *
      ** Initialise error indicators
      *
     C                     MOVEA'00000'   *IN,20
     C                     MOVEA'00000'   *IN,25
     C                     MOVEL*OFF      *IN75
      *
      ** Access ICD Modules
      *
     C                     CALL 'AOMMODR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDMMOD    PARM SDMMOD    DSSDY
     C           @RTCD     IFNE *BLANKS
     C                     MOVEL'SDMMODPD'WWBFIL
     C                     MOVEL'6'       WWBKEY
     C                     Z-ADD03        WWBASE
     C                     EXSR SRDBER
     C                     ENDIF
      *
      ** Verify that ULX043 - IBLC 2002 is activated
      *
     C                     MOVE 'N'       ULX043  1
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*VERIFY' @OPTN
     C                     PARM 'ULX043'  @SARD   6
     C           SCSARD    PARM SCSARD    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
     C           @RTCD     ANDNE'*NRF   '
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVEL'SCSARDPD'DBFILE
     C                     MOVEL@SARD     DBKEY
     C                     Z-ADD1         DBASE
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           @RTCD     IFEQ *BLANKS
     C                     MOVE 'Y'       ULX043
      *                                                                                     BUG11527
      ** If IBLC Returns module is installed, user must access IBLC 2002 -                  BUG11527
      **    BCL Reporting - ICD Maintenance for ILICDRPD to automatically be                BUG11527
      **    populated                                                                       BUG11527
      *                                                                                     BUG11527
     C           BGNWST    IFEQ 'Y'                                                         BUG11527
      *
      ** Access ICD for IBLC 2002
      *
     C           1         SETLLILICDRPD
     C                     READ ILICDRPD                 99
     C           *IN99     IFEQ *ON
     C                     MOVEL'ILICDRPD'WWBFIL
     C                     MOVEL'ICDR'    WWBKEY
     C                     Z-ADD04        WWBASE
     C                     EXSR SRDBER
     C                     ENDIF
      *                                                                                     BUG11527
     C                     ENDIF                                                            BUG11527
      *
     C                     ENDIF
      *
      ** Check if IBLC Returns module installed
      *
     C           BGNWST    IFEQ 'Y'
     C           ULX043    ANDEQ'Y'
     C           AGRDNB    ANDGEICACTD
     C           BGNWST    OREQ 'Y'
     C           ULX043    ANDEQ'Y'
     C           AGRDNB    ANDGT10958
     C                     MOVE '1'       *IN12
      *
     C                     MOVELSPARE     SP246 246
     C                     MOVE SP246     F12     1
      *
     C                     MOVE *BLANK    F12
     C                     MOVE F12       SP246
     C                     MOVELSP246     SPARE
      *
     C                     ELSE
      *
     C                     MOVEL*OFF      *IN12
      *
     C                     MOVELSPARE     SP246 246
     C                     MOVE SP246     F12     1
      *
     C           F12       IFEQ 'R'
     C                     MOVE 'USR0790' WWRTRN
     C                     MOVE *BLANK    F12
     C                     MOVE F12       SP246
     C                     MOVELSP246     SPARE
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2005
