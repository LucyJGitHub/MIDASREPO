     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IBLC 2002 Set Up Chq to be paid extens. file')   *
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  RPG/FT0080XL  - SET UP IBLC CHEQUES TO BE PAID EXTENSION     *
      *                  FILE                                         *
      *                                                               *
      *  Function:  This program set up IBLC Cheques to be paid       *
      *             extended file with default values.                *
      *                                                               *
      *  (phase(s)) Installation                                      *
      *                                                               *
      *  Called By: ILC0000 - IBLC MODULE INSTALLATION                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      *  Last Amend No. CCB020             Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027A            Date 11May06               *
      *                 CER001             Date 25Apr05               *
      *                 ULX043             Date 16Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCB020 - COB Restructure - Secondary COB infrastracture      *
      *  CSD027A- Conversion of cust. no. to alpha (post 103 build)   *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     FCQPADDD IF  E                    DISK
      ***  Cheques to be paid debit details
      *
     F***CQPACXL UF  E           K        DISK                      A                         CER001
     FFTCPX1PDUF  E           K        DISK                      A                            CER001
      ***  Cheques to be paid extended file                Prefix CP
      *
     F***SDCUSTYLIF  E           K        DISK                                                CER001
     FSDCUX1L0IF  E           K        DISK                                                   CER001
      ***  Customer details                                Prefix CB
      *
      /EJECT
      *-------------------------------------------------------------------------
     E                    CPY@    1   1 80               COPYRIGHT  ARRAY
      /EJECT
      *-------------------------------------------------------------------------
     I           SDS
      * Get program name from PSDS
     I                                     *PROGRAM ##PGM
      /EJECT
      *-------------------------------------------------------------------------
     IDLDA        DS                            256
      * Data structure for data-base processing
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*MPHAS****   DS                              1                                          CCB020
     I**********                              1   1 COB                                       CCB020
      /EJECT
      *
      * Redefine data-base error fields for program
      *
     C           *LIKE     DEFN DBFILE    WWBFIL
     C           *LIKE     DEFN DBKEY     WWBKEY
     C           *LIKE     DEFN DBASE     WWBASE
      /EJECT
      *-------------------------------------------------------------------------
      * Main processing
      *-------------------------------------------------------------------------
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C********** *NAMVAR   DEFN           MPHAS                                               CCB020
     C**********           IN   MPHAS                                                         CCB020
     C                     MOVEL'CBC00100'@CLPRM  9                                           CCB020
     C                     MOVE '1'       @CLPRM                                              CCB020
     C                     CALL @CLPRM                                                        CCB020
     C                     PARM *BLANKS   @RTPHS  4                                           CCB020
     C********** COB       IFEQ 'C'                                                           CCB020
     C           @RTPHS    IFEQ '*YES'                                                        CCB020
     C                     Z-ADD1         POPCL
     C                     END
      *
     C           CHEKEY    KLIST
     C                     KFLD           PREF
     C                     KFLD           CQSQ
      *
      ***  First read
      *
     C                     READ CQPADDDF                 88
      *
      ***  Process until 88 indicator not on
      *
     C           *IN88     DOWEQ*OFF
      *
      ***  Check if record exists in the extension file
      *
     C           CHEKEY    CHAINCQPACDL              89
      *
      ***  If record not in the extension file : FIELDS SET UP & WRITE
      *
     C           *IN89     IFEQ '1'
      *
      ***  Retrieve Ordering customer number
      *
     C                     MOVE DRPT      PTYPE   1
     C                     MOVELDRPY      PLINE  35
     C                     MOVELDRCY      WWCYCD  3
      *
     C                     CALL 'IL0421'
      * Outgoing
     C                     PARM WWCYCD    PCCY    3
     C                     PARM           PTYPE
     C                     PARM           PLINE
     C                     PARM           POPCL   10
      * Incoming
     C**********           PARM *BLANKS   PACCD   4                                           CER001
     C                     PARM *BLANKS   PACCD  10                                           CER001
     C                     PARM *BLANKS   PCUST   6
     C                     PARM *BLANK    PFOUND  1
      *
     C                     MOVE PCUST     WWCUST  6
      *
     C********** WWCUST    CHAINSDCUSTYL             87                                       CER001
     C           WWCUST    CHAINSDCUX1L0             87                                       CER001
     C           *IN87     IFEQ *ON
     C**********           MOVEL'SDCUSTYL'DBFILE           ***************                    CER001
     C                     MOVEL'SDCUX1L0'DBFILE           ***************                    CER001
     C                     MOVELWWCUST    DBKEY            * DBERROR 004 *
     C                     Z-ADD4         DBASE            ***************
     C                     EXSR SRDBER
     C                     END
     C                     MOVE CUTYPE    CPORTY
      *
     C                     MOVE PREF      CPPREF
     C                     Z-ADDCQSQ      CPCQSQ
     C                     MOVE '  '      CPBETY
      *
     C                     MOVE *BLANKS   CPOPCO
     C                     MOVE *BLANKS   CPIDCO
     C                     MOVE *BLANKS   CPIDEN
     C                     MOVE *BLANKS   CPCOCO
      *
     C                     WRITECQPACDL
     C                     ENDIF
      *
      ***  Main file read
      *
     C                     READ CQPADDDF                 88
      *
     C                     ENDDO
      *-------------------------------------------------------------------------
      * End of processing
      *-------------------------------------------------------------------------
      *
      ***  Close external program
      *
     C********** COB       IFNE 'C'                                                           CCB020
     C           @RTPHS    IFEQ '*NO '                                                        CCB020
     C                     Z-ADD9         POPCL
     C                     CALL 'IL0421'
     C                     PARM           PCCY
     C                     PARM           PTYPE
     C                     PARM           PLINE
     C                     PARM           POPCL
     C                     PARM           PACCD
     C                     PARM           PCUST
     C                     PARM           PFOUND
     C                     END
      *
     C                     SETON                     LR
      *
     C                     RETRN
      /EJECT
      *-------------------------------------------------------------------------
      * SRDBER - Routine to handle database errors
      *-------------------------------------------------------------------------
      *
     C           SRDBER    BEGSR
      *
      ***  Update data area LDA
      *
     C           *NAMVAR   DEFN LDA       DLDA
     C           *LOCK     IN   DLDA
     C                     OUT  DLDA
      *
      ***  Dump program
      *
     C                     DUMP
      *
     C                     ENDSR
      /EJECT
** CPY@  COPYRIGHT
(c) Misys International Banking Systems Ltd. 2005
