     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FT Incoming payments valid. s1/lvl 1')           *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Funds Transfer Module                                *
     F*                                                               *
     F*  FT0352 - Validate Incoming Payments - Level 1                *
     F*                                                               *
     F*  Function:  This program validates the level 1 input for      *
     F*             Outgoing Payments.                                *
     F*                                                               *
     F*  Called By: FT0350  - Incoming Payments Maintenence           *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      *  Last Amend No. CSW215             Date 06Apr15               *
      *  Prev Amend No. CSW213             Date 03Jun13               *
      *                 MD020161           Date 24Apr13               *
      *                 CRE075             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 BUG24738           Date 06Jul09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 BUG15156           Date 29Oct07               *
      *                 BUG15201           Date 30Oct07               *
      *                 250600             Date 10Oct07               *
      *                 CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CFT032             Date 11Sep06               *
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 BUG3644            Date 12Jul04               *
      *                 CLE025             Date 20Oct03               *
      *                 CSC022             Date 24Feb04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CFT017             Date 08Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CFT014             Date 25May00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 175484             Date 23Feb00               *
      *                 175479             Date 14Feb00               *
      * Midas DBA 3.02 -----------------------------------------------*
     F*                 CFT004             DATE 27Jul99               *
      * Midas DBA 3.01 -----------------------------------------------*
     F*                 CGL009             Date 18Aug99               *
     F*                 166199             Date 18Aug99               *
     F*                 166210             Date 18Aug99               *
     F*                 166214             Date 18Aug99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 CFT002             Date 12Apr99               *
     F*                 148229             Date 13Dec98               *
     F*                 CEU006             Date 06May98               *
     F*                 122060             Date 29Oct97               *
     F*                 CRT002             Date 01Dec95               *
     F*                 S01408             Date 01Nov96               *
     F*                 069250             DATE 03MAY94               *
     F*                 068230             DATE 23JAN94               *
     F*                 S01435             DATE 08OCT93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSW215 - SWIFT Changes 2015                                  *
      *  CSW213 - SWIFT Changes 2013 (Recompile)                      *
      *  MD020161 - Messages sent thru Multicash not processed by STP *
      *  CRE075 - Effective Date for Retail Accounts (Recompile)      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  BUG24738 - DBerr error when retail account with '/' prefix   *
      *  BUG15156 - Beneficiary is not translated correctly into      *
      *             Midas account                                     *
      *  BUG15201 - Encounter Database Errors (Recompile)             *
      *  250600 - Fix Errors in CFT032 and CSW207                     *
      *  CRE026 - Consumer Banking (Recompile)                        *
      *  CFT032 - Account line in field 50k in MT103 (recompile)      *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  BUG3644 - Serious Error in Collateralised Lending (Recompile)*
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CFT017 - Multilateral Inter-bank Exchange Fee. (Recompile)   *
     F*  CFT014 - Addition of MT103 messages                          *
     F*  175484 - Fix on % of discrepancy allowed for entered rate    *
     F*  175479 - FONTIS : Recompile over changes in parameters       *
     F*  CFT004 - International Bank Account Number                   *
     F*           Enable IBAN in field Beneficiary                    *
     F*  CGL009 - FONTIS IAT/TPP interface                            *
     F*  166199 - System rate when CEU006 is on                       *
     F*  166210 - Account DS overlapped by CEU006 changes             *
     F*  166214 - Missing parameters in FT0010                        *
     F*  CFT002 - STP phase 1                                         *
     F*  148229 - Changes to /COPY FT0010I001 (Recompile only)        *
     F*  CEU006 - EMU FT Payments and Settlement.                     *
     F*  122060 - Drafts Issuance Function.                           *
     F*  CRT002 - Retail Branch Access.                               *
     F*  S01408 - Addition of core hook FT0352CCP4                    *
     F*         - Addition of core hook FT0352CCP3                    *
     F*         - Addition of core hook FT0352CCP2                    *
     F*         - Addition of core hook FT0352CCP1                    *
     F*  069250 - Remove dummy OVRDBF command from creation parms.    *
     F*  068230 - Add validation for SWIFT in sender and Beneficiary  *
     F*  S01435 - Incoming Message Management                         *
     F*                                                               *
     F*****************************************************************
     F*
     F*  Indicator usage
     F*  ~~~~~~~~~~~~~~~
     F*
     F*  31    Invalid S.W.I.F.T. Address format
     F*  32    S.W.I.F.T. Address not on file
     F*
     F*  50    Program Initialised
     F*
     F*  89    Error processing on return from FT0010                     CEU006
     F*  90    General Error Indicator
     F*  91    / found
     F*  92    Embedded spaces found
     F*  93    Illegal characters found
     F*  94    Work indicator used in checking illegal characters
     F*
     F*  U7/U8 Error Ocurred
     F*  LR    End program
     F*
     F*****************************************************************
     FACCNT   IF  E           K        DISK         KINFSR SRFILE     UC
     F            ACCNTAAF                          KIGNORE
     F            ACCNTACF                          KIGNORE
      *
      * RTV : General Ledger Accounts by GL Account Number
      *
     FACNUMA  IF  E           K        DISK         KINFSR SRFILE     UC
     F            ACCNTABF                          KRENAMEACNUMAF
      *
      * RSQ : General Ledger Accounts by Retail Account Number
      *
     FPYCHGDB IF  E           K        DISK         KINFSR SRFILE     UC
      *
      * RSQ : Payment and charges details
      *
     FFDDTSTL1IF  E           K        DISK                                                   CFT002
      *                                                                                       CFT002
      ** Standing data module - Deal type/subtype file                                        CFT002
      *                                                                                       CFT002
     FMEISOCL1IF  E           K        DISK                               CFT014
      *                                                                   CFT014
      ** ISO Country Codes file                                           CFT014
      *                                                                   CFT014
     E/COPY FTCPYSRC,SFTSWFTF
     F/COPY WNCPYSRC,FT0352DFPG
      *
      *  /Copy point for File Definition
      *
     F/EJECT
     E                    CPY@    1   1 80
      *
      *  Copyright table
      *
     E/COPY WNCPYSRC,FT0352DEPG
      *
      *  /Copy point for Arrays
      *
     E/COPY FTCPYSRC,SRERRE
      *
      *  End of Copysource for error processing
      *
      *
      *  Error Codes Output
      *
     E                    ERCD       38  4               Codes
     E/COPY FTCPYSRC,SFTSWFTE
      *
      *  Warning codes array
      *
     E                    WRN@       40  1
      *Array for General Ledger Accounts                                  CFT014
     E                    YA3        18  1                                CFT014
      *Alphabetic array                                                   CFT004
     E                    ALPH   26  26  1                                CFT004
      *Numeric                                                            CFT004
     E                    NUMC   10  10  1                                CFT004
      *                                                                   CFT014
      *  Power table                                                      CFT014
      *                                                                   CFT014
     E                    POWER   1   7  7 3                              CFT014
      *
     E/EJECT
      *
      * Rename of ACCNT/ACNUMA Profit Centre to avoid overwriting
      *
     IACCNTABF
     I              PRFC                            ACPRFC
      *
     IACNUMAF
     I              PRFC                            ACPRFC
      /EJECT
      *
     I/COPY WNCPYSRC,FT0352DIPG
      *
      *  /Copy point for Input specifications
      *
      *
      /EJECT                                                              CEU006
      *                                                                   CEU006
     I/COPY FTCPYSRC,FT0010I001                                           CEU006
      *                                                                   CEU006
     ICPYR@#      DS
      *
      *  Data structure for compilation  - Copyright insertion
      *
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I/COPY FTCPYSRC,SRERRI
      *
      *  End of Program Error Processing source
      *
      *  Data Structures for parameters to access programs
      *
     IDSFDY     E DSDSFDY
     IDSSDY     E DSDSSDY
      *
      *  Data Structures used by access programs
      *
     ISDCURR    E DSSDCURRPD
     ISDNOST    E DSSDNOSTPD
      *           Nostro location code
     I                                       29  31 NLOC
     ISDCUST    E DSSDCUSTPD
      *
      *  Calling parameter data structure for AOCUSTR0
      *
     ISDCNST    E DSSDCNSTPD
      *
      *  Calling parameter data structure for ME1400
      *
     IMEBICD    E DSMEBICDPD
      *
      *  Calling parameter data structure for ME1400
      *
     ISDCTRY    E DSSDCTRYPD                                              CFT014
      *                                                                   CFT014
      ** Country codes file format data structure                         CFT014
      *                                                                   CFT014
     ISDLOCN    E DSSDLOCNPD                                              CFT014
      *                                                                   CFT014
      ** Location codes file format data structure                        CFT014
      *                                                                   CFT014
     ISDDLST    E DSSDDLSTPD
     ISDBRCH    E DSSDBRCHPD
     I              QQDFAC                          DFACQQ                                    CGL029
     ISDBOOK    E DSSDBOOKPD
     ISDPRFC    E DSSDPRFCPD
     ISDFTFR    E DSSDFTFRPD                                              166214
     I/COPY WNCPYSRC,FT0352I001
      *
     I*************OACCNT    E DSACCNTAB                   4              CFT014
     IOACCNT    E DSACCNTAB                   5                           CFT014
     I/COPY WNCPYSRC,FT0352I002
      *
      *  Define account data structure
      *
      *  1 = Receiver's Correspondent (DR)
      *  2 = Sender (DR)
      *  3 = Beneficiary (CR)
      *  4 = Account with Institution (CR)
      *  5 = Third Reimbursement Institution (DR)                         CFT014
      *****                                                         CEU006166210
      **Switchable*Features*Descriptions**                          CEU006166210
      *****                                                         CEU006166210
     I*****SCSARD    E DSSCSARDPD                                   CEU006166210
      *****                                                         CEU006166210
      **Rename*of*SCSARDPD/LCD*Last*change*date*for*compile*reasons*CEU006166210
      *****                                                         CEU006166210
     I**************LCD                             SCLCD           CEU006166210
      *
      *  Data Structure redefining ACCNT key fields
      *
     I                                        2   7 CNUMA
     I**********                             11  14 ACODA                                     CGL029
     I                                       11  14 ACODA#                                    CGL029
     I                                       15  16 ACSQA
     I                                        2  19 AKEYF
     I                                      554 563 ACODA                                     CGL029
      *                                                                   166210
      * Switchable Features Descriptions *                                166210
      *                                                                   166210
     ISCSARD    E DSSCSARDPD                                              166210
      *                                                                   166210
      * Rename of SCSARDPD/LCD Last change date for compile reasons       166210
      *                                                                   166210
     I              LCD                             SCLCD                 166210
      *
     IRUNDTA    E DSRUNDAT
      *
      *  Define rundat data area
      *
      *
     IZMUSER    E DSZMUSER
      *
      *  User / Branch Data Area
      *
     E/COPY FTCPYSRC,SFT0350S
     E/COPY FTCPYSRC,SFTVALDI
     E/COPY FTCPYSRC,SFT0350F
     E/COPY FTCPYSRC,SFT0350I
     ID#DTA       DS                            256
      *
      *  Calling parameter data structure for AOCCYCV0
      *
      *                                       From Currency & amount
     I                                        1   3 D#FCCY
     I                                    P   4  110D#FAMT
      *                                       To Currency & amount
     I                                       12  14 D#TCCY
     I                                    P  15  220D#TAMT
      *                                       Rate and multi/div ind.
     I                                       23  23 D#MDIN
     I                                    P  24  368D#RATE
      *                            Output     Rate and multi/div ind.
      *                                       If no in rate
     I                                       37  37 O#MDIN
     I                                    P  38  508O#RATE
      *
     E/COPY FTCPYSRC,SFTADDRI
     ICLSWTK      DS
      *
      *  Redefine SWIFT array as field for SETLL.
      *
     I                                        1  12 SW
      *
     I            DS
      *  Currency, amounts and rates
     I                                        1  48 R#RCHK
     I                                        1   3 R#SMCY
     I                                        4   6 R#PCCY
     I                                        7  190R#SMAM
     I                                       20  320R#PYAM
     I                                       33  458R#RATE
     I                                       46  46 R#OVRD
     I                                       47  48 R#STMT
      *
      *                                                                          CFT014
     I************DS                                                      CFT014
      *  Accounts likely to be DR/CR for this Incoming                    CFT014
     IDRCRAC      DS                                                      CFT014
     I                                        1  18 RCCDS
     I                                       19  36 SNDDS
     I                                       37  54 BNCDS
     I                                       55  72 ACBDS
     I                                       73  90 TRIDS                 CFT014
     I/COPY WNCPYSRC,FT0352I003
      *                                                                   CFT014
      *  Accounts likely to be DR/CR for this Incoming                    CFT014
      *
     IIMACNT      DS                             18
      *
      *  Define full account to be passed for FT IMM update
      *
     I**********                              1   60IMCNUM                                    CSD027
     I                                        1   6 IMCNUM                                    CSD027
     I                                        7   9 IMCCY
     I                                       10  130IMACOD
     I                                       14  150IMACSQ
     I                                       16  18 IMBRCA
      *
      *                                                                   CGL009
      ** Data Structure to hold Beneficiary as a partial account + CCY    CGL009
      *                                                                   CGL009
     IDSBNC1      DS                             35                       CGL009
     I**********                              1  15 WBNPA                              CGL009 CGL029
     I**********                              1  18 WBNPAC                             CGL009 CGL029
     I**********                              1   6 WBNCN                              CGL009 CGL029
     I**********                              7  10 WBNAC                              CGL009 CGL029
     I**********                             11  12 WBNSQ                              CGL009 CGL029
     I**********                             13  15 WBNBR                              CGL009 CGL029
     I**********                             16  18 WBNCY                              CGL009 CGL029
     I**********                             19  35 WBNFI                              CGL009 CGL029
     I                                        1  21 WBNPA                                     CGL029
     I                                        1  24 WBNPAC                                    CGL029
     I                                        1   6 WBNCN                                     CGL029
     I                                        7  16 WBNAC                                     CGL029
     I                                       17  18 WBNSQ                                     CGL029
     I                                       19  21 WBNBR                                     CGL029
     I                                       22  24 WBNCY                                     CGL029
     I                                       25  35 WBNFI                                     CGL029
      *                                                                   CGL009
      ** Data Structure to hold G/L account                               CGL009
      *                                                                   CGL009
     IDSGLAC      DS                                                      CGL009
     I                                        1   6 WGLCN                 CGL009
     I                                        7   9 WGLCY                 CGL009
     I**********                             10  13 WGLAC                              CGL009 CGL029
     I**********                             14  15 WGLSQ                              CGL009 CGL029
     I**********                             16  18 WGLBR                              CGL009 CGL029
     I                                       10  19 WGLAC                                     CGL029
     I                                       20  21 WGLSQ                                     CGL029
     I                                       22  24 WGLBR                                     CGL029
      *                                                                   166199
      ** Data Structure for P#INDC of FT0010                              166199
      *                                                                   166199
     IDSINDC      DS                                                      166199
     I                                        1   1 WINDC1                166199
     I                                        2   2 WINDC2                166199
     I                                        3   3 WINDC3                166199
     I                                        4   4 WINDC4                166199
     I                                        5   5 WINDC5                166199
      *                                                                   CFT014
      ** Data Structure to decompose SWIFT adress                         CFT014
      *                                                                   CFT014
     ICSIDWS      DS                                                      CFT014
     I                                        1  12 W0112W                CFT014
     I                                        5   6 W0506W                CFT014
      *                                                                   CFT014
      ** Data Structure to decompose SWIFT adress                         CFT014
      *                                                                   CFT014
     IBTIDA8      DS                                                      CFT014
     I                                        1  12 W0112A                CFT014
     I                                        5   6 W0506A                CFT014
      *                                                                   CFT014
      ** Data Structure to decompose SWIFT adress (branch)                CFT014
      *                                                                   CFT014
     ICSIDBB      DS                                                      CFT014
     I                                        1  12 W0112B                CFT014
     I                                        5   6 W0506B                CFT014
     IDSMTR       DS
      *
      *  Define fields for message transalation
      *
     I                                        1 132 #MSDTA
     I                                       15  30 #MPREF
     I                                    P   1   20#MPART
     I                                      133 264 #MSTX1
     I#MSTX2      DS
     I                                        1 256 #MSTXA
     I                                      257 512 #MSTXB
     I* DS for IBAN format                                                CFT004
     I            DS                                                      CFT004
     I                                        1   4 IBCHK                 CFT004
     I                                        1   1 ICTY1                 CFT004
     I                                        2   2 ICTY2                 CFT004
     I                                        3   3 IDGT1                 CFT004
     I                                        4   4 IDGT2                 CFT004
     E/COPY FTCPYSRC,SFTVALCS
     E/COPY WNCPYSRC,FT0352E001
     I/EJECT
      *****************************************************************
      *                 M A I N L I N E
      *****************************************************************
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *  Define entry parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           W0RTN   7
     C                     PARM           W0ACT   8
     C                     PARM           S1DATA
     C                     PARM           V#DATA
     C                     PARM           F1DATA
     C                     PARM           E#DATA
     C                     PARM           C#DATA
     C                     PARM           I1DATA
      *
      *  Set up Primary Reference for database errors
      *
     C                     MOVELS1DATA    W0PREF
      *
      *  Initialise program
      *
     C           *IN50     IFEQ '0'
     C                     EXSR SRINIT
     C                     END
      *
      *  Determine the mode of processing and execute subroutine :-
      *
      *
      *  Action Code : *VALID
      *  --------------------
      *
      *  Called from FT0350 to validate all input fields on the detail
      *  screen. Once verified, control returns to the calling program
      *  where the appropriate error indicators are set on and the
      *  processing continues.
      *
      *
     C           W0ACT     CASEQ'*VALID  'SRPROC
     C                     CAS            SRACT
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      *  Return to calling program
      *
     C                     RETRN
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRPROC   : Validate All Detail Fields Entered                *
      *             Seton appropriate indicators and set up the array *
      *             of relevant error codes for display.              *
      *                                                               *
      *  CALLED BY: Main processing section                           *
      *                                                               *
      *  CALLS    : SRERR   - report error and close down program     *
      *                                                               *
      *****************************************************************
     CSR         SRPROC    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRPROC'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352PROC
      *
      *  Move warning array passed to WRN@ and error indicators
      *
     C                     MOVEAE#WARA    WRN@
     C                     MOVEA*BLANKS   ERCD
     C                     CLEARI1DATA
     C                     Z-ADD0         X       50
      *
      * See if Payment Details have changed
      *
      * If the payment has been entered by the user, the settlement
      *  cannot be changed, but if the amount, currency, rate or
      *  override ind is changed, then the amounts will be re-calculat-
      *  ed.
      *
      * If the payment has been generated from an incoming payment, the
      *  amounts, currencies, rate and override ind cannot be changed,
      *  but the settlement type must be entered.  This will cause the
      *  receive amount, which is zero when the payment is generated,
      *  to be calculated from the pay amount.
      *
      *
      * Clear work fields
      *
     C                     EXSR SRCLR
      *
      * Validate Booking Branch
      *
      * If Multibranching environment validate booking branch
      *
     C           WUMBIN    IFEQ 'Y'
     C                     EXSR SRBRCA
     C                     END
      *
      *  Validate Originating Branch
      *
     C           WUMBIN    IFEQ 'Y'
     C           V#OBUV    ANDEQ'Y'
     C                     EXSR SRORBR
     C                     END
      *
      *  Get Access Payment Charges
      *
     C           I1BRCA    IFEQ *BLANKS
     C           I1ORBR    ANDEQ*BLANKS
     C                     EXSR SRPAYC
     C                     END
     C/COPY WNCPYSRC,FT0352C006
      *
      *  Validate Payment Subtype
      *
     C                     EXSR SRPYST
      *
      *  Validate Swift Code
      *
     C                     EXSR SRSWPC
      *
      *  Validate Sender
      *
     C                     EXSR SRSND
      *
      *  Validate Settle Currency
      *
     C                     EXSR SRSMCY
      *
      *  Validate Settle Amount if settle currency valid
      *
     C           I1SMCY    IFEQ *BLANKS
     C                     EXSR SRSMAM
      *
      *  Validate Receiver'Correspondent
      *
     C                     EXSR SRRCCO
      *                                                                   CFT014
      *  Validate Third Reimbursement Institution                         CFT014
      *                                                                   CFT014
     C           CFT014    IFEQ 'Y'                                       CFT014
     C                     EXSR SRTRIB                                    CFT014
     C           I1TRIB    IFEQ *BLANK                                    CFT014
     C                     EXSR ACTRIB                                    CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CFT014
     C                     END
      *
      *  Validate Pay Currency
      *
     C                     EXSR SRPCCY
     C/COPY WNCPYSRC,FT0352C022
      *
      *  Validate Pay Amount if pay currency valid
      *
     C           I1PCCY    IFEQ *BLANKS
     C                     EXSR SRPYAM
      *
      *  Validate Beneficiary
      *
     C                     EXSR SRBNC
      *
      *  Validate Account with Institution
      *
     C                     EXSR SRACBK
     C                     END
      *
      *  Validate Settle Date
      *
     C                     EXSR SRSLDT
      *
      *  Validate Pay Date
      *
     C                     EXSR SRPVDT
      *
      *  Validate Rate if currencies and amounts are valid
      *
     C           I1SMCY    IFEQ *BLANKS
     C           I1PCCY    ANDEQ*BLANKS
     C           I1PYAM    ANDEQ*BLANKS
     C           I1SMAM    ANDEQ*BLANKS
     C                     EXSR SRRATE
      *
      *  Validate Override Flag if rate valid
      *
     C           I1RATE    IFEQ *BLANKS
     C                     EXSR SROVRD
     C                     END
      *
     C                     END
      *
      *  Validate Pay Amount plus Charges
      *
     C                     EXSR SRADDC
      *                                                                   CFT014
      *  Validate Receiver's charges currency from the Network            CFT014
      *                                                                   CFT014
     C           CFT014    IFEQ 'Y'                                       CFT014
     C           I1PCCY    IFEQ *BLANKS                                   CFT014
     C           I1SMCY    OREQ *BLANKS                                   CFT014
     C                     EXSR SRRCCN                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      *  Validate Receiver's charges amount from the Network              CFT014
      *                                                                   CFT014
     C           I1RCCN    IFEQ *BLANKS                                   CFT014
     C                     EXSR SRRCNW                                    CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CFT014
      *
      *  Validate the Book Code.
      *
     C                     EXSR SRBOOK
      *
      *  Validate the Profit Centre if Used.
      *
     C           V#PRCU    IFEQ 'Y'
     C                     EXSR SRPRFC
     C                     END
      *                                                                   CEU006
      *  Validate Notified delivery currency                              CEU006
      *                                                                   CEU006
     C           CEU006    IFEQ 'Y'                                       CEU006
     C                     EXSR SRNDCY                                    CEU006
      *                                                                   CEU006
      * If entered and valid                                              CEU006
     C           V#NDCY    IFNE *BLANKS                                   CEU006
     C           I1NDCY    ANDEQ*BLANKS                                   CEU006
      *                                                                   CEU006
      *  Validate Notified delivery amount                                CEU006
     C                     EXSR SRNDAM                                    CEU006
     C                     ENDIF                                          CEU006
      *                                                                   CEU006
      *  Cross validate amounts                                           CEU006
     C           I1NDCY    IFEQ *BLANKS                                   CEU006
     C           I1NDAM    ANDEQ*BLANKS                                   CEU006
     C           I1SMCY    ANDEQ*BLANKS                                   CEU006
     C           I1SMAM    ANDEQ*BLANKS                                   CEU006
     C           I1PCCY    ANDEQ*BLANKS                                   CEU006
     C           I1PYAM    ANDEQ*BLANKS                                   CEU006
     C                     EXSR SRAMTV                                    CEU006
     C           CFT014    IFEQ 'Y'                                       CFT014
     C                     EXSR SROEXR                                    CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CEU006
      *                                                                   CEU006
     C                     ENDIF                                          CEU006
      *
      *  Cross validation subroutine
      *
     C           E#NOUP    IFEQ *BLANKS
     C                     EXSR SRXVAL
     C                     END
      *
      *  Move waring array back and errors
      *
     C                     MOVEAWRN@      E#WARA
     C                     MOVEAERCD      C#ERCD
      *
      *  Unwind subroutine stack name
      *
     C           EXPROC    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRCLR    : Validate Booking Branch                           *
      *                                                               *
      *  CALLED BY: SRPROC                                            *
      *                                                               *
      *  CALLS    :                                                   *
      *                                                               *
      *****************************************************************
     CSR         SRCLR     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRCLR '  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352CLR
      *
      *  Clear account arrays
      *
     C***********1         DO   4         Y#                              CFT014
     C           1         DO   5         Y#                              CFT014
     C/COPY WNCPYSRC,FT0352C007
     C           Y#        OCUR OACCNT
     C                     CLEAROACCNT
     C                     ENDDO
      *
      *  Set Pay location to System default
      *
     C                     MOVELV#SLCD    PLOC
     C                     MOVEL'S'       NOWRK
      *
      *  Save entered amounts
      *
     C           *LIKE     DEFN S1SMAM    K#SMAM
     C           *LIKE     DEFN S1PYAM    K#PYAM
     C                     MOVELS1SMAM    K#SMAM
     C                     MOVELS1PYAM    K#PYAM
      *                                                                   CFT014
      *  Clear accounts likely to be DR/CR for this Incoming              CFT014
      *  payment                                                          CFT014
      *                                                                   CFT014
     C                     CLEARDRCRAC                                    CFT014
     C           *LIKE     DEFN BBCSID    WSCSID                          CFT014
     C           *LIKE     DEFN BBCOLC    WSCOLC                          CFT014
     C                     MOVEL*BLANK    WSCSID                          CFT014
     C                     MOVEL*BLANK    WSCOLC                          CFT014
      *
      *  Unwind subroutine stack name
      *
     C           EXCLR     TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q       50
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRBRCA   : Validate Booking Branch                           *
      *                                                               *
      *  CALLED BY: SRPROC                                            *
      *                                                               *
      *  CALLS    : AOBRCHR0                                          *
      *             ZVACTBU                                           *
      *                                                               *
      *****************************************************************
     CSR         SRBRCA    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRBRCA'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352BRCA
      *
      * If Booking Branch blank, use default branch on data area ZMUSER
      *
     C           S1BRCA    IFEQ *BLANKS
     C                     MOVELDBRN      S1BRCA
     C                     END
      *
      * Check Booking Branch valid by calling 'AOBRCHR0'
      *
     C**********           CALL 'AOBRCHR0'             9090                                   CGL029
     C                     CALL 'AOBRCHR1'             9090                                   CGL029
     C                     PARM *BLANKS   RTCD    7
     C                     PARM '*KEY   ' @OPTN
     C                     PARM S1BRCA    @BRCD   3
     C********** SDBRCH    PARM *BLANKS   DSFDY                                               CGL029
     C           SDBRCH    PARM *BLANKS   DSSDY                                               CGL029
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AOBRCHR0'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 01 *
     C                     Z-ADD1         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      * Branch does not exist
      *
     C           RTCD      IFNE *BLANKS
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1BRCA    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2021' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BRCA
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXBRCA
     C                     END
      *
      * Check user is authorised to perform action code on
      * booking branch
      *
     C**********           CALL 'ZVACTBU'              9090                                 MD020161
     C                     CALL 'ZVACTBU'              90                                   MD020161
     C                     PARM S1ACTD    @ACTN   1
     C                     PARM S1BRCA    @BRCD   3
     C                     PARM 0         ERR     10
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'ZVACTBU 'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 02 *
     C                     Z-ADD2         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      * User not authorised to any action codes for this item and branch
      *
     C           ERR       IFEQ 1
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2023' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BRCA
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXBRCA
     C                     END
      *
      * User not authorised to action code for this particular item
      * and branch
      *
     C           ERR       IFEQ 2
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1ACTD    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2024' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BRCA
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXBRCA
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXBRCA    TAG
     C                     MOVELS1BRCA    V#BRCA                                              250600
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q       50
      *
     CSR                   ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRORBR   : Validate Originating Branch                       *
      *                                                               *
      *  CALLED BY: SRPROC - Control Process of Validation            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRORBR    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRORBR'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352ORBR
      *
      *  If no entry made to Originating branch then use booking branch
      *  if valid otherwise exit
      *
     C           S1ORBR    IFEQ *BLANKS
     C           I1BRCA    IFEQ *BLANKS
     C                     MOVELS1BRCA    S1ORBR
     C                     ELSE
     C                     GOTO EXORBR
     C                     END
     C                     END
      *
      *  Check Originating Branch valid by calling 'AOBRCHR0'
      *
     C**********           CALL 'AOBRCHR0'             9090                                   CGL029
     C                     CALL 'AOBRCHR1'             9090                                   CGL029
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM S1ORBR    @BRCD
     C********** SDBRCH    PARM *BLANKS   DSFDY                                               CGL029
     C           SDBRCH    PARM *BLANKS   DSSDY                                               CGL029
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AOBRCHR0'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 03 *
     C                     Z-ADD3         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C           @RTCD     IFNE *BLANKS
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1ORBR    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2025' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1ORBR
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXORBR
     C                     END
      *
      *  If originating branch validation required
      *
     C                     CALL 'ZVOBU'                9090
     C                     PARM S1ORBR    ZOBRX   3
     C                     PARM 0         ERR     10
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'ZVOBU   'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 04 *
     C                     Z-ADD4         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      * User is not authorised to any Originating Branches
      *
     C           ERR       IFEQ 1
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2026' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1ORBR
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXORBR
     C                     END
      *
      *  User is not authorised to this Originating Branch
      *
     C           ERR       IFEQ 2
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1ORBR    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2027' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1ORBR
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXORBR
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXORBR    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRPAYC   : Validate Payment Charges                          *
      *                                                               *
      *  CALLED BY:        -                                          *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     C           SRPAYC    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRPAYC'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352PAYC
      *
      *  Determine branch for charge defaults
      *
     C                     SELEC
      *
      *  If single branch mode, use single branch code from SDBANKPD
      *
     C           WUMBIN    WHNE 'Y'
     C                     MOVELV#SBRC    PYBR
      *
      *  Multibranched and originating branches used
      *
     C           V#OBUV    WHEQ 'Y'
     C                     MOVELS1ORBR    PYBR
      *
      *  Multibranched and originating branches not used
      *
     C                     OTHER
     C                     MOVELS1BRCA    PYBR
      *
     C                     ENDSL
      *
     C           PYBR      CHAINPYCHGDB              90    Get defaults.
      *
      *  No record found - DB error only if not multibranched
      *
     C           *IN90     IFEQ '1'
     C           WUMBIN    ANDNE'Y'
     C                     MOVEL'PYCHGDB 'W0FILE           ***************
     C                     MOVELPYBR      W0KEY            * DB ERROR 04 *
     C                     Z-ADD5         W0ERNB           ***************
     C                     MOVEL'MEM5004' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      *  No payment charges information
      *
     C           *IN90     IFEQ '1'
      *
     C           V#OBUV    IFEQ 'Y'
     C                     MOVEL'Y'       I1ORBR
     C                     ELSE
     C                     MOVEL'Y'       I1BRCA
     C                     END
     C                     MOVEL'Y'       E#MSGS
     C                     MOVEL'Y'       E#NOUP
     C                     MOVELPYBR      FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2001' FTMSID
     C                     EXSR FTSNMS
     C                     GOTO EXPAYC
     C                     END
      *
      *  Convert rate % discrepancy to higher & lower rate margins.
      *
     C           1         ADD  V#PCRD    UPMARG  74
     C           1         SUB  V#PCRD    LWMARG  74
      *
      *  Set up default charge and commission
      *
     C                     MOVELDFTRCT    V#TRCT
      *
      *  Unwind subroutine stack name
      *
     C           EXPAYC    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q       50
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRPYST   : Validate Payment Subtype                          *
      *                                                               *
      *  CALLED BY: SRPROC - Control Process of Validation            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRPYST    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRPYST'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352PYST
      *
      *  Payment subtype is a mandatory field
      *
     C           S1PYST    IFEQ *BLANKS
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2028' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1PYST
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXPYST
     C                     END
      *
      * Check sub-type exists
      *
     C                     CALL 'AODLSTR0'             9090
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*VERIFY' @OPTN   7
     C                     PARM S1PYST    @DSTC   2
     C           SDDLST    PARM *BLANKS   DSFDY
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AODLSTR0'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 06 *
     C                     Z-ADD6         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1PYST    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2029' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1PYST
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXPYST
     C                     END
      *                                                                   CFT002
      *** Check the vailidity of the payment type/subtype for incoming    CFT002
      *** payments before STP to ensure correct combination               CFT002
      *                                                                   CFT002
      *** Check in Deal type/Sub type file if combination exists          CFT002
      *** for the entered values of payment type/subtype                  CFT002
      *                                                                   CFT002
     C           @DTSTK    KLIST                                          CFT002
     C                     KFLD           S1PYTP                          CFT002
     C                     KFLD           S1PYST                          CFT002
      *                                                                   CFT002
     C           @DTSTK    CHAINFDDTSTL1             90                   CFT002
     C           *IN90     IFEQ '1'                                       CFT002
     C           RCID11    OREQ '*'                                       CFT002
     C                     MOVEL'Y'       E#MSGS                          CFT002
     C                     MOVELE#CPGM    FTPGMQ                          CFT002
     C                     MOVEL'FTX0009' FTMSID                          CFT002
     C                     EXSR FTSNMS                                    CFT002
     C                     MOVEL'Y'       I1PYST                          CFT002
     C                     MOVEL'Y'       E#NOUP                          CFT002
     C                     GOTO EXPYST                                    CFT002
     C                     ENDIF                                          CFT002
      *
      *  Unwind subroutine stack name
      *
     C           EXPYST    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRSWPC   : Validate Swift Priority Code                      *
      *                                                               *
      *  CALLED BY:        -                                          *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     C           SRSWPC    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRSWPC'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352SWPC
      *
      * Optional default to 'N'
      *
     C           S1SWPC    IFEQ *BLANK
     C                     MOVEL'N'       S1SWPC
     C                     GOTO EXSWPC
     C                     END
      *
      * If entered must be N(ormal) or U(rgent)
      *
     C           S1SWPC    IFNE 'U'
     C           S1SWPC    ANDNE'N'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SWPC    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2030' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SWPC
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXSWPC
     C                     END                             E001
      *
      *  Unwind subroutine stack name
      *
     C           EXSWPC    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q       50
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRSND    : Validate Sender                                   *
      *                                                               *
      *  CALLED BY: SRPROC - Control Process of Validation            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRSND     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRSND '  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352SND
      *
      *  Set Type of Sender to blanks
      *
     C                     MOVEL*BLANK    F1SNTP
      *
      *  ? not allowed
      *
     C           '?'       SCAN S1SND1    Y#             90
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SND1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2031' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXSND
     C                     END
      *                                                                   CRT002
      **  If the message is from the CASHIER mark the beneficiary as      CRT002
      **  an address.                                                     CRT002
      *                                                                   CRT002
     C           V#NWRK    IFEQ 'CASHV8'                                  CRT002
     C                     MOVEL'A'       F1SNTP                          CRT002
     C                     GOTO EXSND                                     CRT002
     C                     END                                            CRT002
      *
      * Sender is a mandatory field
      *
     C           S1SND     IFEQ *BLANKS
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SND1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2032' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXSND
     C                     END
      *
      *  Move Sender to ADDRSS data structure for Analysis
      *
     C                     MOVEL*BLANKS   ADDRSS
     C                     MOVELS1SND     ADDRSS
      *
      *  If first 5 Character are not blank but rest is blank
      *  validate as a full nostro
      *
     C           ADFNOS    IFNE *BLANKS
     C           ADFNEX    ANDEQ*BLANKS
     C           ADCH26    ANDEQ*BLANKS
      *
      * Look up Nostro via Access Object
      *
     C                     CALL 'AONOSTR0'             9090
     C                     PARM *BLANKS   RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM *BLANKS   @CUST
     C                     PARM ADFCCY    @CYCD
     C                     PARM *BLANKS   @ACCD
     C                     PARM *BLANKS   @ACSN
     C                     PARM ADFNNB    @NONB   2
     C                     PARM *BLANKS   @BRCD
     C                     PARM *BLANKS   @CSSN
     C                     PARM *BLANKS   @PNOI
     C           SDNOST    PARM *BLANKS   DSFDY
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AONOSTR0'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 07 *
     C                     Z-ADD7         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      * Customer not found
      *
     C           RTCD      IFNE *BLANKS
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SND1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2033' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXSND
     C                     END
      *
      *  Nostro found mark as full nostro and get GL information
      *
     C           RTCD      IFEQ *BLANKS
     C                     MOVEL'F'       F1SNTP
      *
      * Store in occurrance data structure 2 any information
      *
     C           2         OCUR OACCNT
      *
      *  Set up key for chain
      *
     C                     MOVELA7CUST    CNUM
     C                     MOVELADFCCY    CCY
     C                     MOVELA7ACCD    ACOD
     C                     MOVELA7ACSN    ACSQ
     C                     MOVELA7BRCD    BRCA
      *
     C           AKEY      CHAINACCNTABF             90
      *
      *  Database error account does not exist
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL*BLANKS   ADCHK1
     C                     MOVELA7CUST    ADGCNM
     C                     MOVELADFCCY    ADGCCY
     C                     MOVELA7ACCD    ADGACD
     C                     MOVELA7ACSN    ADGASQ
     C                     MOVELA7BRCD    ADGBRC
     C                     MOVEL'ACCNT   'W0FILE           ***************
     C                     MOVELADCHK1    W0KEY            * DB ERROR 08 *
     C                     Z-ADD8         W0ERNB           ***************
     C                     MOVEL'MEM5004' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C                     END
      *
     C                     END
      *
      *  If first 6 Characters are numeric but rest is blank
      *  validate as a customer reference
      *
      *  Only validate if F1SNTP is blank
      *
     C           ADCNUM    IFNE *BLANKS
     C           ADEXCN    ANDEQ*BLANKS
     C           ADCH26    ANDEQ*BLANKS
     C           F1SNTP    ANDEQ*BLANKS
      *
      *  Check first 6 characters are numeric and rest is blank
      *
     C                     MOVEL*BLANKS   I1FLD
     C                     MOVELS1SND1    I1FLD  35
     C           DIGITS    CHECKI1FLD     Y#      30     90
      *
      *  6 digits allowed
      *
     C           *IN90     IFEQ '1'
     C           Y#        ANDEQ7
      *
     C                     CALL 'AOCUSTR0'             9090
     C                     PARM *BLANKS   RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM ADCNUM    @KEY1
     C                     PARM *BLANKS   @KYST
     C           SDCUST    PARM *BLANKS   DSSDY
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AOCUSTR0'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 09 *
     C                     Z-ADD9         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      * Customer not found
      *
     C           RTCD      IFNE *BLANKS
     C           @KYST     ORNE '*CNUM  '
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SND1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2034' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXSND
     C                     END
      *
      *  Customer found mark as Customer
      *
     C                     MOVEL'C'       F1SNTP
      *                                                                   CFT014
      *  Memorize SWIFT id for S/R SRIDFT                                 CFT014
      *                                                                   CFT014
     C                     MOVE BBCSID    WSCSID                          CFT014
     C                     MOVE BBCOLC    WSCOLC                          CFT014
      *
     C                     END
      *
     C                     END
      *
      *  If first 10 Characters are numeric but rest is blank
      *  validate as a retail account
      *
      *  Only validate if F1SNTP is blank
      *
     C           ADACNO    IFNE *BLANKS
     C           ADEXAC    ANDEQ*BLANKS
     C           ADCH26    ANDEQ*BLANKS
     C           F1SNTP    ANDEQ*BLANKS
      *
      *  Check first 10 characters are numeric and rest is blank
      *
     C                     MOVEL*BLANKS   I1FLD
     C                     MOVELS1SND1    I1FLD  35
     C           DIGITS    CHECKI1FLD     Y#      30     90
      *
      *  10 digits allowed
      *
     C           *IN90     IFEQ '1'
     C           Y#        ANDEQ11
      *
      *  Check that retail account numbers are allowed
      *
     C           V#RANR    IFEQ 'N'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SND1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2035' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXSND
     C                     END
      *
      * Store in occurrance data structure 3 any information
      *
     C           2         OCUR OACCNT
      *
      *  Check account is on ACCNTAB
      *
     C                     MOVELADACNO    ACNO
     C           ACNO      CHAINACNUMAF              90
      *
     C           *IN90     IFEQ '1'
     C           RECI      ORNE 'D'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SND1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2036' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXSND
     C                     END
      *
      *  Retail Account found mark as Retail
      *
     C                     MOVEL'R'       F1SNTP
      *
     C                     END
      *
     C                     END
      *                                                                   068230
      *  If 1st 11 Chars of 1st line then SWIFT address assumed           068230
      *  Or first is '/' and second is SWIFT Address                      068230
      *                                                                   068230
      *                                                                   068230
      *  If field does not begin with '/' and is not longer than 11       068230
      *  characters, validate as a S.W.I.F.T. Address                     068230
      *                                                                   068230
     C           ADCH26    IFEQ *BLANKS                                   068230
     C           ADEXSW    ANDEQ*BLANKS                                   068230
     C           ADSLSH    ANDNE'/'                                       068230
     C           F1SNTP    ANDEQ*BLANKS                                   068230
      *                                                                   068230
      *  First 11 characters should be key for CLSWT.                     068230
      *                                                                   068230
     C                     MOVELADCHK1    CLSWTK                          068230
      *                                                                   068230
     C                     EXSR SWIFT                                     068230
      *                                                                   068230
     C           ##CSID    IFEQ 'Y'                                       068230
     C           *IN32     IFEQ '0'                                       068230
     C           *IN31     ANDEQ'0'                                       068230
      *                                                                   068230
     C                     MOVEL'S'       F1SNTP                          068230
      *                                                                   CFT014
      *  Memorize SWIFT id for S/R SRIDFT                                 CFT014
      *                                                                   CFT014
     C                     MOVELCLSWTK    WSCSID                          CFT014
      *                                                                   068230
      *  No SWIFT address record.                                         068230
      *                                                                   068230
     C                     ELSE                                           068230
     C                     MOVEL'Y'       I1SND1                          068230
     C                     MOVEL'Y'       E#NOUP                          068230
     C                     MOVEL'Y'       E#MSGS                          068230
     C                     MOVELCLSWTK    FTMSDT                          068230
     C                     MOVELE#CPGM    FTPGMQ                          068230
     C                     MOVEL'FTM2357' FTMSID                          068230
     C                     EXSR FTSNMS                                    068230
     C                     GOTO EXSND                                     068230
      *                                                                   068230
     C                     END                                            068230
     C                     END                                            068230
      *                                                                   068230
     C                     END                                            068230
      *
      *  If first 10 Characters are non blank but rest is blank
      *  validate as a customer shortname
      *
      *  Only validate if F1SNTP is blank
      *
     C           ADCUST    IFNE *BLANKS
     C           ADEXCS    ANDEQ*BLANKS
     C           ADCH26    ANDEQ*BLANKS
     C           F1SNTP    ANDEQ*BLANKS
      *
      * Check if name exists
      *
     C                     CALL 'AOCUSTR0'             9090
     C                     PARM *BLANKS   RTCD
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM ADCUST    @KEY1  10
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM *BLANKS   DSSDY
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AOCUSTR0'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 10 *
     C                     Z-ADD10        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C           RTCD      IFNE *BLANKS
     C           @KYST     ORNE '*CSHT  '
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SND1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2037' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXSND
     C                     END                             X001
      *
      *  Customer found mark as Customer
      *
     C                     MOVEL'C'       F1SNTP
      *                                                                   CFT014
      *  Memorize SWIFT id for S/R SRIDFT                                 CFT014
      *                                                                   CFT014
     C                     MOVE BBCSID    WSCSID                          CFT014
     C                     MOVE BBCOLC    WSCOLC                          CFT014
     C                     MOVEL*BLANK    S1SND1
     C                     MOVELBBCUST    S1SND1
      *
     C                     END
      *
      *  If 18 chars have been entered and multi branched , or
      *  15 long and single branch , is a full account.
      *
      *  Only validate if F1SNTP is blank
      *
     C           ADCHK1    IFNE *BLANKS
     C           ADEXGL    ANDEQ*BLANKS
     C           ADCH26    ANDEQ*BLANKS
     C           F1SNTP    ANDEQ*BLANKS
     C           WUMBIN    ANDEQ'Y'
     C           ADCHK1    ORNE *BLANKS
     C           ADEXG1    ANDEQ*BLANKS
     C           ADCH26    ANDEQ*BLANKS
     C           F1SNTP    ANDEQ*BLANKS
     C           WUMBIN    ANDEQ'N'
      *
      *  Customer number, account code and account sequence must be
      *  numeric
      *
     C                     MOVELADGCNM    PTCNUM
     C                     MOVELADGACD    PTACOD
     C                     MOVELADGASQ    PTACSQ
      *
      ***Check*first*12*characters*are*numeric*************************                       CGL029
      ** Check first 18 characters are numeric                                                CGL029
      *
     C                     MOVEL*BLANKS   I1FLD
     C                     MOVELPTACCT    I1FLD  35
     C           DIGITS    CHECKI1FLD     Y#      30     90
      *
      ***12*digits*allowed*********************************************                       CGL029
      ** 18 digits allowed                                                                    CGL029
      *
     C           *IN90     IFEQ '1'
     C********   Y#        ANDNE13                                                            CGL029
     C           Y#        ANDNE19                                                            CGL029
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SND1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2038' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXSND
     C                     END
      *
      * Store in occurrance data structure 2 any information
      *
     C           2         OCUR OACCNT
      *
      *  Set up key for chain
      *
     C                     MOVELADGCNM    CNUM
     C                     MOVELADGCCY    CCY
     C                     MOVELADGACD    ACOD
     C                     MOVELADGASQ    ACSQ
      *
      *  Set up branch depending on multibranching
      *
     C                     SELEC
     C           WUMBIN    WHNE 'Y'
     C                     MOVE V#SBRC    BRCA
     C           ADGBRC    WHEQ *BLANKS
     C                     MOVELDBRN      BRCA
     C                     OTHER
     C                     MOVELADGBRC    BRCA
     C                     ENDSL
      *
     C           AKEY      CHAINACCNTABF             90
      *
     C           *IN90     IFEQ '1'
     C           RECI      ORNE 'D'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SND1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2039' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXSND
     C                     END
      *
      *  GL Account found mark as Ledger
      *
     C                     MOVEL'G'       F1SNTP
     C           WUMBIN    IFEQ 'Y'
     C           ADGBRC    ANDEQ*BLANKS
     C                     MOVELBRCA      ADGBRC
     C                     END
     C                     MOVELADCHK1    S1SND1
      *
     C                     END
      *
      * If F1SNTP is not blank then get other information
      *
      *  If sender is a NOSTRO set pay location to nostro location
      *  Else use system location
      *
     C                     SELEC
     C           F1SNTP    WHEQ 'G'
     C           F1SNTP    OREQ 'R'
      *
     C                     MOVELCNUM      @CUST
      *
     C                     CALL 'AONOSTR0'             9090
     C                     PARM *BLANKS   RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM           @CUST   6
     C                     PARM CCY       @CYCD   3
     C**********           PARM ACODA     @ACCD   4                                           CGL029
     C                     PARM ACODA     @ACCD  10                                           CGL029
     C                     PARM ACSQA     @ACSN   2
     C                     PARM *BLANKS   @NONB
     C                     PARM BRCA      @BRCD
     C                     PARM *BLANKS   @CSSN  10
     C                     PARM *BLANKS   @PNOI   1
     C           SDNOST    PARM *BLANKS   DSFDY
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AONOSTR0'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 11 *
     C                     Z-ADD11        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C           RTCD      IFEQ *BLANKS
     C                     MOVELNLOC      PLOC
     C                     MOVEL'N'       NOWRK
     C                     ELSE
     C                     MOVELV#SLCD    PLOC
     C                     MOVEL'S'       NOWRK
     C                     END
      *
     C           F1SNTP    WHEQ 'F'
     C                     MOVELNLOC      PLOC
     C                     MOVEL'N'       NOWRK
      *
     C                     OTHER
     C                     MOVELV#SLCD    PLOC
     C                     MOVEL'S'       NOWRK
     C                     ENDSL
      *
      * If a valid account has been entered,
      * store full account details for FT Transaction Status File
      * record update.
      *
     C           F1SNTP    IFEQ 'F'
     C           F1SNTP    OREQ 'R'
     C           F1SNTP    OREQ 'G'
     C**********           Z-ADDCNUM      IMCNUM                                              CSD027
     C                     MOVE CNUM      IMCNUM                                              CSD027
     C                     MOVE CCY       IMCCY
     C                     Z-ADDACOD      IMACOD
     C                     Z-ADDACSQ      IMACSQ
     C                     MOVE BRCA      IMBRCA
     C                     MOVELIMACNT    SNDDS
     C                     MOVELA7NONB    V#DBNS                                              250600
     C                     END
      *
      *  If no sender defined validate as an address
      *
     C           F1SNTP    IFEQ *BLANKS
      *
      * Validate as an address
      *
     C                     MOVEL'FTM2342' ADFLDM
     C                     MOVEL'FTUSRMSG'ADFLDF
     C                     EXSR ADCHK
      *
      *  Depending on the indicators, highlight the field in error
      *
     C           *IN91     IFEQ '1'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SND1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2040' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     END
      *
     C           *IN92     IFEQ '1'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SND1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2040' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND
     C                     MOVEL'Y'       E#NOUP
     C                     END
      *
     C           *IN93     IFEQ '1'
     C                     MOVEL'Y'       E#NOUP
     C                     END
     C*
     C           AD#1      IFEQ 'Y'
     C                     MOVEL'Y'       I1SND1
     C                     END
     C           AD#2      IFEQ 'Y'
     C                     MOVEL'Y'       I1SND2
     C                     END
     C           AD#3      IFEQ 'Y'
     C                     MOVEL'Y'       I1SND3
     C                     END
     C           AD#4      IFEQ 'Y'
     C                     MOVEL'Y'       I1SND4
     C                     END
      *
      *  If no errors then customer is an address
      *
     C           I1SND     IFEQ *BLANKS
     C                     MOVEL'A'       F1SNTP
     C                     END
      *
     C                     END
     C/COPY WNCPYSRC,FT0352C008
      *
      *  Unwind subroutine stack name
      *
     C           EXSND     TAG
      *                                                                                       250600
     C           BBCOLC    IFNE *BLANKS                                                       250600
     C                     MOVELBBCOLC    V#SNLC                                              250600
     C                     ELSE                                                               250600
     C           F1SNTP    IFEQ 'S'                                                           250600
     C           2         SUBSTS1SND:5   V#SNLC                                              250600
     C                     ENDIF                                                              250600
     C                     ENDIF                                                              250600
      *                                                                                       250600
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRSMCY   : Validate Settlement Currency                      *
      *                                                               *
      *  CALLED BY: SRPROC - Control Process of Validation            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRSMCY    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRSMCY'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352SMCY
      *
      *  Field is mandatory
      *
     C           S1SMCY    IFEQ *BLANKS
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SMCY    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2041' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SMCY
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXSMCY
     C                     END
      *
      *  Look field up on database
      *
     C                     CALL 'AOCURRR0'             9090
     C                     PARM *BLANKS   RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM S1SMCY    @K101   3
     C           SDCURR    PARM *BLANKS   DSSDY
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AOCURRR0'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 13 *
     C                     Z-ADD13        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C           RTCD      IFNE *BLANKS                    B001
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SMCY    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2042' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SMCY
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXSMCY
     C                     END
      *
      *  Move new standing data field into field used in program
      *
     C           *LIKE     DEFN A6NBDP    SCNBDP
     C           *LIKE     DEFN A6SPRT    SCSPOT
     C           *LIKE     DEFN A6MDIN    SCMDIN
     C           *LIKE     DEFN A6SPRT    SCBUY
     C           *LIKE     DEFN A6SPRT    SCSELL
     C           *LIKE     DEFN A6INCY    SCINCY                          CFT014
     C           *LIKE     DEFN A6TPSD    SCTPSD                          CFT014
      *
      * Store Decimal Places Spot Rate Multiply/Divide Ind.
      *
     C                     Z-ADDA6NBDP    SCNBDP
     C                     Z-ADDA6SPRT    SCSPOT
     C                     MOVELA6MDIN    SCMDIN
     C                     MOVELA6INCY    SCINCY                          CFT014
     C                     Z-ADDA6TPSD    SCTPSD                          CFT014
     C/COPY WNCPYSRC,FT0352C009
      *
     C           A6BYSS    IFEQ '+'
     C           A6SPRT    ADD  A6BYSR    SCBUY
     C                     ELSE
     C           A6SPRT    SUB  A6BYSR    SCBUY
     C                     END
      *
     C           A6SLSS    IFEQ '+'
     C           A6SPRT    ADD  A6SLSR    SCSELL
     C                     ELSE
     C           A6SPRT    SUB  A6SLSR    SCSELL
     C                     END
     C/COPY WNCPYSRC,FT0352C010
      *
      *  Unwind subroutine stack name
      *
     C           EXSMCY    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRSMAM   : Validate Settle Amount                            *
      *                                                               *
      *  CALLED BY: SRPROC - Control Process of Validation            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRSMAM    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRSMAM'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352SMAM
      *
     C                     Z-ADD0         F1SMAM
      *
      * Mandatory if pay amount not entered
      *
     C           S1SMAM    IFEQ *BLANK
     C           S1PYAM    ANDEQ*BLANK
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SMAM    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2043' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SMAM
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXSMAM
     C                     END
      *
      * If entered Validate
      *
     C                     MOVEL*BLANK    ZFIELD
     C                     MOVE S1SMAM    ZFIELD
     C           13        SUB  SCNBDP    ZADIG
     C                     Z-ADDSCNBDP    ZADEC
      *
     C                     EXSR SRALGN
      *
     C           ZRTN      IFNE *BLANKS
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SMAM    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2044' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SMAM
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXSMAM
     C                     END
      *
     C                     MOVE ZFIELD    F1SMAM
      *
      * Set amount entered indicator if pay amount
      * or it is blanks
      *
     C           S1PYAM    IFEQ *BLANK
     C           F1FAIN    OREQ *BLANK
     C                     MOVEL'P'       F1FAIN
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXSMAM    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRBNC    : Validate Beneficiary                              *
      *                                                               *
      *  CALLED BY: SRPROC - Control Process of Validation            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRBNC     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRBNC '  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352BNC
      *
     C                     MOVEL*BLANK    F1BNCT
      *
      *  ? not allowed
      *
     C           '?'       SCAN S1BNC1    Y#             90
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1BNC1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2045' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BNC1
     C                     MOVEL'Y'       E#NOUP
     C                     MOVEA' '       WRN@,3
     C                     GOTO EXBNC
     C                     END
      *                                                                   CRT002
      **  If the message is from the CASHIER mark the beneficiary as      CRT002
      **  an address.                                                     CRT002
      *                                                                   CRT002
     C           V#NWRK    IFEQ 'CASHV8'                                  CRT002
     C                     MOVEL'A'       F1BNCT                          CRT002
     C                     GOTO EXBNC                                     CRT002
     C                     END                                            CRT002
      *
      *  Beneficiary is mandatory
      *
     C           S1BNC     IFEQ *BLANK
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1BNC1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2046' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BNC
     C                     MOVEL'Y'       E#NOUP
     C                     MOVEA' '       WRN@,3
     C                     GOTO EXBNC
     C                     END
      *                                                                   CGL009
      **  If the message is from the FONTIS and the first line is         CGL009
      **  a partial account, we have appended the currency at the end.    CGL009
      **  If this currency is the same as the one of the payment we       CGL009
      **  remove it and let the rest of validation operate it             CGL009
      **  as a partial account                                            CGL009
      **                                                                  CGL009
      *                                                                   CGL009
     C           V#NWRK    IFEQ 'FONTIS'                                  CGL009
     C           ' '       CHEKRS1BNC1    WACLN   20                      CGL009
      ** It is not a retail account                                       CGL009
      ** (Currency has been added at the end of the partial account)      CGL009
     C********** WACLN     IFEQ 18                                                     CGL009 CGL029
     C           WACLN     IFEQ 24                                                            CGL029
     C                     MOVELS1BNC1    DSBNC1                          CGL009
     C           WBNCY     IFEQ S1PCCY                                    CGL009
     C                     MOVEL*BLANK    WBNCY                           CGL009
     C                     MOVELDSBNC1    S1BNC1                          CGL009
     C                     ELSE                                           CGL009
     C                     MOVEL'Y'       E#MSGS                          CGL009
     C                     MOVELWBNBR     WGLBR                           CGL009
     C                     MOVELWBNCN     WGLCN                           CGL009
     C                     MOVELWBNCY     WGLCY                           CGL009
     C                     MOVELWBNAC     WGLAC                           CGL009
     C                     MOVELWBNSQ     WGLSQ                           CGL009
     C                     MOVELDSGLAC    FTMSDT                          CGL009
     C                     MOVELE#CPGM    FTPGMQ                          CGL009
     C                     MOVEL'FTM2609' FTMSID                          CGL009
     C                     EXSR FTSNMS                                    CGL009
     C                     MOVEL'Y'       I1BNC                           CGL009
     C                     MOVEL'Y'       E#NOUP                          CGL009
     C                     MOVEA' '       WRN@,3                          CGL009
     C                     GOTO EXBNC                                     CGL009
     C                     ENDIF                                          CGL009
     C                     ENDIF                                          CGL009
     C                     ENDIF                                          CGL009
      *
      *  Move Beneficiary to ADDRSS data structure for Analysis
      *
     C                     MOVEL*BLANKS   ADDRSS
     C                     MOVELS1BNC     ADDRSS
      *
      *  Check if this is an IBAN account Number                          CFT004
     C           CFT004    IFEQ 'Y'                                       CFT004
     C           ADSLSH    ANDEQ'/'                                       CFT004
     C* If beneficiary start by '/aann' validate as an IBAN               CFT004
     C           4         SUBSTS1BNC1:2  IBCHK                           CFT004
     C           ICTY1     LOKUPALPH                     94               CFT004
     C   94      ICTY2     LOKUPALPH                     94               CFT004
     C   94      IDGT1     LOKUPNUMC                     94               CFT004
     C   94      IDGT2     LOKUPNUMC                     94               CFT004
     C           *IN94     IFEQ *ON                                       CFT004
      *
     C           34        SUBSTS1BNC1:2  P@IBAN                          CFT004
     C                     MOVE *BLANKS   DSSDY                           CFT004
     C                     CALL 'AOIBANR2'                                CFT004
     C                     PARM           @RTCD                           CFT004
     C                     PARM           P@IBAN 34                       CFT004
     C                     PARM           DSSDY                           CFT004
      *                                                                   CFT004
     C           @RTCD     IFEQ *BLANKS                                   CFT004
     C           3         OCUR OACCNT                                    CFT004
     C                     MOVE *BLANKS   DSSDY                           CFT004
      *                                                                   CFT004
      ** If no errors, then retrieve the account.                         CFT004
      *                                                                   CFT004
     C                     CALL 'AOIBANR4'                                CFT004
     C                     PARM           @RTCD                           CFT004
     C                     PARM '*KEY   ' @OPTN                           CFT004
     C                     PARM           P@IBAN 34                       CFT004
     C           OACCNT    PARM           DSSDY                           CFT004
     C                     MOVE 'I'       F1BNCT                          CFT004
     C*                                                                   CFT004
     C                     ELSE                                           CFT004
     C*                                                                   CFT004
     C** Return code 1 is not an error in this case as can only validate  CFT004
     C** the beneficiary as an IBAN if the ISO country code is correct.   CFT004
     C** Return code 1 is for invalid ISO country code.                   CFT004
     C*                                                                   CFT004
     C           @RTCD     IFNE '      1'                                 CFT004
     C                     MOVEL'Y'       E#MSGS                          CFT004
     C                     MOVEL*BLANKS   FTMSDT                          CFT004
     C                     MOVELE#CPGM    FTPGMQ                          CFT004
     C           @RTCD     IFEQ '      2'                                 CFT004
     C                     MOVEL'FTM2407' FTMSID                          CFT004
     C                     END                                            CFT004
     C           @RTCD     IFEQ '      3'                                 CFT004
     C                     MOVEL'FTM2408' FTMSID                          CFT004
     C                     END                                            CFT004
     C           @RTCD     IFEQ '      4'                                 CFT004
     C                     MOVEL'FTM2409' FTMSID                          CFT004
     C                     END                                            CFT004
     C           @RTCD     IFEQ '      5'                                 CFT004
     C                     MOVEL'FTM2410' FTMSID                          CFT004
     C                     END                                            CFT004
     C           @RTCD     IFEQ '      6'                                 CFT004
     C                     MOVEL'FTM2411' FTMSID                          CFT004
     C                     END                                            CFT004
     C                     EXSR FTSNMS                                    CFT004
     C                     MOVEL'Y'       I1BNC1                          CFT004
     C                     MOVEL'Y'       E#NOUP                          CFT004
     C                     ENDIF                                          CFT004
     C                     ENDIF                                          CFT004
     C                     GOTO EXBNC1                                    CFT004
     C                     ENDIF                                          CFT004
     C                     ENDIF                                          CFT004
      *                                                                   CFT004
      *  If first ten characters used then try to find via SDCUSTPD
      *
     C           ADCH26    IFEQ *BLANKS
     C           ADEXCS    ANDEQ*BLANKS
     C           ADSLSH    ANDNE'/'                                       068230
     C           ADCH36    OREQ *BLANKS                                   068230
     C           ADXCS2    ANDEQ*BLANKS                                   068230
     C           ADCHK2    ANDNE*BLANKS                                   068230
     C           ADSLSH    ANDEQ'/'                                       068230
     C           ADSLS2    ANDNE'/'                                       068230
      *                                                                   068230
      *  First 10 characters should be key for SDCUST                     068230
      *                                                                   068230
     C           ADSLSH    IFNE '/'                                       068230
     C                     MOVELADCHK1    @CNUM                           068230
     C                     ELSE                                           068230
     C                     MOVELADCHK2    @CNUM                           068230
     C                     END                                            068230
      *
      *  Call to access pgm for Customer details
      *
     C                     CALL 'AOCUSTR0'             9090
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C***********          PARM ADCUST    @CNUM  10                       068230
     C                     PARM           @CNUM  10                       068230
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM *BLANKS   DSSDY
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AOCUSTR0'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 03 *
     C                     Z-ADD3         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      *  If six numeric customer number
      *
     C           ADSLSH    IFNE '/'                                       068230
     C           DIGITS    CHECKS1BNC1    Y#             90
     C                     ELSE                                           068230
     C           DIGITS    CHECKS1BNC2    Y#             90               068230
     C                     END                                            068230
     C           *IN90     IFEQ '1'
     C           Y#        ANDEQ7
     C           ADSLSH    IFNE '/'                                       068230
     C           ' '       CHECKS1BNC1:7  Y#             90
     C                     ELSE                                           068230
     C           ' '       CHECKS1BNC2:7  Y#             90               068230
     C                     END                                            068230
     C           *IN90     IFEQ '0'
     C           @RTCD     ANDNE*BLANKS
     C                     MOVEL'Y'       E#MSGS
     C***********          MOVELS1BNC1    FTMSDT                          068230
     C                     MOVEL@CNUM     FTMSDT                          068230
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2080' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BNC1
     C                     MOVEL'Y'       E#NOUP
     C                     MOVEA' '       WRN@,3
     C                     GOTO EXBNC
     C                     END
     C                     END
      *
     C           @RTCD     IFEQ *BLANKS
      *
      * Translate to customer number
      *
     C**********           MOVEL*BLANKS   S1BNC                           068230
     C**********           MOVELBBCUST    S1BNC1                          068230
     C                     MOVEL'C'       F1BNCT
     C           ADSLSH    IFNE '/'                                       068230
     C                     MOVEL*BLANKS   S1BNC                           068230
     C                     MOVELBBCUST    S1BNC1                          068230
     C                     ELSE                                           068230
     C                     MOVEL*BLANKS   S1BNC2                          068230
     C                     MOVELBBCUST    S1BNC2                          068230
     C                     END                                            068230
      *
      * Valid information exit
      *
     C                     GOTO EXBNC
      *
     C                     END
      *
     C                     END
      *
      *  If 1st 11 Chars of 1st line then SWIFT address assumed
      *  Or first is '/' and second is SWIFT Address
      *
      *
      *  If field does not begin with '/' and is not longer than 11
      *  characters, validate as a S.W.I.F.T. Address
      *
     C           ADCH26    IFEQ *BLANKS
     C           ADEXSW    ANDEQ*BLANKS
     C           ADSLSH    ANDNE'/'
     C           ADCH36    OREQ *BLANKS
     C           ADEXS2    ANDEQ*BLANKS
     C           ADCHK2    ANDNE*BLANKS
     C           ADSLSH    ANDEQ'/'
     C           ADSLS2    ANDNE'/'
      *
      *  First 11 characters should be key for CLSWT.
      *
     C           ADSLSH    IFNE '/'
     C                     MOVELADCHK1    CLSWTK
     C                     ELSE
     C                     MOVELADCHK2    CLSWTK
     C                     END
      *
     C                     EXSR SWIFT
      *
     C           ##CSID    IFEQ 'Y'
     C           *IN31     IFEQ '1'
     C/COPY WNCPYSRC,FT0352C002
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1BNC1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2081' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BNC1
     C                     MOVEL'Y'       E#NOUP
     C                     MOVEA' '       WRN@,3
     C                     GOTO EXBNC
     C                     END
      *
     C                     MOVEL'S'       F1BNCT
      *
      *  No SWIFT address record.
      *
     C           *IN32     IFEQ '1'
     C           *IN31     ANDEQ'0'
     C/COPY WNCPYSRC,FT0352C003
      *
      *  If text changed then warning needs to be changed
      *
     C                     MOVEL'Y'       I1BNC1
     C                     MOVEL'Y'       E#WARN
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1BNC1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2082' FTMSID
     C                     EXSR FTSNMS
      *
     C           WRN@,3    IFEQ ' '
     C                     MOVEL*BLANKS   E#RSND
     C                     MOVEA'Y'       WRN@,3
     C                     END
      *
     C                     ELSE
     C                     MOVEA' '       WRN@,3
     C                     END
      *
      * Exit as validation complete for S.W.I.F.T.
      *
     C                     GOTO EXBNC
      *
     C                     END
      *
     C                     MOVEA' '       WRN@,3
      *
     C                     END
     C/COPY WNCPYSRC,FT0352C004
      *
      *  If first 10 Characters are numeric but rest is blank
      *  validate as a retail account
      *  OR, if the first character is '/' and the next 10 characters                       BUG15156
      *  are numeric and the rest of line 1 is blanks validate also                         BUG15156
      *  as a retail account.                                                               BUG15156
      *  Lines 2 to 5 should be blanks for Retail account                                   BUG24738
      *
     C           ADACNO    IFNE *BLANKS
     C           ADEXAC    ANDEQ*BLANKS
     C           ADCH26    ANDEQ*BLANKS
     C********** ADSLSH    OREQ '/'                                                BUG15156 BUG24738
     C********** ADACN2    ANDNE*BLANKS                                            BUG15156 BUG24738
     C********** ADCHA2    ANDEQ*BLANKS                                            BUG15156 BUG24738
      *
      *  Check first 10 characters are numeric and rest is blank
      *  If the line starts with '/', get the 10 characters after the '/'                   BUG15156
      *
     C                     MOVEL*BLANKS   I1FLD
     C********** ADSLSH    IFEQ '/'                                                BUG15156 BUG24738
     C********** 34        SUBSTS1BNC1:2  I1FLD                                    BUG15156 BUG24738
     C**********           ELSE                                                    BUG15156 BUG24738
     C                     MOVELS1BNC1    I1FLD  35
     C**********           ENDIF                                                   BUG15156 BUG24738
     C           DIGITS    CHECKI1FLD     Y#      30     90
      *
      *  10 digits allowed
      *
     C           *IN90     IFEQ '1'
     C           Y#        ANDEQ11
      *
      *  Check that retail account numbers are allowed
      *
     C           V#RANR    IFEQ 'N'
     C                     MOVEL'Y'       E#MSGS
     C**********           MOVELS1BNC1    FTMSDT                                            BUG15156
     C**********           MOVELI1FLD     FTMSDT                                   BUG15156 BUG24738
     C                     MOVELS1BNC1    FTMSDT                                            BUG24738
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2083' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BNC1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXBNC
     C                     END
      *
      * Store in occurrance data structure 3 any information
      *
     C           3         OCUR OACCNT
      *
      *  Check account is on ACCNTAB
      *
     C**********           MOVELADACNO    ACNO                                              BUG15156
     C**********           MOVELI1FLD     ACNO                                     BUG15156 BUG24738
     C                     MOVELADACNO    ACNO                                              BUG24738
     C           ACNO      CHAINACNUMAF              90
      *
     C           *IN90     IFEQ '1'
     C           RECI      ORNE 'D'
     C                     MOVEL'Y'       E#MSGS
     C**********           MOVELS1BNC1    FTMSDT                                            BUG15156
     C                     MOVELI1FLD     FTMSDT                                            BUG15156
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2084' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BNC1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXBNC
     C                     END
      *
      * If A/c with Bank is blank currency must equal settle currency
      *
      ** If A/c with Bank is blank currency must equal payment currency   CGL009
      ** Multi currency payments are allowed in FONTIS  :                 CGL009
      ** PCCY : <- tag 32                                                 CGL009
      ** SCCY : <- tag 53 (always an account)                             CGL009
      ** ACBK : will never be mapped into incoming payment                CGL009
     C           S1ACBK    IFEQ *BLANK
     C           CCY       ANDNES1PCCY
     C                     MOVEL'Y'       E#MSGS
     C**********           MOVELS1BNC1    FTMSDT                                            BUG15156
     C                     MOVELI1FLD     FTMSDT                                            BUG15156
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2085' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BNC1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXBNC
     C                     END
      *
      *  Retail Account found mark as Retail
      *
     C                     MOVEL'R'       F1BNCT
      *
     C                     END
      *
     C                     END
      *
      *  If 15 char have been entered and multi branched , or
      *  12 characters and single branch , must be a partial account.
      *
      *  Only validate if F1BNCT is blank
      *
     C           ADCHK1    IFNE *BLANKS
     C           ADEXPA    ANDEQ*BLANKS
     C           ADCH26    ANDEQ*BLANKS
     C           F1BNCT    ANDEQ*BLANKS
     C           WUMBIN    ANDEQ'Y'
     C           ADCHK1    ORNE *BLANKS
     C           ADEXP1    ANDEQ*BLANKS
     C           ADCH26    ANDEQ*BLANKS
     C           F1BNCT    ANDEQ*BLANKS
     C           WUMBIN    ANDEQ'N'
      *
      *  Customer number, account code and account sequence must be
      *  numeric
      *
     C                     MOVELADPCNM    PTCNUM
     C                     MOVELADPACD    PTACOD
     C                     MOVELADPASQ    PTACSQ
      *
      ***Check*first*12*characters*are*numeric*************************                       CGL029
      ** Check first 18 characters are numeric                                                CGL029
      *
     C                     MOVEL*BLANKS   I1FLD
     C                     MOVELPTACCT    I1FLD  35
     C           DIGITS    CHECKI1FLD     Y#      30     90
      *
      ***12*digits*allowed*********************************************                       CGL029
      ** 18 digits allowed                                                                    CGL029
      *
     C           *IN90     IFEQ '1'
     C********** Y#        ANDNE13                                                            CGL029
     C           Y#        ANDNE19                                                            CGL029
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1BNC1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2086' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BNC1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXBNC
     C                     END
      *
      * Store in occurrance data structure 3 any information
      *
     C           3         OCUR OACCNT
      *
      *  Set up key for chain
      *
     C                     MOVELADPCNM    CNUM
     C                     MOVELS1PCCY    CCY
     C                     MOVELADPACD    ACOD
     C                     MOVELADPASQ    ACSQ
      *
      *  Set up branch depending on multibranching
      *
     C                     SELEC
     C           WUMBIN    WHNE 'Y'
     C                     MOVE V#SBRC    BRCA
     C           ADPBRC    WHEQ *BLANKS
     C                     MOVELDBRN      BRCA
     C                     OTHER
     C                     MOVELADPBRC    BRCA
     C                     ENDSL
      *
     C           AKEY      CHAINACCNTABF             90
      *
     C           *IN90     IFEQ '1'
     C           RECI      ORNE 'D'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1BNC1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2087' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BNC1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXBNC
     C                     END
      *
      *  GL Account found mark as Partial Account
      *
     C                     MOVEL'P'       F1BNCT
     C           WUMBIN    IFEQ 'Y'
     C           ADPBRC    ANDEQ*BLANKS
     C                     MOVELBRCA      ADPBRC
     C                     END
     C                     MOVELADCHK1    S1BNC1
      *
     C                     END
      *
      *  If no sender defined validate as an address
      *
     C           F1BNCT    IFEQ *BLANKS
      *
      * Validate as an address
      *
     C                     MOVEL'FTM2343' ADFLDM
     C                     MOVEL'FTUSRMSG'ADFLDF
     C                     EXSR ADCHK
      *
      *  Depending on the indicators, highlight the field in error
      *
      *
     C           *IN92     IFEQ '1'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1BNC1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2088' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BNC1
     C                     MOVEL'Y'       E#NOUP
     C                     END
      *
     C           *IN93     IFEQ '1'
     C                     MOVEL'Y'       E#NOUP
     C                     END
     C*
     C           AD#1      IFEQ 'Y'
     C                     MOVEL'Y'       I1BNC1
     C                     END
     C           AD#2      IFEQ 'Y'
     C                     MOVEL'Y'       I1BNC2
     C                     END
     C           AD#3      IFEQ 'Y'
     C                     MOVEL'Y'       I1BNC3
     C                     END
     C           AD#4      IFEQ 'Y'
     C                     MOVEL'Y'       I1BNC4
     C                     END
      *
      *  If no errors then customer is an address
      *
     C           I1BNC     IFEQ *BLANKS
     C                     MOVEL'A'       F1BNCT
     C                     END
      *
     C                     END
      *
      * If a valid account has been entered,
      * store full account details for FT Transaction Status File
      * record update.
      *
     C           EXBNC1    TAG                                            CFT004
     C*                                                                   CFT004
     C           F1BNCT    IFEQ 'R'
     C           F1BNCT    OREQ 'P'
     C           F1BNCT    OREQ 'I'                                       CFT004
     C           CFT004    ANDEQ'Y'                                       CFT004
     C**********           Z-ADDCNUM      IMCNUM                                              CSD027
     C                     MOVE CNUM      IMCNUM                                              CSD027
     C                     MOVE CCY       IMCCY
     C                     Z-ADDACOD      IMACOD
     C                     Z-ADDACSQ      IMACSQ
     C                     MOVE BRCA      IMBRCA
     C                     MOVELIMACNT    BNCDS
     C                     END
      *                                                                                       CSW215
      ** Validate 59F format if CSW215 is switched on                                         CSW215
      *                                                                                       CSW215
     C           CSW215    IFEQ 'Y'                                                           CSW215
     C           I1BNC     ANDEQ*BLANKS                                                       CSW215
     C                     EXSR VAL59F                                                        CSW215
     C                     ENDIF                                                              CSW215
      *
      *  Unwind subroutine stack name
      *
     C           EXBNC     TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C/EJECT                                                                                  CSW215
      *****************************************************************                       CSW215
      *                                                               *                       CSW215
      *  VAL59  : VALIDATE 59F FORMAT                                 *                       CSW215
      *                                                               *                       CSW215
      *  CALLED BY: SRBNC - Validate Beneficiary                      *                       CSW215
      *                                                               *                       CSW215
      *  CALLS    : MG001001                                          *                       CSW215
      *                                                               *                       CSW215
      *****************************************************************                       CSW215
     CSR         VAL59F    BEGSR                                                              CSW215
      *                                                                                       CSW215
     C                     CALL 'MG001001'                                                    CSW215
     C                     PARM *BLANKS   PRTCD   7                                           CSW215
     C                     PARM 'TRAN'    PCLFR   4                                           CSW215
     C                     PARM           S1BNC1                                              CSW215
     C                     PARM           S1BNC2                                              CSW215
     C                     PARM           S1BNC3                                              CSW215
     C                     PARM           S1BNC4                                              CSW215
     C                     PARM           S1BNC5                                              CSW215
     C                     PARM V#NWRK    PNWRK   6                                           CSW215
     C                     PARM '103'     PTYPE   3                                           CSW215
     C                     PARM ':59F:'   PTAG    5                                           CSW215
     C                     PARM S1BRCA    PBRCH   3                                           CSW215
     C                     PARM V#SNLC    PCNTY   2                                           CSW215
     C                     PARM S1SMCY    PCURR   3                                           CSW215
     C                     PARM V#DBNS    PNOST   2                                           CSW215
     C                     PARM *BLANKS   POUT1  35                                           CSW215
     C                     PARM *BLANKS   POUT2  35                                           CSW215
     C                     PARM *BLANKS   POUT3  35                                           CSW215
     C                     PARM *BLANKS   POUT4  35                                           CSW215
     C                     PARM *BLANKS   POUT5  35                                           CSW215
     C                     PARM *BLANKS   PMSGID  7                                           CSW215
     C                     PARM *BLANKS   PERRT   1                                           CSW215
      *                                                                                       CSW215
     C           PRTCD     IFEQ '*ERROR'                                                      CSW215
      *                                                                                       CSW215
     C           PERRT     IFEQ 'W'                                                           CSW215
     C                     MOVEL'Y'       E#WARN                                              CSW215
     C                     ELSE                                                               CSW215
     C                     MOVEL'Y'       E#NOUP                                              CSW215
     C                     ENDIF                                                              CSW215
      *                                                                                       CSW215
     C                     MOVELPMSGID    FTMSID                                              CSW215
     C                     MOVELE#CPGM    FTPGMQ                                              CSW215
     C                     MOVEL'Y'       E#MSGS                                              CSW215
     C                     MOVEL'Y'       I1BNC1                                              CSW215
     C                     MOVEL'Y'       I1BNC2                                              CSW215
     C                     MOVEL'Y'       I1BNC3                                              CSW215
     C                     MOVEL'Y'       I1BNC4                                              CSW215
     C                     MOVEL'Y'       I1BNC5                                              CSW215
     C                     MOVEL'GBMEMSG' FTMSGF                                              CSW215
      *                                                                                       CSW215
     C                     EXSR FTSNMS                                                        CSW215
     C                     ENDIF                                                              CSW215
     C                     ENDSR                                                              CSW215
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRACBK   : Validate Account with Institution                 *
      *                                                               *
      *  CALLED BY: SRPROC - Control Process of Validation            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRACBK    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRACBK'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352ACBK
      *
     C                     MOVEL*BLANK    F1ACBT
      *
      *  ? not allowed
      *
     C           '?'       SCAN S1ACBK    Y#             90
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1ACBK    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2089' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1ACBK
     C                     MOVEL'Y'       I1ACBB
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXACBK
     C                     END
      *
      *  If blank and branch entered error
      *
     C           S1ACBK    IFEQ *BLANKS
     C           S1ACBB    ANDNE*BLANKS
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1ACBK    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2090' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1ACBK
     C                     MOVEL'Y'       I1ACBB
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXACBK
     C                     END
      *
      *  Field is optional
      *
     C           S1ACBK    IFEQ *BLANK
     C                     GOTO EXACBK
     C                     END
      *
      *  Move Ordering Bank to ADDRSS data structure for Analysis
      *
     C                     MOVEL*BLANKS   ADDRSS
     C                     MOVELS1ACBK    ADDRSS
      *
      *  If first 2 Character are not blank but rest is blank
      *  validate as a nostro number
      *
     C           ADNOSN    IFNE *BLANKS
     C           ADEXNN    ANDEQ*BLANKS
     C           ADCH26    ANDEQ*BLANKS
     C           S1ACBB    ANDEQ*BLANKS
      *
      * Look up Nostro via Access Object
      *
     C                     CALL 'AONOSTR0'             9090
     C                     PARM *BLANKS   RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM *BLANKS   @CUST
     C                     PARM S1PCCY    @CYCD
     C                     PARM *BLANKS   @ACCD
     C                     PARM *BLANKS   @ACSN
     C                     PARM ADNOSN    @NONB
     C                     PARM *BLANKS   @BRCD
     C                     PARM *BLANKS   @CSSN
     C                     PARM *BLANKS   @PNOI
     C           SDNOST    PARM *BLANKS   DSFDY
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AONOSTR0'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 14 *
     C                     Z-ADD14        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      *  Nostro not found
      *
     C           RTCD      IFNE *BLANKS
     C                     MOVEL'Y'       E#MSGS
     C           S1PCCY    CAT  S1ACBK    FTMSDT    P
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2091' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1ACBK
     C                     MOVEL'Y'       I1ACBB
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXACBK
     C                     END
      *
      *  Nostro found mark as full nostro and get GL information
      *
     C                     MOVEL'N'       F1ACBT
      *
      * Store in occurrance data structure 4 any information
      *
     C           4         OCUR OACCNT
      *
      *  Set up key for chain
      *
     C                     MOVELA7CUST    CNUM
     C                     MOVELA7CYCD    CCY
     C                     MOVELA7ACCD    ACOD
     C                     MOVELA7ACSN    ACSQ
     C                     MOVELA7BRCD    BRCA
      *
     C           AKEY      CHAINACCNTABF             90
      *
      *  Database error account does not exist
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL*BLANKS   ADCHK1
     C                     MOVELA7CUST    ADGCNM
     C                     MOVELA7CYCD    ADGCCY
     C                     MOVELA7ACCD    ADGACD
     C                     MOVELA7ACSN    ADGASQ
     C                     MOVELA7BRCD    ADGBRC
     C                     MOVEL'ACCNT   'W0FILE           ***************
     C                     MOVELADCHK1    W0KEY            * DB ERROR 15 *
     C                     Z-ADD15        W0ERNB           ***************
     C                     MOVEL'MEM5004' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C                     END
      *
      *  If first 10 Characters are numeric but rest is blank
      *  validate as a retail account
      *
      *  Only validate if F1ACBT is blank
      *
     C           ADACNO    IFNE *BLANKS
     C           ADEXAC    ANDEQ*BLANKS
     C           ADCH26    ANDEQ*BLANKS
     C           F1ACBT    ANDEQ*BLANKS
     C           S1ACBB    ANDEQ*BLANKS
      *
      *  Check first 10 characters are numeric and rest is blank
      *
     C                     MOVEL*BLANKS   I1FLD
     C                     MOVELS1ACBK    I1FLD  35
     C           DIGITS    CHECKI1FLD     Y#      30     90
      *
      *  10 digits allowed
      *
     C           *IN90     IFEQ '1'
     C           Y#        ANDEQ11
      *
      *  Check that retail account numbers are allowed
      *
     C           V#RANR    IFEQ 'N'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1ACBK    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2092' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1ACBK
     C                     MOVEL'Y'       I1ACBB
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXACBK
     C                     END
      *
      * Store in occurrance data structure 4 any information
      *
     C           4         OCUR OACCNT
      *
      *  Check account is on ACCNTAB
      *
     C                     MOVELADACNO    ACNO
     C           ACNO      CHAINACNUMAF              90
      *
     C           *IN90     IFEQ '1'
     C           RECI      ORNE 'D'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1ACBK    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2093' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1ACBK
     C                     MOVEL'Y'       I1ACBB
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXACBK
     C                     END
      *
      * Currency of account must be equal to settle currency
      *
     C           S1PCCY    IFNE CCY
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1ACBK    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2094' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1ACBK
     C                     MOVEL'Y'       I1ACBB
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXACBK
     C                     END
      *
      *  Retail Account found mark as Retail
      *
     C                     MOVEL'R'       F1ACBT
      *
     C                     END
      *
     C                     END
      *
      * If A/c with Bank still not set validate as partail
      *
     C           F1ACBT    IFEQ *BLANKS
      *
      *  Customer number, account code and account sequence must be
      *  numeric
      *
     C                     MOVELADPCNM    PTCNUM
     C                     MOVELADPACD    PTACOD
     C                     MOVELADPASQ    PTACSQ
      *
      ***Check*first*12*characters*are*numeric*************************                       CGL029
      ** Check first 18 characters are numeric                                                CGL029
      *
     C                     MOVEL*BLANKS   I1FLD
     C                     MOVELPTACCT    I1FLD  35
     C           DIGITS    CHECKI1FLD     Y#      30     90
      *
      ***12*digits*allowed*********************************************                       CGL029
      ** 18 digits allowed                                                                    CGL029
      *
     C           *IN90     IFEQ '1'
     C********** Y#        ANDNE13                                                            CGL029
     C           Y#        ANDNE19                                                            CGL029
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1ACBK    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2095' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1ACBK
     C                     MOVEL'Y'       I1ACBB
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXACBK
     C                     END
      *
      * Store in occurrance data structure 4 any information
      *
     C           4         OCUR OACCNT
      *
      *  Set up key for chain
      *
     C                     MOVELADPCNM    CNUM
     C                     MOVELS1PCCY    CCY
     C                     MOVELADPACD    ACOD
     C                     MOVELADPASQ    ACSQ
      *
      *  Set up branch depending on multibranching
      *
     C                     SELEC
     C           WUMBIN    WHNE 'Y'
     C                     MOVE V#SBRC    BRCA
     C           S1ACBB    WHEQ *BLANKS
     C                     MOVELDBRN      BRCA
     C                     OTHER
     C                     MOVELS1ACBB    BRCA
     C                     ENDSL
      *
     C           AKEY      CHAINACCNTABF             90
      *
     C           *IN90     IFEQ '1'
     C           RECI      ORNE 'D'
     C                     MOVEL'Y'       E#MSGS
     C           S1ACBK    CAT  BRCA      FTMSDT    P
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2096' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1ACBK
     C                     MOVEL'Y'       I1ACBB
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXACBK
     C                     END
      *
      *  GL Account found mark as Partial Account
      *
     C                     MOVEL'P'       F1ACBT
     C                     SELEC
     C           WUMBIN    WHEQ 'Y'
     C           S1ACBB    ANDEQ*BLANKS
     C                     MOVELBRCA      S1ACBB
     C           WUMBIN    WHEQ 'N'
     C                     MOVELBRCA      S1ACBB
     C                     ENDSL
      *
     C                     END
      *
      * If a valid account has been entered,
      * store full account details for FT Transaction Status File
      * record update.
      *
     C           F1ACBT    IFEQ 'N'
     C           F1ACBT    OREQ 'R'
     C           F1ACBT    OREQ 'P'
     C**********           Z-ADDCNUM      IMCNUM                                              CSD027
     C                     MOVE CNUM      IMCNUM                                              CSD027
     C                     MOVE CCY       IMCCY
     C                     Z-ADDACOD      IMACOD
     C                     Z-ADDACSQ      IMACSQ
     C                     MOVE BRCA      IMBRCA
     C                     MOVELIMACNT    ACBDS
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXACBK    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C                     ENDSR
     C*
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRPCCY   : Validate Pay Currency                             *
      *                                                               *
      *  CALLED BY: SRPROC - Control Process of Validation            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRPCCY    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRPCCY'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352PCCY
      *
      * Field defaults to Settle currency if not entered
      *
     C           S1PCCY    IFEQ *BLANK
     C           I1SMCY    ANDEQ*BLANK
     C                     MOVELS1SMCY    S1PCCY
     C                     END
      *
      * Mandatory field
      *
     C           S1PCCY    IFEQ *BLANK
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1PCCY    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2097' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1PCCY
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXPCCY
     C                     END
      *
      *  Look field up on database
      *
     C                     CALL 'AOCURRR0'             9090
     C                     PARM *BLANKS   RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM S1PCCY    @K101
     C           SDCURR    PARM *BLANKS   DSSDY
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AOCURRR0'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 16 *
     C                     Z-ADD16        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C           RTCD      IFNE *BLANKS                    B001
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1PCCY    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2098' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1PCCY
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXPCCY
     C                     END
      *
      *  Move new standing data field into field used in program
      *
     C           *LIKE     DEFN A6NBDP    PCNBDP
     C           *LIKE     DEFN A6SPRT    PCSPOT
     C           *LIKE     DEFN A6MDIN    PCMDIN
     C           *LIKE     DEFN A6SPRT    PCBUY
     C           *LIKE     DEFN A6SPRT    PCSELL
      *
      * Store Decimal Places Spot Rate Multiply/Divide Ind.
      *
     C                     Z-ADDA6NBDP    PCNBDP
     C                     Z-ADDA6SPRT    PCSPOT
     C                     MOVELA6MDIN    PCMDIN
      *
     C           A6BYSS    IFEQ '+'
     C           A6SPRT    ADD  A6BYSR    PCBUY
     C                     ELSE
     C           A6SPRT    SUB  A6BYSR    PCBUY
     C                     END
      *
     C           A6SLSS    IFEQ '+'
     C           A6SPRT    ADD  A6SLSR    PCSELL
     C                     ELSE
     C           A6SPRT    SUB  A6SLSR    PCSELL
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXPCCY    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRPYAM   : Validate Pay Amount                               *
      *                                                               *
      *  CALLED BY: SRPROC - Control Process of Validation            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRPYAM    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRPYAM'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352PYAM
      *
     C                     Z-ADD0         F1PYAM
      *
      * Mandatory if pay amount not entered
      *
     C           S1SMAM    IFEQ *BLANK
     C           S1PYAM    ANDEQ*BLANK
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1PYAM    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2099' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1PYAM
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXPYAM
     C                     END
      *
      * If entered Validate
      *
     C                     MOVEL*BLANK    ZFIELD
     C                     MOVE S1PYAM    ZFIELD
     C           13        SUB  PCNBDP    ZADIG
     C                     Z-ADDPCNBDP    ZADEC
      *
     C                     EXSR SRALGN
      *
     C           ZRTN      IFNE *BLANKS
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1PYAM    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2100' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1PYAM
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXPYAM
     C                     END
      *
     C                     MOVE ZFIELD    F1PYAM
      *
      * Set amount entered indicator if pay amount
      * or it is blanks
      *
     C           S1SMAM    IFEQ *BLANK
     C           F1FAIN    OREQ *BLANK
     C                     MOVEL'R'       F1FAIN
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXPYAM    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRRCCO   : Validate Receiver' Correspondent                  *
      *                                                               *
      *  CALLED BY: SRPROC - Control Process of Validation            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRRCCO    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRRCCO'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352RCCO
      *
     C                     MOVEL*BLANK    F1RCRT
      *                                                                   CFT014
      ** Different types are                                              CFT014
      ** I : Iban (CFT014 on)                                             CFT014
      ** N : Nostro 2 chars (mapped to a Full Nostro F if CFT014 on)      CFT014
      ** F : Full Nostro 5 chars (CFT014 on)                              CFT014
      ** C : Customer number 6 chars (CFT014 on)                          CFT014
      ** R : Retail 10 chars                                              CFT014
      ** C : Customer short name 10 chars (CFT014 on)                     CFT014
      ** S : SWIFT id 11 chars (CFT014 on)                                CFT014
      ** A : Adress 4 X 35 chars (CFT014 on)                              CFT014
      *                                                                   CFT014
      *
      *  ? not allowed
      *
     C           '?'       SCAN S1RCCO    Y#             90
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1RCCO    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2101' FTMSID
     C                     EXSR FTSNMS
     C*********************MOVEL'Y'       I1RCCO                          CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXRCCO
     C                     END
      *
      *  Field is optional
      *
     C           S1RCCO    IFEQ *BLANK
     C                     GOTO EXRCCO
     C                     END
     C/COPY WNCPYSRC,FT0352C011
      *                                                                   CFT014
      *  Field cannot exceed 10 chars if CFT014 is off                    CFT014
      *                                                                   CFT014
     C           ' '       CHEKRS1RCCO    WLNRCV  30                      CFT014
     C           CFT014    IFEQ 'N'                                       CFT014
     C           WLNRCV    IFGT 10                                        CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM4103' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXRCCO                                    CFT014
     C                     ENDIF                                          CFT014
      *  End test for CFT014 off                                          CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *
      *  Move Receiver's Corr to ADDRSS data structure for Analysis
      *
     C                     MOVEL*BLANKS   ADDRSS
     C                     MOVELS1RCCO    ADDRSS
      *                                                                   CFT014
      ** Receiver's Correspondant is an Iban ?                            CFT014
      *                                                                   CFT014
     C           CFT014    IFEQ 'Y'                                       CFT014
     C           CFT004    IFEQ 'Y'                                       CFT014
     C           ADSLSH    IFEQ '/'                                       CFT014
     C           34        SUBSTS1RCO1:2  PIBAN                           CFT014
     C                     ELSE                                           CFT014
     C                     MOVELS1RCO1    PIBAN                           CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     CALL 'AOIBANR3'                                CFT014
     C                     PARM           @RTCD                           CFT014
     C                     PARM           PIBAN                           CFT014
     C                     PARM           POWITH                          CFT014
     C           P@IBAN    PARM           PONOBL                          CFT014
      *                                                                   CFT014
     C           @RTCD     IFEQ *BLANKS                                   CFT014
      *                                                                   CFT014
     C           1         SUBSTP@IBAN:1  WTRD1   1                       CFT014
     C           1         SUBSTP@IBAN:2  WTRD2   1                       CFT014
     C           1         SUBSTP@IBAN:3  WTRD3   1                       CFT014
     C           1         SUBSTP@IBAN:4  WTRD4   1                       CFT014
     C           WTRD1     LOKUPALPH                     94               CFT014
     C   94      WTRD2     LOKUPALPH                     94               CFT014
     C   94      WTRD3     LOKUPNUMC                     94               CFT014
     C   94      WTRD4     LOKUPNUMC                     94               CFT014
      *                                                                   CFT014
      ** Entry is in valid IBAN format.                                   CFT014
      *                                                                   CFT014
     C           *IN94     IFEQ '1'                                       CFT014
      *                                                                   CFT014
      * Store in occurrance data structure 1 any information              CFT014
      *                                                                   CFT014
     C           1         OCUR OACCNT                                    CFT014
      *                                                                   CFT014
     C                     MOVE *BLANKS   DSSDY                           CFT014
     C                     CALL 'AOIBANR2'                                CFT014
     C                     PARM           @RTCD                           CFT014
     C                     PARM           P@IBAN                          CFT014
     C           OACCNT    PARM           DSSDY                           CFT014
      *                                                                   CFT014
     C           @RTCD     IFNE *BLANKS                                   CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
      *                                                                   CFT014
      ** IBAN entry is an invalid ISO country code.                       CFT014
      *                                                                   CFT014
     C           @RTCD     IFEQ '      1'                                 CFT014
     C                     MOVEL'FTM3107' FTMSID                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Invalid BBAN length on country file for IBAN entry.              CFT014
      *                                                                   CFT014
     C           @RTCD     IFEQ '      2'                                 CFT014
     C                     MOVEL'FTM3109' FTMSID                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** IBAN entry contains invalid characters.                          CFT014
      *                                                                   CFT014
     C           @RTCD     IFEQ '      3'                                 CFT014
     C                     MOVEL'FTM3111' FTMSID                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** IBAN length of BBAN  is incorrect.                               CFT014
      *                                                                   CFT014
     C           @RTCD     IFEQ '      4'                                 CFT014
     C                     MOVEL'FTM3113' FTMSID                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Check Digits are incorrect for IBAN entry.                       CFT014
      *                                                                   CFT014
     C           @RTCD     IFEQ '      5'                                 CFT014
     C                     MOVEL'FTM3115' FTMSID                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXRCCO                                    CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** IBAN account does not exist on the system.                       CFT014
      *                                                                   CFT014
     C           @RTCD     IFEQ '      6'                                 CFT014
     C                     MOVEL'A'       F1RCRT                          CFT014
     C                     GOTO EXRCCO                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** IBAN is valid.                                                   CFT014
      *                                                                   CFT014
     C           '/'       CAT  P@IBAN    S1RCO1                          CFT014
     C                     MOVE 'I'       F1RCRT                          CFT014
      *                                                                   CFT014
     C           AKEY      CHAINACCNTABF             90                   CFT014
      *                                                                   CFT014
      *  Database error account does not exist                            CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL*BLANKS   ADCHK1                          CFT014
     C                     MOVE CNUM      ADGCNM                          CFT014
     C                     MOVELCCY       ADGCCY                          CFT014
     C                     MOVE ACOD      ADGACD                          CFT014
     C                     MOVE ACSQ      ADGASQ                          CFT014
     C                     MOVELBRCA      ADGBRC                          CFT014
     C                     MOVEL'ACCNT   'W0FILE           ***************CFT014
     C                     MOVELADCHK1    W0KEY            * DB ERROR 67 *CFT014
     C                     Z-ADD67        W0ERNB           ***************CFT014
     C                     MOVEL'MEM5004' W0MSGD                          CFT014
     C                     MOVEL'MIDAS  ' W0MSGF                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *  End test for IBAN allowed                                        CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *  End test for CFT014 on                                           CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *
      *  If first 2 Character are not blank but rest is blank
      *  validate as a nostro number
      *
     C           ADNOSN    IFNE *BLANKS
     C           ADEXNN    ANDEQ*BLANKS
     C           ADCH26    ANDEQ*BLANKS
     C           F1RCRT    ANDEQ*BLANKS                                   CFT014
      *
      * Look up Nostro via Access Object
      *
     C                     CALL 'AONOSTR0'             9090
     C                     PARM *BLANKS   RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM *BLANKS   @CUST
     C                     PARM S1SMCY    @CYCD
     C                     PARM *BLANKS   @ACCD
     C                     PARM *BLANKS   @ACSN
     C                     PARM ADNOSN    @NONB
     C                     PARM *BLANKS   @BRCD
     C                     PARM *BLANKS   @CSSN
     C                     PARM *BLANKS   @PNOI
     C           SDNOST    PARM *BLANKS   DSFDY
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AONOSTR0'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 17 *
     C                     Z-ADD17        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      *  Nostro not found
      *
     C           RTCD      IFNE *BLANKS
     C                     MOVEL'Y'       E#MSGS
     C           S1SMCY    CAT  S1RCCO    FTMSDT    P
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2114' FTMSID
     C                     EXSR FTSNMS
     C*********************MOVEL'Y'       I1RCCO                          CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXRCCO
     C                     END
      *
      *  Nostro found mark as nostro and get GL information
      *
     C           CFT014    IFEQ 'Y'                                       CFT014
     C           S1SMCY    CAT  ADNOSN    S1RCO1                          CFT014
     C                     MOVEL'F'       F1RCRT                          CFT014
     C                     ELSE                                           CFT014
     C                     MOVEL'N'       F1RCRT
     C                     ENDIF                                          CFT014
      *
      * Store in occurrance data structure 1 any information
      *
     C           1         OCUR OACCNT
      *
      *  Set up key for chain
      *
     C                     MOVELA7CUST    CNUM
     C                     MOVELA7CYCD    CCY
     C                     MOVELA7ACCD    ACOD
     C                     MOVELA7ACSN    ACSQ
     C                     MOVELA7BRCD    BRCA
      *
     C           AKEY      CHAINACCNTABF             90
      *
      *  Database error account does not exist
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL*BLANKS   ADCHK1
     C                     MOVELA7CUST    ADGCNM
     C                     MOVELA7CYCD    ADGCCY
     C                     MOVELA7ACCD    ADGACD
     C                     MOVELA7ACSN    ADGASQ
     C                     MOVELA7BRCD    ADGBRC
     C                     MOVEL'ACCNT   'W0FILE           ***************
     C                     MOVELADCHK1    W0KEY            * DB ERROR 18 *
     C                     Z-ADD18        W0ERNB           ***************
     C                     MOVEL'MEM5004' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C                     END
      *                                                                   CFT014
      ** Receiver's Correspondant is a Full Nostro ?                      CFT014
      *                                                                   CFT014
     C           CFT014    IFEQ 'Y'                                       CFT014
      *                                                                   CFT014
     C           ADFNOS    IFNE *BLANK                                    CFT014
     C           ADFNEX    ANDEQ*BLANK                                    CFT014
     C           ADCH26    ANDEQ*BLANK                                    CFT014
     C           F1RCRT    ANDEQ*BLANK                                    CFT014
      *                                                                   CFT014
      * Look up Nostro via Access Object                                  CFT014
      *                                                                   CFT014
     C                     CALL 'AONOSTR0'             9090               CFT014
     C                     PARM *BLANKS   RTCD                            CFT014
     C                     PARM '*KEY   ' @OPTN                           CFT014
     C                     PARM *BLANKS   @CUST                           CFT014
     C                     PARM ADFCCY    @CYCD                           CFT014
     C                     PARM *BLANKS   @ACCD                           CFT014
     C                     PARM *BLANKS   @ACSN                           CFT014
     C                     PARM ADFNNB    @NONB   2                       CFT014
     C                     PARM *BLANKS   @BRCD                           CFT014
     C                     PARM *BLANKS   @CSSN                           CFT014
     C                     PARM *BLANKS   @PNOI                           CFT014
     C           SDNOST    PARM *BLANKS   DSFDY                           CFT014
      *                                                                   CFT014
      *  If error on call                                                 CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL'AONOSTR0'W0FILE           ***************CFT014
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 70 *CFT014
     C                     Z-ADD70        W0ERNB           ***************CFT014
     C                     MOVEL'MEM5003' W0MSGD                          CFT014
     C                     MOVEL'MIDAS  ' W0MSGF                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     END                                            CFT014
      *                                                                   CFT014
     C           RTCD      IFNE *BLANKS                                   CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3122' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXRCCO                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      *  Nostro found mark as full nostro and get GL information          CFT014
      *                                                                   CFT014
     C                     MOVEL'F'       F1RCRT                          CFT014
      *                                                                   CFT014
      * Store in occurrance data structure 1 any information              CFT014
      *                                                                   CFT014
     C           1         OCUR OACCNT                                    CFT014
      *                                                                   CFT014
      *  Set up key for chain                                             CFT014
      *                                                                   CFT014
     C                     MOVELA7CUST    CNUM                            CFT014
     C                     MOVELADFCCY    CCY                             CFT014
     C                     MOVELA7ACCD    ACOD                            CFT014
     C                     MOVELA7ACSN    ACSQ                            CFT014
     C                     MOVELA7BRCD    BRCA                            CFT014
      *                                                                   CFT014
     C           AKEY      CHAINACCNTABF             90                   CFT014
      *                                                                   CFT014
      *  Database error account does not exist                            CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL*BLANKS   ADCHK1                          CFT014
     C                     MOVELA7CUST    ADGCNM                          CFT014
     C                     MOVELADFCCY    ADGCCY                          CFT014
     C                     MOVELA7ACCD    ADGACD                          CFT014
     C                     MOVELA7ACSN    ADGASQ                          CFT014
     C                     MOVELA7BRCD    ADGBRC                          CFT014
     C                     MOVEL'ACCNT   'W0FILE           ***************CFT014
     C                     MOVELADCHK1    W0KEY            * DB ERROR 68 *CFT014
     C                     Z-ADD68        W0ERNB           ***************CFT014
     C                     MOVEL'MEM5004' W0MSGD                          CFT014
     C                     MOVEL'MIDAS  ' W0MSGF                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Receiver's correspondant is a customer number ?                  CFT014
      *                                                                   CFT014
     C           ADCUST    IFNE *BLANK                                    CFT014
     C           ADEXCN    ANDEQ*BLANK                                    CFT014
     C           ADCH26    ANDEQ*BLANK                                    CFT014
     C           F1RCRT    ANDEQ*BLANK                                    CFT014
      *                                                                   CFT014
      * Check if name exists                                              CFT014
      *                                                                   CFT014
     C                     CALL 'AOCUSTR0'             9090               CFT014
     C                     PARM *BLANKS   RTCD                            CFT014
     C                     PARM '*KEY   ' @OPTN   7                       CFT014
     C                     PARM ADCUST    @KEY1  10                       CFT014
     C                     PARM *BLANKS   @KYST   7                       CFT014
     C           SDCUST    PARM *BLANKS   DSSDY                           CFT014
      *                                                                   CFT014
      *  If error on call                                                 CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL'AOCUSTR0'W0FILE           ***************CFT014
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 10 *CFT014
     C                     Z-ADD10        W0ERNB           ***************CFT014
     C                     MOVEL'MEM5003' W0MSGD                          CFT014
     C                     MOVEL'MIDAS  ' W0MSGF                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     END                                            CFT014
      *                                                                   CFT014
     C           RTCD      IFNE *BLANKS                                   CFT014
     C           @KYST     ORNE '*CNUM'                                   CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3121' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXRCCO                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      *  Customer found mark as Customer                                  CFT014
      *                                                                   CFT014
     C                     MOVEL'C'       F1RCRT                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      *  End test for CFT014 on                                           CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *
      *  If first 10 Characters are numeric but rest is blank
      *  validate as a retail account
      *
      *  Only validate if F1RCRT is blank
      *
     C           ADACNO    IFNE *BLANKS
     C           ADEXAC    ANDEQ*BLANKS
     C           ADCH26    ANDEQ*BLANKS
     C           F1RCRT    ANDEQ*BLANKS
      *
      *  Check first 10 characters are numeric and rest is blank
      *
     C                     MOVEL*BLANKS   I1FLD
     C                     MOVELS1RCCO    I1FLD  35
     C           DIGITS    CHECKI1FLD     Y#      30     90
      *
      *  10 digits allowed
      *
     C           *IN90     IFEQ '1'
     C           Y#        ANDEQ11
      *
      *  Check that retail account numbers are allowed
      *
     C           V#RANR    IFEQ 'N'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1RCCO    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2115' FTMSID
     C                     EXSR FTSNMS
     C*********************MOVEL'Y'       I1RCCO                          CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXRCCO
     C                     END
      *
      * Store in occurrance data structure 1 any information
      *
     C           1         OCUR OACCNT
      *
      *  Check account is on ACCNTAB
      *
     C                     MOVELADACNO    ACNO
     C           ACNO      CHAINACNUMAF              90
      *
     C           *IN90     IFEQ '1'
     C           RECI      ORNE 'D'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1RCCO    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2116' FTMSID
     C                     EXSR FTSNMS
     C*********************MOVEL'Y'       I1RCCO                          CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXRCCO
     C                     END
      *
      * Currency of account must be equal to settle currency
      *
     C           S1SMCY    IFNE CCY
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1RCCO    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2117' FTMSID
     C                     EXSR FTSNMS
     C*********************MOVEL'Y'       I1RCCO                          CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXRCCO
     C                     END
      *
      *  Retail Account found mark as Retail
      *
     C                     MOVEL'R'       F1RCRT
      *
     C                     END
     C                     END                             E001
     C/COPY WNCPYSRC,FT0352C020
      *                                                                   CFT014
     C           CFT014    IFEQ 'Y'                                       CFT014
      *                                                                   CFT014
      ** Receiver's correspondant is a customer short name ?              CFT014
      *                                                                   CFT014
     C           ADCUST    IFNE *BLANK                                    CFT014
     C           ADEXCS    ANDEQ*BLANK                                    CFT014
     C           ADCH26    ANDEQ*BLANK                                    CFT014
     C           F1RCRT    ANDEQ*BLANK                                    CFT014
      *                                                                   CFT014
      * Check if name exists                                              CFT014
      *                                                                   CFT014
     C                     CALL 'AOCUSTR0'             9090               CFT014
     C                     PARM *BLANKS   RTCD                            CFT014
     C                     PARM '*KEY   ' @OPTN   7                       CFT014
     C                     PARM ADCUST    @KEY1  10                       CFT014
     C                     PARM *BLANKS   @KYST   7                       CFT014
     C           SDCUST    PARM *BLANKS   DSSDY                           CFT014
      *                                                                   CFT014
      *  If error on call                                                 CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL'AOCUSTR0'W0FILE           ***************CFT014
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 69 *CFT014
     C                     Z-ADD69        W0ERNB           ***************CFT014
     C                     MOVEL'MEM5003' W0MSGD                          CFT014
     C                     MOVEL'MIDAS  ' W0MSGF                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     END                                            CFT014
      *                                                                   CFT014
     C           RTCD      IFNE *BLANKS                                   CFT014
     C           @KYST     ORNE '*CSHT  '                                 CFT014
      *                                                                   CFT014
      *  Can be a SWIFT id                                                CFT014
      *                                                                   CFT014
     C           WLNRCV    IFNE 8                                         CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3104' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXRCCO                                    CFT014
     C                     ENDIF                                          CFT014
     C                     ELSE                                           CFT014
      *                                                                   CFT014
      *  Customer found mark as Customer                                  CFT014
      *                                                                   CFT014
     C                     MOVEL'C'       F1RCRT                          CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Receiver's correspondant is a SWIFT id ?                         CFT014
      *                                                                   CFT014
     C           ADCH26    IFEQ *BLANKS                                   CFT014
     C           ADEXSW    ANDEQ*BLANKS                                   CFT014
     C           ADSLSH    ANDNE'/'                                       CFT014
     C           F1RCRT    ANDEQ*BLANKS                                   CFT014
     C                     MOVELADCHK1    CLSWTK                          CFT014
      *                                                                   CFT014
     C                     EXSR SWIFT                                     CFT014
      *                                                                   CFT014
     C           ##CSID    IFEQ 'Y'                                       CFT014
     C           *IN32     IFEQ '1'                                       CFT014
     C           *IN31     OREQ '1'                                       CFT014
     C/COPY WNCPYSRC,FT0352C016
      *                                                                   CFT014
      *  No SWIFT address record.                                         CFT014
      *                                                                   CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3119' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXRCCO                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     MOVEL'S'       F1RCRT                          CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CFT014
     C/COPY WNCPYSRC,FT0352C017
      *                                                                   CFT014
      ** Receiver's Correspondant is an adress ?                          CFT014
      *                                                                   CFT014
     C           F1RCRT    IFEQ *BLANKS                                   CFT014
     C                     EXSR ADCHK                                     CFT014
      *                                                                   CFT014
      *  Depending on the indicators, highlight the field in error        CFT014
      *                                                                   CFT014
     C           *IN91     IFEQ '1'                                       CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3117' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     END                                            CFT014
      *                                                                   CFT014
     C           *IN92     IFEQ '1'                                       CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3117' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     END                                            CFT014
      *                                                                   CFT014
     C           *IN93     IFEQ '1'                                       CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     END                                            CFT014
     C*                                                                   CFT014
     C           AD#1      IFEQ 'Y'                                       CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     ENDIF                                          CFT014
     C           AD#2      IFEQ 'Y'                                       CFT014
     C                     MOVEL'Y'       I1RCO2                          CFT014
     C                     ENDIF                                          CFT014
     C           AD#3      IFEQ 'Y'                                       CFT014
     C                     MOVEL'Y'       I1RCO3                          CFT014
     C                     ENDIF                                          CFT014
     C           AD#4      IFEQ 'Y'                                       CFT014
     C                     MOVEL'Y'       I1RCO4                          CFT014
     C                     ENDIF                                          CFT014
     C           AD#5      IFEQ 'Y'                                       CFT014
     C                     MOVEL'Y'       I1RCO5                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      *  If no errors then customer is an address                         CFT014
      *                                                                   CFT014
     C           I1RCO     IFEQ *BLANKS                                   CFT014
     C                     MOVEL'A'       F1RCRT                          CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      *  End test for CFT014 on                                           CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *
      * If Receiver's correspondent type is not setup, then the entry
      * is invalid retail account
      *
     C           F1RCRT    IFEQ *BLANKS
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1RCCO    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2118' FTMSID
     C                     EXSR FTSNMS
     C*********************MOVEL'Y'       I1RCCO                          CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXRCCO
     C                     END
      *
      *  If Receiver's correspondent is a NOSTRO set pay location
      *  to nostro location.Else use system location
      *
     C                     SELEC
     C           F1RCRT    WHEQ 'R'
      *
     C                     MOVELCNUM      @CUST
      *
     C                     CALL 'AONOSTR0'             9090
     C                     PARM *BLANKS   RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM           @CUST   6
     C                     PARM CCY       @CYCD   3
     C**********           PARM ACODA     @ACCD   4                                           CGL029
     C                     PARM ACODA     @ACCD                                               CGL029
     C                     PARM ACSQA     @ACSN   2
     C                     PARM *BLANKS   @NONB
     C                     PARM BRCA      @BRCD
     C                     PARM *BLANKS   @CSSN  10
     C                     PARM *BLANKS   @PNOI   1
     C           SDNOST    PARM *BLANKS   DSFDY
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AONOSTR0'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 19 *
     C                     Z-ADD19        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C           RTCD      IFEQ *BLANKS
     C                     MOVE NLOC      PLOC
     C                     MOVE 'N'       NOWRK   1
     C                     ELSE
     C                     MOVELV#SLCD    PLOC
     C                     MOVEL'S'       NOWRK
     C                     END
      *
     C           F1RCRT    WHEQ 'N'
     C                     MOVE NLOC      PLOC    3
     C                     MOVE 'N'       NOWRK
      *                                                                   CFT014
     C           F1RCRT    WHEQ 'F'                                       CFT014
     C                     MOVE NLOC      PLOC    3                       CFT014
     C                     MOVE 'N'       NOWRK                           CFT014
      *
     C                     OTHER
     C                     MOVELV#SLCD    PLOC
     C                     MOVEL'S'       NOWRK
     C                     ENDSL
      *
      * If a valid account has been entered,
      * store full account details for FT Transaction Status File
      * record update.
      *
     C           F1RCRT    IFEQ 'N'
     C           F1RCRT    OREQ 'R'
     C           F1RCRT    OREQ 'I'                                       CFT014
     C           F1RCRT    OREQ 'F'                                       CFT014
     C**********           Z-ADDCNUM      IMCNUM                                              CSD027
     C                     MOVE CNUM      IMCNUM                                              CSD027
     C                     MOVE CCY       IMCCY
     C                     Z-ADDACOD      IMACOD
     C                     Z-ADDACSQ      IMACSQ
     C                     MOVE BRCA      IMBRCA
     C                     MOVELIMACNT    RCCDS
     C                     MOVELA7NONB    V#DBNS                                              250600
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXRCCO    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRSLDT   : Validate Settle Value Date                        *
      *                                                               *
      *  CALLED BY: SRPROC - Control Process of Validation            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRSLDT    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRSLDT'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352SLDT
      *
     C                     Z-ADD0         F1SLDT
      *
      *  If date not entered default to today
      *
     C           S1SLDT    IFEQ *BLANK
     C                     Z-ADDWURDNB    F1SLDT
     C                     MOVELV#MRDT    S1SLDT
     C                     END
      *
      * Check if date is numeric
      *
     C                     MOVELS1SLDT    DATEL   70
     C                     MOVELDATEL     DATES   6
      *
      * Date not numeric
      *
     C           S1SLDT    IFNE DATES
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SLDT    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2119' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SLDT
     C                     MOVEL'Y'       E#NOUP
     C                     MOVEA' '       WRN@,1
     C                     GOTO EXSLDT
     C                     END
      *
     C                     MOVE S1SLDT    ZDATE
      *
     C                     CALL 'ZDATE1'               9090
     C                     PARM *BLANKS   ZERR    7        error code
     C                     PARM           ZDATE   60       Value date
     C                     PARM WUDFF     ZDATFM  1        Date format ind
     C                     PARM *ZEROS    ZDAYNO  50       Value date
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'ZDATE1  'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 20 *
     C                     Z-ADD20        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      * Date invalid
      *
     C           ZERR      IFEQ '*ERROR*'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SLDT    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2120' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SLDT
     C                     MOVEL'Y'       E#NOUP
     C                     MOVEA' '       WRN@,1
     C                     GOTO EXSLDT
     C                     END
      *
      *  Continue with checks if date is not in error
      *
      *  The Settle date must not be a holiday in local currency
      *
     C                     CALL 'FT0305'               9090
     C                     PARM *BLANKS   W0RTN   7
     C                     PARM ZDAYNO    W0RDNB  50
     C                     PARM V#LCCY    W0CCY   3
     C                     PARM *BLANKS   W0LOC   3
     C                     PARM *BLANKS   W0IND   1
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'FT0305  'W0FILE           ***************
     C*************        MOVEL'*CALL'   W0KEY            * DB ERROR 20 *CFT014
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 51 *CFT014
     C*************        Z-ADD20        W0ERNB           ***************CFT014
     C                     Z-ADD51        W0ERNB           ***************CFT014
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      *  if ZIND = 'H' then the date is a holiday
      *
     C           W0IND     IFEQ 'H'
      *
     C                     MOVEL'Y'       I1SLDT
     C                     MOVEL'Y'       E#WARN
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SLDT    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2121' FTMSID
     C                     EXSR FTSNMS
      *
     C           WRN@,1    IFEQ ' '
     C                     MOVEL*BLANKS   E#RSND
     C                     MOVEA'Y'       WRN@,1
     C                     END
      *
     C                     ELSE
     C                     MOVEA' '       WRN@,1
     C                     END
      *
     C                     Z-ADDZDAYNO    F1SLDT
      *
      *  Unwind subroutine stack name
      *
     C           EXSLDT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRPVDT   : Validate Pay Date                                 *
      *                                                               *
      *  CALLED BY: SRPROC - Control Process of Validation            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRPVDT    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRPVDT  '@STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352PVDT
      *
     C                     Z-ADDZDAYNO    F1PVDT
      *
      *  If blank then find next working day
      *
     C           S1PVDT    IFEQ *BLANK
     C                     MOVEL'D'       ZFREQ   1
      *
     C           NOWRK     IFEQ 'N'
     C                     MOVE S1PCCY    ZCCY    3
     C                     ELSE
     C                     MOVE V#BCCY    ZCCY
     C                     END
      *
     C                     Z-ADDWURDNB    ZDAYNO
     C                     MOVE PLOC      ZLOC    3
      *
      * Find run date plus 1
      *
     C                     CALL 'FT0310'               9090
     C                     PARM *BLANKS   ZZERR@  7
     C                     PARM '*NEWDATE'W0ACT   8
     C                     PARM ZFREQ     W0FREQ  1
     C           ZDAYNO    PARM ZDAYNO    W0RDNB  50
     C                     PARM 0         W0MDAY  20
     C                     PARM ZCCY      W0CCY   3
     C                     PARM ZLOC      W0LOC   3
      *
      *  If error on call
      *
     C           ZZERR@    IFNE *BLANKS
     C           *IN90     OREQ '1'
     C                     MOVEL'FT0310  'W0FILE           ***************
     C                     MOVELZCCY      W0KEY            * DB ERROR 21 *
     C                     Z-ADD21        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      * If not base currency find run date plus 2
      *
     C           S1PCCY    IFNE V#BCCY
      *
     C                     CALL 'FT0310'               9090
     C                     PARM *BLANKS   ZZERR@  7
     C                     PARM '*NEWDATE'W0ACT   8
     C                     PARM ZFREQ     W0FREQ  1
     C           ZDAYNO    PARM ZDAYNO    W0RDNB  50
     C                     PARM 0         W0MDAY  20
     C                     PARM ZCCY      W0CCY   3
     C                     PARM ZLOC      W0LOC   3
      *
      *  If error on call
      *
     C           ZZERR@    IFNE *BLANKS
     C           *IN90     OREQ '1'
     C                     MOVEL'FT0310  'W0FILE           ***************
     C***********          MOVELZCCY      W0KEY            * DB ERROR 21 *CFT014
     C                     MOVELZCCY      W0KEY            * DB ERROR 71 *CFT014
     C***********          Z-ADD21        W0ERNB           ***************CFT014
     C                     Z-ADD71        W0ERNB           ***************CFT014
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
     C                     END
      *
      * Format day
      *
     C                     CALL 'ZDATE2'               9090Entry parameter
     C                     PARM           ZDAYNO           Value date
     C                     PARM WUDFF     ZDATFM           Date format ind
     C                     PARM           ZDATE            Value date
     C                     PARM           ZADATE  7        Run-date in DDM
      *
      *  If error on CALL - process as database error
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'ZDATE2  'W0FILE           ***************
     C                     MOVEL'*CALL  ' W0KEY            * DB ERROR 22 *
     C                     Z-ADD22        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C                     MOVE ZDATE     S1PVDT
      *
     C                     ELSE
      *
      * Check if date is numeric
      *
     C                     MOVELS1PVDT    DATEL
     C                     MOVELDATEL     DATES
      *
     C           S1PVDT    IFNE DATES
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1PVDT    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2122' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1PVDT
     C                     MOVEL'Y'       E#NOUP
     C                     MOVEA' '       WRN@,2
     C                     GOTO EXPVDT
     C                     END
      *
     C                     MOVE S1PVDT    ZDATE
      *
     C                     CALL 'ZDATE1'               9090
     C                     PARM *BLANKS   ZERR    7        error code
     C                     PARM           ZDATE   60       Value date
     C                     PARM           ZDATFM  1        Date format ind
     C                     PARM *ZEROS    ZDAYNO  50       Value date
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'ZDATE1  'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 23 *
     C                     Z-ADD23        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      * Date invalid
      *
     C           ZERR      IFEQ '*ERROR*'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1PVDT    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2123' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1PVDT
     C                     MOVEL'Y'       E#NOUP
     C                     MOVEA' '       WRN@,2
     C                     GOTO EXPVDT
     C                     END
      *
     C                     END
      *
      *  Continue with checks if date is not in error
      *
      *  The pay date must not be a holiday in local currency
      *
     C                     CALL 'FT0305'               9090
     C                     PARM *BLANKS   W0RTN
     C                     PARM ZDAYNO    W0RDNB
     C                     PARM V#LCCY    W0CCY
     C                     PARM *BLANKS   W0LOC
     C                     PARM *BLANKS   W0IND
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'FT0305  'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 20 *
     C                     Z-ADD20        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      *  if ZIND = 'H' then the date is a holiday
      *
     C           W0IND     IFEQ 'H'
      *
     C                     MOVEL'Y'       E#WARN
     C                     MOVEL'Y'       I1PVDT
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1PVDT    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2124' FTMSID
     C                     EXSR FTSNMS
      *
     C           WRN@,2    IFEQ ' '
     C                     MOVEL*BLANKS   E#RSND
     C                     MOVEA'Y'       WRN@,2
     C                     END
      *
     C                     ELSE
     C                     MOVEA' '       WRN@,2
     C                     END
      *
     C                     Z-ADDZDAYNO    F1PVDT
      *
      *
      *  Unwind subroutine stack name
      *
     C           EXPVDT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRRATE   : Validate Rate Field                               *
      *                                                               *
      *  CALLED BY: SRPROC - Control Process of Validation            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRRATE    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRRATE'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352RATE
      *
      * If rate entered validate
      *
     C                     MOVEL'N'       WFRATE  1                       CGL009
     C           S1RATE    IFNE *BLANK
     C                     MOVEL'Y'       WFRATE                          CGL009
      *
      * Check if numeric
      *
     C                     MOVEL*BLANK    ZFIELD
     C                     MOVE S1RATE    ZFIELD
     C                     Z-ADD4         ZADIG
     C                     Z-ADD8         ZADEC
      *
     C                     EXSR SRALGN
      *
     C           ZRTN      IFNE *BLANKS
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1RATE    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2125' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1RATE
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXRATE
     C                     END
      *
     C                     MOVE ZFIELD    F1RATE
      *
     C                     END
      *
      ** We will let FT0010 rebuild the rate, MDI and amounts             166199
     C           CEU006    IFNE 'Y'                                       166199
      *
      * Calculate default rate
      *
     C                     EXSR SRDRAT
      *
      * If rate not entered use defaults.
      *
     C           S1RATE    IFEQ *BLANK
     C                     Z-ADDDRATE     F1RATE
     C                     MOVELDMDIN     F1MDIN
      *
      * Format rate for screen
      *
     C                     MOVE DRATE     ZFIELD
      *
     C                     CALL 'ZEDIT'                9090
     C                     PARM           ZFIELD           B:FUNCTION FIELD
     C                     PARM 8         ZADEC            I:NO OF DECIMALS
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'ZEDIT   'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 24 *
     C                     Z-ADD24        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C                     MOVE ZFIELD    S1RATE
      *
     C                     ELSE
      *
      * Determine multiply/divide
      *
     C                     SELEC
      *
      * If either currencies are base use default
      *
     C           S1SMCY    WHEQ V#BCCY
     C           S1PCCY    OREQ V#BCCY
     C                     MOVELDMDIN     F1MDIN
      *
      * If rate is same side of 1 as default rate use default ind.
      *
     C           DRATE     WHGT 1
     C           F1RATE    ANDGT1
     C           DRATE     ORLE 1
     C           F1RATE    ANDLE1
     C                     MOVELDMDIN     F1MDIN
      *
     C                     OTHER
      *
     C           DMDIN     IFEQ 'M'
     C                     MOVEL'D'       F1MDIN
     C                     ELSE
     C                     MOVEL'M'       F1MDIN
     C                     END
      *
     C                     ENDSL
      *
      *  Check rate
      *
     C           S1OVRD    IFNE 'Y'
     C           S1SMCY    OREQ S1PCCY
     C           S1OVRD    ANDEQ'Y'
     C                     EXSR SRRVAL
      *
      * If error exit
      *
     C           I1RATE    IFNE *BLANKS
     C                     GOTO EXRATE
     C                     END
     C                     END
      *
     C                     END
      *
     C                     EXSR SRRCAL
     C                     ENDIF                                          166199
      *
      *  Unwind subroutine stack name
      *
     C           EXRATE    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************   CFT014
      *                                                               *   CFT014
      *  SROEXR   : Validate Original Rate                            *   CFT014
      *                                                               *   CFT014
      *  CALLED BY: SRPROC - Control Process of Validation            *   CFT014
      *                                                               *   CFT014
      *  CALLS    :        -                                          *   CFT014
      *                                                               *   CFT014
      *****************************************************************   CFT014
     CSR         SROEXR    BEGSR                                          CFT014
      *                                                                   CFT014
      *  Set up subroutine stack name                                     CFT014
      *                                                                   CFT014
     C                     ADD  1         Q       50                      CFT014
     C                     MOVEL'SROEXR'  @STK,Q                          CFT014
      *                                                                   CFT014
      ** Validate non-blank Original Exchange Rate.                       CFT014
      *                                                                   CFT014
     C           S1OEXR    IFNE *BLANKS                                   CFT014
      *                                                                   CFT014
      ** Error if Instructed Amount is zero.                              CFT014
      *                                                                   CFT014
     C           V#NDAM    IFEQ *ZERO                                     CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3139' FTMSID                          CFT014
     C                     MOVEL'FTUSRMSG'FTMSGF                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1OEXR                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXOEXR                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Check if Original Exchange Rate is in valid format.              CFT014
      *                                                                   CFT014
     C                     MOVE *BLANKS   ZFIELD                          CFT014
     C                     MOVE S1OEXR    ZFIELD                          CFT014
     C                     Z-ADD4         ZADIG                           CFT014
     C                     Z-ADD8         ZADEC                           CFT014
     C                     EXSR SRALGN                                    CFT014
      *                                                                   CFT014
      ** If Original Exchange Rate is not in valid format,                CFT014
      ** display error message and bypass remaining validation.           CFT014
      *                                                                   CFT014
     C           ZRTN      IFNE *BLANK                                    CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3140' FTMSID                          CFT014
     C                     MOVEL'FTUSRMSG'FTMSGF                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1OEXR                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXOEXR                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     MOVE ZFIELD    F1OEXR                          CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** If Original Exchange Rate is blank or zero, get the default      CFT014
      ** Original Exchange Rate                                           CFT014
      *                                                                   CFT014
     C           F1OEXR    IFEQ *ZERO                                     CFT014
      *                                                                   CFT014
     C           F1PYAM    IFNE 0                                         CFT014
     C           S1PCCY    ANDNE*BLANKS                                   CFT014
     C           V#NDAM    ANDNE0                                         CFT014
     C           S1NDCY    ANDNE*BLANKS                                   CFT014
      *                                                                   CFT014
      ** Set up paramaters for call to FT0010.                            CFT014
      *                                                                   CFT014
     C                     CLEARP#0010                                    CFT014
     C                     MOVE S1PCCY    P#DBCY                          CFT014
     C                     Z-ADDF1PYAM    P#DBAM                          CFT014
     C                     Z-ADDF1PVDT    P#DBDT                          CFT014
     C                     MOVE S1NDCY    P#CRCY                          CFT014
     C                     Z-ADDV#NDAM    P#CRAM                          CFT014
     C                     Z-ADDF1PVDT    P#CRDT                          CFT014
     C           BTPCRD    MULT 0.01      P#RTDS                          CFT014
     C                     MOVEL'Y'       P#SDIN                          CFT014
      *                                                                   CFT014
     C                     CALL 'FT0010'                                  CFT014
     C                     PARM           P#0010                          CFT014
      *                                                                   CFT014
     C           P#RTCD    IFEQ '*ERROR*'                                 CFT014
     C                     MOVEL'FT0010'  W0FILE           ***************CFT014
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 53 *CFT014
     C                     Z-ADD53        W0ERNB           ***************CFT014
     C                     MOVEL'MEM5003' W0MSGD                          CFT014
     C                     MOVEL'MIDAS  ' W0MSGF                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Get difference in no. of decimal places.                         CFT014
      *                                                                   CFT014
     C           @1NBDP    SUB  PCNBDP    N1      30                      CFT014
      *                                                                   CFT014
     C                     SELEC                                          CFT014
      *                                                                   CFT014
      ** Same number of decimal places, set workfields equal to           CFT014
      ** amounts.                                                         CFT014
      *                                                                   CFT014
     C           N1        WHEQ 0                                         CFT014
     C                     Z-ADDV#NDAM    WNDAMT 130                      CFT014
     C                     Z-ADDF1PYAM    WPYAMT 130                      CFT014
      *                                                                   CFT014
      ** Instructed amount dec. greater than the Payment amount dec.,     CFT014
      ** align Payment amount to Instructed amount.                       CFT014
      *                                                                   CFT014
     C           N1        WHGT 0                                         CFT014
     C           F1PYAM    DIV  POWER,N1  WPYAMT                          CFT014
     C                     Z-ADDV#NDAM    WNDAMT                          CFT014
      *                                                                   CFT014
      ** Instructed amount dec. less  than the Payment amount dec.,       CFT014
      ** align Instructed amount to Payment amount.                       CFT014
      *                                                                   CFT014
     C           N1        WHLT 0                                         CFT014
     C                     Z-SUBN1        N1                              CFT014
     C           V#NDAM    DIV  POWER,N1  WNDAMT                          CFT014
     C                     Z-ADDF1PYAM    WPYAMT                          CFT014
      *                                                                   CFT014
     C                     ENDSL                                          CFT014
      *                                                                   CFT014
      ** Calculate Original Exchange Rate.                                CFT014
      *                                                                   CFT014
     C           P#MDIN    IFEQ 1                                         CFT014
     C           WNDAMT    DIV  WPYAMT    F1OEXR                          CFT014
     C                     ELSE                                           CFT014
     C           WPYAMT    DIV  WNDAMT    F1OEXR                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Format Original Exchange Rate for display.                       CFT014
      *                                                                   CFT014
     C                     MOVE *BLANKS   ZFIELD                          CFT014
     C                     MOVE F1OEXR    ZFIELD                          CFT014
     C                     Z-ADD8         ZADEC                           CFT014
      *                                                                   CFT014
     C                     CALL 'ZEDIT'                9090               CFT014
     C                     PARM           ZFIELD                          CFT014
     C                     PARM           ZADEC                           CFT014
      *                                                                   CFT014
      *  If error on call                                                 CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL'ZEDIT   'W0FILE           ***************CFT014
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 54 *CFT014
     C                     Z-ADD54        W0ERNB           ***************CFT014
     C                     MOVEL'MEM5003' W0MSGD                          CFT014
     C                     MOVEL'MIDAS  ' W0MSGF                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     END                                            CFT014
     C                     MOVE ZFIELD    S1OEXR                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      *  Unwind subroutine stack name                                     CFT014
      *                                                                   CFT014
     C           EXOEXR    TAG                                            CFT014
     C                     MOVEA*BLANKS   @STK,Q                          CFT014
     C                     SUB  1         Q                               CFT014
      *                                                                   CFT014
     CSR                   ENDSR                                          CFT014
     C/EJECT                                                              CFT014
      *****************************************************************   CFT014
      *                                                               *   CFT014
      *  SRIDFT   : Additional Defaulting Process for                 *   CFT014
      *           Instructed Currency / Amount                        *   CFT014
      *                                                               *   CFT014
      *  CALLED BY:        - SRAMTV                                   *   CFT014
      *                                                               *   CFT014
      *  CALLS    :        -                                          *   CFT014
      *                                                               *   CFT014
      *****************************************************************   CFT014
     C           SRIDFT    BEGSR                                          CFT014
      *                                                                   CFT014
      *  Set up subroutine stack name                                     CFT014
      *                                                                   CFT014
     C                     ADD  1         Q                               CFT014
     C                     MOVEL'SRIDFT'  @STK,Q                          CFT014
      *                                                                   CFT014
     C           F1SNTP    IFNE 'F'                                       CFT014
     C           F1SNTP    ANDNE'R'                                       CFT014
     C           F1SNTP    ANDNE'G'                                       CFT014
     C           F1SNTP    ANDNE'C'                                       CFT014
     C           F1SNTP    ANDNE'S'                                       CFT014
     C                     GOTO EXIDFT                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Check for Support Charge Transparency flag for                   CFT014
      ** Sender Country Code.                                             CFT014
      *                                                                   CFT014
     C           F1SNTP    IFEQ 'F'                                       CFT014
     C           F1SNTP    OREQ 'R'                                       CFT014
     C           F1SNTP    OREQ 'G'                                       CFT014
      *                                                                   CFT014
      ** Retrieve Customer details of Sender.                             CFT014
      *                                                                   CFT014
      *                                                                   CFT014
      * Set occurrance structure                                          CFT014
      *                                                                   CFT014
     C           2         OCUR OACCNT                                    CFT014
     C                     MOVEL*BLANK    @KEY1                           CFT014
     C                     MOVE CNUM      @KEY1                           CFT014
     C                     CALL 'AOCUSTR0'                                CFT014
     C                     PARM *BLANKS   @RTCD                           CFT014
     C                     PARM '*KEY   ' @OPTN                           CFT014
     C                     PARM           @KEY1                           CFT014
     C                     PARM *BLANK    @KYST                           CFT014
     C           SDCUST    PARM SDCUST    DSSDY                           CFT014
      *                                                                   CFT014
     C           @RTCD     IFNE *BLANKS                                   CFT014
     C           @KYST     ORNE '*CNUM'                                   CFT014
     C                     Z-ADD058       W0ERNB                          CFT014
     C                     MOVELCNUM      W0KEY                           CFT014
     C                     MOVEL'SDCUSTPD'W0FILE                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     MOVE BBCSID    WSCSID                          CFT014
     C                     MOVE BBCOLC    WSCOLC                          CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** If WSCSID field is not blank, use the 5-6 positions              CFT014
      ** of the field to chain to MEISOCL1.                               CFT014
      *                                                                   CFT014
     C           WSCSID    IFNE *BLANKS                                   CFT014
     C                     MOVE WSCSID    W0112W                          CFT014
     C           W0506W    CHAINMEISOCL1             90                   CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ *ON                                       CFT014
     C           M3SCHT    ORNE 'Y'                                       CFT014
     C                     GOTO EXIDFT                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     ELSE                                           CFT014
      *                                                                   CFT014
     C           WSCOLC    IFNE *BLANKS                                   CFT014
      *                                                                   CFT014
      ** Retrieve Country Code details of Sender.                         CFT014
      *                                                                   CFT014
     C                     CALL 'AOCTRYR0'                                CFT014
     C                     PARM *BLANKS   @RTCD                           CFT014
     C                     PARM '*KEY   ' @OPTN                           CFT014
     C                     PARM WSCOLC    @CNCD   2                       CFT014
     C           SDCTRY    PARM SDCTRY    DSSDY                           CFT014
      *                                                                   CFT014
     C           @RTCD     IFNE *BLANKS                                   CFT014
     C                     Z-ADD059       W0ERNB                          CFT014
     C                     MOVELWSCOLC    W0KEY                           CFT014
     C                     MOVEL'SDCTRYPD'W0FILE                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Use A4ISOC from Country Code details to chain                    CFT014
      ** to MEISOCL1.                                                     CFT014
      *                                                                   CFT014
     C           A4ISOC    CHAINMEISOCL1             90                   CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ *ON                                       CFT014
     C           M3SCHT    ORNE 'Y'                                       CFT014
     C                     GOTO EXIDFT                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     ELSE                                           CFT014
      *                                                                   CFT014
     C                     GOTO EXIDFT                                    CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Check for Support Charge Transparency flag for                   CFT014
      ** Receiver Country Code.                                           CFT014
      *                                                                   CFT014
     C           WUMBIN    IFEQ 'Y'                                       CFT014
     C                     MOVE S1BRCA    WRBRCH  3                       CFT014
     C                     ELSE                                           CFT014
     C                     MOVE V#SBRC    WRBRCH                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Retrieve Branch details of Booking Branch or                     CFT014
      ** Single-Branch code.                                              CFT014
      *                                                                   CFT014
     C**********           CALL 'AOBRCHR0'                                CFT014              CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   @RTCD                           CFT014
     C                     PARM '*KEY   ' @OPTN                           CFT014
     C                     PARM WRBRCH    @BRCD                           CFT014
     C********** SDBRCH    PARM SDBRCH    DSFDY                           CFT014              CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *                                                                   CFT014
     C           @RTCD     IFNE *BLANKS                                   CFT014
     C                     Z-ADD060       W0ERNB                          CFT014
     C                     MOVELWRBRCH    W0KEY                           CFT014
     C                     MOVEL'SDBRCHPD'W0FILE                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** If A8BTID field is not blank, use the 5-6 positions              CFT014
      ** of the field from Branch details to chain to MEISOCL1.           CFT014
      *                                                                   CFT014
     C           A8BTID    IFNE *BLANKS                                   CFT014
     C                     MOVE A8BTID    W0112A                          CFT014
     C           W0506A    CHAINMEISOCL1             90                   CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ *ON                                       CFT014
     C           M3SCHT    ORNE 'Y'                                       CFT014
     C                     GOTO EXIDFT                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     ELSE                                           CFT014
      *                                                                   CFT014
     C           A8LCCD    IFNE *BLANKS                                   CFT014
      *                                                                   CFT014
      ** Retrieve Location Code details of Branch.                        CFT014
      *                                                                   CFT014
     C                     CALL 'AOLOCNR0'                                CFT014
     C                     PARM *BLANKS   @RTCD                           CFT014
     C                     PARM '*KEY   ' @OPTN                           CFT014
     C                     PARM A8LCCD    @LCCD   3                       CFT014
     C           SDLOCN    PARM SDLOCN    DSSDY                           CFT014
      *                                                                   CFT014
     C           @RTCD     IFNE *BLANKS                                   CFT014
     C                     Z-ADD061       W0ERNB                          CFT014
     C                     MOVELA8LCCD    W0KEY                           CFT014
     C                     MOVEL'SDLOCNPD'W0FILE                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Retrieve Country Code details.                                   CFT014
      *                                                                   CFT014
     C                     CALL 'AOCTRYR0'                                CFT014
     C                     PARM *BLANKS   @RTCD                           CFT014
     C                     PARM '*KEY   ' @OPTN                           CFT014
     C                     PARM DVCNCD    @CNCD                           CFT014
     C           SDCTRY    PARM SDCTRY    DSSDY                           CFT014
      *                                                                   CFT014
     C           @RTCD     IFNE *BLANKS                                   CFT014
     C                     Z-ADD062       W0ERNB                          CFT014
     C                     MOVELDVCNCD    W0KEY                           CFT014
     C                     MOVEL'SDCTRYPD'W0FILE                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Use A4ISOC from Country Code details to chain                    CFT014
      ** to MEISOCL1.                                                     CFT014
      *                                                                   CFT014
     C           A4ISOC    CHAINMEISOCL1             90                   CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ *ON                                       CFT014
     C           M3SCHT    ORNE 'Y'                                       CFT014
     C                     GOTO EXIDFT                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     ELSE                                           CFT014
      *                                                                   CFT014
      ** Retrieve Customer details of Branch Internal Customer.           CFT014
      *                                                                   CFT014
     C                     CALL 'AOCUSTR0'                                CFT014
     C                     PARM *BLANKS   @RTCD                           CFT014
     C                     PARM '*KEY   ' @OPTN                           CFT014
     C                     PARM A8BICN    @KEY1                           CFT014
     C                     PARM *BLANK    @KYST                           CFT014
     C           SDCUST    PARM SDCUST    DSSDY                           CFT014
      *                                                                   CFT014
     C           @RTCD     IFNE *BLANKS                                   CFT014
     C           @KYST     ORNE '*CNUM'                                   CFT014
     C                     Z-ADD063       W0ERNB                          CFT014
     C                     MOVELA8BICN    W0KEY                           CFT014
     C                     MOVEL'SDCUSTPD'W0FILE                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** If SWIFT Id is not blank, chain to MEISOCL1 using                CFT014
      ** positions 5 to 6 of the field.                                   CFT014
      *                                                                   CFT014
     C           BBCSID    IFNE *BLANKS                                   CFT014
     C                     MOVE BBCSID    W0112B                          CFT014
     C           W0506B    CHAINMEISOCL1             90                   CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ *ON                                       CFT014
     C           M3SCHT    ORNE 'Y'                                       CFT014
     C                     GOTO EXIDFT                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     ELSE                                           CFT014
      *                                                                   CFT014
      ** Retrieve Country Code details of Branch Internal                 CFT014
      ** Customer.                                                        CFT014
      *                                                                   CFT014
     C                     CALL 'AOCTRYR0'                                CFT014
     C                     PARM *BLANKS   @RTCD                           CFT014
     C                     PARM '*KEY   ' @OPTN                           CFT014
     C                     PARM BBCOLC    @CNCD                           CFT014
     C           SDCTRY    PARM SDCTRY    DSSDY                           CFT014
      *                                                                   CFT014
     C           @RTCD     IFNE *BLANKS                                   CFT014
     C                     Z-ADD064       W0ERNB                          CFT014
     C                     MOVELBBCOLC    W0KEY                           CFT014
     C                     MOVEL'SDCTRYPD'W0FILE                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Use A4ISOC from Country Code details to chain                    CFT014
      ** to MEISOCL1.                                                     CFT014
      *                                                                   CFT014
     C           A4ISOC    CHAINMEISOCL1             90                   CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ *ON                                       CFT014
     C           M3SCHT    ORNE 'Y'                                       CFT014
     C                     GOTO EXIDFT                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** At this point, Support Charge Transparency flag is 'Y'           CFT014
      ** for both Sender and Receiver.  Default Instructed Currency       CFT014
      ** and Amount to Settlement Currency and Amount.                    CFT014
      *                                                                   CFT014
     C                     MOVE S1SMCY    S1NDCY                          CFT014
      *                                                                   CFT014
      ** Save Settle Currency details to Instructed Currency              CFT014
      ** details.                                                         CFT014
      ** (Need to know the number of decimal positions when we will       CFT014
      ** default the rate.)                                               CFT014
      *                                                                   CFT014
     C                     MOVE SCINCY    @1INCY                          CFT014
     C                     MOVE SCTPSD    @1TPSD                          CFT014
     C                     Z-ADDSCNBDP    @1NBDP                          CFT014
     C                     Z-ADDSCSPOT    @1SPOT                          CFT014
     C                     MOVELSCMDIN    @1MDIN                          CFT014
      *                                                                   CFT014
     C                     Z-ADDF1SMAM    V#NDAM                          CFT014
      *                                                                   CFT014
      ** Format Instructed Amount for display.                            CFT014
      *                                                                   CFT014
     C                     MOVE *BLANK    ZFIELD                          CFT014
     C                     MOVE V#NDAM    ZFIELD                          CFT014
     C                     Z-ADD@1NBDP    ZADEC                           CFT014
      *                                                                   CFT014
     C                     CALL 'ZEDIT'                9090               CFT014
     C                     PARM           ZFIELD                          CFT014
     C                     PARM 8         ZADEC                           CFT014
      *                                                                   CFT014
      *  If error on call                                                 CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL'ZEDIT   'W0FILE           ***************CFT014
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 65 *CFT014
     C                     Z-ADD65        W0ERNB           ***************CFT014
     C                     MOVEL'MEM5003' W0MSGD                          CFT014
     C                     MOVEL'MIDAS  ' W0MSGF                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     END                                            CFT014
      *                                                                   CFT014
     C                     MOVE ZFIELD    S1NDAM                          CFT014
      *                                                                   CFT014
     C           EXIDFT    TAG                                            CFT014
     C                     MOVEA*BLANKS   @STK,Q                          CFT014
     C                     SUB  1         Q                               CFT014
      *                                                                   CFT014
     CSR                   ENDSR                                          CFT014
     C/EJECT                                                              CFT014
      *****************************************************************   CFT014
      *                                                               *   CFT014
      *  XCKSLD   : Cross-Validate Settlemente Date                   *   CFT014
      *                                                               *   CFT014
      *  CALLED BY:        - SRAMTV                                   *   CFT014
      *                                                               *   CFT014
      *  CALLS    :        -                                          *   CFT014
      *                                                               *   CFT014
      *****************************************************************   CFT014
     C           XCKSLD    BEGSR                                          CFT014
      *                                                                   CFT014
      *  Set up subroutine stack name                                     CFT014
      *                                                                   CFT014
     C                     ADD  1         Q                               CFT014
     C                     MOVEL'XCKSLD'  @STK,Q                          CFT014
      *                                                                   CFT014
     C                     MOVEL'Y'       WXERAM  1                       CFT014
      *                                                                   CFT014
      *Ensure that date is not before EMU transition start date if        CFT014
      *either Notified Delivery or settlement currency is Euro and        CFT014
      *the other is "In".                                                 CFT014
      *                                                                   CFT014
     C           CEU006    IFEQ 'Y'                                       CFT014
     C           @1INCY    IFEQ 'Y'                                       CFT014
     C           S1SMCY    ANDEQV#EURO                                    CFT014
     C           @1TPSD    ANDGTF1SLDT                                    CFT014
     C           SCINCY    OREQ 'Y'                                       CFT014
     C           S1NDCY    ANDEQV#EURO                                    CFT014
     C           SCTPSD    ANDGTF1SLDT                                    CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM2560' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1SLDT                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXXSLD                                    CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      *  At this point we assume that there are no errors                 CFT014
      *                                                                   CFT014
     C                     MOVEL'N'       WXERAM  1                       CFT014
      *                                                                   CFT014
      *  Unwind subroutine stack name                                     CFT014
      *                                                                   CFT014
     C           EXXSLD    TAG                                            CFT014
     C                     MOVEA*BLANKS   @STK,Q                          CFT014
     C                     SUB  1         Q       50                      CFT014
      *                                                                   CFT014
     C                     ENDSR                                          CFT014
      *                                                                   CFT014
     C/EJECT                                                              CFT014
      *****************************************************************
      *                                                               *
      *  SRDRAT   : Default Rate Calculation                          *
      *                                                               *
      *  CALLED BY: SRRATE - Validate Rate                            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     C           SRDRAT    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRDRAT'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352DRAT
      *
      * Calculate default rate
      *
     C           *LIKE     DEFN A6SPRT    DRATE
     C           *LIKE     DEFN A6MDIN    DMDIN
      *
      * If Currencies are equal then rate is 1 and flag Divide
      *
      *
     C           S1SMCY    IFEQ S1PCCY
     C                     Z-ADD1         DRATE
     C                     MOVEL'D'       DMDIN
     C                     GOTO EXDRAT
     C                     END
      *
      * If Settle currency is base then use Pay Currency Sell
      *
     C           S1SMCY    IFEQ V#BCCY
     C                     Z-ADDPCSELL    DRATE
      *
     C           PCMDIN    IFEQ 'M'
     C                     MOVEL'D'       DMDIN
     C                     ELSE
     C                     MOVEL'M'       DMDIN
     C                     END
      *
     C                     GOTO EXDRAT
     C                     END
      *
      * If Pay currency is base then use Settle Currency Buy
      *
     C           S1PCCY    IFEQ V#BCCY
     C                     Z-ADDSCBUY     DRATE
     C                     MOVELSCMDIN    DMDIN
     C                     GOTO EXDRAT
     C                     END
      *
      * Neither currencies are base or the same
      *
     C                     SELEC
      *
      * If both divide then
      *
     C           SCMDIN    WHEQ 'D'
     C           PCMDIN    ANDEQ'D'
      *
      * Rate is Pay Sell divide by Settle Buy
      *
     C           PCSELL    DIV  SCBUY     DRATE     H
     C                     MOVEL'M'       DMDIN
      *
     C           DRATE     IFLT 1
     C           SCBUY     DIV  PCSELL    DRATE     H
     C                     MOVEL'D'       DMDIN
     C                     END
      *
      * If Settle divide and Pay Multiply
      *
     C           SCMDIN    WHEQ 'D'
     C           PCMDIN    ANDEQ'M'
      *
      * Rate is Pay Sell multiplied by settle Buy
      *
     C           PCSELL    MULT SCBUY     DRATE     H
     C                     MOVEL'D'       DMDIN
      *
     C           DRATE     IFLT 1
     C           1         DIV  DRATE     DRATE     H
     C                     MOVEL'M'       DMDIN
     C                     END
      *
      * If Settle Multiply and Pay Divide
      *
     C           SCMDIN    WHEQ 'M'
     C           PCMDIN    ANDEQ'D'
      *
      * Rate is Pay Sell divide by Settle Buy
      *
     C           PCSELL    MULT SCBUY     DRATE     H
     C                     MOVEL'M'       DMDIN
      *
     C           DRATE     IFLT 1
     C           1         DIV  DRATE     DRATE     H
     C                     MOVEL'D'       DMDIN
     C                     END
      *
      * If both multiply then
      *
     C                     OTHER
      *
      * Rate is Pay Sell divide by Settle Buy
      *
     C           PCSELL    DIV  SCBUY     DRATE     H
     C                     MOVEL'D'       DMDIN
      *
     C           DRATE     IFLT 1
     C           SCBUY     DIV  PCSELL    DRATE     H
     C                     MOVEL'M'       DMDIN
     C                     END
      *
     C                     ENDSL
      *
      *  Unwind subroutine stack name
      *
     C           EXDRAT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q       50
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRRCAL   : Unentered Value Calculation                       *
      *                                                               *
      *  CALLED BY: SRRATE - Validate Rate                            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
      *
     C           SRRCAL    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRRCAL'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352RCAL
      *
      * If amounts entered on screen exit
      *
     C           S1SMAM    IFNE *BLANKS
     C           S1PYAM    ANDNE*BLANKS
     C                     GOTO EXRCAL
     C                     END
      *
      *
      * If Setlle amounts not entered
      *
     C           S1SMAM    IFEQ *BLANKS
      *
     C                     CLEARD#DTA
     C                     Z-ADDF1PYAM    D#FAMT
      *
     C           F1MDIN    IFEQ 'D'
     C                     MOVEL'M'       D#MDIN
     C                     ELSE
     C                     MOVEL'D'       D#MDIN
     C                     END
      *
     C                     Z-ADDF1RATE    D#RATE
     C                     MOVELS1SMCY    D#TCCY
     C                     MOVELS1PCCY    D#FCCY
      *
     C                     CALL 'AOCCYCV0'             9090
     C                     PARM *BLANKS   O#RTN   7
     C                     PARM '*F*T*RAT'W0ACT   8
     C           D#DTA     PARM D#DTA     O#DTA 256
      *
     C           *IN90     IFEQ '1'
     C           O#RTN     ORNE *BLANKS
     C                     MOVEL'AOCCYCV0'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 25 *
     C                     Z-ADD25        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C                     Z-ADDD#TAMT    F1SMAM
      *
      * Format amount for screen
      *
     C                     MOVE F1SMAM    ZFIELD
      *
     C                     CALL 'ZEDIT'                9090
     C                     PARM           ZFIELD           B:FUNCTION FIELD
     C                     PARM SCNBDP    ZADEC            I:NO OF DECIMALS
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'ZEDIT   'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 26 *
     C                     Z-ADD26        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C                     MOVE ZFIELD    S1SMAM
      *
     C                     ELSE
      *
     C                     CLEARD#DTA
     C                     Z-ADDF1SMAM    D#FAMT
     C                     MOVELS1SMCY    D#FCCY
     C                     MOVELS1PCCY    D#TCCY
     C                     MOVELF1MDIN    D#MDIN
     C                     Z-ADDF1RATE    D#RATE
      *
     C                     CALL 'AOCCYCV0'             9090
     C                     PARM *BLANKS   O#RTN   7
     C                     PARM '*F*T*RAT'W0ACT   8
     C           D#DTA     PARM D#DTA     O#DTA 256
      *
     C           *IN90     IFEQ '1'
     C           O#RTN     ORNE *BLANKS
     C                     MOVEL'AOCCYCV0'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 27 *
     C                     Z-ADD27        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C                     Z-ADDD#TAMT    F1PYAM
      *
      * Format amount for screen
      *
     C                     MOVE F1PYAM    ZFIELD
      *
     C                     CALL 'ZEDIT'                9090
     C                     PARM           ZFIELD           B:FUNCTION FIELD
     C                     PARM PCNBDP    ZADEC            I:NO OF DECIMALS
      *
      *  If error on call
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'ZEDIT   'W0FILE           ***************
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 28 *
     C                     Z-ADD28        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C                     MOVE ZFIELD    S1PYAM
      *
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXRCAL    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q       50
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRRVAL   : Validate Rate When Entered                        *
      *                                                               *
      *  CALLED BY: SRRATE - Validate Rate                            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     C           SRRVAL    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRRVAL'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352RVAL
      *
      * If rate not 1 and currencies are equal error
      *
     C           S1SMCY    IFEQ S1PCCY
     C           F1RATE    ANDNE1
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1RATE    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2126' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1RATE
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXRVAL
     C                     END
     C/COPY WNCPYSRC,FT0352C001
      *
      *  Upper and lower limits are determined by % rate discrepancy
      *      from payment charges file .
      *
     C           *LIKE     DEFN A6SPRT    UPRATE
     C           *LIKE     DEFN A6SPRT    DORATE
      *
     C           DRATE     MULT UPMARG    UPRATE    H
     C           DRATE     MULT LWMARG    DORATE    H
      *
     C           F1RATE    IFLT DORATE
     C           F1RATE    ORGT UPRATE
      *
      *  Entered rate may be Pay ccy/Receive ccy or vice versa
      *      (whichever is nearer the default rate(DRATE) is assumed)
      *
     C           1         DIV  DRATE     DRATE
     C           DRATE     MULT UPMARG    UPRATE
     C           DRATE     MULT LWMARG    DORATE
      *
     C           F1RATE    IFLT DORATE
     C           F1RATE    ORGT UPRATE
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1RATE    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2127' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1RATE
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXRVAL
     C                     END
      *
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXRVAL    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q       50
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SROVRD   : Validate Override Flag                            *
      *                                                               *
      *  CALLED BY: SRPROC - Control Process of Validation            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SROVRD    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SROVRD'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352OVRD
      *
      * Optional field defaults to N
      *
     C           S1OVRD    IFEQ *BLANK
     C                     MOVEL'N'       S1OVRD
     C                     END
      *
      * Must be N or Y
      *
     C           S1OVRD    IFNE 'N'
     C           S1OVRD    ANDNE'Y'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1OVRD    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2128' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1OVRD
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXOVRD
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXOVRD    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRADDC   : Validate Add/Deduct Charges Indicator             *
      *                                                               *
      *  CALLED BY: SRPROC - Control Process of Validation            *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRADDC    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRADDC'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352ADDC
      *
      *  Optional field defaults to 'A'
      *
     C           S1ADDC    IFEQ *BLANKS
     C                     MOVEL'A'       S1ADDC
     C                     END
      *
      *  Must be A or D
      *
     C           S1ADDC    IFNE 'A'
     C           S1ADDC    ANDNE'D'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1ADDC    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2129' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1ADDC
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXADDC
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXADDC    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************   CFT014
      *                                                               *   CFT014
      *  SRRCCN   : Validate Receiver's charges ccy from the network  *   CFT014
      *                                                               *   CFT014
      *  CALLED BY: SRPROC - Control Process of Validation            *   CFT014
      *                                                               *   CFT014
      *  CALLS    :        -                                          *   CFT014
      *                                                               *   CFT014
      *****************************************************************   CFT014
     CSR         SRRCCN    BEGSR                                          CFT014
      *                                                                   CFT014
      *  Set up subroutine stack name                                     CFT014
      *                                                                   CFT014
     C                     ADD  1         Q       50                      CFT014
     C                     MOVEL'SRRCCN'  @STK,Q                          CFT014
      *                                                                   CFT014
      *  No need to validate it if empty                                  CFT014
      *                                                                   CFT014
     C           S1RCCN    IFEQ *BLANK                                    CFT014
     C                     GOTO EXRCCN                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      *  No need to validate it if it is the same as the                  CFT014
      *  payment currency or settlement currency                          CFT014
      *                                                                   CFT014
     C           S1RCCN    IFEQ S1PCCY                                    CFT014
     C                     Z-ADDPCNBDP    RCNBDP                          CFT014
     C                     GOTO EXRCCN                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C           S1RCCN    IFEQ S1SMCY                                    CFT014
     C                     Z-ADDSCNBDP    RCNBDP                          CFT014
     C                     GOTO EXRCCN                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      *  Look field up on database                                        CFT014
      *                                                                   CFT014
     C                     CALL 'AOCURRR0'             9090               CFT014
     C                     PARM *BLANKS   RTCD                            CFT014
     C                     PARM '*KEY   ' @OPTN                           CFT014
     C                     PARM S1RCCN    @K101   3                       CFT014
     C           SDCURR    PARM *BLANKS   DSSDY                           CFT014
      *                                                                   CFT014
      *  If error on call                                                 CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL'AOCURRR0'W0FILE           ***************CFT014
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 71 *CFT014
     C                     Z-ADD71        W0ERNB           ***************CFT014
     C                     MOVEL'MEM5003' W0MSGD                          CFT014
     C                     MOVEL'MIDAS  ' W0MSGF                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     END                                            CFT014
      *                                                                   CFT014
     C           RTCD      IFNE *BLANKS                                   CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELS1RCCN    FTMSDT                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM4004' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1RCCN                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXRCCN                                    CFT014
     C                     END                                            CFT014
      *                                                                   CFT014
      *  Move new standing data field into field used in program          CFT014
      *                                                                   CFT014
     C           *LIKE     DEFN A6NBDP    RCNBDP                          CFT014
     C                     Z-ADDA6NBDP    RCNBDP                          CFT014
      *                                                                   CFT014
      *  Unwind subroutine stack name                                     CFT014
      *                                                                   CFT014
     C           EXRCCN    TAG                                            CFT014
     C                     MOVEA*BLANKS   @STK,Q                          CFT014
     C                     SUB  1         Q                               CFT014
      *                                                                   CFT014
     CSR                   ENDSR                                          CFT014
     C/EJECT                                                              CFT014
      *****************************************************************   CFT014
      *                                                               *   CFT014
      *  SRRCNW   : Validate Receiver's charges  from the network     *   CFT014
      *                                                               *   CFT014
      *  CALLED BY: SRPROC - Control Process of Validation            *   CFT014
      *                                                               *   CFT014
      *  CALLS    :        -                                          *   CFT014
      *                                                               *   CFT014
      *****************************************************************   CFT014
     CSR         SRRCNW    BEGSR                                          CFT014
      *                                                                   CFT014
      *  Set up subroutine stack name                                     CFT014
      *                                                                   CFT014
     C                     ADD  1         Q       50                      CFT014
     C                     MOVEL'SRRCNW'  @STK,Q                          CFT014
      *                                                                   CFT014
      **  No need to validate it if empty                                 CFT014
      *                                                                   CFT014
     C           S1RCNW    IFEQ *BLANK                                    CFT014
     C                     GOTO EXRCNW                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Amount need currency to be formated                              CFT014
      *                                                                   CFT014
     C           S1RCNW    IFNE *BLANK                                    CFT014
     C           S1RCCN    ANDEQ*BLANK                                    CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM4005' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1RCNW                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXRCNW                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C           V#DETC    IFEQ 'BEN'                                     CFT014
     C           V#DETC    OREQ 'SHA'                                     CFT014
     C           V#DETC    OREQ 'OUR'                                     CFT014
     C           S1ADDC    ANDNE'A'                                       CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3231' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1RCNW                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXRCNW                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C           11        SUB  RCNBDP    ZADIG                           CFT014
     C                     Z-ADDRCNBDP    ZADEC                           CFT014
     C                     MOVEL*BLANKS   ZFIELD                          CFT014
     C                     MOVELS1RCNW    ZFIELD                          CFT014
     C                     EXSR SRALGN                                    CFT014
      *                                                                   CFT014
     C           ZRTN      IFEQ '*ERROR*'                                 CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C           S1RCCN    CAT  S1RCNW    FTMSDT    P                     CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3232' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1RCNW                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXRCNW                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     MOVE ZFIELD    F1RCNW                          CFT014
      *                                                                   CFT014
      *  Unwind subroutine stack name                                     CFT014
      *                                                                   CFT014
     C           EXRCNW    TAG                                            CFT014
     C                     MOVEA*BLANKS   @STK,Q                          CFT014
     C                     SUB  1         Q                               CFT014
      *                                                                   CFT014
     CSR                   ENDSR                                          CFT014
     C/EJECT                                                              CFT014
      *****************************************************************
      *                                                               *
      *  SRBOOK   : Validate Book Code                                *
      *                                                               *
      *  CALLED BY: SRPROC - Control of Validation Processing         *
      *                                                               *
      *  CALLS    : AOBOOKR0                                          *
      *             FTSNMS                                            *
      *                                                               *
      *****************************************************************
     CSR         SRBOOK    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRBOOK'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352BOOK
      *
      *  Book Code must be entered
      *
     C           S1BOKC    IFEQ *BLANKS
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1BOKC    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2130' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BOKC
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXBOOK
     C                     END
      *
      *  Access the Book Code file for validation if an Insert OR
      *  if an Amend and the Book Code has been changed
      *
     C                     CALL 'AOBOOKR0'             9090
     C                     PARM *BLANKS   U#RTCD  7
     C                     PARM '*KEY   ' U#OPTN  7
     C                     PARM S1BOKC    U#BOKC  2
     C           SDBOOK    PARM *BLANKS   DSFDY
      *
      *  If error on CALL - process as database error
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AOBOOKR0'W0FILE           ***************
     C                     MOVEL'*CALL  ' W0KEY            * DB ERROR 29 *
     C                     Z-ADD29        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      * If return code is not blank - process for an error
      * else map the book code from the data structure to screen field
      *
     C           U#RTCD    IFNE *BLANKS
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1BOKC    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2131' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BOKC
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXBOOK
     C                     END
      *
     C                     MOVELBDBKCD    S1BOKC
      *
      *  Unwind subroutine stack name
      *
     C           EXBOOK    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q       50
      *
     CSR                   ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRPRFC   : Validation Of Profit Centre                       *
      *                                                               *
      *  CALLED BY: SRPROC - Control of Validation Processing         *
      *                                                               *
      *  CALLS    : FTSNMS                                            *
      *             AOPRFMR0                                          *
      *             AOPRFCR0                                          *
      *                                                               *
      *****************************************************************
     CSR         SRPRFC    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRPRFC'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352PRFC
      *
      *  Profit Centre is Optional input.
      *
      *  If the profit centre field is blank set to a default only if
      *  the Profit Centre Defaults Used flag is set to 'Y'.
      *  This could only happen on an Insert.
      *
     C           S1ACTD    IFEQ 'I'
     C           S1PRFC    ANDEQ*BLANKS
     C           V#PCDU    ANDEQ'Y'
      *
      *  Determine parameters to be passed
      *
      *  For Outgoing Payments the Customer and Account Officer will
      *  be blank.
      *
      *
     C                     MOVEL*BLANKS   CUST    6        Customer
     C                     MOVEL*BLANKS   ACOF    2        Account Officer
     C                     MOVELS1PYTP    TRTP    5        Transaction Typ
      *
      *  Get default profit centre
      *
     C                     CALL 'AOPRFMR0'             90
     C                     PARM *BLANKS   U#RTCD  7        Return Code
     C                     PARM S1BOKC    U#BOKC  2        Book Code
     C                     PARM TRTP      U#TRTP  5        Transaction Typ
     C                     PARM S1PYST    U#SUTP  2        Sub Type
     C                     PARM S1BRCA    U#BRAN  3        Branch Code
     C                     PARM DPPT      U#DEPT  3        Department Code
     C                     PARM ##USR     U#USER 10        User
     C                     PARM ACOF      U#ACOF  2        Account Officer
     C                     PARM CUST      U#CUST  6        Customer Number
     C           S1PRFC    PARM *BLANKS   U#PRFC  4        Profit Centre
      *
      *  If error on CALL - process as database error
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AOPRFMR0'W0FILE           ***************
     C                     MOVEL'*CALL  ' W0KEY            * DB ERROR 30 *
     C                     Z-ADD30        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      * If return code is not blank - process for an error
      * If no error then the screen field is filled by passed parameter
      *
     C           U#RTCD    IFEQ '2'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1PRFC    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2132' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1PRFC
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXPRFC
     C                     END
      *
     C                     END
      *
      *  Validate Profit Centre
      *
     C           S1ACTD    IFEQ 'I'
     C           S1PRFC    ANDNE*BLANKS
     C           S1ACTD    OREQ 'A'
     C           S1PRFC    ANDNE*BLANKS
      *
     C                     CALL 'AOPRFCR0'             90
     C                     PARM *BLANKS   U#RTCD  7
     C                     PARM '*KEY'    U#OPTN  7
     C                     PARM S1PRFC    U#PRFC  4
     C           SDPRFC    PARM *BLANKS   DSFDY
      *
      *  If error on CALL - process as database error
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'AOPRFCR0'W0FILE           ***************
     C                     MOVEL'*CALL  ' W0KEY            * DB ERROR 31 *
     C                     Z-ADD31        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      * If return code is not blank - process for an error
      *
     C           U#RTCD    IFNE *BLANKS
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1PRFC    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2133' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1PRFC
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXPRFC
     C                     END
      *
     C                     MOVELDSPCCD    S1PRFC
      *
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXPRFC    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q       50
      *
     CSR                   ENDSR
      *                                                                   CEU006
     C/EJECT                                                              CEU006
      *****************************************************************   CEU006
      *                                                               *   CEU006
      *  SRNDCY   : Notified delivery currency                        *   CEU006
      *                                                               *   CEU006
      *  CALLED BY:        - SRPROC                                   *   CEU006
      *                                                               *   CEU006
      *  CALLS    :        -                                          *   CEU006
      *                                                               *   CEU006
      *****************************************************************   CEU006
     C           SRNDCY    BEGSR                                          CEU006
      *                                                                   CEU006
      *  Set up subroutine stack name                                     CEU006
      *                                                                   CEU006
     C                     ADD  1         Q                               CEU006
     C                     MOVEL'SRNDCY'  @STK,Q                          CEU006
      *                                                                   CEU006
     C           S1NDCY    IFNE *BLANKS                                   CEU006
      *                                                                   CFT014
     C           CFT014    IFEQ 'N'                                       CFT014
     C           SCINCY    IFNE 'Y'                                       CFT014
     C           S1SMCY    ANDNEV#EURO                                    CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3150' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1NDCY                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXNDCY                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      *  Look field up on database                                        CEU006
      *                                                                   CEU006
     C                     CALL 'AOCURRR0'             9090               CEU006
     C                     PARM *BLANKS   RTCD                            CEU006
     C                     PARM '*KEY   ' @OPTN                           CEU006
     C                     PARM S1NDCY    @K101   3                       CEU006
     C           SDCURR    PARM *BLANKS   DSSDY                           CEU006
      *                                                                   CEU006
      *  If error on call                                                 CEU006
      *                                                                   CEU006
     C           *IN90     IFEQ '1'                                       CEU006
     C                     MOVEL'AOCURRR0'W0FILE           ***************CEU006
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 43 *CEU006
     C                     Z-ADD43        W0ERNB           ***************CEU006
     C                     MOVEL'MEM5003' W0MSGD                          CEU006
     C                     MOVEL'MIDAS  ' W0MSGF                          CEU006
     C                     EXSR SRERR                                     CEU006
     C                     ENDIF                                          CEU006
      *                                                                   CEU006
     C           RTCD      IFNE *BLANKS                                   CEU006
     C                     MOVEL'Y'       E#MSGS                          CEU006
     C                     MOVELS1NDCY    FTMSDT                          CEU006
     C                     MOVELE#CPGM    FTPGMQ                          CEU006
     C                     MOVEL'FTM2601' FTMSID                          CEU006
     C                     EXSR FTSNMS                                    CEU006
     C                     MOVEL'Y'       I1NDCY                          CEU006
     C                     MOVEL'Y'       E#NOUP                          CEU006
     C                     GOTO EXNDCY                                    CEU006
     C                     ENDIF                                          CEU006
      *                                                                   CFT014
     C           A6INCY    IFNE 'Y'                                       CFT014
     C           A6CYCD    ANDNEV#EURO                                    CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3151' FTMSID                          CFT014
     C                     MOVEL'FTUSRMSG'FTMSGF                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1NDCY                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXNDCY                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C           S1SMCY    IFEQ V#EURO                                    CFT014
     C           A6INCY    ANDNE'Y'                                       CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3152' FTMSID                          CFT014
     C                     MOVEL'FTUSRMSG'FTMSGF                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1NDCY                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXNDCY                                    CFT014
     C                     ELSE                                           CFT014
      *                                                                   CFT014
     C           SCINCY    IFEQ 'Y'                                       CFT014
     C           S1NDCY    ANDNEV#EURO                                    CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3153' FTMSID                          CFT014
     C                     MOVEL'FTUSRMSG'FTMSGF                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1NDCY                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXNDCY                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      ** End of CFT014 = 'N'                                              CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Validate Instructed Currency if switchable feature               CFT014
      ** CFT014 is installed.                                             CFT014
      *                                                                   CFT014
     C           CFT014    IFEQ 'Y'                                       CFT014
      *                                                                   CFT014
      *  Look field up on database                                        CFT014
      *                                                                   CFT014
     C                     CALL 'AOCURRR0'             9090               CFT014
     C                     PARM *BLANKS   RTCD                            CFT014
     C                     PARM '*KEY   ' @OPTN                           CFT014
     C                     PARM S1NDCY    @K101   3                       CFT014
     C           SDCURR    PARM *BLANKS   DSSDY                           CFT014
      *                                                                   CFT014
      *  If error on call                                                 CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL'AOCURRR0'W0FILE           ***************CFT014
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 52 *CFT014
     C                     Z-ADD52        W0ERNB           ***************CFT014
     C                     MOVEL'MEM5003' W0MSGD                          CFT014
     C                     MOVEL'MIDAS  ' W0MSGF                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C           RTCD      IFNE *BLANKS                                   CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELS1NDCY    FTMSDT                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM2601' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1NDCY                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXNDCY                                    CFT014
     C                     ENDIF                                          CFT014
      ** End of CFT014 is ON                                              CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CEU006
     C                     MOVE S1NDCY    V#NDCY                          CEU006
      *                                                                   CEU006
      *                                                                   CEU006
      *  Move new standing data field into field used in program          CEU006
      *                                                                   CEU006
     C           *LIKE     DEFN A6NBDP    @1NBDP                          CEU006
     C           *LIKE     DEFN A6SPRT    @1SPOT                          CEU006
     C           *LIKE     DEFN A6MDIN    @1MDIN                          CEU006
     C           *LIKE     DEFN A6SPRT    @1BUY                           CEU006
     C           *LIKE     DEFN A6SPRT    @1SELL                          CEU006
     C           *LIKE     DEFN A6INCY    @1INCY                          CFT014
     C           *LIKE     DEFN A6TPSD    @1TPSD                          CFT014
      *                                                                   CEU006
      * Store Decimal Places Spot Rate Multiply/Divide Ind.               CEU006
      *                                                                   CEU006
     C                     Z-ADDA6NBDP    @1NBDP                          CEU006
     C                     Z-ADDA6SPRT    @1SPOT                          CEU006
     C                     MOVELA6MDIN    @1MDIN                          CEU006
     C                     MOVELA6INCY    @1INCY                          CFT014
     C                     Z-ADDA6TPSD    @1TPSD                          CFT014
      *                                                                   CEU006
     C           A6BYSS    IFEQ '+'                                       CEU006
     C           A6SPRT    ADD  A6BYSR    @1BUY                           CEU006
     C                     ELSE                                           CEU006
     C           A6SPRT    SUB  A6BYSR    @1BUY                           CEU006
     C                     ENDIF                                          CEU006
      *                                                                   CEU006
     C           A6SLSS    IFEQ '+'                                       CEU006
     C           A6SPRT    ADD  A6SLSR    @1SELL                          CEU006
     C                     ELSE                                           CEU006
     C           A6SPRT    SUB  A6SLSR    @1SELL                          CEU006
     C                     ENDIF                                          CEU006
      *                                                                   CEU006
     C                     ENDIF                                          CEU006
      *                                                                   CEU006
      *  Unwind subroutine stack name                                     CEU006
      *                                                                   CEU006
     C           EXNDCY    TAG                                            CEU006
     C                     MOVEA*BLANKS   @STK,Q                          CEU006
     C                     SUB  1         Q       50                      CEU006
      *                                                                   CEU006
     C                     ENDSR                                          CEU006
      *                                                                   CEU006
     C/EJECT                                                              CEU006
      *****************************************************************   CEU006
      *                                                               *   CEU006
      *  SRNDAM   : Notified delivery amount                          *   CEU006
      *                                                               *   CEU006
      *  CALLED BY:        - SRPROC                                   *   CEU006
      *                                                               *   CEU006
      *  CALLS    :        -                                          *   CEU006
      *                                                               *   CEU006
      *****************************************************************   CEU006
     C           SRNDAM    BEGSR                                          CEU006
      *                                                                   CEU006
      *  Set up subroutine stack name                                     CEU006
      *                                                                   CEU006
     C                     ADD  1         Q                               CEU006
     C                     MOVEL'SRNDAM'  @STK,Q                          CEU006
      *                                                                   CEU006
      **Mandatory*if*Notified*delivery*currency*entered             CEU006CFT014
      *************************************************             CEU006CFT014
     C***********S1NDCY    IFNE *BLANK                              CEU006CFT014
     C***********S1NDAM    ANDEQ*BLANK                              CEU006CFT014
      *                                                                   CFT014
      * Forbidden if INyment delivery currency not entered                CFT014
      *                                                                   CFT014
     C           S1NDCY    IFEQ *BLANK                                    CFT014
     C           S1NDAM    ANDNE*BLANK                                    CFT014
     C                     MOVEL'Y'       E#MSGS                          CEU006
     C                     MOVELS1NDAM    FTMSDT                          CEU006
     C                     MOVELE#CPGM    FTPGMQ                          CEU006
     C*********************MOVEL'FTM2602' FTMSID                    CEU006CFT014
     C                     MOVEL'FTM2592' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CEU006
     C                     MOVEL'Y'       I1NDAM                          CEU006
     C                     MOVEL'Y'       E#NOUP                          CEU006
     C                     GOTO EXNDAM                                    CEU006
     C                     ENDIF                                          CEU006
      *                                                                   CEU006
      * If entered Validate                                               CEU006
      *                                                                   CEU006
     C                     MOVEL*BLANK    ZFIELD                          CEU006
     C                     MOVE S1NDAM    ZFIELD                          CEU006
     C           13        SUB  @1NBDP    ZADIG                           CEU006
     C                     Z-ADD@1NBDP    ZADEC                           CEU006
      *                                                                   CEU006
     C                     EXSR SRALGN                                    CEU006
      *                                                                   CEU006
     C           ZRTN      IFNE *BLANKS                                   CEU006
     C                     MOVEL'Y'       E#MSGS                          CEU006
     C                     MOVELS1NDAM    FTMSDT                          CEU006
     C                     MOVELE#CPGM    FTPGMQ                          CEU006
     C                     MOVEL'FTM2603' FTMSID                          CEU006
     C                     EXSR FTSNMS                                    CEU006
     C                     MOVEL'Y'       I1NDAM                          CEU006
     C                     MOVEL'Y'       E#NOUP                          CEU006
     C                     GOTO EXNDAM                                    CEU006
     C                     ENDIF                                          CEU006
      *                                                                   CEU006
      ** Remove duplicate variable                                        CFT014
     C*********************MOVE ZFIELD    WNDAM  130                CEU006CFT014
     C*********************Z-ADDWNDAM     V#NDAM                    CEU006CFT014
     C                     MOVE ZFIELD    V#NDAM                          CFT014
      *                                                                   CEU006
      *  Unwind subroutine stack name                                     CEU006
      *                                                                   CEU006
     C           EXNDAM    TAG                                            CEU006
     C                     MOVEA*BLANKS   @STK,Q                          CEU006
     C                     SUB  1         Q       50                      CEU006
      *                                                                   CEU006
     C                     ENDSR                                          CEU006
      *                                                                   CEU006
     C/EJECT                                                              CEU006
      *****************************************************************   CEU006
      *                                                               *   CEU006
      *  SRAMTV   : Cross-Validate All Amounts                        *   CEU006
      *                                                               *   CEU006
      *  CALLED BY:        - SRPROC                                   *   CEU006
      *                                                               *   CEU006
      *  CALLS    :        -                                          *   CEU006
      *                                                               *   CEU006
      *****************************************************************   CEU006
     C           SRAMTV    BEGSR                                          CEU006
      *                                                                   CEU006
      *  Set up subroutine stack name                                     CEU006
      *                                                                   CEU006
     C                     ADD  1         Q                               CEU006
     C                     MOVEL'SRAMTV'  @STK,Q                          CEU006
      *                                                                   CFT014
      ** Cross check pay value date                                       CFT014
      *                                                                   CFT014
     C                     EXSR XCKSLD                                    CFT014
     C           WXERAM    IFEQ 'Y'                                       CFT014
     C                     GOTO EXAMTV                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CEU006
      ** Set paramaters for call to FT0010                                CEU006
      *                                                                   CEU006
     C                     CLEARP#0010                                    CEU006
      *                                                                   CEU006
     C                     MOVEL'FT0010'  P#PMSQ                          CEU006
      *                                                                   CEU006
      ** Its possible to have a difference between Instructed             CFT014
      ** amount and Transaction amount that the Original Exchange rate    CFT014
      ** cannot justify.                                                  CFT014
     C           CFT014    IFEQ 'N'                                       CFT014
     C                     MOVE S1NDCY    P#NDCY                          CEU006
     C*********************Z-ADDWNDAM     P#NDAM                    CEU006CFT014
     C                     Z-ADDV#NDAM    P#NDAM                          CFT014
     C                     ENDIF                                          CFT014
     C*********************Z-ADD0         WNDAM                     CFT002CFT014
     C                     MOVE S1SMCY    P#DBCY                          CEU006
     C                     Z-ADDF1SMAM    P#DBAM                          CEU006
     C                     MOVE S1PCCY    P#CRCY                          CEU006
     C                     Z-ADDF1PYAM    P#CRAM                          CEU006
     C                     Z-ADDF1SLDT    P#DBDT                          CEU006
     C*********************MOVE BTPCRD    P#RTDS                    166214175484
     C           BTPCRD    MULT 0.01      P#RTDS                          175484
     C                     MOVEL'Y'       P#SDIN                          166214
     C                     Z-ADDF1PVDT    P#CRDT                          166214
      *                                                                   CGL009
     C           V#NWRK    IFEQ 'FONTIS'                                  CGL009
      ** FONTIS can transmit another rate by using the code work          CGL009
      ** /RATE/ in tag :72:                                               CGL009
     C           I1RATE    IFEQ *BLANK                                    CGL009
     C           WFRATE    ANDEQ'Y'                                       CGL009
     C                     Z-ADDF1RATE    P#RATE                          CGL009
     C                     ENDIF                                          CGL009
     C                     ENDIF                                          CGL009
      *                                                                   CEU006
     C                     CALL 'FT0010'                                  CEU006
     C                     PARM           P#0010                          CEU006
      *                                                                   CEU006
     C           P#RTCD    IFNE *BLANK                                    CEU006
     C*********************MOVEL'AOCURRR0'W0FILE                    CEU006CFT014
     C                     MOVEL'FT0010'  W0FILE                          CFT014
     C                     MOVEL'*CALL'   W0KEY                           CEU006
     C                     Z-ADD44        W0ERNB                          CEU006
     C                     EXSR SRERR                                     CEU006
     C                     ENDIF                                          CEU006
      *                                                                   CEU006
      * Check for returned error                                          CEU006
     C           '1'       SCAN P#INDC:1  RESULT  10                      CEU006
      *                                                                   CEU006
     C           RESULT    IFGT 0                                         CEU006
     C                     MOVE *ON       *IN89                           CEU006
     C                     ELSE                                           CGL009
     C                     MOVE *OFF      *IN89                           CGL009
     C                     ENDIF                                          CEU006
      *                                                                   CEU006
     C           *IN89     IFEQ *ON                                       CEU006
     C                     MOVE 'Y'       I1NDCY                          CEU006
     C                     MOVE 'Y'       I1NDAM                          CEU006
     C                     MOVE 'Y'       I1SMCY                          CEU006
     C                     MOVE 'Y'       I1SMAM                          CEU006
     C                     MOVE 'Y'       I1PCCY                          CEU006
     C                     MOVE 'Y'       I1PYAM                          CEU006
     C                     MOVE 'Y'       I1RATE                          166199
      *                                                                   166199
     C                     MOVELP#INDC    DSINDC                          166199
      ** Rate incorrect                                                   166199
     C           WINDC5    IFEQ '1'                                       166199
     C                     MOVEL'FTM2611' FTMSID                          166199
     C                     MOVELS1RATE    FTMSDT                          166199
     C                     ELSE                                           166199
     C                     MOVEL'FTM2604' FTMSID                          166199
     C                     ENDIF                                          166199
      *                                                                   166199
     C                     MOVEL'Y'       E#MSGS                          CEU006
     C                     MOVELE#CPGM    FTPGMQ                          CEU006
     C*********************MOVEL'FTM2604' FTMSID                    CEU006166199
     C                     EXSR FTSNMS                                    CEU006
     C                     MOVEL'Y'       E#NOUP                          CEU006
      *                                                                   CGL009
     C                     ELSE                                           CGL009
      *                                                                   CGL009
     C           F1SMAM    IFEQ 0                                         CGL009
     C                     MOVE P#DBAM    F1SMAM                          CGL009
     C                     MOVE *BLANK    ZFIELD                          CGL009
     C                     MOVE P#DBAM    ZFIELD                          CGL009
      *                                                                   CGL009
     C                     CALL 'ZEDIT'                9090               CGL009
     C                     PARM           ZFIELD                          CGL009
     C                     PARM SCNBDP    ZADEC                           CGL009
      *                                                                   CGL009
      *  If error on call                                                 CGL009
      *                                                                   CGL009
     C           *IN90     IFEQ '1'                                       CGL009
     C                     MOVEL'ZEDIT   'W0FILE           ***************CGL009
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 46 *CGL009
     C                     Z-ADD46        W0ERNB           ***************CGL009
     C                     MOVEL'MEM5003' W0MSGD                          CGL009
     C                     MOVEL'MIDAS  ' W0MSGF                          CGL009
     C                     EXSR SRERR                                     CGL009
     C                     END                                            CGL009
      *                                                                   CGL009
     C                     MOVE ZFIELD    S1SMAM                          CGL009
     C                     ENDIF                                          CGL009
      *                                                                   CGL009
     C           F1PYAM    IFEQ 0                                         CGL009
     C                     MOVE P#CRAM    F1PYAM                          CGL009
     C                     MOVE *BLANK    ZFIELD                          CGL009
     C                     MOVE P#CRAM    ZFIELD                          CGL009
      *                                                                   CGL009
     C                     CALL 'ZEDIT'                9090               CGL009
     C                     PARM           ZFIELD                          CGL009
     C                     PARM PCNBDP    ZADEC                           CGL009
      *                                                                   CGL009
      *  If error on call                                                 CGL009
      *                                                                   CGL009
     C           *IN90     IFEQ '1'                                       CGL009
     C                     MOVEL'ZEDIT   'W0FILE           ***************CGL009
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 45 *CGL009
     C                     Z-ADD45        W0ERNB           ***************CGL009
     C                     MOVEL'MEM5003' W0MSGD                          CGL009
     C                     MOVEL'MIDAS  ' W0MSGF                          CGL009
     C                     EXSR SRERR                                     CGL009
     C                     END                                            CGL009
      *                                                                   CGL009
     C                     MOVE ZFIELD    S1PYAM                          CGL009
     C                     ENDIF                                          CGL009
      *                                                                   CGL009
     C           CFT014    IFEQ 'N'                                       CFT014
     C           S1NDCY    IFNE *BLANK                                    CFT014
     C           V#NDAM    ANDEQ0                                         CFT014
     C                     MOVE *BLANK    ZFIELD                          CFT014
     C                     MOVE P#NDAM    ZFIELD                          CFT014
      *                                                                   CFT014
     C                     CALL 'ZEDIT'                9090               CFT014
     C                     PARM           ZFIELD                          CFT014
     C                     PARM @1NBDP    ZADEC                           CFT014
      *                                                                   CFT014
      *  If error on call                                                 CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL'ZEDIT   'W0FILE           ***************CFT014
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 55 *CFT014
     C                     Z-ADD55        W0ERNB           ***************CFT014
     C                     MOVEL'MEM5003' W0MSGD                          CFT014
     C                     MOVEL'MIDAS  ' W0MSGF                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     END                                            CFT014
      *                                                                   CFT014
     C                     MOVE ZFIELD    S1NDAM                          CFT014
     C                     Z-ADDP#PDAM    V#NDAM                          CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CEU006
     C           F1RATE    IFEQ 0                                         166199
     C                     MOVE P#RATE    F1RATE                          166199
     C                     MOVE *BLANK    ZFIELD                          166199
     C                     MOVE P#RATE    ZFIELD                          166199
      *                                                                   166199
     C                     CALL 'ZEDIT'                9090               166199
     C                     PARM           ZFIELD                          166199
     C                     PARM 8         ZADEC                           166199
      *                                                                   166199
      *  If error on call                                                 166199
      *                                                                   166199
     C           *IN90     IFEQ '1'                                       166199
     C                     MOVEL'ZEDIT   'W0FILE           ***************166199
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 48 *166199
     C                     Z-ADD48        W0ERNB           ***************166199
     C                     MOVEL'MEM5003' W0MSGD                          166199
     C                     MOVEL'MIDAS  ' W0MSGF                          166199
     C                     EXSR SRERR                                     166199
     C                     END                                            166199
      *                                                                   166199
     C                     MOVE ZFIELD    S1RATE                          166199
     C                     ENDIF                                          166199
      *                                                                   166199
     C           F1MDIN    IFEQ *BLANK                                    166199
     C           P#MDIN    IFEQ 1                                         166199
     C                     MOVE 'M'       F1MDIN                          166199
     C                     ELSE                                           166199
     C                     MOVE 'D'       F1MDIN                          166199
     C                     ENDIF                                          166199
     C                     ENDIF                                          166199
      *                                                                   166199
      ** Default Instructed Amount                                        CFT014
      *                                                                   CFT014
     C           CFT014    IFEQ 'Y'                                       CFT014
      *                                                                   CFT014
      *                                                                   CFT014
      ** FT0010 was designed to default all the other amounts when        CFT014
      ** at least one currency, one amount have been entered.             CFT014
      ** Except one case: it can calculate the notified dlv amount        CFT014
      ** only when the notified dlv currency is an IN currency.           CFT014
      ** Now the notified dlv currency (renamed instructed currency)      CFT014
      ** can be an OUT currency. Therefore to achieve the previous        CFT014
      ** functionality we are obliged to call FT0010 once again.          CFT014
      ** Setup FT0010 parameters to calculate instructed amount when      CFT014
      ** currency is entered while the amount is not.                     CFT014
      *                                                                   CFT014
     C           S1NDCY    IFNE *BLANK                                    CFT014
     C           S1NDAM    ANDEQ*BLANK                                    CFT014
     C           S1NDCY    ORNE *BLANK                                    CFT014
     C           V#NDAM    ANDEQ*ZERO                                     CFT014
     C                     CLEARP#0010                                    CFT014
     C                     MOVE S1PCCY    P#FRCY                          CFT014
     C                     MOVE F1PVDT    P#FRDT                          CFT014
     C                     Z-ADDF1PYAM    P#FRAM                          CFT014
     C                     MOVE S1NDCY    P#TOCY                          CFT014
     C                     MOVE F1PVDT    P#TODT                          CFT014
     C           BTPCRD    MULT 0.01      P#RTDS                          CFT014
     C                     MOVEL'Y'       P#SDIN                          CFT014
      *                                                                   CFT014
      ** Calculate Instructed amount via FT0010                           CFT014
      *                                                                   CFT014
     C                     CALL 'FT0010'                                  CFT014
     C                     PARM           P#0010                          CFT014
      *                                                                   CFT014
      ** Error calling FT0010                                             CFT014
      *                                                                   CFT014
     C           P#RTCD    IFEQ '*ERROR*'                                 CFT014
     C                     MOVEL'FT0010  'W0FILE                          CFT014
     C                     Z-ADD56        W0ERNB                          CFT014
     C                     MOVEL'*CALL '  W0KEY                           CFT014
     C                     MOVEL'MEM5003' W0MSGD                          CFT014
     C                     MOVEL'MIDAS  ' W0MSGF                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Set Instructed amount (Formatted for screen)                     CFT014
      *                                                                   CFT014
     C                     MOVE *BLANK    ZFIELD                          CFT014
     C                     MOVE P#TOAM    ZFIELD                          CFT014
      *                                                                   CFT014
     C                     CALL 'ZEDIT'                9090               CFT014
     C                     PARM           ZFIELD                          CFT014
     C                     PARM @1NBDP    ZADEC                           CFT014
      *                                                                   CFT014
      *  If error on call                                                 CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL'ZEDIT   'W0FILE           ***************CFT014
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 57 *CFT014
     C                     Z-ADD57        W0ERNB           ***************CFT014
     C                     MOVEL'MEM5003' W0MSGD                          CFT014
     C                     MOVEL'MIDAS  ' W0MSGF                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     END                                            CFT014
      *                                                                   CFT014
     C                     MOVE ZFIELD    S1NDAM                          CFT014
     C                     Z-ADDP#TOAM    V#NDAM                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** When Instructed currency/amount is blank, must be defaulted      CFT014
      ** to be the same as Settl ccy and amount given that the sender     CFT014
      ** and the receiver both support charge transparency                CFT014
      *                                                                   CFT014
     C           S1NDCY    IFEQ *BLANK                                    CFT014
     C           S1NDAM    ANDEQ*BLANK                                    CFT014
     C                     EXSR SRIDFT                                    CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CEU006
     C                     ENDIF                                          CEU006
      *                                                                   CEU006
      *  Unwind subroutine stack name                                     CEU006
      *                                                                   CEU006
     C           EXAMTV    TAG                                            CEU006
     C                     MOVEA*BLANKS   @STK,Q                          CEU006
     C                     SUB  1         Q       50                      CEU006
      *                                                                   CEU006
     C                     ENDSR                                          CEU006
     C/EJECT
      *****************************************************************   CFT014
      *                                                               *   CFT014
      *  SRTRIB   : Third Reimbursement Institution                   *   CFT014
      *                                                               *   CFT014
      *  CALLED BY: SRPROC -                                          *   CFT014
      *                                                               *   CFT014
      *  CALLS    :        -                                          *   CFT014
      *                                                               *   CFT014
      *****************************************************************   CFT014
     C           SRTRIB    BEGSR                                          CFT014
      *                                                                   CFT014
      *  Set up subroutine stack name                                     CFT014
      *                                                                   CFT014
     C                     ADD  1         Q                               CFT014
     C                     MOVEL'SRTRIB'  @STK,Q                          CFT014
      *                                                                   CFT014
      ** Different types are                                              CFT014
      ** I : Iban                                                         CFT014
      ** F : Full Nostro                                                  CFT014
      ** R : Retail 10 chars                                              CFT014
      ** G : General Ledger                                               CFT014
      *                                                                   CFT014
     C                     MOVE *BLANKS   F1TRIT                          CFT014
     C                     MOVE *BLANKS   WATYP5  1                       CFT014
     C                     MOVE *BLANKS   WACST5  1                       CFT014
     C                     Z-ADD0         WDACO5  50                      CFT014
      *                                                                   CFT014
      *  Move 3rd Reimb Inst to ADDRSS data structure for Analysis        CFT014
      *                                                                   CFT014
     C                     MOVEL*BLANKS   ADDRSS                          CFT014
     C                     MOVELS1TRIB    ADDRSS                          CFT014
      *                                                                   CFT014
      ** Third Reimbursement Institution is blank.                        CFT014
      *                                                                   CFT014
     C           S1TRIB    IFEQ *BLANKS                                   CFT014
      *                                                                   CFT014
      ** Error if Receiver's Correspondent is valid but is                CFT014
      ** not a Nostro and not a Retail a/c and not an IBAN a/c.           CFT014
      *                                                                   CFT014
     C           I1RCO     IFEQ *BLANK                                    CFT014
     C           S1RCCO    ANDNE*BLANK                                    CFT014
     C           F1RCRT    ANDNE'N'                                       CFT014
     C           F1RCRT    ANDNE'F'                                       CFT014
     C           F1RCRT    ANDNE'R'                                       CFT014
     C           F1RCRT    ANDNE'I'                                       CFT014
      *                                                                   CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C           CFT004    IFEQ 'Y'                                       CFT014
     C                     MOVEL'FTM3106' FTMSID                          CFT014
     C                     ELSE                                           CFT014
     C                     MOVEL'FTM3124' FTMSID                          CFT014
     C                     ENDIF                                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXTRIB                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Exit because Third Reimb Inst is blank                           CFT014
     C                     GOTO EXTRIB                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** If Receiver's Correspondent has not been entered,                CFT014
      ** Third Reimbursement Institution must be blank.                   CFT014
      *                                                                   CFT014
     C           S1RCCO    IFEQ *BLANKS                                   CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3147' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXTRIB                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** If switchable feature CFT004 is installed, validate entry        CFT014
      ** as an IBAN.                                                      CFT014
      *                                                                   CFT014
     C           CFT004    IFEQ 'Y'                                       CFT014
      *                                                                   CFT014
     C                     MOVELS1TRIB    PIBAN                           CFT014
      *                                                                   CFT014
     C                     CALL 'AOIBANR3'                                CFT014
     C                     PARM           @RTCD                           CFT014
     C                     PARM           PIBAN  47                       CFT014
     C                     PARM           POWITH 47                       CFT014
     C           P@IBAN    PARM           PONOBL 34                       CFT014
      *                                                                   CFT014
     C           @RTCD     IFEQ *BLANKS                                   CFT014
      *                                                                   CFT014
     C           1         SUBSTP@IBAN:1  WTRD1   1                       CFT014
     C           1         SUBSTP@IBAN:2  WTRD2   1                       CFT014
     C           1         SUBSTP@IBAN:3  WTRD3   1                       CFT014
     C           1         SUBSTP@IBAN:4  WTRD4   1                       CFT014
     C           WTRD1     LOKUPALPH                     94               CFT014
     C   94      WTRD2     LOKUPALPH                     94               CFT014
     C   94      WTRD3     LOKUPNUMC                     94               CFT014
     C   94      WTRD4     LOKUPNUMC                     94               CFT014
      *                                                                   CFT014
      ** Entry is in valid IBAN format.                                   CFT014
      *                                                                   CFT014
     C           *IN94     IFEQ '1'                                       CFT014
      *                                                                   CFT014
      * Store in occurrance data structure  any information               CFT014
      *                                                                   CFT014
     C           5         OCUR OACCNT                                    CFT014
      *                                                                   CFT014
     C                     MOVE *BLANKS   DSSDY                           CFT014
     C                     CALL 'AOIBANR2'                                CFT014
     C                     PARM           @RTCD                           CFT014
     C                     PARM           P@IBAN                          CFT014
     C           OACCNT    PARM           DSSDY                           CFT014
      *                                                                   CFT014
     C           @RTCD     IFNE *BLANK                                    CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
      *                                                                   CFT014
      ** IBAN entry is an invalid ISO country code.                       CFT014
      *                                                                   CFT014
     C           @RTCD     IFEQ '      1'                                 CFT014
     C                     MOVEL'FTM3125' FTMSID                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Invalid BBAN length on country file for IBAN entry.              CFT014
      *                                                                   CFT014
     C           @RTCD     IFEQ '      2'                                 CFT014
     C                     MOVEL'FTM3126' FTMSID                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** IBAN entry contains invalid characters.                          CFT014
      *                                                                   CFT014
     C           @RTCD     IFEQ '      3'                                 CFT014
     C                     MOVEL'FTM3127' FTMSID                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** IBAN length of BBAN  is incorrect.                               CFT014
      *                                                                   CFT014
     C           @RTCD     IFEQ '      4'                                 CFT014
     C                     MOVEL'FTM3128' FTMSID                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Check Digits are incorrect for IBAN entry.                       CFT014
      *                                                                   CFT014
     C           @RTCD     IFEQ '      5'                                 CFT014
     C                     MOVEL'FTM3129' FTMSID                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** IBAN account does not exist on the system.                       CFT014
      *                                                                   CFT014
     C           @RTCD     IFEQ '      6'                                 CFT014
     C                     MOVEL'FTM3130' FTMSID                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXTRIB                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** IBAN is valid.                                                   CFT014
      *                                                                   CFT014
     C                     MOVEL*BLANKS   S1TRIB                          CFT014
     C                     MOVELP@IBAN    S1TRIB                          CFT014
     C                     MOVE 'I'       F1TRIT                          CFT014
     C                     GOTO EXTRIB                                    CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** If multi-branching and entry is more than 18 characters          CFT014
      ** OR if single-branching and entry is more than 15                 CFT014
      ** characters, display error message and bypass validation.         CFT014
      *                                                                   CFT014
     C           WUMBIN    IFEQ 'Y'                                       CFT014
     C           ADEXGL    ANDNE*BLANKS                                   CFT014
     C           WUMBIN    ORNE 'Y'                                       CFT014
     C           ADEXG1    ANDNE*BLANKS                                   CFT014
      *                                                                   CFT014
     C           CFT004    IFEQ 'Y'                                       CFT014
     C                     MOVEL'FTM3131' FTMSID                          CFT014
     C                     ELSE                                           CFT014
     C                     MOVEL'FTM3148' FTMSID                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXTRIB                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Check for embedded blanks in positions 1-18 (for                 CFT014
      ** multi-branching) and in positions 1-15 (for single-              CFT014
      ** branching).                                                      CFT014
      *                                                                   CFT014
     C                     MOVEL*BLANKS   YA3                             CFT014
     C           WUMBIN    IFEQ 'Y'                                       CFT014
     C                     MOVEAADGLMB    YA3,1                           CFT014
     C                     Z-ADD18        WNUM    30                      CFT014
     C                     ELSE                                           CFT014
     C                     MOVEAADGLSB    YA3,1                           CFT014
     C                     Z-ADD15        WNUM                            CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     SETOF                     9092                 CFT014
     C           *LIKE     DEFN WNUM      I1                              CFT014
     C                     Z-ADD1         I1                              CFT014
     C           *IN92     DOUEQ'1'                                       CFT014
     C           I1        ORGT WNUM                                      CFT014
     C           YA3,I1    IFEQ *BLANKS                                   CFT014
     C                     SETON                     90                   CFT014
     C                     ELSE                                           CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     SETON                     92                   CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CFT014
     C                     ADD  1         I1                              CFT014
     C                     ENDDO                                          CFT014
      *                                                                   CFT014
      ** Error if there are embedded spaces.                              CFT014
      *                                                                   CFT014
     C           *IN92     IFEQ '1'                                       CFT014
     C           CFT004    IFEQ 'Y'                                       CFT014
     C                     MOVEL'FTM3132' FTMSID                          CFT014
     C                     ELSE                                           CFT014
     C                     MOVEL'FTM3148' FTMSID                          CFT014
     C                     ENDIF                                          CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXTRIB                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** 3rd Reimbursement Institution Full Nostro ?                      CFT014
      *                                                                   CFT014
      ** Check positions 6-34 of Third Reimbursement are blanks           CFT014
      ** If position 5 is blank, display error message and bypass         CFT014
      ** remaining validation.                                            CFT014
      *                                                                   CFT014
     C           ADFNEX    IFEQ *BLANKS                                   CFT014
      *                                                                   CFT014
     C           ADFN05    IFEQ *BLANKS                                   CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3123' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXTRIB                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** If positions 4-5 is not numeric, display error message           CFT014
      ** and bypass remaining validation.                                 CFT014
      *                                                                   CFT014
     C           DIGITS    CHECKADFNNB                   90               CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3123' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXTRIB                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Check if entry is an existing full Nostro identifier.            CFT014
      *                                                                   CFT014
     C                     CALL 'AONOSTR0'                                CFT014
     C                     PARM *BLANKS   @RTCD                           CFT014
     C                     PARM '*KEY   ' @OPTN                           CFT014
     C                     PARM *BLANKS   @CUST                           CFT014
     C                     PARM ADFCCY    @CYCD                           CFT014
     C                     PARM *BLANKS   @ACCD                           CFT014
     C                     PARM *BLANKS   @ACSN                           CFT014
     C                     PARM ADFNNB    @NONB                           CFT014
     C                     PARM *BLANKS   @BRCD                           CFT014
     C                     PARM *BLANKS   @CSSN                           CFT014
     C                     PARM *BLANKS   @PNOI                           CFT014
     C           SDNOST    PARM SDNOST    DSFDY                           CFT014
      *                                                                   CFT014
     C           @RTCD     IFNE *BLANKS                                   CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3133' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXTRIB                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      * Store in occurrance data structure  any information               CFT014
      *                                                                   CFT014
     C           5         OCUR OACCNT                                    CFT014
      *                                                                   CFT014
     C                     MOVE A7ACCD    ACOD                            CFT014
     C                     Z-ADDA7ACSN    ACSQ                            CFT014
     C                     MOVE A7CUST    CNUM                            CFT014
     C                     MOVELA7BRCD    BRCA                            CFT014
     C                     MOVELA7CYCD    CCY                             CFT014
     C           AKEY      CHAINACCNTABF             90                   CFT014
      *                                                                   CFT014
      *  Database error account does not exist                            CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL*BLANKS   ADCHK1                          CFT014
     C                     MOVELA7CUST    ADGCNM                          CFT014
     C                     MOVELA7CYCD    ADGCCY                          CFT014
     C                     MOVELA7ACCD    ADGACD                          CFT014
     C                     MOVELA7ACSN    ADGASQ                          CFT014
     C                     MOVELA7BRCD    ADGBRC                          CFT014
     C                     MOVEL'ACCNT   'W0FILE           ***************CFT014
     C                     MOVELADCHK1    W0KEY            * DB ERROR 66 *CFT014
     C                     Z-ADD66        W0ERNB           ***************CFT014
     C                     MOVEL'MEM5004' W0MSGD                          CFT014
     C                     MOVEL'MIDAS  ' W0MSGF                          CFT014
     C                     EXSR SRERR                                     CFT014
     C                     END                                            CFT014
      *                                                                   CFT014
     C                     MOVE 'F'       F1TRIT                          CFT014
      *                                                                   CFT014
     C                     GOTO EXTRIB                                    CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** 3rd Reimbursement Institution Retail ?                           CFT014
      *                                                                   CFT014
      ** Validate entry as Retail a/c number if positions 11 to 34        CFT014
      ** are blanks and Retail module is ON.                              CFT014
      *                                                                   CFT014
     C           ADACNO    IFNE *BLANKS                                   CFT014
     C           ADEXAC    ANDEQ*BLANKS                                   CFT014
     C           V#RETL    ANDEQ'Y'                                       CFT014
      *                                                                   CFT014
      ** Error if entry is not numeric.                                   CFT014
      *                                                                   CFT014
     C           DIGITS    CHECKADACNO                   90               CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3134' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXTRIB                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      * Store in occurrance data structure  any information               CFT014
      *                                                                   CFT014
     C           5         OCUR OACCNT                                    CFT014
      *                                                                   CFT014
      ** If entry is numeric, save value to ACNO                          CFT014
      ** check if live record exists on file.                             CFT014
      *                                                                   CFT014
     C                     MOVE ADACNO    ACNO                            CFT014
     C           ACNO      CHAINACNUMAF              90                   CFT014
     C           *IN90     IFEQ '0'                                       CFT014
     C           RECI      IFNE 'D'                                       CFT014
     C                     SETON                     90                   CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** If no live record found, display error message and bypass        CFT014
      ** remaining validation.                                            CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3135' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXTRIB                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** If Retail a/c no. is not supported by the system,                CFT014
      ** display error message and bypass remaining validation.           CFT014
      *                                                                   CFT014
     C           V#RANR    IFNE 'Y'                                       CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3136' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXTRIB                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     MOVE 'R'       F1TRIT                          CFT014
     C                     GOTO EXTRIB                                    CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** 3rd Reimbursement Institution General Ledger Account ?           CFT014
      *                                                                   CFT014
      ** If entry for positions 1-6 is not numeric, display               CFT014
      ** error message and bypass remaining validation.                   CFT014
      *                                                                   CFT014
     C           DIGITS    CHECKADGCNM                   90               CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3137' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXTRIB                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** If entry for positions 10-15 is not numeric, display             CFT014
      ** error message and bypass remaining validation.                   CFT014
      *                                                                   CFT014
     C           DIGITS    CHECKADGASQ                   90               CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ '1'                                       CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3137' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXTRIB                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      * Store in occurrance data structure  any information               CFT014
      *                                                                   CFT014
     C           5         OCUR OACCNT                                    CFT014
      *                                                                   CFT014
     C                     MOVE ADGCNM    CNUM                            CFT014
     C                     MOVE ADGCCY    CCY                             CFT014
     C                     MOVE ADGACD    ACOD                            CFT014
     C                     MOVE ADGASQ    ACSQ                            CFT014
      *                                                                   CFT014
      ** If multi-branching, move values of positions 16-18               CFT014
      ** to KBRCA.  Otherwise, move values of V#SBRC if                   CFT014
      ** single -branching.                                               CFT014
      *                                                                   CFT014
     C           WUMBIN    IFEQ 'Y'                                       CFT014
     C                     MOVE ADGBRC    BRCA                            CFT014
     C                     ELSE                                           CFT014
     C                     MOVE V#SBRC    BRCA                            CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C           AKEY      CHAINACCNTABF             90                   CFT014
      *                                                                   CFT014
      ** Error if no existing live account.                               CFT014
      *                                                                   CFT014
     C           *IN90     IFEQ *ON                                       CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3138' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXTRIB                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     MOVE 'G'       F1TRIT                          CFT014
      *                                                                   CFT014
      *  Unwind subroutine stack name                                     CFT014
      *                                                                   CFT014
     C           EXTRIB    TAG                                            CFT014
     C                     MOVEA*BLANKS   @STK,Q                          CFT014
     C                     SUB  1         Q       50                      CFT014
      *                                                                   CFT014
     C                     ENDSR                                          CFT014
     C/EJECT                                                              CFT014
      *****************************************************************   CFT014
      *                                                               *   CFT014
      *  ACTRIB   : Process Third Reimb Institution as an account     *   CFT014
      *                                                               *   CFT014
      *  CALLED BY: SRPROC -                                          *   CFT014
      *                                                               *   CFT014
      *  CALLS    :        -                                          *   CFT014
      *                                                               *   CFT014
      *****************************************************************   CFT014
     C           ACTRIB    BEGSR                                          CFT014
      *                                                                   CFT014
      *  Set up subroutine stack name                                     CFT014
      *                                                                   CFT014
     C                     ADD  1         Q                               CFT014
     C                     MOVEL'ACTRIB'  @STK,Q                          CFT014
      *                                                                   CFT014
     C           F1TRIT    IFNE 'I'                                       CFT014
     C           F1TRIT    ANDNE'F'                                       CFT014
     C           F1TRIT    ANDNE'R'                                       CFT014
     C           F1TRIT    ANDNE'G'                                       CFT014
     C                     GOTO EXATRI                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C           CCY       IFNE S1SMCY                                    CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3146' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXATRI                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      ** Store full account details for FT Transaction Status File        CFT014
      ** record update.                                                   CFT014
      *                                                                   CFT014
     C**********           Z-ADDCNUM      IMCNUM                                       CFT014 CSD027
     C                     MOVE CNUM      IMCNUM                                              CSD027
     C                     MOVE CCY       IMCCY                           CFT014
     C                     Z-ADDACOD      IMACOD                          CFT014
     C                     Z-ADDACSQ      IMACSQ                          CFT014
     C                     MOVE BRCA      IMBRCA                          CFT014
     C                     MOVELIMACNT    TRIDS                           CFT014
     C                     MOVELA7NONB    V#DBNS                                              250600
      *                                                                   CFT014
      *  Unwind subroutine stack name                                     CFT014
      *                                                                   CFT014
     C           EXATRI    TAG                                            CFT014
     C                     MOVEA*BLANKS   @STK,Q                          CFT014
     C                     SUB  1         Q       50                      CFT014
      *                                                                   CFT014
     C                     ENDSR                                          CFT014
     C/EJECT                                                              CFT014
      *****************************************************************
      *                                                               *
      *  SRXVAL   : Cross Validations                                 *
      *                                                               *
      *  CALLED BY:        -                                          *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     C           SRXVAL    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRXVAL'  @STK,Q
      *
      *  /COPY Statement
      *
     C/COPY WNCPYSRC,FT0352XVAL
      *                                                                   CFT014
      ** If 3rd Reimbursment institution is blank                         CFT014
      ** Error if Receiver's Correspondant is a Nostro or a               CFT014
      ** Retail a/c or an IBAN a/c but a/c currency is not                CFT014
      ** equal to settle currency.                                        CFT014
      *                                                                   CFT014
     C           S1TRIB    IFEQ *BLANKS                                   CFT014
      *                                                                   CFT014
      * Set occurrance structure                                          CFT014
      *                                                                   CFT014
     C           1         OCUR OACCNT                                    CFT014
      *                                                                   CFT014
      *                                                                   CFT014
     C           F1RCRT    IFEQ 'N'                                       CFT014
     C           F1RCRT    OREQ 'F'                                       CFT014
     C           F1RCRT    OREQ 'R'                                       CFT014
     C           F1RCRT    OREQ 'I'                                       CFT014
      *                                                                   CFT014
     C           CCY       IFNE S1SMCY                                    CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM3158' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXXVAL                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *
      * If Receiver's Correspondent is blank Sender currency must
      * equal settle
      *
     C           S1RCCO    IFEQ *BLANK
      *
     C           F1SNTP    IFEQ 'F'
     C           F1SNTP    OREQ 'G'
     C           F1SNTP    OREQ 'R'
      *
      * Set occurrance structure
      *
     C           2         OCUR OACCNT
      *
     C                     SELEC
     C           CCY       WHNE S1SMCY
     C           F1SNTP    ANDEQ'R'
     C                     MOVEL'Y'       E#MSGS
     C           S1SMCY    CAT  S1SND     FTMSDT    P
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2134' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
      *
     C           CCY       WHNE S1SMCY
     C           F1SNTP    ANDEQ'F'
     C                     MOVEL'Y'       E#MSGS
     C           S1SMCY    CAT  S1SND     FTMSDT    P
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2135' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
      *
     C           CCY       WHNE S1SMCY
     C                     MOVEL'Y'       E#MSGS
     C           S1SMCY    CAT  S1SND     FTMSDT    P
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2136' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
      *
     C                     ENDSL
      *
     C                     END
      *
     C                     END
      *
      * If Beneficiary not an account and Account with Institution
      * blank then error
      *
     C           F1BNCT    IFNE 'R'
     C           F1BNCT    ANDNE'P'                                       E35508
     C           F1BNCT    ANDNE'I'                                       CFT004
     C           S1ACBK    ANDEQ*BLANKS                                   E35508
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1BNC1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2137' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1ACBK
     C                     MOVEL'Y'       I1ACBB
     C                     MOVEL'Y'       I1BNC1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     END
      *
      * If Receiver's Correspondent bank and Sender not an account
      * error
      *
     C           F1SNTP    IFNE 'R'
     C           F1SNTP    ANDNE'F'                                       E35508
     C           F1SNTP    ANDNE'G'                                       E35508
     C           S1RCCO    ANDEQ*BLANKS                                   E35508
     C/COPY WNCPYSRC,FT0352C012
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SND1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2138' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C*********************MOVEL'Y'       I1RCCO                          CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     END
      *
      *  If Sender is an account then no entry is allowed
      *  into Receiver's Correspondent if currency of account is
      *  Settle currency
      *
     C           F1SNTP    IFEQ 'F'
     C           F1SNTP    OREQ 'R'
     C           F1SNTP    OREQ 'G'
      *
      *  Set occurrance data structure
      *
     C           2         OCUR OACCNT
      *
     C           S1SMCY    IFNE CCY
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SND1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2139' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C*********************MOVEL'Y'       I1RCCO                          CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     END
      *
     C                     END
     C/COPY WNCPYSRC,FT0352C021
      *
      *  Check if settle date is in the future and if the
      *  account is closing today                                         CFT014
      *  Checks are done by priority :                                    CFT014
      *  1 Third Reimb Institution                                        CFT014
      *  2 Receiver correspondant (Can't be blank if 3rd reimb filled)    CFT014
      *  3 Sender                                                         CFT014
      *
     C           F1RCRT    IFEQ *BLANKS
      *
      *  Set occurrance data structure for Sender
      *
     C           2         OCUR OACCNT
      *
     C           F1SLDT    IFGT WURDNB
     C           ACST      ANDEQ'C'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SND1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2140' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     END
      *
      * For back valued transactions check against account open date
      *
     C           F1SLDT    IFLT DACO
     C           ATYP      ANDEQ'R'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SND1    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2141' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     END
      *
      * Check against back value limit
      *
     C           F1SLDT    IFLT V#BVLD
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SLDT    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2142' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1SND1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     END
      *
     C                     END
      *
     C           F1RCRT    IFNE *BLANKS
     C           F1TRIT    ANDEQ*BLANKS                                   CFT014
      *
      *  Set occurrance data structure for Receiver's Correspondent
      *
     C           1         OCUR OACCNT
      *
     C           F1SLDT    IFGT WURDNB
     C           ACST      ANDEQ'C'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1RCCO    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2143' FTMSID
     C                     EXSR FTSNMS
     C*********************MOVEL'Y'       I1RCCO                          CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     END
      *
      * For back valued transactions check against account open date
      *
     C           F1SLDT    IFLT DACO
     C           ATYP      ANDEQ'R'
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1RCCO    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2145' FTMSID
     C                     EXSR FTSNMS
     C*********************MOVEL'Y'       I1RCCO                          CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     END
      *
      * Check against back value limit
      *
      *                                                                   S01408
     C/COPY WNCPYSRC,FT0352CCP3                                           S01408
      *                                                                   S01408
     C           F1SLDT    IFLT V#BVLD
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1SLDT    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2142' FTMSID
     C                     EXSR FTSNMS
     C*********************MOVEL'Y'       I1RCCO                          CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     END
      *
      *                                                                   S01408
     C/COPY WNCPYSRC,FT0352CCP4                                           S01408
      *                                                                   S01408
     C                     END
     C/COPY WNCPYSRC,FT0352C013
      *                                                                   CFT014
     C           F1TRIT    IFNE *BLANKS                                   CFT014
      *                                                                   CFT014
      *  Set occurrance data structure for 3rd Reimb Institution          CFT014
      *                                                                   CFT014
     C           5         OCUR OACCNT                                    CFT014
      *                                                                   CFT014
     C           F1SLDT    IFGT WURDNB                                    CFT014
     C           ACST      ANDEQ'C'                                       CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELS1TRIB    FTMSDT                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM2143' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXXVAL                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      * For back valued transactions check against account open date      CFT014
      *                                                                   CFT014
     C           F1SLDT    IFLT DACO                                      CFT014
     C           ATYP      ANDEQ'R'                                       CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELS1TRIB    FTMSDT                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM2145' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXXVAL                                    CFT014
     C                     ENDIF                                          CFT014
      *                                                                   CFT014
      * Check against back value limit                                    CFT014
      *                                                                   CFT014
     C/COPY WNCPYSRC,FT0352C018
     C           F1SLDT    IFLT V#BVLD                                    CFT014
     C                     MOVEL'Y'       E#MSGS                          CFT014
     C                     MOVELS1SLDT    FTMSDT                          CFT014
     C                     MOVELE#CPGM    FTPGMQ                          CFT014
     C                     MOVEL'FTM2142' FTMSID                          CFT014
     C                     EXSR FTSNMS                                    CFT014
     C                     MOVEL'Y'       I1TRIB                          CFT014
     C                     MOVEL'Y'       E#NOUP                          CFT014
     C                     GOTO EXXVAL                                    CFT014
     C                     ENDIF                                          CFT014
     C/COPY WNCPYSRC,FT0352C019
      *                                                                   CFT014
     C                     ENDIF                                          CFT014
      *
      * Find difference between settle and pay
      *
     C                     Z-ADD0         F1PRBD
     C                     MOVEL'D'       ZFREQ
     C                     MOVELS1PCCY    ZCCY
      *
     C           F1SLDT    IFLT F1PVDT
     C                     Z-ADDF1SLDT    DAYNO1
     C                     Z-ADDF1PVDT    DAYNO2
     C                     ELSE
     C                     Z-ADDF1SLDT    DAYNO2  50
     C                     Z-ADDF1PVDT    DAYNO1  50
     C                     END
      *
     C                     Z-ADDDAYNO1    ZDAYNO
     C                     MOVE PLOC      ZLOC
      *
     C           ZDAYNO    DOWLTDAYNO2
      *
     C                     CALL 'FT0310'               9090
     C                     PARM *BLANKS   ZZERR@  7
     C                     PARM '*NEWDATE'W0ACT   8
     C                     PARM ZFREQ     W0FREQ  1
     C           ZDAYNO    PARM ZDAYNO    W0RDNB  50
     C                     PARM 0         W0MDAY  20
     C                     PARM ZCCY      W0CCY   3
     C                     PARM ZLOC      W0LOC   3
      *
      *  If error on call
      *
     C           ZZERR@    IFNE *BLANKS
     C           *IN90     OREQ '1'
     C                     MOVEL'FT0310  'W0FILE           ***************
     C                     MOVELZCCY      W0KEY            * DB ERROR 21 *
     C                     Z-ADD21        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C                     ADD  1         F1PRBD
     C                     END
      *
     C           F1SLDT    IFGT F1PVDT
     C                     MULT -1        F1PRBD
     C                     END
      *
      *  Check if pay date is in the future and if the
      *  account is closing today
      *
     C           S1ACBK    IFEQ *BLANKS
      *
      *  Set occurrance data structure for Beneficiary
      *
     C           3         OCUR OACCNT
      *
     C           F1PVDT    IFGT WURDNB
     C           ACST      ANDEQ'C'
     C                     MOVEL'Y'       E#MSGS
     C           S1PCCY    CAT  S1BNC1    FTMSDT    P
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2146' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BNC1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     END
      *
      * For back valued transactions check against account open date
      *
     C           F1PVDT    IFLT DACO
     C           ATYP      ANDEQ'R'
     C                     MOVEL'Y'       E#MSGS
     C           S1PCCY    CAT  S1BNC1    FTMSDT    P
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2147' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BNC1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     END
      *
      * Check against back value limit
      *
     C           F1PVDT    IFLT V#BVLD
     C           ATYP      ANDEQ'R'                                       CFT002
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1PVDT    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2148' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1BNC1
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     END
      *
     C                     END
      *
     C           S1ACBK    IFNE *BLANKS
      *
      *  Set occurrance data structure for Account with Institution
      *
     C           4         OCUR OACCNT
      *
     C           F1PVDT    IFGT WURDNB
     C           ACST      ANDEQ'C'
     C                     MOVEL'Y'       E#MSGS
     C           S1PCCY    CAT  S1ACBK    FTMSDT    P
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2150' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1ACBK
     C                     MOVEL'Y'       I1ACBB
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     END
      *
      * For back valued transactions check against account open date
      *
     C           F1PVDT    IFLT DACO
     C           ATYP      ANDEQ'R'
     C                     MOVEL'Y'       E#MSGS
     C           S1PCCY    CAT  S1ACBK    FTMSDT    P
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2149' FTMSID
     C                     EXSR FTSNMS
     C*********************MOVEL'Y'       I1RCCO                          CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     END
      *
      * Check against back value limit
      *
      *                                                                   S01408
     C/COPY WNCPYSRC,FT0352CCP1                                           S01408
      *                                                                   S01408
     C           F1PVDT    IFLT V#BVLD
     C           ATYP      ANDEQ'R'                                       CFT002
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1PVDT    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2148' FTMSID
     C                     EXSR FTSNMS
     C*********************MOVEL'Y'       I1RCCO                          CFT014
     C                     MOVEL'Y'       I1RCO1                          CFT014
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     END
      *                                                                   S01408
     C/COPY WNCPYSRC,FT0352CCP2                                           S01408
      *                                                                   S01408
      *
     C                     END
      *
      * Check if settle/pay/rate information has changed if so
      * one amount field must have been blank
      *
     C                     CLEARR#RCHK
     C                     MOVELS1SMCY    R#SMCY
     C                     MOVELS1PCCY    R#PCCY
     C                     Z-ADDF1SMAM    R#SMAM
     C                     Z-ADDF1PYAM    R#PYAM
     C                     Z-ADDF1RATE    R#RATE
     C                     MOVELS1OVRD    R#OVRD
     C                     MOVEL*BLANKS   R#STMT
      *
     C           V#NWRK    IFNE 'CASHV8'                                  122060
     C           V#NWRK    ANDNE'FONTIS'                                  CGL009
      *                                                                   122060
     C           V#RCHK    IFNE R#RCHK
     C           K#SMAM    ANDNE*BLANKS
     C           K#PYAM    ANDNE*BLANKS
     C                     MOVEL'Y'       E#MSGS
     C                     MOVELS1PYAM    FTMSDT
     C                     MOVELE#CPGM    FTPGMQ
     C                     MOVEL'FTM2151' FTMSID
     C                     EXSR FTSNMS
     C                     MOVEL'Y'       I1RATE
     C                     MOVEL'Y'       I1PYAM
     C                     MOVEL'Y'       I1SMAM
     C                     MOVEL'Y'       E#NOUP
     C                     GOTO EXXVAL
     C                     ELSE
     C                     MOVELR#RCHK    V#RCHK
     C                     END
      *
     C                     ELSE                                           122060
     C                     MOVELR#RCHK    V#RCHK                          122060
     C                     ENDIF                                          122060
      *
      *  Set up From and To GL accounts
      *
     C***********F1RCRT    IFEQ *BLANKS                                   CFT014
     C*********************MOVELSNDDS     V#FRGL                          CFT014
     C*********************ELSE                                           CFT014
     C*********************MOVELRCCDS     V#FRGL                          CFT014
     C*********************END                                            CFT014
      *                                                                   CFT014
      ** Priority on the DR side                                          CFT014
      ** 1 3rd Reimbursement Institution                                  CFT014
      ** 2 Receiver correspondant                                         CFT014
      ** 3 Sender                                                         CFT014
     C                     SELEC                                          CFT014
      * Always an account when filled                                     CFT014
     C           F1TRIT    WHNE *BLANK                                    CFT014
     C                     MOVELTRIDS     V#FRGL                          CFT014
      * Can be something else than an account                             CFT014
     C           F1RCRT    WHEQ 'I'                                       CFT014
     C           F1RCRT    OREQ 'N'                                       CFT014
     C           F1RCRT    OREQ 'F'                                       CFT014
     C           F1RCRT    OREQ 'R'                                       CFT014
     C                     MOVELRCCDS     V#FRGL                          CFT014
      * Must be an account                                                CFT014
     C                     OTHER                                          CFT014
     C                     MOVELSNDDS     V#FRGL                          CFT014
     C                     ENDSL                                          CFT014
     C/COPY WNCPYSRC,FT0352C014
      *
      ** Priority on the CR side                                          CFT014
      ** 1 Account with bank                                              CFT014
      ** 2 Beneficiary                                                    CFT014
     C           F1ACBT    IFEQ *BLANKS
     C                     MOVELBNCDS     V#TOGL
     C                     ELSE
     C                     MOVELACBDS     V#TOGL
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXXVAL    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q       50
      *
     C                     ENDSR
     E/COPY FTCPYSRC,SFTADDRC
     E/COPY FTCPYSRC,SFTADDRSC
     E/COPY FTCPYSRC,SFTSWFTC
     E/COPY FTCPYSRC,SFTSNMSC
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRALGN   : To call ZALIGN                                    *
      *                                                               *
      *  CALLED BY:        -                                          *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRALGN    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRALGN'  @STK,Q
      *
     C                     CALL 'ZALIGN'               90
     C                     PARM *BLANKS   ZRTN    7        B:Return Code
     C                     PARM           ZFIELD 16        B:Check Field
     C                     PARM           ZADEC   10       I:No Of Decs
     C                     PARM           ZADIG   20       I:No Of Digits
     C                     PARM *BLANKS   ZAFLD  16        O:Check Field
      *
      *  If return with an error (LR seton in called program) then
      *  process for database error.
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'ZALIGN  'W0FILE
     C                     MOVEL'*CALL'   W0KEY            ***************
     C                     Z-ADD40        W0ERNB           * DB ERROR 40 *
     C                     MOVEL'MEM5003' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXALGN    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRACT    : Undefined action - error                          *
      *                                                               *
      *  CALLED BY: Main Processing                                   *
      *                                                               *
      *  CALLS    : SRERR   - report error and close down program     *
      *                                                               *
      *****************************************************************
     CSR         SRACT     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRACT '  @STK,Q
      *
      *  Database error undefined action
      *
     C                     MOVEL'*ACTION 'W0FILE           ***************
     C                     MOVELW0ACT     W0KEY            * DB ERROR 41 *
     C                     Z-ADD41        W0ERNB           ***************
     C                     MOVEL'MEM5006' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
      *
      *  Unwind subroutine stack name
      *
     C           EXACT     TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT   : Initialise and define fields                      *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    :        -                                          *
      *                                                               *
      *****************************************************************
     CSR         SRINIT    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
      *
      *  Set up copyright statement
      *
     C                     MOVEACPY@      BIS@   80
      *
      *  Open Files
      *
     C                     OPEN ACCNT
     C                     OPEN ACNUMA
     C                     OPEN PYCHGDB
      *
      *  Set up KLIST for ACCNT
      *
     C           AKEY      KLIST
     C                     KFLD           CNUM
     C                     KFLD           CCY
     C                     KFLD           ACOD
     C                     KFLD           ACSQ
     C                     KFLD           BRCA                            S01117
      *                                                                   CEU006
     C                     MOVE *OFF      *IN89                           CEU006
      *                                                                   CEU006
      *
      *  Get Rundate information
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDTA
     C                     IN   RUNDTA
     C                     MOVE AGMRDT    WUMRDT  7        Midas Run date
     C                     MOVE AGRDNB    WURDNB  50       Run day number
     C                     MOVE AGSUC     WUSUC   1        Set up complete
     C                     MOVE AGDFF     WUDFF   1        Date Format
     C                     MOVE AGMBIN    WUMBIN  1        Multi Branched
      *
     C           *NAMVAR   DEFN           ZMUSER                          S01117
     C                     IN   ZMUSER                                    S01117
      *                                                                   CEU006
      ** Access SAR details file to determine if CEU006 is switched ON    CEU006
      *                                                                   CEU006
     C                     CALL 'AOSARDR0'                                CEU006
     C                     PARM *BLANKS   @RTRN   7                       CEU006
     C                     PARM '*VERIFY' @OPTN   7                       CEU006
     C                     PARM 'CEU006'  @SARD   6                       CEU006
     C           SCSARD    PARM SCSARD    DSFDY                           CEU006
      *                                                                   CEU006
      ** If core feature is switched ON, set a variable field to          CEU006
      ** condition the rest of the processing                             CEU006
      *                                                                   CEU006
     C           @RTRN     IFEQ *BLANKS                                   CEU006
     C                     MOVE 'Y'       CEU006  1                       CEU006
     C                     ELSE                                           CEU006
      *                                                                   CEU006
      ** else, database error (return code other than *NRF)               CEU006
      *                                                                   CEU006
     C           @RTRN     IFNE '*NRF   '                                 CEU006
     C                     MOVEL'SCSARDPD'W0FILE           ************   CEU006
     C                     MOVEL'*VERIFY' W0KEY            * DBERR 42 *   CEU006
     C                     Z-ADD42        W0ERNB           ************   CEU006
     C                     EXSR SRERR                                     CEU006
     C                     ELSE                                           CEU006
     C                     MOVE 'N'       CEU006                          CEU006
     C                     ENDIF                                          CEU006
     C                     ENDIF                                          CEU006
     C/COPY WNCPYSRC,FT0352C005
      *                                                                   CFT004
      ** Access SAR details file to determine if CFT004 is switched ON    CFT004
      *                                                                   CFT004
     C                     CALL 'AOSARDR0'                                CFT004
     C                     PARM *BLANKS   @RTRN   7                       CFT004
     C                     PARM '*VERIFY' @OPTN   7                       CFT004
     C                     PARM 'CFT004'  @SARD   6                       CFT004
     C           SCSARD    PARM SCSARD    DSFDY                           CFT004
      *                                                                   CFT004
      ** If core feature is switched ON, set a variable field to          CFT004
      ** condition the rest of the processing                             CFT004
      *                                                                   CFT004
     C           @RTRN     IFEQ *BLANKS                                   CFT004
     C                     MOVE 'Y'       CFT004  1                       CFT004
     C                     ELSE                                           CFT004
      *                                                                   CFT004
      ** else, database error (return code other than *NRF)               CFT004
      *                                                                   CFT004
     C           @RTRN     IFNE '*NRF   '                                 CFT004
     C                     MOVEL'SCSARDPD'W0FILE           ************   CFT004
     C                     MOVEL'*VERIFY' W0KEY            * DBERR 49 *   CFT004
     C                     Z-ADD49        W0ERNB           ************   CFT004
     C                     EXSR SRERR                                     CFT004
     C                     ELSE                                           CFT004
     C                     MOVE 'N'       CFT004                          CFT004
     C                     ENDIF                                          CFT004
     C                     ENDIF                                          CFT004
      *                                                                   CFT014
      ** Access SAR details file to determine if CFT014 is switched ON    CFT014
      *                                                                   CFT014
     C                     CALL 'AOSARDR0'                                CFT014
     C                     PARM *BLANKS   @RTRN   7                       CFT014
     C                     PARM '*VERIFY' @OPTN   7                       CFT014
     C                     PARM 'CFT014'  @SARD   6                       CFT014
     C           SCSARD    PARM SCSARD    DSFDY                           CFT014
      *                                                                   CFT014
      ** If core feature is switched ON, set a variable field to          CFT014
      ** condition the rest of the processing                             CFT014
      *                                                                   CFT014
     C           @RTRN     IFEQ *BLANKS                                   CFT014
     C                     MOVE 'Y'       CFT014  1                       CFT014
     C                     ELSE                                           CFT014
      *                                                                   CFT014
      ** else, database error (return code other than *NRF)               CFT014
      *                                                                   CFT014
     C           @RTRN     IFNE '*NRF   '                                 CFT014
     C                     MOVEL'SCSARDPD'W0FILE           ************   CFT014
     C                     MOVEL'*VERIFY' W0KEY            * DBERR 72 *   CFT014
     C                     Z-ADD72        W0ERNB           ************   CFT014
     C                     EXSR SRERR                                     CFT014
     C                     ELSE                                           CFT014
     C                     MOVE 'N'       CFT014                          CFT014
     C                     ENDIF                                          CFT014
     C                     ENDIF                                          CFT014
      *                                                                   166214
      ** Extract Fund Transfer Details                                    166214
      *                                                                   166214
     C                     CALL 'AOFTFRR0'             90                 166214
     C                     PARM '*DBERR ' @RTCD   7                       166214
     C                     PARM '*FIRST ' @OPTN   7                       166214
     C           SDFTFR    PARM SDFTFR    DSFDY                           166214
     C           @RTCD     IFNE *BLANK                                    166214
     C           *IN90     OREQ '1'                                       166214
     C                     MOVEL'SDFTFRPD'W0FILE           ************   166214
     C                     MOVEL'*FIRST ' W0KEY            * DBERR 47 *   166214
     C                     Z-ADD47        W0ERNB           ************   166214
     C                     EXSR SRERR                                     166214
     C                     ENDIF                                          166214
      *                                                                                       CSW215
      ** Check if switchable feature CSW215 is on                                             CSW215
      *                                                                                       CSW215
     C                     CALL 'SWIF2015'                                                    CSW215
     C                     PARM *BLANKS   PRTCD                                               CSW215
      *                                                                                       CSW215
     C           PRTCD     IFEQ 'CSW2015'                                                     CSW215
     C                     MOVE 'Y'       CSW215  1                                           CSW215
     C                     ELSE                                                               CSW215
     C                     MOVE 'N'       CSW215                                              CSW215
     C                     ENDIF                                                              CSW215
      *                                                                                       CSW215
     C/COPY WNCPYSRC,FT0352C015
      *
      *  Indicate that set up is complete
      *
     C                     SETON                     50
      *
      *  Unwind subroutine stack name
      *
     C           EXINIT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/COPY WNCPYSRC,FT0352DCPG
      *
      *  /Copy point for Calculations
      *
     C/EJECT
      *
      *  File and Program Error Processing
      *
     C/COPY FTCPYSRC,SRERRC
     C/EJECT
     O/COPY WNCPYSRC,FT0352DOPG
      *
      *  /Copy point for Outputs
      *
     O/EJECT
**  CPY@ table
(c) Misys International Banking Systems Ltd. 2001
**   ALPH  - Array of alphabetic                                          CFT004
ABCDEFGHIJKLMNOPQRSTUVWXYZ
**   NUMC  - Array of numeric                                             CFT004
1234567890
**   POWER - ARRAY OF POWERS OF 10 FOR CURRENCY CONVERSIONS
0000001
0000010
0000100
0001000
0010000
0100000
1000000
