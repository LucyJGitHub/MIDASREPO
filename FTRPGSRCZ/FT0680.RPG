     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FT FLAG P/R EVENTS AS PROCESSED - I/C')          *
     F*****************************************************************
     F*                                                               *
     F*  Midas - FUNDS TRANSFER MODULE
     F*                                                               *
     F*  FT0680 - FLAG P/R EVENTS AS PROCESSED   INPUT CYCLE          *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      *  Last Amend No. CFT120             Date 28Sep12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL049             Date 07Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
     F*                 CFT009             Date  01Jun00              *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 175479             DATE 08FEB00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 CEU006              DATE  29MAY98             *
     F*                 S01435              DATE  08SEP93             *
     F*                 S01117              DATE  21NOV90             *   S01117
     F*                 S01194              DATE  21NOV90             *   S01194
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  CFT120 - FT IN/OP - Charges to DR of Account Currency        *
      *           (Recompile)                                         *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
     F*     CFT009 - FT Fees and Charge Development-Recompiled        *
      *     175479 - Recompiled over changes in OTPAYDD               *
     F*     CEU006 - EMU Funds Transfer Enhancements (Recompile Only) *
     F*     S01435 - Incoming Message Management.                     *
     F*     S01117 - RCF PROCESSING                                   *
     F*     S01194 - NEW DATABASE ERROR HANDLING                      *
     F*                                                               *
     F*****************************************************************
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*    01            OTPAYDD
     F*    02            CQPACDD
     F*    03            NTRANDD
     F*
     F*       20      FIRST CYCLE IND.
     F*
     F*       27         INVALID UPDATE
     F*       88         RECORD NOT FOUND
     F*
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     FPRACN   IP  E           K        DISK
     F            OTPAYDDF                          KRENAMEOTPAYDDR
     F            CQPACDDF                          KRENAMECQPACDDR
     F            NTRANDDF                          KRENAMENTRANDDR
     FOTPAY   UF  E           K        DISK
     F                                              KCOMIT
     FCQPAC   UF  E           K        DISK
     F                                              KCOMIT
     F            CQPADDDF                          KIGNORE
     FNTRAN   UF  E           K        DISK
     F                                              KCOMIT
     FTABFP   IF  E           K        DISK
     F            TABTB11F                          KIGNORE
     F            TABTB20F                          KIGNORE
     F            TABTG10F                          KIGNORE
     F            TABTG20F                          KIGNORE
     F            TABLETPF                          KIGNORE
     F            TABLET5F                          KIGNORE
     F************TABLETJF                          KIGNORE               S01117
     F            TABLETRF                          KIGNORE
     F            TABLETLF                          KIGNORE
     F            TABLETHF                          KIGNORE
     F            TABTE10F                          KIGNORE
     F*FT0680P1O***E********************PRINTER                           S01117
     FFT0680AUO   E                    PRINTER                            S01117
     F*
     F********************************************************************
     E                    CPY@    1   1 80
      **                                                                                      CSC022
      ** Array to hold commitment jobs name                                                   CSC022
      **                                                                                      CSC022
     E                    WCMT       10 10                                                    CSC022
      **                                                                                      CSC022
     I/EJECT
     I********************************************************************
     I*
     I** DEFINE RECORD IDENTIFYING INDICATORS
     I*  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
     I*
     IOTPAYDDR    01
     I*
     ICQPACDDR    02
     I*
     INTRANDDR    03
     I*
     I***
     ITABTB10F
     I***
     I***   STANDING DATA FILE DETAIL  - PF/TABTB10  ICD 1
     I***
     I              RECI                            RECITB
     I*
     I** DATA AREA LDA - USED IN THE EVENT OF DATABASE ERRORS
     I*
     IDLDA        DS                            256
     I**************************************134 138 DBFILE                S01194
     I**************************************139 167 DBKEY                 S01194
     I**************************************168 175 DBPGM                 S01194
     I**************************************176 177 DBASE                 S01194
     I                                      134 141 DBFILE                S01194
     I                                      142 170 DBKEY                 S01194
     I                                      171 180 DBPGM                 S01194
     I                                      181 1830DBASE                 S01194
     I*
     I** PROGRAM STATUS DATA STRUCTURE - RETREIVE USER ID
     I*
     IPSDS       SDS
     I                                      254 263 USRID
     I                                      244 253 WJOBNM                                    CSC022
     I*
     I*     DATA STRUCTURE FOR COMMITMENT BOUNDARY IDENTIFIERS
     I*
     ICMTTXT      DS                            106
     I                                        1   50TIMEX
     I                                        6   7 MDID
     I                                        8  15 PGMN
     I                                       16  18 WSIDX
     I                                       19  240TIME
     I                                       25  25 ACTCDX
     I                                       26  35 USRIDX
     I                                       51  58 CFLD1
     I                                       59  66 CFLD2
     I                                       67  74 CFLD3
     I                                       75  82 CFLD4
     I                                       83  90 CFLD5
     I                                       91  92 CFLD6
     I                                       93 106 CFLD7
     I*
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      **                                                                                      CSC022
     ISCCMT       DS                            256                                           CSC022
     I                                        1   30WCMTNO                                    CSC022
     I                                        4 103 WCJOBS                                    CSC022
      **                                                                                      CSC022
     IDSFDY     E DSDSFDY                                                                     CSC022
      **  Short data structure for access programs                                            CSC022
      **                                                                                      CSC022
     ISCSARD    E DSSCSARDPD                                                                  CSC022
      **  External data structure - SAR details                                               CSC022
      **                                                                                      CSC022
     I********************************************************************
     I/EJECT
     C                     MOVEACPY@      BIS@   80
     C********************************************************************
     C***
     C***    KLIST FOR KEY TO  - PF/CQPAC
     C***
     C           KCQPAC    KLIST
     C                     KFLD           PREF
     C                     KFLD           WCQSQ   20
     C*
     C********************************************************************
     C*
     C**   FIRST CYCLE
     C*    -----------
     C*
     C           *IN20     IFEQ '0'                        ### B001
     C                     SETON                     20
     C*
     C                     MOVEL'01    '  ZTABKY 12
     C                     MOVE '    10'  ZTABKY
     C**
     C           ZTABKY    CHAINTABFP                99    ON NOT THERE
     C*
     C           *IN99     IFEQ '1'                        ### B002
     C           RECITB    ORNE 'D'
     C                     MOVE 'TABFP'   WBFILE  5        ***************
     C                     MOVELZTABKY    WBKEY  28        ** DBERR  01 **
     C                     Z-ADD01        WBASE   20       ***************
     C                     EXSR ABORT
     C                     END                             ### E002
     C*
     C** SET UP FIELDS FOR CMTTXT DATA STRUCTURE
     C*
     C                     MOVEL'FP'      MDID
     C                     MOVEL'FT0680'  PGMN
     C                     MOVEL*BLANKS   WSIDX
     C                     MOVEL*BLANKS   ACTCDX
     C                     MOVELUSRID     USRIDX
     C                     MOVEL'FLAG P/R'CFLD1
     C                     MOVE ' EVENTS 'CFLD2
     C                     MOVEL'AS PROCE'CFLD3
     C                     MOVE 'SSED    'CFLD4
     C                     MOVEL'(IC)    'CFLD5
     C                     MOVE '        'CFLD6
      **                                                                                      CSC022
      ** Initialize CSC022 and Skip Commit/Rollback flags                                     CSC022
      **                                                                                      CSC022
     C                     MOVE 'N'       CSC022  1                                           CSC022
     C                     MOVE 'N'       WCRSKP  1                                           CSC022
      **                                                                                      CSC022
      ** Access SAR details file to determine if CSC022 switchable feature                    CSC022
      ** is switched on                                                                       CSC022
      **                                                                                      CSC022
     C                     CALL 'AOSARDR0'                                                    CSC022
     C                     PARM *BLANKS   @RTCD   7                                           CSC022
     C                     PARM '*VERIFY '@OPTN   7                                           CSC022
     C                     PARM 'CSC022'  @SARD   6                                           CSC022
     C           SCSARD    PARM SCSARD    DSFDY                                               CSC022
      **                                                                                      CSC022
     C           @RTCD     IFEQ *BLANKS                                                       CSC022
     C                     MOVE 'Y'       CSC022                                              CSC022
      **                                                                                      CSC022
      ** Get Jobs currently running i batch mode using SCCMRJOB dataarea                      CSC022
      **                                                                                      CSC022
     C           *NAMVAR   DEFN SCCMTJOB  SCCMT                                               CSC022
     C                     IN   SCCMT                                                         CSC022
      **                                                                                      CSC022
     C           WCMTNO    IFGT 0                                                             CSC022
      ** Move committed jobs to arrary for checking                                           CSC022
     C                     MOVEAWCJOBS    WCMT                                                CSC022
      ** Verify if job running exists in array                                                CSC022
     C           WJOBNM    LOKUPWCMT                     50                                   CSC022
     C           *IN50     IFEQ *ON                                                           CSC022
     C                     MOVE 'Y'       WCRSKP                                              CSC022
     C                     ENDIF                                                              CSC022
     C                     ENDIF                                                              CSC022
      **                                                                                      CSC022
     C                     ELSE                                                               CSC022
      ** Execute *PSSR if CSC022 is not found or Database error                               CSC022
     C           @RTCD     IFNE '*NRF'                                                        CSC022
     C                     MOVEL'CSC022'  DBKEY                                               CSC022
     C                     MOVEL'SCSARDPD'DBFILE                                              CSC022
     C                     Z-ADD4         DBASE                                               CSC022
     C                     EXSR ABORT                                                         CSC022
     C                     ENDIF                                                              CSC022
      **                                                                                      CSC022
     C                     ENDIF                                                              CSC022
      **                                                                                      CSC022
     C*
     C                     END                             ### E001
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C**  DETAIL  PROCESSING
     C*   ~~~~~~~~~~~~~~~~~~
     C*
     C** PERFORM SUBROUTINE ACCORDING TO RECORD TYPE
     C*
     C                     SETOF                     2788  WORK IND.
     C*
     C           *IN01     CASEQ'1'       OTPAYS
     C*
     C           *IN02     CASEQ'1'       CQPACS
     C*
     C           *IN03     CASEQ'1'       NTRANS
     C*
     C                     END
     C*
     C** FINAL PROCESSING : PRINT RUN CONTROLS REPORT
     C*  ~~~~~~~~~~~~~~~~
     C*
     CLR         *IN20     IFEQ '0'                        ### B001
     CLR                   SETON                     20
     C*
     CLR                   MOVEL'01    '  ZTABKY 12
     CLR                   MOVE '    10'  ZTABKY
     C**
     CLR         ZTABKY    CHAINTABFP                99    ON NOT THERE
     C*
     CLR         *IN99     IFEQ '1'                        ### B002
     CLR         RECITB    ORNE 'D'
     CLR                   MOVE 'TABFP'   WBFILE  5        ***************
     CLR                   MOVELZTABKY    WBKEY  28        ** DBERR  02 **
     CLR                   Z-ADD02        WBASE   20       ***************
     CLR                   EXSR ABORT
     CLR                   END                             ### E002
     C*
     CLR                   END                             ### E001
     C*
     C*LR*******************WRITEP1CONT                                   S01117
     CLR                   WRITEAUCONT                                    S01117
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C**   PROCESSING OF A 'OTPAYDD' RECORD
     C*
     C           OTPAYS    BEGSR
     C*
     C           PREF      CHAINOTPAY                88
     C*
     C           *IN88     IFEQ '1'                        ### B001
     C                     MOVE 'OTPAY'   WBFILE  5        ***************
     C                     MOVELPREF      WBKEY  28        ** DBERR  02 **
     C                     Z-ADD02        WBASE   20       ***************
     C                     EXSR ABORT
     C                     END                             ### E001
     C*
     C**   UPDATE THE P/R INDICATOR
     C*
     C                     MOVE 'Y'       PRIN
     C*
     C                     EXCPTOTPAYO
     C*
     C                     ADD  1         TOTREC           RECORD
     C*                                                    OUTPUT
     C**   COMIT CHANGES TO THE FILE
     C*
     C                     MOVE TIME      TIMEX
     C                     MOVELPREF      CFLD7
     C*
      **                                                                                      CSC022
      ** Execute commit if SAR CSC022 is not enrolled or                                      CSC022
      **   job is not currently running in batch mode                                         CSC022
      **                                                                                      CSC022
     C           CSC022    IFEQ 'N'                                                           CSC022
     C           CSC022    OREQ 'Y'                                                           CSC022
     C           WCRSKP    ANDEQ'N'                                                           CSC022
     C           CMTTXT    COMIT
     C                     ENDIF                                                              CSC022
     C*                                                                **
     C                     ENDSR                                        *
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C**   PROCESSING OF A 'CQPACDD' RECORD
     C*
     C           CQPACS    BEGSR
     C*
     C           KCQPAC    CHAINCQPAC                88    CHAIN FAIL
     C*
     C           *IN88     IFEQ '1'                        ### B001
     C                     MOVE 'CQPAC'   WBFILE  5        ***************
     C                     MOVELPREF      WBKEY  28        ** DBERR  02 **
     C                     Z-ADD02        WBASE   20       ***************
     C                     EXSR ABORT
     C                     END                             ### E001
     C*
     C**   UPDATE THE P/R INDICATOR
     C*
     C                     MOVE 'Y'       PRIN
     C*
     C                     EXCPTCQPACO
     C*
     C                     ADD  1         TOTREC           RECORD
     C*                                                    OUTPUT
     C**   COMIT CHANGES TO THE FILE
     C*
     C                     MOVE TIME      TIMEX
     C                     MOVELPREF      CFLD7
     C*
      **                                                                                      CSC022
      ** Execute commit if SAR CSC022 is not enrolled or                                      CSC022
      **   job is not currently running in batch mode                                         CSC022
      **                                                                                      CSC022
     C           CSC022    IFEQ 'N'                                                           CSC022
     C           CSC022    OREQ 'Y'                                                           CSC022
     C           WCRSKP    ANDEQ'N'                                                           CSC022
     C           CMTTXT    COMIT
     C                     ENDIF                                                              CSC022
     C*                                                                **
     C                     ENDSR                                        *
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C**   PROCESSING OF A 'NTRANDD' RECORD
     C*
     C           NTRANS    BEGSR
     C*
     C           TFRF      CHAINNTRAN                88    CHAIN FAIL
     C*
     C           *IN88     IFEQ '1'                        ### B001
     C                     MOVE 'NTRAN'   WBFILE  5        ***************
     C                     MOVELTFRF      WBKEY  28        ** DBERR  03 **
     C                     Z-ADD03        WBASE   20       ***************
     C                     EXSR ABORT
     C                     END                             ### E001
     C*
     C**   UPDATE THE P/R INDICATOR
     C*
     C                     MOVE 'Y'       PRIN
     C*
     C                     EXCPTNTRANO
     C*
     C                     ADD  1         TOTREC           RECORD
     C*                                                    OUTPUT
     C**   COMIT CHANGES TO THE FILE
     C*
     C                     MOVE TIME      TIMEX
     C                     MOVELPREF      CFLD7
     C*
      **                                                                                      CSC022
      ** Execute commit if SAR CSC022 is not enrolled or                                      CSC022
      **   job is not currently running in batch mode                                         CSC022
      **                                                                                      CSC022
     C           CSC022    IFEQ 'N'                                                           CSC022
     C           CSC022    OREQ 'Y'                                                           CSC022
     C           WCRSKP    ANDEQ'N'                                                           CSC022
     C           CMTTXT    COMIT
     C                     ENDIF                                                              CSC022
     C*                                                                **
     C                     ENDSR                                        *
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C*  FUNCTION : TO ABORT THE PROGRAM IN THE EVENT OF A DATABASE
     C*             ERROR.
     C*
     C           ABORT     BEGSR
     C*
     C** SETON INDICATORS TO INDICATE DATABASE ERRORS
     C*
     C                     SETON                     U7U8LR
     C*
     C           *NAMVAR   DEFN LDA       DLDA
     C           *LOCK     IN   DLDA
     C                     MOVEL'FT0680'  DBPGM
     C                     MOVELWBKEY     DBKEY
     C                     MOVELWBFILE    DBFILE
     C                     MOVELWBASE     DBASE
     C                     OUT  DLDA
     C*
     C** REPORT DATABASE ERROR
     C*
     C*********************WRITEP1CONT                                    S01117
     C                     WRITEAUCONT                                    S01117
     C*
     C*********************MOVELDBASE     P2DBAS                          S01117
     C*********************MOVELDBFILE    P2DBFL                          S01117
     C*********************MOVELDBKEY     P2DBKY                          S01117
     C*********************WRITEP1DBER                                    S01117
     C                     MOVELDBASE     AUDBAS                          S01117
     C                     MOVELDBFILE    AUDBFL                          S01117
     C                     MOVELDBKEY     AUDBKY                          S01117
     C                     WRITEAUDBER                                    S01117
     C*
     C** PRODUCE PROGRAM DUMP
     C*
     C                     DUMP
     C*
     C** END PROGRAM
     C*
     C                     RETRN
     C*
     C           UEEND     ENDSR
     C********************************************************************
     OOTPAYDDFE                OTPAYO
     O                         PRIN
     O*
     OCQPACDDFE                CQPACO
     O                         PRIN
     O*
     ONTRANDDFE                NTRANO
     O                         PRIN
     O*
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
