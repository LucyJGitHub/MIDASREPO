     H        1
      *****************************************************************
/*XBIA*  OVRDBF INPAYJ2 INPAYXJ0                                      *
/*XBIB*  OVRDBF OTPAYJ2 OTPAYXJ0                                      *
/*XBIC*  OVRDBF FT101X0 FT101HPD                                      *
/*XBID*  OVRDBF FT102X0 FT102HPD                                      *
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FT Mark payments for authorisation')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module.                               *
      *                                                               *
      *  FT0806 - Mark Payments for Authorisation                     *
      *                                                               *
      *  Function: To update the batch number in OTPAYXPD & INPAYXPD  *
      *                                                               *
      *  Called by: FTC0803 - Reporting/Authorising Program           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CFT162             Date 08Sep17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CSW209             Date 01Apr09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 07Jul06               *
      *                 221761             Date 01Apr04               *
      * Midas Release 4.01.03 ----------------------------------------*
      *                 211662             Date 20Nov02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CFT009             Date 05Jun00               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 185107             Date 17Oct00               *
      *                 184060             Date 22Sep00               *
      *                 CFT014             Date 25May00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CFT006             Date 17Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CFT003  *CREATE    Date 06May98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFT162 - MT900 and MT910 Message Generation (Recompile)      *
      *  CSW209 - SWIFT 2009 Changes (Recompile)                      *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  221761 - Recompiled due to change made in PF/FT101HPD.       *
      *  211662 - Call QSYS/QUSRJOBI to retrieve the username that    *
      *           submitted job FTC0803.                              *
      *  CFT009 - Addition of fields for FT fees and charges          *
      *           Recompile                                           *
      *  185107 - Mapping of details from swift message - Recompile   *
      *  184060 - No transaction type code in detail screen -Recompile*
      *  CFT014 - Straight-through Processing Phase 2 MT103 Messages  *
      *           Generation for FT. Recompile only.                  *
      *  CFT006 - Funds Transfer addition of MT101 and MT102 messages *
      *  CFT003 - FT Batch Authorisation                              *
      *           Based on FTH006 original references:                *
      *           CFT078/IMP060/HUB01B                                *
      *                                                               *
      *****************************************************************
      *
     FINPAYJ2 IF  E           K        DISK
     FOTPAYJ2 IF  E           K        DISK
     FFT101X0 IF  E           K        DISK                               CFT006
     FFT102X0 IF  E           K        DISK                               CFT006
     FFT101DL2IF  E           K        DISK                               CFT006
     FFT102DL1IF  E           K        DISK                               CFT006
     FFT101HL3UF  E           K        DISK                               CFT006
     F                                              KCOMIT                CFT006
     F            FT101HD0                          KRENAMEFT101H         CFT006
     FFT102HL3UF  E           K        DISK                               CFT006
     F                                              KCOMIT                CFT006
     F            FT102HD0                          KRENAMEFT102H         CFT006
     FINPAYXL0UF  E           K        DISK
     F                                              KCOMIT
     FOTPAYXL0UF  E           K        DISK
     F                                              KCOMIT
     FFTCONTL0UF  E           K        DISK                      A
     F                                              KCOMIT
      *
     F/EJECT
      *****************************************************************
      *                                                               *
      * ID F  C  H  L    FUNCTION OF INDICATORS                       *
      *                                                               *
      *       10         READ on INPAYJ2                              *
      *       11         CHAIN on INPAYXL0                            *
      *       12         READ on OTPAYJ2                              *
      *       13         CHAIN on OTPAYXL0                            *
      *       14         READ on FT101X0                              *   CFT006
      *       15         CHAIN on FT101HL3                            *   CFT006
      *       16         READ on FT102X0                              *   CFT006
      *       17         CHAIN on FT102HL3                            *   CFT006
      *       18         CHAIN on FT101DL2                            *   CFT006
      *       19         CHAIN on FT102DL1                            *   CFT006
      *                                                               *
      *                                                               *
      *****************************************************************
     F/SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   -  initialise the program.                            *
      *  #HUPD  -  Update the Batch number field.                     *
      *  *PSSR  -  Error.                                             *
      *                                                               *
      *****************************************************************
     E/EJECT
     E*
     E                    CPY@    1   1 80               Midas copyright  ay
     E*
     IFT102HD0                                                            CFT006
     I              SINST                           CSINST                CFT006
     I              INUSER                          CINUSE                CFT006
     I**  CCT Header                                                      CFT006
     I*                                                                   CFT006
     IFT102H                                                              CFT006
     I              SINST                           DSINST                CFT006
     I**  CCT Header                                                      CFT006
     I*                                                                   CFT006
     IFT102DD0                                                            CFT006
     I              INUSER                          CDINUS                CFT006
     I              INDATE                          CINDAT                CFT006
     I              INTIME                          CINTIM                CFT006
     I              UPDUSR                          CUPDUS                CFT006
     I              UPDDTE                          CUPDDT                CFT006
     I              UPDTIM                          CUPDTI                CFT006
     I**  CCT Detail                                                      CFT006
     I*                                                                   CFT006
     IFT101DD0                                                            CFT006
     I              INUSER                          RDINUS                CFT006
     I              INDATE                          RINDAT                CFT006
     I              INTIME                          RINTIM                CFT006
     I              UPDUSR                          RUPDUS                CFT006
     I              UPDDTE                          RUPDDT                CFT006
     I              UPDTIM                          RUPDTI                CFT006
     I**  RFT Detail                                                      CFT006
     I*                                                                   CFT006
     I*
     I           UDS
     I**  Data Area Data structure for *LDA
     I*
     I                                      131 140 @WSID
     IFTBDTA      DS                             10
     I                                        1   4 @BANO
     I*
     ILDA         DS                            256
     I**  Local data area data structure
     I*
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I           SDS
     I**  Program status data structure
     I*
     I                                     *PARMS   PGMPRM
     I                                      244 253 WSID
     I                                      254 263 PGMUSR
     I                                      276 281 SDATE
     I*                                                                                       211662
     IP#USRJ      DS                            136                                           211662
      ** QUSRJOBI data structure                                                              211662
      *                                                                                       211662
     I                                      127 136 J#SUSR                                    211662
     I            DS                                                                          211662
     I                                    B   1   40P#LEN                                     211662
      *                                                                   CFT006
     ISCSARD    E DSSCSARDPD                                              CFT006
      * External data structure for SAR file details                      CFT006
      *                                                                   CFT006
     IDSFDY     E DSDSFDY                                                 CFT006
      * DS for access program , short data structure                      CFT006
     C/EJECT
     C*================================================================
     C*  P R O G R A M   S T A R T                                    *
     C*================================================================
     C*
     C**  Initialise program
     C*
     C                     EXSR INIT
     C*
     C**  Update the Batch no. field and User for those transactions
     C**  requested for reporting.
     C*
     C                     EXSR #HUPD
     C*
     C**  End program
     C                     SETON                     LR
     C/EJECT
     C*****************************************************************
     C*   INIT     : Initial processing                               *
     C*   Called by: Main process                                     *
     C*   Calls    : None                                             *
     C*****************************************************************
     C           INIT      BEGSR
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C**  Set up LDA
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'FT0806'  DBPGM
     C                     MOVE *BLANKS   DBASE
     C                     OUT  LDA
     C*
     C           *NAMVAR   DEFN           FTBDTA
     C                     IN   FTBDTA
     C*
      ** Access SAR details file to determine if CFT006 is switched ON    CFT006
      *                                                                   CFT006
     C                     CALL 'AOSARDR0'                                CFT006
     C                     PARM *BLANKS   @RTCD   7                       CFT006
     C                     PARM '*VERIFY' @OPTN   7                       CFT006
     C                     PARM 'CFT006'  @SARD   6                       CFT006
     C           SCSARD    PARM SCSARD    DSFDY                           CFT006
      *                                                                   CFT006
      ** If CFT006 is switched ON                                         CFT006
      *                                                                   CFT006
     C           @RTCD     IFEQ *BLANKS                                   CFT006
     C                     MOVE 'Y'       CFT006  1                       CFT006
     C                     ELSE                                           CFT006
     C                     MOVE 'N'       CFT006                          CFT006
     C                     ENDIF                                          CFT006
      *                                                                   CFT006
      ** Access SAR details file to determine if CFT008 is switched ON    CFT006
      *                                                                   CFT006
     C                     CALL 'AOSARDR0'                                CFT006
     C                     PARM *BLANKS   @RTCD   7                       CFT006
     C                     PARM '*VERIFY' @OPTN   7                       CFT006
     C                     PARM 'CFT008'  @SARD   6                       CFT006
     C           SCSARD    PARM SCSARD    DSFDY                           CFT006
      *                                                                   CFT006
      ** If CFT008 is switched ON                                         CFT006
      *                                                                   CFT006
     C           @RTCD     IFEQ *BLANKS                                   CFT006
     C                     MOVE 'Y'       CFT008  1                       CFT006
     C                     ELSE                                           CFT006
     C                     MOVE 'N'       CFT008                          CFT006
     C                     ENDIF                                          CFT006
     C*
     C                     MOVEL*BLANKS   P#USRJ136                                           211662
     C                     MOVEL*BLANKS   P#JBNM 26                                           211662
      *                                                                                       211662
      ** Determine who submitted batch job FTC0803                                            211662
      *                                                                                       211662
     C                     CALL 'QUSRJOBI'             81                                     211662
     C                     PARM           P#USRJ                                              211662
     C                     PARM 136       P#LEN                                               211662
     C                     PARM 'JOBI0300'P#FTNM  8                                           211662
     C                     PARM '*'       P#JBNM 26                                           211662
     C                     PARM *BLANKS   P#INJI 16                                           211662
      *                                                                                       211662
      ** If no error occurred, move the username who submitted this job                       211662
      ** to field WSUSR (originally from system data struc.)                                  211662
      *                                                                                       211662
     C           *IN81     IFEQ '0'                                                           211662
     C           J#SUSR    ANDNE*BLANKS                                                       211662
     C                     MOVE *BLANKS   PGMUSR                                              211662
     C                     MOVE J#SUSR    PGMUSR                                              211662
     C                     ENDIF                                                              211662
     C*                                                                                       211662
     C                     ENDSR
     C*
     C*
     C/EJECT
     C*****************************************************************
     C*   #HUPD    : Update the batch no. field.                      *
     C*   Called by: Main process                                     *
     C*   Calls    : None                                             *
     C*****************************************************************
     C           #HUPD     BEGSR
     C*
     C** Do while not EOF for INPAYJ2
     C*
     C                     Z-ADD0         @NREC   40
     C*
     C                     READ INPAYJ2                  10
     C*
     C           *IN10     DOWNE'1'
     C*
     C**  Call CL program to check if any user is locking the payment.
     C**  If yes, the payment will not be selected for Batch
     C**  Authorisation.
     C*
     C                     CALL 'AOC8000'
     C                     PARM *BLANKS   @RTCDE  7
     C                     PARM 'FT'      @MODLE  2
     C                     PARM PREF      @TREF  15
     C                     PARM *BLANKS   @RTINF 80
     C*
     C** If no record locking then update batch number.
     C*
     C           @RTCDE    IFNE '*IN USE'
     C*
     C           PREF      CHAININPAYXL0             11
     C*
     C           *IN11     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'INPAYXL0'DBFILE           * DBERR 001   *
     C                     MOVELPREF      DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C           INBANO    IFEQ *BLANKS
     C           INRUSR    ANDEQ*BLANKS
     C                     MOVE @BANO     INBANO
     C                     MOVELPGMUSR    INRUSR
     C                     MOVEL@WSID     INWSID
     C                     UPDATINPAYXDF
     C                     ADD  1         @NREC
     C                     END
     C*
     C                     END
     C*
     C                     READ INPAYJ2                  10
     C*
     C                     END
     C*
     C** Do while not EOF for OTPAYJ2
     C*
     C                     READ OTPAYJ2                  12
     C*
     C           *IN12     DOWNE'1'
     C*
     C**  Call CL program to check if any user is locking the payment.
     C**  If yes, the payment will not be selected for Batch
     C**  Authorisation.
     C*
     C                     CALL 'AOC8000'
     C                     PARM *BLANKS   @RTCDE  7
     C                     PARM 'FT'      @MODLE  2
     C                     PARM PREF      @TREF  15
     C                     PARM *BLANKS   @RTINF 80
     C*
     C** If no record locking then update batch number.
     C*
     C           @RTCDE    IFNE '*IN USE'
     C*
     C           PREF      CHAINOTPAYXL0             13
     C*
     C           *IN11     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVEL'OTPAYXL0'DBFILE           * DBERR 002   *
     C                     MOVELPREF      DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C           OPBANO    IFEQ *BLANKS
     C           OPRUSR    ANDEQ*BLANKS
     C                     MOVE @BANO     OPBANO
     C                     MOVELPGMUSR    OPRUSR
     C                     MOVEL@WSID     OPWSID
     C                     UPDATOTPAYXDF
     C                     ADD  1         @NREC
     C                     END
     C*
     C                     END
     C*
     C                     READ OTPAYJ2                  12
     C*
     C                     END
      *                                                                   CFT006
     C           CFT006    IFEQ 'Y'                                       CFT006
      *                                                                   CFT006
      ** Do while not EOF for FT101X0                                     CFT006
      *                                                                   CFT006
     C                     READ FT101X0                  14               CFT006
      *                                                                   CFT006
     C           *IN14     DOWNE*ON                                       CFT006
      *                                                                   CFT006
     C           RFTID     CHAINFT101HL3             15                   CFT006
      *                                                                   CFT006
     C           *IN15     IFEQ *ON                                       CFT006
     C           *LOCK     IN   LDA                                       CFT006
     C                     Z-ADD3         DBASE            ***************CFT006
     C                     MOVEL'FT101HL3'DBFILE           * DBERR 003   *CFT006
     C                     MOVELRFTID     DBKEY            ***************CFT006
     C                     OUT  LDA                                       CFT006
     C                     EXSR *PSSR                                     CFT006
     C                     ENDIF                                          CFT006
      *                                                                   CFT006
     C           RFTID     CHAINFT101DL2             18                   CFT006
     C           *IN18     IFEQ *OFF                                      CFT006
     C           BAUTH     IFEQ *BLANKS                                   CFT006
     C                     MOVE @BANO     BAUTH                           CFT006
     C                     MOVELPGMUSR    BATUSR                          CFT006
     C                     UPDATFT101H                                    CFT006
     C                     ADD  1         @NREC                           CFT006
     C                     ENDIF                                          CFT006
     C                     ENDIF                                          CFT006
      *                                                                   CFT006
     C                     READ FT101X0                  14               CFT006
      *                                                                   CFT006
     C                     ENDDO                                          CFT006
     C                     ENDIF                                          CFT006
      *                                                                   CFT006
      ** Do while not EOF for FT102X0                                     CFT006
      *                                                                   CFT006
     C           CFT008    IFEQ 'Y'                                       CFT006
     C                     READ FT102X0                  16               CFT006
      *                                                                   CFT006
     C           *IN16     DOWNE*ON                                       CFT006
      *                                                                   CFT006
     C           CCTID     CHAINFT102HL3             17                   CFT006
      *                                                                   CFT006
     C           *IN17     IFEQ *ON                                       CFT006
     C           *LOCK     IN   LDA                                       CFT006
     C                     Z-ADD4         DBASE            ***************CFT006
     C                     MOVEL'FT102HL3'DBFILE           * DBERR 004   *CFT006
     C                     MOVELCCTID     DBKEY            ***************CFT006
     C                     OUT  LDA                                       CFT006
     C                     EXSR *PSSR                                     CFT006
     C                     ENDIF                                          CFT006
      *                                                                   CFT006
     C           CCTID     CHAINFT102DL1             19                   CFT006
     C           *IN19     IFEQ *OFF                                      CFT006
     C           BAUTH     IFEQ *BLANKS                                   CFT006
     C                     MOVE @BANO     BAUTH                           CFT006
     C                     MOVELPGMUSR    BATUSR                          CFT006
     C                     UPDATFT102H                                    CFT006
     C                     ADD  1         @NREC                           CFT006
     C                     ENDIF                                          CFT006
     C                     ENDIF                                          CFT006
      *                                                                   CFT006
     C                     READ FT102X0                  16               CFT006
      *                                                                   CFT006
     C                     ENDDO                                          CFT006
      *                                                                   CFT006
     C                     ENDIF                                          CFT006
     C*
     C**  Check if a record for the user exists in FTCONTPD. If there
     C**  is, update that record else add a new record.
     C*
     C           @NREC     IFNE 0
     C*
     C           PGMUSR    CHAINFTCONTL0             20
     C*
     C                     MOVELPGMUSR    COUSER
     C                     MOVE @BANO     COBATN
     C                     MOVEL@WSID     COTMID
     C                     Z-ADD@NREC     CONREC
     C*
     C           *IN20     IFEQ '1'
     C                     WRITE@CONTPD
     C                     ELSE
     C                     UPDAT@CONTPD
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*   *PSSR - error handling                                      *
     C*   Called by - anywhere within the program                     *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     DUMP
     C                     RETRN
     C*
     C                     ENDSR
     C*
     C*****************************************************************
** CPY@   **      Object copyright
(c) Misys International Banking Systems Ltd. 2001
