     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FT De-select by user prompt')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  FT0808 - DE-SELECT BY USER from Authorisation Batch.         *
      *                                                               *
      *  Function:  This de-selects transactions by USER name.        *
      *                                                               *
      *  Called By: On Request                                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CSW209             Date 01Apr09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 221761             Date 01Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CFT009             Date 30May00               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 185107             Date 17Oct00               *
      *                 CFT014             Date 25May00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CFT006             Date 26Oct99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CFT003  *CREATE    Date 24Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *  CSW209 - SWIFT 2009 Changes (Recompile)                      *
      *  221761 - Recompiled due to change made in PF/FT101HPD.       *
      *  CFT009 - Addition of fields for FT fees and charges          *
      *           Recompile                                           *
      *  185107 - Mapping of details from swift message - Recompile   *
      *  CFT014 - Straight-through Processing Phase 2 MT103 Messages  *
      *           Generation for FT. Recompile only.                  *
      *  CFT006 - Funds Transfer addition of MT101 and MT102 messages *
      *  CFT003 - FT Batch Authorisation                              *
      *           Based on FTH008 original references HUB01B/093404   *
      *                                                               *
      *****************************************************************
     FINOTPXL0UF  E           K        DISK
     F                                              KINFDS INFDS
     F                                              KINFSR INFSR
     F                                              KCOMIT
     FFTCONTL0UF  E           K        DISK
     F                                              KINFSR INFSR
     F                                              KCOMIT
     FFT101HL7UF  E           K        DISK                               CFT006
     F                                              KINFSR INFSR          CFT006
     F                                              KCOMIT                CFT006
     FFT102HL4UF  E           K        DISK                               CFT006
     F                                              KINFSR INFSR          CFT006
     F                                              KCOMIT                CFT006
     FFT0808DFCF  E                    WORKSTN
     F*****************************************************************
     F/EJECT
     F* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     F*                                                               *
     F*       U7         System error, wrong status                   *
     F*       U8         Database error                               *
     F*                                                               *
     F*****************************************************************
     F/SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*   INIT  - Initialisation processing                           *
     F*   DETL  - Process detail                                      *
     F*   TERM  - Terminate program                                   *
     F*   *PSSR - Error                                               *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E*
     E                    CPY@    1   1 80                   COPYRIGHT
     I/EJECT
     IFT102HD0                                                            CFT006
     I              SINST                           CSINST                CFT006
     I**  CCT Header                                                      CFT006
     I*                                                                   CFT006
     ILDA         DS                            256
     I**  Local data area data structure
     I*
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     IDSRUN       DS                             13
     I**  RUNDAT data area data structure (date format -ddmmmyy)
     I*
     I                                        1   7 RDAT
     I                                        1   2 @DD
     I                                        3   5 @MMM
     I                                        6   7 @YY
     I                                       12  12 DATF
     IINFDS       DS
     I                                     *STATUS  STATUS
     I           SDS
     I**  Program status data structure
     I*
     I                                     *PARMS   PGMPRM
     I                                      244 253 PGMDEV
     I                                      254 263 PGMUSR
      *                                                                   CFT006
     ISCSARD    E DSSCSARDPD                                              CFT006
      * External data structure for SAR file details                      CFT006
      *                                                                   CFT006
     IDSFDY     E DSDSFDY                                                 CFT006
      * DS for access program , short data structure                      CFT006
     I*
     C/EJECT
     C*================================================================
     C*  P R O G R A M   S T A R T                                    *
     C*================================================================
     C                     MOVEACPY@      BIS@   80
     C*
     C** Establish the message file
     C*
     C                     MOVE *BLANK    ZAMSID 10
     C                     MOVEL'GBFTUSR' @MSGF  10
     C                     MOVE 'MSG'     @MSGF
     C*
     C** ZA0340 Error
     C           #AERR     PLIST
     C                     PARM @MSGF     ZAMSGF 10        Message file
     C                     PARM           ZAMSID 10        Message ID
     C*
     C                     MOVEL'FT0808'  PGMQ   10
     C                     MOVEL*BLANKS   MSGKEY  4
     C*
     C/EJECT
     C*****************************************************************
     C*   MAIN  :    Main process                                     *
     C*   Calls :    INIT - Init process                              *
     C*              DETL - Detail processing                         *
     C*              TERM - Termination processing                    *
     C*****************************************************************
     C*
     C**  Perform initial process
     C*
     C                     EXSR INIT
     C*
     C**  Perform detail process
     C*
     C                     EXSR DETL
     C*
     C**  Perform termination process
     C*
     C                     EXSR TERM
     C/EJECT
     C*****************************************************************
     C*   INIT     : Initial processing                               *
     C*   Called by: Main process                                     *
     C*****************************************************************
     C           INIT      BEGSR
     C*
     C**  Set up LDA
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'FT0808'  DBPGM
     C                     MOVE *BLANKS   DBASE
     C                     OUT  LDA
     C*
     C**  Access data area DSRUN to find date
     C*
     C           *NAMVAR   DEFN RUNDAT    DSRUN
     C                     IN   DSRUN
     C*
     C**  If data area not found then perform database error processing
     C*
     C           RDAT      IFEQ *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'RUNDAT'  DBFILE           * DBERR 001   *
     C                     MOVEL'FIRST'   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
      ** Access SAR details file to determine if CFT006 is switched ON    CFT006
      *                                                                   CFT006
     C                     CALL 'AOSARDR0'                                CFT006
     C                     PARM *BLANKS   @RTCD   7                       CFT006
     C                     PARM '*VERIFY' @OPTN   7                       CFT006
     C                     PARM 'CFT006'  @SARD   6                       CFT006
     C           SCSARD    PARM SCSARD    DSFDY                           CFT006
      *                                                                   CFT006
      ** If CFT006 is switched ON                                         CFT006
      *                                                                   CFT006
     C           @RTCD     IFEQ *BLANKS                                   CFT006
     C                     MOVE 'Y'       CFT006  1                       CFT006
     C                     ELSE                                           CFT006
     C                     MOVE 'N'       CFT006                          CFT006
     C                     ENDIF                                          CFT006
      *                                                                   CFT006
      ** Access SAR details file to determine if CFT008 is switched ON    CFT006
      *                                                                   CFT006
     C                     CALL 'AOSARDR0'                                CFT006
     C                     PARM *BLANKS   @RTCD   7                       CFT006
     C                     PARM '*VERIFY' @OPTN   7                       CFT006
     C                     PARM 'CFT008'  @SARD   6                       CFT006
     C           SCSARD    PARM SCSARD    DSFDY                           CFT006
      *                                                                   CFT006
      ** If CFT008 is switched ON                                         CFT006
      *                                                                   CFT006
     C           @RTCD     IFEQ *BLANKS                                   CFT006
     C                     MOVE 'Y'       CFT008  1                       CFT006
     C                     ELSE                                           CFT006
     C                     MOVE 'N'       CFT008                          CFT006
     C                     ENDIF                                          CFT006
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*   DETL     : Detail processing                                *
     C*   Called by: Main process                                     *
     C*   Calls    : INIT - Init process                              *
     C*****************************************************************
     C           DETL      BEGSR
     C*
     C                     SETON                         29
     C*
     C**  Do while not F3 or errors outstanding
     C*
     C           *INKC     DOWEQ'0'
     C           *IN29     ANDEQ'1'
     C*
     C                     SETOF                         11
     C                     WRITEINITSCN
     C                     WRITEFOOTER
     C                     WRITESFLMSGC
     C                     CALL 'ZA0250'
     C                     EXFMTPROMPT
     C*
     C** Validate Authorise User name.
     C*
     C                     MOVE '0'       *IN29
     C*
     C           *INKC     IFNE '1'
     C*
     C** Check there are transactions selected by this user.
     C*
     C           SUSER     IFEQ *BLANKS
     C                     MOVEL'U1B0029' ZAMSID
     C                     CALL 'ZA0340'  #AERR
     C                     SETON                     29
     C                     ELSE
     C           SUSER     CHAINOTPAYXDF             15    Rtv Record
     C           SUSER     CHAININPAYXDF             10    Rtv Record
     C           SUSER     CHAINFT101HL7             20    Rtv Record     CFT006
     C           SUSER     CHAINFT102HL4             25    Rtv Record     CFT006
     C           *IN15     IFEQ '1'
     C           *IN10     ANDEQ'1'
     C           *IN20     ANDEQ*ON                                       CFT006
     C           *IN25     ANDEQ*ON                                       CFT006
     C                     MOVEL'U1B0026' ZAMSID
     C                     CALL 'ZA0340'  #AERR
     C                     MOVE '1'       *IN29
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C**  If no error, display confirmation screen.
     C*
     C           *IN29     IFNE '1'
     C*
     C           *INKF     DOWEQ'0'
     C           *INKC     ANDEQ'0'
     C           *INKL     ANDEQ'0'
     C                     SETON                     11
     C                     SETOF                     10
     C                     WRITEFOOTER
     C                     WRITEPROMPT
     C                     WRITESFLMSGC
     C                     EXFMTMSGDSP
     C*
     C** If F6 UPDATE FILE.
     C*
     C           *INKF     IFEQ '1'
     C           *IN10     IFNE '1'
     C           SUSER     SETLLINPAYXDF                   Postn File
     C*
     C                     READ INPAYXDF                 30Rtv Record
     C*
     C           SUSER     DOWEQINRUSR                     DoWhile Eq User
     C           *IN30     ANDNE'1'
     C                     MOVE *BLANKS   INRUSR           Reset Rpt User
     C                     MOVE *BLANKS   INBANO           Reset Batch Nbr
     C                     MOVE *BLANKS   INWSID           Reset WrkStn Id
     C                     UPDATINPAYXDF                   Upd Record
     C                     READ INPAYXDF                 30Rtv Record
     C                     END
     C                     END
     C*
     C           *IN15     IFNE '1'
     C           SUSER     SETLLOTPAYXDF                   Postn File
     C*
     C                     READ OTPAYXDF                 31Rtv Record
     C*
     C           SUSER     DOWEQOPRUSR                     DoWhile Eq User
     C           *IN31     ANDNE'1'
     C                     MOVE *BLANKS   OPRUSR           Reset Rpt User
     C                     MOVE *BLANKS   OPBANO           Reset Batch Nbr
     C                     MOVE *BLANKS   OPWSID           Reset WrkStn Id
     C                     UPDATOTPAYXDF                   Upd Record
     C                     READ OTPAYXDF                 31Rtv Record
     C                     END
     C                     END
     C*                                                                   CFT006
     C           CFT006    IFEQ 'Y'                                       CFT006
     C           *IN20     IFEQ *OFF                                      CFT006
     C           SUSER     SETLLFT101HL7                   Postn File     CFT006
     C*                                                                   CFT006
     C                     READ FT101HL7                 32Rtv Record     CFT006
     C*                                                                   CFT006
     C           SUSER     DOWEQBATUSR                     DoWhile Eq UserCFT006
     C           *IN32     ANDEQ*OFF                                      CFT006
     C                     MOVE *BLANKS   BATUSR           Reset Rpt User CFT006
     C                     MOVE *BLANKS   BAUTH            Reset Batch NbrCFT006
     C                     UPDATFT101HD0                   Upd Record     CFT006
     C                     READ FT101HL7                 32Rtv Record     CFT006
     C                     ENDDO                                          CFT006
     C                     ENDIF                                          CFT006
     C                     ENDIF                                          CFT006
     C*                                                                   CFT006
     C           CFT008    IFEQ 'Y'                                       CFT006
     C           *IN25     IFEQ *OFF                                      CFT006
     C           SUSER     SETLLFT102HL4                   Postn File     CFT006
     C*                                                                   CFT006
     C                     READ FT102HL4                 33Rtv Record     CFT006
     C*                                                                   CFT006
     C           SUSER     DOWEQBATUSR                     DoWhile Eq UserCFT006
     C           *IN33     ANDEQ*OFF                                      CFT006
     C                     MOVE *BLANKS   BATUSR           Reset Rpt User CFT006
     C                     MOVE *BLANKS   BAUTH            Reset Batch NbrCFT006
     C                     UPDATFT102HD0                   Upd Record     CFT006
     C                     READ FT102HL4                 33Rtv Record     CFT006
     C                     ENDDO                                          CFT006
     C                     ENDIF                                          CFT006
     C                     ENDIF                                          CFT006
     C*
     C**  Blank out fields in control file FTCONTDP.
     C*
     C           SUSER     CHAINFTCONTL0             35    Rtv Record
     C           *IN35     IFEQ '0'
     C                     MOVE *BLANKS   COBATN           Reset Batch Nbr
     C                     MOVE *BLANKS   COTMID           Reset Terminal
     C                     Z-ADD0         CONREC           Reset Nbr Recs
     C                     MOVE *BLANKS   COSTAT           Reset Status
     C                     UPDAT@CONTPD                    Upd Record
     C                     END
     C*
     C                     MOVE *BLANKS   SUSER
     C                     SETON                         29
     C*
     C                     END
     C*
     C** If F12 re-display initial screen.
     C*
     C           *INKL     IFEQ '1'
     C                     MOVE *BLANKS   SUSER
     C                     SETON                     29
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*   TERM     : Termination Processing                           *
     C*   Called by: Main Process                                     *
     C*****************************************************************
     C           TERM      BEGSR
     C*
     C                     COMIT
     C                     SETON                     LR
     C                     ENDSR
     C/EJECT
     C**************************************************************************
     C*   *PSSR - error handling                                               *
     C*   Called by - INIT initial process                                     *
     C**************************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
     C*
     C                     ENDSR
     C*****************************************************************
     C*   INFSR - error handling                                      *
     C*****************************************************************
     C*
     C           INFSR     BEGSR
     C*
     C                     ROLBK
     C           STATUS    IFEQ 01218
     C                     MOVE '*DETC '  EXTTAG  6
     C                     ELSE
     C                     MOVE '*CANCL'  EXTTAG
     C                     END
     C*
     C                     ENDSREXTTAG
     C*****************************************************************
     C/EJECT
** CPY@   **      Object copyright
(c) Misys International Banking Systems Ltd. 2001
