      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FX Complete Deal Amendable Fields Maint')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Foreign Exchange (Dealing)
     A*                                                               *
     A*     DSPF/FX0030DD - COMPLETE DEAL AMENDABLE FIELDS            *
     A*                     MAINTENANCE                               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 090199             Date 12JUL95               *
     A*  PREV AMEND NO. S01319             DATE 19NOV91               *
     A*       AMEND NO. E23462             DATE  1OCT90               *
     A*                 S01253             DATE 17APR90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  S01319  -  Changes for Project Harry - add help              *
     A*  E23462  -  Remove all references to 'DRS'/'TS'               *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*                                                              *
     A****************************************************************
     A*                                                               *
     A**  Indicator Function Summary
     A*
     A* ID F  C  H  L    FUNCTION OF INDICATORS
     A* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
     A*
     A*       KA         Exit program.
     A*
     A*       10         Controls SFLEND on Display File.
     A*
     A*       20         Error on field DDACTN.
     A*       21         Error on field DDBRCA.
     A*       22         Error on field DDCNMA.
     A*       23         Error on field DDBKRA.
     A*       24         Error on field DDBKGA.
     A*       25         Error on field DDDLSA.
     A*                                                               *
     A*****************************************************************
     A*
     A*       29         Error has occurred in screen validation.
     A*                                                               *
     A*****************************************************************
     A*
     A*
     A*
     A*
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      MSGLOC(22)
      *                          Beginning of change       /S01319/
      *                                End of change       /S01319/
     A*
     A          R FX0030S1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A          R FX0030S0                  TEXT('MAINTENANCE SCREEN')
     A                                      SFLCTL(FX0030S1)
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A                                  1  2'Ref: FX0030'
      *                                                  START OF CHANGE  E23462
     A**********************************1 28'MIDAS DEALING ROOM SYSTEM'
     A                                  1 26'            Midas            '
     A**************************************DSPATR(UL HI)
     A                                      DSPATR(HI)
      *                                                     END OF CHANGE E23462
     A            DDTIME    R           1 63REFFLD(TIME)
     A                                      EDTWRD('0 :  :  ')
     A                                      OVRDTA
     A                                  1 72'WID:'
     A            DDWSID    R           1 77REFFLD(WSID)
     A*
     A                                  3 26'FX DEALER -'
     A                                  3 38'DEAL AMENDABILITY'
     A                                  3  2'Action:'
     A*
     A*
     A*
     A*****************************************************************
     A*****************************************************************
     A*
     A            DDACTN    R        B  3 10REFFLD(ACTN)
     A N29
     AO 20                                  DSPATR(PC)
     A  20                                  DSPATR(RI)
     A  20                                  OVRATR
     A N20                                  OVRDTA
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*****************************************************************
     A*
     A*
     A            DDLIST    R           3 12REFFLD(LIST)
     A                                      OVRDTA
     A*
     A*
     A*
     A*
     A*
     A                                  7 26'Branch'
     A            DDBRCA    R        B  7 40REFFLD(BRNA)
     A  21                                  DSPATR(PC RI)
     A  21                                  OVRATR
     A N21                                  OVRDTA
     A*
     A                                  9 26'Customer'
     A            DDCNMA    R        B  9 40REFFLD(CNMA)
     A  22                                  DSPATR(PC RI)
     A  22                                  OVRATR
     A N22                                  OVRDTA
     A*
     A                                 11 26'Broker'
     A            DDBKRA    R        B 11 40REFFLD(BKRA)
     A  23                                  DSPATR(RI PC)
     A  23                                  OVRATR
     A N23                                  OVRDTA
     A*
     A                                 13 26'Brokerage'
     A            DDBKGA    R        B 13 40REFFLD(BKGA)
     A  24                                  DSPATR(RI PC)
     A  24                                  OVRATR
     A N24                                  OVRDTA
     A*
     A                                 15 26'Sub-Type'
     A            DDDLSA    R        B 15 40REFFLD(DLSA)
     A  25                                  DSPATR(RI PC)
     A  25                                  OVRATR
     A N25                                  OVRDTA
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*
     A*
     A          R FX0030D0                  TEXT('MAIN SCREEN FOOTER')
     A                                      OVERLAY
     A                                      BLINK
      *                          Beginning of change       /S01319/
      *                                End of change       /S01319/
     A                                 24  1'F3-End'
