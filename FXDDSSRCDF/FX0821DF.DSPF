      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FX Transactions')                                *
      *****************************************************************
      *                                                               *
      *  Midas - Foreign Exchange (Dealing)                           *
      *                                                               *
      *  FX0821DF - FX Transactions Extension Details                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. LUC139             Date 15Jan21               *
      *  Prev Amend No. CER009  *CREATE    Date 16Aug06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  CER009 - Puma II Upgrade to MidasPlus                        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
      *
     A          R FX0821F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                      KEEP
     A                                      PUTOVR
     A                                      OVERLAY
     A                                      BLINK
     A                                      CF03
     A                                      CF12
      *
     A            ##USR         10      1  2
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                  1 66'DATE'
     A            ##MRDT         7      1 72TEXT('midas rundate')
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                  2 28'European Reporting - Italy'
     A                                      DSPATR(HI)
     A                                  2 66'TIME'
     A            #0TME          6  0   2 71
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  2'Motivation Of Operation . . :'
     A            #0MOOP         1A  O  4 32DSPATR(HI)
      *
     A                                  5  2'Type of Product . . . . . . :'
     A            #0TPPR         6A  O  5 32DSPATR(HI)
      *
     A                                  6  2'Personalisation of Operation:'
     A            #0PEPR         6A  O  6 32DSPATR(HI)
      *
     A                                  7  2'Global/Partial Operation  . :'
     A            #0GLPA         1A  O  7 32DSPATR(HI)
      *
     A                                  8  2'Operation with Third Party  :'
     A            #0TPFD         1A  O  8 32DSPATR(HI)
      *---------------------------------------------------------------*
      ** Main Screen Footer                                           *
      *---------------------------------------------------------------*
     A                                 22  2'F3=Exit   F12=Previous'
     A                                      COLOR(BLU)
      *---------------------------------------------------------------*
      ** Message Subfile Format                                       *
      *---------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(10)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
      *---------------------------------------------------------------*
      ** Message Subfile Control                                      *
      *---------------------------------------------------------------*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
