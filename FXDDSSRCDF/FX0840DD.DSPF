      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FX Unauthorised FX Deals Enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Foreign Exchange (Dealing)                       *
     A*                                                               *
     A*  DSPF/FX0840DD - Unauthorised Deals Enquiry                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 066872 *CREATE     DATE 26JUL94               *
     A*  PREV AMEND NO. 000000             DATE DDMMMYY               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  066872 - Include FX/MM Unauthorised Deals Enquiry.           *
     A*           Display file created.                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  F U N C T I O N   O F   I N D I C A T O R S                  *
     A*                                                               *
     A*   10  - Controls SFLEND on message sub-file.                  *
     A*                                                               *
     A*   20  - Error on field DDDSTS.                                *
     A*   21  - Error on field DDDUSC.                                *
     A*   22  - Error on field DDDN38.                                *
     A*   29  - Error has occurred in screen validation.              *
     A*                                                               *
     A*   41  - Controls SFLCLR on select screen.                     *
     A*   43  - Subfile being displayed                               *
     A*   46  - F12 narrative displayed.                              *
     A*                                                               *
     A*   50  - ROLLUP key pressed and more forward records in        *
     A*         subfile                                               *
     A*   51  - ROLLDOWN key pressed and more previous records in     *
     A*         subfile.                                              *
     A*                                                               *
     A*   70  - Controls SFLEND on main sub-file.                     *
     A*   71  - ROLLUP key control.                                   *
     A*   73  - ROLLDOWN key control.                                 *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      CA12
     A                                      MSGLOC(23)
     A*
     A*
     A          R FX0840S1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A          R FX0840S0                  TEXT('ENQUIRY SCREEN')
     A                                      SFLCTL(FX0840S1)
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A N42                                  SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      BLINK
     A                                      OVERLAY
     A            DDPGMQ                    SFLPGMQ
     A                                 24  4'F3 for End of Job'
     A                                 24 29'F12 for New Enquiry'
     A N46                                  DSPATR(ND)
     A*
     A          R FX0840S3                  TEXT('Enquiry Subfile')
     A                                      SFL
     A            DDDNO2    R        O  7  2REFFLD(DN38)
     A            DDAMNT        18   O  7  9
     A            DDCCY1    R        O  7 28REFFLD(CCY1)
     A            DDCCY2    R        O  7 32REFFLD(CCY2)
     A            DDVDAT         7   O  7 36
     A            DDDOED         7   O  7 44
     A            DDSTAT         3   O  7 52
     A            DDCSNM    R        O  7 56REFFLD(CSNM)
     A            DDXRAT         9      7 67
     A            DDDLR2    R        O  7 77REFFLD(DUSC)
     A*
     A          R FX0840S2                  TEXT('Enquiry Screen')
     A                                      SFLCTL(FX0840S3)
     A                                      SFLSIZ(17)
     A                                      SFLPAG(16)
     A N41 43                               SFLDSP
     A N41                                  SFLDSPCTL
     A  41                                  SFLCLR
     A  70                                  SFLEND
     A N71                                  ROLLUP(50)
     A N73                                  ROLLDOWN(51)
     A                                      BLINK
     A            @RRN           3  0H      SFLRCDNBR(CURSOR)
     A                                  1  2'Ref: FX0840'
     A                                  1 21'FX DEALER - UNAUTHORISED DEALS'
     A                                      DSPATR(UL HI)
     A            DDDATE         7   O  1 61
     A            DDTIME         4  0O  1 70
     A                                      EDTWRD('0 :  ')
     A            DDWID          3   O  1 77
     A                                  3  3'Incomplete/Complete:'
     A                                      DSPATR(HI)
     A            DDDSTS    R        B  3 26REFFLD(DSTS)
     A                                      TEXT('Incomplete/Complete')
     A                                      DSPATR(HI)
     A  20                                  DSPATR(PC)
     A  20                                  DSPATR(RI)
     A N20                                  DSPATR(UL)
     A                                  3 33'Dealer:'
     A                                      DSPATR(HI)
     A            DDDUSC    R        B  3 44REFFLD(DUSC)
     A                                      DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                  3 52'Deal Number:'
     A                                      DSPATR(HI)
     A            DDDN38    R        B  3 68REFFLD(DN38)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A                                  5  2'Deal No.  Deal Amount'
     A                                  5 28'1CY 2CY Val Dat Opt Dat Stat'
     A                                  5 57'Customer  Rate   Dealer'
     A                                  6 44'/Ass Dl Inds'
