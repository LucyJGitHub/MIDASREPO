      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FX Net summary/detail totals screen')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module - Foreign Exchange.                   *
      *                                                               *
      *  DSPF/FX0910DF - FX Currency Net Detail                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CDL002             Date 07Feb97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL002 - FX Netting                                          *
      *                                                               *
      *****************************************************************
     A* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     A*       30         PROTECT SUBFILE SELECT FIELD                 *
     A*                    AND NON-DISPLAY SUBFILE FIELDS
     A*       31         DISPLAY SUBFILE CONTROL RECORD               *
     A*       32         DISPLAY SUBFILE                              *
     A*       33         INITIALIZE SUBFILE                           *
     A*       34         SFLEND INDICATOR                             *
     A*       35         CLEAR SUBFILE                                *
     A*       36         SFLNXTCHG INDICATOR                          *
     A*       51         SUMMARY DISPLAY CONTROL
     A*       52         DETAIL DISPLAY CONTROL
     A*       53         PAIRED CCY'S SELECTED DISPLAY CONTROL
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      OPENPRT
     A                                      INDARA
     A                                      CA03
     A                                      CF15
     A                                      CA05
     A                                      CF09
     A                                      CA12
     A          R FX0910S1                  SFL
     A                                      CHGINPDFT
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A  36                                  SFLNXTCHG
     A            DDBTOT        20A  H      TEXT('BUY TOTAL')
     A            DDSTOT        20A  H      TEXT('SELL TOTAL')
     A            DDNETT        21A  H      TEXT('Net Total')
     A            DDBMIS        20A  H      TEXT('Buy Mismatched')
     A            DDSMIS        20A  H      TEXT('Sell Mismatched')
     A            DDNMIS        21A  H      TEXT('Net Mismatched')
     A            DDBUNM        20A  H      TEXT('Buy Unmatched')
     A            DDSUNM        20A  H      TEXT('Sell Unmatched')
     A            DDNUNM        21A  H      TEXT('Nett Unmatched')
     A            DDBOUT        20A  H      TEXT('Buy System Total')
     A            DDSOUT        20A  H      TEXT('Sell System Total')
     A            DDNOUT        21A  H      TEXT('Net System Total')
     A            DDBSYS        20A  H      TEXT('Buy System Total')
     A            DDSSYS        20A  H      TEXT('Sell System Total')
     A            DDNSYS        21A  H      TEXT('Net System Total')
     A            DDNMTN        15S 0H      TEXT('Net Matched')
     A            DDCONF         1A  H      TEXT('CONFIRMED')
     A            DDSEL          1A  B  7  2TEXT('SELECT FIELD')
     A  30                                  DSPATR(PR)
     A  25                                  DSPATR(RI)
     A N30                                  DSPATR(UL)
     A N30N25                               DSPATR(HI)
     A N30
     AO 25                                  DSPATR(PC)
     A            DCCYS1         3A  O  7  5
     A N29                                  DSPATR(HI)
     A N53
     AO 30                                  DSPATR(ND)
     A            DCCYS2         3A  O  7  9
     A N29                                  DSPATR(HI)
     A N53
     AO 30                                  DSPATR(ND)
     A            DDCCY          3A  O  7 13
     A N29                                  DSPATR(HI)
     A  30                                  DSPATR(ND)
     A            PCCYS1         3A  H
     A            PCCYS2         3A  H
     A                                  7  2'Matched'
     A            DDBMAT        20   O  7 17TEXT('Buy Matched')
     A            DDSMAT        20   O  7 38TEXT('Sell Matched')
     A            DDNMAT        21   O  7 59TEXT('Net Matched')
     A          R FX0910C1                  SFLCTL(FX0910S1)
     A                                      TEXT('DEALS SUBFILE CONTROL')
     A                                      CF02
     A                                      CF21
     A                                      CF22
     A                                      OVERLAY
     A  32                                  SFLDSP
     A  31                                  SFLDSPCTL
     A  33                                  SFLINZ
     A  35                                  SFLCLR
     A  34                                  SFLEND
     A                                      SFLRNA
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0016)
     A            S1RRND         4S 0H      SFLRCDNBR(CURSOR)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FX0910'
     A                                  1 30'FX CURRENCY NET SUMMARY'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            BJMRDT    R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  3  2'C/Party :'
     A            CUSHNO        10A  B  3 12
     A  71                                  DSPATR(RI)
     A  10
     AO 71                                  DSPATR(PC)
     A N71                                  DSPATR(HI)
     A                                  3 24'OR'
     A                                      DSPATR(HI)
     A                                  3 28'Group :'
     A            GRPNO          4A  B  3 36
     A  71                                  DSPATR(RI)
     A  10
     AO 71                                  DSPATR(PC)
     A N71                                  DSPATR(HI)
      *
     A N81        BBCSSN        10A  O  4  2DSPATR(HI)
     A N81        BBCRNM        20A  O  4 17DSPATR(HI)
     A  81                              4  9'Description'
     A  81        D6GPDS        30A  O  4 21DSPATR(HI)
     A N81        BBCRTN        10A  O  4 42DSPATR(HI)
     A                                  4 54'Pair ccys'
     A            DDPCCY         1A  B  4 64
     A  69                                  DSPATR(RI)
     A  10
     AO 69                                  DSPATR(PC)
     A N69                                  DSPATR(HI)
     A                                  4 66'Value'
     A            DDVALD         6A  B  4 72
     A  68                                  DSPATR(RI)
     A  10
     AO 68                                  DSPATR(PC)
     A N68                                  DSPATR(HI)
     A                                  5  2'E=Display Details,   V=View -
     A                                      Deals,   C=Confirm Net '
     A                                  6  1'(X)'
     A  53                              6  6'Pairs'
     A                                  6 13'CCY'
     A                                  6 17'*-------BUY--------*'
     A                                  6 38'*------SELL--------*'
     A                                  6 59'*--------NET--------*'
     A          R FX0910F2
     A                                      TEXT('SEARCH SCREEN')
     A                                      OVERLAY
     A                                      CF06
     A                                      CF22
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FX0910'
     A                                  1 25'FX Currency Net Detail'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            BJMRDT    R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A N81                              3  2'C/Party :'
     A N81        CUSHNO        10A  O  3 12DSPATR(HI)
     A  81                              3  2'Group :'
     A  81        GRPNO          4A  O  3 10DSPATR(HI)
      *
     A N81        BBCSSN        10A  O  4  2DSPATR(HI)
     A N81        BBCRNM        20A  O  4 17DSPATR(HI)
     A  81                              4  9'Description'
     A  81        D6GPDS        30A  O  4 21DSPATR(HI)
     A N81        BBCRTN        10A  O  4 42DSPATR(HI)
     A                                  4 54'Pair ccys'
     A            DDPCCY         1A  O  4 64DSPATR(HI)
     A                                  4 66'Value'
     A            DDVALD         6A  O  4 72DSPATR(HI)
     A                                  5  5'CCY'
     A            DDCCY          3A  O  5  9DSPATR(HI)
     A            TLCCNM        14A  O  5 13DSPATR(HI)
     A                                  5 28'CCY 1:'
     A N53                                  DSPATR(ND)
     A            PCCYS1         3A  O  5 36
     A N53                                  DSPATR(ND)
     A            P1CCNM        14A  O  5 40
     A N53                                  DSPATR(ND)
     A                                  5 55'CCY 2:'
     A N53                                  DSPATR(ND)
     A            PCCYS2         3A  O  5 63
     A N53                                  DSPATR(ND)
     A            P2CCNM        14A  O  5 67
     A N53                                  DSPATR(ND)
     A                                  6 17'*-------BUY--------*'
     A                                  6 38'*------SELL--------*'
     A                                  6 59'*--------NET--------*'
     A                                  7  2'Matched'
     A            DDBMAT        20   O  7 17TEXT('Buy Matched')
     A            DDSMAT        20   O  7 38TEXT('Sell Matched')
     A            DDNMAT        21   O  7 59TEXT('Net Matched')
     A                                  8  2'Mismatch'
     A            DDBMIS        20   O  8 17TEXT('Buy Mismatched')
     A            DDSMIS        20   O  8 38TEXT('Sell Mismatched')
     A            DDNMIS        21   O  8 59TEXT('Net Mismatched')
     A                                  9  2'Unmatch'
     A            DDBUNM        20   O  9 17TEXT('Buy Unmatched')
     A            DDSUNM        20   O  9 38TEXT('Sell Unmatched')
     A            DDNUNM        21   O  9 59TEXT('Nett Unmatched')
     A                                 10 17'--------------------'
     A                                 10 38'--------------------'
     A                                 10 59'---------------------'
     A                                 11  2'Net Total'
     A                                      DSPATR(HI)
     A            DDBTOT        20   O 11 17TEXT('Buy Total')
     A                                      DSPATR(HI)
     A            DDSTOT        20   O 11 38TEXT('Sell Total')
     A                                      DSPATR(HI)
     A            DDNETT        21   O 11 59TEXT('Net Total')
     A                                      DSPATR(HI)
     A                                 12 17'--------------------'
     A                                 12 38'--------------------'
     A                                 12 59'---------------------'
     A                                 13  2'Outs. Total'
     A            DDBOUT        20   O 13 17TEXT('Buy Out.')
     A            DDSOUT        20   O 13 38TEXT('Sell Out.')
     A            DDNOUT        21   O 13 59TEXT('Net Out.')
     A                                 14 17'--------------------'
     A                                 14 38'--------------------'
     A                                 14 59'---------------------'
     A                                 15  2'System Total'
     A                                      DSPATR(HI)
     A            DDBSYS        20   O 15 17TEXT('Buy System Total')
     A                                      DSPATR(HI)
     A            DDSSYS        20   O 15 38TEXT('Sell System Total')
     A                                      DSPATR(HI)
     A            DDNSYS        21   O 15 59TEXT('Net System Total')
     A                                      DSPATR(HI)
     A            DDNMTN        15S 0H      TEXT('Net Matched')
     A            DDCONF         1A  H      TEXT('CONFIRMED')
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A N89                                  SFLINZ SFLDSP SFLDSPCTL
     A
     A N89                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R FX0910CM
     A                                      OVERLAY
     A  51                             23  2'F2=Cursor'
     A  51                             23 12'F3=Exit'
     A  51                             23 20'F5=Refresh'
     A  51                             23 31'F12=Cancel'
     A  51                             23 42'F15=View Deals'
     A  51                             23 57'F21=Print Ext'
     A  51                             23 71'F22=Print'
     A  52                             23  2'F3=Exit'
     A  52                             23 10'F5=Refresh'
     A  52                             23 21'F6=Confirm'
     A  52                             23 32'F12=Cancel'
     A  52                             23 43'F15=View Deals'
     A  52                             23 58'F22=Print Detail'
