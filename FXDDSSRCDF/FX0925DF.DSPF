      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FX Netting Multiple Settlements Select')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module - Foreign Exchange.                   *
      *                                                               *
      *  FX0925DF - Select Netting Multiple Settlements DETAILS       *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 29Nov05               *
      *                 CDL025  *CREATE    Date 07Nov04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL025 - FX Netting Payment Generation                       *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *--------------------------------------------------------------------*
      ** Split Details Subfile                                             *
      *--------------------------------------------------------------------*
     A          R FX0925S1                  SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2004')
     A  95                                  SFLNXTCHG
     A            #1SEQN         4A  B 14 49TEXT('Sequence No.')
     A N62                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            #1SAMT        20A  B 14 24TEXT('Split Amount')
     A N61                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A                                      CHECK(RB)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            #1AMND         1A  B 14 56TEXT('Amend Flag')
     A N63                                  DSPATR(HI)
     A N71                                  DSPATR(UL)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A            #PSEQN         4A  H      TEXT('Sequence Number')
     A            #PSAMT        20A  H      TEXT('Sequence Number')
     A            #SERR          1A  H      TEXT('Sett. Error Flag')
     A            #PDBUY    R        H      REFFLD(ASDBUY *LIBL/FXNETSPD)
     A            #NETR     R        H      REFFLD(ASNETR *LIBL/FXNETSPD)
     A            #NTNO     R        H      REFFLD(ASNTNO *LIBL/FXNETSPD)
     A            #RECI     R        H      REFFLD(ASRECI *LIBL/FXNETSPD)
     A            #VALD     R        H      REFFLD(ASVALD *LIBL/FXNETSPD)
     A            #DCSN     R        H      REFFLD(ASDCSN *LIBL/FXNETSPD)
     A            #VDTY     R        H      REFFLD(ASVDTY *LIBL/FXNETSPD)
     A            #CCYD     R        H      REFFLD(ASCCYD *LIBL/FXNETSPD)
     A            #CCY1     R        H      REFFLD(ASCCY1 *LIBL/FXNETSPD)
     A            #CCY2     R        H      REFFLD(ASCCY2 *LIBL/FXNETSPD)
     A            #SEQN     R        H      REFFLD(ASSEQN *LIBL/FXNETSPD)
     A            #LSWC     R        H      REFFLD(ASLSWC *LIBL/FXNETSPD)
     A            #LSWS     R        H      REFFLD(ASLSWS *LIBL/FXNETSPD)
     A            #BRCA     R        H      REFFLD(ASBRCA *LIBL/FXNETSPD)
     A            #DBUY     R        H      REFFLD(ASDBUY *LIBL/FXNETSPD)
     A            #BSIN     R        H      REFFLD(ASBSIN *LIBL/FXNETSPD)
     A            #PRGI     R        H      REFFLD(ASPRGI *LIBL/FXNETSPD)
      * Begin /CSD027/
     A**********  #CNUM     R        H      REFFLD(ASCNUM *LIBL/FXNETSPD)                     CSD027
     A            #CNUM          6A  H      TEXT('Net Customer')
      * End /CSD027/
     A            #SQNO     R        H      REFFLD(ASSQNO *LIBL/FXNETSPD)
     A            #SETM     R        H      REFFLD(ASSETM *LIBL/FXNETSPD)
     A            #ONOB     R        H      REFFLD(ASONOB *LIBL/FXNETSPD)
     A            #OURN     R        H      REFFLD(ASOURN *LIBL/FXNETSPD)
      * Begin /CSD027/
     A**********  #OBNK     R        H      REFFLD(ASOBNK *LIBL/FXNETSPD)                     CSD027
     A**********  #ORDC     R        H      REFFLD(ASORDC *LIBL/FXNETSPD)                     CSD027
     A            #OBNK          6A  H      TEXT('Ordering Bank')
     A            #ORDC          6A  H      TEXT('Ordering Customer')
      * End /CSD027/
     A            #IBNK     R        H      REFFLD(ASIBNK *LIBL/FXNETSPD)
     A            #IBNA     R        H      REFFLD(ASIBNA *LIBL/FXNETSPD)
     A            #CVMR     R        H      REFFLD(ASCVMR *LIBL/FXNETSPD)
     A            #RCRN     R        H      REFFLD(ASRCRN *LIBL/FXNETSPD)
     A            #RCRA     R        H      REFFLD(ASRCRA *LIBL/FXNETSPD)
     A            #RVNO     R        H      REFFLD(ASRVNO *LIBL/FXNETSPD)
     A            #AWBN     R        H      REFFLD(ASAWBN *LIBL/FXNETSPD)
     A            #AWBA     R        H      REFFLD(ASAWBA *LIBL/FXNETSPD)
     A            #BENN     R        H      REFFLD(ASBENN *LIBL/FXNETSPD)
     A            #BENA     R        H      REFFLD(ASBENA *LIBL/FXNETSPD)
     A            #DTP1     R        H      REFFLD(ASDTP1 *LIBL/FXNETSPD)
     A            #DTP2     R        H      REFFLD(ASDTP2 *LIBL/FXNETSPD)
     A            #DTP3     R        H      REFFLD(ASDTP3 *LIBL/FXNETSPD)
     A            #DTP4     R        H      REFFLD(ASDTP4 *LIBL/FXNETSPD)
     A            #DCHG     R        H      REFFLD(ASDCHG *LIBL/FXNETSPD)
     A            #BTB1     R        H      REFFLD(ASBTB1 *LIBL/FXNETSPD)
     A            #BTB2     R        H      REFFLD(ASBTB2 *LIBL/FXNETSPD)
     A            #BTB3     R        H      REFFLD(ASBTB3 *LIBL/FXNETSPD)
     A            #BTB4     R        H      REFFLD(ASBTB4 *LIBL/FXNETSPD)
     A            #BTB5     R        H      REFFLD(ASBTB5 *LIBL/FXNETSPD)
     A            #BTB6     R        H      REFFLD(ASBTB6 *LIBL/FXNETSPD)
      *--------------------------------------------------------------------*
      ** Customer Details Subfile Control                                  *
      *--------------------------------------------------------------------*
     A          R FX0925C1                  SFLCTL(FX0925S1)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      TEXT('Subfile control.')
     A                                      ROLLUP(98)
     A                                      CF03(03 'Exit.')
     A                                      CF12(12 'Previous.')
     A                                      CF06(06 'Confirm.')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A  92                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  97                                  SFLCLR
     A  96                                  SFLEND
     A            #1SFRN         4S 0H      SFLRCDNBR
     A            #1USER        10A  O  1  2OVRDTA
     A                                  1 13'FX0925C1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            #1WID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 21'FX Netting Multiple Settlements Se-
     A                                      lect'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4 16'Action:'
     A            #1ACTN         1A  O  4 24DSPATR(HI)
     A                                  4 28'Value Date:'
     A            #1VDAT         6Y 0O  4 40DSPATR(HI)
     A                                      EDTCDE(Y)
     A                                  5 10'Counterparty:'
     A            #1CUST         6A  O  5 24DSPATR(HI)
     A                                  5 38'Report Name:'
     A            #1CRNM        20A  O  5 51DSPATR(HI)
     A                                  6  4'Customer Shortname:'
     A            #1CSNM        10A  O  6 24DSPATR(HI)
     A                                  8  5'Netting Reference:'
     A            #1NETR        19A  O  8 24DSPATR(HI)
     A                                  8 52'Pair currencies:'
     A            #1PIND         1A  O  8 69DSPATR(HI)
     A  31                              9 47'Currency 1:'
     A  31        #1PCY1         3A  O  9 59DSPATR(HI)
     A  31        #1PC1N        14A  O  9 63DSPATR(HI)
     A                                 10 14'Currency:'
     A            #1NCCY         3A  O 10 24DSPATR(HI)
     A            #1NCCN        14A  O 10 28DSPATR(HI)
     A  31                             10 47'Currency 2:'
     A  31        #1PCY2         3A  O 10 59DSPATR(HI)
     A  31        #1PC2N        14A  O 10 63DSPATR(HI)
     A                                 11 13'Net Total:'
     A            #1NAMT        20A  O 11 24DSPATR(HI)
      * 32                             12 11'Split Total:'
      * 32        #1CAMT        20A  O 12 24DSPATR(HI)
      *                                     OVRDTA
      * 32                             12 46'  ** DIFFRERENCE **  '
      *                                     DSPATR(RI)
     A                                 13 31'Split Amount'
     A                                 13 49'Seqn'
     A                                 13 56'*'
     A                                  6 38'Report Town:'
     A            #1CRTN        10A  O  6 51DSPATR(HI)
     A*--------------------------------------------------------------------*
     A**  Footer                                                           *
     A*--------------------------------------------------------------------*
     A          R FX0925F1
     A                                      TEXT('Command keys')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'F3=Exit  F12=Previous'
     A                                 23 25'F6=Confirm'
     A  70                                  DSPATR(ND)
      *--------------------------------------------------------------------*
      **  Message Subfile Format                                           *
      *--------------------------------------------------------------------*
     A          R FX0925S2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #2SFKY                    SFLMSGKEY
     A            #2PGMQ                    SFLPGMQ
      *--------------------------------------------------------------------*
      **  Message Subfile Control                                          *
      *--------------------------------------------------------------------*
     A          R FX0925C2                  TEXT('Program messages')
     A                                      SFLCTL(FX0925S2)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR BLINK
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  94                                  SFLEND
     A            #2PGMQ                    SFLPGMQ
