     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FX Outstanding nets screen')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module - Foreign Exchange.                   *
      *                                                               *
      *  DSPF/FX0930DF - Unconfirmed Net Input/Enquiry.               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CDL002             Date 06Mar97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL002 - UNCONFIRMED NETS INPUT/ENQUIRY                      *
      *                                                               *
     A*****************************************************************
     A                                      REF(*LIBL/MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CHGINPDFT
     A*=========================================================================
      *
     A          R FX0930S1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SFL
     A                                      CHGINPDFT
     A  36                                  SFLNXTCHG
     A            DDSEL          1A  B  6 11TEXT('SELECT FIELD')
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A N30N38                               DSPATR(HI)
     A  38                                  DSPATR(RI)
     A N30                                  DSPATR(PC)
     A            DDCSSN        10A  O    +3DSPATR(HI)
     A  30                                  DSPATR(ND)
     A            DDRPNM        20A  O    +3DSPATR(HI)
     A  30                                  DSPATR(ND)
     A            DDRPTN        10A  O    +2DSPATR(HI)
     A  30                                  DSPATR(ND)
     A            DDVALD         6Y 0O    +3DSPATR(HI)
     A                                      EDTCDE(Y)
     A  30                                  DSPATR(ND)
     A            DDCUST         6A  H      TEXT('CUSTOMER NUMBER FIELD')
     A          R FX0930C1                  SFLCTL(FX0930S1)
     A                                      TEXT('DEALS SUBFILE CONTROL')
     A                                      CF03(03 'TERMINATE')
     A                                      CF05(05 'REDISPLAY')
     A                                      CF14(14 'NETTING')
     A                                      CF15(15 'DISPLAY DEALS')
     A                                      CF16(16 'CONFIRMED NETS')
     A                                      OVERLAY
     A  32                                  SFLDSP
     A  31                                  SFLDSPCTL
     A  33                                  SFLINZ
     A  35                                  SFLCLR
     A  34                                  SFLEND
     A                                      SFLRNA
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0014)
     A            S1RRND         4S 0H      SFLRCDNBR(CURSOR)
      *
      * Standard header details:-
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FX0930'
     A                                  1 30'FX UNCONFIRMED NETS'
     A                                      DSPATR(HI UL)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  3 16'Group or'
     A                                    +3'Group:'
     A N81                                  DSPATR(ND)
     A            DDGPDS        30A  O    +1TEXT('Group Description')
     A N81                                  DSPATR(ND)
     A                                  4 16'Customer'
     A                                  4 62'Value Date'
     A                                  5 10'(X)'
     A                                  5 28'Report Name'
     A                                  5 50'Town'
     A            PDCSSN        10A  B  5 15
     A  71                                  DSPATR(RI)
     A  75                                  DSPATR(PC)
     A N71                                  DSPATR(HI UL)
     A            PVDATE         6A  B  5 64
     A N72                                  DSPATR(HI UL)
     A  72                                  DSPATR(RI PC)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A                                 22  9'E=Ccy Net Summary, V=View Deals-
     A                                       for Customer and Value Date'
     A                                 23  3'F3=Exit'
     A                                    +5'F14=Ccy Net Summary'
     A                                    +5'F15=View Deals'
     A                                    +5'F16=Confirmed Nets'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
