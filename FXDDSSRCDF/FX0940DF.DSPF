/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FX Netting deals enquiry')                       *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Dealing Module - Foreign Exchange                    *
     A*                                                               *
     A*  FX0940DF - FX Netting Deals Enquiry                          *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. 245692             Date 09Dec13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 126779             Date 28Nov97               *
     A*                 CDL002             Date 24Apr97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  245692 - Incorrect validation of rates when the decimal se-  *
      *           parator in the Dealing ICD was defined as "," ins-  *
      *           tead of ".".                                        *
      *         - Applied for MD-23981.                               *
     A*  126779 - Include F5 To Refresh Screen.                       *
     A*  CDL002 - FX Netting incorporation into core.                 *
     A*                                                               *
     A*****************************************************************
     A* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     A*    03            EXIT SCREEN                                  *
     A*    12            CANCEL                                       *
     A*       31         DEACTIVATE ROLLUP KEY                        *
     A*       33         ROLLUP INDICATOR                             *
     A*      N34         DISPLAY SUBFILE                              *
     A*       35         CLEAR SUBFILE                                *
     A*       36         INITIALIZE SUBFILE                           *
     A*      N37         DISPLAY SUBFILE CONTROL RECORD               *
     A*       38         SFLEND INDICATOR                             *
     A*      N57         DISPLAY GROUP, DESCR     on DEALS SELECTION  *
     A*       57         DISPLAY CUST, CSSN, CRTN on DEALS SELECTION  *
     A*       59         DISPLAY CURRENCY         on DEALS SELECTION  *
     A*       60         DISPLAY CURRENCY PAIRS   on DEALS SELECTION  *
     A*       61         DISPLAY NET REFERENCE    on DEALS SELECTION  *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      OPENPRT
     A                                      INDARA
     A                                      CA03(03 'EXIT')
      *                                             Start  /CDL002/
     A                                      CA05(05 'REFRESH')
      *                                             End    /CDL002/
     A                                      CA12(12 'CANCEL')
     A*
     A          R FX0940S1                  SFL
     A                                      CHGINPDFT
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            S1DLNO         6A  O  8  3
     A                                      DSPATR(HI)
     A            S1PCCY         3A  O  8 10
     A                                      DSPATR(HI)
      *                                                                                       245692
      ** Start of change 245692                                                               245692
      *                                                                                       245692
     A**********  S1PUAM        10Y 0O  8 14EDTCDE(1)                                         245692
     A            S#PUAM        13A  O  8 14                                                  245692
     A                                      DSPATR(HI)
     A            S1NBUY         1A  O  8 29
     A                                      DSPATR(HI)
     A**********  S1EXRT        13  8O  8 31EDTCDE(3)                                         245692
     A            S#EXRT        14   O  8 31                                                  245692
     A                                      DSPATR(HI)
     A            S1SCCY         3A  O  8 46
     A                                      DSPATR(HI)
     A**********  S1SLAM        10Y 0O  8 50EDTCDE(1)                                         245692
     A            S#SLAM        13A  O  8 50                                                  245692
     A                                      DSPATR(HI)
      *                                                                                       245692
      ** End of change 245692                                                                 245692
      *                                                                                       245692
     A            S1NSEL         1A  O  8 65
     A                                      DSPATR(HI)
     A            S1PRVS         1A  O  8 69
     A                                      DSPATR(HI)
     A            S1CSNM        10A  O  8 71
     A                                      DSPATR(HI)
     A*
     A          R FX0940C1                  SFLCTL(FX0940S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('DEALS SUBFILE CONTROL')
     A                                      OVERLAY
     A N34N36                               SFLDSP
     A  35                                  SFLCLR
     A  36                                  SFLINZ
     A N37                                  SFLDSPCTL
     A                                      SFLRNA
     A  38                                  SFLEND
     A N31                                  ROLLUP(33 'ROLLUP')
     A            S1RRND         4S 0H      SFLRCDNBR(CURSOR)
     A            ##USR         10A  O  1  2
     A                                  1 13'FX0940'
     A                                  1 27'NETTING DEALS ENQUIRY'
     A                                      DSPATR(HI UL)
     A            BJMRDT         7A  O  1 72
     A            ##JOB         10A  O  2  2
     A                                  2 71TIME
     A  57                              4  2'Counterparty:'
     A  57        S1@CUS         6A  O  4 17
     A                                      DSPATR(HI)
     A  57        S1@CSN        10A  O  4 25
     A                                      DSPATR(HI)
     A  57        S1@CRN        20A  O  4 37
     A                                      DSPATR(HI)
     A N57                              4  2'Group:'
     A N57        S1@GRP         4A  O  4  9
     A                                      DSPATR(HI)
     A N57                              4 14'Description:'
     A N57        S1@GPD        30A  O  4 27
     A                                      DSPATR(HI)
     A                                  4 60'Value Date:'
     A            S1@VLD         6A  O  4 73
     A                                      DSPATR(HI)
     A  59                              5  3'Currency:'
     A  59        S2CCY          3A  O  5 13
     A                                      DSPATR(HI)
     A  60                              5 18'Pair:'
     A  60        S2CCY1         3A  O  5 25
     A                                      DSPATR(HI)
     A  60        S2CCY2         3A  O  5 29
     A                                      DSPATR(HI)
     A  61                              5 40'Net Reference:'
     A  61        S2NETR        19A  O  5 56
     A                                      DSPATR(HI)
     A                                  6 10'*--------BUY--------*'
     A                                  6 34'Exchange'
     A                                  6 46'*-------SELL--------*'
     A                                  6 68'Match'
     A                                  6 75'Cust'
     A  57                                  DSPATR(ND)
     A                                  7  4'Deal '
     A                                  7 10'CCY'
     A                                  7 14'Amount In 000'
     A                                  7 28'Net'
     A                                  7 36'Rate'
     A                                  7 46'CCY'
     A                                  7 50'Amount In 000'
     A                                  7 64'Net'
     A                                  7 68'Status'
     A                                  7 75'Name'
     A  57                                  DSPATR(ND)
     A          R FX0940SM                  TEXT('MESSAGE SUBFILE DATA')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*
     A          R FX0940CM                  TEXT('MESSAGE SFL CONTROL')
     A                                      SFLCTL(FX0940SM)
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A N89                                  SFLDSP SFLDSPCTL
     A N89                                  SFLEND SFLINZ SFLCLR
     A            ##PGM                     SFLPGMQ
     A                                 23  2'F3=Exit'
      *                                             Start  /CDL002/
     A***********                      23 17'F12=Cancel'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F12=Cancel'
      *                                             End    /CDL002/
