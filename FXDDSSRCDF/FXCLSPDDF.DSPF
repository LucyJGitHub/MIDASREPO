      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FX CLS Pay-in Schedule Display Screen')
      *****************************************************************
      *                                                               *
      *  Midas - Foreign Exchange Dealing Module                      *
      *                                                               *
      *  DSPF/FXCLSPDDF - CLS Pay-in Schedule                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAP084             Date 02Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. 187730             Date 15Dec00               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 187252             Date 04Dec00               *
      *                 CDL008  *CREATE    Date 02May00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP084 - Synchronous calling of APIs                         *
      *  187730 - Allow enquiry of deleted CLS pay-in schedules       *
      *  187252 - F6 to authorise CLS pay-in schedules                *
      *  CDL008 - Continuous Linked Settlement                        *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      CA03(03 'Exit')
      *                                              Begin /187252/
     A N82                                  CF06(06 'Confirm')
      *                                              End   /187252/
      **Start of change CAP084
     A N85                                  CF07(07 'Previous')
     A N86                                  CF08(08 'Next')
      **End of change CAP084
     A                                      CF12(12 'Cancel')
     A N32                                  CF10(10 'Delete')
     A N70                                  CF11(11 'Toggle')
     A                                      CF15(15 'Select')
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      ** Header Format
      *
     A
     A          R FXCLSPH0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'FXCLSP'
     A                                  1 31'CLS Pay-in Schedule'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
      ** Detail Format
      *
     A          R FXCLSPD0
     A                                      PUTOVR
     A                                      OVERLAY
      *
      *
      *
      *
      *
      *
      *
     A                                  4  3'Action:'
     A            SACTN          1A  B  4 13DSPATR(HI)
     A  60                                  DSPATR(RI PC)
     A N60N25                               DSPATR(UL)
     A  25                                  DSPATR(PR)
      *                                              Begin /187730/
     A*********** SSTAT         12A  B  4 67DSPATR(PR)
     A            SSTAT         28A  B  4 52DSPATR(PR)
      *                                              End   /187730/
     A  81                                  DSPATR(RI)
     A N81                                  DSPATR(ND)
     A                                  5  3'Currency:'
     A            SCURR          3A  B  5 13DSPATR(HI)
     A  61                                  DSPATR(RI PC)
     A N61N25                               DSPATR(UL)
     A  25                                  DSPATR(PR)
     A                                  5 19'Nostro:'
     A            SNOST          2A  B  5 28DSPATR(HI)
     A  62                                  DSPATR(RI PC)
     A N62N25                               DSPATR(UL)
     A  25                                  DSPATR(PR)
     A                                  5 32'Value Date:'
     A                                  5 48'/'
     A                                  5 53'/'
     A            SVDAT1         2A  B  5 45DSPATR(HI)
     A  63                                  DSPATR(RI PC)
     A N63N25                               DSPATR(UL)
     A  25                                  DSPATR(PR)
     A            SVDAT2         2A  B  5 50DSPATR(HI)
     A  63                                  DSPATR(RI PC)
     A N63N25                               DSPATR(UL)
     A  25                                  DSPATR(PR)
     A            SVDAT3         2A  B  5 55DSPATR(HI)
     A  63                                  DSPATR(RI PC)
     A N63N25                               DSPATR(UL)
     A  25                                  DSPATR(PR)
     A                                  5 59'Sequence number:'
     A            SSEQNO         2A  B  5 76DSPATR(HI)
     A  64                                  DSPATR(RI PC)
     A N64N25                               DSPATR(UL)
     A  25                                  DSPATR(PR)
     A                                  6  3'Local Time difference'
     A                                  6 25'from CET:'
     A            STDCET         5A  B  6 38DSPATR(HI)
     A  65                                  DSPATR(RI PC)
     A N65N25                               DSPATR(UL)
     A  25                                  DSPATR(PR)
     A                                  6 51'Profit Centre:'
     A            SPRFC          4A  B  6 68DSPATR(HI)
     A  66                                  DSPATR(RI PC)
     A N66N25                               DSPATR(UL)
     A  25                                  DSPATR(PR)
      *
      ** Totals
      *
     A          R FXCLSPD1
     A                                      OVERLAY
      *
      *
     A                                 17 23'Total:'
     A            STPAMT        18A  O 17 30DSPATR(HI)
     A  35                                  DSPATR(ND)
     A            STRAMT        18A  B 17 55DSPATR(HI)
     A  67                                  DSPATR(RI PC)
     A N67N26                               DSPATR(UL)
     A  26                                  DSPATR(PR)
     A N35                                  DSPATR(ND)
     A                                 19  3'Nostro Balance Total:'
     A            SNPAMT        18A  O 19 30DSPATR(HI)
     A            SNRAMT        18A  O 19 55DSPATR(HI)
      *
      ** Subfile detail format
      *
     A          R FXCLSPS0                  SFL
     A  40                                  SFLNXTCHG
     A            STIME          4A  B 12 14DSPATR(HI)
     A  68                                  DSPATR(RI PC)
     A N68N27                               DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  35                                  DSPATR(ND)
     A            SAMNT         18A  B 12 30DSPATR(HI)
     A  69                                  DSPATR(RI PC)
     A N69N27                               DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  35                                  DSPATR(ND)
     A            #SAMNT        17S 0H      TEXT('CLS Amount')
      *
      ** Subfile control
      *
     A          R FXCLSPC0                  TEXT('Subfile control')
     A                                      SFLCTL(FXCLSPS0)
     A                                      SFLPAG(4) SFLSIZ(5)
     A                                      OVERLAY
     A  41                                  SFLCLR
     A N41                                  SFLDSPCTL
     A N41 42                               SFLDSP
     A  43                                  SFLEND(*MORE)
      *
      ** Command Keys
      *
     A N43                                  ROLLUP(45 'Next page.')
      *
      *
      *
     A            SRLRN          4  0H      SFLRCDNBR
     A                                  9 37'Pay' DSPATR(UL)
     A                                  9 60'Receive' DSPATR(UL)
     A                                 11  3'Time (CET):'
     A                                 11 22'Amount:'
      *
      ** Format trailer
      *
     A          R FXCLSPF0
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRATR
      *                                              Begin /187252/
     A***********                      24  3'F3=Exit'
     A***********                      24 12'F12=Cancel'
     A***********                      24 24'F15=Select'
     A*N35*******                      24 36'Rollup/Rolldown'
     A**30*******                           DSPATR(ND)
     A***********                      24 53'F11=Pay/Rcv'
     A**70*******                           DSPATR(ND)
     A***********                      24 66'F10=Delete'
     A**32*******                           DSPATR(ND)
      **Start of change CAP084
     A**********                       24  2'F3=Exit'                                         CAP084
     A**********                       24 11'F12=Cancel'                                      CAP084
     A**********                       24 23'F15=Select'                                      CAP084
     A*N35******                       24 35'Rollup/down'                                     CAP084
     A**30******                            DSPATR(ND)                                        CAP084
     A**********                       24 48'F11=Pay/Rcv'                                     CAP084
     A**70******                            DSPATR(ND)                                        CAP084
     A**********                       24 60'F10=Delete'                                      CAP084
     A**32******                            DSPATR(ND)                                        CAP084
     A**********                       24 71'F6=Confirm'                                      CAP084
     A**82******                            DSPATR(ND)                                        CAP084
     A                                 24  2'F3=Exit'
     A                                    +1'F12=Cancel'
     A                                    +1'F15=Select'
     A N70                             24 32'F11=Pay/Rcv'
     A N85                             24 44'F7=Prev'
     A N32                             24 45'F10=Delete'
     A N86                             24 52'F8=Next'
     A N82                             24 56'F6=Confirm'
     A N35                             24 68'Rollup/down'
     A  30                                  DSPATR(ND)
      **End of change CAP084
      *                                              End   /187252/
      *
      ** Error message subfile record format
      *
     A          R FXCLSPS1                  SFL
     A                                      SFLMSGRCD(22)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      ** Subfile Control for error messages
      *
     A          R FXCLSPC1                  SFLCTL(FXCLSPS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      OVERLAY
     A  50
     AON50                                  SFLEND
     A            SPGMQ                     SFLPGMQ
