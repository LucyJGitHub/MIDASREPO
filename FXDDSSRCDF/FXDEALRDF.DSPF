     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FX Deal range authorization')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MM Dealer Module                                     *
     A*                                                               *
     A*  FXDEALRDF  - FX DEAL RANGE AUTHORIZATION                     *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG2113            Date 07May04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CAP002  *CREATE    Date 01May98               *
     A*                                    Date ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG2113- Fields appear in Webfacing when they should not be  *
      *           showing                                             *
     A*  CAP002 - Conversion of MM inputs into modular structure to   *
     A*           use as APIs.                                        *
     A*                                                               *
     A*****************************************************************
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A*
     A* -------------------------------------------------------------------*
     A* * MESSAGE SUBFILE FORMAT                                           *
     A* -------------------------------------------------------------------*
     A*
     A          R FXDEALS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MESSAGE SUBFILE CONTROL                                          *
     A* -------------------------------------------------------------------*
     A*
     A          R FXDEALS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      SFLCTL(FXDEALS1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MAIN DISPLAY SCREEN                                              *
     A* -------------------------------------------------------------------*
     A*
     A          R FXDEALD1                  TEXT('MAIN DISPLAY SCREEN')
     A                                      CF03
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                  1  2'Ref: FXDEAL'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 25'FX DEALER - RANGE AUTHORIZATIONS'
     A*
     A                                  5 21'First Deal in Range:'
     A  13                                  DSPATR(ND)
     A            DDFDLN    R     A  B  5 42REFFLD(DLNO)
     A                                      OVRDTA
     A  10                                  DSPATR(RI)
     A  10
     AON11N13                               DSPATR(PC)
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(PR)
      ** Start of change BUG2113
     A  13                                  DSPATR(ND)
      ** End of change BUG2113
     A                                  7 21'Last Deal in Range:'
     A  13                                  DSPATR(ND)
     A            DDLDLN    R     A  B  7 42REFFLD(DLNO)
     A                                      OVRDTA
     A  11                                  DSPATR(RI PC)
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(PR)
      ** Start of change BUG2113
     A  13                                  DSPATR(ND)
      ** End of change BUG2113
     A*
     A* -------------------------------------------------------------------*
     A* * MAIN SCREEN FOOTER                                               *
     A* -------------------------------------------------------------------*
     A*
     A          R FXDEALF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                 24  1'F3-End'
