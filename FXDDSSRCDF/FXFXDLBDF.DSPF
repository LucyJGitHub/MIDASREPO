/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas FX Deals browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FX Dealer Module                                     *
     A*                                                               *
     A*  FXFXDLBDF - FX DEALS BROWSE                                  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 29Nov05               *
     A*  Prev Amend No. CDL011             Date 07Jul02               *
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CAP004             Date 15Aug98               *
     A*                 CAP002  *CREATE    Date 01Nov97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  CDL011 - Expansion of Customer Number on Dealing Transactions*
     A*  CAP004 - API's phase 3.                                      *
     A*           Allow browse by Front Office Id.                    *
     A*  CAP002 - Conversion of FX inputs into modular structure to   *
     A*           use as APIs.                                        *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R FXFXDLS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDDEAL    R     A     8  7REFFLD(DEAL)
     A                                      DSPATR(HI)
     A            DDDLTP    R           8 16REFFLD(DTPE)
     A            DDBAMT    R   +2A     8 20REFFLD(AMNP)
     A                                      TEXT('BUY AMOUNT')
     A            DDBCCY    R           8 39REFFLD(CCY)
     A                                      TEXT('BUY CURRENCY')
     A            DDSCCY    R           8 44TEXT('SELL CURRENCY')
     A                                      REFFLD(CCY)
     A            DDVDAT    R     A     8 49TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
      * Begin /CSD027/
     A**********  DDCUSN    R           8 58REFFLD(CNUM)                                      CSD027
     A            DDCUSN         6A     8 58TEXT('Customer number')
      * End /CSD027/
     A            DDSTAT    R           8 67REFFLD(DSTS)
     A            DDTEXT         7A     8 71TEXT('"DELETED" OR CP/BK STATUS')
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R FXFXDLS0                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
      * Start CAP004
     A                                      CF11
      * End CAP004
     A                                      SFLCTL(FXFXDLS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'FXFXDLB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'FX DEALER - DEAL INPUT & AUTHORIZA+
     A                                      TION'
     A                                  4 37'ENQUIRY'
     A*/COPY WNCPYSRC,FXFXDLBD01
     A                                  6  2'Select'
     A                                  6  9'Deal'
     A                                  6 15'Deal'
     A                                  6 24'Buy Amount'
     A                                  6 39'Buy  Sell'
     A                                  6 49'Val Date'
     A                                  6 59'Cust  Status'
     A*
     A                                  7 10'No   Type'
     A                                  7 39'CCY  CCY'
     A                                  7 58'Number'
     A*/COPY WNCPYSRC,FXFXDLBD02
      *
      * Start of change CDL011
     A          R FXFXDLS5
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDDEAL    R     A     8  7REFFLD(DEAL)
     A                                      DSPATR(HI)
     A            DDDLTP    R           8 16REFFLD(DTPE)
     A            DDBAMT    R   +2A     8 20REFFLD(AMNP)
     A                                      TEXT('BUY AMOUNT')
     A            DDBCCY    R           8 39REFFLD(CCY)
     A                                      TEXT('BUY CURRENCY')
     A            DDSCCY    R           8 44TEXT('SELL CURRENCY')
     A                                      REFFLD(CCY)
     A            DDVDAT    R     A     8 49TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
     A            DDDCSN        10A     8 57
     A            DDSTAT    R           8 70REFFLD(DSTS)
     A            DDTEXT         7A     8 73TEXT('"DELETED" OR CP/BK STATUS')
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R FXFXDLS4                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLCTL(FXFXDLS5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'FXFXDLB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'FX DEALER - DEAL INPUT & AUTHORIZA+
     A                                      TION'
     A                                  4 37'ENQUIRY'
     A                                  6  2'Select'
     A                                  6  9'Deal'
     A                                  6 15'Deal'
     A                                  6 24'Buy Amount'
     A                                  6 39'Buy  Sell'
     A                                  6 49'Value'
     A                                  6 58'Customer'
     A                                  6 68'Status'
     A*
     A                                  7 10'No   Type'
     A                                  7 39'CCY  CCY'
     A                                  7 49'Date'
     A                                  7 60'Name'
     A*
      * End of change CDL011
      * Start CAP004
     A          R FXFXDLS3                  TEXT('SELECT SCREEN SUBFILE')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFODN        20A     8  7TEXT('FRONT OFFICE ID.')
     A                                      DSPATR(HI)
     A            DDDEAL    R     A     8 29REFFLD(DEAL)
     A            DDDLTP    R           8 38REFFLD(DTPE)
     A            DDBAMT    R   +2A     8 42REFFLD(AMNP)
     A                                      TEXT('BUY AMOUNT')
     A            DDBCCY    R           8 60REFFLD(CCY)
     A                                      TEXT('BUY CURRENCY')
     A            DDSCCY    R           8 65TEXT('SELL CURRENCY')
     A                                      REFFLD(CCY)
     A            DDVDAT    R     A     8 70TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
     A*
     A          R FXFXDLS2                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLCTL(FXFXDLS3)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'FXFXDLB2'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'FX DEALER - DEAL INPUT & AUTHORIZA+
     A                                      TION'
     A                                  4 37'ENQUIRY'
     A*/COPY WNCPYSRC,FXFXDLBD03
     A                                  6  2'Select'
     A                                  6  9'Front Office'
     A                                  6 28'Deal'
     A                                  6 37'Deal'
     A                                  6 44'Buy Amount'
     A                                  6 60'Buy  Sell'
     A                                  6 70'Val Date'
     A*
     A                                  7  9'Identifier'
     A                                  7 28'Number'
     A                                  7 37'Type'
     A                                  7 60'CCY  CCY'
     A*/COPY WNCPYSRC,FXFXDLBD04
      * End CAP004
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R FXFXDLF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
      * Start CAP004
     A*********************************24  2'F3=Exit F12=Cancel'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
      * End CAP004
