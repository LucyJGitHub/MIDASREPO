/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas FX Invalid FX deals browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FX Dealer Module                                     *
     A*                                                               *
     A*  FXFXDLRDF - INVALID FX DEALS BROWSE                          *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. 166907             Date 04Jan00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP014             Date 29Nov99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CAP006             Date 25Feb99               *
     A*                 CAP004             Date 01Sep98               *
     A*                 CAP002  *CREATE    Date 01Nov97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  166907 - Show 'Deal No. 999999 successfully inserted'        *
     A*           message on screen.                                  *
     A*  CAP014 - Repair function enhancements                        *
     A*  CAP006 - Show error message on screen                        *
     A*  CAP004 - API's phase 3.                                      *
     A*           Enable delete of invalid transactions               *
     A*  CAP002 - Conversion of FX inputs into modular structure to   *
     A*           use as APIs.                                        *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R FXIFXDLS1                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1   B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDACTN    R           8  6REFFLD(ACTO)
     A                                      DSPATR(HI)
     A            DDFOTRANIDR     A     8  9REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A            DDDLTP    R           8 30REFFLD(DTPE)
     A            DDBAMT    R           8 34REFFLD(AM14)
     A                                      TEXT('BUY AMOUNT')
     A            DDBCCY    R           8 53REFFLD(CCY)
     A                                      TEXT('BUY CURRENCY')
     A            DDSCCY    R           8 57TEXT('SELL CURRENCY')
     A                                      REFFLD(CCY)
     A            DDVDAT    R     A     8 62TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
     A            DDCUST    R           8 71REFFLD(CSNM)
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
      * Start CAP004
     A            DDDLNO    R     A  H      REFFLD(DEAL)
     A            DDDLDT    R     A  H      REFFLD(CHGD)
     A            DDSAMT    R        H      REFFLD(AM14)
      * End CAP004
      * Start CAP014
     A            DDBRSN    R        H      REFFLD(BRSN)
      * End CAP014
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R FXIFXDLS0                 TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(FXIFXDLS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
      * Start CAP004
     A********************************  1  2'FXIFXDLB'
     A                                  1  2'FXFXDLRB'
      * End CAP004
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'FX DEALER - INVALID DEAL MAINTENAN+
     A                                      CE'
     A                                  4 37'ENQUIRY'
     A                                  6  1'Sel'
     A                                  6  5'Act'
     A                                  6  9'Front Office ID'
     A                                  6 29'Deal'
     A                                  6 35'Buy Amount'
     A                                  6 53'Buy Sell'
     A                                  6 62'Val Date'
     A                                  6 72'Cust'
     A*
      * Start CAP014
      * Start CAP004
     A**********************************7  1'A,D'
      * End CAP004
      * End CAP014
     A                                  7 29'Type'
     A                                  7 53'CCY CCY'
     A                                  7 71'Number'
     A*/COPY WNCPYSRC,FXFXDLRD01
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R FXIFXDLF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      * Start of CAP006
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of CAP006
     A*
     A* -------------------------------------------------------------------*
     A* * END MESSAGE                                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R FXIFXDLEND                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
      * Start of 166907
     A                                      PUTOVR
      * End of 166907
      * Start CAP004
     A********************************  1  2'FXIFXDLB'
     A                                  1  2'FXFXDLRB'
      * End CAP004
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'FX DEALER - INVALID DEAL MAINTENAN+
     A                                      CE'
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
      * Start of 166907
     A            DDFOTX        40A    24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of 166907
     A                                 23  2'Press enter to end this function'
