      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas FX Confirmed split nets master file')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing (FX) Module                                  *
      *                                                               *
      *  PF/FXNETSPD - Confirmed split nets master file               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 25Nov05               *
      *  Prev Amend No. CDL025   *CREATE   Date 20Nov04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL025 - FX Netting Payment Generation                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R FXNETSD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A            ASRECI    R               REFFLD(RECI)
     A                                      TEXT('Record Id')
     A                                      COLHDG('Rec' 'Id.')
     A            ASNETR    R               REFFLD(NTRF)
     A                                      TEXT('Net Reference')
     A                                      COLHDG('Net Reference')
     A            ASDCSN    R               REFFLD(DCSN)
     A                                      TEXT('Shortname or Group')
     A                                      COLHDG('Deal Cust' 'Shortname')
      * Begin /CSD027/
     A**********  ASCNUM    R               REFFLD(POCN)                                      CSD027
     A            ASCNUM         6A
      * End /CSD027/
     A                                      TEXT('Net Customer')
     A                                      COLHDG('Customer' 'Number')
     A            ASVDTY    R               REFFLD(DLNO)
     A                                      TEXT('Value Date DD/MM/YY')
     A                                      COLHDG('Value' 'Date' 'DDMMYY')
     A            ASVALD    R               REFFLD(VALD)
     A            ASCCYD    R               REFFLD(CYCD)
     A                                      TEXT('Currency Code')
     A                                      COLHDG('Ccy.' 'Code')
     A            ASCCY1    R               REFFLD(CCY1)
     A                                      TEXT('Pair CCY 1')
     A                                      COLHDG('Pair' 'Ccy1')
     A            ASCCY2    R               REFFLD(CCY2)
     A                                      TEXT('Pair CCY 2')
     A                                      COLHDG('Pair' 'Ccy2')
     A            ASSEQN    R               REFFLD(DS38)
     A                                      TEXT('Sequence')
     A                                      COLHDG('Seq.')
     A            ASBRCA    R               REFFLD(BRCA)
     A            ASDBUY    R               REFFLD(DBUY)
     A                                      TEXT('Net Amount')
     A                                      COLHDG('Net Amount')
     A            ASBSIN    R               REFFLD(BSIN)
     A                                      TEXT('Buy/Sell Indicator')
     A                                      COLHDG('B/S' 'Ind')
     A            ASPRGI    R               REFFLD(DSTS)
     A                                      TEXT('Pay/Rec Generated Ind')
     A                                      COLHDG('P/R' 'Ind')
     A            ASSETM    R               REFFLD(PSTM)
     A                                      TEXT('Settlement Method')
     A                                      COLHDG('Sett' 'Mthd')
     A            QQOURN        12A
     A            ASONOB    R               REFFLD(BRCA)
     A                                      TEXT('Our Nostro Branch')
     A                                      COLHDG('Our' 'Nostr' 'Branc')
     A            ASIBNK    R               REFFLD(PIBN)
     A                                      TEXT('Intermediary Bank')
     A                                      COLHDG('Intermed.' '   Bank')
     A            ASIBNA    R               REFFLD(PIBA)
     A                                      TEXT('Intermediary Bank')
     A                                      COLHDG('Intermediary Bank')
      * Begin /CSD027/
     A**********  ASOBNK    R               REFFLD(POBN)                                      CSD027
     A            ASOBNK         6A
      * End /CSD027/
     A                                      TEXT('Ordering Bank')
     A                                      COLHDG('Ordering' ' Bank')
      * Begin /CSD027/
     A**********  ASORDC    R               REFFLD(POCN)                                      CSD027
     A            ASORDC         6A
      * End /CSD027/
     A                                      TEXT('Ordering Customer')
     A                                      COLHDG('Ordering' 'Customer')
     A            ASRCRN    R               REFFLD(RCRN)
     A            ASRCRA    R               REFFLD(RCRA)
     A            ASRVNO    R               REFFLD(RVNO)
     A            ASAWBN    R               REFFLD(AWBN)
     A            ASAWBA    R               REFFLD(AWBA)
     A            ASBENN    R               REFFLD(BENN)
     A            ASBENA    R               REFFLD(BENA)
     A            ASDTP1    R               REFFLD(DTP1)
     A            ASDTP2    R               REFFLD(DTP2)
     A            ASDTP3    R               REFFLD(DTP3)
     A            ASDTP4    R               REFFLD(DTP4)
     A            ASDCHG    R               REFFLD(DCHG)
     A            ASBTB1    R               REFFLD(BTB1)
     A            ASBTB2    R               REFFLD(BTB2)
     A            ASBTB3    R               REFFLD(BTB3)
     A            ASBTB4    R               REFFLD(BTB4)
     A            ASBTB5    R               REFFLD(BTB5)
     A            ASBTB6    R               REFFLD(BTB6)
     A            ASCVMR    R               REFFLD(CVMR)
     A            ASNTNO    R               REFFLD(DLNO)
     A                                      COLHDG('Net Number')
     A                                      TEXT('Net Number')
     A            ASLSWC    R               REFFLD(LSWC)
     A            ASLSWS    R               REFFLD(LSWS)
     A            ASDTOE    R               REFFLD(DATDFO)
     A                                      COLHDG('Date of' ' Entry')
     A                                      TEXT('Date of Entry YYYY/MM/DD')
     A            ASDTCH    R               REFFLD(DATDFO)
     A                                      COLHDG('Date of' ' Change')
     A                                      TEXT('Date of Change YYYY/MM/DD')
     A            ASCHTI    R               REFFLD(TIME)
     A                                      COLHDG('Change' ' Time')
     A                                      TEXT('Change Time')
     A            ASCHTY    R               REFFLD(ACTN)
     A                                      COLHDG('Chg' 'Typ')
     A                                      TEXT('Change Type')
     A            ASCHUS    R               REFFLD(JOUSER)
     A                                      COLHDG('Change' 'User Id.')
     A                                      TEXT('Change User Id.')
     A            ASCHWS    R               REFFLD(JOUSER)
     A                                      COLHDG('Change' 'Wrkstn Id.')
     A                                      TEXT('Change WrkStn Id.')
     A            ASEXPF    R               REFFLD(DLTF)
     A                                      COLHDG('Exp' 'Flg')
     A                                      TEXT('Expiry Flag')
     A            ASOURN        18A         TEXT('Our Nostro')
     A            ASSQNO         4S 0       TEXT('Ext. Settlements sequence')
 
     A          K ASNETR
     A          K ASSQNO
