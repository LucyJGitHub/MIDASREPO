      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas FX Valid FX deals')                              *
      *****************************************************************
      *                                                               *
      *  Midas - FX ILE Module                                        *
      *                                                               *
      *  PF/FXVFXDLPD - Valid FX Deals output from FXFXDLVAL          *
      *                                                               *
      *  This file has the same layout as FXDEALPP                    *
      *  (but its field names are prefixed 'F3' instead of 'FH')      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CDL099             Date 06Oct17               *
      *                 CDL094             Date 30May14               *
      *                 CSW212             Date 03May12               *
      *                 CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 31Mar06               *
      *                 CSD027             Date 16Jan06               *
      *                 CDL038             Date 10May05               *
      *                 CGL029             Date 01Sep03               *
      *                 CDL010             Date 02Aug02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP051             Date 10Nov99               *
      *                 CPB001             Date 01Jun99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP002  *CREATE    Date 08Oct97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL099 - Split Value Date                                    *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *  CSW212 - SWIFT 2012 Changes                                  *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *  CDL049 - Addition of a Reference Rate field                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDL010 - Prevent last user that actioned the trade from      *
      *           authorising it.                                     *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *  CDL008 - Continuous Linked Settlement                        *
      *  CAP051 - Automatic Authorisation (FX Part)                   *
      *  CPB001 - Meridian DBA Middleware Replication Customization.  *
      *           Add Replication Indicator.                          *
      *  CAP002 - Conversion of FX inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R FXVFXDLD0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            F3RCID    R               REFFLD(RCID)
     A            F3DLTF    R               REFFLD(DLTF)
     A            F3DLUP    R               REFFLD(DLUP)
     A            F3MLUP    R               REFFLD(MLUP)
     A            F3YLUP    R               REFFLD(YLUP)
     A            F3TLUP    R               REFFLD(TLUP)
     A            F3CHTP    R               REFFLD(CHTP)
     A            F3LCD     R               REFFLD(LCD)
     A            F3LUID    R               REFFLD(LUID)
     A            F3DN38    R               REFFLD(DN38)
     A            F3VDYY    R               REFFLD(VDYY)
     A            F3VDMM    R               REFFLD(VDMM)
     A            F3VDDD    R               REFFLD(VDDD)
     A            F3DTYY    R               REFFLD(DTYY)
     A            F3DTMM    R               REFFLD(DTMM)
     A            F3DTDD    R               REFFLD(DTDD)
     A            F3DOEY    R               REFFLD(DOEY)
     A            F3DOEM    R               REFFLD(DOEM)
     A            F3DOED    R               REFFLD(DOED)
     A            F3DADN    R               REFFLD(DADN)
     A            F3DDLT    R               REFFLD(DDLT)
     A            F3DSTS    R               REFFLD(DSTS)
     A            F3DTYP    R               REFFLD(DTYI)
     A            F3DCSN    R               REFFLD(DCSN)
      * Begin /CSD027/
     A**********  F3DCNO    R               REFFLD(DCNO)                                      CSD027
     A            F3DCNO         6A
     A                                      TEXT('Deal customer number')
     A                                      COLHDG('Deal                ' +
     A                                      'customer            ' +
     A                                      'number')
      * End /CSD027/
     A            F3DXRT    R               REFFLD(EXRT)
     A            F3DAM1    R               REFFLD(DAM1)
     A            F3CCY1    R               REFFLD(DCYF)
     A            F3CCY2    R               REFFLD(DCYS)
     A            F3DAM2    R               REFFLD(DAM2)
     A            F3DMD1    R               REFFLD(DMD1)
     A            F3DBUY    R               REFFLD(DBUY)
     A            F3DSEL    R               REFFLD(DSEL)
     A            F3BCAE    R               REFFLD(BCAE)
     A            F3BCDE    R               REFFLD(BCDE)
     A            F3DSBE    R               REFFLD(DSBE)
     A            F3AOFR    R               REFFLD(AOFR)
     A            F3DUSC    R               REFFLD(DUSC)
     A            F3DBRC    R               REFFLD(DBRC)
     A            F3MBCA    R               REFFLD(MBCA)
     A            F3DDYY    R               REFFLD(DDYY)
     A            F3DDMM    R               REFFLD(DDMM)
     A            F3DDDD    R               REFFLD(DDDD)
     A            F3FWPP    R               REFFLD(FWPP)
     A            F3ORCM    R               REFFLD(ORCM)
     A            F3DORM    R               REFFLD(DORM)
     A**Start of change CGL029
     A**********  F3MORI    R               REFFLD(MORI)                                      CGL029
     A            QQMORI        12A
     A**********  F3DORI    R               REFFLD(DORI)                                      CGL029
     A            QQDORI        12A
     A**End of change CGL029
     A            F3MOPM    R               REFFLD(MOPM)
     A            F3DOPM    R               REFFLD(DOPM)
     A**Start of change CGL029
     A**********  F3MOPI    R               REFFLD(MOPI)                                      CGL029
     A            QQMOPI        12A
     A**********  F3DOPI    R               REFFLD(DOPI)                                      CGL029
     A            QQDOPI        12A
     A**End of change CGL029
     A            F3TYPE    R               REFFLD(DTPE)
     A            F3STYP    R               REFFLD(DLSD)
     A            F3BROK    R               REFFLD(BROK)
     A            F3BRKG    R               REFFLD(BRKG)
     A            F3DCPI    R               REFFLD(DCPI)
     A            F3MCPI    R               REFFLD(MCPI)
     A            F3DCRI    R               REFFLD(DCRI)
     A            F3MCRI    R               REFFLD(MTDT)
     A            F3DFAC    R               REFFLD(DFAC)
     A            F3MFAO    R               REFFLD(MFAC)
     A            F3SPEC    R               REFFLD(SPEC)
     A            F3LACT    R               REFFLD(LACT)
     A            F3ETIM    R               REFFLD(TIME)
     A            F3OOFA    R               REFFLD(OOFA)
     A            F3OOSA    R               REFFLD(OOSA)
     A            F3BKCY    R               REFFLD(BKCY)
     A            F3PCDN    R               REFFLD(DLPC)
     A            F3PCTN    R               REFFLD(PCTN)
     A            F3ADAT    R               REFFLD(DACT)
     A            F3FEDF    R               REFFLD(FEDF)
     A            F3BOKC    R               REFFLD(BOKC)
     A            F3LNKN    R               REFFLD(LNKDN)
     A            F3RSTM    R               REFFLD(RSTM)
     A**Start of change CGL029
     A**********  F3RONS    R               REFFLD(RONS)                                      CGL029
     A            QQRONS        12A
     A**End of change CGL029
     A            F3RIBN    R               REFFLD(RIBN)
     A            F3RIBA    R               REFFLD(RIBA)
      * Begin /CSD027/
     A**********  F3ROBN    R               REFFLD(ROBN)                                      CSD027
     A**********  F3ROCN    R               REFFLD(ROCN)                                      CSD027
     A            F3ROBN         6A
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            F3ROCN         6A
     A                                      TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            F3PSTM    R               REFFLD(PSTM)
     A**Start of change CGL029
     A**********  F3PONS    R               REFFLD(PONS)                                      CGL029
     A            QQPONS        12A
     A**End of change CGL029
     A            F3PIBN    R               REFFLD(PIBN)
     A            F3PIBA    R               REFFLD(PIBA)
      * Begin /CSD027/
     A**********  F3POBN    R               REFFLD(POBN)                                      CSD027
     A**********  F3POCN    R               REFFLD(POCN)                                      CSD027
     A            F3POBN         6A
     A                                      TEXT('pay - ordering bank no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            F3POCN         6A
     A                                      TEXT('pay - ordering customer no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            F3RCRN    R               REFFLD(RCRN)
     A            F3RCRA    R               REFFLD(RCRA)
     A            F3RVNO    R               REFFLD(RVNO)
     A            F3AWBN    R               REFFLD(AWBN)
     A            F3AWBA    R               REFFLD(AWBA)
     A            F3BENN    R               REFFLD(BENN)
     A            F3BENA    R               REFFLD(BENA)
     A            F3DTP1    R               REFFLD(DTP1)
     A            F3DTP2    R               REFFLD(DTP2)
     A            F3DTP3    R               REFFLD(DTP3)
     A            F3DTP4    R               REFFLD(DTP4)
     A            F3DCHG    R               REFFLD(DCHG)
     A            F3BTB1    R               REFFLD(BTB1)
     A            F3BTB2    R               REFFLD(BTB2)
     A            F3BTB3    R               REFFLD(BTB3)
     A            F3BTB4    R               REFFLD(BTB4)
     A            F3BTB5    R               REFFLD(BTB5)
     A            F3BTB6    R               REFFLD(BTB6)
     A            F3ORBR    R               REFFLD(ORBR)
     A            F3PRFC    R               REFFLD(PRFC)
     A            F3ROBR    R               REFFLD(ROBR)
     A            F3POBR    R               REFFLD(POBR)
     A            F3CVMR    R               REFFLD(CVMR)
     A            F3MGPT    R               COLHDG('Margin' 'Points')
     A                                      TEXT('Margin Points')
     A                                      REFFLD(HIPT)
     A            F3MGAM    R               COLHDG('Margin' 'Amount')
     A                                      TEXT('Margin Amount')
     A                                      REFFLD(PCPL)
     A            F3MGCY    R               COLHDG('Marg' 'Ccy')
     A                                      TEXT('Marg Ccy')
     A                                      REFFLD(CCY)
     A            F3MGBS    R               COLHDG('Marg buy/' 'sell ind')
     A                                      TEXT('Marg buy/ sell ind')
     A                                      REFFLD(BYSL)
     A            F3OSRT    R               COLHDG('Original' 'Spot rate')
     A                                      TEXT('Original Spot rate')
     A                                      REFFLD(SPOT)
     A            F3BFPA    R               COLHDG('Buy)forward' 'Points +
     A                                              amount')
     A                                      TEXT('Buy forward Points amount')
     A                                      REFFLD(PCPL)
     A            F3SFPA    R               COLHDG('Sell forward' 'Points +
     A                                              amount')
     A                                      TEXT('Sell forward Points amount')
     A                                      REFFLD(PCPL)
     A            F3BDBS    R               COLHDG('Input DL base' 'spot +
     A                                              rate')
     A                                      TEXT('Input DL base spot rate')
     A                                      REFFLD(SPOT)
     A            F3SDBS    R               COLHDG('Other DL base' 'spot +
     A                                              rate')
     A                                      TEXT('Other DL base spot rate')
     A                                      REFFLD(SPOT)
     A            F3SDBE    R               COLHDG('Other DL base' 'spot +
     A                                              equiv')
     A                                      TEXT('Other DL base spot equiv')
     A                                      REFFLD(PCPL)
     A            F3BDBF    R               COLHDG('Buy)DL base' 'fwd +
     A                                              points')
     A                                      TEXT('Buy DL base fwd points')
     A                                      REFFLD(HIPT)
     A            F3SDBF    R               COLHDG('Sell DL base' 'fwd +
     A                                              points')
     A                                      TEXT('Sell DL base fwd points')
     A                                      REFFLD(HIPT)
     A            F3BDFA    R               COLHDG('Buy)DL base' 'fwd +
     A                                              amount')
     A                                      TEXT('Buy DL base fwd amount')
     A                                      REFFLD(PCPL)
     A            F3SDFA    R               COLHDG('Sell DL base' 'fwd +
     A                                              amount')
     A                                      TEXT('Sell DL base fwd amount')
     A                                      REFFLD(PCPL)
     A            F3PTFR    R               REFFLD(PTFR)
     A            F3NBUY    R               REFFLD(NTBS)
     A                                      TEXT('Net Buy Indicator')
     A                                      COLHDG('Net' 'Buy')
     A            F3NBRF    R               REFFLD(NTRF)
     A                                      TEXT('Net Buy Reference')
     A                                      COLHDG('Net Buy' 'Reference')
     A            F3NSEL    R               REFFLD(NTBS)
     A                                      TEXT('Net Sell Indicator')
     A                                      COLHDG('Net' 'Sel')
     A            F3NSRF    R               REFFLD(NTRF)
     A                                      TEXT('Net Sell Reference')
     A                                      COLHDG('Net Sell' 'Reference')
     A            F3MTCH    R               REFFLD(MADL)
     A                                      TEXT('Matched Deal')
     A                                      COLHDG('Mat' 'Dl.')
 
     A            F3RSCY    R               TEXT('Receipt Settlement +
                                                 Currency')
                                            COLHDG('Receipt' +
                                                 'Settlement Currency')
                                            REFFLD(CCY)
     A            F3PSCY    R               TEXT('Payment Settlement +
                                                 Currency')
                                            COLHDG('Payment' +
                                                 'Settlement Currency')
                                            REFFLD(CCY)
     A            F3IC72    R               TEXT('''In'' Currency +
                                                 in Field 72')
                                            COLHDG('''In'' Currency' +
                                                   'in Field 72')
                                            REFFLD(CCY)
 
     A            F3DDDN    R               REFFLD(DDDN)
     A            F3VDDN    R               REFFLD(VDDN)
     A            F3OTDN    R               REFFLD(OTDN)
     A            F3FRNT    R               REFFLD(FOTRNID)
     A            F3AFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Assoc Front Office Trans Id')
     A                                      COLHDG('Associated' 'Front Office' +
     A                                             'Trans Id')
     A            F3REPA    R               REFFLD(RPRLOC)
      ** Timestamp
     A            F3TMESTMP R               REFFLD(TIMESTMP)
      *                                              Begin /CPB001/
     A            F3REPI         1          TEXT('Replication Indicator')
     A                                      COLHDG('Replication' 'Indicator')
      *                                              End   /CPB001/
      *                                              Begin /CAP051/
      ** Automatic Authorisation
     A            F3AUTH         1A         TEXT('Automatic Authorisation')
     A                                      COLHDG('Automatic' 'Authorisation')
      *                                              End   /CAP051/
      *                                              Begin /CDL008/
     A            F3CLSS         1          TEXT('CLS Settlement')
     A                                      COLHDG('CLS Settlement')
      *                                              End   /CDL008/
      **Start of change CAS001
     A            F3OYLC         5          TEXT('Our/Buy Yield Curve')
     A                                      COLHDG('Our/Buy' +
     A                                             'Yield Curve')
     A            F3TYLC         5          TEXT('Their/Sell Yield Curve')
     A                                      COLHDG('Their/Sell' +
     A                                             'Yield Curve')
      **End of change CAS001
      * Start of change CDL010
     A            F3USER        10          TEXT('User of Last Update')
     A                                      COLHDG('Deal Authorisation')
      * End of change CDL010
     A**Start of change CGL029
     A            F3MORI        18A
     A            F3DORI        18A
     A            F3MOPI        18A
     A            F3DOPI        18A
     A            F3RONS        18A
     A            F3PONS        18A
     A**End of change CGL029
     A*                                              Begin /CDL038/
     A            F3CLAS         4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
     A**Start of change CDL049
     A            F3RRAT        13P 8       TEXT('Reference Rate')
     A                                      COLHDG('Reference Rate')
     A**End of change CDL049
      **Start of change CER043
      *
     A            F3REIN         1          TEXT('Reciprocal Rate +
     A                                            Indicator')
     A                                      COLHDG('Reciprocal Rate' +
     A                                             'Indicator')
      **End of change CER043
      **Start of change CSW212
     A            F3DSR1        35          TEXT('Def sender-receiver info. 1')
     A                                      COLHDG('Def sender-receiver' +
     A                                             'info. 1')
     A            F3DSR2        35          TEXT('Def sender-receiver info. 2')
     A                                      COLHDG('Def sender-receiver' +
     A                                             'info. 2')
     A            F3DSR3        35          TEXT('Def sender-receiver info. 3')
     A                                      COLHDG('Def sender-receiver' +
     A                                             'info. 3')
     A            F3DSR4        35          TEXT('Def sender-receiver info. 4')
     A                                      COLHDG('Def sender-receiver' +
     A                                             'info. 4')
     A            F3DSR5        35          TEXT('Def sender-receiver info. 5')
     A                                      COLHDG('Def sender-receiver' +
     A                                             'info. 5')
     A            F3DSR6        35          TEXT('Def sender-receiver info. 6')
     A                                      COLHDG('Def sender-receiver' +
     A                                             'info. 6')
     A            F3SSR1        35          TEXT('Set sender-receiver info. 1')
     A                                      COLHDG('Set sender-receiver' +
     A                                             'info. 1')
     A            F3SSR2        35          TEXT('Set sender-receiver info. 2')
     A                                      COLHDG('Sef sender-receiver' +
     A                                             'info. 2')
     A            F3SSR3        35          TEXT('Sef sender-receiver info. 3')
     A                                      COLHDG('Set sender-receiver' +
     A                                             'info. 3')
     A            F3SSR4        35          TEXT('Set sender-receiver info. 4')
     A                                      COLHDG('Set sender-receiver' +
     A                                             'info. 4')
     A            F3SSR5        35          TEXT('Set sender-receiver info. 5')
     A                                      COLHDG('Set sender-receiver' +
     A                                             'info. 5')
     A            F3SSR6        35          TEXT('Set sender-receiver info. 6')
     A                                      COLHDG('Set sender-receiver' +
     A                                             'info. 6')
     A            F3CND1        35          TEXT('Confirmation condition 1')
     A                                      COLHDG('Confirmation' +
     A                                             'Condition 1')
     A            F3CND2        35          TEXT('Confirmation condition 2')
     A                                      COLHDG('Confirmation' +
     A                                             'Condition 2')
     A            F3CND3        35          TEXT('Confirmation condition 3')
     A                                      COLHDG('Confirmation' +
     A                                             'Condition 3')
     A            F3CND4        35          TEXT('Confirmation condition 4')
     A                                      COLHDG('Confirmation' +
     A                                             'Condition 4')
     A            F3CND5        35          TEXT('Confirmation condition 5')
     A                                      COLHDG('Confirmation' +
     A                                             'Condition 5')
     A            F3CND6        35          TEXT('Confirmation condition 6')
     A                                      COLHDG('Confirmation' +
     A                                             'Condition 6')
     A            F3ISDA         4S 0       TEXT('Version of ISDA')
     A                                      COLHDG('Version of ISDA')
     A            F3AGTY         6          TEXT('Type of ISDA Agreement')
     A                                      COLHDG('Type of ISDA' +
     A                                             'Agreement')
     A            F3AGDT         6S 0       TEXT('Date of ISDA Agreement')
     A                                      COLHDG('Date of ISDA' +
     A                                             'Agreement')
     A            F3AGVV         2S 0       TEXT('Version of ISDA Agreement')
     A                                      COLHDG('Version of ISDA' +
     A                                             'Agreement')
     A            F3AGVC         2S 0       TEXT('Version of ISDA Agreement +
     A                                            Century')
     A                                      COLHDG('Version of ISDA' +
     A                                             'Agreement Century')
      **End of change CSW212
      ** Start of change CDL094
     A            F3RBB1        35          TEXT('Recv Bank to Bank Info 1')
     A                                      COLHDG('Recv Bank' 'to Bank Info 1')
     A            F3RBB2        35          TEXT('Recv Bank to Bank Info 2')
     A                                      COLHDG('Recv Bank' 'to Bank Info 2')
     A            F3RBB3        35          TEXT('Recv Bank to Bank Info 3')
     A                                      COLHDG('Recv Bank' 'to Bank Info 3')
     A            F3RBB4        35          TEXT('Recv Bank to Bank Info 4')
     A                                      COLHDG('Recv Bank' 'to Bank Info 4')
     A            F3RBB5        35          TEXT('Recv Bank to Bank Info 5')
     A                                      COLHDG('Recv Bank' 'to Bank Info 5')
     A            F3RBB6        35          TEXT('Recv Bank to Bank Info 6')
     A                                      COLHDG('Recv Bank' 'to Bank Info 6')
      ** End of change CDL094
      ** Start of change CDL099
     A            F3VS1Y         4S 0       TEXT('Deal split value date - YYYY')
     A                                      COLHDG('Deal split value' +
     A                                             'date - YYYY')
     A            F3VS1M         2S 0       TEXT('Deal split value date - MM')
     A                                      COLHDG('Deal split value' +
     A                                             'date - MM')
     A            F3VS1D         2S 0       TEXT('Deal split value date - DD')
     A                                      COLHDG('Deal split value' +
     A                                             'date - DD')
     A            F3VS2Y         4S 0       TEXT('Deal split value date - YYYY')
     A                                      COLHDG('Deal split value' +
     A                                             'date - YYYY')
     A            F3VS2M         2S 0       TEXT('Deal split value date - MM')
     A                                      COLHDG('Deal split value' +
     A                                             'date - MM')
     A            F3VS2D         2S 0       TEXT('Deal split value date - DD')
     A                                      COLHDG('Deal split value' +
     A                                             'date - DD')
     A            F3S1DN         5S 0       TEXT('Deal split value date +
     A                                           1 day number')
     A                                      COLHDG('Deal split value' +
     A                                             'date 1 day no')
     A            F3S2DN         5S 0       TEXT('Deal split value date +
     A                                           2 day number')
     A                                      COLHDG('Deal split value' +
     A                                             'date 2 day no')
      ** End of change CDL099
