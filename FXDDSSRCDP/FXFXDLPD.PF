      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas FX API FXDL format definition file')             *
      *****************************************************************
      *                                                               *
      *  Midas - Application Program Interface Module                 *
      *                                                               *
      *  PF/FXFXDLPD - API Incoming Message Definition File for       *
      *                Foreign Exchange Deals                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CSF011A            Date 28Nov11               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 31Mar06               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 CDL038             Date 10May05               *
      *                 201083             Date 15May02               *
      *                 196952             Date 06May02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP002  *CREATE    Date 06Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSF011A - CCR015: Display Order in Confirmation Pages        *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *  CDL049 - Addition of a Reference Rate field                  *
      *  CDL038 - Extended Deal Sub Type                              *
      *  201083 - Wrong field description for Margin information. Fix *
      *           is to replace field descriptions with correct one.  *
      *  196952 - Ensure Rate has 5 digits before decimal separator   *
      *           and 8 digits after. Introduce new field DDRAT1.     *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *  CDL008 - Continuous Linked Settlement                        *
      *  CAP002 - Conversion of FX inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R FXFXDLD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
      ** Message contents
      *
      ** Action code
     A            DDACTN    R               REFFLD(ACTO)
      *
      ** Deal number
     A            DDDLNO    R     A         REFFLD(DEAL)
      *
      ** Deal type
     A            DDDLTP    R               REFFLD(DTPE)
      *
      ** Deal subtype
     A            DDDLST    R               REFFLD(DLST)
      *
      ** Federal funds
     A            DDFEDF    R               REFFLD(FEDF)
      *
      ** Deal date
     A            DDDLDT    R     A         REFFLD(CHGD)
     A                                      TEXT('Deal Date')
     A                                      COLHDG('Deal' 'Date')
      *
      ** Branch code
     A            DDBRSN    R               REFFLD(BRSN)
     A                                      TEXT('Branch code')
      *
      ** Broker code
     A            DDBRKR    R               REFFLD(BKCD)
      *
      ** Brokerage
     A            DDBKRG    R   +1A         TEXT('Brokerage')
     A                                      REFFLD(AMNP)
      *
      ** Swap/Option deal number
     A            DDSDNO    R     A         TEXT('Swap/Option Deal No')
     A                                      REFFLD(DEAL)
      *
      ** Customer
     A            DDCUST    R               REFFLD(CSNM)
     A                                      TEXT('Customer Shortname or number')
      **Start of change CSF011A
     A            DDCRSN        10A         TEXT('Customer Shortname')
     A                                      COLHDG('Customer Shortname')
     A            DDCRNM        20A         TEXT('Customer Report Name')
     A                                      COLHDG('Customer Report Name')
     A            DDCRTN        10A         TEXT('Customer Report Town')
     A                                      COLHDG('Customer Report Town')
      **End of change CSF011A
      *
      ** Value date
     A            DDVDAT    R     A         REFFLD(CHGD)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value' 'Date')
      *
      ** Option to Date
     A            DDODAT    R     A         TEXT('Option To Date')
     A                                      COLHDG('Opt To' 'Date')
     A                                      REFFLD(CHGD)
      *
      ** Book code
     A            DDBOKC    R               TEXT('Book Code')
     A                                      REFFLD(BOKC)
      *
      ** Linked reference number
     A            DDLNKN    R     A         TEXT('Linked Ref No')
     A                                      REFFLD(LNKDN)
      *
      ** Portfolio reference
     A            DDPTFR    R               TEXT('Portfolio Ref')
     A                                      REFFLD(PTFR)
      *
      ** Buy Currency
     A            DDBCCY    R               TEXT('Buy Currency')
     A                                      COLHDG('Buy' 'Ccy')
     A                                      REFFLD(CCY)
      *
      ** Buy Amount
     A            DDBAMT    R               TEXT('Buy Amount')
     A                                      COLHDG('Buy' 'Amount')
     A                                      REFFLD(AM14)
      *
      ** Buy Net Indicator
     A            DDNBUY    R               TEXT('Buy Net Ind')
     A                                      COLHDG('Buy' 'Net')
     A                                      REFFLD(NTBS)
      *
      **                                                 Begin /196952/
      ***Exchange*Rate*************************************************
     A**********  DDRATE    R     A         REFFLD(EXRT)
      ** Filler
     A            ZZ013     R
      **                                                 End   /196952/
      *
      ** Forward Points
     A            DDFPNT    R   +1A         REFFLD(FWPP)
      *
      ** Forward Points Sign
     A            DDSIGN    R               REFFLD(FWPS)
      *
      ** Margin Points
      **Start of change 201083
     A**********  DDMGIN    R   +1A         REFFLD(FWPP)
     A            DDMGIN         8A         TEXT('Margin Points')
     A                                      COLHDG('Margin Pts')
      **End of change 201083
      *
      ** Margin Points Buy/sell Ind
      **Start of change 201083
     A**********  DDMBSI    R               REFFLD(FWPS)
     A            DDMBSI         1A         TEXT('Margin Pts Sign')
     A                                      COLHDG('Margin' 'Pts' 'Sign')
      **End of change 201083
      *
      ** Sell Currency
     A            DDSCCY    R               TEXT('Sell Currency')
     A                                      COLHDG('Sell' 'Ccy')
     A                                      REFFLD(CCY)
      *
      ** Sell Amount
     A            DDSAMT    R               TEXT('Sell Amount')
     A                                      COLHDG('Sell' 'Amount')
     A                                      REFFLD(AM14)
      *
      ** Profit Centre
     A            DDPRFC    R               REFFLD(PRFC)
      *
      ** Sell Net Indicator
     A            DDNSEL    R               TEXT('Sell Net Ind')
     A                                      COLHDG('Sell' 'Net')
     A                                      REFFLD(NTBS)
      *
      ** Base Currency Equiv
     A            DDBCE     R   14A         REFFLD(BCDE)
      *
      ** Originating branch
     A            DDORBR    R               REFFLD(ORBR)
      *
      ** DL Base Spot Rate
     A            DDDBSR    R     A         REFFLD(EXRT)
      *
      ** Buy DL Base Fwd Pts
     A            DDBBFP    R   +1A         REFFLD(FWPP)
     A                                      TEXT('Buy DL Base Fwd Pts')
     A                                      COLHDG('Buy DL' 'Base Fwd Pts')
      *
      ** Buy DL Base Fwd Pts sign
     A            DDBBFS    R               REFFLD(FWPS)
     A                                      TEXT('Buy DL Base Fwd Pts Sign')
     A                                      COLHDG('Buy DL' 'Base Fwd Sgn')
      *
      ** Sell DL Base Fwd Pts
     A            DDSBFP    R   +1A         REFFLD(FWPP)
     A                                      TEXT('Sell DL Base Fwd Pts')
     A                                      COLHDG('Sell DL' 'Base Fwd Pts')
      *
      ** Sell DL Base Fwd Pts sign
     A            DDSBFS    R               REFFLD(FWPS)
     A                                      TEXT('Sell DL Base Fwd Pts Sign')
     A                                      COLHDG('Sell DL' 'Base Fwd Sgn')
      *
      ** Manually Matched
     A            DDMTCH    R               REFFLD(MADL)
     A                                      TEXT('Manually Matched')
     A                                      COLHDG('Manually Matched')
      *                                              Begin /CDL008/
     A            DDCLSS         1          TEXT('CLS Settlement ')
     A                                      COLHDG('CLS Settlement')
      *                                              End   /CDL008/
      **Start of change CAS001
     A            DDOYLC         5          TEXT('Our/Buy Yield Curve')
     A                                      COLHDG('Our/Buy' +
     A                                             'Yield Curve')
     A            DDTYLC         5          TEXT('Their/Sell Yield Curve')
     A                                      COLHDG('Their/Sell' +
     A                                             'Yield Curve')
      **End of change CAS001
      **                                                 Begin /196952/
      ** Exchange Rate
     A            DDRAT1    R   +1A         REFFLD(EXRT)
     A                                      TEXT('Exchange Rate')
     A                                      COLHDG('Exchange Rate')
      **                                                 End   /196952/
     A*                                              Begin /CDL038/
     A            DDCLAS         4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
     A**Start of change CDL049
     A            DDRRAT        14A         TEXT('Reference Rate')
     A                                      COLHDG('Reference Rate')
     A**End of change CDL049
      ** Start of change CER043
     A            DDREIN         1          TEXT('Reciprocal Rate +
     A                                            Indicator')
     A                                      COLHDG('Reciprocal Rate' +
     A                                             'Indicator')
      ** End of change CER043
