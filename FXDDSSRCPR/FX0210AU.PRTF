      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Daylight & overnight ccy limits rept')
     F****************************************************************
     F*                                                              *
     A*  Midas - Foreign Exchange (Dealing)
     F*                                                              *
     F*    PRTF/FX0210AU - DAYLIGHT & OVERNIGHT CURRENCY LIMITS      *
     F*                    AUDIT REPORT                              *
     F*                                                              *
     F*  (c) Finastra International Limited 2001                     *
     F*                                                              *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*    PREV AMEND NO. X00000              DATE 00XXX00           *
     F*                                                              *
     F****************************************************************
     F*                                                              *
     F*  MD046248 - Finastra Rebranding                              *
     F*                                                              *
     F****************************************************************
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
     F*
     F*       65         ERROR INDICATOR - INCORRECT NO OF RECS ON FILE
     F*       66         EXCEPTION ITEM APPEARS ON DETAIL REPORT
     F*       67         NO CHANGES MADE TO THE FILE DURING THE DAY
     A*
     A**  FILE LEVEL KEYWORDS
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A**  FORMAT FOR HEADINGS
     A          R FXAUDAR0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(2)
     A                                     8'REFERENCE FX0210AU'
     A            RRTITL    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            RRRUNA    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A*
     A                                    22'            Midas            '
     A                                    52'- DAYLIGHT/OVERNIGHT '
     A                                    73'LIMITS AUDIT REPORT'
     A                                      SPACEA(1)
     A                                    22'            -----------------'
     A                                    52'---------------------'
     A                                    73'-------------------'
     A                                      SPACEA(2)
     A*
     A**  FORMAT FOR DETAILS
     A          R FXAUDAR1                  TEXT('DETAILS FORMAT')
     A                                    10'FILE NAME :'
     A            RRFILE    R             22REFFLD(DBFILE)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF RECORDS BEFORE -
     A                                       UPDATE:'
     A            RRNREC         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF INSERTIONS:'
     A            RRNINS         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF DELETIONS:'
     A            RRNDEL         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF AMENDMENTS:'
     A            RRNAMD         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF RECORDS AFTER -
     A                                      UPDATE:'
     A            RRNCAL         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'ACTUAL NUMBER OF RECORDS-
     A                                       ON THE FILE:'
     A            RRNORE         9  0     51EDTCDE(3)
     A*
     A  65                                70'** DIFFERENCE **'
     A*
     A**  PRINT THIS MESSAGE IF AN EXCEPTION ITEM IS ON DETAIL REPORT
     A  66                                11'***** AN EXCEPTION +
     A                                      ITEM APPEARS ON THE DETAIL +
     A                                      AUDIT REPORT ASSOCIATED +
     A                                      WITH THIS CONTROL +
     A                                      REPORT *****'
     A                                      SPACEB(3)
     A*
     A**  FORMAT FOR DATABASE ERROR REPORTING
     A          R FXAUDAR2                  TEXT('DATABASE ERROR')
     A                                      SKIPB(20)
     A                                    41'DATABASE ERROR'
     A            RRDBSE    R             56REFFLD(DBASE)
     A                                    60'IN PROGRAM'
     A            RRPGM     R             71REFFLD(DBPGM)
     A                                      SPACEA(1)
     A                                    41'FILE'
     A            RRDBFL    R             46REFFLD(DBFILE)
     A                                    55'KEY'
     A            RRDKEY    R             59REFFLD(DBKEY)
     A                                      SPACEB(3)
     A**  FORMAT FOR NO DETAILS
     A          R FXAUDAR3                  TEXT('NO DETAILS FORMAT')
     A                                    44'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(2)
