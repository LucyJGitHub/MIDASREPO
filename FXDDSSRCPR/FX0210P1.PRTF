      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Daylight & Overnight Ccy Limits Rep.')
     F****************************************************************
     F*                                                              *
     A*  Midas - Foreign Exchange (Dealing)
     F*                                                              *
     F*    PRTF/FX0210P1 - DAYLIGHT & OVERNIGHT CURRENCY LIMITS      *
     F*                    AUDIT REPORT.                             *
     F*                                                              *
      *  (c) Finastra International Limited 2001                      *
     F*                                                              *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01319             Date 25Sep91               *
      *                 XXXXX              Date XX/xx/XX              *
     F****************************************************************
     A*                                                              *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01319 - Remove redundant processing
     A*             File renamed from FX0110RR
     F****************************************************************
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
     F*
     F*       65         ERROR INDICATOR - INCORRECT NO OF RECS ON FILE
     F*       67         NO CHANGES MADE TO THE FILE DURING THE DAY
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A**  FORMAT FOR HEADINGS
     A          R FX0210R0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(2)
     A*                                              Begin /S01319/
     A*************************************8'REFERENCE'
     A                                     8'REFERENCE FX0210P1'
     A************RRPGM*****R*************18REFFLD(DBPGM)
     A*                                              End   /S01319/
     A            RRTITL    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            RRRUNA    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A*                                              Begin /S01319/
     A************RRPRNM****R*************21REFFLD(TTIT)******************S01319
     A**************************************SPACEA(1)*********************S01319
     A************RRPRUL****R*************21REFFLD(TTIT)******************S01319
     A                                    22'            Midas            '
     A                                    52'- DAYLIGHT/OVERNIGHT '
     A                                    73'LIMITS AUDIT REPORT'
     A                                      SPACEA(1)
     A                                    22'            -----------------'
     A                                    52'---------------------'
     A                                    73'-------------------'
     A*                                              End   /S01319/
     A                                      SPACEA(3)
     A*
     A**  FORMAT FOR DETAIL HEADER
     A          R FX0210R4                  TEXT('DET. HEADER FORMAT')
     A                                    21'USER ID/'
     A                                    31'CHANGE TYPE/'
     A                                    66'CURRENCY'
     A                                    81'DAYLIGHT LIMIT'
     A                                   104'OVERNIGHT LIMIT'
     A                                      SPACEA(1)
     A                                    21'PROGRAM'
     A                                    38'TIME'
     A                                    46'WSID'
     A                                    68'CODE'
     A                                    84'IN 000s'
     A                                   108'IN 000s'
     A                                      SPACEA(2)
     A*
     A**  FORMAT FOR DETAIL LINES
     A          R FX0210R1                  TEXT('DETAIL LINES FORMAT')
     A            RRUSER    R             21REFFLD(JOUSER)
     A            RRTYP          3        35
     A                                    55'BEFORE'
     A            RRCCY     R             68REFFLD(CCY)
     A            RRDLTC    R             81REFFLD(DLTC)
     A                                      EDTCDE(Z)
     A            RROLTC    R            105REFFLD(OLTC)
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A            RRPGMA    R             21REFFLD(JOPGM)
     A            RRTIME    R             33REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A            RRJOB     R             43REFFLD(JOJOB)
     A                                    55'AFTER'
     A            RRCCA     R             68REFFLD(CCY)
     A            RRDLTA    R             81REFFLD(DLTC)
     A                                      EDTCDE(Z)
     A            RROLTA    R            105REFFLD(OLTC)
     A                                      EDTCDE(Z)
     A N64N63                               SPACEA(3)
     A  63                                21'**********'
     A  63                                  SPACEB(1)
     A  63N64                               SPACEA(3)
     A  64                                33'*****  THIS ACTION IS NOT +
     A                                      REFLECTED IN THE DATABASE +
     A                                      DUE TO RECOVERY PROCESSING  *****'
     A  64N63                               SPACEB(1)
     A  64                                  SPACEA(3)
     A*
     A*                                              Begin /S01319/
     A****FORMAT*FOR*NO*DETAILS****************************************
     A**********R*FX0210R6******************TEXT('NO*DETAILS*FORMAT')**
     A************************************44'****NO*DETAILS*TO*REPORT****'
     A**************************************SPACEB(2)******************
     A*                                              End   /S01319/
     A*
     A**  FORMAT FOR END OF REPORT
     A          R FX0210R5                  TEXT('END OF REPORT FORMAT')
     A                                    48'*** END OF REPORT ***'
     A                                      SPACEB(2)
     A*
     A**  FORMAT FOR CONTROL TOTALS
     A          R FX0210R2                  TEXT('CONTROLS FORMAT')
     A                                      SKIPB(10)
     A                                    10'NUMBER OF RECORDS BEFORE -
     A                                       UPDATE:'
     A            RRNREC         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF INSERTIONS:'
     A            RRNINS         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF DELETIONS:'
     A            RRNDEL         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF AMENDMENTS:'
     A            RRNAMD         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF RECORDS AFTER -
     A                                      UPDATE:'
     A            RRNCAL         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'ACTUAL NUMBER OF RECORDS-
     A                                       ON THE FILE:'
     A            RRNORE         9  0     51EDTCDE(3)
     A*
     A  65                                70'** DIFFERENCE **'
     A                                    48'*** END OF REPORT ***'
     A                                      SPACEB(3)
     A*
     A*
     A**  FORMAT FOR DATABASE ERROR REPORTING
     A          R FX0210R3                  TEXT('DATABASE ERROR')
     A                                      SKIPB(20)
     A                                    41'DATABASE ERROR'
     A            RRDBSE    R             56REFFLD(DBASE)
     A                                    60'IN PROGRAM'
     A            RRPGM     R             71REFFLD(DBPGM)
     A                                      SPACEA(1)
     A                                    41'FILE'
     A            RRDBFL    R             46REFFLD(DBFILE)
     A                                    55'KEY'
     A            RRDKEY    R             59REFFLD(DBKEY)
     A                                    48'*** END OF REPORT ***'
     A                                      SPACEB(3)
