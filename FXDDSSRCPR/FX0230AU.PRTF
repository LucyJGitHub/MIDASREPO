      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Amendable Fields For Complete Deals')
     F****************************************************************
     F*                                                              *
     A*  Midas - Foreign Exchange (Dealing)
     F*                                                              *
     F*    PRTF/FX0230AU - FX DEALER - DEALER AMENDABILITY AUDIT     *
     F*                    REPORT                                    *
     F*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*    LAST AMEND NO. X00000              DATE 00XXX00           *
     F*                                                              *
     F****************************************************************
     F*                                                              *
     F*                                                              *
     F****************************************************************
     F*
     F**  Indicator Function Summary
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
     F*
     F*       65         ERROR INDICATOR - INCORRECT NO OF RECS ON FILE
     F*       66         EXCEPTION ITEM APPEARS ON DETAIL REPORT
     A*
     A*
     A**  FILE LEVEL KEYWORDS
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A**  FORMAT FOR TITLE
     A          R FXAUDBR0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     8'REFERENCE FX0230AU'
     A            RRTITL    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            RRRUNA    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A*
     A                                    22'            Midas            '
     A                                    52'- DEAL AMENDABILITY AUDIT '
     A                                    78'REPORT'
     A                                      SPACEA(1)
     A                                    22'            -----------------'
     A                                    52'--------------------------'
     A                                    78'------'
     A                                      SPACEA(2)
     A*
     A**  FORMAT FOR TITLE
     A          R FXAUDBR1                  TEXT('DETAILS FORMAT')
     A                                    10'FILE NAME :'
     A            RRFILE    R             22REFFLD(DBFILE)
     A                                      SPACEA(2)
     A*
     A                                    10'NUMBER OF RECORDS ON THE FILE'
     A            RRNREC         9  0     47TEXT('NUMBER RECORDS')
     A                                      EDTCDE(3)
     A*
     A  65                                70'** DIFFERENCE **'
     A*
     A**  PRINT THIS MESSAGE IF AN EXCEPTION ITEM IS ON DETAIL REPORT
     A  66                                11'***** AN EXCEPTION +
     A                                      ITEM APPEARS ON THE DETAIL +
     A                                      AUDIT REPORT ASSOCIATED +
     A                                      WITH THIS CONTROL +
     A                                      REPORT *****'
     A  66                                  SPACEB(3)
     A*
     A**  FORMAT FOR DATABASE ERROR REPORTING
     A          R FXAUDBR2                  TEXT('DATABASE ERROR')
     A                                      SKIPB(20)
     A                                    41'DATABASE ERROR'
     A            RRDBSE    R             56REFFLD(DBASE)
     A                                    60'IN PROGRAM'
     A            RRPGM     R             71REFFLD(DBPGM)
     A                                      SPACEA(1)
     A                                    41'FILE'
     A            RRDBFL    R             46REFFLD(DBFILE)
     A                                    55'KEY'
     A            RRDKEY    R             59REFFLD(DBKEY)
     A                                    48'*** END OF REPORT ***'
     A                                      SPACEB(3)
     A**  FORMAT FOR NO DETAILS MESSAGE
     A          R FXAUDBR3                  TEXT('END OF REPORT MESSAGE')
     A    N67                             44'** NO DETAILS TO REPORT **'
     A    N67                               SPACEB(3)
     A    N67                               SPACEA(3)
