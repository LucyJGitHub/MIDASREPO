      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FX Net Summary/Detail Reports Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/FX0911P1 - FX Currency Net Print                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CDL002             Date 04Feb97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDL002 - FX Netting                                          *
      *                                                               *
     A*****************************************************************
     A*                  FUNCTION OF INDICATORS                       *
     A*       51         Summary Display Control                      *
     A*       52         Detail Display Control                       *
     A*       53         Paired CCYS Display Control                  *
     A*       87         Extended Settlements display Control         *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE FX0911P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  51                                46
     A                                      'FX Currency Net Summary'
     A  52                                46
     A                                      'FX Currency Net Detail'
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'-----------------------'
     A                                      SPACEA(2)
     A                                     2
     A                                      'C/party:'
     A                                      SPACEB(001)
     A            DDCUST        10A  O    11
     A N81        BBCRNM        20A  O    22
     A N81        BBCRTN        10A  O    43
     A  81        D6GPDS        30A  O    22
     A                                    54
     A                                      'Pair ccys'
     A            DDPCCY         1A  O    64
     A                                    66
     A                                      'Value'
     A            DDVALD         6  0O    72
     A                                      EDTCDE(Y)
     A                                     5
     A                                      'CCY'
     A                                      SPACEB(001)
     A  51 53N52                           9
     A                                      'Pairs'
     A  52        DDCCY          3A  O     9
     A  52        TLCCNM        14A  O    13
     A  52 53                             28
     A                                      'CCY 1:'
     A  52 53     PCCYS1         3A  O    36
     A  52 53     P1CCNM        14A  O    40
     A  52 53                             55
     A                                      'CCY 2:'
     A  52 53     PCCYS2         3A  O    63
     A  52 53     P2CCNM        14A  O    67
     A N54                                17
     A                                      '*-------BUY--------*'
     A  52                                  SPACEB(001)
     A N54                                38
     A                                      '*------SELL--------*'
     A N54                                +1
     A                                      '*--------NET--------*'
     A  54                                17
     A                                      '*--------NET--------*'
      *
      *------------------------------------*
      ***  Report Settlement Record Format *
      *------------------------------------*
      *
     A          R SETTLMNT
      *
     A                                      SPACEB(002)
     A                                      TEXT('Summary with Instructions')
     A                                      SPACEA(002)
     A            DDCCY          3A  O     5
     A                                      TEXT('Currency')
     A  53        PCCYS1         3A  O     9
     A                                      TEXT('1st pair Currency')
     A  53        PCCYS2         3A  O    13
     A                                      TEXT('2nd pair Currency')
     A            DDNMAT        21A  O    17
     A                                      TEXT('Net Total')
     A  55                                +1
     A                                      'Receive'
     A                                    +2
     A                                      'Type:'
     A  55N87     BYRSTY         2A  O    +1
     A                                      TEXT('Settlement Type')
     A N55                                39
     A                                      'Pay'
     A N55N87     BYPSTY         2A  O    54
     A                                      TEXT('Settlement Type')
     A                                   +10
     A                                      'Nostro:'
     A**Start of change CGL029
     A*N87******  WFONS         15A  O    +1                                                  CGL029
     A N87        WFONS         21A  O    +1
     A**End of change CGL029
     A                                      TEXT('Receipt - Our Nostro')
     A                                    37
     A                                      '50 Ordering Customer'
     A                                      SPACEB(003)
     A N87        DDOCS         10   O    +3
     A                                      TEXT('Ordering Cust')
     A N87        DDOCSS        12   O    +1
     A                                      TEXT('Ordering Cust SWIFT code')
     A                                    +6
     A                                      '52 Ordering Bank'
     A N87        DDOBN         10   O    +2
     A                                      TEXT('ordering bank')
     A N87        DDOBNS        12   O    +1
     A                                      TEXT('ordering bank SWIFT code')
     A                                    37
     A                                      '56 Intermediary Bank'
     A                                      SPACEB(003)
     A N87        DDIBN         10   O    +3
     A                                      TEXT('Intermediary')
     A N87        DDIBNS        12   O    +1
     A                                      TEXT('Intermediary SWIFT code')
     A N55                                37
     A                                      '57 ''Account With'' Bank'
     A                                      SPACEB(003)
     A N55N87     DDACBN        10   O    +1
     A                                      TEXT('A/c with')
     A N55N87     DDACBS        12   O    +1
     A                                      TEXT('A/c with SWIFT code')
     A N55                                +6
     A                                      '58/59 Beneficiary'
     A N55N87     DDBYNB        10   O    +1
     A                                      TEXT('Beneficiary')
     A N55N87     DDBYNS        12   O    +1
     A                                      TEXT('Beneficiary SWIFT code')
     A N55                                16
     A                                      '72 Bank to Bank Info'
     A                                      SPACEB(003)
     A N55N87     BYBBI1        35   O    +1
     A                                      TEXT('Bank to Bank Info. 1')
     A N55N87     BYBBI2        35   O    +1
     A                                      TEXT('Bank to Bank Info. 2')
     A N55N87     BYBBI3        35   O    37
     A                                      TEXT('Bank to Bank Info. 3')
     A                                      SPACEB(003)
     A N55N87     BYBBI4        35   O    +1
     A                                      TEXT('Bank to Bank Info. 4')
      *                                                                *********
      *--------------------------------*
      ***  Report Totals Record Format *
      *--------------------------------*
      *
     A          R NETTOTAL
      *                                                                *********
     A                                      SPACEB(001)
     A                                      TEXT('Summary')
     A            DDCCY          3A  O     5
     A                                      TEXT('Currency')
     A  53        PCCYS1         3A  O     9
     A                                      TEXT('1st pair Currency')
     A  53        PCCYS2         3A  O    13
     A                                      TEXT('2nd pair Currency')
     A            DDBMAT        20A  O    17
     A                                      TEXT('BUY TOTAL')
     A            DDSMAT        20A  O    38
     A                                      TEXT('Sell Total')
     A            DDNMAT        21A  O    59
     A                                      TEXT('Net Total')
      *                                                                *********
      *--------------------------------*
      ***  Report Detail Record Format *
      *--------------------------------*
      *
     A          R NETDETLS
      *                                                                *********
     A                                      TEXT('Detail')
     A                                      SPACEB(001)
     A                                     2
     A                                      'Matched'
     A            DDBMAT        20   O    17
     A                                      TEXT('Buy Matched')
     A            DDSMAT        20   O    38
     A                                      TEXT('Sell Matched')
     A            DDNMAT        21   O    59
     A                                      TEXT('Net Matched')
     A                                     2
     A                                      'Mismatch'
     A                                      SPACEB(001)
     A            DDBMIS        20   O    17
     A                                      TEXT('Buy Mismatched')
     A            DDSMIS        20   O    38
     A                                      TEXT('Sell Mismatched')
     A            DDNMIS        21   O    59
     A                                      TEXT('Net Mismatched')
     A                                     2
     A                                      'Unmatch'
     A                                      SPACEB(001)
     A            DDBUNM        20   O    17
     A                                      TEXT('Buy Unmatched')
     A            DDSUNM        20   O    38
     A                                      TEXT('Sell Unmatched')
     A            DDNUNM        21   O    59
     A                                      TEXT('Nett Unmatched')
     A                                    17
     A                                      '--------------------'
     A                                      SPACEB(001)
     A                                    38
     A                                      '--------------------'
     A                                    59
     A                                      '---------------------'
     A                                     2
     A                                      'Net Total'
     A                                      SPACEB(001)
     A            DDBTOT        20   O    17
     A                                      TEXT('Buy Total')
     A            DDSTOT        20   O    38
     A                                      TEXT('Sell Total')
     A            DDNETT        21   O    59
     A                                      TEXT('Net Total')
     A                                    17
     A                                      '--------------------'
     A                                      SPACEB(001)
     A                                    38
     A                                      '--------------------'
     A                                    59
     A                                      '---------------------'
     A                                     2
     A                                      'Outstanding'
     A                                      SPACEB(001)
     A            DDBOUT        20   O    17
     A                                      TEXT('Buy Total')
     A            DDSOUT        20   O    38
     A                                      TEXT('Sell Total')
     A            DDNOUT        21   O    59
     A                                      TEXT('Net Total')
     A                                    17
     A                                      '--------------------'
     A                                      SPACEB(001)
     A                                    38
     A                                      '--------------------'
     A                                    59
     A                                      '---------------------'
     A                                     2
     A                                      'System Total'
     A                                      SPACEB(001)
     A            DDBSYS        20   O    17
     A                                      TEXT('Buy System Total')
     A            DDSSYS        20   O    38
     A                                      TEXT('Sell System Total')
     A            DDNSYS        21   O    59
     A                                      TEXT('Net System Total')
     A                                    17
     A                                      '--------------------'
     A                                      SPACEB(001)
     A                                    38
     A                                      '--------------------'
     A                                    59
     A                                      '---------------------'
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *-------------------*
      ***  No Details     *
      *-------------------*
      *
     A          R NODETL
     A                                    51'***   NO MATCHED DEALS   ***'
     A                                      SPACEB(3)
