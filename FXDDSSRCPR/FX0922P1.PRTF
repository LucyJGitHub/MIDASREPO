      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FX Confirmed Net Deals Detail')
      *****************************************************************
      *                                                               *
      *  Midas - (Dealing FX) Module                                  *
      *                                                               *
      *  PRTF/FX0922P1 - Details of Deals within a Net.               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CDL002             Date 06Mar97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDL002 - Details of deals within a Net.                      *
      *                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
      *
     A          R FX0922PH                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE FX0922P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A            STIME          6  0O   121
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    50
     A                                      'LIST OF DEALS IN NET REFERENCE'
     A            DDNETR        19A  O    81
      *
     A                                   132' '
     A                                      SPACEA(1)
     A N81                                 2
     A                                      'C/party:'
     A  81                                 2
     A                                      '  Group:'
     A                                      SPACEB(001)
     A            DDCUST        10A  O    11
     A N81        DDCRNM        20A  O    22
     A N81        DDCTWN        10A  O    43
     A  81        GCGRPN        30A  O    22
     A                                    54
     A                                      'Value'
     A            DDVALD         6  0O    60
     A                                      EDTCDE(Y)
     A  27                                +1
     A                                      'PAIR CCY 1:'
     A  27        DCCYS1         3A  O    +1
     A  27        DDPC1N        14A  O    +1
     A  27                                +2
     A                                      'PAIR CCY 2:'
     A  27        DCCYS2         3A  O    +1
     A  27        DDPC2N        14A  O    +1
     A                                     2
     A                                      'CCY:'
     A                                      SPACEB(001)
     A            DDCCY          3A  O    +1
     A            DDNCCN        14A  O    +1
     A                                    +2
     A                                      'Net'
     A            DDNETT        21A  O    +1
     A                                      TEXT('Net Total')
     A            DDPYRC         7A       +1
     A                                    +2
     A                                      'Type:'
     A            DDSETM         2A  O    +1
     A**Start of change CGL029
     A**********  DDSEAC        15A  O    +1                                                  CGL029
     A            DDSEAC        21A  O    +1
     A**End of change CGL029
     A                                      TEXT('Our Nostro')
     A                                     7'DEAL'
     A                                      SPACEB(002)
     A                                    26'DEAL'
     A                                    51'PURCHASE'
     A                                   103'SALE'
     A                                       SPACEA(1)
     A
     A                                     6'NUMBER'
     A                                    19'TYPE'
     A                                    26'DATE'
     A  81                                33'COUNTERPARTY'
     A                                    47'CCY'
     A                                    60'AMOUNT'
     A                                    82'RATE'
     A                                    95'CCY'
     A                                   105'AMOUNT'
     A                                       SPACEA(1)
     A*%%***********************************************************************
     A          R FX0922PN
     A*%%***********************************************************************
     A                                      TEXT('Deal details')
     A                                      SPACEA(001)
     A            RRDLNO         6         6TEXT('DEAL NUMBER')
     A            RRDTYP         5        18TEXT('DEAL TYPE & SUBTYPE')
     A                                    20'-'
     A            RRDDAT         7        25TEXT('DEAL DATE')
     A  81        RRCSSN        10        34TEXT('CUSTOMER SHORTNAME')
     A            RRPCCY         3        47TEXT('BUY CURRENCY')
     A            RRAMT         22        51TEXT('PCHASE AMOUNT')
     A            RRRATE        13S 8     76TEXT('RATE')
     A                                      EDTCDE(3)
     A            RRSCCY         3        95TEXT('SELL CCY')
     A            RRAMTA        22        99TEXT('SALE AMOUNT')
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
