      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FX Net Deals completed by I/C Termination')
      *****************************************************************
      *                                                               *
      *  Midas:- Dealing Module - Foreign Exchange.                   *
      *                                                               *
      *  PRTF/FX0950P1 - Deals completed by I/C Termination           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CDL002  *CREATE    Date 01May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL002 - Deals completed by I/C Termination                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R FX0950H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE FX0950P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'UNCONFIRMED FX NET DEALS+
     A                                        - INPUT CYCLE TERMINATION'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    40'------------------------+
     A                                      ---------------------------'
     A                                      SPACEA(2)
     A                                   118'VALUE'
     A                                   126'STATUS'
     A                                      SPACEA(1)
     A                                     1'DEAL #'
     A                                     9'COUNTERPARTY'
     A                                    40'CCY'
     A                                    49'PURCHASE AMOUNT'
     A                                    68'NET'
     A                                    76'RATE'
     A                                    85'CCY'
     A                                    95'PURCHASE AMOUNT'
     A                                   113'NET'
     A                                   119'DATE'
     A                                   126'BEFORE'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R FX0950D1
      *
     A            FHDN38         6S 0O     1
     A            BBCRNM        20A  O    +1
     A            BBCRTN        10A  O    +1
     A            FHCCY1         3A  O    +1
     A            PRBAMT        24A  O    +1
     A            FHNBUY         1A  O    +1
     A            PREXRT        13A  O    +1
     A            FHCCY2         3A  O    +1
     A            PRSAMT        24A  O    +1
     A            FHNSEL         1A  O    +1
     A            DATE#1         2S 0O   117
     A            DATE#2         2S 0O   119
     A            FHVDYY         4S 0O   121
     A            BUYPRV         1A  O    +2
     A            SELPRV         1A  O    +1
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R FX0950T1
     A                                      SPACEB(2)
     A                                    45
     A                                      DFT('TOTAL NUMBER OF DEALS MODIFIED-
     A                                       DURING IC TERMINATION:')
     A            WFHSH#         4S 0O    +2
     A                                      EDTCDE(Z)
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
