      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FX Confirmed Net Update Detail')
      *****************************************************************
      *                                                               *
      *  Midas - DEALING Module                                       *
      *                                                               *
      *  PRTF/FX0960P1 - FX CONFIRMED NET DEALS PRINT                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CDL002             Date 23MAY97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDL002 - FX Netting                                          *
      *                                                               *
     A*****************************************************************
     A* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     A*       27         PAIRED CCYS DISPLAY CONTROL
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R FX0960HH                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE FX0960P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    55'LIST OF ACTIONED NETS'
      *
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    55'--------------------'
     A                                      SPACEA(2)
     A                                     2
     A                                      'Net reference'
     A                                      SPACEB(001)
     A                                    +9
     A                                      'C/party'
     A                                    +4
     A                                      'Value'
     A                                    +3
     A                                      'Pair Ccys'
     A                                    +1
     A                                      'Ccy'
     A                                    +4
     A                                      'Net Amount'
     A                                      TEXT('Net Total')
     A                                   +10
     A                                      'Settlement Details'
     A                                      SPACEA(001)
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R FX0960DD
     A                                      TEXT('Deal details')
     A                                      SPACEA(001)
     A            DDNETR        19A  O     1
     A            DDCUST        10A  O    +2
     A            DDVALD         6  0O    +2
     A                                      EDTCDE(Y)
     A  27        DCCYS1         3A  O    +2
     A  27        DCCYS2         3A  O    +1
     A            DDCCY          3A  O    +2
     A            DDNETT        21A  O    +1
     A            DDPYRC         7A       +1
     A            DDSETP         2A  O    +1
     A**Start of change CGL029
     A**********  DDSEAC        15A  O    +1                                                  CGL029
     A            DDSEAC        21A  O    +1
     A**End of change CGL029
     A                                      TEXT('Our Nostro')
      *=========================================================================
     A          R FX0960ND                  TEXT('No details to report     ')
     A                                    48'*** No details to report ***'
     A                                      SPACEB(8)
      *=========================================================================
     A          R FX0960EN                  TEXT('End of Report            ')
     A                                    52'*** End of Report ***'
     A                                      SPACEB(3)
