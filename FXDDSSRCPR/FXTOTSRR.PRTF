      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas File Control Report Totals')
     A****************************************************************
     A*                                                              *
     A*  Midas - Foreign Exchange (Dealing)
     A*                                                              *
     A*     PRTF/FXTOTSRR - STANDARD TOTALS REPORTING FOR DRS FX     *
     A*     STANDING DATA.                                           *
     A*                                                              *
      *  (c) Finastra International Limited 2001                      *
     A*                                                              *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. BHF656             Date 02Nov90               *
     A*                                                              *
     A****************************************************************
     A*                                                              *
      *  MD046248 - Finastra Rebranding                               *
     A*    BHF656 - AS THIS IS AN AUDIT REPORT SHOULD BE ON          *
     A*             OUTPUT QUEUE MPRINTAU                            *
     A*                                                              *
     A****************************************************************
     A*
     A**  Indicator Function Summary
     A*
     A* ID F  C  H  L    FUNCTION OF INDICATORS
     A* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
     A*       63         End of day processing.                        UT
     A*       64         Input cycle processing.                       UT
     A*
     A*
     A**  FILE LEVEL KEYWORDS
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A**  FORMAT FOR HEADERS.
     A          R FXTOTSR0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A*
     A                                     7'REFERENCE'
     A            RRPGM     R             17REFFLD(DBPGM)
     A            RRTITL    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            RRRUNA    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A*
     A            RRPRNM    R             21REFFLD(TTIT)
     A                                      SPACEA(1)
     A            RRPRUL    R             21REFFLD(TTIT)
     A                                      SPACEA(2)
     A*
     A**  FORMAT FOR DETAILS.
     A          R FXTOTSR1                  TEXT('DETAIL FORMAT')
     A*
     A N63                                 7'INPUT CYCLE'
     A N63        RRTIME    R             19REFFLD(TIME)
     A N63                                  SPACEA(3)
     A                                      EDTWRD('0 :  :  ')
     A  63                                 7'END OF DAY'
     A  63                                  SPACEA(3)
     A*
     A  63                                51'INPUT'
     A                                    63'CALCULATED'
     A                                      SPACEA(1)
     A*
     A  63                                51'-----'
     A                                    63'----------'
     A                                      SPACEA(2)
     A*
     A                                    21'NUMBER OF RECORDS ON FILE'
     A  63        RRNREC    R             51REFFLD(NREC)
     A                                      EDTWRD('  0 ')
     A            RRCALC    R             65REFFLD(CALR)
     A                                      EDTWRD('  0 ')
     A  64                                81'***  DIFFERENCE  ***'
     A*
     A                                    51'*** END OF REPORT ***'
     A                                      SPACEB(3)
     A*
     A**  DATABASE ERROR MESSAGE.
     A          R FXTOTSR3                  TEXT('DATABASE ERROR MSG')
     A*
     A                                    33'DATABASE ERROR'
     A                                      SKIPB(35)
     A            RRBASE    R             48REFFLD(DBASE)
     A                                    52'IN PROGRAM'
     A            RRPGM     R             63REFFLD(DBPGM)
     A                                      SPACEA(1)
     A*
     A                                    33'FILE'
     A            RRFILE    R             38REFFLD(DBFILE)
     A                                    46', KEY'
     A            RRKEY     R             52REFFLD(DBKEY)
     A*
