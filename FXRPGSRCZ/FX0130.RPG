     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Comp Deal Amendable Fields File List')
     F****************************************************************
     F*                                                              *
     F*  Midas - FX Dealer Module
     F*                                                              *
     F*     FX0130 - COMPLETE DEAL AMENDABLE FIELDS FILE LIST        *
     F*                                                              *
      *  (c) Finastra International Limited 2001                      *
     F*                                                              *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01319             Date 25Sep91               *
     F*                 E23462             Date  1OCT90               *
     F*                                                              *
     F*                                                              *
     F****************************************************************
     F*                                                              *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*     S01319 - Remove redundant processing                      *
     F*     E23462 - REPLACE DRS WITH TRADING SUPPORT, (TS).         *
     F*                                                              *
     F****************************************************************
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*       63         Program called at end of day.
     F*       80         Database error indicator.
     F*       U7         Database error (Job Switch).
     F*       U8         Database error (Job Switch).
     F*       LR         Last Record.
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FFDICDRLLIF  E           K        DISK         KINFSR *PSSR
     F            TABTB11F                          KIGNORE
     F            TABTB20F                          KIGNORE
     F*FXTOTSRRO***E*******************PRINTER******KINFSR**PSSR**********S01319
     FFX0130AUO   E                    PRINTER      KINFSR *PSSR          S01319
     F                                              KINFDS SPOOLU         S01319
     FFXCDAFLLIF  E                    DISK         KINFSR *PSSR
     F*FX0130RRO***E*******************PRINTER******KINFSR**PSSR**********S01319
     FFX0130P1O   E                    PRINTER      KINFSR *PSSR      UC  S01319
     F                                              KINFDS SPOOL          S01319
     E                    CPY@    1   1 80
     E**  ARRAY TO HOLD COPYRIGHT STATEMENT
     E********************@T******1***2*80********************************S01319
     E********************************************************************S01319
     E****Array*to*hold*report*title.*************************************S01319
     IPSDS       SDS
     I                                      244 253 @JOB
     I                                      254 263 @USER
     I** Program status data structure
     I*
     I           UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 183 DBASE
     I** L.D.A. Data Area to hold information on Database error.
     I*
     ISPOOL       DS                                                      S01319
     I**   FILE INFORMATION DATA STRUCTURE                                S01319
     I                                       83  92 SFILE                 S01319
     I                                    B 123 1240SFNUM                 S01319
     I**                                                                  S01319
     ISPOOLU      DS                                                      S01319
     I**   FILE INFORMATION DATA STRUCTURE                                S01319
     I                                       83  92 SFILEU                S01319
     I                                    B 123 1240SFNUMU                S01319
     I*                                                                   S01319
     C/EJECT
     C*
     C**  Initial processing.
     C                     EXSR #A
     C*
     C**  Main processing
     C                     EXSR #B
     C*
     C**  Termination processing
     C                     EXSR #C
     C*
     C           ENDPGM    TAG                             **ENDPGM**
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*  Index to subroutines - Order of listing due to frequency of  *
     C*                         usage.                                *
     C*                                                               *
     C*   1. #B     - Main processing.                                *
     C*   2. #A     - Initial processing.                             *
     C*   3. #C     - Termination processing.                         *
     C*   4. ZA0150 - Accesses ICD file.                              *
     C*   5. #AA    - Accesses FXCDAFLL file.                         *
     C*   6. *PSSR  - Error handling sub- routine.                    *
     C****************************************************************
     C/EJECT
     C****************************************************************
     C*                                                              *
     C*     #B CARRIES OUT THE MAIN PROCESSING.                      *
     C*        CALLED BY  : MAIN PROGRAM.                            *
     C*        CALLS      :                                          *
     C*                                                              *
     C*        WORK FIELDS: NONE                                     *
     C*                                                              *
     C****************************************************************
     C           #B        BEGSR
     C*
     C** Execute SR only if no Database error has been detected
     C           *INLR     CABEQ'1'       #B9
     C*
     C** Test each field amendable 'Y' or 'N' indicator.
     C           FDBRCA    IFEQ 'Y'                        B1
     C                     MOVE 'YES'     RRBRCA           *1
     C                     ELSE                            X1
     C                     MOVE ' NO'     RRBRCA           *1
     C                     END                             E1
     C*
     C           FDCNMA    IFEQ 'Y'                        B1
     C                     MOVE 'YES'     RRCNMA           *1
     C                     ELSE                            X1
     C                     MOVE ' NO'     RRCNMA           *1
     C                     END                             E1
     C*
     C           FDBKRA    IFEQ 'Y'                        B1
     C                     MOVE 'YES'     RRBKRA           *1
     C                     ELSE                            X1
     C                     MOVE ' NO'     RRBKRA           *1
     C                     END                             E1
     C*
     C           FDBKGA    IFEQ 'Y'                        B1
     C                     MOVE 'YES'     RRBKGA           *1
     C                     ELSE                            X1
     C                     MOVE ' NO'     RRBKGA           *1
     C                     END                             E1
     C*
     C           FDDLSA    IFEQ 'Y'                        B1
     C                     MOVE 'YES'     RRDLSA           *1
     C                     ELSE                            X1
     C                     MOVE ' NO'     RRDLSA           *1
     C                     END                             E1
     C*
     C**  Print detail line.
     C                     WRITEFX0130R2
     C           #B9       ENDSR                           **#B9**
     C/EJECT
     C****************************************************************
     C*                                                              *
     C*    #A INITIALISES  PROGRAM.                                  *
     C*       CALLED BY  : INITIAL PROCESSING.                       *
     C*       CALLS      : ZA150 #A                                  *
     C*       WORK FIELDS: @TYPP    FIELD TO INDICATE TYPE OF PROC.  *
     C*                  : @T       ARRAY TO HOLD REPORT TITLE.      *
     C*                                                              *
     C****************************************************************
     C           #A        BEGSR
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C**  Receive parameter indicating type of processing.
     C           *ENTRY    PLIST
     C                     PARM           @TYPP   2
     C                     PARM           SEQ                             S01319
     C*
     C**  Move program name into Status Data Structure Field.
     C                     MOVEL'FX0130'  DBPGM
     C                     MOVEL'FX0130'  RRPGM
     C*
     C****Load*headers.***************************************************S01319
     C*********************MOVE*@T,1******RRPRNM**************************S01319
     C*********************MOVE*@T,2******RRPRUL**************************S01319
     C*
     C**  Call subroutine to access ICD file for MIDAS run date.
     C                     EXSR ZA0150
     C*
     C**  Check for data base error.
     C           *IN80     IFEQ '1'                        B1
     C*
     C                     MOVE '1'       *INU7            *1
     C                     MOVE '1'       *INU8            *1
     C                     MOVE '1'       *INLR            *1
     C*
     C                     ELSE                            X1
     C*
     C** No db error - access input file FXCDAFLL + check for db error.
     C                     EXSR #AA                        *1
     C*
     C                     END                             E1
     C*
     C** If db error is found end initialisation.
     C           *INLR     CABEQ'1'       #A9
     C**                                                                  S01319
     C                     OPEN FX0130P1                                  S01319
     C**                                                                  S01319
     C* REPORT SPOOL FILE GENERATION                                      S01319
     C* transfer binary data into numeric field                           S01319
     C                     Z-ADDSFNUM     ZSNUM   60                      S01319
     C                     CALL 'ZSFILE'                                  S01319
     C                     PARM           SEQ     5                       S01319
     C                     PARM *BLANKS   @PARM   3                       S01319
     C                     PARM           SFILE                           S01319
     C                     PARM           ZSNUM                           S01319
     C                     PARM *BLANK    ZSERR   1                       S01319
     C*                                                                   S01319
     C** Error during ZSFILE processing, return to calling program        S01319
     C           ZSERR     IFEQ 'Y'                                       S01319
     C                     SETON                     U7U8LR               S01319
     C                     EXSR *PSSR                                     S01319
     C                     RETRN                                          S01319
     C                     END                                            S01319
     C*
     C** Report headings.
     C                     MOVE RUNA      RRRUNA
     C                     MOVE TITL      RRTITL
     C                     WRITEFX0130R0
     C                     WRITEFX0130R1
     C*
     C           #A9       ENDSR                           **#A9**
     C/EJECT
     C****************************************************************
     C*                                                              *
     C*    #C TERMINATES PROGRAM.                                    *
     C*       CALLED BY  : MAIN PROGRAM.                             *
     C*       CALLS      :                                           *
     C*                                                              *
     C****************************************************************
     C           #C        BEGSR
     C*
     C* REPORT SPOOL FILE GENERATION                                      S01319
     C* transfer binary data into numeric field                           S01319
     C                     Z-ADDSFNUMU    ZSNUMU  60                      S01319
     C                     CALL 'ZSFILE'                                  S01319
     C                     PARM           SEQ     5                       S01319
     C                     PARM *BLANKS   @PARM   3                       S01319
     C                     PARM           SFILEU                          S01319
     C                     PARM           ZSNUMU                          S01319
     C                     PARM *BLANK    ZSERR   1                       S01319
     C*                                                                   S01319
     C** Error during ZSFILE processing, return to calling program        S01319
     C           ZSERR     IFEQ 'Y'                                       S01319
     C                     SETON                     U7U8LR               S01319
     C                     EXSR *PSSR                                     S01319
     C                     RETRN                                          S01319
     C                     END                                            S01319
     C*                                                                   S01319
     C**  Produce error report and exit if *IN80 is on -
     C**  Database error during initialistion.
     C           *IN80     IFEQ '1'                        B1
     C*
     C**  Load printer file with error data , Print error messages
     C**  & exit program.
     C                     MOVE DBASE     RRBASE           *1
     C                     MOVE DBFILE    RRFILE           *1
     C                     MOVE DBKEY     RRKEY            *1
     C                     WRITEFX0130R0
     C                     WRITEFX0130R3                   *1
     C                     WRITEFXTOTSR0                   *1
     C                     WRITEFXTOTSR3                   *1
     C                     GOTO #C9                        *1
     C                     END                             E1
     C*
     C**  Set up time field.
     C                     TIME           RRTIME
     C*
     C**  Assume number of records on file is 1.
     C                     Z-ADD1         RRNREC
     C                     Z-ADD1         RRCALC
     C*
     C**  If type of processing is 'End of Day'
     C           @TYPP     IFEQ 'ED'                       B1
     C                     MOVE '1'       *IN63            *1
     C                     END                             E1
     C*
     C**  Print file control report.
     C                     WRITEFXTOTSR0
     C                     WRITEFXTOTSR1
     C*
     C** Set on LR to terminate program.
     C                     MOVE '1'       *INLR
     C*
     C*
     C           #C9       ENDSR                           **#C9**
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE ZA0150 - THIS SUBROUTINE CHAINS TO FILE LF/       *
     C*  FDICDRLL TO GET THE INSTALLATION CONTROL DETAILS RECORD 1    *
     C*  (HELD ON PF/TABTB10)                                         *
     C*  INDICATOR 80 IS SET ON IF THE CHAIN FAILS OR THE RECORD IS   *
     C*  FLAGGED FOR DELETION.                                        *
     C*  IF INDICATOR 80 IS SET ON DETAILS OF THE ATTEMPTED ACCESS    *
     C*  ARE PLACED IN THE LDA.                                       *
     C*                                                               *
     C*  FIELDS USED : SS0150 - KEY USED TO ACCESS FDICDRLL           *
     C*                                                               *
     C*****************************************************************
     C*
     C           ZA0150    BEGSR
     C*
     C** Set up key to obtain format TABTB10F '01        10'
     C                     MOVEL'01'      SS0150 12
     C                     MOVE '10'      SS0150
     C*
     C** CHAIN TO FILE FDICDRLL
     C           SS0150    CHAINFDICDRLL             80
     C           RECI      IFNE 'D'                        B1
     C                     MOVE '1'       *IN80            *1
     C                     END                             E1
     C*
     C** DEAL WITH DATA BASE ERROR
     C           *IN80     IFEQ '1'                        ***************
     C                     MOVEL'FDICDRLL'DBFILE           *             *
     C                     MOVEL'900'     DBASE            * DBERROR 900 *
     C                     MOVELSS0150    DBKEY            *             *
     C                     END                             ***************
     C*
     C           ZA0159    ENDSR
     C/EJECT
     C/EJECT
     C****************************************************************
     C*                                                              *
     C*   #AA CHECKS INPUT FILE FXCDAFLL FOR DATABASE ERROR          *
     C*       CALLED BY  : #A                                        *
     C*       CALLS      :                                           *
     C*                                                              *
     C****************************************************************
     C           #AA       BEGSR
     C*
     C**  Chain to the input file.
     C**  - *IN80 set on if the record is not found.
     C           1         CHAINFXCDAFLL             80
     C*
     C**  If *IN80 is not on, and FDDLTF is not '*', a valid record
     C**  exists on file - otherwise process a database error.
     C           *IN80     IFEQ '1'                        B1
     C           FDDLTF    OREQ '*'                        X1
     C*
     C**  Load printer file with error data , set error indicators.
     C*
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     MOVEL'FXCDAFLL'DBFILE           * DB ERROR 001*
     C                     MOVEL'001'     DBASE            ***************
     C                     MOVE '1'       *INU7            *1
     C                     MOVE '1'       *INU8            *1
     C                     MOVE '1'       *INLR            *1
     C                     END                             E1
     C*
     C           #AA9      ENDSR                           **#AA9**
     C/EJECT
     C*****************************************************
     C*                                                   *
     C*   EXCEPTION ERROR SUBROUTINE                      *
     C*                                                   *
     C*   SUBROUTINE CALLED: EXECUTES WHENEVER EITHER     *
     C*   FILE OR PROGRAM ERROR OCCURS                    *
     C*                                                   *
     C*   SUBROUTINE CALLS : NO OTHER SUBROUTINES         *
     C*                                                   *
     C*****************************************************
     C           *PSSR     BEGSR
     C                     DUMP
     C                     ENDSR
     C*** REPORT TITLE    @T                                              E23462
     C*EALING ROOM SYSTEM - AMENDABLE FIELDS FOR COMPLETE DEALS - REPORT TE23462
   REP*RT*TITLE****@T*****************************************************S01319
MIDASC*RADER*SUPPORT******-*AMENDABLE*FIELDS*FOR*COMPLETE*DEALS*-*REPORT*TS01319
------*-------------------------------------------------------------------S01319
**
(c) Finastra International Limited 2001
