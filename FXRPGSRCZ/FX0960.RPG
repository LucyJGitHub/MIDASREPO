     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FX Netting Close of Business update')
      *****************************************************************
      *                                                               *
      *  Midas - DEALING Module                                       *
      *                                                               *
      *  FX0960 - Daily Processing of NETS                            *
      *                                                               *
      *  Function:  This program writes to the Net Actions file       *
      *  for any confirmed Net with value date before or equal to     *
      *  the control date with record id changed to 'M'.              *
      *  Nets with record id 'R' or 'X' are deleted from the Nets     *
      *  master file.                                                 *
      *  (phase(s)) CoB                                               *
      *                                                               *
      *  Called By: FXC0960 - Daily Nets Processing                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD048795           Date 15Jan18               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL025             Date 25Jan05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CDL002             Date 21May97               *
      *                                    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *           (Recompile)                                         *
      *  MD048795 - Is now called in DLC10, and, FX0960 differentiates*
      *             between actual posting and shadow posting through *
      *             *INU1 - only FXNACTPD and FX0960AU are updated in *
      *             shadow posting setup.                             *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL025 - FX Netting Payment Generation                       *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDL002 - FX Netting                                          *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS                       *
      *****************************************************************
      *       27         PAIRED CCY RECORD                            *
      *       30         LOKUP IND FOR AMOUNT FORMATTING              *
      *       31         NO MORE FILE RECORDS TO UPDATE               *
      *       39         OVERFLOW IND FOR REPORT                      *
      *       40         DETAIL RECORD REPORTED                       *
      * U7+U8 - Database Error                                        *
      *                -----------------
      *                Unused indicators
      *                -----------------
      *       01  02  03  04  05  06  07  08  09  10
      *       11  12  13  14  15  16  17  18  19  20
      *       21  22  23  24  25  26  ..  28  29  ..
      *       ..  32  33  34  35  36  37  38  ..  ..
      *       41  42  43  44  45  46  47  48  49  50
      *       51  52  53  54  55  56  57  58  59  60
      *       61  62  63  64  65  66  67  68  69  70
      *       71  72  73  74  75  76  77  78  79  80
      *       81  82  83  84  85  86  87  88  89  90
      *       91  92  93  94  95  96  97  98  ..
      *
      *
      *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   - Program Initialisation                              *
      *  REPORT - Process Report Lines                                *
      *  EXIT   - End and return to calling program                   *
      *                                                               *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *  ZFRPED - Amount format Subroutine                            *
      *                                                               *
      *****************************************************************
     FFXNETML0UF  E           K        DISK
      *  Nets by value date
     FFXNETSPDUF  E           K        DISK                                              CDL025
      *  Split Net details File                                                          CDL025
     FFXNACTPDO   E           K        DISK
      *  MIDAS DL Net Actions File
     FFX0960AUO   E                    PRINTER      KINFDS SPOOLU     UC
     F*  PRINTER FILE - AUDIT
     FFX0960P1O   E             39     PRINTER      KINFDS SPOOL1     UC
     F*  PRINTER FILE
      *
      *  Arrays and Tables
      *
     E                    CPY@    1   1 80
     E                    CY        200  3
     E                    CDP       200  1 0
      /COPY ZSRSRC,ZFRPEDZ1
      *  /Copy is for ARRAYS/TABLES for amount formatting
     IFXNETMD0
     I              AMNETR                          RANETR
     I              AMCCYD                          RAECCY
     I              AMBRCA                          RABRCA
     I              AMVALD                          RAEDAT
     I              AMBSIN                          RABSIN
     I              AMSETM                          RASETP
     I              AMOURN                          RAOSAC
     I              AMONOB                          RAOSBR
      **  Data Structure:  layout of net reference field
     INETFMT      DS
     I                                        1  19 RANETR
     I                                        1   3 WFDLCN
     I                                        4   5 WFVDMM
     I                                        6   7 WFVDDD
     I                                        8  10 WFCCY
     I                                       11  16 WFCUSN
     I                                       15  16 GRPREF
     I                                       17  19 WFSEQN
      **  Local data area for PGM-TO-PGM info
     IDBERR     E DSLDA
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                        1  10 ##PGM
     I                                     *PARMS   NOPARM
     I                                      244 253 WRKSID
     I                                      254 263 USERID
     ICYDDS     E DSSDCURRPD
      **   Currency details from SDCURRPD
      *
     ISDMMOD    E DSSDMMODPD
      ** MIDAS Modules Details Accessed Via Access Program
      *
     ISDBANK    E DSSDBANKPD
      ** MIDAS Bank Details Accessed Via Access Program
      *
     ISDGELR    E DSSDGELRPD
      ** MIDAS General Ledger Accessed Via Access Program
     I*
     ISCSARD    E DSSCSARDPD                                                             CDL025
     I* SAR File Details Accessed Via Access Program                                     CDL025
      *                                                                                  CDL025
     IDSFDY     E DSDSFDY
     I* First DS For Access Programs, SHORT DATA STRUCTURE
     I*
     IDSSDY     E DSDSSDY
     I* Second DS For Access Programs, LONG DATA STRUCTURE
     I*
      /COPY ZSRSRC,ZFRPEDZ2
      *  /Copy is for DS statements for amount formatting
     ISPOOL1      DS
      ***  File Information Data Structure for FX0960P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for FX0960AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
     I*
      /EJECT
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR REPORT
      *
      ***  End Program and Return.
      *
     C                     EXSR EXIT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      /EJECT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  RCFP1  - RCF Processing for Report                           *
      *  AOBANKR0 Access Program (Bank Details)                       *
      *  AOMMODR0 Access Program (MIDAS Modules)                      *
      *  AOGELRR0 Access Program (General Ledger)                     *
      *  *PSSR  - Error Control Program                               *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Initialise LDA field.
      *
     C                     MOVEL'FX0960'  DBPGM
      *
      ** Access Bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C           @RTCD     IFNE *BLANKS                    ***************
     C                     MOVEL'SDBANKPD'DBFILE           *             *
     C                     MOVEL'001'     DBASE            * DBERROR 001 *
     C                     MOVEL@OPTN     DBKEY            *             *
     C                     EXSR *PSSR                      ***************
     C                     END
      *
      ** Access MIDAS Modules.
      *
     C                     CALL 'AOMMODR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*FIRST ' @OPTN
     C           SDMMOD    PARM SDMMOD    DSFDY
      *
     C           @RTCD     IFNE *BLANKS                    ***************
     C                     MOVEL'SDMMODPD'DBFILE           *             *
     C                     MOVEL'002'     DBASE            * DBERROR 002 *
     C                     MOVEL@OPTN     DBKEY            *             *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF
      *
      ** Check if CDL025 is switched *ON.                                                CDL025
     C                     CALL 'AOSARDR0'                                               CDL025
     C                     PARM *BLANKS   @RTCD                                          CDL025
     C                     PARM '*VERIFY' @OPTN                                          CDL025
     C                     PARM 'CDL025'  @SARD   6                                      CDL025
     C           SCSARD    PARM SCSARD    DSFDY                                          CDL025
      *                                                                                  CDL025
     C           @RTCD     IFNE *BLANKS                                                  CDL025
     C                     MOVE 'N'       CDL025  1                                      CDL025
     C                     ELSE                                                          CDL025
     C                     MOVE 'Y'       CDL025                                         CDL025
     C                     ENDIF                                                         CDL025
      *                                                                                  CDL025
      ** Access MIDAS General Ledger
      *
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*FIRST ' @OPTN
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
     C           @RTCD     IFNE *BLANKS                    ***************
     C                     MOVEL'SDGELRPD'DBFILE           *             *
     C                     MOVEL'003'     DBASE            * DBERROR 003 *
     C                     MOVEL@OPTN     DBKEY            *             *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF
     C*
     C** LOAD CURRENCY AND DECIMAL PLACES ARRAYS
     C*
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*FIRST ' @OPTN
     C                     PARM *BLANKS   @KEYCY  3
     C           CYDDS     PARM CYDDS     DSSDY
     C*
     C           @RTCD     IFNE *BLANKS
     C                     MOVE '*FIRST ' DBKEY            ***************
     C                     MOVEL'SDCURRPD'DBFILE           * DBERROR 006 *
     C                     Z-ADD6         DBASE            ***************
     C                     EXSR *PSSR
     C                     END
     C                     Z-ADD0         X       30
     C*
     C           @RTCD     DOWEQ*BLANKS
     C           X         OREQ 200
     C*
     C                     ADD  1         X
     C                     MOVELA6CYCD    CY,X
     C                     Z-ADDA6NBDP    CDP,X
     C*
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*NEXT  ' @OPTN
     C                     PARM *BLANKS   @KEYCY
     C           CYDDS     PARM CYDDS     DSSDY
     C                     ENDDO
      *
      ** CALCULATE CONTROL DATE
      *
     C           BJDNWD    SUB  1         CTLDT   50
     C           BKAPDT    IFLT CTLDT
     C                     Z-ADDBKAPDT    CTLDT
     C                     END
      *
      **  Open and write header on printer file and ensure
      **  Spool File recorded by RCF.
      *
     C           *INU1     IFEQ *ON                                                         MD048795
     C                     OPEN FX0960P1
     C                     EXSR RCFP1
     C                     WRITEFX0960HH
     C                     ENDIF                                                            MD048795
     C                     MOVE 'D'       RARECI
     C                     MOVE '01'      RABOKC
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/REPORT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  ZFRPED - Amount Format Subroutine                            *
      *  *PSSR  - Error Control Program                               *
      *                                                               *
      *****************************************************************
      *
     C           REPORT    BEGSR
      *
     C                     READ FXNETML0                 31
      *
      ** Process NET Reference file
      *
     C           *IN31     DOWEQ'0'
      *
      ** Delete reversed and expired NETS
      *
     C           AMRECI    IFEQ 'R'
     C           AMRECI    OREQ 'X'
     C           *INU1     IFEQ *ON                                                         MD048795
     C                     DELETFXNETMD0
     C                     ENDIF                                                            MD048795
      ** Delete splits if exist                                                          CDL025
     C           CDL025    IFEQ 'Y'                                                      CDL025
     C           RANETR    SETLLFXNETSPD                                                 CDL025
     C           RANETR    READEFXNETSPD                 99                              CDL025
     C           *IN99     DOWEQ'0'                                                      CDL025
     C           *INU1     IFEQ *ON                                                         MD048795
     C                     DELETFXNETSD0                                                 CDL025
     C                     ENDIF                                                            MD048795
     C           RANETR    READEFXNETSPD                 99                              CDL025
     C                     ENDDO                                                         CDL025
     C                     ENDIF                                                         CDL025
      *                                                                                  CDL025
     C                     ELSE
     C           AMRECI    IFEQ 'C'
     C           RAEDAT    ANDGTCTLDT
     C           AMRECI    OREQ 'M'
     C           AMRECI    OREQ 'N'
      *
      ** Release future valued confirmed and matured NETS
      *
     C           *INU1     IFEQ *ON                                                         MD048795
     C                     EXCPTRELNET
     C                     ENDIF                                                            MD048795
     C                     ELSE
     C           AMRECI    IFNE 'C'
      *
      ** Invalid record found. Give DB ERROR
      *
     C*     Set up Error Condition Fields
     C                     MOVEL##PGM     DBPGM            ***************
     C                     MOVELRANETR    DBKEY            *             *
     C                     MOVE 'FXNETMD0'DBFILE           * DB ERROR 04 *
     C                     MOVE '004'     DBASE            *             *
     C                     EXSR *PSSR                      ***************
     C*
     C                     ELSE
      *
      ** NET to be processed today. Write action record and
      ** update file
      *
     C                     MOVE AMCNUM    RACNUM
     C                     Z-ADDAMDBUY    RAEAMT
     C                     Z-ADDRAEDAT    RAVDAT
     C                     Z-ADDRAEDAT    RAFFVD
     C                     MOVELAMBENN    RAFACO
      *
      ** Setup pair other currency for POSTING narrative. NOTE don't
      ** need to check for pair 'NO' because CCY will then be blank
      *
     C           AMCCY1    IFEQ RAECCY
     C                     MOVE AMCCY2    RAOTHC
     C                     ELSE
     C                     MOVE AMCCY1    RAOTHC
     C                     END
     C                     WRITEFXNACTD0
     C                     MOVE 'M'       AMRECI
     C           *INU1     IFEQ *ON                                                         MD048795
     C                     UPDATFXNETMD0
     C                     ENDIF                                                            MD048795
      ** Update splits if exist                                                          CDL025
     C           CDL025    IFEQ 'Y'                                                      CDL025
     C           RANETR    SETLLFXNETSPD                                                 CDL025
     C           RANETR    READEFXNETSPD                 99                              CDL025
     C           *IN99     DOWEQ'0'                                                      CDL025
     C                     MOVE 'M'       ASRECI                                         CDL025
     C           *INU1     IFEQ *ON                                                         MD048795
     C                     UPDATFXNETSD0                                                 CDL025
     C                     ENDIF                                                            MD048795
     C           RANETR    READEFXNETSPD                 99                              CDL025
     C                     ENDDO                                                         CDL025
     C                     ENDIF                                                         CDL025
      *                                                                                  CDL025
     C                     MOVE RANETR    DDNETR
     C                     MOVELAMDCSN    DDCUST    P
     C           GRPREF    IFEQ '##'
     C           4         SUBSTAMDCSN:1  DDCUST    P
     C                     MOVE '*GRP*'   DDCUST
     C                     END
     C                     MOVE RAECCY    DDCCY
     C                     MOVE AMCCY1    DCCYS1
     C                     MOVE AMCCY2    DCCYS2
     C                     MOVE AMVDTY    DDVALD
     C           DCCYS1    IFNE *BLANKS
     C                     MOVE '1'       *IN,27
     C                     ELSE
     C                     MOVE '0'       *IN,27
     C                     END
     C           RABSIN    IFEQ 'B'
     C                     MOVE 'RECEIVE' DDPYRC
     C                     ELSE
     C                     MOVE '    PAY' DDPYRC
     C                     END
      *
      ** Format amount as required
      *
     C                     Z-ADD1         K       30
     C           RAECCY    LOKUPCY,K                     30
      *
      ** Get currency details
      *
     C           *IN30     IFEQ '1'
     C                     MOVE CDP,K     A6NBDP
     C                     ELSE
     C                     Z-ADD0         A6NBDP
     C                     END
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
      *
      ** Because amounts held as POSITIVE, make sale NEGATIVE
      *
     C           RABSIN    IFEQ 'S'
     C                     Z-SUBAMDBUY    ZFLD15
     C                     ELSE
     C                     Z-ADDAMDBUY    ZFLD15
     C                     END
     C                     EXSR ZFRPED
     C                     MOVELZOUT22    DDNETT
     C                     MOVE RASETP    DDSETP
     C                     MOVE *BLANKS   DDSEAC
      *
      ** Show branch code for multi branch SETTL TYPE 05
      *
     C           DDSETP    IFEQ '05'
     C           BGMBIN    ANDEQ'Y'
     C                     MOVE RAOSAC    DDSEAC
     C                     MOVELRAOSBR    DDSEAC
     C                     ELSE
     C                     MOVELRAOSAC    DDSEAC
     C                     END
     C           *IN39     IFEQ '1'
      *
      ** Print Header if overflow
      *
     C           *INU1     IFEQ *ON                                                         MD048795
     C                     WRITEFX0960HH
     C                     ENDIF                                                            MD048795
     C                     MOVE '0'       *IN39
     C                     END
      *
      ** Print Detail
      *
     C           *INU1     IFEQ *ON                                                         MD048795
     C                     WRITEFX0960DD
     C                     ENDIF                                                            MD048795
      *
      ** Set indicator to show record reported
      *
     C                     MOVE '1'       *IN40
     C                     END
     C                     END
     C                     END
     C                     READ FXNETML0                 31
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/EXIT
      *****************************************************************
      *                                                               *
      *  EXIT   - Write End of Report and End Program.                *
      *                                                               *
      *  Called By: Main Processing, *PSSR                            *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           EXIT      BEGSR                           *** EXIT   ***
      *
      **  Print End of Report, So long as an Error has
      **  not occured.(*PSSR).
      *
     C           WRUN      IFEQ *BLANK
     C           *INU1     IFEQ *ON                                                         MD048795
     C           *IN40     IFEQ *ON
     C                     WRITEFX0960EN
     C                     ELSE
     C                     WRITEFX0960ND
     C                     ENDIF
     C                     ENDIF                                                            MD048795
     C                     ENDIF
      *
      **  End Program and Return.
      *
     C                     SETON                         LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           *NAMVAR   DEFN           LDA   256
     C           *LOCK     IN   LDA
     C                     MOVELDBERR     LDA
     C                     OUT  LDA
      *
     C                     SETON                     U7U8LR
     C                     OPEN FX0960AU
     C                     EXSR RCFAU
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8
     C                     EXSR EXIT
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: *PSSR                                             *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  Output Report Header
      *
     C                     WRITEHEADAU
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C                     WRITEDBERROR
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *  Amount Format Subroutine
      /COPY ZSRSRC,ZFRPEDZ3
      *
      *****************************************************************
     OFXNETMD0E                RELNET
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2001
