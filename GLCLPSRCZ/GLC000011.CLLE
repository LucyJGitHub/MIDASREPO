/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas GL Revalue All 'Pledge' Collateral Items')      */
/*********************************************************************/
/*                                                                   */
/*       Midas - General Ledger Module                               */
/*                                                                   */
/*       GLC000011 - Revalue All 'Pledge' Collateral Items           */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CGL014  *CREATE    Date 20Oct03              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CGL014 - Collaterals Processing                             */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2003')
 
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&GROUP)   TYPE(*CHAR) LEN(50)
             DCL        VAR(&USER)    TYPE(*CHAR) LEN(25)
             DCL        VAR(&SLEVEL)  TYPE(*DEC)  LEN(4)
             DCL        VAR(&TIMEO)   TYPE(*DEC)  LEN(5)
             DCL        VAR(&ERRORC)  TYPE(*DEC)  LEN(1)   VALUE(0)
             DCL        VAR(&MIDASLG) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MULT)    TYPE(*CHAR) LEN(2)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2003')
 
/** Monitor for any errors and send appropriate message to MOPERQ */
 
             MONMSG     MSGID(RPG0000 CPF0000 MCH0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
 
/* Multiple language support                                         */
 
             CALL       PGM(SF0410) PARM(&GROUP &USER &SLEVEL &TIMEO  +
                          &ERRORC &MULT)
 
             IF         COND(&MULT = '  ') THEN(DO)
               CHGVAR     VAR(&MULT) VALUE('GB')
             ENDDO
 
/* Build multiple language support object name                       */
 
             CHGVAR     VAR(&MIDASLG) VALUE(&MULT *CAT 'MIDAS     ')
 
/* Apply multiple language support overrides                         */
 
             OVRMSGF    MSGF(MIDAS) TOMSGF(&MIDASLG)
 
             SNDMSG     MSG('GLC000011 - Revaluation of All Pledge +
                          Collateral Items being initiated') +
                        TOMSGQ(MRUNQ)
 
/** Call GL000011 */
 
             CHGJOB     SWS(XXXXXX00)
             CALL       PGM(GL000011)
 
/** Monitor for database error and send appropriate message to MOPERQ */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             SNDMSG     MSG('GLC000011 - Revaluation of All Pledge +
                          Collateral Items ended normally') +
                        TOMSGQ(MRUNQ)
             GOTO       CMDLBL(END)
 
ABNOR:
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          GLC000011 ended abnormally - see job log') +
                          TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
             DMPCLPGM
 
END:
             ENDPGM
