/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas GL Build XML Data')                             */
/*********************************************************************/
/*                                                                   */
/*       Midas - General Ledger Module                               */
/*                                                                   */
/*       GLC000771 - Midas GL Build XML Data (CRS)                   */
/*                                                                   */
/*       (c) Finastra International Limited 2016                     */
/*                                                                   */
/*       Last Amend No. MD054986           Date 27Jan20              */
/*       Prev Amend No. CGL177  *CREATE    Date 11Jan16              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD054986 - CRS Issue: Account Holder/Organisation           */
/*       CGL177 - CRS Reporting Phase 2                              */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&MSID)

/**********  DCL        VAR(&MSID)   TYPE(*CHAR) LEN(21)             */                 /*MD054986*/
             DCL        VAR(&MSID)   TYPE(*CHAR) LEN(29)                                /*MD054986*/
             DCL        VAR(&NAME)   TYPE(*CHAR) LEN(9)
             DCL        VAR(&NAME2)   TYPE(*CHAR) LEN(10)

             DCL        VAR(&RTCDE) TYPE(*CHAR) LEN(7) VALUE(' ')
             DCL        VAR(&SQLSTRING) TYPE(*CHAR) LEN(500)
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(7) VALUE('  DPLIB')


             COPYRIGHT  TEXT('(c) Finastra International Limited 2016')

/** Global monitor message */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

             CRTDUPOBJ  OBJ(GLNXMLPD) FROMLIB(*LIBL) OBJTYPE(*FILE) TOLIB(QTEMP)

             CRTDUPOBJ  OBJ(GLVXMLPD) FROMLIB(*LIBL) OBJTYPE(*FILE) TOLIB(QTEMP)

             OVRDBF     FILE(GLNXMLPD) TOFILE(QTEMP/GLNXMLPD) OVRSCOPE(*JOB)

             OVRDBF     FILE(GLVXMLPD) TOFILE(QTEMP/GLVXMLPD) OVRSCOPE(*JOB)
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(%SUBSTRING(&DPLIB 1 2)) VALUE(&SYSID)
/**********  CHGVAR     VAR(&NAME) VALUE(%SUBSTRING(&MSID 12 9))     */                 /*MD054986*/
             CHGVAR     VAR(&NAME) VALUE(%SUBSTRING(&MSID 20 9))                        /*MD054986*/
             CHGVAR     VAR(%SUBSTRING(&NAME 4 1)) VALUE('0')
             CHGVAR     VAR(&NAME2) VALUE('F' *CAT &NAME)

             CHGVAR     VAR(&SQLSTRING) VALUE('INSERT INTO GLNXMLPD SELECT XMORDR, +
                          XMELMT, XMTPMT, XMLEVL, XMTLIN, XMREPR, XMNMSP, XMFLDA, +
                          MGDATA, MGDAT2, MGSEQU, MGSEQ2, MGACNO, MGACHO FROM ' +
                          *CAT &NAME2 *CAT ' JOIN GLGXMLPD on xmlevl=mglevl and +
                          mgelmt = xmelmt WHERE mgmsid =')
             CHGVAR     VAR(&SQLSTRING) VALUE(&SQLSTRING *TCAT '''' *CAT &MSID +
                          *CAT '''' *CAT ' ORDER BY mgacno, mgacho, mgsequ, +
                          xmordr')
             RUNSQL     SQL(&SQLSTRING) COMMIT(*NONE)

             CALL       PGM(GL000771) PARM(&RTCDE)

             IF         COND(&RTCDE *NE ' ') THEN(GOTO CMDLBL(ABNOR))

             CALL       PGM(GL000769) PARM(&RTCDE)

             IF         COND(&RTCDE *NE ' ') THEN(GOTO +
                          CMDLBL(ABNOR))

             DLTOVR     FILE(GLVXMLPD GLNXMLPD) LVL(*JOB)
             DLTF       FILE(&DPLIB/&NAME2)
             MONMSG     MSGID(CPF2105)



/* Normal ending: */
/*================*/

NORMAL:
             GOTO       CMDLBL(ENDPGM)

/* Abnormal ending: */
/*==================*/

ABNOR:
             SNDMSG     MSG('Midas GL Build XML Data has terminated +
                          abnormally.') TOMSGQ(MOPERQ MRUNQ)
             DMPCLPGM

ENDPGM:
             ENDPGM
