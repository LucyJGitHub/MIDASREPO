   /******************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas GL RWA Netting Program')                        */
   /******************************************************************/
   /*                                                                */
   /*     Midas - Dealing Module                                     */
   /*                                                                */
   /*     GLC001244 - GL RWA Netting Program                         */
   /*                                                                */
   /*     (c) Finastra International Limited 2002                    */
   /*                                                                */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
   /*       Prev Amend No. CGL018 *Create       Date 10Sep02         */
   /*       Prev Amend No. xxxxxx               Date xxxxxxx         */
   /*                                                                */
   /******************************************************************/
   /*                                                                */
   /*    MD046248 - Finastra Rebranding                              */
   /*    CGL018 - Risk Weighted Assets                               */
   /*                                                                */
   /******************************************************************/
             PGM        PARM(&CNAM &CSEQ)
 
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RSEQ) TYPE(*CHAR) LEN(5)
             DCL        VAR(&RLEV) TYPE(*CHAR) LEN(1)
             DCL        VAR(&RENT) TYPE(*CHAR) LEN(3)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&OPTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SARD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DSFDY) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SYST) TYPE(*CHAR) LEN(2)
             DCL        VAR(&BUSY) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited +
                          2001')
 
/*  Check if feature on                                              */
 
             CHGVAR     VAR(&RTCD) VALUE('       ')
             CHGVAR     VAR(&OPTN) VALUE('*VERIFY')
             CHGVAR     VAR(&SARD) VALUE('CGL018')
             CALL       PGM(AOSARDR0) PARM(&RTCD &OPTN &SARD &DSFDY)
 
             IF         COND(&RTCD *NE '       ') THEN(DO)
             GOTO       CMDLBL(ENDPGM)
             ENDDO
 
/* Check if LDA Dataarea exists and create it if not                 */
 
             CHKOBJ     OBJ(LDA) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801) EXEC(CRTDTAARA +
                        DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                        VALUE(' ') TEXT('Midas SD LOCAL +
                          DATA AREA EQUIVALENT') AUT(*USE))
 
/* Global monitor message                                            */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Set up susbsytem data */
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYST)
             CHGVAR     VAR(&DPLIB) VALUE(&SYST *CAT 'DPLIB')
             CHGVAR     VAR(&DMLIB) VALUE(&SYST *CAT 'DMLIB')
 
             CHGJOB     SWS(XXXXXX00)
 
             SNDPGMMSG  MSG('GLC001244 - GL Risk Weighted Assets +
                          Netting Program') TOMSGQ(MRUNQ)
 
/* Check for component restart                */
 
             CHGVAR     VAR(&BUSY) VALUE(' ')
             CALL       CB0160 PARM(&CNAM &CSEQ &BUSY)
 
/* If Restart copy file to back up copy    */
 
             IF         COND(&BUSY = 'Y') THEN(DO)
             CPYF       FROMFILE(&DPLIB/XGLNETGPD) +
                          TOFILE(&DMLIB/GLNETGPD) MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(GLNETGPD))
             ENDDO
 
             ELSE       CMD(DO)
             CPYF       FROMFILE(&DMLIB/GLNETGPD) +
                          TOFILE(&DPLIB/XGLNETGPD) MBROPT(*REPLACE) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(XGLNETGPD))
             CHGVAR     VAR(&BUSY) VALUE('Y')
             CALL       CB0150 PARM(&CNAM &CSEQ &BUSY)
             ENDDO
 
/* Call the netting program                       */
 
             CHGJOB     SWS(XXXXXX00)
             CALL       PGM(GL001244)
 
/* Database Error Handling                                           */
 
             IF   COND(%SWITCH(XXXXXX11))   THEN(GOTO +
                          CMDLBL(ABNOR))
             ELSE       DLTF FILE(XGLNETGPD)
             SNDPGMMSG  MSG('GLC001244 GL Risk Weighted Assets +
                          Netting Program - Completed Normally')        +
                          TOMSGQ(MRUNQ)
             CHGVAR     VAR(&BUSY) VALUE('N')
             CALL       CB0150 PARM(&CNAM &CSEQ &BUSY)
             GOTO       CMDLBL(ENDPGM)
 
 ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                     GLC001244 ended abnormally - see job log') +
                     TOMSGQ(MOPERQ MRUNQ)
             RTVDTAARA   DTAARA(LDA (134 50))   RTNVAR(&MEM)
                     SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                     TOMSGQ(MRUNQ MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
 ENDPGM:     CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Finastra International Limited')
 
             ENDPGM
