/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas GL Risk weighted assets reports')               */
/*********************************************************************/
/*                                                                   */
/*        Midas - Dealing Module                                     */
/*                                                                   */
/*        GLC001249 - RWA Reports                                    */
/*                                                                   */
/*        (c) Finastra International Limited 2002                    */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CGL018  *CREATE    Date 10Sep02              */
/*                      XXXXXX             Date XXXxxXX              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CGL018 - Risk Weighted Assets                               */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ &RSEQ)
 
             DCL        VAR(&RSEQ) TYPE(*CHAR) LEN(5)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&OPTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SARD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DSFDY) TYPE(*CHAR) LEN(200)
             DCL        VAR(&BUSY) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5)
             DCL        VAR(&SYST) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited +
                          2002')
 
/*  Check if feature on                                              */
 
             CHGVAR     VAR(&RTCD) VALUE('       ')
             CHGVAR     VAR(&OPTN) VALUE('*VERIFY')
             CHGVAR     VAR(&SARD) VALUE('CGL018')
 
             CALL       PGM(AOSARDR0) PARM(&RTCD &OPTN &SARD &DSFDY)
 
             IF         COND(&RTCD *NE '       ') THEN(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
/* Check if LDA Dataarea exists and create it if not                 */
 
             CHKOBJ     OBJ(LDA) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801) EXEC(CRTDTAARA +
                          DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                          VALUE(' ') TEXT('Midas SD Local data area +
                          equivalent') AUT(*USE))
 
/* Global monitor message                                            */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Set up susbsytem data */
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYST)
             CHGVAR     VAR(&DPLIB) VALUE(&SYST *CAT 'DPLIB')
 
             CHGJOB     SWS(XXXXXX00)
 
             SNDPGMMSG  MSG('GLC001249 - GL RWA Reports +
                          ') TOMSGQ(MRUNQ)
 
/* Check for component restart                */
 
             CHGVAR     VAR(&BUSY) VALUE(' ')
             CALL       PGM(CB0160) PARM(&CNAM &CSEQ &BUSY)
 
/* If Restart copy file to back up copy    */
 
             IF         COND(&BUSY = 'Y') THEN(DO)
                CPYF       FROMFILE(&DPLIB/XGLRWARPD) +
                             TOFILE(&DPLIB/GLRWARPD) MBROPT(*REPLACE)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                             FILE(GLRWARPD))
             ENDDO
 
             ELSE       CMD(DO)
                CPYF       FROMFILE(&DPLIB/GLRWARPD) +
                             TOFILE(&DPLIB/XGLRWARPD) MBROPT(*REPLACE) +
                             CRTFILE(*YES)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                             FILE(XGLRWARPD))
                CHGVAR     VAR(&BUSY) VALUE('Y')
                CALL       PGM(CB0150) PARM(&CNAM &CSEQ &BUSY)
             ENDDO
 
/* Copy the deals, loans & securities RWA balances to GLRWARPD */
 
             CPYF       FROMFILE(DLRWARPD) TOFILE(GLRWARPD) +
                          MBROPT(*ADD)
             CPYF       FROMFILE(LERWARPD) TOFILE(GLRWARPD) +
                          MBROPT(*ADD)
             CPYF       FROMFILE(SERWARPD) TOFILE(GLRWARPD) +
                          MBROPT(*ADD)
 
/* Call the RWA Balances Report program */
 
             CALLPRC    PRC(GL001249) PARM(&RSEQ)
 
/* Database Error Handling                                           */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('GL001249 RWA Balances Report - +
                          Completed Normally') TOMSGQ(MRUNQ)
 
/* Call the RWA Netted Balances Report program */
 
             CALLPRC    PRC(GL001248) PARM(&RSEQ)
 
/* Database Error Handling                                           */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('GL001248 RWA Netted Balances Report - +
                          Completed Normally') TOMSGQ(MRUNQ)
 
             CHGVAR     VAR(&BUSY) VALUE('N')
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &BUSY)
             DLTF       FILE(XGLRWARPD)
             GOTO       CMDLBL(ENDPGM)
 
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
 
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          GLC001249 ended abnormally - see job +
                          log') TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
             RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
             MONMSG     MSGID(CPF0000 MCH0000)
 
             SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                          TOMSGQ(MRUNQ MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
 ENDPGM:
             CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Finastra International Limited')
 
             ENDPGM
