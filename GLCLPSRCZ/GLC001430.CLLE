/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI    TEXT('Midas GL MT940 File Extract')                         */
/*********************************************************************/
/*                                                                   */
/*       Midas     - General Ledger Module                           */
/*                                                                   */
/*       GLC001430 - Midas GL MT940 File Extract                     */
/*                                                                   */
/*       (c) Finastra International Limited 2002                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. MD023328           Date 11Nov13              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/*                      239446             Date 08Nov06              */
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CGL013  *CREATE    Date 31Apr02              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       MD023328 - Parameters not passed to GL001430 & GL001431     */
/*                  Added hooks: CGL146_131, CGL146_132, CGL146_133  */
/*       239446 - Change commitment definition from CMTSCOPE(*ACTGRP)*/
/*                to CMTSCOPE(*JOB).                                 */
/*       CGL013 - MT94x Message Generation                           */
/*                                                                   */
/*********************************************************************/
/*/COPY WNCPYSRC,GLC001430A                                          */
             PGM
 
/*/COPY WNCPYSRC,GLC001430A                                          */
 
             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&CCB009) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&RTNCDE) TYPE(*CHAR) LEN(7) VALUE('       ')
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&JLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOURNAL) TYPE(*CHAR) LEN(10) +
                          VALUE('JRNGL1430')
             DCL        VAR(&RECEIVER) TYPE(*CHAR) LEN(10) +
                          VALUE('RCVGL1430')
             DCL        VAR(&SDATE) TYPE(*CHAR) LEN(6)
             DCL        VAR(&RDATE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&RPARM) TYPE(*CHAR) LEN(100)
/*/COPY OFCPYSRCZ,CGL146_133                                         */                 /*MD023328*/
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2002')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Check feature CGL013 MT94x Message Generation */
 
             CHGVAR     VAR(&RTNCDE) VALUE('       ')
             CALL       PGM(AOSARDR0) PARM(&RTNCDE '*VERIFY' 'CGL013')
             IF         COND(&RTNCDE *NE '       ') THEN(GOTO +
                          CMDLBL(END))
 
             CHKOBJ     OBJ(LDA) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801) EXEC(CRTDTAARA +
                          DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256))
 
             RTVJOBA    TYPE(&JOBTYPE)
             CHGDTAARA  DTAARA(LDA) VALUE(' ')
             CHGJOB     SWS(XXXXXX00)
 
/*/COPY WNCPYSRC,GLC001430B                                          */
 
/* Prepare the Jounaling environment if the feature CCB009 is off */
 
             CHGVAR     VAR(&RTNCDE) VALUE('       ')
             CALL       PGM(AOSARDR0) PARM(&RTNCDE '*VERIFY' 'CCB009')
             IF         COND(&RTNCDE = '       ') THEN(CHGVAR +
                          VAR(&CCB009) VALUE('Y'))
 
             IF         COND(&CCB009 = 'N') THEN(DO)
 
               RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
               CHGVAR     VAR(&JLIB) VALUE(&SYSID *CAT 'JLIB')
 
               ENDCMTCTL
               MONMSG     MSGID(CPF0000)
 
               ENDJRNPF   FILE(*ALL) JRN(&JLIB/&JOURNAL)
               MONMSG     MSGID(CPF7032 CPF9801)
 
               DLTJRN     JRN(&JLIB/&JOURNAL)
               MONMSG     MSGID(CPF2105 CPF9801)
 
               CHGJOB     INQMSGRPY(*DFT)
 
               DLTJRNRCV  JRNRCV(&JLIB/&RECEIVER)
               MONMSG     MSGID(CPF2105 CPF9801)
 
               CHGJOB     INQMSGRPY(*RQD)
 
               CRTJRNRCV  JRNRCV(&JLIB/&RECEIVER) AUT(*LIBCRTAUT)
               CRTJRN     JRN(&JLIB/&JOURNAL) JRNRCV(&JLIB/&RECEIVER) +
                          AUT(*LIBCRTAUT)
 
               STRJRNPF   FILE(GLPOSTPD GLNW94PD MGF940PD MGX940PD +
                          MGY940PD MGOMSGPD MGOREFPD) +
                          JRN(&JLIB/&JOURNAL) IMAGES(*BOTH) +
                          OMTJRNE(*OPNCLO)
 
/*/COPY WNCPYSRC,GLC001430C                                          */
 
             ENDDO
 
/*********   STRCMTCTL  LCKLVL(*CHG) NFYOBJ(MNTYF (*FILE))   */                           /*239446*/
             STRCMTCTL  LCKLVL(*CHG) NFYOBJ(MNTYF (*FILE)) +
                          CMTSCOPE(*JOB)                                                  /*239446*/
 
/*/COPY WNCPYSRC,GLC001430D                                          */
 
/* MT940 File Extracts */
 
/*/COPY OFCPYSRCZ,CGL146_131                                         */                 /*MD023328*/
             CALLPRC    PRC(GL001430)
/*/COPY OFCPYSRCZ,CGL146_132                                         */                 /*MD023328*/
 
 
/*/COPY WNCPYSRC,GLC001430E                                          */
 
/* Database error processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                ROLLBACK
                SNDPGMMSG  MSGID(MEM0001) MSGF(GBMIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                GOTO       CMDLBL(END)
             ENDDO
 
/* Generate MT940 */
 
             CALLPRC    PRC(MG000940)
 
/* Database error processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                ROLLBACK
                SNDPGMMSG  MSGID(MEM0001) MSGF(GBMIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                GOTO       CMDLBL(END)
             ENDDO
 
/*/COPY WNCPYSRC,GLC001430F                                          */
 
/* Remove the Jounaling environment if the feature CCB009 is off */
 
             IF         COND(&CCB009 = 'N') THEN(DO)
 
               ENDCMTCTL
               MONMSG     MSGID(CPF0000)
 
               ENDJRNPF   FILE(*ALL) JRN(&JLIB/&JOURNAL)
               MONMSG     MSGID(CPF7032 CPF9801)
 
/*/COPY WNCPYSRC,GLC001430G                                          */
 
               DLTJRN     JRN(&JLIB/&JOURNAL)
               MONMSG     MSGID(CPF2105 CPF9801)
 
               CHGJOB     INQMSGRPY(*DFT)
 
               DLTJRNRCV  JRNRCV(&JLIB/&RECEIVER)
               MONMSG     MSGID(CPF2105 CPF9801)
 
               CHGJOB     INQMSGRPY(*RQD)
 
             ENDDO
 
/* Call Message Generation Audit Report                            */
             CHGVAR     VAR(&RPARM) VALUE('GL            9401234')
             RTVDTAARA  DTAARA(RUNDAT (1 7)) RTNVAR(&RDATE)
             IF         COND(%SST(&RDATE 1 1) *EQ ' ') THEN(CHGVAR +
                          VAR(%SST(&RDATE 1 1)) VALUE('0'))
             CALL       PGM(ZM0085) PARM(&RDATE &SDATE)
             CHGVAR     VAR(%SST(&RPARM 23 6)) VALUE(&SDATE)
             CHGVAR     VAR(%SST(&RPARM 29 6)) VALUE(&SDATE)
             CALL       PGM(MEC0810) PARM('     ' 'B' 'ALL ' &RPARM)
 
/*/COPY WNCPYSRC,GLC001430H                                          */
 
             GOTO       CMDLBL(END)
 
ABNOR:
 
/* Abnormal termination  */
 
/*/COPY WNCPYSRC,GLC001430I                                          */
 
             CHGJOB     SWS(XXXXXX11)
 
             ROLLBACK
             MONMSG     MSGID(CPF0000 MCH0000)
 
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          GLC001430 ended abnormally - see job +
                          log') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
/*/COPY WNCPYSRC,GLC001430J                                          */
 
END:
 
/*/COPY WNCPYSRC,GLC001430K                                          */
 
             ENDPGM
