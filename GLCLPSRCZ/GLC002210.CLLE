/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas GL End Shadow Post Interface')                  */
/*********************************************************************/
/*                                                                   */
/*       Midas - GL Shadow Post Interface                            */
/*                                                                   */
/*       GLC002210 - End Shadow Post Interface                       */
/*                                                                   */
/*       (c) Finastra International Limited 2017                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CGL027  *CREATE    Date 04Apr17              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CGL027 - Shadow Postings and Sufficient Funds Check         */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&IN_DTAQ)
/* */
             COPYRIGHT  TEXT('(c) Finastra International Limited +
                          2017')
/* */
/* Declare variables */
/* */
             DCL        VAR(&IN_DTAQ) TYPE(*CHAR) LEN(20)
             DCL        VAR(&NBR) TYPE(*DEC) LEN(1 0) VALUE(9)
             DCL        VAR(&COUNT) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&IN_NAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN_LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DATA) TYPE(*CHAR) LEN(50)
             DCL        VAR(&FLDLEN) TYPE(*DEC) LEN(5 0) VALUE(256)
             DCL        VAR(&IN_DATA) TYPE(*CHAR) LEN(256) VALUE('End')
/* */
/* Global Monitor Message */
/* */
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) +
                           EXEC(GOTO ABNORMAL)
/* */
/* Send Message to MRUNQ */
/* */
             SNDPGMMSG  MSG('ENDSHDINT - End Shadow Post Interface') +
                          TOMSGQ(MRUNQ)
             CHGJOB     SWS(XXXXX000)
/* */
/* Check In data queue exists */
/* */
             CHGVAR     VAR(&IN_NAME) VALUE(%SST(&IN_DTAQ 1 10))
             CHGVAR     VAR(&IN_LIB) VALUE(%SST(&IN_DTAQ 11 10))
             CHKOBJ     OBJ(&IN_LIB/&IN_NAME) OBJTYPE(*DTAQ)
/* */
/* Allocate In data queue - if not successful then process started */
/* */
 ALCOBJ:     ALCOBJ     OBJ((&IN_LIB/&IN_NAME *DTAQ *EXCL)) WAIT(30)
             MONMSG     MSGID(CPF1002) EXEC(DO)
             CHGVAR     VAR(&COUNT) VALUE(0)
/* */
/* send end instruction */
/* */
 LOOP:       IF         COND(&COUNT *NE &NBR) THEN(DO)
             CALL       PGM(QSNDDTAQ) PARM(&IN_NAME &IN_LIB &FLDLEN +
                          &IN_DATA)
             CHGVAR     VAR(&COUNT) VALUE(&COUNT + 1)
             GOTO       CMDLBL(LOOP)
             ENDDO
             GOTO       CMDLBL(ALCOBJ)
             ENDDO
/* */
/* Clear data queue */
/* */
             CALL       PGM(QCLRDTAQ) PARM(&IN_NAME &IN_LIB)
/* */
/* Check for Database errors trapped by program */
/* */
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
             RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&DATA)
             SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&DATA) +
                          TOPGMQ(*EXT) TOMSGQ(MOPERQ)
             GOTO       CMDLBL(ABNORMAL)
             ENDDO
/**/
             GOTO       CMDLBL(ENDCLPGM)
/**/
/* Abnormal termination processing                       */
/**/
ABNORMAL:
             ROLLBACK
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             CHGJOB     SWS(XXXXXX11)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             DMPCLPGM
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
/**/
             SNDPGMMSG  MSG('Program GLC002210 ended abnormally') +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          GLC002210 ended abnormally') +
                          MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
/**/
 ENDCLPGM:
/* */
             RCLRSC     LVL(*CALLER)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             CHGVAR     VAR(&IN_NAME) VALUE(%SST(&IN_DTAQ 1 10))
             CHGVAR     VAR(&IN_LIB) VALUE(%SST(&IN_DTAQ 11 10))
             DLCOBJ     OBJ((&IN_LIB/&IN_NAME *DTAQ *SHRUPD))
             CHGVAR     VAR(&IN_NAME) VALUE(%SST(&IN_DTAQ 1 10))
             CHGVAR     VAR(&IN_LIB) VALUE(%SST(&IN_DTAQ 11 10))
             DLCOBJ     OBJ((&IN_LIB/&IN_NAME *DTAQ *EXCL))
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
/* */
             ENDPGM
