/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas GL Housekeeping of MEMOS Shadow files')         */
/*********************************************************************/
/*                                                                   */
/*       Midas - General Ledger Module                               */
/*                                                                   */
/*       GLC004609 - Midas GL Housekeeping of MEMOS Shadow files     */
/*                                                                   */
/*       (c) Finastra International Limited 2023                     */
/*                                                                   */
/*       Last Amend No. MD060909 *CREATE   Date 14Feb23              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD060909 - After COB job FTC0630 go in MSGW.                */
/*                                                                   */
/*********************************************************************/
             PGM
/**/
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) Finastra +
                          International Limited 2023')

             DCL        VAR(&PREFIX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RUNP) TYPE(*CHAR) LEN(3) VALUE('NO')

/*/COPY GPCPYSRCG,GPSVALDCL */

/**/
/* Global monitor message */
/**/
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
/**/
/* Start of program */


             CHGJOB     SWS(XXXXXX00)

             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)

/* Create backup */

             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&PREFIX)


             CHGVAR     VAR(&DMLIB) VALUE(&PREFIX *TCAT 'DMLIB')
             CHGVAR     VAR(&DPLIB) VALUE(&PREFIX *TCAT 'DPLIB')

             CHKOBJ     OBJ(&DPLIB/YYGLMEMOTD) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(BACKUP))
             DLTF       FILE(YYGLMEMOTD)

BACKUP:
             CPYF       FROMFILE(&DMLIB/GLMEMOTD) TOFILE(&DPLIB/YYGLMEMOTD) +
                        MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(DO)
             CLRPFM     FILE(YYGLMEMOTD)
             ENDDO
             MONMSG     MSGID(CPF2817 CPF3130 CPF2909) EXEC(DO)
             SNDUSRMSG  MSG('Error occurred in copying PF/GLMEMOTD to +
                PF/YYGLMEMOTD. Process terminated.') MSGTYPE(*INFO)
             GOTO       CMDLBL(END)
             ENDDO

             CHKOBJ     OBJ(&DPLIB/YYGLMEMOT2) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(BACKUP2))
             DLTF       FILE(YYGLMEMOT2)

BACKUP2:
             CPYF       FROMFILE(&DMLIB/GLMEMOT2) TOFILE(&DPLIB/YYGLMEMOT2) +
                        MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(DO)
             CLRPFM     FILE(YYGLMEMOT2)
             ENDDO
             MONMSG     MSGID(CPF2817 CPF3130 CPF2909) EXEC(DO)
             SNDUSRMSG  MSG('Error occurred in copying PF/GLMEMOT2 to +
                PF/YYGLMEMOT2. Process terminated.') MSGTYPE(*INFO)
             GOTO       CMDLBL(END)
             ENDDO

             CHGVAR     VAR(&RUNP) VALUE('YES')
             CALL       PGM(GL004609)

/* If errror occurred*/
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))

             CALL       PGM(GL004611)

/* If errror occurred*/
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))

             SNDPGMMSG  MSG('GLC004609 completed normally.')

             GOTO       CMDLBL(END)

/**/
/* Abnormal Termination of the program or */
/* Restore backup if an error occurred in update mode */
/**/
ABNOR:
             IF         COND(&RUNP *EQ 'YES') THEN(DO)

                CPYF FROMFILE(&DPLIB/YYGLMEMOTD) TOFILE(&DMLIB/GLMEMOTD) +
                     MBROPT(*REPLACE) FMTOPT(*NONE)
                MONMSG     MSGID(CPF2817 CPF3130 CPF2909) EXEC(DO)
                SNDUSRMSG  MSG('Error occurred in restoring PF/YYGLMEMOTD +
                        from  PF/GLMEMOTD. Process terminated.  Please restore +
                        PF/GLMEMOTD manually from PF/YYGLMEMOTD.') +
                        MSGTYPE(*INFO)
                ENDDO

                CPYF FROMFILE(&DPLIB/YYGLMEMOT2) TOFILE(&DMLIB/GLMEMOT2) +
                     MBROPT(*REPLACE) FMTOPT(*NONE)
                MONMSG     MSGID(CPF2817 CPF3130 CPF2909) EXEC(DO)
                SNDUSRMSG  MSG('Error occurred in restoring PF/YYGLMEMOT2 +
                        from  PF/GLMEMOT2. Process terminated.  Please restore +
                        PF/GLMEMOT2 manually from PF/YYGLMEMOT2.') +
                        MSGTYPE(*INFO)
                ENDDO

             ENDDO

/**/
/* Abnormal Termination of the program */
/**/
ABNOR1:     SNDPGMMSG  MSG('Error occurred in GLC004609 program')

END:        CHGVAR     VAR(&CPYFLD) VALUE('(c) Finastra International Limited +
                          2023')
             SNDPGMMSG  MSG('End of GLC004609')
             ENDPGM

