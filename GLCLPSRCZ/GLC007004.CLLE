/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    DFTACTGRP(*NO) ACTGRP(GLC007004)                            */
/*EXI    TEXT('Midas GL Background Update of GL Files - MEMOS')      */
/*********************************************************************/
/*                                                                   */
/*       Midas - General Ledger                                      */
/*                                                                   */
/*       GLC007004 - Background Update of GL Files (MEMOS)           */
/*                                                                   */
/*       (c) Finastra International Limited 2023                     */
/*                                                                   */
/*       Last Amend No. MD060909  *CREATE  Date 14Feb23              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       MD060909 - After COB job FTC0630 go in MSGW.                */
/*                                                                   */
/*********************************************************************/
             PGM

             DCL        VAR(&JRCVR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JSEQA) TYPE(*CHAR) LEN(20)
             DCL        VAR(&JSEQN) TYPE(*DEC) LEN(10 0)

             DCL        VAR(&CMTRTN) TYPE(*CHAR) LEN(10)

             DCL        VAR(&DLYTIM) TYPE(*DEC) LEN(3)
             DCL        VAR(&PRTCD) TYPE(*CHAR) LEN(7) VALUE('       +
                          ')
             DCL        VAR(&POP01) TYPE(*CHAR) LEN(20) +
                          VALUE('DelayTimeValue')
             DCL        VAR(&POP02) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP03) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP04) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP05) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP06) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP07) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP08) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP09) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP10) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PVL01) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL02) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL03) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL04) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL05) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL06) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL07) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL08) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL09) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL10) TYPE(*CHAR) LEN(200)

             DCLF       FILE(GLMEJSTD)

             COPYRIGHT  TEXT('(c) Finastra International Limited +
                          2023')

/* Global monitor message */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

             CHGJOB SWS(00000000)

/**/
/* Retrieve the current setting of 'DelayTimeValue' system value     */
/**/
             CALL       PGM(AOSVALR0) PARM(&PRTCD &POP01 &PVL01 +
                          &POP02 &PVL02 &POP03 &PVL03 &POP04 &PVL04 +
                          &POP05 &PVL05 &POP06 &PVL06 &POP07 &PVL07 +
                          &POP08 &PVL08 &POP09 &PVL09 &POP10 &PVL10)
             IF         COND(&PRTCD *EQ '       ') THEN(DO)
               CHGVAR     VAR(&DLYTIM) VALUE(%SUBSTRING(&PVL01 1 3))
             ENDDO
             ELSE       CMD(DO)
               SNDPGMMSG  MSG('Database Error in File SDSVALPD, Key +
                          DelayTimeValue') TOMSGQ(MRUNQ)
               GOTO       CMDLBL(END)
             ENDDO

             ALCOBJ     OBJ((GLMEJSTD *FILE *EXCLRD)) WAIT(0)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(DO)
             GOTO       CMDLBL(END)
             ENDDO

             CALL       PGM(SCCMTCTL) PARM('S' &CMTRTN)

             OVRDBF     FILE(GLMEJSTD) SHARE(*NO)

             SCRTVJEVT  EVENT(NDSUJR) RECEIVER(&JRCVR) SEQ(&JSEQN)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO

/* Check if journal sequence exists. */
/* If journal sequence does not exist, set to restart at first entry */

             RCVF
             MONMSG     MSGID(CPF0864)

             IF         COND(&JRCVR *LT &OLJRCV) THEN(DO)
                CHGVAR     VAR(&JRCVR) VALUE(&OLJRCV)
                CHGVAR     VAR(&JSEQA) VALUE(&OLJSEQ)
             ENDDO
             ELSE       CMD(DO)
                IF         COND(&JRCVR *EQ *BLANKS) THEN(DO)
                   CHGVAR     VAR(&JRCVR) VALUE('*CURCHAIN')
                ENDDO
                CHGVAR     VAR(&JSEQA) VALUE('*FIRST')
             ENDDO

RERUN:
             RTVJRNE    JRN(ICJRN) RCVRNG(&JRCVR) FROMENTLRG(&JSEQA) +
                          RTNRCV(&JRCVR) RTNSEQLRG(&JSEQA)

             MONMSG     MSGID(CPF0000) EXEC(DO)
                CHGVAR     VAR(&JRCVR) VALUE('*CURCHAIN')
                CHGVAR     VAR(&JSEQA) VALUE('*FIRST')
                GOTO       CMDLBL(RERUN)
             ENDDO

             CALL       PGM(GL007004) PARM(&JSEQA &JRCVR 'INCREMENT')

/* Receive journal entries*/

             RCVJRNE    JRN(ICJRN) EXITPGM(GL007003) FILE((GLMEPSTD) +
                          (GLPRONTD)) RCVRNG(&JRCVR) +
                          FROMENTLRG(&JSEQA) TOENTLRG(*NONE) +
                          JRNCDE((R) (U *IGNFILSLT) (J *IGNFILSLT)) +
                          ENTTYP(PT PX DR OL NR) ENTFMT(*JRNENTFMT) +
                          DELAY(*NEXTENT &DLYTIM) BLKLEN(*NONE) +
                          JRNENTFMT(RJNE0200) RTNPTR(*SYSMNG)

/* Receiver has changed - get new starting details for RCVJRNE command */

             IF COND(%SWITCH(XXXXX100)) THEN(DO)
                CALL       PGM(GL007004) PARM(&JSEQA &JRCVR 'JRNDTL')
                CHGJOB     SWS(00000000)
                GOTO       CMDLBL(RERUN)
             ENDDO

             IF COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO

             GOTO       CMDLBL(END)

/* Abnormal termination */

ABNOR:
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          GLC007004 ended abnormally - see job log') +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)

END:
             DLTOVR     FILE(*ALL)

             RCLACTGRP  ACTGRP(*ELIGIBLE)
             MONMSG     MSGID(CPF0000)

             ENDPGM
