/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas GL Stamp Tax Copy postings to GLGEST files')    */
/*********************************************************************/
/*                                                                   */
/*       Midas - General Ledger                                      */
/*                                                                   */
/*       GLC008143 - Copy postings to GLGEST files                   */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2010           */
/*                                                                   */
/*       Last Amend No. MD037303           Date 26Apr16              */
/*       Prev Amend No. AR820962           Date 26Aug11              */
/*                      CER049  *Create    Date 06Dec10              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       MD037303 - DBIC changed to DBICDM on FB 2.1                 */
/*       AR820962 - Component GLC008143 00001 failed                 */
/*       CER049 - Stamp Tax                                          */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ)
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5)
             DCL        VAR(&BUSY) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DILIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2010')
 
/** DB Error Definition */
 
             DCL        VAR(&DATA) TYPE(*CHAR) LEN(50)
 
/** Global Monitor Message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/** Allocate object and check for restart */
 
             ALCOBJ     OBJ((GLSTDTPD *FILE *EXCL)) WAIT(120)
/**********  RTVOBJD    OBJ(DBIC) OBJTYPE(*FILE) RTNLIB(&DILIB)  */                     /*MD037303*/
             RTVOBJD    OBJ(DBICDM) OBJTYPE(*FILE) RTNLIB(&DILIB)                       /*MD037303*/
             RTVOBJD    OBJ(GLSTDTPD) OBJTYPE(*FILE) RTNLIB(&DPLIB)
             CALL       CB0160 PARM(&CNAM &CSEQ &BUSY)
 
/** No restart - save object */
 
/**********  IF         COND(&BUSY *EQ 'N') THEN(DO)                 */                 /*AR820962*/
             IF         COND(&BUSY *NE 'Y') THEN(DO)                                    /*AR820962*/
 
/** If no save file create */
 
             CHKOBJ     OBJ(&DILIB/SAVFGLGEST) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CRTSAVF    FILE(&DILIB/SAVFGLGEST) TEXT('Save of GLGEST* +
                          before update')
             ENDDO
             CLRSAVF    FILE(&DILIB/SAVFGLGEST)
 
/** Save data */
 
             SAVOBJ     OBJ(GLGESTHH GLSTDTPD GLSTTRPD) LIB(&DPLIB) +
                          DEV(*SAVF) SAVF(&DILIB/SAVFGLGEST)
 
/** Set active flag */
 
             CHGVAR     VAR(&BUSY) VALUE('Y')
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &BUSY)
 
             ENDDO
             ELSE       CMD(DO)
 
/** Restore copy of GLGEST */
 
             RSTOBJ     OBJ(GLGESTHH GLSTDTPD GLSTTRPD) +
                          SAVLIB(&DPLIB) DEV(*SAVF) +
                          SAVF(&DILIB/SAVFGLGEST)
             ENDDO
 
/** Copy records in and update trailer */
 
             OVRDBF     FILE(GESUHH) TOFILE(GLGESTHH) SEQONLY(*YES +
                          1000)
             OVRDBF     FILE(GESUPD) TOFILE(GLSTDTPD) SEQONLY(*YES +
                          1000)
             OVRDBF     FILE(GESUZZ) TOFILE(GLSTTRPD) SEQONLY(*YES +
                          1000)
             OVRDBF     FILE(GLVCPSPD) SEQONLY(*YES 1000)
             CALL       PGM(GL008113)
 
/** DB Error */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
             RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&DATA)
             SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&DATA) +
                          TOMSGQ(MOPERQ MRUNQ)
             GOTO       CMDLBL(ABNOR)
             ENDDO
 
/** Set OK flag  */
 
             CHGVAR     VAR(&BUSY) VALUE('N')
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &BUSY)
 
             GOTO       CMDLBL(END)
 
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          GLC008143 ended abnormally - see job +
                          log') TOPGMQ(*EXT) TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 END:
             CHGVAR     VAR(&CPYFLD) VALUE('(c) Misys International +
                          Banking Systems Ltd')
             ENDPGM
