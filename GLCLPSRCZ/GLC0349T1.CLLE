/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas GL Cleared Balance - Start Task Split')         */
/*********************************************************************/
/*                                                                   */
/*       Midas - General Ledger Module                               */
/*                                                                   */
/*       GLC0349T1 - Midas GL Cleared Balance - Start Task Split     */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2012           */
/*                                                                   */
/*       Last Amend No. MD041376           Date 16Sep16              */
/*       Prev Amend No. LUC101             Date 21Jan15              */
/*                      CGL120   *CREATE   Date 06Aug12              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       MD041376 - Component failed due to missing members and data */
/*                  queues                                           */
/*       LUC101 - Amend Profit & Loss Transfer at End of Year        */
/*       CGL120 - GL COB Components Task Split                       */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&POSTNUM &CNAM &CSEQ)

             DCL        VAR(&CNAME) TYPE(*CHAR) LEN(10) +
                          VALUE('GLC0349')
             DCL        VAR(&DRIVER) TYPE(*CHAR) LEN(10) +
                          VALUE('GLCBUSPD')
             DCL        VAR(&INDEXFL) TYPE(*CHAR) LEN(10) +
                          VALUE('GLCBUIPD')
             DCL        VAR(&NAME1) TYPE(*CHAR) LEN(5) VALUE('GLCBU')
             DCL        VAR(&RCVDTAQ) TYPE(*CHAR) LEN(10) +
                          VALUE('GLCBUSERVE')
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DTAQLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NAME2_C) TYPE(*CHAR) LEN(5) VALUE('00001')
             DCL        VAR(&NAME2_N) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&LOOP) TYPE(*DEC) LEN(3 0) VALUE(1)
             DCL        VAR(&CURRCD) TYPE(*DEC) LEN(10 0)
             DCL        VAR(&POSTNUM) TYPE(*CHAR) LEN(1)

             DCLF       FILE(CBCMPNLC)

             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2012')

/* Global monitor message. */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

/* Send message to MRUNQ. */

             SNDPGMMSG  MSG('GLC0349T1 - Cleared Balance - Start +
                          Task Split') TOMSGQ(MRUNQ)

             IF         COND(&POSTNUM = '3') THEN(DO)
                RTVMBRD    FILE(GEXBPD) NBRCURRCD(&CURRCD)
                IF         COND(&CURRCD = 0) THEN(DO)
                   GOTO       CMDLBL(END)
                ENDDO
             ENDDO

/* Determine full DTAQLIB library name. */

             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(&DTAQLIB) VALUE(&SYSID *CAT 'DPLIB')

/* Clear index file. */

             CLRPFM     FILE(&INDEXFL)

/* Remove all members from driving file. */

             RMVM       FILE(&DRIVER) MBR(*ALL)
             MONMSG     MSGID(CPF7301 CPF7303)

/* Clear main data queue. */

             CALL       PGM(QCLRDTAQ) PARM(&RCVDTAQ  '*LIBL')

/* Retrieve number of sub-tasks. */

 CHAIN:
             RCVF
             CHGVAR     VAR(&NAME2_N) VALUE(&DHCSEQ)
             IF         COND((&DHCOTT *NE &CNAME) *OR (&NAME2_N *NE +
                          &CSEQ)) THEN(DO)
                GOTO       CMDLBL(CHAIN)
             ENDDO

/* Add all members and create data queues. */

             CHGVAR     VAR(&NAME2_C) VALUE(&DHCSEQ)
 LOOP:
             IF         COND(&LOOP *GT &DHTSNO) THEN(DO)
                GOTO       CMDLBL(START)
             ENDDO

             CHGVAR     VAR(&MEMBER) VALUE(&NAME1 *CAT &NAME2_C)
             ADDPFM     FILE(&DRIVER) MBR(&MEMBER)

/* Create data queue if it doesn't already exist */
/* and set authorities. */

             CHKOBJ     OBJ(&DTAQLIB/&MEMBER) OBJTYPE(*DTAQ)
             MONMSG     MSGID(CPF9801) EXEC(DO)
                CRTDTAQ    DTAQ(&DTAQLIB/&MEMBER) MAXLEN(50)
             ENDDO
             CALL       PGM(QCLRDTAQ) PARM(&MEMBER &DTAQLIB)

/*           IF         COND(&LOOP *EQ 1) THEN(DO)              */ /*LUC101*/
/**********  IF         COND(&LOOP *EQ 1 *AND &CSEQ < 10) THEN(DO) */            /*LUC101 MD041376*/
             IF         COND(&LOOP *EQ 1 *AND &CSEQ *NE 90001) THEN(DO)                 /*MD041376*/
                   CHGVAR     VAR(&NAME2_N) VALUE(01000)
                   CHGVAR     VAR(&NAME2_N) VALUE(&NAME2_N * &CSEQ)
             ENDDO

             IF         COND(&LOOP *EQ 1 *AND &CSEQ = 90001) THEN(DO) /*LUC101*/
                CHGVAR     VAR(&NAME2_N) VALUE(91000) /*LUC101*/
             ENDDO      /*LUC101*/

             CHGVAR     VAR(&LOOP) VALUE(&LOOP + 1)
             CHGVAR     VAR(&NAME2_N) VALUE(&NAME2_N + 1)
             CHGVAR     VAR(&NAME2_C) VALUE(&NAME2_N)
             GOTO       CMDLBL(LOOP)

 START:

             GOTO       CMDLBL(END)

 ABNOR:
             SNDPGMMSG  MSG('Program GLC0349T1 ended abnormally') +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)
             CHGJOB     SWS(XXXXXX11)

 END:
             ENDPGM
