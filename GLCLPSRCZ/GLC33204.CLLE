/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    DFTACTGRP(*NO) ACTGRP(GLC33204)                             */
/*EXI    TEXT('Midas GL Balance Statement - Spot Reval & TfrBccy')   */
/*********************************************************************/
/*                                                                   */
/*       Midas - General Ledger Module                               */
/*                                                                   */
/*       GLC33204 - GL Account Balance Extract - Spot Reval & Tfr    */
/*                  Bccy Equivalents                                 */
/*                                                                   */
/*       (c) Misys Banking Systems Ltd. 2012                         */
/*                                                                   */
/*       Last Amend No. CGL120  *CREATE    Date 06Aug12              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       CGL120 - GL COB Components Task Split                       */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&POSTNUM &CNAM &CSEQ)
 
/* Declare file and variables                                      */
 
             DCL        VAR(&POSTNUM) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&CURN) TYPE(*DEC) LEN(10 0)
             DCL        VAR(&GLSTAT) TYPE(*CHAR) LEN(257)
             DCL        VAR(&POST) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&ACJND) TYPE(*CHAR) LEN(6)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems +
                          Ltd. 2012')
 
/* Global monitor message                                          */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Send message to MRUNQ                                           */
 
             SNDPGMMSG  MSG('GLC33204 - Balance Statements - Spot Reval') +
                          TOMSGQ(MRUNQ)
 
/* Set Job switches                                                */
 
             CHGJOB     SWS(00000000)
 
             STRCMTCTL  LCKLVL(*CHG) CMTSCOPE(*JOB)
             MONMSG     MSGID(CPF8351)
 
/* Clear work file                                                 */
 
             CLRPFM     FILE(ZACCNTAB)
 
             RTVMBRD    FILE(GEXBPD) NBRCURRCD(&CURN)
             IF         COND(&POSTNUM = '3' *AND &CURN = 0) THEN(DO)
             GOTO       CMDLBL(END)
             ENDDO
 
             OVRDBF     FILE(ZACCNTAB) OVRSCOPE(*JOB) SEQONLY(*YES 100)
             CHGVAR     VAR(&ACJND) VALUE('ACJND' *CAT &POSTNUM)
             OVRDBF     FILE(ACJND4) TOFILE(&ACJND) OVRSCOPE(*JOB) +
                          SHARE(*YES) SEQONLY(*YES 500)
             CALL       PGM(GL33204)
             DLTOVR     FILE(ACJND4) LVL(*JOB)
             DLTOVR     FILE(ZACCNTAB) LVL(*JOB)
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
             GOTO       CMDLBL(ABNOR)
             ENDDO
 
             IF         COND(%SWITCH(XXXXXX00)) THEN(DO)
             COMMIT
             GOTO       CMDLBL(END)
             ENDDO
 
             GOTO       CMDLBL(END)
 
 ABNOR:
             ROLLBACK
             SNDPGMMSG  MSG('Program GLC33204 ended abnormally') +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)
             CHGJOB     SWS(XXXXXX11)
 
 END:
             ENDCMTCTL
             MONMSG     MSGID(CPF0000)
             ENDPGM
