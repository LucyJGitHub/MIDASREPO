      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Additional Information Maintenance')          *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL000185DF - Additional Information Maintenance         *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG23742           Date 09May09               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      *                 CGL013  *CREATE    Date 29Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23742 - Improper handling of Ext Narr from Batch          *
      *             Interface Input                                   *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA12
      **Start of change BUG23742
     A                                      CA03
      **End of change BUG23742
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      ** Header
      *
     A          R GL000185F0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      OVERLAY PUTOVR
     A                                      OVRDTA OVRATR
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'GL000185F0'
      *
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 25'Additional Information +
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
     A                                  5  5'Batch Number:'
     A            #0BTNB         3   O  5 21TEXT('Batch Number')
     A                                      DSPATR(HI)
      *
     A                                  5 27'Item No.:'
     A            #0BINB         3   O  5 37TEXT('Item Number')
     A                                      DSPATR(HI)
      *
     A                                  6  5'Account Number:'
     A**Start of change CGL029
     A**********  #0ACCN        18   O  6 21TEXT('Account Number')                            CGL029
     A            #0ACCN        24   O  6 21TEXT('Account Number')
     A**End of change CGL029
     A                                      DSPATR(HI)
      *
     A**Start of change CGL029
     A**********                        6 42'Retail:'                                         CGL029
     A**********  #0RACN        10   O  6 50TEXT('Retail Account +                            CGL029
     A                                  6 50'Retail:'
     A            #0RACN        10   O  6 58TEXT('Retail Account +
     A**End of change CGL029
     A                                      Number')
     A                                      DSPATR(HI)
      *
     A                                  7  5'Amount:'
     A            #0PTAM        14   O  7 13TEXT('Posting Amount')
     A                                      DSPATR(HI)
      *
     A                                  7 30'D/C:'
     A            #0DCIN         1   O  7 35TEXT('Debit/Credit +
     A                                      Indicator')
     A                                      DSPATR(HI)
      *
     A                                  7 39'Narrative:'
     A            #0NRDC        30   O  7 50TEXT('Narrative Description')
     A                                      DSPATR(HI)
      *
     A                                  8  5'Value Date:'
     A            #0VLDT         7   O  8 17TEXT('Value Date')
     A                                      DSPATR(HI)
      *
     A                                  8 27'Type:'
     A            #0TRTY         5   O  8 33TEXT('Transaction Type')
     A                                      DSPATR(HI)
      *
     A                                  8 41'Subtype:'
     A            #0TSTY        10   O  8 50TEXT('Transaction Subtype')
     A                                      DSPATR(HI)
      *
     A                                  8 63'Cheque:'
     A            #0CQNB         8   O  8 71TEXT('Cheque Number')
     A                                      DSPATR(HI)
      *
     A                                 10  5'Additional Information:'
     A            #0ADI1        65   B 11 10TEXT('Additional Info 1')
     A                                      CHECK(LC)
     A  28                                  DSPATR(PR)
     A  31N28                               DSPATR(RI PC)
     A N28N31                               DSPATR(UL)
     A            #0ADI2        65   B 12 10TEXT('Additional Info 2')
     A                                      CHECK(LC)
     A  28                                  DSPATR(PR)
     A  32N28                               DSPATR(RI PC)
     A N28N32                               DSPATR(UL)
     A            #0ADI3        65   B 13 10TEXT('Additional Info 3')
     A                                      CHECK(LC)
     A  28                                  DSPATR(PR)
     A  33N28                               DSPATR(RI PC)
     A N28N33                               DSPATR(UL)
     A            #0ADI4        65   B 14 10TEXT('Additional Info 4')
     A                                      CHECK(LC)
     A  28                                  DSPATR(PR)
     A  34N28                               DSPATR(RI PC)
     A N28N34                               DSPATR(UL)
     A            #0ADI5        65   B 15 10TEXT('Additional Info 5')
     A                                      CHECK(LC)
     A  28                                  DSPATR(PR)
     A  35N28                               DSPATR(RI PC)
     A N28N35                               DSPATR(UL)
     A            #0ADI6        65   B 16 10TEXT('Additional Info 6')
     A                                      CHECK(LC)
     A  28                                  DSPATR(PR)
     A  36N28                               DSPATR(RI PC)
     A N28N36                               DSPATR(UL)
      *
     A                                 18  5'ERI Information Indicator:'
     A            #0AERI         1   B 18 32TEXT('ERI Info Indicator')
     A  28                                  DSPATR(PR)
     A  37N28                               DSPATR(RI PC)
     A N28N37                               DSPATR(UL)
      *
      **Start of change BUG23742
     A**********                       22  3'F12=Previous'                                  BUG23742
     A                                 22  3'F3=Exit   F12=Previous'
      **End of change BUG23742
      *
      ** Message Subfile
      *
     A          R GL000185S0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #0MSGK                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
      ** Message Subfile Control
      *
     A          R GL000185C0                TEXT('Program messages')
     A                                      SFLCTL(GL000185S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGMQ                    SFLPGMQ
