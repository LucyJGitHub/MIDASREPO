     A*****************************************************************
 *STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL GL Reporting Group / Account Report')         *
      *****************************************************************
      *                                                               *
      *  Midas General Ledger Module                                  *
      *                                                               *
      *  DSPF/GL000663DF - Midas GL Reporting Group / Account Report  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. MD043079           Date 08Dec16               *
      *  Prev Amend No. MD043045           Date 07Dec16               *
      *                 CGL154  *CREATE    Date 13Oct14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD043079 - Error Message "Action Code must be 1" is still    *
      *             displayed even if input action code is already    *
      *             corrected and next screen is displayed            *
      *  MD043045 - Confirmation Messages are truncated for Actions   *
      *             Amend and Void                                    *
      *  CGL154 - FATCA Reporting                                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
     A                                      INDARA
      *
      ** Message Subfile Format
      *
     A          R GL00066XS0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2014')
     A                                      SFL
     A                                      SFLMSGRCD(27)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Message Subfile Control
      *
     A          R GL00066XC0                TEXT('MSG Subfile Control')
     A                                      SFLCTL(GL00066XS0)
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      SFLINZ
      ** Start of Change MD043079
     A**40******                            SFLDSP                                          MD043079
     A                                      SFLDSP
      ** End of Change MD043079
     A                                      SFLDSPCTL
     A  46                                  SFLEND
     A  41                                  SFLCLR
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      ** Reporting Group / Account Report (Subfile Details)
      *
     A          R GL00066XS1                TEXT('Select Screen Subfile')
     A                                      SFL
     A  34                                  SFLNXTCHG
 
     A            DDMTYP         4A  H
 
     A            DDACTN         1A  B 12  6
     A                                      DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A            DDCNNH        10A  O 12 12
     A                                      DSPATR(HI)
     A            DDRPTN        20A  O 12 25
     A                                      DSPATR(HI)
     A            DDACHT         8A  O 12 48
     A                                      DSPATR(HI)
     A            DDACNO        26A  O 12 65
     A                                      DSPATR(HI)
     A            DDDRID        35A  O 12 94
     A                                      DSPATR(HI)
      *
      ** Reporting Group / Account Report (Subfile Control)
      *
     A          R GL00066XC1                SFLCTL(GL00066XS1)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      TEXT('Select Screen Control')
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF16(16)
     A                                      CF21(21)
     A                                      CF22(22)
     A  97                                  SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  95                                  SFLCLR
     A N96                                  ROLLUP(98)
     A N96                                  ROLLDOWN
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'GL000663'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 53'FATCA Reports Management'
     A                                      DSPATR(HI UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  3'Document Type:'
     A            DDDTIN         7A  O  4 18OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  4 29'Action Code:'
     A            DDACTC         1A  O  4 42TEXT('Action Code')
     A                                      DSPATR(HI)
     A                                  4 48'Report Type:'
     A            DDRTYP        18A  O  4 61TEXT('Report Type')
     A                                      DSPATR(HI)
     A                                  4 84'Year:'
     A            DDRYER         4A  O  4 90TEXT('Year')
     A                                      DSPATR(HI)
     A                                  6  3'Report - Reporting Group / +
     A                                      Account Report'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  3'Action'
     A                                  8 12'Customer /'
     A                                  9 12'NAH'
     A                                  8 25'Report Name'
     A                                  8 48'Account Holder'
     A                                  9 48'Type'
     A                                  8 65'Account Number'
     A                                  8 94'Doc. Ref. ID'
     A            D1CNNH        10A  O 10 12OVRDTA
     A                                      DSPATR(UL HI)
     A            D1RPTN        20A  O 10 25OVRDTA
     A                                      DSPATR(UL HI)
     A            D1ACHT         8A  O 10 48OVRDTA
     A                                      DSPATR(UL HI)
     A            D1ACNO        26A  O 10 65OVRDTA
     A                                      DSPATR(UL HI)
     A            D1DRID        35A  O 10 94OVRDTA
     A                                      DSPATR(UL HI)
      *
      ** Account Report - Individual / Organisation Detail
      *
     A          R GL00066XF0                TEXT('SELECT SCREEN CONTROL')
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'GL000663'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 53'FATCA Reports Management'
     A                                      DSPATR(HI UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  3'Document Type:'
     A            D2DTIN         7A  O  4 18OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  4 29'Action Code:'
     A            DDACTC         1A  O  4 42TEXT('Action Code')
     A                                      DSPATR(HI)
     A                                  4 48'Report Type:'
     A            DDRTYP        18A  O  4 61TEXT('Report Type')
     A                                      DSPATR(HI)
     A                                  4 84'Year:'
     A            DDRYER         4A  O  4 90TEXT('Year')
     A                                      DSPATR(HI)
     A                                  6  3'Report - Reporting Group / +
     A                                      Account Report / Account +
     A                                      Holder /'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 64'Individual Or Organisation'
 
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  3'Document Ref. ID:'
 
     A            D2DRID        31A  O  8 21OVRDTA
 
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  8 55'Corrected Ref.ID:'
 
     A            D2CRID        31A  O  8 73OVRDTA
 
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  9  3'Account Number..:'
     A            D2ACNO        26A  O  9 21OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  9 55'Balance.........:'
 
     A            D2ACBL        21A  O  9 73TEXT('Account Balance')
 
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  9 96'Currency.............:'
     A            D2ABCC         3A  O  9119TEXT('Currency ISO Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 10  3'Customer/NAH....:'
     A            D2CNNH        10A  O 10 21OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 10 55'Report Name.....:'
     A            D2RPTN        20A  O 10 73OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 10 96'Account Holder Type..:'
     A            D2ACHT         8A  O 10119OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A N81                             12  3'Name......................:'
     A N81        D2NAME        70A  O 12 31TEXT('Name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  81                             12  3'First Name................:'
     A  81        D2FNME        70A  O 12 31TEXT('First Name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  81                             13  3'Last Name.................:'
     A  81        D2LNME        70A  O 13 31TEXT('Last Name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 14  3'Address...................:'
     A            D2ADD1        60A  O 14 31TEXT('Address')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            D2ADD2        60A  O 15 31TEXT('Address')
     A                                      OVRDTA
     A                                      DSPATR(HI)
 
     A                                 16  3'Residing Country Code.....:'
 
     A            D2RECO         2A  O 16 31TEXT('Residence Country Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 17  3'Country Code..............:'
     A            D2COCO         2A  O 17 31TEXT('Country Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 18  3'Birth Date................:'
 
     A            D2BRTD        10A  O 18 31TEXT('Birth Date')
 
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 20  3'Tax Identification Numbers:'
     A            D2COT1         2A  O 20 31TEXT('Country of TIN 1')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            D2TIN1        25A  O 20 38TEXT('Tax Identification No.1')
     A                                      OVRDTA
     A                                      DSPATR(HI)
 
      *
      ** Account Report - Account Bal/Payment Detail - (Subfile Details)
      *
     A          R GL00066XS2                SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            D3PAYT         8A  O 14 11
     A                                      DSPATR(HI)
     A            D3PACC         3A  O 14 38
     A                                      DSPATR(HI)
 
     A            D3PSTA        21N  O 14 55
 
     A                                      DSPATR(HI)
      *
      ** Account Report - Account Bal/Payment Detail - (Subfile Control)
      *
     A          R GL00066XC2                SFLCTL(GL00066XS2)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A N90                                  CF03(03)
     A                                      CF12(12)
     A  97                                  SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  95                                  SFLCLR
     A N96                                  ROLLUP(98)
     A N96                                  ROLLDOWN
     A                                      OVERLAY
     A                                      PUTOVR
     A            D2SFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'GL000663'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 53'FATCA Reports Management'
     A                                      DSPATR(HI UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  3'Document Type:'
     A            D2DTIN         7A  O  4 18OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  4 29'Action Code:'
     A            DDACTC         1A  O  4 42TEXT('Action Code')
     A                                      DSPATR(HI)
     A                                  4 48'Report Type:'
     A            DDRTYP        18A  O  4 61TEXT('Report Type')
     A                                      DSPATR(HI)
     A                                  4 84'Year:'
     A            DDRYER         4A  O  4 90TEXT('Year')
     A                                      DSPATR(HI)
     A                                  6  3'Report - Reporting Group / +
     A                                      Account Report / Account +
     A                                      Holder / Account Balance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  3'Document Ref. ID:'
 
     A            D2DRID        31A  O  8 21OVRDTA
 
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  8 55'Corrected Ref.ID:'
 
     A            D2CRID        31A  O  8 73OVRDTA
 
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  9  3'Account Number..:'
     A            D2ACNO        26A  O  9 21OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  9 55'Balance.........:'
 
     A            D2ACBL        21A  O  9 73TEXT('Account Balance')
 
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  9 96'Currency.............:'
     A            D2ABCC         3A  O  9119TEXT('Currency ISO Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 10  3'Customer/NAH....:'
     A            D2CNNH        10A  O 10 21OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 10 55'Report Name.....:'
     A            D2RPTN        20A  O 10 73OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 10 96'Account Holder Type..:'
     A            D2ACHT         8A  O 10119OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 12 11'FATCA Payment Type'
     A                                 12 38'Currency'
     A                                 12 55'Payment Amount'
      *
      ** Account Report - Substantial Owner - (Subfile Details)
      *
     A          R GL00066XS3                SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            D5ACNO        26A  O 15  6
     A                                      DSPATR(HI)
 
     A            D5RECO         2A  O 15 79
     A                                      DSPATR(HI)
     A            D5COCO         2A  O 15 94
     A                                      DSPATR(HI)
 
     A            D5BRTD        10A  O 15109
 
     A                                      DSPATR(HI)
 
     A            D5FNME        70A  O 16  7
     A                                      DSPATR(HI)
     A            D5LNME        70A  O 17  7
     A                                      DSPATR(HI)
 
     A            D5ADD1       120   O 18  7
 
     A                                      DSPATR(HI)
     A            D5TIN1        28A  O 19  8
     A                                      DSPATR(HI)
 
      *
      ** Account Report - Substantial Owner - (Subfile Control)
      *
     A          R GL00066XC3                SFLCTL(GL00066XS3)
 
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
 
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03(03)
     A                                      CF12(12)
     A  97                                  SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  95                                  SFLCLR
     A N96                                  ROLLUP(98)
     A N96                                  ROLLDOWN
     A                                      OVERLAY
     A                                      PUTOVR
     A            D4SFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'GL000663'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 53'FATCA Reports Management'
     A                                      DSPATR(HI UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  3'Document Type:'
     A            D4DTIN         7A  O  4 18OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  4 29'Action Code:'
     A            DDACTC         1A  O  4 42TEXT('Action Code')
     A                                      DSPATR(HI)
     A                                  4 48'Report Type:'
     A            DDRTYP        18A  O  4 61TEXT('Report Type')
     A                                      DSPATR(HI)
     A                                  4 84'Year:'
     A            DDRYER         4A  O  4 90TEXT('Year')
     A                                      DSPATR(HI)
     A                                  6  3'Report - Reporting Group / +
     A                                      Account Report / Account +
 
     A                                      Holder /'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 64'Substantial US Owner(s)'
 
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  3'Document Ref. ID:'
 
     A            D2DRID        31A  O  8 21OVRDTA
 
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  8 55'Corrected Ref.ID:'
 
     A            D2CRID        31A  O  8 73OVRDTA
 
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  9  3'Account Number..:'
     A            D2ACNO        26A  O  9 21OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  9 55'Balance.........:'
 
     A            D2ACBL        21A  O  9 73TEXT('Account Balance')
 
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  9 96'Currency.............:'
     A            D2ABCC         3A  O  9119TEXT('Currency ISO Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 10  3'Customer/NAH....:'
     A            D2CNNH        10A  O 10 21OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 10 55'Report Name.....:'
     A            D2RPTN        20A  O 10 73OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 10 96'Account Holder Type..:'
     A            D2ACHT         8A  O 10119OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 12  6'Account Number'
 
     A                                 13  7'Name and Address'
     A                                 14  8'TIN (Issued By)'
 
     A                                 12 79'Residence'
     A                                 13 79'Country'
     A                                 12 94'Country'
     A                                 13 94'Code'
     A                                 12109'Birth Date'
      *
      ** Selection Screen Footer - Reporting Group - Account Report Main Display
      *
     A          R GL00066XF1                TEXT('Select Screen Footer')
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF16(16)
     A                                      CF21(21)
     A                                      CF22(22)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 25  2'1=Select'
     A                                 26  2'F3=Exit   F5=Refresh   +
     A                                       F16=Message Header   +
     A                                       F21=Pool Report   +
     A                                       F22=Exception List'
     A                                      OVRATR
      *
      ** Selection Screen Footer - Reporting Group - Account Report Others
      *
     A          R GL00066XF2                TEXT('Select Screen Footer')
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 26  3'F3=Exit   F12=Previous'
     A                                      OVRATR
      *
      ** Selection Screen Footer - Reporting Group - Account Balance
      *
     A          R GL00066XF3                TEXT('Select Screen Footer')
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PUTOVR
 
     A  51                             26  2'Account Holder will be -
     A                                      selected for exclusion. -
     A                                      Press Enter to proceed'
     A  51                                  DSPATR(HI)
     A  52                             26  2'Account Holder will be -
     A                                      selected for inclusion. -
     A                                      Press Enter to proceed'
     A  52                                  DSPATR(HI)
      ** Start of Change MD043045
     A  54                             26  2'Account Holder will be -
     A                                      selected for amendment. -
     A                                      Press Enter to proceed'
     A  54                                  DSPATR(HI)
     A  55                             26  2'Account Holder will be -
     A                                      voided. -
     A                                      Press Enter to proceed -
     A                                      or F12 to cancel       '
     A  55                                  DSPATR(HI)
     A**********                       26 72' or F12 to cancel'                             MD043045
     A**********                            DSPATR(HI)                                      MD043045
     A N55                             26 72'or F12 to cancel'
     A N55                                  DSPATR(HI)
      ** End of Change MD043045
     A  53                             26  2'Account Holder will be -
     A                                      selected for correction. -
     A                                      Press Enter to proceed'
     A  53                                  DSPATR(HI)
      ** Start of Change MD043045
     A**53******                       26 73' or F12 to cancel'                             MD043045
     A  53                             26 73'or F12 to cancel'
      ** End of Change MD043045
 
     A                                      DSPATR(HI)
      *
      ** Account Report - Individual / Organisation Detail (Remarks)
      *
     A          R GL00066XF4                TEXT('SELECT SCREEN CONTROL')
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'GL000663'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 53'FATCA Reports Management'
     A                                      DSPATR(HI UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  3'Document Type:'
     A            D2DTIN         7A  O  4 18OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  4 29'Action Code:'
     A            DDACTC         1A  O  4 42TEXT('Action Code')
     A                                      DSPATR(HI)
     A                                  4 48'Report Type:'
     A            DDRTYP        18A  O  4 61TEXT('Report Type')
     A                                      DSPATR(HI)
     A                                  4 84'Year:'
     A            DDRYER         4A  O  4 90TEXT('Year')
     A                                      DSPATR(HI)
     A                                  6  3'Report - Reporting Group / +
     A                                      Account Report / Account +
 
     A                                      Holder /'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 64'Individual Or Organisation'
 
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  3'Document Ref. ID:'
 
     A            D2DRID        31A  O  8 21OVRDTA
 
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  8 55'Corrected Ref.ID:'
 
     A            D2CRID        31A  O  8 73OVRDTA
 
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  9  3'Account Number..:'
     A            D2ACNO        26A  O  9 21OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  9 55'Balance.........:'
 
     A            D2ACBL        21A  O  9 73TEXT('Account Balance')
 
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  9 96'Currency.............:'
     A            D2ABCC         3A  O  9119TEXT('Currency ISO Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 10  3'Customer/NAH....:'
     A            D2CNNH        10A  O 10 21OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 10 55'Report Name.....:'
     A            D2RPTN        20A  O 10 73OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 10 96'Account Holder Type..:'
     A            D2ACHT         8A  O 10119OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
 
     A                                 12 03'Remarks...................:'
 
     A            D2MER1        64A  B 12 31
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            D2MER2        64A  B 13 31
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            D2MER3        64A  B 14 31
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            D2MER4        64A  B 15 31
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
 
      *
      ** Account Report - Individual / Organisation Detail (Include Remarks)
      *
     A          R GL00066XF5                TEXT('SELECT SCREEN CONTROL')
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'GL000663'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 53'FATCA Reports Management'
     A                                      DSPATR(HI UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  3'Document Type:'
     A            D2DTIN         7A  O  4 18OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  4 29'Action Code:'
     A            DDACTC         1A  O  4 42TEXT('Action Code')
     A                                      DSPATR(HI)
     A                                  4 48'Report Type:'
     A            DDRTYP        18A  O  4 61TEXT('Report Type')
     A                                      DSPATR(HI)
     A                                  4 84'Year:'
     A            DDRYER         4A  O  4 90TEXT('Year')
     A                                      DSPATR(HI)
     A                                  6  3'Report - Reporting Group / +
     A                                      Account Report / Account +
 
     A                                      Holder /'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 64'Individual Or Organisation'
 
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  3'Document Ref. ID:'
     A            D2DRID        31A  O  8 21OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  8 55'Corrected Ref.ID:'
     A            D2CRID        31A  O  8 73OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  9  3'Account Number..:'
     A            D2ACNO        26A  O  9 21OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  9 55'Balance.........:'
     A            D2ACBL        21A  O  9 73TEXT('Account Balance')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  9 96'Currency.............:'
     A            D2ABCC         3A  O  9119TEXT('Currency ISO Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 10  3'Customer/NAH....:'
     A            D2CNNH        10A  O 10 21OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 10 55'Report Name.....:'
     A            D2RPTN        20A  O 10 73OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 10 96'Account Holder Type..:'
     A            D2ACHT         8A  O 10119OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 12 03'Manual Inclusion Remarks..:'
     A            D2MIR1        64A  O 12 31TEXT('Include Remark 1')
     A                                      OVRDTA
 
     A                                      DSPATR(HI)
 
     A            D2MIR2        64A  O 13 31TEXT('Include Remark 2')
     A                                      OVRDTA
 
     A                                      DSPATR(HI)
 
     A            D2MIR3        64A  O 14 31TEXT('Include Remark 3')
     A                                      OVRDTA
 
     A                                      DSPATR(HI)
 
     A            D2MIR4        64A  O 15 31TEXT('Include Remark 4')
     A                                      OVRDTA
 
     A                                      DSPATR(HI)
 
     A                                 16 03'Timestamp.................:'
     A            D2TMST        26A  O 16 31TEXT('Timestamp')
     A                                      OVRDTA
 
     A                                      DSPATR(HI)
 
     A                                 17 03'User......................:'
     A            D2USER        10A  O 17 31TEXT('User')
     A                                      OVRDTA
 
     A                                      DSPATR(HI)
 
      *****************************************************************
