      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Reporting Group - Pool Report')               *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL000664DF - Midas GL Reporting Group - Pool Report     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. CGL154  *CREATE    Date 13Oct14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL154 - FATCA Reporting                                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      CHGINPDFT
     A                                      MSGLOC(27)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R GL000664M1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2014')
     A                                      SFL
     A                                      SFLMSGRCD(27)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R GL000664M0                TEXT('Message Subfile Control')
     A                                      SFLCTL(GL000664M1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  55                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A          R GL000664S0                TEXT('Subfile Record')
     A                                      SFL
     A  63                                  SFLNXTCHG
     A            DSACTN         1A  B 14 05
     A  69                                  DSPATR(RI PC)
     A N69                                  DSPATR(HI UL)
     A  53                                  DSPATR(ND)
     A  72                                  DSPATR(PR)
     A            DCUSNH        10A  O 14 19
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(ND)
     A            DSRPTN        20A  O 14 41
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(ND)
     A            DSRTWN        10A  O 14 73
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(ND)
     A            DSTOTB        22A  O 14 96
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(ND)
      *
     A          R GL000664C0                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      SFLCTL(GL000664S0)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      CF20(20)
     A                                      CF22(22)
     A  50                                  SFLDSP
     A                                      SFLDSPCTL
     A  55                                  SFLEND
     A  51                                  SFLCLR
     A N56                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            D0SFRN         4S 0H      SFLRCDNBR(CURSOR)
     A            DDUSER        10A  O  1  2TEXT('Current User')
     A                                      OVRDTA
     A                                  1 13'GL000664'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('WorkStation ID')
     A                                      OVRDTA
     A                                  2 53'FATCA Reports Management'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Document Type:'
     A            DDOCTP         7A  O  4 18TEXT('Document Type')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 29'Action Code:'
     A            DDACTC         1A  O  4 42TEXT('Action Code:')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 48'Report Type:'
     A            DREPTY        18A  O  4 61TEXT('Report Type:')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 84'Year:'
     A            DCYEAR         4A  O  4 90TEXT('Calendar Year')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  6  2'Reporting Group / Pool Report'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  2'Document Ref. ID:'
     A            D0DOCRF       35A     8 20TEXT('Document Ref.ID')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  8 73'Corrected Ref. ID:'
     A            D0CDCRF       35A     8 92TEXT('Corrected Ref.ID')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  9  2'Account Count:'
     A            DCOUNT        10A     9 17TEXT('Account Count')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  9 29'Pool Balance:'
     A            DPBALC        22A     9 43TEXT('Pool Balance')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  9 73'Currency:'
     A            DBALCC         3A     9 83TEXT('Pool Balance (Currency Code)')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A                                 11  3 'Action'
     A                                 11 19'Cust/NAH'
     A                                 11 41'Report Name'
     A                                 11 73'Report Town'
     A                                 11 96'Total Balance'
     A            DICSNH        10A  B 12 19TEXT('CUST/NAH')
     A  54                                  DSPATR(PC)
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A            DRPTNM        20A  B 12 41TEXT('Report Name')
     A  52                                  DSPATR(PC)
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A            DRTOWN        10A  B 12 73TEXT('Report Town')
     A  68                                  DSPATR(PC)
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A          R GL000664S1                TEXT('Subfile Record')
     A                                      SFL
     A            DSACNT        26A  O 11 14
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(ND)
     A            DSAMNT        22A  O 11 50
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(ND)
     A            DSCURR         3A  O 11 82
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(ND)
      *
     A          R GL000664C1                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      SFLCTL(GL000664S1)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      CF03(03)
     A                                      CF12(12)
     A  64                                  SFLDSP
     A                                      SFLDSPCTL
     A  65                                  SFLEND
     A  67                                  SFLCLR
     A N66                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            D1SFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2TEXT('Current User')
     A                                      OVRDTA
     A                                  1 13'GL000664'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('WorkStation ID')
     A                                      OVRDTA
     A                                  2 53'FATCA Reports Management'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Document Type:'
     A            DDOCTP         7A  O  4 18TEXT('Document Type')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 29'Action Code:'
     A            DDACTC         1A  O  4 42TEXT('Action Code:')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 48'Report Type:'
     A            DREPTY        18A  O  4 61TEXT('Report Type:')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 84'Year:'
     A            DCYEAR         4A  O  4 90TEXT('Calendar Year')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  6  2'Reporting Group / Pool Report'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  2'Customer Number:'
     A            DCUSNH        10A  O  7 19TEXT('Customer Number')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  7 34'Total Balance:'
     A            DTOTBL        22A     7 49TEXT('Total Customer Balance')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  7 76'Currency:'
     A            DBALCC         3A     7 86TEXT('Pool Balance (Currency Code)')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  9 14'Account Number'
     A                                  9 58'Account Balance'
     A                                  9 80'Currency'
      *
     A          R GL000664S2                TEXT('Subfile Record')
     A                                      SFL
     A  77                                  SFLNXTCHG
     A            DMACTN         1A  B 11 07
     A  79                                  DSPATR(RI PC)
     A N79                                  DSPATR(HI UL)
     A  73                                  DSPATR(ND)
     A            DMTYPE         8A  O 11 17
     A N73                                  DSPATR(HI)
     A  73                                  DSPATR(ND)
     A            DMCOUNT        9A  O 11 34
     A N73                                  DSPATR(HI)
     A  73                                  DSPATR(ND)
     A            DMPBALC       22A  O 11 50
     A N73                                  DSPATR(HI)
     A  73                                  DSPATR(ND)
     A            DBALCC         3A  O 11 79
     A N73                                  DSPATR(HI)
     A  73                                  DSPATR(ND)
     A            DMDOCRF       35A  O 11 90
     A N73                                  DSPATR(HI)
     A  73                                  DSPATR(ND)
      *
     A          R GL000664C2                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      SFLCTL(GL000664S2)
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF16(16)
     A                                      CF20(20)
     A                                      CF22(22)
     A  70                                  SFLDSP
     A                                      SFLDSPCTL
     A  75                                  SFLEND
     A  71                                  SFLCLR
     A N76                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            D2SFRN         4S 0H      SFLRCDNBR(CURSOR)
     A            DDUSER        10A  O  1  2TEXT('Current User')
     A                                      OVRDTA
     A                                  1 13'GL000664'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('WorkStation ID')
     A                                      OVRDTA
     A                                  2 53'FATCA Reports Management'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Document Type:'
     A            DDOCTP         7A  O  4 18TEXT('Document Type')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 29'Action Code:'
     A            DDACTC         1A  O  4 42TEXT('Action Code:')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 48'Report Type:'
     A            DREPTY        18A  O  4 61TEXT('Report Type:')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 84'Year:'
     A            DCYEAR         4A  O  4 90TEXT('Calendar Year')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  6  2'Reporting Group / Pool Report'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A                                  8  4 'Action'
     A                                  8 15'Account Pool'
     A                                  8 32'Account Count'
     A                                  8 54'Pool Balance'
     A                                  8 77'Currency'
     A                                  8 90'Doc. Ref ID'
     A                                  9 15'Report Type'
      *
      **Selection Screen Footer
      *
     A          R GL000664F0                TEXT('Select Screen Footer')
     A                                      CF03
     A                                      CF05
     A                                      CF16
     A                                      CF20
     A                                      CF22
     A                                      OVERLAY
     A                                      PUTOVR

     A                                 25  2'1=Select'

     A                                 26  2'F3=Exit   F5=Refresh   +
     A                                       F16=Message Header   +
     A                                       F20=Account Report   +

     A                                       F22=Exception'
     A                                 26 81'List'

      *
      **Selection Screen Footer
      *
     A          R GL000664F1                TEXT('Select Screen Footer')
     A                                      CF03
     A                                      CF05
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A N72                             25  2'1= Select'
     A N72                             26  2'F3=Exit   F5=Refresh'
     A N72                             26 25'F12=Previous'
     A  72                             26  2'Press any key to continue +
     A                                      or F12 to cancel'
     A  72                                  DSPATR(HI)
      *
      **Selection Screen Footer
      *
     A          R GL000664F2                TEXT('Select Screen Footer')
     A                                      CF03
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A N74                             26  2'F3=Exit   F12=Previous'
     A  74                             26  2'Press any key to continue +
     A                                      or F12 to cancel'
     A  74                                  DSPATR(HI)

      *
      ** Report Pool - Include Remarks Screen
      *
     A          R GL000664C3                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            DDUSER        10A  O  1  2TEXT('Current User')
     A                                      OVRDTA
     A                                  1 13'GL000664'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('WorkStation ID')
     A                                      OVRDTA
     A                                  2 53'FATCA Reports Management'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Document Type:'
     A            DDOCTP         7A  O  4 18TEXT('Document Type')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 29'Action Code:'
     A            DDACTC         1A  O  4 42TEXT('Action Code:')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 48'Report Type:'
     A            DREPTY        18A  O  4 61TEXT('Report Type:')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 84'Year:'
     A            DCYEAR         4A  O  4 90TEXT('Calendar Year')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  6  2'Reporting Group / Pool Report'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  2'Customer Number:'
     A            DCUSNH        10A  O  7 19TEXT('Customer Number')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  7 34'Total Balance:'
     A            DTOTBL        22A     7 49TEXT('Total Customer Balance')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  7 76'Currency:'
     A            DBALCC         3A     7 86TEXT('Pool Balance (Currency Code)')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  9 02'Manual Inclusion Remarks..:'
     A            D2MIR1        64A  O  9 30TEXT('Include Remark 1')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            D2MIR2        64A  O 10 30TEXT('Include Remark 2')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            D2MIR3        64A  O 11 30TEXT('Include Remark 3')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            D2MIR4        64A  O 12 30TEXT('Include Remark 4')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A                                 13 02'Timestamp.................:'
     A            D2TMST        26A  O 13 30TEXT('Timestamp')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A                                 14 02'User......................:'
     A            D2USER        10A  O 14 30TEXT('User')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)

     A          R GL000664C4                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            DDUSER        10A  O  1  2TEXT('Current User')
     A                                      OVRDTA
     A                                  1 13'GL000664'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('WorkStation ID')
     A                                      OVRDTA
     A                                  2 53'FATCA Reports Management'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Document Type:'
     A            DDOCTP         7A  O  4 18TEXT('Document Type')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 29'Action Code:'
     A            DDACTC         1A  O  4 42TEXT('Action Code:')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 48'Report Type:'
     A            DREPTY        18A  O  4 61TEXT('Report Type:')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 84'Year:'
     A            DCYEAR         4A  O  4 90TEXT('Calendar Year')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  6  2'Reporting Group / Pool Report'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  2'Customer Number:'
     A            DCUSNH        10A  O  7 19TEXT('Customer Number')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  9 02'Manual Exclusion Remarks..:'
     A            D3MIR1        64A  B  9 30TEXT('Exclude Remark 1')
     A                                      OVRDTA
     A  56                                  DSPATR(RI PC)
     A N56                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A            D3MIR2        64A  B 10 30TEXT('Exclude Remark 2')
     A                                      OVRDTA
     A  56                                  DSPATR(RI PC)
     A N56                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A            D3MIR3        64A  B 11 30TEXT('Exclude Remark 3')
     A                                      OVRDTA
     A  56                                  DSPATR(RI PC)
     A N56                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A            D3MIR4        64A  B 12 30TEXT('Exclude Remark 4')
     A                                      OVRDTA
     A  56                                  DSPATR(RI PC)
     A N56                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 02'                           '
     A                                 14 02'                           '
      *
      **Selection Screen Footer
      *
     A          R GL000664F4                TEXT('Select Screen Footer')
     A                                      CF03
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 26  2'F3=Exit   F12=Previous'

