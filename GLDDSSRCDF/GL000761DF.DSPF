      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Reports Management')                          *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL000761DF - Midas GL Reports Management (CRS)          *
      *                                                               *
      *  (c) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(25)
     A                                      INDARA
      *
      ** Message Subfile Format
      *
     A          R GLCRMGM1                  TEXT('(c) Finastra International +
     A                                      Limited 2016')
     A                                      SFL
     A                                      SFLMSGRCD(25)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Message Subfile Control
      *
     A          R GLCRMGM0                  TEXT('MSG Subfile Control')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      SFLCTL(GLCRMGM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      ** Report Management Selection sceen subfile details
      *
     A          R GLCRMGS1                  SFL
     A                                      TEXT('Select Screen Subfile')
     A            DDACTN         1A  B 10  6
     A     95                               DSPATR(PC)
     A     95                               DSPATR(RI)
     A    N80                               DSPATR(UL)
     A     80                               DSPATR(PR)
     A            DDMSGR        21A  O 10 13
     A                                      DSPATR(HI)
     A            DDRPTY        18A  O 10 41
     A                                      DSPATR(HI)
     A            DDSTAT         4A  O 10 67
     A                                      DSPATR(HI)
     A            DDDTTM        19A  O 10 83
     A                                      DSPATR(HI)
     A            DDUSNM        10A  O 10114
     A                                      DSPATR(HI)
     A            DDAMID        21A  H
     A            DDRBR1         3A  H
     A            DDCTR1         2A  H
     A            DDSTT1         4A  H
     A            DDWAR1        50A  H
      *
      ** Report Management Selection sceen subfile control
      *
     A          R GLCRMGS0                  SFLCTL(GLCRMGS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Select Screen Control')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'GL000761'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 54'CRS Reports Management'
     A                                      DSPATR(HI UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  6'Year:'
     A            D1YEAR         4A  B  4 12OVRDTA
     A     20                               DSPATR(RI PC)
     A    N20                               DSPATR(UL HI)
     A                                      OVRATR
     A                                  6  4'Action'
     A                                  6 16'Message Ref ID'
     A                                  6 44'Report Type'
     A                                  6 66'Status'
     A                                  6 88'Date/Time'
     A                                  6114'User Name'
     A            D1MSGR        21A  B  8 13OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            D1RPTY        18A  B  8 41OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            D1STAT         4A  B  8 67OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            D1DTTM        19A  B  8 83OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            D1USNM        10A  B  8114OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
      *
      ** Report Management Details Screen
      *
     A          R GLCRMGS2                  TEXT('Detail Screen')
     A                                      CF03
     A                                      CF05
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'GL000761'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 54'CRS Reports Management'
     A                                      DSPATR(HI UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 55'Create a New Report'
     A                                      DSPATR(HI UL)
     A                                  6  2'Calendar Year:'
     A            DDCDYR         4A  B  6 17OVRDTA
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRATR
     A                                  7  2'Branch.......:'
     A            DDRBRC         3A  B  7 17OVRDTA
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRATR
     A                                  8  2'Country Code.:'
     A            DDCTRC         2A  B  8 17OVRDTA
     A                                      DSPATR(PR HI)
     A                                      OVRATR
     A                                  9  2'Status.......:'
     A            DDSTTS         4A  B  9 17OVRDTA
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI PC)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRATR
     A                                 10  2'Warning......:'
     A            DDWARN        50A  B 10 17OVRDTA
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRATR
      *
      ** Selection Screen Footer
      *
     A          R GLCRMGF1                  TEXT('Select Screen Footer')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 26  2'F3=Exit '
     A                                 26 30'F5=Refresh'
     A                                 26 61'F9=Create a New Report'
     A                                 27  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A          R GLCRMGF2                  TEXT('Select Screen Footer')
     A                                      CF03
     A                                      CF05
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 26  2'F3=Exit'
     A                                 26 34'F5=Refresh'
     A                                 26 66'F12=Cancel'
