     A*****************************************************************
 *STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL CRS Reports Management Header/FI')            *
      *****************************************************************
      *                                                               *
      *  Midas General Ledger Module                                  *
      *                                                               *
      *  DSPF/GL000762DF - Midas GL CRS Reports Management Header/FI  *
      *                                                               *
      *  (c) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. MD054905           Date 04Dec19               *
      *  Prev Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD054905 - Change Message Ref Id format                      *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
     A                                      INDARA
      *
      ** Message Subfile Format
      *
     A          R GL000762S0                TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
     A                                      SFL
     A                                      SFLMSGRCD(27)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Message Subfile Control
      *
     A          R GL000762C0                TEXT('MSG Subfile Control')
     A                                      SFLCTL(GL000762S0)
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      SFLINZ
     A  40                                  SFLDSP
     A                                      SFLDSPCTL
     A  46                                  SFLEND
     A  41                                  SFLCLR
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      ** Report Management Details - Message Header and Reporting FI
      *
     A          R GL000762F0                TEXT('Detail Screen')
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      CF20(20)
     A                                      CF22(22)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'GL000762'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 53'CRS Reports Management'
     A                                      DSPATR(HI UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  3'Document Type:'
     A            DDOCTP         7A  O  4 18OVRDTA
     A                                      DSPATR(HI)
     A                                  4 29'Action Code:'
     A            DDACTC         1A  O  4 42TEXT('Action Code')
     A                                      DSPATR(HI)
     A                                  4 48'Report Type:'
     A            DDRTYP        18A  O  4 61TEXT('Report Type')
     A                                      DSPATR(HI)
     A                                  4 84'Year:'
     A            DDRYER         4A  O  4 90TEXT('Year')
     A                                      DSPATR(HI)
     A                                 19 79'Document Ref.ID..:'
      ** Start of Change MD054905
     A**********  DDOCRF        30A  O 19 98OVRDTA                                          MD054905
     A**********                            DSPATR(HI)                                      MD054905
     A**********                       20 79'Corrected Ref.ID.:'                            MD054905
     A**********  DCORRF        30A  O 20 98OVRDTA                                          MD054905
     A**********                            DSPATR(HI)                                      MD054905
     A            DDOCRF        33A  O 19 98OVRDTA
     A                                      DSPATR(HI)
     A                                 20 79'Corrected Ref.ID.:'
     A            DCORRF        33A  O 20 98OVRDTA
     A                                      DSPATR(HI)
      ** End of Change MD054905
     A                                  6  3'Report - Message Header and +
     A                                      Reporting Financial +
     A                                      Institution'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  3'Message Header'
     A                                      DSPATR(HI UL)
     A                                  8  3'Sending Company IN+
     A                                       ........:'
     A            DSNDCI        25A  O  8 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  5  3'CRS Schema +
     A                                       Version........:'
     A            DFOECD         5A  O  5 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  9  3'Transmitting Country+
     A                                       ......:'
     A            DTRANC         2A  O  9 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 10  3'Receiving Country+
     A                                       .........:'
     A            DRECVC         2A  O 10 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 11  3'Message Type.....+
     A                                      .........:'
     A            DMSGTP        10A  O 11 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 12  3'Warning..........+
     A                                      .........:'
     A            DWARNM        50A  O 12 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 13  3'Message Reference ID+
     A                                      ......:'
     A            DMRFID        30A  O 13 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 14  3'Corrected Message Ref+
     A                                      . ID.:'
     A            DCMRID        30A  O 14 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 15  3'Reporting Period....+
     A                                      ......:'
     A            DRPTPD        10A  O 15 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 16  3'Timestamp...........+
     A                                      ......:'
     A            DTSTMP        26A  O 16 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A            DCRFIS         1A  I 18  1
     A  90                                  DSPATR(HI)
     A  90                                  DSPATR(UL)
     A N90                                  DSPATR(ND)
     A N90                                  DSPATR(PR)
     A                                 18  3'Reporting Financial +
     A                                       Institution'
     A                                      DSPATR(HI UL)
     A                                 19  3'Identification +
     A                                      Number.....:'
     A            DTINNO        25A  O 19 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 20  3'IN (Issued By).....+
     A                                      .......:'
     A            DTINIB         2A  O 20 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 21  3'Residence Country +
     A                                      Code....:'
     A            DRESCD         2A  O 21 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 22  3'Customer Name.....+
     A                                       ........:'
     A            DCNAME        70A  O 22 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 23  3'Address...........+
     A                                       ........:'
     A            DADDR1        75A  O 23 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A            DADDR2        60A  O 24 31OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
      *
      ** Selection Screen Footer - Message Header / Reporting FI
      *
     A          R GL000762F1                TEXT('Select Screen Footer')
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      CF20(20)
     A                                      CF22(22)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 26  2'F3=Exit   F5=Refresh   +
     A                                       F12=Previous   +
     A                                       F20=Account Report   +
     A                                       F22=Exception List'
     A                                      OVRATR
