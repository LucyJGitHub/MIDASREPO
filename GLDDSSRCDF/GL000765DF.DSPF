      *****************************************************************
 *STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Reports Management - Exception')              *
      *****************************************************************
      *                                                               *
      *  Midas General Ledger Module                                  *
      *                                                               *
      *  DSPF/GL000765DF - Midas GL Reports Management - Exception    *
      *                                                               *
      *  (c) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. MD040841           Date 26Aug16               *
      *  Prev Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD040841 - CRS Reports Screenflow for Actions Exclude, Cor-  *
      *             rect and Include should be adjusted               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      CHGINPDFT
     A                                      MSGLOC(27)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A          R GL000765S0                TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
     A                                      SFL
     A  34                                  SFLNXTCHG
      *
     A            DDACTN         1A  B 11  6TEXT('Action')
     A                                      DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A            DDCSNH        10A  O 11 13TEXT('Cust/NAH')
     A                                      DSPATR(HI)
     A            DDRPNM        20A  O 11 27TEXT('Report Name')
     A                                      DSPATR(HI)
     A            DDRPTN        10A  O 11 51TEXT('Report Town')
     A                                      DSPATR(HI)
     A            DDEXLT        64A  O 11 66TEXT('Exception List')
     A                                      DSPATR(HI)
      *
      **Exception Management Selection sceen subfile control
      *
     A          R GL000765C0                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      SFLCTL(GL000765S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0013)
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF16(16)
     A                                      CF20(20)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  36                                  SFLEND
     A  31                                  SFLCLR
     A N36                                  ROLLUP(98)
     A N36                                  ROLLDOWN
     A                                      OVERLAY
      *
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2TEXT('Current User')
     A                                  1 13'GL000765'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125TEXT('Midas Run Date')
     A            DDWID         10A  O  2  2TEXT('WorkStation ID')
      *
     A                                  2 53'CRS Reports Management'
     A                                      DSPATR(HI UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Document Type:'
     A                                  4 29'Action Code:'
     A                                  4 48'Report Type:'
     A                                  4 84'Year:'
     A                                  6  2'List of Customers/Non-Account +
     A                                      Holders under Exception +
     A                                      Management'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  3'Action'
     A                                  8 13'Cust/NAH'
     A                                  8 27'Report Name'
     A                                  8 51'Report Town'
     A                                  8 66'Exception List'
     A            DDOCTP         7A  O  4 17TEXT('Document Type')
     A                                      DSPATR(HI)
     A            DDACTC         1A  O  4 42TEXT('Action Code')
     A                                      DSPATR(HI)
     A            DDRTYP        18A  O  4 61TEXT('Report Type')
     A                                      DSPATR(HI)
     A            DDRYER         4A  O  4 90TEXT('Year')
     A                                      DSPATR(HI)
      **Filter fields
     A            DDCSNS        10A  B  9 13TEXT('Cust/NAH filter')
     A                                      DSPATR(UL HI)
     A            DDRPNS        20A  B  9 27TEXT('Rprt Name filter')
     A                                      DSPATR(UL HI)
     A            DDRPTS        10A  B  9 51TEXT('Rprt Town filter')
     A                                      DSPATR(UL HI)
     A            DDEXLS        64A  B  9 66TEXT('Exception List')
     A                                      DSPATR(UL HI)
      *
      **Selection Screen Footer
      *
     A          R GL000765F0                TEXT('Select Screen Footer')
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF16(16)
     A                                      CF20(20)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 25  2'1=Select'
     A                                 26  2'F3=Exit   F5=Refresh   +
     A                                       F16=Message Header   +
     A                                       F20=Account Report'
      *
      ** Exception Message Description Screen
      *
     A          R GL000765S1                TEXT('Select Screen Subfile')
     A                                      SFL
     A            DXEXMA        64A  O 12  2TEXT('Exception Description')
     A                                      DSPATR(HI)
      *
      **Exception Messages Subfile Control
      *
     A          R GL000765C1                TEXT('Select Screen Control')
     A                                      SFLCTL(GL000765S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A N78                                  CF03(03)
     A                                      CF12(12)
     A N50                                  SFLDSP
     A N50                                  SFLDSPCTL
     A  56N50                               SFLEND
     A  50                                  SFLCLR
     A N56                                  ROLLUP(68)
     A                                      OVERLAY
     A                                      PUTOVR
     A            DXSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'GL000765'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 53'CRS Reports Management'
     A                                      DSPATR(HI UL)
     A                                  4  2'Document Type:'
     A            DDOCTP         7A  O  4 17TEXT('Document Type')
     A                                      DSPATR(HI)
     A                                  4 29'Action Code:'
     A            DDACTC         1A  O  4 42TEXT('Action Code')
     A                                      DSPATR(HI)
     A                                  4 48'Report Type:'
     A            DDRTYP        18A  O  4 61TEXT('Report Type')
     A                                      DSPATR(HI)
     A                                  4 84'Year:'
     A            DDRYER         4A  O  4 90TEXT('Year')
     A                                      DSPATR(HI)
      ** Start of Change MD040841
     A**********                        6  2'List of Customers/Non-Account +                MD040841
     A**********                            Holders under Exception +                       MD040841
     A**********                            Management'                                     MD040841
     A**********                            DSPATR(HI)                                      MD040841
     A**********                            DSPATR(UL)                                      MD040841
      ** End of Change MD040841
     A                                  8  2'Cust/NAH:'
     A            DXCSNH        10A  O  8 12TEXT('Customer/Non-Account Hldr')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  8 27'Report Name:'
     A            DXRPNM        20A  O  8 40TEXT('Report Name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  8 65'Report Town:'
     A            DXRPTN        10A  O  8 78TEXT('Report Town')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  8 93'User:'
     A            DXUSER        10A  O  8 99TEXT('User')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'Timestamp:'
     A            DXTMST        19A  O  9 13TEXT('Timestamp')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N77                             11  2'List of Exceptions:'
     A N77                                  OVRATR
      ** Start of Change MD040841
     A**77******                       11  2'Remarks:           '                           MD040841
     A  77                             11  2'Remarks of Manually Excluded +
     A                                      Account Holder:'
      ** End of Change MD040841
     A  77                                  OVRATR
      *
      **Selection Screen Footer
      *
     A          R GL000765F1                TEXT('Select Screen Footer')
     A N78                                  CF03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PUTOVR
     A N78                             26  2'F3=Exit   F12=Previous'
      **Start of change MD040841
     A**78******                       26  2'Press any key to continue +                    MD040841
     A**********                            or F12 to cancel'                               MD040841
      **End of change MD040841
     A  78                                  DSPATR(HI)
      *
      **Message Subfile
      *
     A          R GL000765M1                SFL
     A                                      SFLMSGRCD(27)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R GL000765M0                TEXT('Message Subfile Control')
     A                                      SFLCTL(GL000765M1)
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      SFLINZ
     A  40                                  SFLDSP
     A                                      SFLDSPCTL
     A  46                                  SFLEND
     A  41                                  SFLCLR
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A            DDPGMQ                    SFLPGMQ
      *
      ** Exception Messages - Include Remarks Screen
      *
     A          R GL000765C2                TEXT('Select Screen Control')
     A N78                                  CF03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'GL000765'
     A                                  1118'Date:'
     A            BJMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 53'CRS Reports Management'
     A                                      DSPATR(HI UL)
     A                                  4  2'Document Type:'
     A            DDOCTP         7A  O  4 17TEXT('Document Type')
     A                                      DSPATR(HI)
     A                                  4 29'Action Code:'
     A            DDACTC         1A  O  4 42TEXT('Action Code')
     A                                      DSPATR(HI)
     A                                  4 48'Report Type:'
     A            DDRTYP        18A  O  4 61TEXT('Report Type')
     A                                      DSPATR(HI)
     A                                  4 84'Year:'
     A            DDRYER         4A  O  4 90TEXT('Year')
     A                                      DSPATR(HI)
      ** Start of change MD040841
     A**********                        6  2'List of Customers/Non-Account +                MD040841
     A**********                            Holders under Exception +                       MD040841
     A**********                            Management'                                     MD040841
     A**********                            DSPATR(HI)                                      MD040841
     A**********                            DSPATR(UL)                                      MD040841
      ** End of change MD040841
     A                                  8  2'Cust/NAH:'
     A            DXCSNH        10A  O  8 12TEXT('Customer/Non-Account Hldr')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  8 27'Report Name:'
     A            DXRPNM        20A  O  8 40TEXT('Report Name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  8 65'Report Town:'
     A            DXRPTN        10A  O  8 78TEXT('Report Town')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  8 93'User:'
     A            DXUSER        10A  O  8 99TEXT('User')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'Timestamp:'
     A            DXTMST        19A  O  9 13TEXT('Timestamp')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 11 02'Remarks...................:'
     A            D2MIR1        64A  B 11 30
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            D2MIR2        64A  B 12 30
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            D2MIR3        64A  B 13 30
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            D2MIR4        64A  B 14 30
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      ** Start of Change MD040841
      *
      ** Selection Screen Footer - Reporting Group - Account Balance
      *
     A          R GL000765F2                TEXT('Select Screen Footer')
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PUTOVR
     A  51                             26  2'Account Holder will be -
     A                                      selected for exclusion. -
     A                                      Press Enter to proceed'
     A  51                                  DSPATR(HI)
     A  52                             26  2'Account Holder will be -
     A                                      selected for inclusion. -
     A                                      Press Enter to proceed'
     A  52                                  DSPATR(HI)
     A  53                             26  2'Account Holder will be -
     A                                      selected for correction. -
     A                                      Press Enter to proceed'
     A  53                                  DSPATR(HI)
     A                                 26 72' or F12 to cancel'
     A                                      DSPATR(HI)
      ** End of Change MD040841
