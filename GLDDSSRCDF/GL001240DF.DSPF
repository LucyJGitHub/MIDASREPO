/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Accounts Risk Weighted Asset Codes Maint.')
     A*****************************************************************
     A*                                                               *
     A*  Midas - GL General Ledger                                    *
     A*                                                               *
     A*  GL001240DF - Account Risk Weighted Asset Codes Maintenance   *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2002            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. CGL018  *CREATE    Date 28Sep02               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CGL018 - Risk Weighted Assets                                *
     A*           (TO COMPILE USE OPTION: RSTDSP = *YES)              *
     A*                                                               *
     A*****************************************************************
     A**Start of change CGL029
     A**********                            DSPSIZ(24 80 *DS3)                                CGL029
     A                                      DSPSIZ(*DS4)
     A**End of change CGL029
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03(03 'Main Menu')
      *
      * --------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                      *
      * --------------------------------------------------------------*
      *
     A          R GL001240S2                TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * --------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                     *
      * --------------------------------------------------------------*
      *
     A          R GL001240C2                TEXT('MSG SUBFILE CONTRL')
     A  77                                  CF09(09 'Add Mode')
     A                                      CF12(12 'Previous')
     A                                      SFLCTL(GL001240S2)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  90                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * --------------------------------------------------------------*
      * TRANSACTION NUMBER SUBFILE                                    *
      * --------------------------------------------------------------*
      *
     A          R GL001240S1                SFL
     A                                      TEXT('SELECT SCR. SUBFILE')
     A            DDOPT          1A  B  8  4TEXT('Action')
     A  77                                  DSPATR(UL)
     A  92                                  DSPATR(RI)
     A N77                                  DSPATR(PR)
     A  92                                  DSPATR(PC)
     A            DDCODE         2A  O  8 14TEXT('RWA Code')
     A                                      DSPATR(HI)
     A            DDSETN         1A  O  8 25TEXT('RWA Set')
     A                                      DSPATR(HI)
     A*
     A* --------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                          *
     A* --------------------------------------------------------------*
     A*
     A          R GL001240C1                SFLCTL(GL001240S1)
     A                                      SFLSIZ(9999)
     A**Start of change CGL029
     A**********                            SFLPAG(0012)                                      CGL029
     A                                      SFLPAG(0015)
     A**End of change CGL029
     A                                      TEXT('SELECT SCR. CONTROL')
     A                                      CF05(05 'Refresh')
     A  77                                  CF09(09 'Add Mode')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  97                                  SFLCLR
     A  91                                  SFLEND
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                      TEXT('User ID')
     A                                  1 13'GL001240S1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A                                      TEXT('Run date')
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 16'Account Risk Weighted Assets Codes-
     A                                       Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Branch:'
     A            #1BRCA         3A  O  4 10DSPATR(HI)
     A                                      TEXT('Branch')
     A                                  4 15'Customer:'
     A            #1CUST         6A  O  4 25DSPATR(HI)
     A                                      TEXT('Customer No.')
     A                                  4 32'A/c code:'
     A**Start of change CGL029
     A**********  #1ACOD         4A  O  4 42DSPATR(HI)                                        CGL029
     A            #1ACOD        10A  O  4 42DSPATR(HI)
     A**End of change CGL029
     A                                      TEXT('Account Code')
     A**Start of change CGL029
     A**********                        4 47'A/s seq:'                                        CGL029
     A**********  #1ACSQ         2A  O  4 56DSPATR(HI)                                        CGL029
     A                                  4 53'A/s seq:'
     A            #1ACSQ         2A  O  4 62DSPATR(HI)
     A**End of change CGL029
     A                                      TEXT('Account seq')
     A**Start of change CGL029
     A**********                        4 59'RE number:'                                      CGL029
     A**********  #1RETL        10A  O  4 70DSPATR(HI)                                        CGL029
     A                                  4 65'RE number:'
     A            #1RETL        10A  O  4 76DSPATR(HI)
     A**End of change CGL029
     A                                      TEXT('Retail Number')
     A                                  6  2'Action'
     A                                  6 22'RWA Set'
     A                                  6 11'RWA code'
      *
      * -------------------------------------------------------------------*
      * ADD NEW RECORD                                                     *
      * -------------------------------------------------------------------*
      *
     A          R GL001240M1
     A                                      TEXT('ADD SCREEN')
     A  21                                  CF10(10 'Confirm Delete')
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2OVRDTA
     A                                      TEXT('User ID')
     A                                  1 13'GL001240M1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A                                      TEXT('Run date')
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A  21                              2 18'Delete Accounts Risk Weighted -
     A                                      Assets Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  22                              2 19'Amend Accounts Risk Weighted -
     A                                      Assets Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  23                              2 18'Enquiry Accounts Risk Weighted -
     A                                      Assets Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  24                              2 20'New Accounts Risk Weighted Assets -
     A                                      Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  2'Branch:'
     A            #1BRCA         3A  O  4 10DSPATR(HI)
     A                                      TEXT('Branch')
     A                                  4 15'Customer:'
     A            #1CUST         6A  O  4 25DSPATR(HI)
     A                                      TEXT('Customer No.')
     A                                  4 32'A/c code:'
     A**Start of change CGL029
     A**********  #1ACOD         4A  O  4 42DSPATR(HI)                                        CGL029
     A            #1ACOD        10A  O  4 42DSPATR(HI)
     A**End of change CGL029
     A                                      TEXT('Account Code')
     A**Start of change CGL029
     A**********                        4 47'A/s seq:'                                        CGL029
     A**********  #1ACSQ         2A  O  4 56DSPATR(HI)                                        CGL029
     A                                  4 53'A/s seq:'
     A            #1ACSQ         2A  O  4 62DSPATR(HI)
     A**End of change CGL029
     A                                      TEXT('Account seq')
     A**Start of change CGL029
     A**********                        4 59'RE number:'                                      CGL029
     A**********  #1RETL        10A  O  4 70DSPATR(HI)                                        CGL029
     A                                  4 65'RE number:'
     A            #1RETL        10A  O  4 76DSPATR(HI)
     A**End of change CGL029
     A                                      TEXT('Retail Number')
     A                                  2 66'Time: TT:TT:TT'
     A                                  6  2'RWA'
     A                                  6  6'Code:'
     A            DDCODE         2A  B  6 13TEXT('RWA Code')
     A                                      OVRATR
     A                                      OVRDTA
     A  21
     AO 23                                  DSPATR(PR)
     A
     A N83                                  DSPATR(HI)
     A  83                                  DSPATR(RI)
     A  83                                  DSPATR(PC)
     A  24
     AO 22                                  DSPATR(UL)
     A                                  6 19'RWA Set:'
     A            DDSETN         1   B  6 29TEXT('RWA Set')
     A                                      OVRATR
     A                                      OVRDTA
     A  21
     AO 23                                  DSPATR(PR)
     A N82                                  DSPATR(HI)
     A  82                                  DSPATR(RI)
     A  82                                  DSPATR(PC)
     A  24
     AO 22                                  DSPATR(UL)
     A**Start of change CGL029
     A**21******                       23  2'F3=Main Menu F10=Delete-                         CGL029
     A**********                             F12=Previous'                                    CGL029
     A*N21******                       23  2'F3=Main Menu F12=Previous'                       CGL029
     A  21                             27  2'F3=Main Menu F10=Delete-
     A                                       F12=Previous'
     A N21                             27  2'F3=Main Menu F12=Previous'
     A**End of change CGL029
      *
      * -------------------------------------------------------------------*
      * FOOTER                                                             *
      * -------------------------------------------------------------------*
      *
     A          R GL001240F1
     A                                      CF05(05 'Refresh')
     A  77                                  CF09(09 'Add Mode')
     A                                      TEXT('Footer')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A**Start of change CGL029
     A*N77******                       23  2'F3=Main Menu F5=Refresh'                         CGL029
     A**77******                       23  2'D=Delete F3=Main Menu F5=Refresh-                CGL029
     A**********                              F9=Go to ''Add'' Mode'                          CGL029
     A N77                             27  2'F3=Main Menu F5=Refresh'
     A  77                             27  2'D=Delete F3=Main Menu F5=Refresh-
     A                                        F9=Go to ''Add'' Mode'
     A**End of change CGL029
     A* -------------------------------------------------------------------*
     A* * NO RECORD SCREEN                                                 *
     A* -------------------------------------------------------------------*
     A*
     A          R GL001240N1                TEXT('No record Screen')
     A                                      CF05(05 'Refresh')
     A  77                                  CF09(09 'Add Mode')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                      TEXT('User ID')
     A                                  1 13'GL001240S1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A                                      TEXT('Run date')
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 16'Account Risk Weighted Assets Codes-
     A                                       Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Branch:'
     A            #1BRCA         3A  O  4 10DSPATR(HI)
     A                                      TEXT('Branch')
     A                                  4 15'Customer:'
     A            #1CUST         6A  O  4 25DSPATR(HI)
     A                                      TEXT('Customer No.')
     A                                  4 32'A/c code:'
     A**Start of change CGL029
     A**********  #1ACOD         4A  O  4 42DSPATR(HI)                                        CGL029
     A            #1ACOD        10A  O  4 42DSPATR(HI)
     A**End of change CGL029
     A                                      TEXT('Account Code')
     A**Start of change CGL029
     A**********                        4 47'A/s seq:'                                        CGL029
     A**********  #1ACSQ         2A  O  4 56DSPATR(HI)                                        CGL029
     A                                  4 53'A/s seq:'
     A            #1ACSQ         2A  O  4 62DSPATR(HI)
     A**End of change CGL029
     A                                      TEXT('Account seq')
     A**Start of change CGL029
     A**********                        4 59'RE number:'                                      CGL029
     A**********  #1RETL        10A  O  4 70DSPATR(HI)                                        CGL029
     A                                  4 65'RE number:'
     A            #1RETL        10A  O  4 76DSPATR(HI)
     A**End of change CGL029
     A                                      TEXT('Retail Number')
     A                                  6  2'No records found'
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A*N77******                       23  2'F3=Main Menu F5=Refresh'                         CGL029
     A**77******                       23  2'F3=Main Menu F5=Refresh-                         CGL029
     A**********                              F9=Go to ''Add'' Mode'                          CGL029
     A N77                             27  2'F3=Main Menu F5=Refresh'
     A  77                             27  2'F3=Main Menu F5=Refresh-
     A                                        F9=Go to ''Add'' Mode'
     A**End of change CGL029
