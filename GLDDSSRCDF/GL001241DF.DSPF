/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Collateral Risk Weighted Asset Codes Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - GL General Ledger                                    *
     A*                                                               *
     A*  GL001241DF - Collateral Risk Weighted Asset Codes Maintenance*
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2002            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*  Last Amend No. CGL018  *CREATE    Date 18Sep02               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CGL018 - Risk Weighted Assets                                *
     A*           (TO COMPILE USE OPTION: RSTDSP = *YES)              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R GL1241M1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R GL1241M0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A N30                                  CF09
     A                                      CF12
     A                                      SFLCTL(GL1241M1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  91                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A* TRANSACTION NUMBER SUBFILE
     A* -------------------------------------------------------------------*
     A*
     A          R GL1241S1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  9  4TEXT('Action')
     A  32                                  DSPATR(ND)
     A  32                                  DSPATR(PR)
     A N32                                  DSPATR(UL)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A            DDCODE         2   O  9 14TEXT('RWA Code')
     A            DDSETN         1   O  9 27TEXT('RWA Set')
     A*
     A* -------------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                               *
     A* -------------------------------------------------------------------*
     A*
     A          R GL1241S0                  SFLCTL(GL1241S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0010)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A N30                                  CF09
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  97                                  SFLCLR
     A  25N97                               SFLEND(*MORE)
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2TEXT('Midas User')
     A                                      OVRDTA
     A                                  1 13'GL001241S1'
     A            BJMRDT         7A  O  1 73TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 18'Collateral Risk Weighted Assets Co-
     A                                      des Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Collateral:'
     A            DDCREF        10A  O  4 14TEXT('Customer')
     A            DDDESC        30A  O  4 41TEXT('Coll. Description')
     A                                  6  2'Action'
     A                                  6 24'RWA Set'
     A                                  6 11'RWA Code'
     A                                  4 28'Description:'
     A*
     A* -------------------------------------------------------------------*
     A* ADDING NEW RECORD                                                  *
     A* -------------------------------------------------------------------*
     A*
     A          R GL1241A1
     A                                      TEXT('ADD SCREEN')
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2TEXT('Midas User')
     A                                      OVRDTA
     A                                  1 13'GL001241M1'
     A            BJMRDT         7A  O  1 73TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A  27                              2 23'New Collateral Risk Weighted Asset-
     A                                      s Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  24                              2 18'Delete Collateral Risk Weighted As-
     A                                      sets Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Collateral:'
     A            DDCREF        10A  O  4 14TEXT('Customer')
     A                                  4 28'Description:'
     A            DDDESC        30A  O  4 41TEXT('Coll. Description')
     A                                  6  2'RWA Code:'
     A            DDCODE         2A  B  6 12TEXT('RWA Code')
     A                                      OVRDTA
     A  27                                  DSPATR(UL)
     A  83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A  22
     AO 29
     AO 24                                  DSPATR(PR)
     A                                  6 19'RWA Set:'
     A            DDSETN         1A  B  6 28TEXT('RWA Set')
     A                                      OVRDTA
     A  27                                  DSPATR(UL)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A  22
     AO 29
     AO 24                                  DSPATR(PR)
     A N24                             23  2'F3=Main Menu   F12=Previous       '
     A  24                             23  2'F3=Main Menu   F12=Previous   F10=-
     A                                      Delete'
      *
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R GL1241F1
     A                                      TEXT('FOOTER')
     A                                      CF03
     A N30                                  CF09
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A  79                             23  2'F3=Main Menu   F12=Previous'
     A N79                             23  2'D=Delete   F3=Main Menu   F5=Refre+
     A                                      sh   F9=Go to ''Add'' Mode   F12=Pr+
     A                                      evious'
     A*
     A* -------------------------------------------------------------------*
     A* * NO RECORD SCREEN                                                 *
     A* -------------------------------------------------------------------*
     A*
     A          R GL1241N1
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A N30                                  CF09
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'GL001241S1'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 18'Collateral Risk Weighted Assets Co-
     A                                      des Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Collateral:'
     A            DDCREF        10A  O  4 14
     A                                  4 28'Description:'
     A            DDDESC        30A  O  4 41
     A                                  6  2'Action'
     A                                  6 11'RWA Code'
     A                                  6 24'RWA Set'
