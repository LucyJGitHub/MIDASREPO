/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Netting Groups Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - GL General Ledger                                    *
     A*                                                               *
     A*  GL001242DF - Netting Groups Maintenance                      *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2002            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. CGL018  *CREATE    Date 18Sep02               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CGL018 - Risk Weighted Assets                                *
     A*           (TO COMPILE USE OPTION: RSTDSP = *YES)              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R GL1242M1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R GL1242M0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      SFLCTL(GL1242M1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  91                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A* TRANSACTION NUMBER SUBFILE
     A* -------------------------------------------------------------------*
     A*
     A          R GL1242S1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B 13  2TEXT('Action')
     A N41                                  DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********  DDACC2        22   O 13  5TEXT('Account number')                            CGL029
     A            DDACC2        28   O 13  5TEXT('Account number')
     A**End of change CGL029
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********  DDRET2        10   O 13 29TEXT('Retail a/c number')                         CGL029
     A            DDRET2        10   O 13 35TEXT('Retail a/c number')
     A**End of change CGL029
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********  DDACN2        20   O 13 41TEXT('Account Name')                              CGL029
     A            DDACN2        20   O 13 47TEXT('Account Name')
     A**End of change CGL029
     A                                      COLOR(WHT)
     A            DDYN2B         1A  H      TEXT('Y/N Indicator-Backup Value')
     A            DDPS2          1A  O 13 76TEXT('Primary/Secondary')
     A                                      COLOR(WHT)
     A            DDPS2B         1A  H      TEXT('Primary/Secondary-Backup Valu+
     A                                      e')
     A*
     A* -------------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                               *
     A* -------------------------------------------------------------------*
     A*
     A          R GL1242S0                  SFLCTL(GL1242S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0008)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A N27                                  CF09
     A  48                                  CF10
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  97                                  SFLCLR
     A  02                                  SFLEND
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2TEXT('Midas User')
     A                                      OVRDTA
     A                                  1 13'GL001242S1'
     A            BJMRDT         7A  O  1 73TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 28'Netting Groups Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Primary Account'
     A                                      DSPATR(UL)
     A                                  5  2'Branch'
     A            DDBRCS         3   O  5  9TEXT('Branch')
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********                        5 16'Customer number'                                 CGL029
     A**********  DDCUSS         6A  O  5 32TEXT('Customer number')                           CGL029
     A                                  5 15'Customer number'
     A            DDCUSS         6A  O  5 31TEXT('Customer number')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********                        5 41'Currency'                                        CGL029
     A**********  DDCCYS         3A  O  5 50TEXT('Currency')                                  CGL029
     A                                  5 39'Currency'
     A            DDCCYS         3A  O  5 48TEXT('Currency')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********                        5 57'A/c code'                                        CGL029
     A**********  DDACOS         4A  O  5 66TEXT('Account code')                              CGL029
     A                                  5 53'A/c code'
     A            DDACOS        10A  O  5 62TEXT('Account code')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A                                  5 74'Seq'
     A            DDSEQS         2A  O  5 78TEXT('Account sequence')
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A                                  6  2'Retail Number'
     A            DDRETS        10A  O  6 16TEXT('Retail number')
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A            DDCONF        17A  O  6 57TEXT('Confirmed/Unconfirmed')
     A                                      OVRDTA
     A                                  8  2'Secondary Accounts'
     A                                      DSPATR(UL)
     A**Start of change CGL029
     A**********                       10  8'Account Number'                                  CGL029
     A**********                       10 31'Retail'                                          CGL029
     A**********                       10 45'Account name'                                    CGL029
     A                                 10 12'Account Number'
     A                                 10 37'Retail'
     A                                 10 51'Account name'
     A**End of change CGL029
     A                                 10 73'Pri/Sec'
     A**Start of change CGL029
     A**********                       11 31'Number'                                          CGL029
     A                                 11 37'Number'
     A**End of change CGL029
     A* -------------------------------------------------------------------*
     A* FIRST SCREEN
     A* -------------------------------------------------------------------*
     A          R GL1242D1
     A                                      TEXT('ADD SCREEN')
     A                                      CF03
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2TEXT('Midas User')
     A                                      OVRDTA
     A                                  1 13'GL001242D1'
     A            BJMRDT         7A  O  1 73TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 28'Netting Groups Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Action'
     A            DDACOD         1A  B  4 10TEXT('Action Code')
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A                                  6  2'Branch'
     A                                  6 10'Cust No'
     A                                  6 19'Ccy'
     A**Start of change CGL029
     A**********                        6 24'Account'                                         CGL029
     A**********                        6 33'Seq'                                             CGL029
     A**********                        6 40'or'                                              CGL029
     A**********                        6 47'Retail No'                                       CGL029
     A                                  6 25'Account'
     A                                  6 39'Seq'
     A                                  6 46'or'
     A                                  6 53'Retail No'
     A**End of change CGL029
     A                                  6 72'All/RE'
     A                                  7 25'Code'
     A            DDBRCI         3A  B  8  3TEXT('Branch')
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A            DDCUSI         6A  B  8 10TEXT('Customer')
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A  31                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A            DDCCYI         3A  B  8 19TEXT('Currency')
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A  31                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********  DDACOI         4A  B  8 25TEXT('Account Code')                              CGL029
     A            DDACOI        10A  B  8 25TEXT('Account Code')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A  31                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********  DDSEQI         2A  B  8 33TEXT('Account Sequence')                          CGL029
     A            DDSEQI         2A  B  8 39TEXT('Account Sequence')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A  31                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********  DDRETI        10A  B  8 47TEXT('Retail')                                    CGL029
     A            DDRETI        10A  B  8 53TEXT('Retail')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A                                      COLOR(WHT)
     A            DDALRI         3A  B  8 73TEXT('All/RE')
     A                                      OVRDTA
     A N38                                  DSPATR(UL)
     A  38                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                      COLOR(WHT)
     A                                 12  2'Review from'
     A            DDBRCR         3A  B 14  3TEXT('Branch')
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A            DDCUSR         6A  B 14 10TEXT('Customer')
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A  33                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A            DDCCYR         3A  B 14 19TEXT('Currency')
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A  33                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********  DDACOR         4A  B 14 25TEXT('Account Code')                              CGL029
     A            DDACOR        10A  B 14 25TEXT('Account Code')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A  33                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********  DDSEQR         2A  B 14 33TEXT('Account Sequence')                          CGL029
     A            DDSEQR         2A  B 14 39TEXT('Account Sequence')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A  33                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********  DDRETR        10A  B 14 47TEXT('Retail')                                    CGL029
     A            DDRETR        10A  B 14 53TEXT('Retail')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A                                      COLOR(WHT)
     A                                 23  2'I=Insert  A=Amend  E=Enquiry  D=De-
     A                                      lete  F3=Main Menu'
     A*
     A* -------------------------------------------------------------------*
     A* SELECT SCREEN FOOTER                                               *
     A* -------------------------------------------------------------------*
     A*
     A          R GL1242F1
     A                                      TEXT('FOOTER')
     A                                      CF03
     A N27                                  CF09
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A                                      OVRATR
     A                                 23  2'Y=Include  N=Exclude'
     A  42
     AO 43                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 24'P=Primary'
     A  42
     AO 50
     AO 43                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 35'F3=Main Menu  F12=Previous'
     A                                 23 63'F9=Goto ''Add'' Mode'
     A  27
     AO 42
     AO 43                                  DSPATR(ND)
     A                                      OVRATR
     A* -------------------------------------------------------------------*
     A* SECOND SUBFILE
     A* -------------------------------------------------------------------*
     A*
     A          R GL1242S3                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  8  3TEXT('Action')
     A                                      DSPATR(UL)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********  DDACCS        22   O  8  7TEXT('SFL field - Full Account')                  CGL029
     A            DDACCS        28   O  8  7TEXT('SFL field - Full Account')
     A**End of change CGL029
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********  DDRETS        10   O  8 33TEXT('SFL field - Retail')                        CGL029
     A            DDRETS        10   O  8 39TEXT('SFL field - Retail')
     A**End of change CGL029
     A                                      COLOR(WHT)
     A*
     A* -------------------------------------------------------------------*
     A* SECOND SUBFILE CONTROL
     A* -------------------------------------------------------------------*
     A*
     A          R GL1242S2                  SFLCTL(GL1242S3)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0010)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  97                                  SFLCLR
     A  02                                  SFLEND
     A            DDSFR2         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2TEXT('Midas User')
     A                                      OVRDTA
     A                                  1 13'GL001242S1'
     A            BJMRDT         7A  O  1 73TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 28'Netting Groups Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4 26'Review from'
     A**Start of change CGL029
     A**********  DDACCR        18A  B  4 39TEXT('Review from')                               CGL029
     A            DDACCR        24A  B  4 39TEXT('Review from')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6  2'Sel'
     A                                  6  7'Primary Account Number'
     A**Start of change CGL029
     A**********                        6 33'Retail Number'                                   CGL029
     A                                  6 39'Retail Number'
     A**End of change CGL029
     A*
     A* -------------------------------------------------------------------*
     A* FOOTER 2                                                           *
     A* -------------------------------------------------------------------*
     A*
     A          R GL1242F2
     A                                      TEXT('FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A                                      OVRATR
     A                                 23  2'F3=Exit   F12=Cancel   1=Select re+
     A                                      quest'
     A*
     A* -------------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                               *
     A* -------------------------------------------------------------------*
     A*
     A          R GL1242N0
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A N27                                  CF09
     A  48                                  CF10
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2TEXT('Midas User')
     A                                      OVRDTA
     A                                  1 13'GL001242S1'
     A            BJMRDT         7A  O  1 73TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 28'Netting Groups Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Primary Account'
     A                                      DSPATR(UL)
     A                                  5  2'Branch'
     A            DDBRCS         3A  O  5  9TEXT('Branch')
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********                        5 16'Customer number'                                 CGL029
     A**********  DDCUSS         6A  O  5 32TEXT('Customer number')                           CGL029
     A                                  5 15'Customer number'
     A            DDCUSS         6A  O  5 31TEXT('Customer number')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********                        5 41'Currency'                                        CGL029
     A**********  DDCCYS         3A  O  5 50TEXT('Currency')                                  CGL029
     A                                  5 39'Currency'
     A            DDCCYS         3A  O  5 48TEXT('Currency')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**********                        5 57'A/c code'                                        CGL029
     A**********  DDACOS         4A  O  5 66TEXT('Account code')                              CGL029
     A                                  5 53'A/c code'
     A            DDACOS        10A  O  5 62TEXT('Account code')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A                                  5 74'Seq'
     A            DDSEQS         2A  O  5 78TEXT('Account sequence')
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A                                  6  2'Retail Number'
     A            DDRETS        10A  O  6 16TEXT('Retail number')
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A            DDCONF        17A  O  6 57TEXT('Confirmed/Unconfirmed')
     A                                      OVRDTA
     A                                  8  2'Secondary Accounts'
     A                                      DSPATR(UL)
     A                                 10  8'Account Number'
     A                                 10 31'Retail'
     A                                 10 45'Account name'
     A                                 10 73'Pri/Sec'
     A                                 11 31'Number'
     A                                 22  2'No Data to Display'
     A                                      COLOR(WHT)
     A                                      OVRATR
     A                                 23  2'Y=Include  N=Exclude'
     A  42
     AO 43                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 24'P=Primary'
     A  42
     AO 50
     AO 43                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 35'F3=Main Menu  F12=Previous'
     A                                 23 63'F9=Goto ''Add'' Mode'
     A  27
     AO 42
     AO 43                                  DSPATR(ND)
     A                                      OVRATR
     A*
     A* -------------------------------------------------------------------*
     A* SECOND NO RECORD SCREEN
     A* -------------------------------------------------------------------*
     A*
     A          R GL1242N2
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2TEXT('Midas User')
     A                                      OVRDTA
     A                                  1 13'GL001242S1'
     A            BJMRDT         7A  O  1 73TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 28'Netting Groups Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4 26'Review from'
     A**Start of change CGL029
     A**********  DDACCR        18A  B  4 39TEXT('Review from')                               CGL029
     A            DDACCR        24A  B  4 39TEXT('Review from')
     A**End of change CGL029
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6  2'Sel'
     A                                  6  7'Primary Account Number'
     A**Start of change CGL029
     A**********                        6 33'Retail Number'                                   CGL029
     A                                  6 39'Retail Number'
     A**End of change CGL029
     A                                 22  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A                                      OVRATR
     A                                 23  2'F3=Exit   F12=Cancel   1=Select re-
     A                                      quest'
