      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Account Enquiry Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL002000DF - Account Enquiry Prompt                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BG1769             Date 23May03               *
      *  Prev Amend No. CGP004  *CREATE    Date 25May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BG1769 - Complete 10 digit account code changes.             *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF12
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN SCREEN                                                      *
      * -------------------------------------------------------------------*
      *
     A          R MAIN                      TEXT('MAIN SCREEN')
     A                                      RTNDTA
     A                                                                                    /*BG1769*/
     A                                      OVERLAY
     A                                                                                    /*BG1769*/
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'GL002000'
     A                                  1 32'ACCOUNT ENQUIRY PROMPT'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  5  2'Branch..................:'
     A            DDBRCA         3A  B  5 28
     A N41                                  DSPATR(UL)
     A  41                                  DSPATR(PC HI RI)
     A                                      OVRDTA
     A                                  6  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                  8  2'S E Q U E N C E'
     A                                      DSPATR(UL)
     A                                 10  2'Currency, customer .....:'
     A            DDENQ1         1A  B 10 28
     A N42                                  DSPATR(UL)
     A  42                                  DSPATR(PC HI RI)
     A                                      OVRDTA
     A                                 12  2'Currency, account code .:'
     A            DDENQ2         1A  B 12 28DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'Customer, currency .....:'
     A            DDENQ3         1A  B 14 28DSPATR(UL)
     A                                      OVRDTA
     A                                 14 30'selecting retail accounts only .:'
     A            DDRET3         1A  B 14 64DSPATR(UL)
     A                                      OVRDTA
     A                                 16  2'Retail account number ..:'
     A            DDENQ4         1A  B 16 28DSPATR(UL)
     A                                      OVRDTA
     A                                 17  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 20  2'Omit closed accounts ...........-
     A                                      .....:'
     A            DDOMCL         1A  B 20 43
     A N43                                  DSPATR(UL)
     A  43                                  DSPATR(PC HI RI)
     A                                      OVRDTA
     A                                 21  2'Omit zero balances with no activit-
     A                                      y for:'
     A            DDACMN         2A  B 21 43
     A N44                                  DSPATR(UL)
     A  44                                  DSPATR(PC HI RI)
     A                                      OVRDTA
     A                                 21 46'months'
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                                                                    /*BG1769*/
     A                                      OVERLAY
     A                                                                                    /*BG1769*/
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit F12=Abandon               '
     A                                      OVRATR
