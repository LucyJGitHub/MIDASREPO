      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Account Enquiry - By Currency, A/c Code')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL002002DF - Account Enquiry - By Currency, A/c Code         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 29Nov05               *
      *  Prev Amend No. CGL029             Date 29Mar04               *
      *                 CGP004  *CREATE    Date 25May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - 10 digit account code changes.                      *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF12
     A                                      CF16
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A            DDACTN         1A  I  6  2
     A N65                                  DSPATR(HI UL)
     A  65                                  DSPATR(RI PC)
     A            CCY            3A  O  6  4DSPATR(HI)
      * Start of change CGL029
     A**********  ACOD           4S 0O  6  8DSPATR(HI)                                        CGL029
     A            ACOD          10S 0O  6  8DSPATR(HI)
      * End of change CGL029
      * Begin /CSD027/
     A**********  CNUM           6S 0O  6 19DSPATR(HI)                                        CSD027
     A            CNUM           6A  O  6 19DSPATR(HI)
      * End /CSD027/
     A            ACSQ           2S 0O  6 26DSPATR(HI)
     A            DDACIN        52A  O  6 29DSPATR(HI)
     A            BRCA           3A  H
     A            A5ACTY         1A  H
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'GL002002'
     A                                  1 30'ACCOUNTS - BY CURRENCY, A/C CODE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  2 32'BRANCH:'
     A            I#BRCA         3   O  2 40DSPATR(HI)
     A                                      OVRDTA
     A                                  3  4'Ccy'
     A                                  3  8'A/c code'
     A                                  3 19'Cust'
     A                                  3 26'Sq'
     A            DDNARR        52A     3 29
     A                                      OVRDTA
     A            DDCCY          3A  B  4  4DSPATR(HI UL PC)
     A                                      OVRDTA
      * Start of change CGL029
     A**********  DDACOD         4A  B  4  8DSPATR(HI UL PC)                                  CGL029
     A            DDACOD        10A  B  4  8DSPATR(HI UL PC)
      * End of change CGL029
     A                                      OVRDTA
     A            DDCNUM         6A  B  4 19DSPATR(HI UL PC)
     A                                      OVRDTA
     A            DDACSQ         2A  B  4 26DSPATR(HI UL PC)
     A                                      OVRDTA
     A                                  5  2'                                  -
     A                                                                         -
     A                                                '
     A                                      DSPATR(UL)
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit F12=Abandon F16=Toggle A/c-
     A                                       Information     Rollup  Rolldown'
     A                                      OVRATR
