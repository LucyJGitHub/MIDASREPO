      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Available Balance Enquiry Display File')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL002020DF - Available Balance Enquiry Display File     *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG5334  *CREATE   Date 10Sep04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG5334 - Retail Account Balance Check. Fix for CRE010.      *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R GL002020D0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRATR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'GL002020F1'
     A                                  1 31'Available Balance Enquiry'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            BJMRDT    R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  4  2'Account'
     A            DDBRCA         3   O  4 18DSPATR(HI)
     A                                      OVRDTA
     A            DDCNUM         6   O  4 22DSPATR(HI)
     A                                      OVRDTA
     A            DDCCY          3   O  4 29DSPATR(HI)
     A                                      OVRDTA
     A            DDACOD        10   O  4 33DSPATR(HI)
     A                                      OVRDTA
     A            DDACSQ         2   O  4 44DSPATR(HI)
     A                                      OVRDTA
     A            DDANAM        20   O  4 47DSPATR(HI)
     A                                      OVRDTA
      *
     A            DDCLBL        21   O  8 23DSPATR(HI)
     A                                      OVRDTA
     A                                  8 50'Cleared Balance'
     A                                  9 15'-'
     A            DDHELD        21   O  9 23DSPATR(HI)
     A                                      OVRDTA
     A                                  9 50'Held Items'
     A                                 10 15'-'
     A            DDBCOA        21   O 10 23DSPATR(HI)
     A                                      OVRDTA
     A                                 10 50'Blocked Collateral Amount'
     A  26                             11 15'+'
     A  26        DDODLN        21   O 11 23DSPATR(HI)
     A                                      OVRDTA
     A  26                             11 50'Overdraft Line'
      *
     A                                 13 15'-----------------------------'
     A            DDTOTA        21   O 14 23DSPATR(HI)
     A                                      OVRDTA
     A                                 14 50'AVAILABLE BALANCE'
      *
     A          R GL002020F0                TEXT('Screen Footer')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRATR
     A                                 23  3'F3=End   F12=Cancel'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
