      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Amount Amortisation Maintenance Screen')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL002100DF - Midas GL Amount Amortisation Input              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS011             Date 16May05               *
      *  Prev Amend No. CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS011 - EIR/AC Accounting Upgrade to MP1                    *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CF03
     A  26                                  CF05
     A  27                                  CF10
     A  28                                  CF12
      *
      ** Header
      *
     A          R GL002100F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A            #1USER    R        O  1  2REFFLD(USER)
     A            #1RUNA    R        O  1 72REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  1 13'GL002100'
     A                                 01 28'AMOUNT AMORTISATION INPUT'
     A                                      DSPATR(UL HI)
     A                                 02 72TIME
      *
      ** Initial screen
      *
     A          R GL002100F1                TEXT('Intial Screen')
     A                                      OVERLAY
      *
     A                                 04 02'Reference'
     A            #1AREF         6A  B 04 12DSPATR(HI)
     A  50                                  DSPATR(PR)
     A N30N50                               DSPATR(UL)
     A  30                                  DSPATR(RI PC)
      *
     A                                 04 22'Action'
     A            #1ACTN         1A  B 04 29DSPATR(HI)
     A  50                                  DSPATR(PR)
     A N31N50                               DSPATR(UL)
     A  31                                  DSPATR(RI PC)
      *
      ** Details screen
      *
     A          R GL002100F2                TEXT('Details screen')
     A                                      OVERLAY
      *
     A                                 06 02'Branch'
     A            #2BRCA         3A  B 06 09DSPATR(HI)
     A  51                                  DSPATR(PR)
     A N32N51                               DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A                                 06 14'Currency'
     A            #2CCY          3A  B 06 23DSPATR(HI)
     A  51                                  DSPATR(PR)
     A N33N51                               DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A                                 06 28'Profit Centre'
     A            #2PRFC         4A  B 06 42DSPATR(HI)
     A  51                                  DSPATR(PR)
     A N34N51                               DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A                                 08 02'Start date'
     A            #2STDT         6A  B 08 13DSPATR(HI)
     A  51                                  DSPATR(PR)
     A N35N51                               DSPATR(UL)
     A  35                                  DSPATR(RI PC)
     A                                 08 21'Maturity Date'
     A            #2MDAT         6A  B 08 35DSPATR(HI)
     A  53                                  DSPATR(PR)
     A N36N53                               DSPATR(UL)
     A  36                                  DSPATR(RI PC)
     A                                 10 02'Disc./Prem. Amount'
     A            #2DSAM        14A  B 10 21DSPATR(HI)
     A  53                                  DSPATR(PR)
     A N37N53                               DSPATR(UL)
     A  37                                  DSPATR(RI PC)
     A                                 10 37'Par Amount'
     A            #2PRAM        14A  B 10 48DSPATR(HI)
     A  53                                  DSPATR(PR)
     A N38N53                               DSPATR(UL)
     A  38                                  DSPATR(RI PC)
     A                                 12 02'Effective Yield'
     A            #2EFYL        16A  B 12 18DSPATR(HI)
     A  52                                  DSPATR(PR)
     A N39N52                               DSPATR(UL)
     A  39                                  DSPATR(RI PC)
     A                                 12 36'Calculation Basis'
     A            #2CLBS         1A  B 12 54DSPATR(HI)
     A  51                                  DSPATR(PR)
     A N40N51                               DSPATR(UL)
     A  40                                  DSPATR(RI PC)
     A                                 12 57'Amortisation Method'
     A            #2AMMD         1A  B 12 77DSPATR(HI)
     A  51                                  DSPATR(PR)
     A N41N51                               DSPATR(UL)
     A  41                                  DSPATR(RI PC)
     A                                 14  2'Debit A/C code'
     A** Start of change CAS011
     A**********  #2DRCO         4A  B 14 18DSPATR(HI)
     A            #2DRCO        10A  B 14 18DSPATR(HI)
     A** End of change CAS011
     A  51                                  DSPATR(PR)
     A N42N51                               DSPATR(UL)
     A  42                                  DSPATR(RI PC)
     A** Start of change CAS011
     A**********                       14 28'A/C sequence no.'
     A**********  #2DRSQ         2A  B 14 45DSPATR(HI)
     A                                 14 29'A/C sequence no.'
     A            #2DRSQ         2A  B 14 46DSPATR(HI)
     A** End of change CAS011
     A  51                                  DSPATR(PR)
     A N43N51                               DSPATR(UL)
     A  43                                  DSPATR(RI PC)
     A** Start of change CAS011
     A**********                       14 53'Customer number'
     A**********  #2DRCN         6A  B 14 69DSPATR(HI)
     A                                 14 54'Customer number'
     A            #2DRCN         6A  B 14 70DSPATR(HI)
     A** End of change CAS011
     A  51                                  DSPATR(PR)
     A N44N51                               DSPATR(UL)
     A  44                                  DSPATR(RI PC)
     A                                 15  2'Credit A/C code'
     A** Start of change CAS011
     A**********  #2CRCO         4A  B 15 18DSPATR(HI)
     A            #2CRCO        10A  B 15 18DSPATR(HI)
     A** End of change CAS011
     A  51                                  DSPATR(PR)
     A N45N51                               DSPATR(UL)
     A  45                                  DSPATR(RI PC)
     A** Start of change CAS011
     A**********                       15 28'A/C sequence no.'
     A**********  #2CRSQ         2A  B 15 45DSPATR(HI)
     A                                 15 29'A/C sequence no.'
     A            #2CRSQ         2A  B 15 46DSPATR(HI)
     A** End of change CAS011
     A  51                                  DSPATR(PR)
     A N46N51                               DSPATR(UL)
     A  46                                  DSPATR(RI PC)
     A** Start of change CAS011
     A**********                       15 53'Customer number'
     A**********  #2CRCN         6A  B 15 69DSPATR(HI)
     A                                 15 54'Customer number'
     A            #2CRCN         6A  B 15 70DSPATR(HI)
     A** End of change CAS011
     A  51                                  DSPATR(PR)
     A N47N51                               DSPATR(UL)
     A  47                                  DSPATR(RI PC)
     A                                 17 02'Narrative'
     A            #2NARR        30A  B 17 12DSPATR(HI)
     A  51                                  DSPATR(PR)
     A N48N51                               DSPATR(UL)
     A  48                                  DSPATR(RI PC)
      *
      ** Program messages subfile
      *
     A          R GL002100S1                TEXT('Messages subfile')
     A                                      SFL SFLMSGRCD(24)
     A            ##MSGK                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
      ** Message Control
      *
     A          R GL002100C1                TEXT('Messages control')
     A                                      SFLCTL(GL002100S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGMQ                    SFLPGMQ
      *
      ** Function key narratives
      *
     A          R GL002100F3                TEXT('Function keys')
     A                                      OVERLAY
     A                                 23  2'F3=Exit    '
     A  26                             23 14'F5=Refresh '
     A  27                             23 14'F10=Delete '
     A  28                             23 29'F12=Previous'
