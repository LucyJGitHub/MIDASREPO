      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Account Transfer - Selected Accnt Maint')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL003100DF - Midas GL Account Transfer - Selected       *
      *                    Accounts Maintenance                       *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      INDARA
      *
     A          R GL003100S1                SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
      *
     A  11                                  SFLNXTCHG
     A            S1OPTN         2A  B  9  3TEXT('OPTION')
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A N70                                  DSPATR(HI)
     A            S1RFNO        10A  O  9  8TEXT('Reference')
     A            S1BRCA         3A  O  9 19TEXT('Branch')
     A            S1CUST         6A  O  9 23TEXT('Customer')
     A            S1CCY          3A  O  9 30TEXT('Currency')
     A            S1ACOD        10A  O  9 34TEXT('Account Code')
     A            S1ACSQ         2A  O  9 45TEXT('Account Seq')
     A            S1ACNO        10A  O  9 48TEXT('Retail #')
     A            S1STAT         8A  O  9 59TEXT('Status')
     A            S1TBRC         3A  O  9 68TEXT('To Branch')
     A            S1TCUS         6A  O  9 72TEXT('To Customer')
     A            S1TACO        10A  O  9 79TEXT('To Account Code')
     A            S1TACS         2A  O  9 90TEXT('To Account Seq')
      *
     A          R GL003100C1                SFLCTL(GL003100S1)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      CA03
     A                                      OVERLAY
     A N82                                  SFLDSP
     A  81                                  SFLDSPCTL
     A  82                                  SFLCLR
     A N83                                  SFLEND(*MORE)
     A N82 83                               ROLLUP(01)
     A  81                                  ROLLDOWN(02)
     A            SCRRN1         4S 0H      TEXT('SFLRCDNBR field')
     A                                      SFLRCDNBR(CURSOR)
     A            SCUSER        10A  O  1  2
     A                                  1 13'GL003100'
     A                                  1 78'Date:'
     A            SCDATE         7A  O  1 85
     A            SCWSID        10A  O  2  2
     A                                  1 34'Selected Account Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 78'Time:'
     A                                  2 84TIME
     A                                      EDTWRD('0 :  :  ')
     A                                  2  2'Work With Screen'
     A                                      DSPATR(HI)
     A                                  4  2'Select option and press Enter'
     A                                      COLOR(BLU)
     A                                  5  2'A=Amend'
     A                                      COLOR(BLU)
     A                                  5 10'D=Delete'
     A                                      COLOR(BLU)
     A                                  5 19'E=Enquire'
     A                                      COLOR(BLU)
     A            SCOPTN         2A  B  8  3TEXT('OPTION')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A N71                                  DSPATR(HI)
     A            SCPOS1        10A  B  8  8TEXT('Reference')
     A                                      CHECK(LC)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A N54                                  DSPATR(HI)
     A            SCPOS2        24A  B  8 19TEXT('GL Account')
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A N53                                  DSPATR(HI)
     A            SCPOS3        10A  B  8 48TEXT('Retail#')
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A N52                                  DSPATR(HI)
     A                                  7  8'Reference'
     A                                      DSPATR(HI)
     A                                  7 19'GL Account'
     A                                      DSPATR(HI)
     A                                  7 48'Retail'
     A                                      DSPATR(HI)
     A                                  7 59'Status'
     A                                      DSPATR(HI)
     A                                  7 68'Transfer To'
     A                                      DSPATR(HI)
      *
     A          R GL003100D1
     A                                      CA03
     A                                      CA04
     A N77                                  CA05
     A                                      CA12
     A                                      OVERLAY
     A            SCUSER        10A  O  1  2
     A  76 77                               DSPATR(PC)
     A            SCDATE         7A  O  1 73
     A            SCWSID        10A  O  2  2
     A                                  1 20'Selected Accounts Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
     A                                      EDTWRD('0 :  :  ')
     A                                  1 73SYSNAME
     A                                  2  2'Detail Screen -'
     A                                      DSPATR(HI)
     A  75                              4  2'Press ENTER to Confirm Deletion'
     A                                      COLOR(BLU)
     A  77                              4  2'Press ENTER to Continue'
     A                                      COLOR(BLU)
     A N75N77                           4  2'Change details,'
     A                                      COLOR(BLU)
     A N75N77                           4 18'Press ENTER to Continue'
     A                                      COLOR(BLU)
     A                                  6  2'Transfer From Details:'
     A                                      DSPATR(HI)
     A                                  7  3'Reference  . . . . . . . . . .:'
     A            S2RFNO        10   O  7 37TEXT('Reference')
     A                                      DSPATR(HI)
     A            S2DESC        30A  O  7 49TEXT('Description')
     A                                      DSPATR(HI)
      *
     A                                  8  3'Branch Code  . . . . . . . . .:'
     A            S2BRCA         3A  O  8 37TEXT('Branch Code')
     A                                      DSPATR(HI)
     A            S2BRNM        30A  O  8 49TEXT('Branch Name')
      *
     A                                  9  3'Customer Number  . . . . . . .:'
     A            S2CNU1         6A  O  9 37TEXT('Customer Number')
     A                                      DSPATR(HI)
     A            S2CCRN        20A  O  9 49TEXT('Customer Name')
      *
     A                                 10  3'Currency . . . . . . . . . . .:'
     A            S2CCY          3A  O 10 37TEXT('Currency')
     A                                      DSPATR(HI)
     A            S2CYNM        14A  O 10 49TEXT('Currency Description')
      *
     A                                 11  3'Account Code . . . . . . . . .:'
     A            S2ACO1        10A  O 11 37TEXT('Account Code')
     A                                      DSPATR(HI)
     A            S2ACCN        30A  O 11 49TEXT('Account Description')
      *
     A                                 12  3'Account Sequence . . . . . . .:'
     A            S2ACSQ         2A  O 12 37TEXT('Account Sequence')
     A                                      DSPATR(HI)
      *
     A                                 13  3'Retail Number  . . . . . . . .:'
     A            S2ACN1        10A  O 13 37TEXT('Retail Number 1')
     A                                      DSPATR(HI)
      *
     A                                 14  3'Account Officer  . . . . . . .:'
     A            S2ACOC         2A  O 14 37TEXT('Account Officer')
     A                                      DSPATR(HI)
     A            S2ACON        30A  O 14 49TEXT('Account Officer Description')
      *
     A                                 15  3'Account Type . . . . . . . . .:'
     A            S2ATYP         1A  O 15 37TEXT('Account Type')
     A                                      DSPATR(HI)
     A            S2ATYD        30A  O 15 49TEXT('Account Type Description')
      *
     A                                 16  3'Country of Domicile  . . . . .:'
     A            S2COLC         2A  O 16 37TEXT('Country of Domicile')
     A                                      DSPATR(HI)
     A            S2CNNM        30A  O 16 49TEXT('Domicile Description')
      *
     A                                 17  2'Transfer To Details:'
     A                                      DSPATR(HI)
     A                                 18  3'Branch Code  . . . . . . . . .:'
     A            S2TBRC         3A  B 18 37TEXT('Branch Code')
     A  62
     AO 48                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N62                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A            S2TBRN        30A  O 18 49TEXT('Branch Description')
     A                                 19  3'Customer Number  . . . . . . .:'
     A            S2TCNU         6A  B 19 37TEXT('Customer Number')
     A  63
     AO 49                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N63                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A            S2TCRN        20A  O 19 49TEXT('Customer Name')
     A                                 20  3'Account Code . . . . . . . . .:'
     A            S2TACO        10A  B 20 37TEXT('Account Code')
     A  64
     AO 50                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N64                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A            S2TACC        30A  O 20 49TEXT('Account Code Description')
     A                                 21  3'Account Sequence . . . . . . .:'
     A            S2TACS         2A  B 21 37TEXT('Account Sequence')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N65                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
      *
     A          R GL003100F1
     A                                      OVERLAY
     A                                 22 30'Total Accounts to be +
     A                                      Transferred :'
     A            WCOUNT2       13A  O 22 65
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A          R GL003100F2
     A                                      OVERLAY
     A N77                             23  2'F3=Exit  F4=Prompt  F5=Refresh -
                                             F12=Previous'
     A                                      COLOR(BLU)
     A  77                             23  2'F3=Exit  F12=Previous'
     A                                      COLOR(BLU)
      *
     A          R MSGSFL                    TEXT('Message Subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SCMGKY                    SFLMSGKEY
     A            D@PGMN                    SFLPGMQ
      *
     A          R MSGCTL                    TEXT('Control Record')
     A                                      SFLCTL(MSGSFL)
     A                                      SFLSIZ(20)
     A                                      SFLPAG(1)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            W@PGMN                    SFLPGMQ
      *
