      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Account Keys Report Prompt')
     A*****************************************************************
     A*                                                               *
     A*  Midas  GENERAL LEDGER MODULE                       *
     A*                                                               *
     A*    GL0050V# - Account Keys Report Prompt Display File         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     LAST AMEND NO. XXXXXX            DATE  DDMMMYY            *
     A*     PREV AMEND NO. XXXXXX            DATE  DDMMMYY            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*     XXXXXX  -  XXXXXXXXXXXXXXXX                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A*
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF10(10)
     A*
     A          R HEADER                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            USRID         10A  O  1  2
     A                                  1 13'GL0050V'
     A            RUNA           7A  O  1 72
     A            WSID          10A  O  2  2
     A                               O  2 30'Account Keys Report-
     A                                       Prompt'
     A                                      DSPATR(HI)
     A                               O  2 73TIME
     A                               O  5 12'Enter  Y  to select-
     A                                       Account Keys Report'
     A*
     A          R KEYFMT
     A                                      OVERLAY
     A                                      PUTOVR
     A N80        FLD1           1A  B  8 22
     A N25                                  DSPATR(UL HI)
     A  25                                  DSPATR(PC RI)
     A                                      OVRDTA
     A  80        FLD1@          1A  O  8 22
     A                                      DSPATR(HI)
     A  60                              8 25'Dealing, Lending-
     A                                       and Funds Transfer'
     A  61                              8 25'Dealing and Lending'
     A  62                              8 25'Dealing and Funds-
     A                                       Transfer'
     A  63                              8 25'Dealing'
     A N80 64     FLD2           1A  B 10 22
     A N26                                  DSPATR(UL HI)
     A  26                                  DSPATR(PC RI)
     A                                      OVRDTA
     A  80 64     FLD2@          1A  O 10 22
     A                                      DSPATR(HI)
     A  64                             10 25'Securities'
     A N80 65     FLD3           1A  B 12 22
     A N27                                  DSPATR(UL HI)
     A  27                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                      CHECK(FE)
     A  80 65     FLD3@          1A  O 12 22
     A                                      DSPATR(HI)
     A  65                             12 25'Futures & Options'
     A  90                             23  3'F3=Exit   F10=Delete -
     A                                        F12=Previous'
     A N90                             23  3'F3=Exit   F12=Previou-
     A                                      s'
     A*
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
     A*
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  20
     AON20                                  SFLEND
     A            SPGMQ                     SFLPGMQ
