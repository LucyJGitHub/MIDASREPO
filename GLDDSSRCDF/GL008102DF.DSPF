     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Stamp Tax Details Screen')                    *
     A*****************************************************************
     A*                                                               *
     A*  Midas - General Ledger                                       *
     A*                                                               *
     A*  DSPF/GL008102DF - Stamp Tax Details Screen                   *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd 2010             *
     A*                                                               *
     A*  Last Amend No. CER049  *CREATE    Date 06Dec10               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  CER049 - Stamp Tax                                           *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      CF12(12 'Cancel')
     A*
     A          R SCREENF
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2010')
     A                                      OVERLAY
     A                                      PUTOVR
     A*
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A            STITL         17A  O  1 32DSPATR(HI)
     A*
     A                                  3  5'Stamp tax Liable....:'
     A            T#TAX          1A  B  3 27OVRDTA
     A N51N20                               DSPATR(HI)
     A  51N20                               DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                  3 29'(Y or N)'
     A  23                              3 39'Close'
     A            T#CLO          1A  B  3 45OVRDTA
     A N53 23N20                            DSPATR(HI)
     A  53 23N20                            DSPATR(RI)
     A  53 23N20                            DSPATR(PC)
     A N23
     AO 20                                  DSPATR(PR)
     A N23                                  DSPATR(ND)
     A  23N20                           3 47'(C N)'
     A  36                              5  5'Set as default tax account?'
     A*
     A  36        T#DEF          1A  B  5 33OVRDTA
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A*----------------------------------------------------------------
     A                                  6  1'                                  -
     A                                                         '
     A                                      DSPATR(UL)
     A N41                              7  5'Average Balance:'
     A            T#AVBL         1A  B  7 22OVRDTA
     A N56N24N20                            DSPATR(HI)
     A  56N24N20                            DSPATR(RI)
     A  56N24N20                            DSPATR(PC)
     A  20
     AO 24                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A N42                              7 38'Start..:'
     A            T#AVDT         6A  B  7 47OVRDTA
     A N57N25N20                            DSPATR(HI)
     A  57N25N20                            DSPATR(RI)
     A  57N25N20                            DSPATR(PC)
     A  20
     AO 25                                  DSPATR(PR)
     A  42                                  DSPATR(ND)
     A N47                              8  5'Tax Free Amount:'
     A            T#FREA        15A  B  8 22OVRDTA
     A N62N34N20                            DSPATR(HI)
     A  62N34N20                            DSPATR(RI)
     A  62N34N20                            DSPATR(PC)
     A  20
     AO 34                                  DSPATR(PR)
     A  47                                  DSPATR(ND)
     A N48                              8 38'End....:'
     A            T#FRED         6A  B  8 47OVRDTA
     A N63N35N20                            DSPATR(HI)
     A  63N35N20                            DSPATR(RI)
     A  63N35N20                            DSPATR(PC)
     A  20
     AO 35                                  DSPATR(PR)
     A  48                                  DSPATR(ND)
     A N45                              9  5'Interest.......:'
     A            T#INLF         1A  B  9 22OVRDTA
     A N60N28N20                            DSPATR(HI)
     A  60N28N20                            DSPATR(RI)
     A  60N28N20                            DSPATR(PC)
     A  20
     AO 28                                  DSPATR(PR)
     A  45                                  DSPATR(ND)
     A N46                              9 38'Start..:'
     A            T#INDT         6A  B  9 47OVRDTA
     A N61N29N20                            DSPATR(HI)
     A  61N29N20                            DSPATR(RI)
     A  61N29N20                            DSPATR(PC)
     A  20
     AO 29                                  DSPATR(PR)
     A  46                                  DSPATR(ND)
     A N43                             10  5'Flat Fee.......:'
     A            T#HFLF         1A  B 10 22OVRDTA
     A N58N26N20                            DSPATR(HI)
     A  58N26N20                            DSPATR(RI)
     A  58N26N20                            DSPATR(PC)
     A  20
     AO 26                                  DSPATR(PR)
     A  43                                  DSPATR(ND)
     A N44                             10 38'Start..:'
     A            T#HFDT         6A  B 10 47OVRDTA
     A N59N27N20                            DSPATR(HI)
     A  59N27N20                            DSPATR(RI)
     A  59N27N20                            DSPATR(PC)
     A  20
     AO 27                                  DSPATR(PR)
     A  44                                  DSPATR(ND)
     A N30                             12  5'Debit Account Number..:'
     A*
     A            T#ACNO        24A  B 12 32OVRDTA
     A N52N22                               DSPATR(HI)
     A  52N22                               DSPATR(RI)
     A  52N22                               DSPATR(PC)
     A  22                                  DSPATR(PR)
     A  30                                  DSPATR(ND)
     A*
     A            T#SCLM         3A  O 12 57
     A  32                                  DSPATR(ND)
     A N31                             13  5'Stamp Tax Amount......:'
     A*
     A            T#STAF        14A  O 13 42
     A  31                                  DSPATR(ND)
     A*
     A            T#SCAF         3A  O 13 57
     A  31                                  DSPATR(ND)
     A N33                             14  5'Stamp Tax on Interest.:'
     A*
     A            T#STAI        14A  O 14 42
     A  33                                  DSPATR(ND)
     A*
     A            T#SCAI         3A  O 14 57
     A  33                                  DSPATR(ND)
     A  72                             15  5'Tax on Avg this month.:'
     A  73                             15  5'Tax on Average........:'
     A            T#STMI        14A  O 15 42
     A N72N73                               DSPATR(ND)
     A            T#SCMI         3A  O 15 57
     A N72                                  DSPATR(ND)
     A*    Allowed function key
     A                                 23  2'F3=Exit'
     A                                 23 12'F12=Cancel'
     A N21                             23 24'F5=Refresh'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
