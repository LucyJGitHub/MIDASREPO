      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Stamp Tax Movements Enquiry - Retail')        *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL008160DF - Stamp Tax Movements Enquiry - Retail       *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      *                 CER049  *CREATE    Date 06Dec10               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
     A          R #SFLRCD                   SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited 2010')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(40 'Invalid: #2SEL')
     A  84                                  SFLNXTCHG
     A            #2KEY         10   H      TEXT('Origin')
      **Start of change CLE148
     A**********  #2LNRF         6  0H      TEXT('Loan')                                      CLE148
     A            #2LNRF         6A  H      TEXT('Loan')
      **End of change CLE148
     A            #2FCNM         6   H      TEXT('Customer')
     A            #2FACT         3  0H      TEXT('Facility Type')
     A            #2FCNO         2  0H      TEXT('Facility Nr')
     A            #2FSEQ         2  0H      TEXT('Facility Sq')
     A            #2ORED         5  0H      TEXT('Original Entry Date')
     A            #2DLNO         6  0H      TEXT('Deal nr')
     A            #2TDRF         6   H      TEXT('Trade nr')
     A            #2BRCA         3   H      TEXT('Branch')
     A            #2CNUM         6   H      TEXT('Customer')
     A            #2CCY          3   H      TEXT('Currency')
     A            #2ACOD        10  0H      TEXT('Account Code')
     A            #2ACSQ         2  0H      TEXT('Account Sequence')
     A            #2CDTE         5  0H      TEXT('Action Date')
     A            #2PREF        15   H      TEXT('Payment Ref')
     A            #2CQSQ         2  0H      TEXT('Sequence')
     A            #2PBRC         3   H      TEXT('Pos Sett Branch')
     A            #2PSSH        10   H      TEXT('Pos Sett Shortname')
     A            #2PCPY         6   H      TEXT('Pos Sett Cparty')
     A            #2PDUD         5  0H      TEXT('Pos Sett Due Date')
     A            #2PEVT         2   H      TEXT('Pos Sett Event Type')
      *
     A            #2PSDT         5  0H      TEXT('Posting Date')
     A            #2VALD         5  0H      TEXT('Value Date')
     A            #2STDT         5  0H      TEXT('Start Period Date')
     A            #2ENDT         5  0H      TEXT('End Period Date')
     A            #2ECCY         3   H      TEXT('Event Ccy')
     A            #2SCCY         3   H      TEXT('Stamp Tax ccy')
     A            #2TASC        15  0H      TEXT('Stamp Tax amn')
     A            #2TXRB        13  8H      TEXT('Stamp Tax amn')
     A            #2TADC        15  0H      TEXT('Stamp Dr acc ccy')
     A            #2STBL        15  0H      TEXT('OPENING BALANCE')
     A            #2ENBL        15  0H      TEXT('Closing Balance')
      *
     A            #2SEL          1   B 10  2
     A                                      VALUES('E' +
     A                                             'P' +
     A                                                  )
     A                                      CHECK(AB)
     A N40                                  DSPATR(HI)
     A N98
     AO 40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A            #2ACCD        24     10  4TEXT('Account Code')
     A                                      DSPATR(HI)
     A            #2GROS        21     10 29TEXT('Average Balance')
     A                                      DSPATR(HI)
     A            #2STAX        21     10 51TEXT('Stamp Tax')
     A                                      DSPATR(HI)
     A            #2EDTE         7     10 73TEXT('Val Dat')
     A                                      DSPATR(HI)
      *
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(10) SFLSIZ(11)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND(*MORE)
      ** Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF17(17 'Refresh')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
      ** SETOFFS
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2ACCD')
     A                                      SETOF(32 'Invalid: #2EDTE')
      *
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'GL008160DF'
      *
     A            ##MRDT         7      1 73TEXT('Midas run date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  1 32'STAMP TAX MOVEMENTS'
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 29TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
      *
     A                                  4  3'Start Date'
     A            #3CDTE         6   B  4 14TEXT('Effective date')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
      *
     A                                  4 22'End Date'
     A            #3EDTE         6   B  4 31TEXT('End date')
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
      *
     A                                  5  3'Interest/Average (I/A)'
     A            #3TTYPE        1   B  5 26TEXT('Stamp Tax Type')
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            #3TTYDS        8   B  5 28TEXT('Stamp Tax Type')
     A                                      DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A                                  5 38'Sort Key D/A'
     A            #3SORTK        1   B  5 51TEXT('Sort Key')
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            #3SORTD        7   B  5 53TEXT('Sort Key Descr')
     A                                      DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A                                  6  3'Customer'
     A            #3CUST         6   B  6 14TEXT('Customer')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A                                  6 22'Ccy'
     A            #3CCY          3   B  6 26TEXT('CCy')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
      *
     A                                  6 31'A/c'
     A            #3ACOD        10   B  6 35TEXT('Account code')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
      *
     A                                  6 47'Seq'
     A            #3ACSQ         2   B  6 51TEXT('Account section')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
      *
     A                                  6 55'Branch'
     A            #3BRCA         3   B  6 62TEXT('Branch')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
      *
     A  81                              8  2'?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8  6'Account Code'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81        #ODESC        21      8 29
     A                                      OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 51'    Stamp Tax amount '
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 73'Val Dat'
     A                                      DSPATR(HI) COLOR(WHT)
      *
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
