      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Stamp Tax Movements Enquiry RE Screen 2')     *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL008161DF - Stamp Tax Movements Enquiry RE Screen 2    *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER049  *CREATE    Date 06Dec10               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
     A                                      PRINT
      *
     A          R SCREENF                   TEXT('(c) Finastra International +
     A                                      Limited 2010')
      *
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'GL008161DF'
     A            ##MRDT         7      1 73TEXT('Midas run date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            STITL         30A  O  2 29
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
      *
     A                                  1 32'STAMP TAX MOVEMENTS'
     A                                      DSPATR(HI)
     A            #2KEY         20      4  2TEXT('Key Description')
     A            #2KEYF        28      4 26TEXT('Key Field')
     A                                      DSPATR(HI)
      *
     A                                  5  2'Stamp Tax Debit Account'
     A            #1DACC        28      5 26TEXT('Account Code')
     A                                      DSPATR(HI)
     A                                  7  2'Posting Date'
     A            #2PSTD         7      7 26TEXT('Posting Date')
     A                                      DSPATR(HI)
     A                                  8  2'Value Date'
     A            #2VALD         7      8 26TEXT('Value Date')
     A                                      DSPATR(HI)
     A            #2FLDD        18     10  2
     A            #2GROS        21     10 28TEXT('Gross Amount')
     A                                      DSPATR(HI)
     A            #1CCY1         3     10 50TEXT('Event Ccy')
     A                                      DSPATR(HI)
     A                                 12  2'Tax Amount'
     A            #2TAX1        21     12 28TEXT('Tax in Event Ccy')
     A                                      DSPATR(HI)
     A            #2CCY1         3     12 50TEXT('Event Ccy')
     A                                      DSPATR(HI)
     A                                 13  2'Tax Amount in Dr a/c Ccy'
     A            #2TAX2        21     13 28TEXT('Tax in Dr Acc Ccy')
     A                                      DSPATR(HI)
     A            #2CCY2         3     13 50TEXT('Dr Acc Ccy')
     A                                      DSPATR(HI)
     A                                 14  2'Tax Amount in Stamp Ccy'
     A            #2TAX3        21     14 28TEXT('Tax in Stamp Tx Ccy')
     A                                      DSPATR(HI)
     A            #2CCY3         3     14 50TEXT('Stamp Tax Ccy')
     A                                      DSPATR(HI)
     A                                 15  2'Tax Rate'
     A            #2TAXR        21     15 28TEXT('Tax Rate')
     A                                      DSPATR(HI)
     A            #TRATT        50     16  2TEXT('Tax on Interest')
     A            #1DES1        21     17  2TEXT('Tot Tax on Average')
     A            #1STAX        21     17 28TEXT('Tax on Average')
     A                                      DSPATR(HI)
     A            #1CCY2         3     17 50TEXT('Event Ccy')
     A                                      DSPATR(HI)
     A                                 22  2'Press ENTER to return'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(18)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
