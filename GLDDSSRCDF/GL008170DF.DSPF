      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Stamp Tax Movements Enquiry')                 *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL008170DF - Stamp Tax Movements Enquiry                *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      *                 CER049  *CREATE    Date 06Dec10               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
 
     A          R #SFLRCD                   SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited 2010')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(40 'Invalid: #2SEL')
     A  84                                  SFLNXTCHG
     A            #2KEY         10A  H      TEXT('Origin')
      **Start of change CLE148
     A**********  #2LNRF         6S 0H      TEXT('Loan')                                      CLE148
     A            #2LNRF         6A  H      TEXT('Loan')
      **End of change CLE148
     A            #2FCNM         6A  H      TEXT('Customer')
     A            #2FACT         3S 0H      TEXT('Facility Type')
     A            #2FCNO         2S 0H      TEXT('Facility Nr')
     A            #2FSEQ         2S 0H      TEXT('Facility Sq')
     A            #2ORED         5S 0H      TEXT('Original Entry Date')
     A            #2DLNO         6S 0H      TEXT('Deal nr')
     A            #2TDRF         6A  H      TEXT('Trade nr')
     A            #2BRCA         3A  H      TEXT('Branch')
     A            #2CNUM         6A  H      TEXT('Customer')
     A            #2CCY          3A  H      TEXT('Currency')
     A            #2ACOD        10S 0H      TEXT('Account Code')
     A            #2ACSQ         2S 0H      TEXT('Account Sequence')
     A            #2CDTE         5S 0H      TEXT('Action Date')
     A            #2PREF        15A  H      TEXT('Payment Ref')
     A            #2CQSQ         2S 0H      TEXT('Sequence')
     A            #2PBRC         3A  H      TEXT('Pos Sett Branch')
     A            #2PSSH        10A  H      TEXT('Pos Sett Shortname')
     A            #2PCPY         6A  H      TEXT('Pos Sett Cparty')
     A            #2PDUD         5S 0H      TEXT('Pos Sett Due Date')
     A            #2PEVT         2A  H      TEXT('Pos Sett Event Type')
     A            #2PSDT         5S 0H      TEXT('Posting Date')
     A            #2VALD         5S 0H      TEXT('Value Date')
     A            #2STDT         5S 0H      TEXT('Start Period Date')
     A            #2ENDT         5S 0H      TEXT('End Period Date')
     A            #2ECCY         3A  H      TEXT('Event Ccy')
     A            #2SCCY         3A  H      TEXT('Stamp Tax ccy')
     A            #2TASC        15S 0H      TEXT('Stamp Tax amn')
     A            #2TAXE        15S 0H      TEXT('Stamp Tax amn')
     A            #2TXRB        13S 8H      TEXT('Tax rate')
     A            #2TADC        15S 0H      TEXT('Stamp Dr acc ccy')
     A            #2STBL        15S 0H      TEXT('OPENING BALANCE')
     A            #2ENBL        15S 0H      TEXT('Closing Balance')
     A            #2STFB         1A  H      TEXT('Balance Tax')
     A            #2STFC         1A  H      TEXT('Charge Tax')
     A            #2STFF         1A  H      TEXT('Flat Tax')
     A            #2STFI         1A  H      TEXT('Interest Tax')
     A            #2AKEY        14A  H      TEXT('Account Key')
     A            #2MNTH         5S 0H      TEXT('Number of Months')
     A            #2VDAT         5S 0H      TEXT('Value date')
     A            #2MDAT         5S 0H      TEXT('Maturity Date')
     A            #2KCCY         3A  H      TEXT('Account key ccy')
     A            #2KAMT        13S 0H      TEXT('Account key amn')
     A            #2DSPR        13S 8H      TEXT('Exchange Rate')
     A            #2ETYP         2A  H      TEXT('Event Type')
     A            #2REVI         1A  H      TEXT('Reverse Indicator')
     A            #2TREF        28A  H      TEXT('Transaction Ref')
     A            #2DRAC        28A  H      TEXT('Dr Account')
     A            #2DCUS         6A  H
     A            #2SEL          1A  B 15  2
     A                                      VALUES('E' +
     A                                             'P' +
     A                                                  )
     A  40                                  CHECK(AB)
     A N40                                  DSPATR(HI)
     A N98
     AO 40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A            #2REF1        28A  O 15  4TEXT('Reference 1')
     A                                      DSPATR(HI)
     A            #2TAXT         1A  O 15 33TEXT('Tax Type')
     A                                      DSPATR(HI)
     A            #2DCCY         3A  O 15 36DSPATR(HI)
     A                                      TEXT('Debit Ccy')
     A            #2STAX        20A  O 15 40TEXT('Stamp Tax')
     A                                      DSPATR(HI)
     A            #2REVE         1A  O 15 62DSPATR(HI)
     A            #2DAT1         7A  O 15 65TEXT('Date 1')
     A                                      DSPATR(HI)
     A            #2TKEY         8   O 15 73DSPATR(HI)
     A                                      TEXT('Transaction Type')
 
      ** Second row
 
     A            #2ROW2        28A  O 16  4TEXT('Reference 2')
     A            #2GCCY         3A  O 16 36TEXT('Event Ccy')
     A            #2GROS        20A  O 16 40TEXT('Gross Amount')
     A            #2DAT2         7A  O 16 65TEXT('Date 2')
 
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
 
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0003)
     A                                      SFLDROP(CF08)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF17(17 'Refresh')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2ACCD')
     A                                      SETOF(32 'Invalid: #2EDTE')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A N25N80 81                            SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND(*MORE)
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'GL008170DF'
     A            ##MRDT         7A  O  1 73TEXT('Midas run date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  1 28'STAMP TAX MOVEMENTS ENQUIRY'
     A                                      DSPATR(HI)
 
     A                                  3  2'Value Date'
     A                                  3 14'From'
     A            #3CDTE         6A  B  3 19TEXT('Effective date')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  3 26'To'
     A            #3EDTE         6A  B  3 29TEXT('End date')
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N41                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  3 39'Posting Dt'
     A                                  3 51'From'
     A            #3FPSD         6A  B  3 56
     A N25                                  OVRDTA
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N42                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  3 63'To'
     A            #3EPSD         6   B  3 66
     A N25                                  OVRDTA
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N43                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
 
     A                                  4  2'Module'
     A            #3KEY          3   B  4 10DSPATR(UL)
     A N25                                  OVRDTA
     A  58                                  DSPATR(RI)
     A  58                                  DSPATR(PC)
     A N58                                  DSPATR(HI)
     A                                  4 14'(ACCount/SECurity/FACility/LOAn/DE-
     A                                      Al/PAYment/CHEque/FEE/POS Sett)'
 
     A                                  6  2'Debit a/c'
     A                                  6 14'Branch'
     A            #3BRCA         3A  B  6 22TEXT('Branch')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6 28'Customer'
     A            #3CUST         6A  B  6 38TEXT('Customer')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 46'Ccy'
     A            #3CCY          3A  B  6 52TEXT('CCy')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6 57'A/c'
     A            #3ACOD        10A  B  6 61TEXT('Account code')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6 73'Seq'
     A            #3ACSQ         2A  B  6 78TEXT('Account section')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
 
     A                                  7  2'Account'
     A                                  7 14'Branch'
     A            BRANCH         3A  B  7 22
     A N25                                  OVRDTA
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A N44                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  7 28'Customer'
     A            CUSTOM         6A  B  7 38
     A N25                                  OVRDTA
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A N52                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  7 46'Ccy'
     A            CCY            3A  B  7 52
     A N25                                  OVRDTA
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A N53                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  7 57'A/c'
     A            ACOD          10A  B  7 61
     A N25                                  OVRDTA
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A N54                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  7 73'Seq'
     A            SEQU           2A  B  7 78
     A N25                                  OVRDTA
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A N55                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
 
     A                                  8 14'Deal'
     A            DEALN          6A  B  8 22
     A N25                                  OVRDTA
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A N45                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  8 29'Loan'
     A            LOANNR         6A  B  8 39
     A N25                                  OVRDTA
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A N46                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  8 47'Trade'
     A            TRADEN         6A  B  8 53
     A N25                                  OVRDTA
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A N47                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  8 60'Fee'
     A            FEESEQ         2A  B  8 64
     A N25                                  OVRDTA
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A N48                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  8 69'Dat'
     A            FEEDAT         6A  B  8 73
     A N25                                  OVRDTA
     A  59                                  DSPATR(RI)
     A  59                                  DSPATR(PC)
     A N59                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
      *
     A                                  9 14'Facility'
     A                                  9 29'Customer'
     A            FCUST          6A  B  9 39
     A N25                                  OVRDTA
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A N49                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  9 47'Type'
     A            FTYPE          3A  B  9 53
     A N25                                  OVRDTA
     A  56                                  DSPATR(RI)
     A  56                                  DSPATR(PC)
     A N56                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  9 60'Nr'
     A            FNUM           2A  B  9 64
     A N25                                  OVRDTA
     A  57                                  DSPATR(RI)
     A  57                                  DSPATR(PC)
     A N57                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
 
     A                                 10 14'Payment/Cheque'
     A                                 10 29'Reference'
     A            PAYCHQ        15A  B 10 39
     A N25                                  OVRDTA
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A N50                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                 10 60'Chq Sequence'
     A            CHQSEQ         2A  B 10 73
     A N25                                  OVRDTA
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A N51                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
 
     A                                 11  2'Posn Settle'
     A                                 11 14'Branch'
     A            PSBRCA         3A  B 11 22
     A N25                                  OVRDTA
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A N60                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                 11 26'Security'
     A            PSSESH        10A  B 11 35
     A N25                                  OVRDTA
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                 11 46'Cparty'
     A            PSCPTY         6A  B 11 53
     A N25                                  OVRDTA
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                 11 60'Due'
     A            PSDUED         6A  B 11 64
     A N25                                  OVRDTA
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                 11 71'Event'
     A            PSEVNT         2A  B 11 77
     A N25                                  OVRDTA
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A N64                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
 
     A            #PACTC         1A  H      TEXT('Action Code')
     A  81                             13  1'E'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                             13  3'Original Transaction'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                             13 32'Tax'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                             13 36'Dr'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                             13 40'Stamp Tax in dr ccy'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                             13 61'Rev'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81        DATE1          7A  O 13 65DSPATR(HI)
     A N25                                  OVRDTA
     A N25                                  OVRATR
     A  81                             13 73'Module'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
 
     A  81                             14  4'Account Debited'
     A  81                             14 36'Ccy'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                             14 47'Gross Amount'
     A  81        DATE2          7A  O 14 65
     A N25                                  OVRDTA
     A N25                                  OVRATR
 
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78   H
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
 
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
 
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
