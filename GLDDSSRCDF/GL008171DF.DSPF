      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Stamp Tax Movements Enquiry Screen 2')        *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL008171DF - Stamp Tax Enquiry Screen 2                 *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER049  *CREATE    Date 06Dec10               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
     A                                      PRINT
      *
     A          R SCREENF                   TEXT('(c) Finastra International +
     A                                      Limited 2010')
 
     A                                      OVERLAY
     A                                      PUTOVR
 
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'GL008161DF'
     A            ##MRDT         7      1 73TEXT('Midas run date')
     A N25                                  OVRDTA
 
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            STITL         30A  O  2 33
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  1 32'STAMP TAX MOVEMENTS'
     A                                      DSPATR(HI)
 
     A            #2KEY         10      3  2TEXT('Key Description')
     A            #2KEYF        31      3 16TEXT('Key Field')
     A                                      DSPATR(HI)
     A            #1SHNM        10   O  3 49DSPATR(HI)
     A                                  4  2'Debit Account'
     A            #1DACC        28      4 16TEXT('Account Code')
     A                                      DSPATR(HI)
     A            #1DSHO        10   O  4 49DSPATR(HI)
     A            #1CSNM        35   O  5 16DSPATR(HI)
     A            #TRATT        50      6  2TEXT('Tax on Interest')
 
     A                                  7  2'Posting Date'
     A            #2PSTD         7      7 24TEXT('Posting Date')
     A                                      DSPATR(HI)
     A                                  8  2'Value Date'
     A            #2VALD         7      8 24TEXT('Value Date')
     A                                      DSPATR(HI)
     A                                  9 50'Rev'
 
     A            #2DESC        20   O 10  2
     A            #2GROS        21     10 23TEXT('Gross Amount')
     A                                      DSPATR(HI)
     A            #1CCY1         3     10 45TEXT('Event Ccy')
     A                                      DSPATR(HI)
     A            #2REVI         1   O 10 51DSPATR(HI)
     A                                 11  2'Rate %'
     A            #2TAXR        21     11 23TEXT('Tax Rate')
     A                                      DSPATR(HI)
 
     A                                 12  2'Stamp Tax'
     A            #2TAX1        21     12 23TEXT('Tax in Event Ccy')
     A                                      DSPATR(HI)
     A            #2CCY1         3     12 45TEXT('Event Ccy')
     A                                      DSPATR(HI)
 
     A                                 13  2'Stamp Tax in Dr Ccy'
     A            #2TAX2        21     13 23TEXT('Tax in Dr Acc Ccy')
     A                                      DSPATR(HI)
     A            #2CCY2         3     13 45TEXT('Dr Acc Ccy')
     A                                      DSPATR(HI)
     A            #REVTX         1   O 13 51DSPATR(HI)
     A                                 14  2'Exchange Rate'
     A            #2DSPR        21     14 23TEXT('Tax in Stamp Tx Ccy')
     A                                      DSPATR(HI)
 
     A                                 16  2'Value/Maturity Date'
     A            #2VDAT         7   O 16 23DSPATR(HI)
     A            #2MDAT         7   O 16 31DSPATR(HI)
     A                                 17  2'Number of months'
     A            #2MNTH         5A  O 17 23DSPATR(HI)
 
     A                                 22  2'Press ENTER to return'
 
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(17)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
 
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
