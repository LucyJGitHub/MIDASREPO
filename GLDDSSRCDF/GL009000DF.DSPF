      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Account Transfer Default Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL009000DF - Postings Interface (Default Maintenance)   *
      *                                                               *
      *  Function:   This display enables the maintenance of the      *
      *              Batch Interface Input defaults.                  *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CGL114A            Date 12Mar10               *
      *                 CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL114A - CCR Interest Capitalisation                        *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      INDARA
      *
     A          R GL009000H1
     A                                      OVERLAY
     A            @USER         10A  O  1  2
     A            @PGM          10A  O  1 13
     A                                  1 25'Account Transfer - Default Maint-
     A                                      enance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SCDAT#         7A  O  1 74
     A            @JOB          10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 73TIME
     A                                      EDTWRD('0 :  :  ')
     A                                  4  2'Type data, press Enter.'
      *
     A          R GL009000D1
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
      ** Start of change CGL114A
     A**********                        6  2'Gen. Balancing Entries Booking Bra-             CGL114A
     A**********                            nch.'                                            CGL114A
     A**********  SCBRCH         3A  B  6 46                                                 CGL114A
     A                                  6  2'Generation of Balancing Entries'
     A                                      DSPATR(UL)
     A                                  7 12'Booking Branch. . . . . . . :'
     A            SCBRCH         3A  B  7 42
      ** End of change CGL114A
     A N54                                  DSPATR(HI)
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
     A  54                                  DSPATR(RI)
     A  54
     AO 61                                  DSPATR(PC)
      ** Start of change CGL114A
     A**********                        8  2'Gen. Balancing Entries Transaction-             CGL114A
     A**********                             Type.'                                          CGL114A
     A**********  SCTRTY         5A  B  8 46                                                 CGL114A
     A                                  8 12'Transaction Type. . . . . . :'
     A            SCTRTY         5A  B  8 42
      ** End of change CGL114A
     A N55                                  DSPATR(HI)
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
     A  55                                  DSPATR(RI)
     A  55
     AO 62                                  DSPATR(PC)
      ** Start of change CGL114A
     A**********                       10  2'Gen. Balancing Entries Department -             CGL114A
     A**********                            Code.'                                           CGL114A
     A**********  SCDPCD         3A  B 10 46                                                 CGL114A
     A                                  9 12'Department Code . . . . . . :'
     A            SCDPCD         3A  B  9 42
      ** End of change CGL114A
     A N56                                  DSPATR(HI)
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
     A  56                                  DSPATR(RI)
     A  56
     AO 63                                  DSPATR(PC)
      ** Start of change CGL114A
     A**********                       13  2'Gen. Dummy DR/CR Entries Transacti-             CGL114A
     A**********                            on Type.'                                        CGL114A
     A**********  SCTRAT         5A  B 13 46                                                 CGL114A
     A                                 11  2'Generation of Dummy DR/CR Entries'
     A                                      DSPATR(UL)
     A                                 12 12'Transaction Type. . . . . . :'
     A            SCTRAT         5A  B 12 42
      ** End of change CGL114A
     A N57                                  DSPATR(HI)
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
     A  57                                  DSPATR(RI)
     A  57
     AO 64                                  DSPATR(PC)
      ** Start of change CGL114A
     A**********                       15  2'Gen. Dummy DR/CR Entries Posting -              CGL114A
     A**********                            Amount.'                                         CGL114A
     A**********  SCPSTA        13A  B 15 46                                                 CGL114A
     A                                 13 12'Posting Amount. . . . . . . :'
     A            SCPSTA        13A  B 13 42
      ** End of change CGL114A
     A N58                                  DSPATR(HI)
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
     A  58                                  DSPATR(RI)
     A  58
     AO 65                                  DSPATR(PC)
      ** Start of change CGL114A
     A**********                       17  2'Gen. Dummy DR/CR Entries Posting -              CGL114A
     A**********                            Narrative.'                                      CGL114A
     A**********  SCPNAR        30A  B 17 46                                                 CGL114A
     A                                 14 12'Posting Narrative . . . . . :'
     A            SCPNAR        30A  B 14 42
      ** End of change CGL114A
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
      ** Start of change CGL114A
     A                                 16  2'Generation of Demand Interest +
     A                                      Capitalisation'
     A                                      DSPATR(UL)
     A                                 17 12'Request for Capitalisation. :'
     A            SCDICR         1A  B 17 42
     A N59                                  DSPATR(HI)
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
     A  59                                  DSPATR(RI)
     A  59
     AO 66                                  DSPATR(PC)
      ** End of change CGL114A
      *
     A          R GL009000F1
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
     A                                 23 24'F12=Previous'
      *
     A          R MSGSFL                    SFL
     A                                      TEXT('MESSAGE SUBFILE')
     A                                      SFLMSGRCD(24)
     A            SCMGKY                    SFLMSGKEY
     A            @PGM                      SFLPGMQ(10)
      *
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A                                      TEXT('CONTROL RECORD')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            @PGM                      SFLPGMQ(10)
