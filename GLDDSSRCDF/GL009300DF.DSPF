      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Account Transfer Submit Processing in COB')
      *****************************************************************
      *                                                               *
      *  Midas -  General Ledger Module                               *
      *                                                               *
      *  GL009300DF - Midas GL Account Transfer Submit Job for        *
      *               Processing in COB                               *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
      *  Formats      Description                                     *
      *  -------      -----------                                     *
      *  GL009300H1   Header.                                         *
      *  GL009300D1   Maintenance Screen.                             *
      *  GL009300F1   Footer.                                         *
      *  MSGSFL/CTL   Message Subfile.                                *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      INDARA
      *----------------------------------------------------------------
     A          R GL009300H1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
     A                                      OVERLAY
     A            @USER         10A  O  1  2
     A            @PGM          10A  O  1 13
     A                                  1 29'Account Transfer Utility'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SCDAT#         7A  O  1 74
     A            @JOB          10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 25'Submit Job for Processing in COB'
     A                                      DSPATR(HI)
     A                                  2 73TIME
     A                                      EDTWRD('0 :  :  ')
     A                                  4  2'Type data, press Enter.'
      *----------------------------------------------------------------
     A          R GL009300D1
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CA21
     A                                      OVERLAY
     A                                  6  5'Run Account Transfer Utility -
     A                                      after COB? (Y/N)'
     A            SREQD          1A  B  6 51
     A N54                                  DSPATR(HI)
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
     A  54                                  DSPATR(RI)
     A  54
     AO 61                                  DSPATR(PC)
     A                                  8  5'Total Accounts to be Transferred:'
     A            ACTRAN        13A  O  8 39
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
     A          R GL009300F1
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
     A                                 23 37'F21=Print Accounts'
      *----------------------------------------------------------------
     A          R MSGSFL                    SFL
     A                                      TEXT('MESSAGE SUBFILE')
     A                                      SFLMSGRCD(24)
     A            SCMGKY                    SFLMSGKEY
     A            @PGM                      SFLPGMQ(10)
      *----------------------------------------------------------------
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A                                      TEXT('CONTROL RECORD')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            @PGM                      SFLPGMQ(10)
      *================================================================
