      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Edit journal entry batch posting')            *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL0121M# - Edit Journal Entry Batch posting             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. MD027587           Date 19Jun14               *
      *  Prev Amend No. AR787620           Date 10Jun11               *
      *                 AR739731           Date 03Apr11               *
      *                 AR740098           Date 02Apr11               *
      *                 CER059             Date 19Jul10               *
      *                 BUG23742           Date 09May09               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 204679             Date 24Jul02               *
      *                 TDA025             Date 18Mar04               *
      *                 CRE019             Date 12Jan04               *
      *                 CGL029             Date 01Sep03               *
      *                 CGL013             Date 29Mar02               *
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 127199             Date  7Apr00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP032             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU013             Date 23Feb98               *
      *                 091729             Date 21Jan98               *
      *                 S01408             DATE 26JUN95               *
      *                 S01413             DATE 31JUL93               *
      *                 047414             DATE 17FEB92               *
      *                 S10736             DATE 09SEP92               *
      *                 E38270             DATE 22JUN92               *
      *                 S01340             DATE 19FEB92               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD027587 - Unable to used the C for Copy and T for Template  *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,GLH00128                                 *
      *             WNCPYSRC,GLH00129                                 *
      *             WNCPYSRC,GLH00130                                 *
      *             WNCPYSRC,GLH00131                                 *
      *             WNCPYSRC,GLH00132                                 *
      *             WNCPYSRC,GLH00133                                 *
      *             WNCPYSRC,GLH00134                                 *
      *             WNCPYSRC,GLH00135                                 *
      *             WNCPYSRC,GLH00136                                 *
      *             WNCPYSRC,GLH00137                                 *
      *             WNCPYSRC,GLH00138                                 *
      *             WNCPYSRC,GLH00139                                 *
      *             WNCPYSRC,GLH00140                                 *
      *             WNCPYSRC,GLH00141                                 *
      *             WNCPYSRC,GLH00142                                 *
      *             WNCPYSRC,GLH00143                                 *
      *             WNCPYSRC,GLH00144                                 *
      *             WNCPYSRC,GLH00145                                 *
      *             WNCPYSRC,GLH00146                                 *
      *             WNCPYSRC,GLH00147                                 *
      *             WNCPYSRC,GLH00148                                 *
      *             WNCPYSRC,GLH00149                                 *
      *             WNCPYSRC,GLH00150                                 *
      *             WNCPYSRC,GLH00151                                 *
      *             WNCPYSRC,GLH00152                                 *
      *             WNCPYSRC,GLH00168                                 *
      *             WNCPYSRC,GLH00153                                 *
      *             WNCPYSRC,GLH00154                                 *
      *             WNCPYSRC,GLH00155                                 *
      *             WNCPYSRC,GLH00156                                 *
      *             WNCPYSRC,GLH00157                                 *
      *             WNCPYSRC,GLH00158                                 *
      *             WNCPYSRC,GLH00159                                 *
      *             WNCPYSRC,GLH00160                                 *
      *             WNCPYSRC,GLH00161                                 *
      *             WNCPYSRC,GLH00162                                 *
      *             WNCPYSRC,GLH00209                                 *
      *             WNCPYSRC,GLH00210                                 *
      *             WNCPYSRC,GLH00163                                 *
      *             WNCPYSRC,GLH00164                                 *
      *             WNCPYSRC,GLH00165                                 *
      *             WNCPYSRC,GLH00166                                 *
      *  AR739731 - Journal Entry Postings Maintenance Error stucked  *
      *             on error (Child: AR739758)                        *
      *  AR740098 - Journal Entry Postings Maintenance unlabeled      *
      *             fields (Child: AR740099)                          *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23742 - Improper handling of Ext Narr from Batch Interface*
      *             Input                                             *
      *  204679 - Several fields should not be amendable if Private   *
      *  TDA025 - Program exits on initial menu after pressing F3 -   *
      *           Batch Summary. Fix will ensure that the batch       *
      *           summary will be shown after pressing F3.            *
      *  CRE019 - Cheque Processing and Maintenance                   *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL013 - MT94x Messages Generation                           *
      *  127199 - Journal Entries with no error are not saved when    *
      *           F3 is pressed. Fix is to ensure that valid entries  *
      *           are saved & only those invalid will be disregarded  *
      *           when F3 key is pressed.                             *
      *  CAP032 - Conversion of Journal Batch Entry inputs into       *
      *           modular structure to use as APIs.                   *
      *  CEU013 - EMU Account Posting Narrative changes               *
      *      091729 - Add hidden fields Prev Value Date and Prev Ccy  *
      *               Code, to allow GL0121M to determine if these    *
      *               fields have changed, for holiday validation.    *
      *      S01408 - ADDITION OF CORE HOOKS GL0121M#F1               *
      *                                      GL0121M#F2               *
      *                                      GL0121M#F3               *
      *                                      GL0121M#F4               *
      *                                      GL0121M#F5               *
      *                                      GL0121M#F6               *
      *                                      GL0121M#F7               *
      *                                      GL0121M#F8               *
      *      S01413 - RETAIL 3 INCORPORATION:- NUMBER OF ITEMS        *
      *               AVAILABLE.                                      *
      *      047414 - MULTI-BRANCH BATCHES WHEN MULTIBRANCH BATCH = N *
      *               CAUSE EOD CURRENCY OUT OF BALANCE PROBLEMS      *
      *      S10736 - ADD REVERSAL/VOID INDICATOR                     *
      *      E38270 - DO NOT DISPLAY BRANCH IF MBIN IS 'N'            *
      *      S01340 - MIS RELEASE 10 CHANGES                          *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A**Start of change CGL029
     A                                      DSPSIZ(*DS4)
     A**End of change CGL029
     A*=========================================================================
     A*                                                    /*S01340*/
     A*                                                    /*S01340*/
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(31 'Invalid: #1SEL')
     A                                      INDTXT(32 'Invalid: #RACID')
     A                                      INDTXT(33 'Invalid: #1CYCD')
     A                                      INDTXT(34 'Invalid: #1ACCD')
     A                                      INDTXT(35 'Invalid: #1ACSN')
     A                                      INDTXT(36 'Invalid: #1IBCA')
     A                                      INDTXT(37 'Invalid: #RFMPA')
     A                                      INDTXT(38 'Invalid: #1DCIN')
     A                                      INDTXT(39 'Invalid: #RNRTV')
     A                                      INDTXT(40 'Invalid: #1TRTY')
     A                                      INDTXT(41 'Invalid: #1TSTY')
     A                                      INDTXT(42 'Invalid: #RVDDS')
     A                                      INDTXT(43 'Invalid: #1CQNB')
     A                                      INDTXT(44 'Invalid: #1SWCR')
     A                                      INDTXT(45 'Invalid: #1PRCN')
     A                                      INDTXT(46 'Invalid: #RASCD')
     A                                      INDTXT(47 'Invalid: #1BKCD')
     A                                      INDTXT(48 'Invalid: #1RVDI')
     A                                      INDTXT(49 'Invalid: #RNITD')
     A                                      INDTXT(50 'Invalid: #1RINO')
     A*                                           Begin of /*CEU013*/
     A                                      INDTXT(51 'Invalid: #1OCCY')
     A                                      INDTXT(52 'Invalid: #RFMOA')
     A*                                           End of   /*CEU013*/
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0121M#F1
     A*                                                    End of   /*S01408*/
     A  84                                  SFLNXTCHG
     A*/COPY WNCPYSRC,GL0121M#09
     A*                                           Begin of /*CEU013*/
     A************#1DBRC       164   H      TEXT('Existing record fields')
      *                                              Begin /CAP032/
     A*********** #1DBRC       175   H      TEXT('Existing record fields')
     A**Start of change TDA025
     A**********  #1DBRC       242   H      TEXT('Existing record fields')                    TDA025
     A**Start of change CRE019
     A**********  #1DBRC       262   H      TEXT('Existing record fields')                    CRE019
     A            #1DBRC       274   H      TEXT('Existing record fields')
     A**End of change CRE019
     A**End of change TDA025
      *                                              End   /CAP032/
     A*                                           End of   /*CEU013*/
     A            #RIEST         1   H      TEXT('Item error state')
     A            #1BTNB         3   H      TEXT('Batch number')
     A            #1BINB         3  0H      TEXT('Batch item no.')
     A            #1CUST         6   H      TEXT('Customer number')
     A            #1NNBI         2   H      TEXT('Nostro number (Item)')
     A            #1RACN        10  0H      TEXT('Retail account number')
     A            #1PTAM        13  0H      TEXT('Posting amount')
     A            #1RINE         1   H      TEXT('Retail ind.: Error')
     A            #1PTDT         5  0H      TEXT('Posting date')
     A            #1NVCD         2   H      TEXT('Narrative code')
     A            #1NRDC        30   H      TEXT('Narrative description')
     A            #1ACTY         1   H      TEXT('Account type')
     A            #1VLDT         5  0H      TEXT('Value date')
     A*                                              Begin /091729/
     A            #1PVDT         5  0H      TEXT('Prev Value date')
     A            #1PCCY         3   H      TEXT('Prev Currency code')
     A*                                              End   /091729/
     A            #1ACNB         6   H      TEXT('Assoc. Customer number')
     A            #1COIN         1   H      TEXT('Customer originated ind.')
     A            #RREVI         1   H      TEXT('Reversal/void indicator')
     A            #1SPFC         8  0H      TEXT('Spread Factor')
     A            #1NITM         5  0H      TEXT('Number of Items')
     A            #1BCBH         3   H      TEXT('Branch Code (Batch Headr)')
     A            #1RADP         1   H      TEXT('Retail advice printed')
     A            #1SHPI         1   H      TEXT('Shadow post indicator')
     A            #1RRNM         5  0H      TEXT('Relative rec no of memos')
     A            #1RNBE         1   H      TEXT('Retail Number Entered')
     A            #1RARC        10   H      TEXT('Retail Ac Referral Cond.')
     A*                                           Begin of /*CEU013*/
     A            #1OAMT        15  0H      TEXT('Original amount')
     A            #1SVAI        10   H      TEXT('Screen saved Acc idntifer')
     A            #1SVCY         3   H      TEXT('Screen saved Currency code')
     A**Start of change CGL029
     A**********  #1SVAC         4   H      TEXT('Screen saved Account code')                 CGL029
     A            #1SVAC        10   H      TEXT('Screen saved Account code')
     A**End of change CGL029
     A            #1SVCS         2   H      TEXT('Screen saved Acc Seq no.')
     A            #1SVBC         3   H      TEXT('Screen saved Input Branch')
     A            #1SVOA        16   H      TEXT('Screen saved Org amt')
     A*                                           End of   /*CEU013*/
      **Start of change MD027587
     A            #1PDID        15  0H      TEXT('Pre-Deal Check ID')
      **End of change MD027587
     A*                                              Begin /127199/
     A            #ERRIN         1   H      TEXT('Error Indicator')
     A*                                              End   /127199/
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0121M#F2
     A*                                                    End of   /*S01408*/
     A            #1SEL          1   B  4  2
     A**Start of change AR739731
     A**********                            VALUES('C' +                                    AR739731
     A**********                                   'T' +                                    AR739731
     A**********                                   'D')                                     AR739731
     A**********                            CHECK(AB)                                       AR739731
     A**End of change AR739731
     A*/COPY WNCPYSRC,GLH00128
     A  78                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A  78N31                               DSPATR(ND)
     A  78
     AO 31                                  COLOR(WHT)
     A N78N31                               DSPATR(UL HI) COLOR(GRN)
     A*/COPY WNCPYSRC,GLH00129
     A            #RACID        10   B  4  4TEXT('Account Identifier')
     A*/COPY WNCPYSRC,GLH00130
     A**Start of change 204679
     A  65
     A**87******                            DSPATR(PR)                                        204679
     AO 87                                  DSPATR(PR)
     A**End of change 204679
     A  32                                  DSPATR(RI PC)
     A  87
     A**Start of change 204679
     AO 65
     A**End of change 204679
     AO 32                                  COLOR(WHT)
     A**Start of change 204679
     A*N87N32***                            DSPATR(UL) COLOR(GRN)                             204679
     A N87N32N65                            DSPATR(UL) COLOR(GRN)
     A**End of change 204679
     A N32                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00131
     A            #1CYCD         3   B  4 15TEXT('Currency code')
     A*/COPY WNCPYSRC,GLH00132
     A**Start of change 204679
     A  65
     A**87******                            DSPATR(PR)                                        204679
     AO 87                                  DSPATR(PR)
     A**End of change 204679
     A  33                                  DSPATR(RI PC)
     A  87
     A**Start of change 204679
     AO 65
     A**End of change 204679
     AO 33                                  COLOR(WHT)
     A**Start of change 204679
     A*N87N33***                            DSPATR(UL) COLOR(GRN)                             204679
     A N87N33N65                            DSPATR(UL) COLOR(GRN)
     A**End of change 204679
     A N33                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00133
     A**Start of change CGL029
     A**********  #1ACCD         4   B  4 19TEXT('Account code')                              CGL029
     A            #1ACCD        10   B  4 19TEXT('Account code')
     A**End of change CGL029
     A*/COPY WNCPYSRC,GLH00134
     A**Start of change 204679
     A  65
     A**87******                            DSPATR(PR)                                        204679
     AO 87                                  DSPATR(PR)
     A**End of change 204679
     A  34                                  DSPATR(RI PC)
     A  87
     A**Start of change 204679
     AO 65
     A**End of change 204679
     AO 34                                  COLOR(WHT)
     A**Start of change 204679
     A*N87N34***                            DSPATR(UL) COLOR(GRN)                             204679
     A N87N34N65                            DSPATR(UL) COLOR(GRN)
     A**End of change 204679
     A N34                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00135
     A**Start of change CGL029
     A**********  #1ACSN         2   B  4 24TEXT('Account sequence number')                   CGL029
     A            #1ACSN         2   B  4 30TEXT('Account sequence number')
     A**End of change CGL029
     A*/COPY WNCPYSRC,GLH00136
     A**Start of change 204679
     A  65
     A**87******                            DSPATR(PR)                                        204679
     AO 87                                  DSPATR(PR)
     A**End of change 204679
     A  35                                  DSPATR(RI PC)
     A  87
     A**Start of change 204679
     AO 65
     A**End of change 204679
     AO 35                                  COLOR(WHT)
     A**Start of change 204679
     A*N87N35***                            DSPATR(UL) COLOR(GRN)                             204679
     A N87N35N65                            DSPATR(UL) COLOR(GRN)
     A**End of change 204679
     A N35                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00137
     A**Start of change CGL029
     A**********  #1IBCA         3   B  4 28TEXT('Input Branch Code - Alpha')                 CGL029
     A            #1IBCA         3   B  4 34TEXT('Input Branch Code - Alpha')
     A**End of change CGL029
     A*/COPY WNCPYSRC,GLH00138
     A  77                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC) COLOR(WHT)
     A  77N36                               DSPATR(ND)
     A N77N36                               DSPATR(UL HI)
     A N36                                  COLOR(GRN)
     A*/COPY WNCPYSRC,GL0121M#12
     A**Start of change CGL029
     A**********  #RFMPA        14   B  4 33TEXT('Formatted Posting amount')                  CGL029
     A            #RFMPA        14   B  4 39TEXT('Formatted Posting amount')
     A**End of change CGL029
     A*/COPY WNCPYSRC,GLH00139
     A**Start of change 204679
     A  65
     A**87******                            DSPATR(PR)                                        204679
     AO 87                                  DSPATR(PR)
     A**End of change 204679
     A  37                                  DSPATR(RI PC)
     A  87
     A**Start of change 204679
     AO 65
     A**End of change 204679
     AO 37                                  COLOR(WHT)
     A**Start of change 204679
     A*N87N37***                            DSPATR(UL) COLOR(GRN)                             204679
     A N87N37N65                            DSPATR(UL) COLOR(GRN)
     A**End of change 204679
     A N37                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00140
     A**Start of change CGL029
     A**********  #1DCIN         1   B  4 48TEXT('Debit/credit indicator')                    CGL029
     A            #1DCIN         1   B  4 54TEXT('Debit/credit indicator')
     A**End of change CGL029
     A*/COPY WNCPYSRC,GLH00141
     A**Start of change 204679
     A  65
     A**87******                            DSPATR(PR)                                        204679
     AO 87                                  DSPATR(PR)
     A**End of change 204679
     A  38                                  DSPATR(RI PC)
     A  87
     A**Start of change 204679
     AO 65
     A**End of change 204679
     AO 38                                  COLOR(WHT)
     A**Start of change 204679
     A*N87N38***                            DSPATR(UL) COLOR(GRN)                             204679
     A N87N38N65                            DSPATR(UL) COLOR(GRN)
     A**End of change 204679
     A N38                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GL0121M#01
     A**Start of change CGL029
     A**********  #RNRTV        30   B  4 50TEXT('Narrative')                                 CGL029
     A            #RNRTV        30   B  4 56TEXT('Narrative')
     A**End of change CGL029
     A*/COPY WNCPYSRC,GL0121M#02
     A  87                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A  87
     AO 39                                  COLOR(WHT)
     A N87N39                               DSPATR(UL) COLOR(GRN)
     A N39                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00142
     A                                  5  4'Type'
     A                                      COLOR(TRQ)
     A            #1TRTY         5   B  5  9TEXT('Transaction type')
     A*/COPY WNCPYSRC,GLH00143
     A**Start of change 204679
     A  65
     A**87******                            DSPATR(PR)                                        204679
     AO 87                                  DSPATR(PR)
     A**End of change 204679
     A  40                                  DSPATR(RI PC)
     A  87
     A**Start of change 204679
     AO 65
     A**End of change 204679
     AO 40                                  COLOR(WHT)
     A**Start of change 204679
     A*N87N40***                            DSPATR(UL) COLOR(GRN)                             204679
     A N87N40N65                            DSPATR(UL) COLOR(GRN)
     A**End of change 204679
     A N40                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GL0121M#04
     A                                  5 17'Sub Type'
     A                                      COLOR(TRQ)
     A            #1TSTY        10   B  5 26TEXT('Transaction (Sub - Type)')
     A*/COPY WNCPYSRC,GLH00144
     A  87                                  DSPATR(PR)
     A  41                                  DSPATR(RI PC)
     A  87
     AO 41                                  COLOR(WHT)
     A N87N41                               DSPATR(UL) COLOR(GRN)
     A N41                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00145
     A                                  5 38'Value date'
     A                                      COLOR(TRQ)
     A            #RVDDS         6   B  5 49TEXT('Value date (display)')
     A*/COPY WNCPYSRC,GL0121M#10
     A**Start of change 204679
     A  65
     A**87******                            DSPATR(PR)                                        204679
     A*/COPY WNCPYSRC,GL0121M#11
     AO 87                                  DSPATR(PR)
     A**End of change 204679
     A  42                                  DSPATR(RI PC)
     A  87
     A**Start of change 204679
     AO 65
     A**End of change 204679
     AO 42                                  COLOR(WHT)
     A**Start of change 204679
     A*N87N42***                            DSPATR(UL) COLOR(GRN)                             204679
     A N87N42N65                            DSPATR(UL) COLOR(GRN)
     A**End of change 204679
     A N42                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00146
     A                                  5 57'Cheque'
     A                                      COLOR(TRQ)
     A            #1CQNB         8   B  5 64TEXT('Cheque number')
     A*/COPY WNCPYSRC,GLH00147
     A  87                                  DSPATR(PR)
     A  43                                  DSPATR(RI PC)
     A  87
     AO 43                                  COLOR(WHT)
     A N87N43                               DSPATR(UL) COLOR(GRN)
     A N43                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00148
     A                                  6  4'SWIFT'
     A                                      COLOR(TRQ)
     A            #1SWCR        16   B  6 10TEXT('Swift common reference')
     A*/COPY WNCPYSRC,GLH00149
     A  87                                  DSPATR(PR)
     A  44                                  DSPATR(RI PC)
     A  87
     AO 44                                  COLOR(WHT)
     A N87N44                               DSPATR(UL) COLOR(GRN)
     A N44                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GL0121M#05
     A                                  6 27'Profit centre'
     A  76                                  DSPATR(ND) COLOR(WHT)
     A N76                                  COLOR(TRQ)
     A            #1PRCN         4   B  6 41TEXT('Profit centre')
     A*/COPY WNCPYSRC,GLH00150
     A  76                                  DSPATR(PR)
     A  45                                  DSPATR(RI PC)
     A  76N45                               DSPATR(ND)
     A  76
     AO 45                                  COLOR(WHT)
     A N76N45                               DSPATR(UL HI) COLOR(GRN)
     A*/COPY WNCPYSRC,GL0121M#13
     A                                  6 46'Associated Customer'
     A                                      COLOR(TRQ)
     A            #RASCD        10   B  6 66TEXT('Associated Cust - Display')
     A*/COPY WNCPYSRC,GLH00151
     A  87                                  DSPATR(PR)
     A  46                                  DSPATR(RI PC)
     A  87
     AO 46                                  COLOR(WHT)
     A N87N46                               DSPATR(UL) COLOR(GRN)
     A N46                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GL0121M#06
     A                                  7  4'Book Code'
     A*/COPY WNCPYSRC,GLH00152
     A                                      COLOR(TRQ)
     A            #1BKCD         2   B  7 14TEXT('Book Code')
     A*/COPY WNCPYSRC,GLH00168
     A  87                                  DSPATR(PR)
     A  47                                  DSPATR(RI PC)
     A  87
     AO 47                                  COLOR(WHT)
     A N87N47                               DSPATR(UL) COLOR(GRN)
     A N47                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00153
     A                                  7 17'Void'
     A                                      COLOR(TRQ)
     A            #1RVDI         1   B  7 22TEXT('Reverse/Void Indicator')
     A*/COPY WNCPYSRC,GLH00154
     A  87                                  DSPATR(PR)
     A  48                                  DSPATR(RI PC)
     A  87
     AO 48                                  COLOR(WHT)
     A N87N48                               DSPATR(UL) COLOR(GRN)
     A N48                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00155
     A                                  7 25'No. Items'
     A  75                                  DSPATR(ND) COLOR(WHT)
     A N75                                  COLOR(TRQ)
     A            #RNITD         5   B  7 35TEXT('Number of items - display')
     A*/COPY WNCPYSRC,GLH00156
     A                                      CHECK(LC)
     A  75                                  DSPATR(PR)
     A  49                                  DSPATR(RI PC)
     A  75N49                               DSPATR(ND)
     A  75
     AO 49                                  COLOR(WHT)
     A N75N49                               DSPATR(UL HI) COLOR(GRN)
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0121M#F3
     A*                                                    End of   /*S01408*/
     A                                  7 41'Override'
     A  74                                  DSPATR(ND) COLOR(WHT)
     A N74                                  COLOR(TRQ)
     A            #1RINO         1   B  7 50TEXT('Retail ind.: Override')
     A*/COPY WNCPYSRC,GLH00157
     A  74                                  DSPATR(PR)
     A  50                                  DSPATR(RI PC)
     A  74N50                               DSPATR(ND)
     A  74
     AO 50                                  COLOR(WHT)
     A N74N50                               DSPATR(UL HI) COLOR(RED)
     A*/COPY WNCPYSRC,GLH00158
     A*                                           Begin of /*CEU013*/
     A                                  8  4'Original Ccy/Amount'
     A  66                                  DSPATR(ND)
     A N66                                  COLOR(TRQ)
     A            #1OCCY         3   B  8 24TEXT('Original Currency')
     A*/COPY WNCPYSRC,GLH00159
     A  66                                  DSPATR(PR)
     A  51                                  DSPATR(RI PC)
     A  66                                  DSPATR(ND)
     A  66
     AO 51                                  COLOR(WHT)
     A N66N51                               DSPATR(UL HI) COLOR(RED)
     A*/COPY WNCPYSRC,GLH00160
     A            #RFMOA        16   B  8 28TEXT('Original Amount')
     A*/COPY WNCPYSRC,GLH00161
     A  66                                  DSPATR(PR)
     A  52                                  DSPATR(RI PC)
     A  66                                  DSPATR(ND)
     A  66
     AO 52                                  COLOR(WHT)
     A N66N52                               DSPATR(UL HI) COLOR(RED)
     A*/COPY WNCPYSRC,GLH00162
      ** Start of Change CGL013
     A                                  8 45'Additional Information'
      **Start of change BUG23742
     A*N67******                            DSPATR(ND)                                      BUG23742
     A**67******                            COLOR(TRQ)                                      BUG23742
     A N67N54                               DSPATR(ND)
     AO 67 54                               COLOR(TRQ)
      **End of change BUG23742
     A            #1ADIN         1   B  8 68TEXT('Additional Information')
     A*/COPY WNCPYSRC,GLH00209
      **Start of change BUG23742
     A*N67******                            DSPATR(PR ND)                                   BUG23742
     A**68******                            DSPATR(RI PC)                                   BUG23742
     A**67N68***                            DSPATR(UL HI) COLOR(GRN)                        BUG23742
     A N67N54                               DSPATR(PR ND)
     A  68
     AO 67 54                               DSPATR(RI PC)
     A N68                                  DSPATR(UL HI) COLOR(GRN)
      **End of change BUG23742
     A*/COPY WNCPYSRC,GLH00210
     A            #1AIPI         1   O  8 70TEXT('Additional Info Indicator')
     A N67                                  DSPATR(ND)
     A  67                                  DSPATR(HI) COLOR(GRN)
      ** End of Change CGL013
      **Start of change BUG23742
     A            #1AIP1         1   O  8 72TEXT('Additional Info1 Indicator')
     A N67N57                               DSPATR(ND)
     AO 67 57                               DSPATR(HI) COLOR(GRN)
      **End of change BUG23742
     A************#RIEMG        76      8  4TEXT('Item error message')
     A*/COPY WNCPYSRC,GL0121M#03
     A            #RIEMG        76      9  4TEXT('Item error message')
     A*                                           End of   /*CEU013*/
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0121M#F4
     A*                                                    End of   /*S01408*/
     A  73                                  COLOR(RED)
     A N73                                  COLOR(WHT)
     A*/COPY WNCPYSRC,GL0121M#14
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0121M#F5
     A*                                                    End of   /*S01408*/
     A                                      SFLPAG(03) SFLSIZ(04)
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0121M#F6
     A*                                                    End of   /*S01408*/
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A*                                              Begin /127199/
     A***********                           CA03(03 'Exit.')
     A                                      CF03(03 'Exit.')
     A*                                              End   /127199/
     A N82                                  ROLLUP(27 'Next page.')
     A*/COPY WNCPYSRC,GLH00163
     A                                      CF05(05 'Reset.')
     A*                                                    /*S01340*/
     A*                                                    /*S01340*/
     A                                      CF09(09 'Go to ''Add'' mode.')
     A                                      CF16(16 'CF16.')
     A*/COPY WNCPYSRC,GLH00164
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A*                                                    /*S01340*/
     A*/COPY WNCPYSRC,GL0121M#07
     A*/COPY WNCPYSRC,GL0121M#08
     A*                                                    /*S10736*/
     A*                                                    /*S10736*/
     A*                                                    /*S01413*/
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0121M#F7
     A*                                                    End of   /*S01408*/
     A*                                           Begin of /*CEU013*/
     A*                                           End of   /*CEU013*/
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0121M#F8
     A*                                                    End of   /*S01408*/
     A*                                                    /*S01413*/
     A*                                                    /*S01340*/
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'GL0121M1'
     A                                      COLOR(WHT)
     A            ##MRDT         7      1 72TEXT('Midas Rundate')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A                                  2 24'Journal Entry Postings Maintenance'
     A                                      DSPATR(HI) COLOR(WHT)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            #2BTNB         3   H      TEXT('Batch number')
     A            #2BINB         3  0H      TEXT('Batch item no.')
     A*/COPY WNCPYSRC,GL0121M#15
     A                                  3  9'Account number'
     A                                      COLOR(TRQ)
     A**Start of change CGL029
     A**********                        3 27'Branch'                                          CGL029
     A                                  3 33'Branch'
     A**End of change CGL029
     A  79                                  DSPATR(ND)
     A N79                                  COLOR(TRQ)
     A N25                                  OVRATR
     A**Start of change CGL029
     A**********                        3 36'Amount                Narrative'                 CGL029
     A**Start of change AR740098
     A**********                        3 42'Amount                Narrative'               AR740098
     A                                  3 42'Amount'
     A                                      COLOR(TRQ)
     A                                  3 53'D/C'
     A                                      COLOR(TRQ)
     A                                  3 64'Narrative'
     A**End of change AR740098
     A**End of change CGL029
     A                                      COLOR(TRQ)
     A*/COPY WNCPYSRC,GL0121M#16
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A*/COPY WNCPYSRC,GLH00165
     A**Start of change CGL029
     A**********  ##CTX1        78     22  2TEXT('Command text 1')                            CGL029
     A            ##CTX1        78     25  2TEXT('Command text 1')
     A**End of change CGL029
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**89******                       23  2'F3=Batch Summary   F5=Refresh Scr-               CGL029
     A**********                            een   F9=Go to ''Change'' mode'                   CGL029
     A  89                             26  2'F3=Batch Summary   F5=Refresh Scr-
     A                                      een   F9=Go to ''Change'' mode'
     A**End of change CGL029
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A*N89******                       23  2'F3=Batch Summary   F5=Refresh Scr-               CGL029
     A**********                            een   F9=Go to ''Add'' mode  F16=F-               CGL029
     A**********                            ind Error'                                        CGL029
     A N89                             26  2'F3=Batch Summary   F5=Refresh Scr-
     A                                      een   F9=Go to ''Add'' mode  F16=F-
     A                                      ind Error'
     A**End of change CGL029
     A                                      COLOR(WHT)
     A*/COPY WNCPYSRC,GLH00166
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A**Start of change CGL029
     A**********                            SFL SFLMSGRCD(24)                                 CGL029
     A                                      SFL SFLMSGRCD(27)
     A**End of change CGL029
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
