      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Edit abbreviated posting')                    *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL0122M# - Edit abbreviated posting                     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD048429           Date 30Oct17               *
      *                 AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 237620             Date 23Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP032             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01413             Date 28Jul93               *
      *                 S01340             Date 19Feb92               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD048429 - Batch item cannot be deleted.                     *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,GLH00096                                 *
      *             WNCPYSRC,GLH00097                                 *
      *             WNCPYSRC,GLH00098                                 *
      *             WNCPYSRC,GLH00099                                 *
      *             WNCPYSRC,GLH00100                                 *
      *             WNCPYSRC,GLH00101                                 *
      *             WNCPYSRC,GLH00102                                 *
      *             WNCPYSRC,GLH00103                                 *
      *             WNCPYSRC,GLH00104                                 *
      *             WNCPYSRC,GLH00105                                 *
      *             WNCPYSRC,GLH00106                                 *
      *             WNCPYSRC,GLH00107                                 *
      *             WNCPYSRC,GLH00108                                 *
      *             WNCPYSRC,GLH00109                                 *
      *             WNCPYSRC,GLH00110                                 *
      *             WNCPYSRC,GLH00111                                 *
      *             WNCPYSRC,GLH00112                                 *
      *             WNCPYSRC,GLH00113                                 *
      *             WNCPYSRC,GLH00114                                 *
      *             WNCPYSRC,GLH00115                                 *
      *             WNCPYSRC,GLH00116                                 *
      *  237620 - Increase 1DBRC field to 250.                        *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP032 - Conversion of Journal Batch Entry inputs into       *
      *           modular structure to use as APIs.                   *
      *  S01413 - RETAIL 3 INCORPORATION:- REGEN AND RECOMPILE FOR    *
      *           NO. OF ITEMS ADDED TO GLJENPPD.                     *
      *  S01340 - MIS RELEASE 10 CHANGES                              *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A**Start of change CGL029
     A                                      DSPSIZ(*DS4)
     A**End of change CGL029
     A*=========================================================================
     A*                                                    /*S01340*/
     A*                                                    /*S01340*/
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(31 'Invalid: #1SEL')
     A                                      INDTXT(32 'Invalid: #RACID')
     A                                      INDTXT(33 'Invalid: #1CYCD')
     A                                      INDTXT(34 'Invalid: #1ACCD')
     A                                      INDTXT(35 'Invalid: #1ACSN')
     A                                      INDTXT(36 'Invalid: #RFMPA')
     A                                      INDTXT(37 'Invalid: #1DCIN')
     A                                      INDTXT(38 'Invalid: #RNRTV')
     A                                      INDTXT(39 'Invalid: #1RINO')
     A*/COPY WNCPYSRC,GL0122M#02
     A  84                                  SFLNXTCHG
      *                                              Begin /CAP032/
     A*********** #1DBRC       164   H      TEXT('Existing record fields')
     A**Start of 237620
     A**********  #1DBRC       242   H      TEXT('Existing record fields')                    237620
     A**Start of change MD048429
     A**********  #1DBRC       250   H      TEXT('Existing record fields')                  MD048429
     A            #1DBRC       265   H      TEXT('Existing record fields')
     A**End of change MD048429
     A**End of 237620
      *                                              End   /CAP032/
     A            #RIEST         1   H      TEXT('Item error state')
     A            #1BTNB         3   H      TEXT('Batch number')
     A            #1BINB         3  0H      TEXT('Batch item no.')
     A            #1IBCA         3   H      TEXT('Input Branch Code - Alpha')
     A            #1CUST         6   H      TEXT('Customer number')
     A            #1NNBI         2   H      TEXT('Nostro number (Item)')
     A            #1RACN        10  0H      TEXT('Retail account number')
     A            #1PTAM        13  0H      TEXT('Posting amount')
     A            #1RINE         1   H      TEXT('Retail ind.: Error')
     A            #1PTDT         5  0H      TEXT('Posting date')
     A            #1NVCD         2   H      TEXT('Narrative code')
     A            #1NRDC        30   H      TEXT('Narrative description')
     A            #1ACTY         1   H      TEXT('Account type')
     A            #1TRTY         5   H      TEXT('Transaction type')
     A            #1CQNB         8   H      TEXT('Cheque number')
     A            #1VLDT         5  0H      TEXT('Value date')
     A*/COPY WNCPYSRC,GL0122M#08
     A            #1ACNB         6   H      TEXT('Assoc. Customer number')
     A*/COPY WNCPYSRC,GL0122M#09
     A            #RVDDS         6   H      TEXT('Value date (display)')
     A            #1COIN         1   H      TEXT('Customer originated ind.')
     A            #1PRCN         4   H      TEXT('Profit centre')
     A            #1SWCR        16   H      TEXT('Swift common reference')
     A            #RIEMG        76   H      TEXT('Item error message')
     A            #1BCBH         3   H      TEXT('Branch Code (Batch Headr)')
     A            #1RADP         1   H      TEXT('Retail advice printed')
     A            #1SHPI         1   H      TEXT('Shadow post indicator')
     A            #1RRNM         5  0H      TEXT('Relative rec no of memos')
     A            #1RNBE         1   H      TEXT('Retail Number Entered')
     A            #1RARC        10   H      TEXT('Retail Ac Referral Cond.')
     A            #1TSTY        10   H      TEXT('Transaction (Sub - Type)')
     A*/COPY WNCPYSRC,GL0122M#03
     A            #1BKCD         2   H      TEXT('Book Code')
     A*/COPY WNCPYSRC,GL0122M#04
     A            #1SPFC         8  0H      TEXT('Spread Factor')
     A            #1RVDI         1   H      TEXT('Reverse/Void Indicator')
     A            #1NITM         5  0H      TEXT('Number of Items')
     A*/COPY WNCPYSRC,GL0122M#01
     A            #1SEL          1   B  4  2
     A                                      VALUES('C' +
     A                                             'T' +
     A                                             'D')
     A                                      CHECK(AB)
     A*/COPY WNCPYSRC,GLH00096
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A  79N31                               DSPATR(ND)
     A  79
     AO 31                                  COLOR(WHT)
     A N79N31                               DSPATR(UL HI) COLOR(GRN)
     A*/COPY WNCPYSRC,GLH00097
     A            #RACID        10   B  4  4TEXT('Account Identifier')
     A*/COPY WNCPYSRC,GLH00098
     A  87                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  87
     AO 32                                  COLOR(WHT)
     A N87N32                               DSPATR(UL) COLOR(GRN)
     A N32                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00099
     A            #1CYCD         3   B  4 15TEXT('Currency code')
     A*/COPY WNCPYSRC,GLH00100
     A  87                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A  87
     AO 33                                  COLOR(WHT)
     A N87N33                               DSPATR(UL) COLOR(GRN)
     A N33                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00101
     A**Start of change CGL029
     A**********  #1ACCD         4   B  4 19TEXT('Account code')                              CGL029
     A            #1ACCD        10   B  4 19TEXT('Account code')
     A**End of change CGL029
     A*/COPY WNCPYSRC,GLH00102
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A  87
     AO 34                                  COLOR(WHT)
     A N87N34                               DSPATR(UL) COLOR(GRN)
     A N34                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00103
     A**Start of change CGL029
     A**********  #1ACSN         2   B  4 24TEXT('Account sequence number')                   CGL029
     A            #1ACSN         2   B  4 30TEXT('Account sequence number')
     A**End of change CGL029
     A*/COPY WNCPYSRC,GLH00104
     A  87                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A  87
     AO 35                                  COLOR(WHT)
     A N87N35                               DSPATR(UL) COLOR(GRN)
     A N35                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00105
     A**Start of change CGL029
     A**********  #RFMPA        14   B  4 27TEXT('Formatted Posting amount')                  CGL029
     A            #RFMPA        14   B  4 33TEXT('Formatted Posting amount')
     A**End of change CGL029
     A*/COPY WNCPYSRC,GLH00106
     A  87                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A  87
     AO 36                                  COLOR(WHT)
     A N87N36                               DSPATR(UL) COLOR(GRN)
     A N36                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00107
     A**Start of change CGL029
     A**********  #1DCIN         1   B  4 42TEXT('Debit/credit indicator')                    CGL029
     A            #1DCIN         1   B  4 48TEXT('Debit/credit indicator')
     A**End of change CGL029
     A*/COPY WNCPYSRC,GLH00108
     A  87                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A  87
     AO 37                                  COLOR(WHT)
     A N87N37                               DSPATR(UL) COLOR(GRN)
     A N37                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GLH00109
     A**Start of change CGL029
     A**********  #RNRTV        30   B  4 44TEXT('Narrative')                                 CGL029
     A            #RNRTV        30   B  4 50TEXT('Narrative')
     A**End of change CGL029
     A*/COPY WNCPYSRC,GLH00110
     A  87                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A  87
     AO 38                                  COLOR(WHT)
     A N87N38                               DSPATR(UL) COLOR(GRN)
     A N38                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GL0122M#05
     A                                  5  4'O''ride'
     A  78                                  DSPATR(ND) COLOR(WHT)
     A N78                                  COLOR(TRQ)
     A            #1RINO         1   B  5 11TEXT('Retail ind.: Override')
     A*/COPY WNCPYSRC,GLH00111
     A  78                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A  78N39                               DSPATR(ND)
     A  78
     AO 39                                  COLOR(WHT)
     A N78N39                               DSPATR(UL HI) COLOR(RED)
     A*/COPY WNCPYSRC,GLH00112
     A            #RIEMA        67      5 13TEXT('Item error msg. abbrev.')
     A  77                                  COLOR(RED)
     A N77                                  COLOR(WHT)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A**Start of change CGL029
     A**********                            SFLPAG(08) SFLSIZ(09)                             CGL029
     A                                      SFLPAG(10) SFLSIZ(11)
     A**End of change CGL029
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A*/COPY WNCPYSRC,GLH00113
     A                                      CF05(05 'Reset.')
     A*                                                    /*S01340*/
     A*                                                    /*S01340*/
     A                                      CF09(09 'Go to ''Add'' mode.')
     A*/COPY WNCPYSRC,GLH00114
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A*                                                    /*S01340*/
     A*                                                    /*S01340*/
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'GL0122M1'
     A                                      COLOR(WHT)
     A            ##MRDT         7      1 72TEXT('Midas Rundate')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A                                  2 18'Abbreviated Journal Entry Posting-
     A                                      s Maintenance'
     A                                      DSPATR(HI) COLOR(WHT)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            #2BTNB         3   H      TEXT('Batch number')
     A            #2BINB         3  0H      TEXT('Batch item no.')
     A**Start of change CGL029
     A**********                        3  4'----Account number---- -----Amoun-               CGL029
     A**********                            t----- ----------Narrative---------               CGL029
     A                                  3  4'-------Account number------- -----
     A                                      -Amount----- ----------Narrative---
     A**End of change CGL029
     A*/COPY WNCPYSRC,GL0122M#06
     A**Start of change CGL029
     A**********                            ---'                                              CGL029
     A                                      ---------'
     A**End of change CGL029
     A*/COPY WNCPYSRC,GL0122M#07
     A                                      COLOR(TRQ)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A*/COPY WNCPYSRC,GLH00115
     A**Start of change CGL029
     A**********  ##CTX1        78     22  2TEXT('Command text 1')                            CGL029
     A            ##CTX1        78     25  2TEXT('Command text 1')
     A**End of change CGL029
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A**89******                       23  2'F3=Batch Summary   F5=Refresh Scr-               CGL029
     A**********                            een   F9=Go to ''Change'' mode'                   CGL029
     A  89                             26  2'F3=Batch Summary   F5=Refresh Scr-
     A                                      een   F9=Go to ''Change'' mode'
     A**End of change CGL029
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A*N89******                       23  2'F3=Batch Summary   F5=Refresh Scr-               CGL029
     A**********                            een   F9=Go to ''Add'' mode'                      CGL029
     A N89                             26  2'F3=Batch Summary   F5=Refresh Scr-
     A                                      een   F9=Go to ''Add'' mode'
     A**End of change CGL029
     A                                      COLOR(WHT)
     A*/COPY WNCPYSRC,GLH00116
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A**Start of change CGL029
     A**********                            SFL SFLMSGRCD(24)                                 CGL029
     A                                      SFL SFLMSGRCD(27)
     A**End of change CGL029
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
