/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas JE Posting Extension Window')                    *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL0123#0 - Extended narr. description - Window DSPF     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. EML102  *CREATE    Date 28Nov19               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  EML102 - Upgrade of UNL007 to FB2.1 SP21                     *
      *       UNL007 - Extended Narratives                            *
      *       BUG24535 - Application error in Account Movements       *
      *                  and Account Master Enquiry                   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(*DS3  *DS4)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
     A                                      HELP

     A          R GL0123F0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')

     A                                      WINDOW(#MSGCTL)
     A                                      OVERLAY
     A                                      KEEP
     A                                      PUTOVR

     A                                      RTNDTA

     A                                      CA03(03 'Exit')
     A N24                                  CF05(05 ' Reset')
     A  21                                  CF10(10 ' Delete')
     A  22                                  CF11(11 ' Retrieve')
     A N23                                  CF12(12 ' Previous')

     A            STITL         50   O  1  1
     A                                  3  1'Narr.des.  2:'
     A            #FNA02        35   B    +2OVRDTA
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                  4  1'Narr.des.  3:'
     A            #FNA03        35   B    +2
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                  5  1'Narr.des.  4:'
     A            #FNA04        35   B    +2
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                  6  1'Narr.des.  5:'
     A            #FNA05        35   B    +2
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                  7  1'Narr.des.  6:'
     A            #FNA06        35   B    +2
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                  8  1'Narr.des.  7:'
     A            #FNA07        35   B    +2
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                  9  1'Narr.des.  8:'
     A            #FNA08        35   B    +2
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                 10  1'Narr.des.  9:'
     A            #FNA09        35   B    +2
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                 11  1'Narr.des. 10:'
     A            #FNA10        35   B    +2
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                 12  1'Narr.des. 11:'
     A            #FNA11        35   B    +2
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                 13  1'Narr.des. 12:'
     A            #FNA12        35   B    +2
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                 14  1'Narr.des. 13:'
     A            #FNA13        35   B    +2
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                 15  1'Narr.des. 14:'
     A            #FNA14        35   B    +2
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                 16  1'Narr.des. 15:'
     A            #FNA15        35   B    +2
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                 17  1'Narr.des. 16:'
     A            #FNA16        35   B    +2
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                 18  1'Narr.des. 17:'
     A            #FNA17        35   B    +2
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
      *
     A            SFOOT         50A  O 19  1
     A                                      COLOR(BLU)
      *-------------------------------------------------------------------------
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(20)

     A            MSGKEY                    SFLMSGKEY
     A            WWPGM                     SFLPGMQ
      *-------------------------------------------------------------------------
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      WINDOW(&SWROW &SWCOL 21 55)
     A                                      USRRSTDSP
     A                                      SFLPAG(01) SFLSIZ(10)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  99
     AON99                                  SFLEND
     A            UFPGMN                    SFLPGMQ
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
      *-------------------------------------------------------------------------
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  2  1' '
      *-------------------------------------------------------------------------
