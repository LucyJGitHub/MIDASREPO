      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Edit Standing Journal posting')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL0181M# - Edit Standing Journal posting                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. MD039461           Date 26Jul16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. TDA029             Date 22Mar04               *
      *                 CGL029             Date 01Sep03               *
      *                 CGL013             Date 29Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP032             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC001             Date 01Feb96               *
      *                 S01408             Date 26Jun95               *
      *                 E38270             DATE 22JUN92               *
      *                 S01340             DATE 19FEB92               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD039461 - Batch items cannot be amended or deleted in       *
      *             Standing Journal Template. Amend field length of  *
      *             #1DBRC to match definition of LF/GLJENPL1.        *
      *  TDA029 - Program doesn't save the changes on template. Fix   *
      *           will ensure that amendments on journal entry tem-   *
      *           plate will be saved.                                *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL013 - MT94x Messages Generation                           *
      *  CAP032 - Conversion of Journal Batch Entry inputs into       *
      *           modular structure to use as APIs.                   *
      *  CAC001 - Profit Centre Accounting Development                *
      *           Amended to include #1NITM field in order to test    *
      *           changes made to GL0181M.                            *
      *      S01408 - ADDITION OF CORE HOOKS GL0181M#F1               *
      *                                      GL0181M#F2               *
      *                                      GL0181M#F3               *
      *                                      GL0181M#F4               *
      *                                      GL0181M#F5               *
      *                                      GL0181M#F6               *
      *                                      GL0181M#F7               *
      *      E38270 - DO NOT DISPLAY BRANCH IF MBIN IS 'N'            *
      *      S01340 - MIS RELEASE 10 CHANGES                          *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A**Start of change CGL029
     A                                      DSPSIZ(*DS4)
     A**End of change CGL029
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(31 'Invalid: #1SEL')
     A                                      INDTXT(32 'Invalid: #RACID')
     A                                      INDTXT(33 'Invalid: #1CYCD')
     A                                      INDTXT(34 'Invalid: #1ACCD')
     A                                      INDTXT(35 'Invalid: #1ACSN')
     A                                      INDTXT(36 'Invalid: #1IBCA')
     A                                      INDTXT(37 'Invalid: #RFMPA')
     A                                      INDTXT(38 'Invalid: #1DCIN')
     A                                      INDTXT(39 'Invalid: #RNRTV')
     A                                      INDTXT(40 'Invalid: #1TRTY')
     A                                      INDTXT(41 'Invalid: #1TSTY')
     A                                      INDTXT(42 'Invalid: #RVDDS')
     A                                      INDTXT(43 'Invalid: #1CQNB')
     A                                      INDTXT(44 'Invalid: #1SWCR')
     A                                      INDTXT(45 'Invalid: #1PRCN')
     A                                      INDTXT(46 'Invalid: #RASCD')
     A                                      INDTXT(47 'Invalid: #1BKCD')
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0181M#F1
     A*                                                    End of   /*S01408*/
     A  84                                  SFLNXTCHG
     A*                                                    Begin  /CAC001/
     A**********  #1DBRC       161   H      TEXT('Existing record fields')
      *                                              Begin /CAP032/
     A*********** #1DBRC       164   H      TEXT('Existing record fields')
     A**Start of change TDA029
     A**********  #1DBRC       242   H      TEXT('Existing record fields')                    TDA029
      **Start of change MD039461
     A**********  #1DBRC       262   H      TEXT('Existing record fields')                  MD039461
     A            #1DBRC       265   H      TEXT('Existing record fields')
      **End of change MD039461
     A**End of change TDA029
      *                                              End   /CAP032/
     A*                                                    End    /CAC001/
     A            #RIEST         1   H      TEXT('Item error state')
     A            #1BTNB         3   H      TEXT('Batch number')
     A            #1BINB         3  0H      TEXT('Batch item no.')
     A            #1CUST         6   H      TEXT('Customer number')
     A            #1NNBI         2   H      TEXT('Nostro number (Item)')
     A            #1RACN        10  0H      TEXT('Retail account number')
     A            #1PTAM        13  0H      TEXT('Posting amount')
     A            #1RINE         1   H      TEXT('Retail ind.: Error')
     A            #1PTDT         5  0H      TEXT('Posting date')
     A            #1NVCD         2   H      TEXT('Narrative code')
     A            #1NRDC        30   H      TEXT('Narrative description')
     A            #1ACTY         1   H      TEXT('Account type')
     A            #1VLDT         5  0H      TEXT('Value date')
     A            #1ACNB         6   H      TEXT('Assoc. Customer number')
     A            #1COIN         1   H      TEXT('Customer originated ind.')
     A            #1SPFC         8  0H      TEXT('Spread Factor')
     A            #1RVDI         1   H      TEXT('Reverse/Void Indicator')
     A            #1RINO         1   H      TEXT('Retail ind.: Override')
     A            #1BCBH         3   H      TEXT('Branch Code (Batch Headr)')
     A            #1RADP         1   H      TEXT('Retail advice printed')
     A            #1SHPI         1   H      TEXT('Shadow post indicator')
     A            #1RRNM         5  0H      TEXT('Relative rec no of memos')
     A            #1RNBE         1   H      TEXT('Retail Number Entered')
     A            #1RARC        10   H      TEXT('Retail Ac Referral Cond.')
     A*                                                    Begin  /CAC001/
     A            #1NITM         5  0H      TEXT('Number of Items')
     A*                                                    End    /CAC001/
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0181M#F2
     A*                                                    End of   /*S01408*/
     A            #1SEL          1   B  4  2
     A                                      VALUES('C' +
     A                                             'T' +
     A                                             'D')
     A                                      CHECK(AB)
     A  78                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A  78N31                               DSPATR(ND)
     A  78
     AO 31                                  COLOR(WHT)
     A N78N31                               DSPATR(UL HI) COLOR(GRN)
     A            #RACID        10   B  4  4TEXT('Account Identifier')
     A  87                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  87
     AO 32                                  COLOR(WHT)
     A N87N32                               DSPATR(UL) COLOR(GRN)
     A N32                                  DSPATR(HI)
     A            #1CYCD         3   B  4 15TEXT('Currency code')
     A  87                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A  87
     AO 33                                  COLOR(WHT)
     A N87N33                               DSPATR(UL) COLOR(GRN)
     A N33                                  DSPATR(HI)
     A**Start of change CGL029
     A**********  #1ACCD         4   B  4 19TEXT('Account code')                              CGL029
     A            #1ACCD        10   B  4 19TEXT('Account code')
     A**End of change CGL029
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A  87
     AO 34                                  COLOR(WHT)
     A N87N34                               DSPATR(UL) COLOR(GRN)
     A N34                                  DSPATR(HI)
     A**Start of change CGL029
     A**********  #1ACSN         2   B  4 24TEXT('Account sequence number')                   CGL029
     A            #1ACSN         2   B  4 30TEXT('Account sequence number')
     A**End of change CGL029
     A  87                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A  87
     AO 35                                  COLOR(WHT)
     A N87N35                               DSPATR(UL) COLOR(GRN)
     A N35                                  DSPATR(HI)
     A**Start of change CGL029
     A**********  #1IBCA         3   B  4 28TEXT('Input Branch Code - Alpha')                 CGL029
     A            #1IBCA         3   B  4 34TEXT('Input Branch Code - Alpha')
     A**End of change CGL029
     A  77                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A  77N36                               DSPATR(ND)
     A  77
     AO 36                                  COLOR(WHT)
     A N77N36                               DSPATR(UL HI) COLOR(GRN)
     A**Start of change CGL029
     A**********  #RFMPA        14   B  4 33TEXT('Formatted Posting amount')                  CGL029
     A            #RFMPA        14   B  4 39TEXT('Formatted Posting amount')
     A**End of change CGL029
     A  87                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A  87
     AO 37                                  COLOR(WHT)
     A N87N37                               DSPATR(UL) COLOR(GRN)
     A N37                                  DSPATR(HI)
     A**Start of change CGL029
     A**********  #1DCIN         1   B  4 48TEXT('Debit/credit indicator')                    CGL029
     A            #1DCIN         1   B  4 54TEXT('Debit/credit indicator')
     A**End of change CGL029
     A  87                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A  87
     AO 38                                  COLOR(WHT)
     A N87N38                               DSPATR(UL) COLOR(GRN)
     A N38                                  DSPATR(HI)
     A*/COPY WNCPYSRC,GL0181M#01
     A**Start of change CGL029
     A**********  #RNRTV        30   B  4 50TEXT('Narrative')                                 CGL029
     A            #RNRTV        30   B  4 56TEXT('Narrative')
     A**End of change CGL029
     A*/COPY WNCPYSRC,GL0181M#02
     A  87                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A  87
     AO 39                                  COLOR(WHT)
     A N87N39                               DSPATR(UL) COLOR(GRN)
     A N39                                  DSPATR(HI)
     A                                  5  4'Type'
     A                                      COLOR(TRQ)
     A            #1TRTY         5   B  5  9TEXT('Transaction type')
     A  87                                  DSPATR(PR)
     A  40                                  DSPATR(RI PC)
     A  87
     AO 40                                  COLOR(WHT)
     A N87N40                               DSPATR(UL) COLOR(GRN)
     A N40                                  DSPATR(HI)
     A                                  5 16'Sub Type'
     A                                      COLOR(TRQ)
     A            #1TSTY        10   B  5 25TEXT('Transaction (Sub - Type)')
     A  87                                  DSPATR(PR)
     A  41                                  DSPATR(RI PC)
     A  87
     AO 41                                  COLOR(WHT)
     A N87N41                               DSPATR(UL) COLOR(GRN)
     A N41                                  DSPATR(HI)
     A                                  5 36'Value Date'
     A                                      COLOR(TRQ)
     A            #RVDDS         6   B  5 47TEXT('Value date (display)')
     A  87                                  DSPATR(PR)
     A  42                                  DSPATR(RI PC)
     A  87
     AO 42                                  COLOR(WHT)
     A N87N42                               DSPATR(UL) COLOR(GRN)
     A N42                                  DSPATR(HI)
     A                                  5 54'Cheque'
     A                                      COLOR(TRQ)
     A            #1CQNB         8   B  5 61TEXT('Cheque number')
     A  87                                  DSPATR(PR)
     A  43                                  DSPATR(RI PC)
     A  87
     AO 43                                  COLOR(WHT)
     A N87N43                               DSPATR(UL) COLOR(GRN)
     A N43                                  DSPATR(HI)
     A                                  6  4'SWIFT'
     A                                      COLOR(TRQ)
     A            #1SWCR        16   B  6 10TEXT('Swift common reference')
     A  87                                  DSPATR(PR)
     A  44                                  DSPATR(RI PC)
     A  87
     AO 44                                  COLOR(WHT)
     A N87N44                               DSPATR(UL) COLOR(GRN)
     A N44                                  DSPATR(HI)
     A                                  6 27'Profit centre'
     A                                      COLOR(TRQ)
     A            #1PRCN         4   B  6 41TEXT('Profit centre')
     A  87                                  DSPATR(PR)
     A  45                                  DSPATR(RI PC)
     A  87
     AO 45                                  COLOR(WHT)
     A N87N45                               DSPATR(UL) COLOR(GRN)
     A N45                                  DSPATR(HI)
     A                                  6 46'Associated Customer'
     A                                      COLOR(TRQ)
     A            #RASCD        10   B  6 66TEXT('Associated Cust - Display')
     A  87                                  DSPATR(PR)
     A  46                                  DSPATR(RI PC)
     A  87
     AO 46                                  COLOR(WHT)
     A N87N46                               DSPATR(UL) COLOR(GRN)
     A N46                                  DSPATR(HI)
     A                                  7  4'Book Code'
     A                                      COLOR(TRQ)
     A            #1BKCD         2   B  7 14TEXT('Book Code')
     A  87                                  DSPATR(PR)
     A  47                                  DSPATR(RI PC)
     A  87
     AO 47                                  COLOR(WHT)
     A N87N47                               DSPATR(UL) COLOR(GRN)
     A N47                                  DSPATR(HI)
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0181M#F3
     A*                                                    End of   /*S01408*/
     A            #RIEMG        76      8  4TEXT('Item error message')
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0181M#F4
     A*                                                    End of   /*S01408*/
     A  76                                  COLOR(RED)
     A N76                                  COLOR(WHT)
      ** Start of Change CGL013
     A                                  7 18'Additional Information'
     A N48                                  DSPATR(ND)
     A  48                                  COLOR(TRQ)
     A            #1ADIN         1   B  7 41TEXT('Additional Information')
     A N48                                  DSPATR(PR ND)
     A  49                                  DSPATR(RI PC)
     A  48N49                               DSPATR(UL HI) COLOR(GRN)
     A            #1AIPI         1   O  7 43TEXT('Additional Info Indicator')
     A N48                                  DSPATR(ND)
     A  48                                  DSPATR(HI) COLOR(GRN)
      ** End of Change CGL013
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0181M#F5
     A*                                                    End of   /*S01408*/
     A**Start of change CGL029
     A**********                            SFLPAG(03) SFLSIZ(04)                             CGL029
     A                                      SFLPAG(04) SFLSIZ(05)
     A**End of change CGL029
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0181M#F6
     A*                                                    End of   /*S01408*/
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0181M#F7
     A*                                                    End of   /*S01408*/
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'GL0181M1'
     A                                      COLOR(WHT)
     A            ##MRDT         7      1 72TEXT('Midas Rundate')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A                                  2 23'Standing Journal Postings Maintena-
     A                                      nce'
     A                                      DSPATR(HI) COLOR(WHT)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            #2BTNB         3   H      TEXT('Batch number')
     A            #2BINB         3  0H      TEXT('Batch item no.')
     A                                  3  8'Account number'
     A                                      COLOR(TRQ)
     A**Start of change CGL029
     A**********                        3 27'Branch'                                          CGL029
     A                                  3 33'Branch'
     A**End of change CGL029
     A  79                                  DSPATR(ND)
     A N79                                  COLOR(TRQ)
     A N25                                  OVRATR
     A**Start of change CGL029
     A**********                        3 39'Amount                Narrative'                 CGL029
     A                                  3 45'Amount                Narrative'
     A**End of change CGL029
     A                                      COLOR(TRQ)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A**Start of change CGL029
     A**89******                       23  2'F3=Batch Summary   F5=Refresh Scre-              CGL029
     A**********                            en   F9=Go to ''Change'' mode'                    CGL029
     A  89                             26  2'F3=Batch Summary   F5=Refresh Scre-
     A                                      en   F9=Go to ''Change'' mode'
     A**End of change CGL029
     A                                      COLOR(WHT)
     A**Start of change CGL029
     A*N89******                       23  2'D=Delete   F3=Batch Summary   F5=R-              CGL029
     A**********                            efresh Screen   F9=Go to ''Add'' mo-              CGL029
     A**********                            de'                                               CGL029
     A N89                             26  2'D=Delete   F3=Batch Summary   F5=R-
     A                                      efresh Screen   F9=Go to ''Add'' mo-
     A                                      de'
     A**End of change CGL029
     A                                      COLOR(WHT)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A**Start of change CGL029
     A**********                            SFL SFLMSGRCD(24)                                 CGL029
     A                                      SFL SFLMSGRCD(27)
     A**End of change CGL029
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
