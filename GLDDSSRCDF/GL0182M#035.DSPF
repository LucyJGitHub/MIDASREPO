      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Edit Spread Jrnl Postings')                   *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL0182M# - Edit Spread Jrnl Postings                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. MD027557           Date 25Jun14               *
      *  Prev Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP032             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CGL002             Date 28Oct96               *
      *                 110305             Date 28Oct96               *
      *                 S01408             Date 26JUN95               *
      *                 E38270             Date 22JUN92               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD027557 - The credit amount input becomes blank once press  *
      *             enter. Increase existing record fields.           *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,GLH00250                                 *
      *             WNCPYSRC,GLH00251                                 *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP032 - Conversion of Journal Batch Entry inputs into       *
      *           modular structure to use as APIs.                   *
      *  CGL002 - Block GL Accounts in Journal Entry Input            *
      *  110305 - Batch item record is not being updated.             *
      *      S01408 - ADDITION OF CORE HOOKS GL0182M#F1               *
      *                                      GL0182M#F2               *
      *                                      GL0182M#F3               *
      *                                      GL0182M#F4               *
      *                                      GL0182M#F5               *
      *                                      GL0182M#F6               *
      *                                      GL0182M#F7               *
      *      E38270 - DO NOT DISPLAY BRANCH IF MBIN IS 'N'            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A**Start of change CGL029
     A                                      DSPSIZ(*DS4)
     A**End of change CGL029
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(31 'Invalid: #RACID')
     A                                      INDTXT(32 'Invalid: #1CYCD')
     A                                      INDTXT(33 'Invalid: #1ACCD')
     A                                      INDTXT(34 'Invalid: #1ACSN')
     A                                      INDTXT(35 'Invalid: #1IBCA')
     A                                      INDTXT(36 'Invalid: #RNRTV')
     A                                      INDTXT(37 'Invalid: #RKCNB')
     A                                      INDTXT(38 'Invalid: #1PRCN')
     A                                      INDTXT(39 'Invalid: #RVDDS')
     A*                                              Begin /CGL002/
     A                                      INDTXT(40 'Invalid: #1RINO')
     A*                                              End   /CGL002/
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0182M#F1
     A*                                                    End of   /*S01408*/
     A  84                                  SFLNXTCHG
     A*                                              Begin /110305/
     A*********** #1DBRC       161   H      TEXT('Existing record fields')
      *                                              Begin /CAP032/
     A*********** #1DBRC       164   H      TEXT('Existing record fields')
     A**Start of change CGL029
     A**********  #1DBRC       242   H      TEXT('Existing record fields')                    CGL029
      **Start of change MD027557
     A**********  #1DBRC       248   H      TEXT('Existing record fields')                  MD027557
     A            #1DBRC       265   H      TEXT('Existing record fields')
      **End of change MD027557
     A**End of change CGL029
      *                                              End   /CAP032/
     A*                                              End   /110305/
     A            #RIEST         1   H      TEXT('Item error state')
     A            #1BTNB         3   H      TEXT('Batch number')
     A            #1BINB         3  0H      TEXT('Batch item no.')
     A            #1CUST         6   H      TEXT('Customer number')
     A            #1NNBI         2   H      TEXT('Nostro number (Item)')
     A            #1RACN        10  0H      TEXT('Retail account number')
     A            #1PTAM        13  0H      TEXT('Posting amount')
     A            #1RINE         1   H      TEXT('Retail ind.: Error')
     A            #1PTDT         5  0H      TEXT('Posting date')
     A            #1NVCD         2   H      TEXT('Narrative code')
     A            #1NRDC        30   H      TEXT('Narrative description')
     A            #1ACTY         1   H      TEXT('Account type')
     A            #1TRTY         5   H      TEXT('Transaction type')
     A            #1TSTY        10   H      TEXT('Transaction (Sub - Type)')
     A            #1CQNB         8   H      TEXT('Cheque number')
     A            #1VLDT         5  0H      TEXT('Value date')
     A            #1ACNB         6   H      TEXT('Assoc. Customer number')
     A            #1COIN         1   H      TEXT('Customer originated ind.')
     A            #1SWCR        16   H      TEXT('Swift common reference')
     A            #RASCD        10   H      TEXT('Associated Cust - Display')
     A            #1BKCD         2   H      TEXT('Book Code')
     A            #1SPFC         8  0H      TEXT('Spread Factor')
     A            #1RVDI         1   H      TEXT('Reverse/Void Indicator')
     A*                                              Begin /CGL002/
     A*********** #1RINO         1   H      TEXT('Retail ind.: Override')
     A*                                              End   /CGL002/
     A            #1BCBH         3   H      TEXT('Branch Code (Batch Headr)')
     A            #1RADP         1   H      TEXT('Retail advice printed')
     A            #1SHPI         1   H      TEXT('Shadow post indicator')
     A            #1RRNM         5  0H      TEXT('Relative rec no of memos')
     A            #1RNBE         1   H      TEXT('Retail Number Entered')
     A            #1RARC        10   H      TEXT('Retail Ac Referral Cond.')
     A*                                              Begin /110305/
     A            #1NITM         5  0H      TEXT('Number of Items')
     A*                                              End   /110305/
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0182M#F2
     A*                                                    End of   /*S01408*/
     A            #RACID        10   B  5  2TEXT('Account Identifier')
     A  78                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A  78
     AO 31                                  COLOR(WHT)
     A N78N31                               DSPATR(UL) COLOR(GRN)
     A N31                                  DSPATR(HI)
     A            #1CYCD         3   B  5 13TEXT('Currency code')
     A  78                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  78
     AO 32                                  COLOR(WHT)
     A N78N32                               DSPATR(UL) COLOR(GRN)
     A N32                                  DSPATR(HI)
     A**Start of change CGL029
     A**********  #1ACCD         4   B  5 17TEXT('Account code')                              CGL029
     A            #1ACCD        10   B  5 17TEXT('Account code')
     A**End of change CGL029
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A  78
     AO 33                                  COLOR(WHT)
     A N78N33                               DSPATR(UL) COLOR(GRN)
     A N33                                  DSPATR(HI)
     A**Start of change CGL029
     A**********  #1ACSN         2   B  5 22TEXT('Account sequence number')                   CGL029
     A            #1ACSN         2   B  5 28TEXT('Account sequence number')
     A**End of change CGL029
     A  78                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A  78
     AO 34                                  COLOR(WHT)
     A N78N34                               DSPATR(UL) COLOR(GRN)
     A N34                                  DSPATR(HI)
     A**Start of change CGL029
     A**********  #1IBCA         3   B  5 26TEXT('Input Branch Code - Alpha')                 CGL029
     A            #1IBCA         3   B  5 32TEXT('Input Branch Code - Alpha')
     A**End of change CGL029
     A  77                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A  77N35                               DSPATR(ND)
     A  77
     AO 35                                  COLOR(WHT)
     A N77N35                               DSPATR(UL HI) COLOR(GRN)
     A**Start of change CGL029
     A**********  #RFMPA        14      5 31TEXT('Formatted Posting amount')                  CGL029
     A            #RFMPA        14      5 37TEXT('Formatted Posting amount')
     A**End of change CGL029
     A                                      DSPATR(HI) COLOR(WHT)
     A**Start of change CGL029
     A**********  #1DCIN         1      5 46TEXT('Debit/credit indicator')                    CGL029
     A            #1DCIN         1      5 52TEXT('Debit/credit indicator')
     A**End of change CGL029
     A                                      DSPATR(HI) COLOR(WHT)
     A**Start of change CGL029
     A**********  #RNRTV        30   B  5 48TEXT('Narrative')                                 CGL029
     A            #RNRTV        30   B  5 54TEXT('Narrative')
     A**End of change CGL029
     A  87                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A  87
     AO 36                                  COLOR(WHT)
     A N87N36                               DSPATR(UL) COLOR(GRN)
     A N36                                  DSPATR(HI)
     A                                  6  2'Spread factor'
     A                                      COLOR(TRQ)
     A            #RKCNB         9   B  6 16TEXT('Spread details')
     A                                      CHECK(FE)
     A  76                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A  76
     AO 37                                  COLOR(WHT)
     A N76N37                               DSPATR(UL) COLOR(GRN)
     A N37                                  DSPATR(HI)
     A                                  6 27'Profit centre'
     A                                      COLOR(TRQ)
     A            #1PRCN         4   B  6 41TEXT('Profit centre')
     A  76                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A  76
     AO 38                                  COLOR(WHT)
     A N76N38                               DSPATR(UL) COLOR(GRN)
     A N38                                  DSPATR(HI)
     A                                  6 48'Value date'
     A                                      COLOR(TRQ)
     A            #RVDDS         6   B  6 59TEXT('Value date (display)')
     A  87                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A  87
     AO 39                                  COLOR(WHT)
     A N87N39                               DSPATR(UL) COLOR(GRN)
     A N39                                  DSPATR(HI)
     A*                                              Begin /CGL002/
     A                                  6 67'Override'
     A  74                                  DSPATR(ND) COLOR(WHT)
     A N74                                  COLOR(TRQ)
     A            #1RINO         1   B  6 77TEXT('Retail ind.: Override')
     A  74                                  DSPATR(PR)
     A  40                                  DSPATR(RI PC)
     A  74N40                               DSPATR(ND)
     A  74
     AO 40                                  COLOR(WHT)
     A N74N40                               DSPATR(UL HI) COLOR(RED)
     A*                                              End   /CGL002/
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0182M#F3
     A*                                                    End of   /*S01408*/
     A            #RIEMG        76      7  2TEXT('Item error message')
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0182M#F4
     A*                                                    End of   /*S01408*/
     A  75                                  COLOR(RED)
     A N75                                  COLOR(WHT)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0182M#F5
     A*                                                    End of   /*S01408*/
     A**Start of change CGL029
     A**********                            SFLPAG(05) SFLSIZ(06)                             CGL029
     A                                      SFLPAG(06) SFLSIZ(07)
     A**End of change CGL029
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0182M#F6
     A*                                                    End of   /*S01408*/
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A*/COPY WNCPYSRC,GLH00250
     A                                      CF05(05 'Reset.')
     A                                      CF08(08 'CF08.')
     A*/COPY WNCPYSRC,GLH00251
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A*                                              Begin /CGL002/
     A*                                              End   /CGL002/
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,GL0182M#F7
     A*                                                    End of   /*S01408*/
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'GL0182M1'
     A                                      COLOR(WHT)
     A            ##MRDT         7      1 72TEXT('Midas Rundate')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A                                  2 24'Spread Journal Entries Maintenance-
     A                                      '
     A                                      DSPATR(HI) COLOR(WHT)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            #2BTNB         3   H      TEXT('Batch number')
     A            #2BINB         3  0H      TEXT('Batch item no.')
     A                                  4  6'Account number'
     A                                      COLOR(TRQ)
     A**Start of change CGL029
     A**********                        4 25'Branch'                                          CGL029
     A                                  4 31'Branch'
     A**End of change CGL029
     A  79                                  DSPATR(ND)
     A N79                                  COLOR(TRQ)
     A N25                                  OVRATR
     A**Start of change CGL029
     A**********                        4 35'Amount                Narrative'                 CGL029
     A                                  4 41'Amount                Narrative'
     A**End of change CGL029
     A                                      COLOR(TRQ)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A**Start of change CGL029
     A**********  ##CTX1        78     23  2TEXT('Command text 1')                            CGL029
     A            ##CTX1        78     26  2TEXT('Command text 1')
     A**End of change CGL029
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A**Start of change CGL029
     A**********                            SFL SFLMSGRCD(24)                                 CGL029
     A                                      SFL SFLMSGRCD(27)
     A**End of change CGL029
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
