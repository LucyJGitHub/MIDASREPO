      *================================================================
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Account Transfer Criteria Maintenance')
      *================================================================
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL019000DF - Midas GL Account Transfer Criteria Maintenance  *
      *                                                               *
      *  Function:  This display enables the creation of Account      *
      *             Transfer Criteria which are used to generate      *
      *             accounts for transfer details.                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *================================================================
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      INDARA
     A*---------------------------------------------------------------------
     A          R GL019000S1                SFL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2010')
     A  11                                  SFLNXTCHG
     A            S1OPTN         2A  B  9  3TEXT('OPTION')
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A N70                                  DSPATR(HI)
     A            S1RFNO        10A  O  9  8TEXT('Reference')
     A            S1STAT         8A  O  9 19TEXT('Status')
     A            S1CRTD         9A  O  9 28TEXT('Create Date')
     A            S1PRCD         9A  O  9 38TEXT('Process Date')
     A            S1DESC        30A  O  9 48TEXT('Description')
     A*---------------------------------------------------------------------
     A          R GL019000C1                SFLCTL(GL019000S1)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      CA03
     A                                      OVERLAY
     A N82                                  SFLDSP
     A  81                                  SFLDSPCTL
     A  82                                  SFLCLR
     A N83                                  SFLEND(*MORE)
     A N82 83                               ROLLUP(01)
     A  81                                  ROLLDOWN(02)
     A            SCRRN1         4S 0H      TEXT('SFLRCDNBR field')
     A                                      SFLRCDNBR(CURSOR)
     A            SCUSER        10A  O  1  2
     A                                  1 13'GL001900'
     A            SCDATE         7A  O  1 73
     A            SCWSID        10A  O  2  2
     A                                  2 23'Account Selection Criteria Mainten-
     A                                      ance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
     A                                      EDTWRD('0 :  :  ')
     A                                  4  2'Select option and press Enter'
     A                                      COLOR(BLU)
     A                                  5  2'A=Amend'
     A                                      COLOR(BLU)
     A                                  5 10'D=Delete'
     A                                      COLOR(BLU)
     A                                  5 19'E=Enquire'
     A                                      COLOR(BLU)
     A                                  5 29'I=Insert'
     A                                      COLOR(BLU)
     A                                  5 38'RE=Reverse'
     A                                      COLOR(BLU)
     A                                  7  8'Reference'
     A                                      DSPATR(HI)
     A                                  7 19'Status'
     A                                      DSPATR(HI)
     A                                  7 28'Creation'
     A                                      DSPATR(HI)
     A                                  7 38'Process'
     A                                      DSPATR(HI)
     A                                  7 48'Description'
     A                                      DSPATR(HI)
     A            SCOPTN         2A  B  8  3TEXT('OPTION')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A N71                                  DSPATR(HI)
     A            SCPOS1        10A  B  8  8TEXT('Reference')
     A                                      CHECK(LC)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A N54                                  DSPATR(HI)
     A*---------------------------------------------------------------------
     A          R GL019000D1
     A                                      CA03
     A                                      CA04
     A N77                                  CA05
     A                                      CA12
     A                                      OVERLAY
     A            SCUSER        10A  O  1  2
     A                                  1 13'GL001900'
     A            SCDATE         7A  O  1 73
     A            SCWSID        10A  O  2  2
     A                                  2 23'Account Transfer Criteria Maintena-
     A                                      nce'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
     A                                      EDTWRD('0 :  :  ')
     A  69                              4  2'Press ENTER to Confirm Reversal'
     A                                      COLOR(BLU)
     A  75                              4  2'Press ENTER to Confirm Deletion'
     A                                      COLOR(BLU)
     A  77                              4  2'Press ENTER to Continue'
     A                                      COLOR(BLU)
     A N75N77N69                        4  2'Change details,'
     A                                      COLOR(BLU)
     A N75N77N69                        4 18'Press ENTER to Continue'
     A                                      COLOR(BLU)
     A                                  6  2'Transfer From Details:'
     A                                      DSPATR(HI)
     A                                  7  3'Reference  . . . . . . . . . .:'
     A            S2RFNO        10A  B  7 37TEXT('Reference')
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A N72                                  DSPATR(HI)
     A  76                                  DSPATR(PR)
     A N76                                  DSPATR(UL)
     A            S2DESC        30A  B  7 48TEXT('Description')
     A                                      CHECK(LC)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A N66                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A                                  8  3'Branch Code  . . . . . . . . .:'
     A            S2BRCA         3A  B  8 37TEXT('Branch Code')
     A  73
     AO 40                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A N73                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A            S2BRNM        30A  O  8 44TEXT('Branch Name')
     A                                  9  3'Customer Number  . . . . . . .:'
     A            S2CNU1         6A  B  9 37TEXT('Customer Number 1')
     A  74
     AO 41                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A N74                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A                                  9 44'-'
     A            S2CNU2         6A  B  9 46TEXT('Customer Number 2')
     A  55
     AO 42                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A N55                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A                                 10  3'Currency . . . . . . . . . . .:'
     A            S2CCY          3A  B 10 37TEXT('Currency')
     A  80
     AO 43                                  DSPATR(PC)
     A  80                                  DSPATR(RI)
     A N80                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A            S2CYNM        14A  O 10 44TEXT('Currency Description')
     A                                 11  3'Account Code . . . . . . . . .:'
     A            S2ACO1        10A  B 11 37TEXT('Account Code 1')
     A  78
     AO 44                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
     A N78                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A                                 11 48'-'
     A            S2ACO2        10A  B 11 50TEXT('Account Code 2')
     A  56
     AO 45                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A N56                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A                                 12  3'Account Sequence . . . . . . .:'
     A            S2ACSQ         2A  B 12 37TEXT('Account Sequence')
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A N79                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A                                 13  3'Retail Number  . . . . . . . .:'
     A            S2ACN1        10A  B 13 37TEXT('Retail Number 1')
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A N57                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A                                 13 48'-'
     A            S2ACN2        10A  B 13 50TEXT('Retail Number 2')
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A N58                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A                                 14  3'Account Officer  . . . . . . .:'
     A            S2ACOC         2A  B 14 37TEXT('Account Officer')
     A  59
     AO 46                                  DSPATR(PC)
     A  59                                  DSPATR(RI)
     A N59                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A            S2ACON        30A  O 14 44TEXT('Account Officer Description')
     A                                 15  3'Account Type . . . . . . . . .:'
     A            S2ATYP         1A  B 15 37TEXT('Account Type')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N60                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A            S2ATYD        30A  O 15 44TEXT('Account Type Description')
     A                                 16  3'Country of Domicile  . . . . .:'
     A            S2COLC         2A  B 16 37TEXT('Country of Domicile')
     A  61
     AO 47                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A            S2CNNM        30A  O 16 44TEXT('Domicile Description')
     A                                 17  2'Transfer To Details:'
     A                                      DSPATR(HI)
     A                                 18  3'Branch Code  . . . . . . . . .:'
     A            S2TBRC         3A  B 18 37TEXT('Branch Code')
     A  62
     AO 48                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N62                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A            S2TBRN        30A  O 18 44TEXT('Branch Description')
     A                                 19  3'Customer Number  . . . . . . .:'
     A            S2TCNU         6A  B 19 37TEXT('Customer Number')
     A  63
     AO 49                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N63                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A            S2TCRN        20A  O 19 44TEXT('Customer Name')
     A                                 20  3'Account Code . . . . . . . . .:'
     A            S2TACO        10A  B 20 37TEXT('Account Code')
     A  64
     AO 50                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N64                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A            S2TACC        30A  O 20 50TEXT('Account Code Description')
     A                                 21  3'Account Sequence . . . . . . .:'
     A            S2TACS         2A  B 21 37TEXT('Account Sequence')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N65                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A*N77                             23  2'F3=Exit  F4=Prompt  F5=Refresh -
      *                                      F12=Previous'
     A*                                     COLOR(BLU)
     A* 77                             23  2'F3=Exit  F12=Previous'
     A*                                     COLOR(BLU)
      *---------------------------------------------------------------------
     A          R GL019000F1
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A          R GL019000F2
     A                                      OVERLAY
     A N77                             23  2'F3=Exit  F4=Prompt  F5=Refresh -
                                             F12=Previous'
     A                                      COLOR(BLU)
     A  77                             23  2'F3=Exit  F12=Previous'
     A                                      COLOR(BLU)
      *---------------------------------------------------------------------
     A          R MSGSFL                    TEXT('Message Subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SCMGKY                    SFLMSGKEY
     A            D@PGMN                    SFLPGMQ
      *---------------------------------------------------------------------
     A          R MSGCTL                    TEXT('Control Record')
     A                                      SFLCTL(MSGSFL)
     A                                      SFLSIZ(20)
     A                                      SFLPAG(1)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            D@PGMN                    SFLPGMQ
