      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Account master details (extract)')
     A********************************************************************
     A*                                                                  *
     A*  Midas  GENERAL LEDGER MODULE                          *
     A*                                                                  *
     A*    GL0781DF - ACCOUNT MASTER DETAILS.                            *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 112563             Date 07Feb97               *
      *                 RT0027             Date 09May91               *
     A*                 LN0493             DATE 06JUL90                  *
     A*                   LN0281             DATE    29MAY90             *
     A*                   S01199             DATE    02/05/89            *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  112563 - Header and footer changes required for GUI             *
     A*           Multilanguage support.                                 *
     A*    RT0027 - F6 CANNOT BE TAKEN UNTIL A RANGE HAS BEEN ENTERED    *
     A*    LN0493 - ONCE 'ALL' ACCOUNTS HAVE BEEN CHOSEN (ONE BRANCH)    *
     A*             NO FURTHER CHOICES ARE ALLOWED IN THE SAME REQUEST   *
     A*    LN0281 - SINGLE BRANCH STILL SHOWS BRANCH CODE                *
     A*    S01199 - HELP SYSTEM                                          *
     A********************************************************************
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      CF03(03 'CANCEL REQUEST')
     A                                      CF06(06 'END OF REQUEST')
     A                                      CF12(12 'RESTART REQUEST')
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A          R GL0781F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A            USRID         10A  O 01 02
     A                                  1 13'GL0781'
     A            SWSID         10   O 02 02
     A                                  1 21'ACCOUNT MASTER DETAILS/CARDS REQUE-
     A                                      ST'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A                                  3  9'Account numbers requested:'
     A                                      DSPATR(UL)
     A                                  4  9'From            To'
     A****     LN0281 BEGINS                                          /*LN0281*/
     A***                               4 38'Branch'                  /*LN0281*/
     A  80                              4 38'Branch'
     A****     LN0281 ENDS                                            /*LN0281*/
     A*
     A          R GL0781F2
     A                                      OVERLAY
     A****     RT0027 BEGINS                                          /*RT0027*/
     A**61        ERR           20A  O 18 30DSPATR(RI)                /*RT0027*/
     A  61        ERR           30A  O 18 30DSPATR(RI)
     A                                      DSPATR(HI)
     A****     RT0027 ENDS                                            /*RT0027*/
     A                                 20  5'Enter account number'
     A                                 20 30'From '
     A**Start of change CGL029
     A**********  RANGA         15A  B 20 37                                                  CGL029
     A            RANGA         21A  B 20 37
     A**End of change CGL029
     A                                      DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI)
     A**                                                              /*LN0493*/
     A  84                                  DSPATR(PR)
     A**                                                              /*LN0493*/
     A                                 21 30'To '
     A**Start of change CGL029
     A**********  RANGZ         15A  B 21 37                                                  CGL029
     A            RANGZ         21A  B 21 37
     A**End of change CGL029
     A                                      DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI)
     A**                                                              /*LN0493*/
     A  84                                  DSPATR(PR)
     A**                                                              /*LN0493*/
     A  80                             21 55'Branch -'
     A  80        BRCDFM         3A  O 21 64
     A**                                             Begin /112563/
     A***********                      22 02'Press F3 to end,'
     A***********                      22 19'F6 to accept request'
     A***********                      22 40'or F12 to return'
     A                                 22 02'F3=Exit  F6=Accept Request  F12=-
     A                                      Cancel'
     A**                                             End   /112563/
     A                                 23 02'Press Rollup/down to view Account -
     A                                      numbers'
     A**   TO WRITE CORRECT MESSAGE IN SINGLE BRANCH ENVIRONMENT      /*LN0281*/
     A****     LN0281 BEGINS                                          /*LN0281*/
     A**82                             24 02'NO FURTHER REQUESTS ALLOWED AF+
     A  82 80                          24 02'NO FURTHER REQUESTS ALLOWED AF+
     A                                      TER SELECTING ''ALL'' ACCOUNTS FOR +
     A                                      ''ALL'' BRANCHES'
     A                                      DSPATR(RI)
     A**82N80                          24 02'NO FURTHER REQUESTS ALLOWED AF+
     A**                                                              /*LN0493*/
     A  83                             24 02'NO FURTHER REQUESTS ALLOWED AF+
     A                                      TER SELECTING ''ALL'' ACCOUNTS'
     A                                      DSPATR(RI)
     A**                                                              /*LN0493*/
     A****     LN0281 ENDS                                            /*LN0281*/
     A*
     A          R GL0781S1                  SFL
     A            SFOUT         35A  O  6  9DSPATR(HI)
     A*
     A          R GL0781C1                  SFLCTL(GL0781S1)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      ROLLUP(55)
     A  51                                  SFLCLR
     A  53                                  SFLDSP
     A  52                                  SFLDSPCTL
     A  50                                  SFLEND
     A            PGREC1         4S 0H      SFLRCDNBR
     A*
