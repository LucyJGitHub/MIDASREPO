      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Account master detail - authorise extract')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL0783DF - Account Master Details Prompt                     *
      *             for Authorised Accounts.                          *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CSD012  *CREATE    Date 15Oct02               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD012 - Standing Data Authorisation                         *
      *                                                               *
      *****************************************************************
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      CF03(03 'CANCEL REQUEST')
     A                                      CF06(06 'END OF REQUEST')
     A                                      CF12(12 'RESTART REQUEST')
      *
     A          R GL0783F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      OVERLAY
     A            USRID         10A  O 01 02
     A                                  1 13'GL0783'
     A            SWSID         10   O 02 02
     A                                  1 21'Account Master Details/Cards Reque-
     A                                      st - Authorised'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A                                  3  9'Account numbers requested:'
     A                                      DSPATR(UL)
     A                                  4  9'From            To'
     A  80                              4 38'Branch'
      *
     A          R GL0783F2
     A                                      OVERLAY
     A  61        ERR           30A  O 18 30DSPATR(RI)
     A                                      DSPATR(HI)
     A                                 20  5'Enter account number'
     A                                 20 30'From '
      **Start of change CGL029
     A**********  RANGA         15A  B 20 37
     A            RANGA         21A  B 20 37                                                  CGL029
      **End of change CGL029
     A                                      DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI)
     A  84                                  DSPATR(PR)
     A                                 21 30'To '
      **Start of change CGL029
     A**********  RANGZ         15A  B 21 37                                                  CGL029
     A            RANGZ         21A  B 21 37
      **End of change CGL029
     A                                      DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI)
     A  84                                  DSPATR(PR)
     A  80                             21 55'Branch -'
     A  80        BRCDFM         3A  O 21 64
     A                                 22 02'F3=Exit  F6=Accept Request  F12=-
     A                                      Cancel'
     A                                 23 02'Press Rollup/down to view Account -
     A                                      numbers'
      *
      ** To Write correct message in single branch environment
     A  82 80                          24 02'NO FURTHER REQUESTS ALLOWED AF+
     A                                      TER SELECTING ''ALL'' ACCOUNTS FOR +
     A                                      ''ALL'' BRANCHES'
     A                                      DSPATR(RI)
     A  83                             24 02'NO FURTHER REQUESTS ALLOWED AF+
     A                                      TER SELECTING ''ALL'' ACCOUNTS'
     A                                      DSPATR(RI)
      *
     A          R GL0783S1                  SFL
     A            SFOUT         35A  O  6  9DSPATR(HI)
      *
     A          R GL0783C1                  SFLCTL(GL0783S1)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      ROLLUP(55)
     A  51                                  SFLCLR
     A  53                                  SFLDSP
     A  52                                  SFLDSPCTL
     A  50                                  SFLEND
     A            PGREC1         4S 0H      SFLRCDNBR
