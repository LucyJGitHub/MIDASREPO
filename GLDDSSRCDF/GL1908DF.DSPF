      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Customer Fees Charge maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL1908DF - GL Customer Service Fees Charge Screen            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG9525            Date 20Feb06               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 202033             Date 07Mar02               *
      *                 CSD011             Date 04Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG9525 - Customer service fees maintenance error            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  202033 - Status Narrative not showing first 3 characters.    *
      *  CSD011 - Performance Incentive Fees.                         *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CA03
     A                                      PRINT
     A                                      MSGLOC(24)
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(46 'ANY VALID KEY')
      *
      ** Subfile record format
      *
     A          R GL1908S0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
      *
     A  84                                  SFLNXTCHG
      *
     A            #0SELE         1A  B  8  3TEXT('Selection Field')
     A N57N62                               DSPATR(UL HI)
     A  57                                  DSPATR(PR)
     A  57N62                               DSPATR(ND)
     A  62                                  DSPATR(RI PC)
      *
     A            #0REFE         8A  B  8  6TEXT('Charge reference')
     A N58N61N32                            DSPATR(UL)
     A N32N55                               DSPATR(HI)
     A  58
     AO 61                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
      *
     A            #0FTYP         2A  B  8 15TEXT('Fee Type')
     A N58N61N39                            DSPATR(UL)
     A N39N55                               DSPATR(HI)
     A  58
     AO 61                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
      *
     A            #0GROU         2A  B  8 19TEXT('Charge Group')
     A N58N61N36                            DSPATR(UL)
     A N36N55                               DSPATR(HI)
     A  58
     AO 61                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
      *
     A            #0CNUM         6A  B  8 23TEXT('Customer Number')
     A N58N61N34                            DSPATR(UL)
     A N34N55                               DSPATR(HI)
     A  58
     AO 61                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
      *
     A            #0CRNM        20A  O  8 30TEXT('Cust.Report Name')
     A N37N55                               DSPATR(HI)
     A  37                                  DSPATR(RI PC)
      *
     A            #0PORT         4A  B  8 51TEXT('Portfolio Reference')
     A N65
     AAN58N61N35                            DSPATR(UL)
     A N35N55N65                            DSPATR(HI)
     A  58
     AO 61
     AO 65                                  DSPATR(PR)
     A  65                                  DSPATR(ND)
     A  35N65                               DSPATR(RI PC)
      *
     A            #0FREQ         1A  O  8 58TEXT('Frequency')
     A N37N55                               DSPATR(HI)
     A  37                                  DSPATR(RI PC)
      *
     A            #0FNSD         6A  O  8 61TEXT('Next Settlement Date')
     A N37N55                               DSPATR(HI)
     A  37                                  DSPATR(RI PC)
      *
     A            #0FIND         6A  O  8 68TEXT('Final Settlement Date')
     A N37N55                               DSPATR(HI)
     A  37                                  DSPATR(RI PC)
      *
     A            #0STAT         1A  O  8 77TEXT('Status')
     A N37N55                               DSPATR(HI)
     A  37                                  DSPATR(RI PC)
      *
      * Start of change CSD011
     A************#0HFLD       246A  H                                                        CSD011
      **Start of change BUG9525
     A**********  #0HFLD       253A  H                                                       BUG9525
     A            #0HFLD       277A  H
      **End of change BUG9525
      * End of change CSD011
     A            #0HSEL         1A  H      TEXT('Selection Field')
     A            #0HREF         8A  H      TEXT('Charge reference')
     A            #0HFTY         2A  H      TEXT('Fee Type')
     A            #0HGRO         2A  H      TEXT('Charge Group')
     A            #0HCNU         6A  H      TEXT('Customer Number')
     A            #0HCRN        20A  H      TEXT('Cust.Report Name')
     A            #0HPOR         4A  H      TEXT('Portfolio Reference')
     A            #0HFRE         1A  H      TEXT('Frequency.')
     A            #0HFNS         6A  H      TEXT('Next Settlement Date')
     A            #0HFIN         6A  H      TEXT('Final Settlement Date')
     A            #0HSTA         1A  H      TEXT('Status')
      *
     A            #0HCSS        10A  H      TEXT('Customer Shortname')
     A            #0HCCY         3A  H      TEXT('Charge currency')
     A            #0HAMT        16A  H      TEXT('Charge Amount')
     A            #0HBRC         3A  H      TEXT('Charge branch')
     A            #0HSEA        18A  H      TEXT('Settlement Account')
     A            #0HSLS         6A  H      TEXT('Fee Start/Last settl')
     A            #0HAPO         1A  H      TEXT('Automatic Posting')
     A            #0HPFT         4A  H      TEXT('Profit Centre')
     A            #0HSED         2A  H      TEXT('Settl.generation date')
     A            #0HDAM         2A  H      TEXT('Settl.Day in Mth')
     A            #0HACC         1A  H      TEXT('Accrual Indicator')
     A            #0HNA1        35A  H      TEXT('Narrative 1')
     A            #0HNA2        35A  H      TEXT('Narrative 2')
     A            #0HNA3        35A  H      TEXT('Narrative 3')
     A            #0HNA4        35A  H      TEXT('Narrative 4')
      *
     A            #0HWAR         1A  H      TEXT('Warning')
      *
      ** Subfile control record format
      *
     A          R GL1908C0                  TEXT('Subfile control')
     A                                      SFLCTL(GL1908S0)
      *
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      OVERLAY
     A                                      PUTOVR
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 30                            SFLEND
     A N30                                  ROLLUP(27 'Next page')
     A                                      CF05
     A N61                                  CF09
     A  66N67N68                            CF12
      *
     A            #0SRRN         4S 0H      SFLRCDNBR
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'GL1908C0'
     A                                  1 26'Customer Fees Charge'
     A                                      DSPATR(HI)
     A N61                              1 47'Maintenance'
     A                                      DSPATR(HI)
     A  61                              1 47'Enquiry'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
      *
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A            #0RREF         8A  B  7  6TEXT('Charge reference')
     A                                      OVRDTA
     A N56N50                               DSPATR(UL HI)
     A  56                                  DSPATR(PR)
     A  50                                  DSPATR(PC RI)
     A            #0RFTY         2A  B  7 15TEXT('Fee Type')
     A                                      OVRDTA
     A N56N51                               DSPATR(UL HI)
     A  56                                  DSPATR(PR)
     A  51                                  DSPATR(PC RI)
     A            #0RGRO         2A  B  7 19TEXT('Charge Group')
     A                                      OVRDTA
     A N56N53                               DSPATR(UL HI)
     A  56                                  DSPATR(PR)
     A  53                                  DSPATR(PC RI)
     A            #0RCNU         6A  B  7 23TEXT('Customer Number')
     A                                      OVRDTA
     A N56N52                               DSPATR(UL HI)
     A  56                                  DSPATR(PR)
     A  52                                  DSPATR(PC RI)
     A            #0RPOR         4A  B  7 51TEXT('Portfolio Reference')
     A                                      OVRDTA
     A N56N86N65                            DSPATR(UL HI)
     A  56
     AO 65                                  DSPATR(PR)
     A  86N65                               DSPATR(PC RI)
     A  65                                  DSPATR(ND)
     A            #0RFRE         1A  B  7 58TEXT('Frequency.')
     A                                      OVRDTA
     A N56N76                               DSPATR(UL HI)
     A  56                                  DSPATR(PR)
     A  76                                  DSPATR(PC RI)
     A            #0RFNS         6A  B  7 61TEXT('Next Settlement Date')
     A                                      OVRDTA
     A N56N77                               DSPATR(UL HI)
     A  56                                  DSPATR(PR)
     A  77                                  DSPATR(PC RI)
     A            #0RFIN         6A  B  7 68TEXT('Final Settlement Date')
     A                                      OVRDTA
     A N56N78                               DSPATR(UL HI)
     A  56                                  DSPATR(PR)
     A  78                                  DSPATR(PC RI)
     A            #0RSTA         1A  B  7 77TEXT('Status')
     A                                      OVRDTA
     A N56N82                               DSPATR(UL HI)
     A  56                                  DSPATR(PR)
     A  82                                  DSPATR(PC RI)
      *
     A                                  5  7'Charge'
     A                                  5 14'Fee'
     A                                  5 18'Charge'
     A                                  5 26'Customer'
     A N65                              5 51'Port'
     A                                  5 56'Freq'
     A                                  5 61'Next'
     A                                  5 68'Final'
     A                                  5 74'Status'
      *
     A                                  6  7'Ref.'
     A                                  6 14'Type'
     A                                  6 19'Grp'
     A                                  6 23'Number'
     A                                  6 30'Report Name'
     A N65                              6 51'Ref.'
     A                                  6 61'Settl. Date'
      *
     A N61N57                           5  1'(ADE)'
     A N61N57                           6  1'(RSX)'
     A  61N57                           5  1'(  E)'
     A  61N57                           6  1'(   )'
     A  57                              5  1'(   )'
     A  57                              6  1'(   )'
      *
     A            #0HRSE         1A  H      TEXT('Action Code')
     A            #0HRRE         8A  H      TEXT('Charge Reference')
     A            #0HRFT         2A  H      TEXT('Fee Type')
     A            #0HRGR         2A  H      TEXT('Charge Group')
     A            #0HRCN         6A  H      TEXT('Customer Number')
     A            #0HRPO         4A  H      TEXT('Portfolio Reference')
     A            #0HRFR         1A  H      TEXT('Frequency.')
     A            #0HRFN         6A  H      TEXT('Next Settlement Date')
     A            #0HRFI         6A  H      TEXT('Final Settlement Date')
     A            #0HRST         1A  H      TEXT('Status')
      *
      ** Bottom lines of screen: command keys
      *
     A          R GL1908F0                  TEXT('Command keys')
      *
     A                                      OVERLAY
     A  66N67N68                       23 02'F3=Exit  F5=Refresh Screen  +
     A                                      F12=Previous Screen'
      *
     A  67N66N68                       23 02'F3=Exit  F5=Refresh +
     A                                      Screen  F9=Go to Change +
     A                                      Mode  Rollup/Rolldown'
      *
     A  68N66N67                       23 02'F3=Exit  F5=Refresh +
     A                                      Screen  F9=Go to Add +
     A                                      Mode  Rollup/Rolldown'
      *
      ** Detail and Confirmatiom screen
      *
     A          R GL1908F1
     A                                      OVERLAY
     A  70N71N72
     AAN73                                  CF10
     A  71N70N72
     AAN73                                  CF11
     A                                      CF12
     A  72N70N71
     AO 73N70N71                            CF22
      *
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'GL1908F1'
     A                                  1 26'Customer Fees Charge +
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
      *
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  4  5'Action Code:'
     A            #1SELE         1A  O  4 18TEXT('Action Code')
     A                                      DSPATR(HI)
      *
     A                                  4 45'Charge Reference . :'
     A            #1REFE         8A  O  4 66TEXT('Charge Reference')
     A                                      DSPATR(HI)
      *
     A                                  5  5'Customer Shortname:'
     A            #1CSSN        10A  O  5 25TEXT('Customer Number/Shortname')
     A                                      DSPATR(HI)
      *
     A N65                              5 45'Portfolio  . . . . :'
     A            #1PORT         4A  O  5 66TEXT('Portfolio')
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(ND)
      *
     A                                  6  5'Fee Type  . . . . :'
     A            #1FTYP         2A  O  6 25TEXT('Fee Type')
     A                                      DSPATR(HI)
      *
     A                                  8  5'Charge Group . . . :'
     A            #1GROU         2A  B  8 29TEXT('Charge Group')
     A N28                                  DSPATR(HI)
     A N28N61N18                            DSPATR(UL)
     A  18
     AO 61                                  DSPATR(PR)
     A  28                                  DSPATR(PC RI)
      * Start of change CSD011
     A  75                              8 38'High Water Mark Fee. . :'
     A            #1HWMF         1A  B  8 63TEXT('High Water Mark Fee')
     A N60                                  DSPATR(HI)
     A N60N61N18                            DSPATR(UL)
     A  18
     AO 61                                  DSPATR(PR)
     A  60                                  DSPATR(RI PC)
     A N75                                  DSPATR(ND)
      * End of change CSD011
     A                                  9  5'Charge Currency . . . :'
     A            #1CCY          3A  B  9 29TEXT('Charge Currency')
     A N15                                  DSPATR(HI)
     A N42
     AAN15N61N18                            DSPATR(UL)
     A  18
     AO 42
     AO 61                                  DSPATR(PR)
     A  15                                  DSPATR(PC RI)
      *
     A                                  9 38'Amount . . . . . . . . :'
     A            #1AMT         16A  B  9 63TEXT('Amount')
     A N16                                  DSPATR(HI)
     A N42
     AAN16N61N18                            DSPATR(UL)
     A  18
     AO 42
     AO 61                                  DSPATR(PR)
     A  16                                  DSPATR(PC RI)
      *
     A                                 10  5'Branch  . . . . . . . :'
     A            #1BRCA         3A  B 10 29TEXT('Branch')
     A N17                                  DSPATR(HI)
     A N29
     AAN17N61N18                            DSPATR(UL)
     A  18
     AO 29
     AO 61                                  DSPATR(PR)
     A  17                                  DSPATR(PC RI)
      *
     A**Start of change CGL029
     A**********                       10 38'Settlement Account . . :'                        CGL029
     A**********  #1SEAC        18A  B 10 63TEXT('Settlement Account')                        CGL029
     A                                 10 38'Settlement A/C :'
     A            #1SEAC        24A  B 10 55TEXT('Settlement Account')
     A**End of change CGL029
     A N19                                  DSPATR(HI)
     A N19N61N18                            DSPATR(UL)
     A  18
     AO 61                                  DSPATR(PR)
     A  19                                  DSPATR(PC RI)
      *
     A                                 11  5'Fee Start/Last Settlement Date :'
     A            #1SLSD         6A  B 11 38TEXT('Fee St/Last Settl Dte')
     A N20                                  DSPATR(HI)
     A N42
     AAN54
     AAN20N61N18                            DSPATR(UL)
     A  18
     AO 42
     AO 54
     AO 61                                  DSPATR(PR)
     A  20                                  DSPATR(PC RI)
      *
     A                                 12  5'Automatic Posting. . .:'
     A            #1APOS         1A  B 12 29TEXT('Automatic Posting')
     A N21                                  DSPATR(HI)
     A N21N61N18                            DSPATR(UL)
     A  18
     AO 61                                  DSPATR(PR)
     A  21                                  DSPATR(PC RI)
      *
     A                                 13  5'Settl. Frequency . . .:'
     A            #1FREQ         1A  B 13 29TEXT('Settl. Frequency')
     A N22                                  DSPATR(HI)
     A N22N61N18                            DSPATR(UL)
     A  18
     AO 61                                  DSPATR(PR)
     A  22                                  DSPATR(PC RI)
      *
     A  10                             13 38'Profit Centre . . . . :'
     A  10        #1PFTC         4A  B 13 63TEXT('Profit Centre')
     A  10N11                               DSPATR(HI)
     A  10
     AAN11N61N18                            DSPATR(UL)
     A  10 18
     AO 10 61                               DSPATR(PR)
     A  10 11                               DSPATR(PC RI)
      *
     A                                 14 38'Settl. Generation Days :'
     A            #1SEDA         2A  B 14 63TEXT('Settl. Gen. Days')
     A N23                                  DSPATR(HI)
     A N23N61N18                            DSPATR(UL)
     A  18
     AO 61                                  DSPATR(PR)
     A  23                                  DSPATR(PC RI)
      *
     A                                 14  5'Settl. Day in Month. .:'
     A            #1DAMO         2A  B 14 29TEXT('Settl. Day in month')
     A N24                                  DSPATR(HI)
     A N24N61N18                            DSPATR(UL)
     A  18
     AO 61                                  DSPATR(PR)
     A  24                                  DSPATR(PC RI)
      *
     A                                 15  5'First/Next Settl.Date :'
     A            #1FNSD         6A  B 15 29TEXT('Settl. Day in month')
     A N25                                  DSPATR(HI)
     A N54
     AAN25N61N18                            DSPATR(UL)
     A  18
     AO 54
     AO 61                                  DSPATR(PR)
     A  25                                  DSPATR(PC RI)
      * Start of change CSD011
     A  75                             15 38'Calculation Frequency .:'
     A            #1CFRE         1A  B 15 63TEXT('Calculation Frequency')
     A N48                                  DSPATR(HI)
     A N48N61N18                            DSPATR(UL)
     A  18
     AO 61                                  DSPATR(PR)
     A  48                                  DSPATR(RI PC)
     A N75                                  DSPATR(ND)
      * End of change CSD011
     A                                 16  5'Final Settl.Date. . . :'
     A            #1FIND         6A  B 16 29TEXT('Final Settl.Date')
     A N26                                  DSPATR(HI)
     A N54
     AAN26N61N18                            DSPATR(UL)
     A  18
     AO 54
     AO 61                                  DSPATR(PR)
     A  26                                  DSPATR(PC RI)
      * Start of change CSD011
     A  75                             16 38'Calcultn day in month. :'
     A            #1CDMO         2A  B 16 63TEXT('Calculation day in month')
     A N59                                  DSPATR(HI)
     A N59N61N18                            DSPATR(UL)
     A  18
     AO 61                                  DSPATR(PR)
     A  59                                  DSPATR(RI PC)
     A N75                                  DSPATR(ND)
      * End of change CSD011
     A                                 17  5'Accrue Fees . . . . . :'
     A            #1ACCR         1A  B 17 29TEXT('Accrual Indicator')
     A N31                                  DSPATR(HI)
     A N42
     AAN31N61N18                            DSPATR(UL)
     A  18
     AO 42
     AO 61                                  DSPATR(PR)
     A  31                                  DSPATR(PC RI)
      * Start of change CSD011
     A  75                             17 38'Next calculation date. :'
     A            #1NCDA         6A  B 17 63TEXT('Next calculation date')
     A N79                                  DSPATR(HI)
     A N79N61N18                            DSPATR(UL)
     A  18
     AO 61                                  DSPATR(PR)
     A  79                                  DSPATR(RI PC)
     A N75                                  DSPATR(ND)
      * End of change CSD011
     A                                 18  5'Fee Narrative 1 . . . :'
     A            #1NAR1        35A  B 18 29TEXT('Fee Narrative 1')
     A N38                                  DSPATR(HI)
     A N38N61N18                            DSPATR(UL)
     A  18
     AO 61                                  DSPATR(PR)
     A  38                                  DSPATR(PC RI)
      *
     A                                 19  5'Fee Narrative 2 . . . :'
     A            #1NAR2        35A  B 19 29TEXT('Fee Narrative 2')
     A                                      DSPATR(HI)
     A    N61N18                            DSPATR(UL)
     A  18
     AO 61                                  DSPATR(PR)
      *
     A                                 20  5'Fee Narrative 3 . . . :'
     A            #1NAR3        35A  B 20 29TEXT('Fee Narrative 3')
     A                                      DSPATR(HI)
     A    N61N18                            DSPATR(UL)
     A  18
     AO 61                                  DSPATR(PR)
      *
     A                                 21  5'Fee Narrative 4 . . . :'
     A            #1NAR4        35A  B 21 29TEXT('Fee Narrative 4')
     A                                      DSPATR(HI)
     A    N61N18                            DSPATR(UL)
     A  18
     AO 61                                  DSPATR(PR)
      *
     A                                 22  5'Status  . . . . . . . :'
     A************#1TEXT        12A  O 22 29TEXT('Status')                                    202033
     A            #1TEXT        15A  O 22 29TEXT('Status')                                    202033
     A                                      DSPATR(HI)
     A                                      DSPATR(RI PC)
      *
     A          R GL1908F2
     A                                      OVERLAY
      *
     A N70N71N72
     AAN73                             23 02'F3=Exit  F12=Previous +
     A                                      Screen'
      *
     A  70
     AAN71N72N73                       23 02'F3=Exit  F10=Delete +
     A                                       F12=Previous Screen'
      *
     A  71
     AAN70N72N73                       23 02'F3=Exit  F11=Authorise +
     A                                       F12=Previous Screen'
      *
     A  72
     AAN70N71N73                       23 02'F3=Exit  F12=Previous +
     A                                      Screen  F22=Confirm Stop'
      *
     A  73
     AAN70N71N72                       23 02'F3=Exit  F12=Previous +
     A                                      Screen  F22=Confirm Release'
      *
      ** Subfile program messages
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      ** Subfile control program messages
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
