      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Cust. Serv. Fees - Accruals Adjustments')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL1910DF - Customer Service Fees -  Accruals Adjustments     *
      *             Maintenance                                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CA03
     A                                      PRINT
      *
      **  Screen 1 Subfile Record Format.
      *
     A          R GL1910S0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  31                                  SFLNXTCHG
     A            #0SELE         1A  B  8  3DSPATR(HI)
     A N60N39                               DSPATR(UL)
     A  60N39                               DSPATR(RI PC)
     A  39                                  DSPATR(PR)
     A            #0TYPE         1A  O  8  5DSPATR(BL)
     A  39                                  DSPATR(ND)
     A            #0CUST         6A  O  8  7DSPATR(HI)
     A            #0PTFR         4A  O  8 16DSPATR(HI)
     A            #0FETP         2A  O  8 23DSPATR(HI)
     A            #0CHGR         2A  O  8 28DSPATR(HI)
     A            #0CCCY         3A  O  8 32DSPATR(HI)
     A            #0ACDT        16A  O  8 37DSPATR(HI)
     A            #0OTAJ        16A  O  8 55DSPATR(HI)
     A            #0OTSG         1A  O  8 72DSPATR(HI)
     A            #0ACD2        15S 0H
     A            #0OTA2        15S 0H
     A            #0DECE         1S 0H
     A            #0SPRT        13S 8H
     A            #0MDIN         1A  H
     A            #0HACD        15S 0H
     A            #0HMAC        15S 0H
      *
      ** Accruals period
      *
     A            #0FRPE         5S 0H
     A            #0TOPE         5S 0H
      *
      **  Screen 1 Control Record Format
      *
     A          R GL1910C0                  SFLCTL(GL1910S0)
     A                                      OVERLAY
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  29                                  SFLCLR
     A N29                                  SFLDSPCTL
     A N29 28                               SFLDSP
     A  37 28N29                            SFLEND
     A N35                                  ROLLUP(27)
     A                                      CF05
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'GL1910F2'
     A                                  1 24'Adjustments to Accrued Cust+
     A                                      omer Fees'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A N45                             04 07'Branch'
     A            #0BRCA         3A  B 04 14
     A  45                                  DSPATR(PR ND)
     A N45                                  DSPATR(HI)
     A N63N45                               DSPATR(UL)
     A  63N45                               DSPATR(RI PC)
     A                                 04 19'Customer'
     A            #0RCUS         6A  B 04 28DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                 04 36'Portfolio'
     A            #0RPTF         4A  B 04 46DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A                                 06 07'Customer'
     A                                 06 16'Port.'
     A                                 06 22'Fee'
     A                                 06 27'Chg.'
     A                                 06 32'Chg.'
     A                                 06 37'Fee Accrued'
     A                                 06 55'Adjustment Amount'
     A                                 07 07'Number'
     A                                 07 16'Ref.'
     A                                 07 22'Type'
     A                                 07 27'Grp'
     A                                 07 32'Ccy'
     A                                 07 37'To Date'
     A                                 07 55'in Chg. Ccy'
      *
      **  Screen 1 Footings Format.
      *
     A          R GL1910F0                  OVERLAY
     A  51                             22 02'A=Amend'
     A  52                                +2'D=Delete'
     A  53                                +2'E=Enquiry'
     A                                 23 02'F3=Exit  F5=Refresh'
      *
      **  Screen 2
      *
     A          R GL1910F1
     A                                      OVERLAY
     A  36                                  CF10
     A                                      CF12
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'GL1910F3'
     A                                  1 24'Adjustments to Accrued Cust+
     A                                      omer Fees'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A  32                             03 72'DELETED'
     A                                      DSPATR(BL)
     A                                  2 73TIME
      *
     A                                  4 10'Branch'
     A            #1BRCA         3A  O  4 26
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(ND)
     A                                  5 10'Customer'
     A            #1CUST         6A  O  5 26DSPATR(HI)
     A                                  5 35'Report name'
     A            #1CRNM        20A  O  5 47DSPATR(HI)
     A                                  6 10'Portfolio'
     A            #1PTFR         4A  O  6 26DSPATR(HI)
     A                                  6 35'Narrative'
     A            #1PTFN        20A  O  6 47DSPATR(HI)
     A                                 07 10'Fee Type'
     A            #1FETP         2A  O 07 26DSPATR(HI)
     A                                 07 35'Narrative'
     A            #1NARR        30A  O 07 47DSPATR(HI)
     A                                 08 10'Charge Group'
     A            #1CHGR         2A  O 08 26DSPATR(HI)
      *
     A                                 10 10'System Accruals'
     A            #1ACTD        16A  O 10 26DSPATR(HI)
      *
     A                                 11 10'Manual Accruals'
     A            #1MACP        16A  O 11 26DSPATR(HI)
     A            #1MACS         1A  O 11 43DSPATR(HI)
      *
     A                                 13 10'Fee Accrued'
     A            #1TOTA        16A  O 13 26DSPATR(HI)
      *
      **  Fees for period :
      *
     A                                 13 45'Period :'
     A            #1FRPE         8A  O    +1DSPATR(HI)
     A                                    +1'-'
     A            #1TOPE         8A  O    +1DSPATR(HI)
      *
     A                                 14 10'To Date in'
     A            #1CCCY         3A  O    +1
     A                                 16 10'Adjustment'
     A            #1OTAJ        16A  B 16 26DSPATR(HI)
     A N64N38                               DSPATR(UL)
     A  64N38                               DSPATR(RI PC)
     A  38                                  DSPATR(PR)
     A            #1OTSG         1A  B 16 43DSPATR(HI)
     A N65N38                               DSPATR(UL)
     A  65N38                               DSPATR(RI PC)
     A  38                                  DSPATR(PR)
     A                                 17 10'Amount in'
     A            #1ACCY         3A  O    +1
     A                                 19 10'Accrued Amount'
     A            #1STCH        16A  O 19 26DSPATR(HI)
     A                                 20 10'in Charging CCY'
     A            #1HCCY         3A  O    +1
     A  66                             19 45'Converted in'
     A  66        #1STST        16A  O 19 60DSPATR(HI)
     A  66                             20 45'Settlement CCY'
     A  66        #1SCCY         3A  O    +1
     A            #1OTA2        16A  H
     A            #1OTS2         1A  H
      *
     A          R GL1910F2
     A                                      OVERLAY
     A  36                             23 02'F3=Exit  F10=Delete  +
     A                                      F12=Previous Screen'
     A N36                             23 02'F3=Exit  F12=Previous Screen'
      *
      **  Error message Subfile Format
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      **  Error message Subfile Controller Format
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  34                                  SFLCLR
     A  33
     AON33                                  SFLEND
     A            ##PGM                     SFLPGMQ
