      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Cust Serv Fees - Settl. Adjust./Authoris.')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL1911DF - Customer Service Fees - Settlements Adjustment/   *
      *             Authorisation Maintenance                         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CA03
     A                                      PRINT
      *
      **  Screen 1 Subfile Record Format.
      *
     A          R GL1911S0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  31                                  SFLNXTCHG
     A            #0SELE         1A  B  8  3DSPATR(HI)
     A N60N39                               DSPATR(UL)
     A  60N39                               DSPATR(RI PC)
     A  39                                  DSPATR(PR)
     A            #0TYPE         1A  O  8  5
     A  32                                  DSPATR(BL)
     A  25                                  DSPATR(HI)
     A            #0CUST         6A  O  8  7DSPATR(HI)
     A            #0PTFR         4A  O  8 16DSPATR(HI)
     A            #0FETP         2A  O  8 23DSPATR(HI)
     A            #0CHGR         2A  O  8 28DSPATR(HI)
     A            #0CCCY         3A  O  8 32DSPATR(HI)
     A            #0ACDT        16A  O  8 37DSPATR(HI)
     A            #0OTAJ        16A  O  8 54DSPATR(HI)
     A            #0OTSG         1A  O  8 71DSPATR(HI)
     A            #0STVD         6A  O  8 74DSPATR(HI)
     A            #0ACD2        15S 0H
     A            #0OTA2        15S 0H
     A            #0DECE         1S 0H
     A            #0STV2         5S 0H
      **Start of change CGL029
     A**********  #0STT2        18A  H                                                        CGL029
     A            #0STT2        24A  H
      **End of change CGL029
     A            #0SPRT        13S 8H
     A            #0MDIN         1A  H
     A            #0HACD        15S 0H
     A            #0HMAC        15S 0H
     A            #0FRPE         5S 0H
     A            #0TOPE         5S 0H
      *
      **  Screen 1 Control Record Format
      *
     A          R GL1911C0                  SFLCTL(GL1911S0)
     A                                      OVERLAY
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  29                                  SFLCLR
     A N29                                  SFLDSPCTL
     A N29 28                               SFLDSP
     A  37 28N29                            SFLEND
     A  35                                  ROLLUP(27)
     A                                      CF05
     A  54                                  CF09
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'GL1911F2'
     A                                  1 26'Settlements Adjustment/+
     A                                      Authorisation'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A N45                             04 07'Branch'
     A            #0RBRC         3A  B 04 14
     A  45                                  DSPATR(PR ND)
     A N45                                  DSPATR(HI)
     A N63N45                               DSPATR(UL)
     A  63N45                               DSPATR(RI PC)
      *
     A                                 04 19'Customer'
     A            #0RCUS         6A  B 04 28
     A                                      DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
      *
     A                                 04 36'Portfolio'
     A            #0RPTF         4A  B 04 46
     A                                      DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI PC)
      *
     A                                 06 07'Customer'
     A                                 06 16'Port.'
     A                                 06 22'Fee'
     A                                 06 27'Chg.'
     A                                 06 32'Chg.'
     A                                 06 37'Fee Accrued'
     A                                 06 55'Adjustment Amount'
     A                                 06 74'Value'
     A                                 07 07'Number'
     A                                 07 16'Ref.'
     A                                 07 22'Type'
     A                                 07 27'Grp'
     A                                 07 32'Ccy'
     A                                 07 37'To Date'
     A                                 07 55'in Chg. Ccy'
     A                                 07 74'Date'
      *
      **  Screen 1 Footings Format.
      *
     A          R GL1911F0                  OVERLAY
     A  51                             22 02'A=Amend'
     A  52                                +2'D=Delete'
     A  53                                +2'E=Enquiry'
     A  55                                +2'Y=Authorise'
     A                                 23 02'F3=Exit  F5=Refresh'
     A  54                                +2'F9=Insert'
      *
      **  Screen 2
      *
     A          R GL1911F1
     A                                      OVERLAY
     A  36                                  CF10
     A  26                                  CF11
     A                                      CF12
      *
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'GL1911F3'
     A                                  1 26'Settlements Adjustment/+
     A                                      Authorisation'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A  32                              3 68'DELETED'
     A  32                                  DSPATR(BL)
     A  25                              3 68'AUTHORISED'
     A  25                                  DSPATR(HI)
     A                                  2 73TIME
      *
      **  Details
      *
     A N45                              4 07'Branch'
     A            #1BRCA         3A  B  4 25
     A N45                                  DSPATR(HI)
     A N70N45 46                            DSPATR(UL)
     A  70N45 46                            DSPATR(RI PC)
     A  45                                  DSPATR(ND)
     A N46N45                               DSPATR(PR)
      *
     A                                  5 07'Customer'
     A            #1CUST         6A  B  5 25
     A                                      DSPATR(HI)
     A N66 46                               DSPATR(UL)
     A  66 46                               DSPATR(RI PC)
     A N46                                  DSPATR(PR)
      *
     A N46                              5 36'Report name'
     A            #1CRNM        20A  O  5 48
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
      *
     A                                  6 07'Portfolio'
     A            #1PTFR         4A  B  6 25
     A                                      DSPATR(HI)
     A N67 46                               DSPATR(UL)
     A  67 46                               DSPATR(RI PC)
     A N46                                  DSPATR(PR)
      *
     A N46                              6 36'Narrative'
     A            #1PTFN        20A  O  6 48
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
      *
     A                                  7 07'Fee Type'
     A            #1FETP         2A  B  7 25
     A                                      DSPATR(HI)
     A N68 46                               DSPATR(UL)
     A  68 46                               DSPATR(RI PC)
     A N46                                  DSPATR(PR)
      *
     A N46                              7 36'Narrative'
     A            #1NARR        30A  O  7 48
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
      *
     A                                  8 07'Charge Group'
     A            #1CHGR         2A  B  8 25
     A                                      DSPATR(HI)
     A N69 46                               DSPATR(UL)
     A  69 46                               DSPATR(RI PC)
     A N46                                  DSPATR(PR)
      *
     A N46                             10 07'System Accruals'
     A            #1ACDT        16   O 10 25
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
      *
     A N46                             11 07'Manual Accruals'
     A            #1MACP        16   O 11 25
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
      *
     A            #1MACS         1   O 11 42
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
      *
     A N46                             12 07'Fee Accrued'
     A            #1TOT1        16A  O 12 25
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
      *
     A            #1ACCY         3A  O 12 44
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
      *
     A N46                             14 07'Adjustments'
     A            #1OTAJ        16A  B 14 25
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
     A N64N38N46                            DSPATR(UL)
     A  64N38N46                            DSPATR(RI)
     A  64N38N46                            DSPATR(PC)
     A  38
     AO 46                                  DSPATR(PR)
      *
     A            #1OTSG         1A  B 14 42
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
     A N65N38N46                            DSPATR(UL)
     A  65N38N46                            DSPATR(RI)
     A  65N38N46                            DSPATR(PC)
     A  38
     AO 46                                  DSPATR(PR)
      *
     A N46                             14 44'Settl. Period'
     A            #1FRPE         8A  O 14 59
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
     A N46                             14 68'-'
     A            #1TOPE         8A  O 14 70
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
      *
     A N46                             16 07'Amount to Settle'
     A            #1STCH        16A  O 16 25
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
      *
     A N46 56                          16 44'Converted in'
     A     56     #1STTL        16A  O 16 59
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
     A N46                             17 07'in Charging Ccy'
     A            #1CCCY         3A  O 17 25
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
      *
     A N46 56                          17 44'Settlement Ccy'
     A     56     #1SCCY         3A  O 17 59
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
      *
     A N46                             19 07'Settlement A/C'
     A**Start of change CGL029
     A**********  #1STAC        18A  B 19 25                                                  CGL029
     A            #1STAC        24A  B 19 25
     A**End of change CGL029
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
     A N71N38N46                            DSPATR(UL)
     A  71N38N46                            DSPATR(RI PC)
     A  38
     AO 46                                  DSPATR(PR)
      *
     A**Start of change CGL029
     A*N46******                       19 44'Value Date'                                      CGL029
     A**********  #1STVD         6A  B 19 59                                                  CGL029
     A N46                             19 50'Value Date'
     A            #1STVD         6A  B 19 65
     A**End of change CGL029
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(ND)
     A N72N38N46                            DSPATR(UL)
     A  72N38N46                            DSPATR(RI PC)
     A  38
     AO 46                                  DSPATR(PR)
      *
     A  76                             20 07'Profit Centre'
     A  76        #1PFTC         4A  B 20 25
     A  76                                  DSPATR(HI)
     A  76 46N75                            DSPATR(UL)
     A  76 75 46                            DSPATR(RI PC)
     A  76N46                               DSPATR(PR)
      *
     A            #1OTA2        16A  H
     A            #1OTS2         1A  H
     A**Start of change CGL029
     A**********  #1STA2        18A  H 19 25                                                  CGL029
     A            #1STA2        24A  H
     A**End of change CGL029
     A            #1STV2         6A  H
     A            #1AUID         1A  H
     A            #1PERF         5S 0H
     A            #1PRTP         2A  H
      *
     A          R GL1911F2
     A                                      OVERLAY
     A N26N36                          23 02'F3=Exit  F12=Previous Screen'
     A  36N26                          23 02'F3=Exit  F10=Delete  +
     A                                      F12=Previous Screen'
     A  26N36                          23 02'F3=Exit  F11=Authorise  +
     A                                      F12=Previous Screen'
      *
      **  Error message Subfile Format
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      **  Error message Subfile Controller Format
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  34                                  SFLCLR
     A  33
     AON33                                  SFLEND
     A            ##PGM                     SFLPGMQ
