      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Cust. Serv. Fees -Accruals To Date prompt')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL1984DF - Customer Service Fees Accrued to Date - Prompt    *
      *             Screen                                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CA03(03 'End of job')
     A                                      PRINT
      *
     A          R GL1984F0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'GL1984F0'
     A                                  1 23'Fees Accrued to Date Report Prompt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'Time '
     A                                  2 73TIME
     A                                  9 25'Account Officer'
     A            #0ACOC         4A  B  9 45
     A                                      DSPATR(HI)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(RI PC)
     A                                 11 25'Customer Number'
     A            #0CUST         6A  B 11 45
     A                                      DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                 13 25'Portfolio Reference'
     A            #0PTFR         4A  B 13 45
     A                                      DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A                                 15 25'Fee Type'
     A            #0FETP         4A  B 15 45
     A                                      DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A                                 23 02'F3=Exit'
      *
      **  Error message Subfile Format
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      **  Error message Subfile Controller Format
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  34                                  SFLCLR
     A  33
     AON33                                  SFLEND
     A            ##PGM                     SFLPGMQ
