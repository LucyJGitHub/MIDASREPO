      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL CS Fees Outstanding Settlement-Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL1986DF - Customer Service Fees Outstanding Settlements*
      *                  Report Prompt Screen                         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CA03(03 'End of job')
     A                                      PRINT
      *
     A          R GL1986F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
      *
      *
      *
      *
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'GL1986F0'
     A                                  1 22'Outstanding Settlements Prompt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date '
     A            #0MRDT    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'Time '
     A                                  2 73TIME
     A                                  8 25'Account Officer'
     A            #0ACOC         4A  B  8 50
     A                                      DSPATR(HI)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(RI PC)
     A                                 10 25'Customer Number'
     A            #0CUST         6A  B 10 50
     A                                      DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                 12 25'Portfolio Reference'
     A            #0PTFR         4A  B 12 50
     A                                      DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A                                 14 25'Fee Type'
     A            #0FETP         4A  B 14 50
     A                                      DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A                                 17 25'Authorisation (Y,N,X)'
     A            #0AUID         1A  B 17 50
     A                                      DSPATR(HI)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(RI PC)
     A                                 23 02'F3=Exit'
      *
      ***  Error message Subfile Format
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      ***  Error message Subfile Controller Format
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  34                                  SFLCLR
     A  33
     AON33                                  SFLEND
     A            ##PGM                     SFLPGMQ
