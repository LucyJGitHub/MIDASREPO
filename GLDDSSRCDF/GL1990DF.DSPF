      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Customer Holdings Report Pmpt')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL1990DF - Customer Service Fees - Customer Holdings Report  *
      *             Prompt Screen                                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CF03
      *
     A          R GL1990F0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      OVERLAY
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'GL1990F0'
     A                                  1 27'Customer Holdings Report Prompt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date'
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'Time'
     A                                  2 73TIME
      *
     A                                 11 12'Select customer holdings to print'
     A                                 13 18'From  . . . . .:'
     A                                 15 18'To  . . . . . .:'
     A            #0CUS1         6   B 13 36TEXT('From Customer')
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(RI PC)
     A            #0CUS2         6   B 15 36TEXT('To Customer')
     A                                      DSPATR(HI)
     A N52                                  DSPATR(UL)
     A  52                                  DSPATR(RI PC)
      *
     A                                 23  2'F3=Exit'
     A                                 23 13'?=List'
      *
      **  Error message Subfile Format
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      **  Error message Subfile Controller Format
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  34                                  SFLCLR
     A  33
     AON33                                  SFLEND
     A            ##PGM                     SFLPGMQ
