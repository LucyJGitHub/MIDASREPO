     A*****************************************************************
/*STD *  DSPFBASE
/*EXI *  TEXT('Midas GL Fontis account details maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Midas-Fontis Interface                               *
     A*                                                               *
     A*  DSPF/GL2800DF - Fontis Account Details Maintenance           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CGL004 *C *CREATE  Date 16Jan97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CGL004 - Midas-Fontis Interface                              *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  F U N C T I O N   O F   I N D I C A T O R S                  *
     A*                                                               *
     A*  10 - SFLEND                                                  *
     A*                                                               *
     A*                                                               *
     A*  29 - Protect fields for enquiry                              *
     A*  31 - Error in INFONA field                                   *
     A*  32 - Error in INACTD field                                   *
     A*  33 - Error in INDACD field                                   *
     A*  34 - Error in INGN42 field                                   *
     A*  35 - Error in INWD42 field                                   *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R GL2800F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            INUSER    R        O  1  2REFFLD(USER)
     A                                  1 13'GL2800'
     A            INRUNA    R        O  1 72REFFLD(RUNA)
      *
     A            INWSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 22'ELECTRONIC BANKING ACCOUNT DETAILS'
     A                                      DSPATR(UL HI)
     A            INTIME    R        O  2 72REFFLD(TIME)
     A                                      EDTWRD('0 :  :  ')
     A                                  3  2'Action'
     A            INACTN         1A  O  3 10
     A                                      DSPATR(HI)
     A            INANAM        20A     4  2
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
      *
     A                                  6 02'Branch'
     A            INBRCA    R        O  6 10REFFLD(BRCA)
     A                                      DSPATR(HI)
     A                                  6 15'Customer number'
     A            INCNUM    R        O  6 32REFFLD(CNUMA)
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********                        6 40'Currency'                                        CGL029
     A**********  INCCY     R        O  6 50REFFLD(CCY)                                       CGL029
     A                                  6 40'Ccy'
     A            INCCY     R        O  6 45REFFLD(CCY)
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********                        6 55'A/c. code'                                       CGL029
     A**********  INACOD    R        O  6 66REFFLD(ACODA)                                     CGL029
     A                                  6 50'A/c. code'
     A            INACOD        10A  O  6 60
     A**End of change CGL029
     A                                      DSPATR(HI)
     A                                  6 72'Seq.'
     A            INACSQ    R        O  6 78REFFLD(ACSQA)
     A                                      DSPATR(HI)
     A                                  7  2'Retail Number'
     A            INACNO    R        O  7 19REFFLD(RETL)
     A                                      DSPATR(HI)
      *
     A                                 10  3'Electronic Banking Enabled'
     A            INFONA         1A  B 10 30
     A                                      DSPATR(HI)
     A  29                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N31N29                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 11  3'Activation Date'
     A            INATVD         6A  B 11 30
     A                                      DSPATR(HI)
     A  29                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N32N29                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 12  3'De-Activation Date'
     A            INDATV         6A  B 12 30
     A                                      DSPATR(HI)
     A  29                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N33N29                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 13  3'Last Statement Seq. No.'
     A            INFNSQ         5A  O 13 30
     A                                      DSPATR(HI)
     A N29                                  DSPATR(UL)
      *
     A                                 14  3'Generate Interim Statement'
     A            INGN42         1A  B 14 30
     A                                      DSPATR(HI)
     A  29                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N34N29                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 15  3'Date for Next MT942'
     A            INWD42         6A  B 15 30
     A                                      DSPATR(HI)
     A  29                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N35N29                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 23  2'F3=Exit'
     A                                 23 10'F5=Refresh Screen'
     A                                 23 29'F12=Start'
      *
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program Messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program Messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(05)
     A                                      OVERLAY
     A                                      PUTOVR
     A  11                                  SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  10                                  SFLEND
     A            ##PGM                     SFLPGMQ
