     A**************************************************************
/*STD *  DSPFBASE
/*EXI *  TEXT('Midas GL Fontis details display selection')
     A**************************************************************
     A*                                                            *
     A*  Midas - Midas-Fontis Interface                            *
     A*                                                            *
     A*   DSPF/GL2801DF - Fontis Details Display Selection         *
     A*                                                            *
     A*  (c) Finastra International Limited 2001                   *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    PREV AMEND NO. CGL004 *CREATE  DATE 22JAN97             *
     A*                                                            *
     A*------------------------------------------------------------*
     A*                                                            *
     A*    MD046248 - Finastra Rebranding                          *
     A*    CGL004 - Midas-Fontis Interface                         *
     A*                                                            *
     A**************************************************************
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A*=========================================================================
     A          R #RCDFM1                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      BLINK
     A                                      OVERLAY
     A* Command keys............................................................
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'CF12 Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(31 'Invalid: #1OPTN')
     A*.........................................................................
     A            ##USR         10A  O  1  2TEXT('*USER')
     A                                      DSPATR(HI)
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A                                      DSPATR(HI)
     A                                  1 26'- - - ¦ M I D A S ¦ - - -'
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A                                  2 28'Electronic Banking Interface'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A                                 12 10'Do you wish to see Electronic +
     A                                      Banking Details?'
     A            ##OPTN         1A  B 12 64
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A  31                                  DSPATR(PC)
     A                                 12 57'(Y/N)'
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78A  O 22  2TEXT('Command Line 1')
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit    F12=+
                                            Previous'
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
