     A*****************************************************************
/*STD *  DSPFBASE
/*EXI *  TEXT('Midas GL Fontis file re-send function')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Midas-Fontis Interface                               *
     A*                                                               *
     A*  DSPF/GL2802DF - Fontis File Re-send Function                 *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Last Amend No. CGL004 *CREATE     Date 04FEB97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CGL004 - Midas-Fontis Interface                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*
     A*================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      SFL
     A  84                                  SFLNXTCHG
     A*
     A            #RSET          1   B  6  2
     A                                      DSPATR(HI UL)
     A*
     A            #MBRN     R        O  6 14REFFLD(USRP)
     A                                      DSPATR(HI)
     A*
     A            #DNLS     R        O  6 27REFFLD(MSTS)
     A                                      DSPATR(HI)
     A*
     A            #DLDL          6A  O  6 35
     A                                      DSPATR(HI)
     A*
     A            #TDNL     R        O  6 42REFFLD(LATM)
     A                                      DSPATR(HI)
     A*
     A            #RSTN          1N 0O  6 54
     A                                      DSPATR(HI)
     A*
     A*================================================================
     A*
     A          R #SFLCTL                   TEXT('Subfile Control')
     A                                      SFLCTL(#SFLRCD)
     A                                      SFLPAG(10)
     A                                      SFLSIZ(50)
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A  81                                  SFLDSPCTL
     A  82                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27)
     A                                      CF05(05 'Refresh.')
     A                                      CF08(08 'Request Transfer.')
     A                                      CF09(09 'Restart Background Job')
     A                                      CF10(10 'Stop Job.')
      *
     A                                 16  2'------------------------------+
     A                                       ------------------------------+
                                             -------------------'
     A*
     A                                 18 16'Background Job'
     A                                 18 48'Status'
     A*
     A            #GLFON        26A    19 16TEXT('Job')
     A                                      DSPATR(HI)
     A            #JSTS          8A    19 48TEXT('Status')
     A                                      DSPATR(HI)
     A*
     A                                 23  2'F3=Main Menu,'
     A                                 23 16'F5=Refresh,'
     A                                 23 28'F8=Request Tfr,'
     A                                 23 44'F9=Restart Backgd Job,'
     A                                 23 67'F10=Stop Job'
     A*
     A*=========================================================================
     A*
     A          R #HDR                      TEXT('HEADER')
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                      OVRDTA
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 25'Electronic Banking File Resend'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A*
     A                                  4  2'Reset'
     A                                  4 12'Generation'
     A                                  4 26'Status'
     A                                  4 35'Last Download'
     A                                  4 51'No. of'
     A*
     A                                  5  2'Status'
     A                                  5 15'Date'
     A                                  5 51'Tries'
     A*
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
