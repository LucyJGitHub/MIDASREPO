     A*****************************************************************
/*STD *  DSPFBASE
/*EXI *  TEXT('Midas GL Fontis narrative codes maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Midas-Fontis Interface                               *
     A*                                                               *
     A*  DSPF/GL2811DF - Fontis Narrative Codes Maintenance           *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. CGL004 *CREATE     Date 20FEB97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  CGL004 - Midas-Fontis Interface                              *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*
     A*================================================================
     A*
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  84                                  SFLNXTCHG
      *
     A            #1DBRC        48   H      TEXT('Existing record fields')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A*
     A            #1SEL          1   B  7  2
     A                                      VALUES('D')
     A                                      CHECK(AB)
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A  79N33                               DSPATR(ND)
     A N79N33                               DSPATR(UL HI)
      *
     A            #PMTT          2   B  7  6TEXT('Proj. Mvmt Trans. Type')
     A  88                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N88N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
      *
     A            #FTTR          1   B  7 13TEXT('Funds Transfer')
     A  88                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N88N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
      *
     A            #LANC          2   B  7 20TEXT('Language Code')
     A  78                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N78N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
      *
     A            #NATV         34   B  7 24TEXT('Narrative')
     A                                      CHECK(LC)
     A  78                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N78N37                               DSPATR(UL)
     A N37                                  DSPATR(HI)
      *
     A            #STTI          3   B  7 61TEXT('SWIFT Transaction Type ID')
     A  78                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A N78N38                               DSPATR(UL)
     A N38                                  DSPATR(HI)
      *
     A            #TRMD          1   B  7 74TEXT('Transaction Module Code')
     A  78                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A N78N39                               DSPATR(UL)
     A N39                                  DSPATR(HI)
     A*
     A*================================================================
     A*
     A          R #SFLCTL                   TEXT('Subfile Control')
     A                                      SFLCTL(#SFLRCD)
     A                                      SFLPAG(15)
     A                                      SFLSIZ(16)
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
      *
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2PMTT')
     A                                      SETOF(32 'Invalid: #2FTTR')
     A*.........................................................................
     A*.........................................................................
      *
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
      *
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
      *
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
      *
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
      *
     A                                  2 17'Electronic Banking Narrative +
                                            Codes Maintenance'
     A                                      DSPATR(HI)
      *
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
      *
     A                                  4  4'Trans.'
     A                                  4 11'Funds'
     A                                  4 19'Lang.'
     A                                  4 27'Narrative'
     A                                  4 57'Swift Trans.'
     A                                  4 72'Midas'
      *
     A                                  5  5'Type'
     A                                  5 11'Trf.'
     A                                  5 19'Code'
     A                                  5 61'Type'
     A                                  5 70'Mod. Code'
      *
     A N89        #2PMTT         2   B  6  6
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        #2FTTR         1   B  6 13
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
