      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL IAT/TPP activation deactivation function')
      *****************************************************************
      *                                                               *
      *  Midas - GL Module                                            *
      *                                                               *
      *  DSPF/GL2821DF - IAT/TPP Activation Deactivation Function     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 22Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CGL009  *CREATE    Date 20May99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      * CAAA03 - Webfacing Changes.                                   *
      * CGL009  - Midas-Fontis IAT/TPP interface                      *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R GL2821F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CF05(05 'Refresh')
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
      *
     A                                      PUTOVR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'GL2821F1'
     A                                  1 23'IAT/TPP Activation Deactivation F-
     A                                      unction'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  7  2'Action'
     A                                      DSPATR(HI)
     A                                  7 10'Processing'
     A                                      DSPATR(HI)
     A                                  7 23'Background Job'
     A                                      DSPATR(HI)
     A                                  7 52'Status'
     A                                      DSPATR(HI)
     A                                  7 68'No. of Files'
     A                                      DSPATR(HI)
     A                                  8 13'Task'
     A                                      DSPATR(HI)
     A                                  8 68'To Process'
     A                                      DSPATR(HI)
     A            SACIA          1A  B 11  3
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A                                      OVRDTA
     A            SPCIA          3A  O 11 13
     A                                      OVRDTA
     A            SBGIA         28A  O 11 22
     A                                      OVRDTA
     A            SSTIA         21A  O 11 51
     A                                      OVRDTA
     A            SNFIA          3A  O 11 74
     A                                      OVRDTA
     A            SACTP          1A  B 13  3
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A                                      OVRDTA
     A            SPCTP          3A  O 13 13
     A                                      OVRDTA
     A            SBGTP         28A  O 13 22
     A                                      OVRDTA
     A            SSTTP         21A  O 13 51
     A                                      OVRDTA
     A            SNFTP          3A  O 13 74
     A                                      OVRDTA
     A*
     A                                 23  2'S=Start Job, T=Terminate Job, F3=-
     A                                      Exit, F5=Refresh'
     A*
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A
     A  25                                  SFLEND
     A            ##PGM                     SFLPGMQ
