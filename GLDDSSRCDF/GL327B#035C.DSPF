      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Account Details Display - TA')
     A*****************************************************************
     A*                                                               *
     A*  Midas   General Ledger Module                            *
     A*                                                               *
     A*  DSPF/GL327B#C - ACCOUNT DETAILS DISPLAY FILE                 *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 18Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 079441             Date 30Nov94               *
     A*                 S01413 *CREATE     Date 13Apr93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Webfacing Changes                                   *
      *         - This DSPF has been rewritten and code stripped for  *
      *           proper display of window in WebFacing.              *
     A*  079441 - Modernise window display.                           *
     A*  S01413 - Retail 3 Incorporation                              *
     A*                                                               *
     A*****************************************************************
     A*                                                             : *
     A*  C R E A T I O N     P A R A M E T E R S                    : *
     A*                                                             : *
     A*---------------------------------------------------------------*
     A*
     A*  The naming standards for the record formats should be the
     A*  file name but changing # to F, i.e.
     A*
     A*  GL3271#C  ===>  GL3271F0
     A*
     A*  Field names should always start with # followed by the format
     A*  number (i.e. 1) and the file field name, i.e.
     A*
     A*  CANT ===> #0CANT
     A*
     A*-------------------------------------------------------------------------
     A*
     A*  The code in this display file that can be changed is within
     A*  the record format(s).
     A*  None of the following keywords can be changed : ASSUME
     A*                                                  OVERLAY
     A*                                                  PUTOVR
     A*                                                  OVRATR
     A*                                                  OVRDTA
     A*
     A*-------------------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A  11                                  CF10
     A                                      CF03
     A                                      CF12
     A** NOT IN SKELETON PGM DSPF - BEGIN
     A                                      ROLLUP
     A                                      ROLLDOWN(08 'Rolldown requested')
     A** NOT IN SKELETON PGM DSPF - END
     A*
     A          R GL327BF0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      KEEP
     A                                      OVERLAY
     A  72 73                               PUTOVR
     A                                      WINDOW(07 15 11 62)
     A                                  3  2' Interest taxable. . . . . . . . .-
     A                                       . . . . . . :  '
     A N12                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
     A  12        #0ITAX         1A  B  3 53OVRDTA
     A                                      OVRATR
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  21N10                               DSPATR(PC)
     A                                  4  2' Statement charge. . . . . . . . .-
     A                                       . . . . . :  '
     A                                      OVRATR
     A                                      OVRDTA
     A            #0STCI         1A  B  4 53OVRDTA
     A                                      OVRATR
     A N21
     A  21                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  21N10                               DSPATR(PC)
     A                                  5  2' Statement fee amount. . . . . . .-
     A                                       . . . :  '
     A                                      OVRATR
     A                                      OVRDTA
     A N22                                  DSPATR(ND)
     A  22        #0FAMT         8   B  5 53OVRDTA
     A                                      DSPATR(HI)
     A  10                                  DSPATR(PR)
     A N22                                  DSPATR(ND)
     A                                  6  2' Subject to account charge (Y/N) .-
     A                                       . . :  '
     A                                      OVRATR
     A                                      OVRDTA
     A            #0CHIN         1A  B  6 53OVRDTA
     A                                      OVRATR
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  23N10                               DSPATR(PC)
     A                                  7  2' Account charge amount . . . . . .-
     A                                       . . . :  '
     A                                      OVRATR
     A                                      OVRDTA
     A            #0CHAM         8A  B  7 53OVRDTA
     A                                      OVRATR
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  24N10                               DSPATR(PC)
     A                                 10  2'  Rollup Rolldown  '
     A                                      OVRATR
     A                                      OVRDTA
     A                                 10 25' F10=Confirm Delete  '
     A                                      OVRATR
     A N11                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 10 48'F12=Cancel'
     A                                      OVRATR
     A                                      OVRDTA
     A          R GL327BF1
     A                                      KEEP
     A                                      OVERLAY
     A N73                                  PUTOVR
     A                                      WINDOW(07 15 08 62)
     A                                  3  2' Interest taxable. . . . . . . . .-
     A                                       . . . . . . :  '
     A N12                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
     A  12        #0ITAX         1A  B  3 53OVRDTA
     A                                      OVRATR
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  21N10                               DSPATR(PC)
     A                                  4  2' Statement charge. . . . . . . . .-
     A                                       . . . . . :  '
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
     A  72        #0STCI         1A  B  4 53OVRDTA
     A                                      OVRATR
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  21N10                               DSPATR(PC)
     A                                  5  2' Statement fee amount. . . . . . .-
     A                                       . . . :  '
     A                                      OVRATR
     A                                      OVRDTA
     A N22                                  DSPATR(ND)
     A  22        #0FAMT         8   B  5 53OVRDTA
     A                                      DSPATR(HI)
     A  10                                  DSPATR(PR)
     A N22                                  DSPATR(ND)
     A                                  7  2'  Rollup Rolldown  '
     A                                      OVRATR
     A                                  7 25' F10=Confirm Delete  '
     A                                      OVRATR
     A N11                                  DSPATR(ND)
     A                                      OVRDTA
     A                                  7 48'F12=Cancel'
     A                                      OVRATR
     A                                      OVRDTA
     A          R GL327BF2
     A                                      KEEP
     A                                      OVERLAY
     A  73N72                               PUTOVR
     A                                      WINDOW(07 15 08 62)
     A                                  3  2' Interest taxable. . . . . . . . .-
     A                                       . . . . . . :  '
     A N12                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
     A  12        #0ITAX         1A  B  3 53OVRDTA
     A                                      OVRATR
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  21N10                               DSPATR(PC)
     A                                  4  2' Subject to account charge (Y/N) .-
     A                                       . . :  '
     A                                      OVRATR
     A                                      OVRDTA
     A            #0CHIN         1A  B  4 53OVRDTA
     A                                      OVRATR
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  23N10                               DSPATR(PC)
     A                                  5  2' Account charge amount . . . . . .-
     A                                       . . . :  '
     A                                      OVRATR
     A                                      OVRDTA
     A            #0CHAM         8A  B  5 53OVRDTA
     A                                      OVRATR
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  24N10                               DSPATR(PC)
     A                                  7  2'  Rollup Rolldown  '
     A                                      OVRATR
     A                                  7 25' F10=Confirm Delete  '
     A                                      OVRATR
     A N11                                  DSPATR(ND)
     A                                      OVRDTA
     A                                  7 48'F12=Cancel'
     A                                      OVRATR
     A                                      OVRDTA
     A          R GL327BF3
     A                                      ASSUME
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2' '
     A                                      OVRATR
     A                                      OVRDTA
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            WWPGM                     SFLPGMQ
      *-------------------------------------------------------------------------
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  99
     AON99                                  SFLEND
     A            WWPGM                     SFLPGMQ
