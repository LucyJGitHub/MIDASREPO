      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Account balances')
     A*****************************************************************
     A*                                                               *
     A*  Midas GENERAL LEDGER MODULE                              *
     A*                                                               *
     A*  DSPF/GL4610DF - ACCOUNT CODE BALANCE ENQUIRY DISPLAY FILE    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*      COMPANY CONFIDENTIAL                                     *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG11262           Date 27Apr06               *
      *  Prev Amend No. BUG1855            Date 22Apr04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 048565             Date 06Jan93               *
      *                 S01399 *CREATE     Date 19May92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  BUG11262 - No previous or back button from account code      *
      *             balance enquiry; Webfacing.                       *
      *  BUG1855 - Webfacing Changes                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  048565 - No help for Account Balances Enquiry.               *
     A*                                                               *
     A*  S01399 - ACCOUNT CODES BALANCES ENQUIRY ENHANCEMENT.         *
     A*           (SAR NO. MIS00020) UPGRADE TO REL 10                *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(22)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03(03)
     A***********       Start of Change                      /048565/
     A***               End of Change                        /048565/
     A*
     A          R AU4610X0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A          R AU4610X1                  TEXT('Heading')
     A                                      OVERLAY
     A            USRID         10   O  1  2TEXT('User ID')
     A                                  1 13'GL4610'
     A            RUNA           7   O  1 72TEXT('System Run Date')
     A            WSID          10   O  2  2TEXT('Workstation ID')
     A            #ATIME         6  0O  2 71EDTWRD('  :  :  ')
     A                                  2 27'ACCOUNT CODE BALANCE +
     A                                      ENQUIRY'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R AU4610X2                  TEXT('Screen Display 1')
     A                                      OVERLAY
     A***********       Start of Change                      /048565/
     A***               End of Change                        /048565/
     A                                  6 27'Enter Account Code'
     A**Start of change CGL029
     A**********  #ACODE         4A  B  6 46TEXT('Account Code')                              CGL029
     A            #ACODE        10A  B  6 46TEXT('Account Code')
     A**End of change CGL029
     A  20                                  DSPATR(PC RI)
     A                                      DSPATR(UL)
     A  85                              8 27'Branch'
     A  85        #ABRAN         3A  B  8 46TEXT('Branch')
     A  85 21                               DSPATR(PC RI)
     A  85                                  DSPATR(UL)
     A                                 10 27'Currency'
     A            #ACCY          3A  B 10 46TEXT('Currency')
     A  22                                  DSPATR(PC RI)
     A                                      DSPATR(UL)
     A                                 12 27'Start Date'
     A            #ASDAT         6A  B 12 46TEXT('Start Date')
     A  23                                  DSPATR(PC RI)
     A                                      DSPATR(UL)
     A                                 14 27'End Date'
     A            #AEDAT         6A  B 14 46TEXT('End Date')
     A  24                                  DSPATR(PC RI)
     A                                      DSPATR(UL)
     A*/COPY WNCPYSRC,GL4610DF01
     A*
     A          R AU4610X3                  TEXT('Screen Display 2')
     A                                      OVERLAY
     A                                  5  2'Account Code:'
     A**Start of change CGL029
     A**********  #ACODE         4A  O  5 16TEXT('Account Code')                              CGL029
     A            #ACODE        10A  O  5 16TEXT('Account Code')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A                                  5 31'Start Date:'
     A            #ASDTE         8A  O  5 43TEXT('Start Date')
     A                                      DSPATR(HI)
     A                                  5 61'End Date:'
     A            #AEDTE         8A  O  5 71TEXT('End Date')
     A                                      DSPATR(HI)
     A                                  7  2'Posting'
     A                                  7 22'Customer'
      ** Start of change BUG1855
     A**********                        7 33'Sequence'                                       BUG1855
     A                                  7 31'Sequence'
     A**********                        7 45'Module'                                         BUG1855
     A                                  7 41'Module'
      ** End of change BUG1855
     A                                  7 61'Transaction'
     A                                  8  3'Date'
     A  85                              8 13'Branch'
     A                                  8 23'Number'
      ** Start of change BUG1855
     A**********                        8 34'Number'                                         BUG1855
     A                                  8 32'Number'
     A**********                        8 47'ID'                                             BUG1855
     A                                  8 43'ID'
      ** End of change BUG1855
     A                                  8 63'Amount'
     A*
     A          R AU4610X4                  TEXT('Screen Footer 1')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  5'F3=Exit'
     A*
     A          R AU4610X5                  TEXT('Screen Footer 2')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  5'F3=Exit'
     A                                 23 22'F12=Previous'
     A                                 23 45'Roll Up/Down'
     A*
     A          R AU4610S1                  TEXT('Message Subfile')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A          R AU4610C1                  TEXT('Message Subfile Control')
     A**Start of change BUG11262
     A                                      OVERLAY
     A**End of change BUG11262
     A N40                                  CA12(12)
     A                                      SFLCTL(AU4610S1)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0001)
     A N30                                  SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A                                      PUTOVR
     A  31                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
     A*
     A          R AU4610S2                  TEXT('Subfile Record Format')
     A                                      SFL
     A            #AGENL        78A  O 10  2TEXT('Detail Line')
     A  29                                  DSPATR(HI)
     A*
     A          R AU4610C2                  TEXT('Subfile Control Format')
     A                                      OVERLAY
     A                                      CA12(12)
     A                                      SFLCTL(AU4610S2)
     A                                      SFLSIZ(11)
     A                                      SFLPAG(10)
     A  28                                  SFLDSP
     A  28                                  SFLDSPCTL
     A N28                                  SFLCLR
     A  35                                  SFLEND
     A  36                                  ROLLUP(32)
     A            #ASFRN         4  0H      SFLRCDNBR
