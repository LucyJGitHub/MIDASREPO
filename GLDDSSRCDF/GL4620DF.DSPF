      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Certificate of Balances Prompt Screen')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL4620DF - Certificate of Balances prompt screen        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG3074            Date 08Jun04               *
      *  Prev Amend No. CGL024  *CREATE    Date 21Jul03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG3074- Webfacing Changes.  Created new record format for   *
      *           the function keys narratives.                       *
      *  CGL024 - Certificates & Confirmations of Balances.           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R GL4620F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A                                      CF05
     A                                      PUTOVR
      ** Start of change BUG3074
     A                                      OVERLAY
      ** End of change BUG3074
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'GL4620DF'
     A                                  2 25'CERTIFICATES OF BALANCES PROMPT'
     A                                      DSPATR(UL HI)
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 73TIME
      *
     A                                  4  3'Customer Number..........:'
     A            SCNUM          6A  B  4 30DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  5  3'Customer Reference.......:'
     A            SCREF         50A  B  5 30DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  6  3'Date of Reference........:'
     A            SDREF          6A  B  6 30DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  7  3'Balance Date.............:'
     A            SBALD          6A  B  7 30DSPATR(HI)
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  8  3'Customer''s Auditor Line 1:'
     A            SCAL1         50A  B  8 30DSPATR(HI)
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  9  3'Customer''s Auditor Line 2:'
     A            SCAL2         50A  B  9 30DSPATR(HI)
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 10  3'Customer''s Auditor Line 3:'
     A            SCAL3         50A  B 10 30DSPATR(HI)
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 11  3'Customer''s Auditor Line 4:'
     A            SCAL4         50A  B 11 30DSPATR(HI)
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(UL)
     A                                      OVRDTA
      *
      ** Start of change BUG3074
     A          R GL4620F1
     A                                      OVERLAY PUTOVR
      ** End of change BUG3074
     A                                 23  3'F3=End  F5=Refresh'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
