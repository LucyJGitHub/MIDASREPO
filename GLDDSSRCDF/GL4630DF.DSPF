      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Confirmation of Balances Prompt Screen')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL4630DF - Confirmation of Balances Prompt Screen       *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG3074            Date 08Jun04               *
      *                 CGL024  *CREATE    Date 21Jul03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG3074- Webfacing Changes.  Created new record format for   *
      *           the function keys narratives.                       *
      *  CGL024 - Certificates & Confirmations of Balances.           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R GL4630F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A                                      CF05
     A                                      PUTOVR
      ** Start of change BUG3074
     A                                      OVERLAY
      ** End of change BUG3074
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'GL4630DF'
     A                                  2 25'CONFIRMATION OF BALANCES PROMPT'
     A                                      DSPATR(UL HI)
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 73TIME
      *
     A                                  4  3'Customer Number...........:'
     A            SCNUM          6A  B  4 31DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  5  3'Balance Date..............:'
     A            SBALD          6A  B  5 31DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  6  3'Confirmation to be Sent To:'
     A                                  6 31'Us'
     A            SS2US          1A  B  6 34DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  6 36'Our Auditor'
     A            SS2AU          1A  B  6 48DSPATR(HI)
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  7  3'Bank Auditor Line 1.......:'
     A            SBAU1         50A  B  7 31DSPATR(HI)
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  8  3'Bank Auditor Line 2.......:'
     A            SBAU2         50A  B  8 31DSPATR(HI)
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  9  3'Bank Auditor Line 3.......:'
     A            SBAU3         50A  B  9 31DSPATR(HI)
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 10  3'Bank Auditor Line 4.......:'
     A            SBAU4         50A  B 10 31DSPATR(HI)
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 11  3'Auditor''s Fax No..........:'
     A            SFXNO         50A  B 11 31DSPATR(HI)
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(UL)
     A                                      OVRDTA
      *
      ** Start of change BUG3074
     A          R GL4630F1
     A                                      OVERLAY PUTOVR
      ** End of change BUG3074
     A                                 23  3'F3=End  F5=Refresh'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
