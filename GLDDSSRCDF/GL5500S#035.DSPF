      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Select spread profile header')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL5500S# - Select Spread Profile Header                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(35 'Invalid #1SEL     ')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last change date')
     A            #1TYLC         1   H      TEXT('Type of last change')
     A            #1SEL          1   B  9  2
     A                                      VALUES('1')
     A                                      CHECK(AB)
     A  35                                  DSPATR(RI PC) COLOR(WHT)
     A N35                                  DSPATR(UL HI) COLOR(GRN)
     A            #1SPRF         4      9  6TEXT('Spread Profile Code')
     A                                      DSPATR(HI) COLOR(WHT)
     A            #1SPDC        30      9 15TEXT('Spread Profile Descr.')
     A                                      DSPATR(HI) COLOR(WHT)
     A            #1CTTL        10  0   9 49TEXT('Control Total')
     A                                      EDTCDE(4)
     A                                      DSPATR(HI) COLOR(WHT)
     A            #1NUPC         2  0   9 65TEXT('No. of Profit Centres')
     A                                      EDTCDE(4)
     A                                      DSPATR(HI) COLOR(WHT)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2SPRF')
     A                                      SETOF(32 'Invalid: #2SPDC')
     A                                      SETOF(33 'Invalid: #2CTTL')
     A                                      SETOF(34 'Invalid: #2NUPC')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'GL5500S1'
     A                                      COLOR(WHT)
     A            ##MRDT         7      1 72TEXT('Midas Rundate')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A                                  2 31'Select Spread Profile'
     A                                      DSPATR(HI) COLOR(WHT)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            #2LCD          5  0H      TEXT('Last change date')
     A            #2TYLC         1   H      TEXT('Type of last change')
     A                                  4  6'Spread'
     A                                      COLOR(TRQ)
     A                                  4 21'Spread Profile'
     A                                      COLOR(TRQ)
     A                                  4 50'Control'
     A                                      COLOR(TRQ)
     A                                  4 61'No. of Profit'
     A                                      COLOR(TRQ)
     A                                  5  6'Profile'
     A                                      COLOR(TRQ)
     A                                  5 21' Description'
     A                                      COLOR(TRQ)
     A                                  5 50' Total'
     A                                      COLOR(TRQ)
     A                                  5 61'  Centres'
     A                                      COLOR(TRQ)
     A                                  6  6' Code'
     A                                      COLOR(TRQ)
     A            #2SPRF         4   B  7  6TEXT('Spread Profile Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC) COLOR(WHT)
     A N31                                  DSPATR(UL HI) COLOR(GRN)
     A N25                                  OVRATR
     A            #2SPDC        30   B  7 15TEXT('Spread Profile Descr.')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC) COLOR(WHT)
     A N32                                  DSPATR(UL HI) COLOR(GRN)
     A N25                                  OVRATR
     A            #2CTTL        10  0B  7 49TEXT('Control Total')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC) COLOR(WHT)
     A N33                                  DSPATR(UL HI) COLOR(GRN)
     A N25                                  OVRATR
     A            #2NUPC         2  0B  7 65TEXT('No. of Profit Centres')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC) COLOR(WHT)
     A N34                                  DSPATR(UL HI) COLOR(GRN)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select request   F3=Exit        -
     A                                                                         -
     A                                               '
     A                                      COLOR(WHT)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
