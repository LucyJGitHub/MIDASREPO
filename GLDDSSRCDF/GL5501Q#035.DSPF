      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Display spread profile')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL5500S# - Display Spread Profile                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. NNNNNN  *CREATE    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A            #1SPRF         4   H      TEXT('Spread Profile Code')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1PCCD         4     10  4TEXT('Profit Centre Code')
     A                                      DSPATR(HI)
     A            #1AMOT         8  0  10 15TEXT('Amount')
     A                                      EDTCDE(3)
     A                                      DSPATR(HI)
     A            #RPCDF        30     10 30TEXT('Profit Centre Desc field')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A                                      INDTXT(87 'Display detils.')
     A                                      INDTXT(88 'Prompt header key.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'GL5501Q1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 23'Profit Centre Spread Profile Enqui-
     A                                      ry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
     A                                  4  2'Spread Profile Code.:'
     A            #2SPRF         4      4 24TEXT('Spread Profile Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N88                              4 36'Description:'
     A N88        #2SPDC        30      4 49TEXT('Spread Profile Descr.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N88                              6  2'Control Total.......:'
     A N88        #2CTTL        10  0   6 24TEXT('Control Total')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N88                              6 36'Number of Profit Centres:'
     A N88        #2NUPC         2  0   6 62TEXT('No. of Profit Centres')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N88                              8  3'Profit'
     A N88                              8 15'Amount'
     A N88                              8 30'Profit Centre'
     A N88                              9  3'Centre'
     A N88                              9 30'Description'
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu   F12=Previous       -
     A                                                                         -
     A                                               '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
