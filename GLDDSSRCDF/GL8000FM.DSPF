      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL History Postings Enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - General Ledger Module                            *
     A*                                                               *
     A*  DSPF/GL8000FM - History Postings Enquiry                     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA03             Date 19Apr04               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01520  *CREATE    Date 02Nov94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CAAA03 - Webfacing Changes.                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  S01520 - BLI Account Statements Changes.                     *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A                                      CF03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A*
     A** Screen heading record format
     A*
     A          R GL8000H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      OVERLAY
     A                                      PUTRETAIN
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'GL8000'
     A                                  1 28'HISTORIC MOVEMENTS ENQUIRY'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*
     A** Customer and Account details record format
     A*
     A          R GL8000F1
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  4  2'Customer Name'
     A N64                                  DSPATR(ND)
     A                                      OVRATR
     A            CRNM      R        O  4 16
     A                                      DSPATR(HI)
     A N64                                  DSPATR(ND)
     A                                      OVRDTA
     A**Start of change CGL029
     A**********                        4 54'A/c'                                             CGL029
     A                                  4 47'A/c'
     A**End of change CGL029
     A N64                                  DSPATR(ND)
     A                                      OVRATR
     A**Start of change CGL029
     A*N20******  ACNOS         18   O  4 58                                                  CGL029
     A N20        ACNOS         24   O  4 51
     A**End of change CGL029
     A                                      DSPATR(HI)
     A N64                                  DSPATR(ND)
     A                                      OVRDTA
     A**Start of change CGL029
     A**20******  ACNOM         22   O  4 58                                                  CGL029
     A  20        ACNOM         28   O  4 51
     A**End of change CGL029
     A                                      DSPATR(HI)
     A N64                                  DSPATR(ND)
     A                                      OVRDTA
     A            CTWN      R        O  5  2
     A                                      DSPATR(HI)
     A N64                                  DSPATR(ND)
     A                                      OVRDTA
     A            ANAM      R        O  5 16
     A                                      DSPATR(HI)
     A N64                                  DSPATR(ND)
     A                                      OVRDTA
     A                                  5 47'Ret A/c no'
     A N64
     AON21                                  DSPATR(ND)
     A                                      OVRATR
     A            ACNO      R        O  5 58
     A N64
     AON21                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A*
     A** Subfile heading record format
     A*
     A          R GL8000F2
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  6  2'Posting'
     A  68
     AO 31                                  DSPATR(ND)
     A                                      OVRATR
     A                                  6 12'Value'
     A  68
     AO 31                                  DSPATR(ND)
     A                                      OVRATR
     A                                  6 22'Narrative'
     A  68
     AO 31                                  DSPATR(ND)
     A                                      OVRATR
     A                                  6 58'CLOSED ACCOUNT'
     A  31
     AO 68
     AON25                                  DSPATR(ND)
     A                                      OVRATR
     A*
     A** Subfile record format
     A*
     A          R GL8000S1                  SFL
     A            DATE1          7A  O  7  2DSPATR(HI)
     A            VALDAT         7A  O  7 12DSPATR(HI)
     A            NARR          30A  O  7 22DSPATR(HI)
     A            PSTAM         22A  O  7 55DSPATR(HI)
     A*
     A** Subfile control record format
     A*
     A          R GL8000C1                  SFLCTL(GL8000S1)
     A                                      BLINK
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A  22                                  SFLDSPCTL
     A  23                                  SFLDSP
     A  31
     AO 68
     AON23N22                               SFLCLR
     A  27                                  SFLEND
     A  63                                  SFLMSGID(CPF5203 QCPFMSG)
     A N27 23                               ROLLUP(08)
     A                                      TEXT('SUBFILE CONTROL REC')
     A                                      OVERLAY
     A                                      PUTOVR
     A  68
     AO 31                                  ERASE(GL8000S1)
     A            DSPREC         4S 0H      SFLRCDNBR
     A*
     A*                                19 31'* COMPLETE *'
     A*N66                                  DSPATR(ND)
     A*                                     OVRATR
     A            ERRMSG        76A  O 20  2
     A N31                                  DSPATR(ND)
      ** Start of Change CAAA03
     A**********                            DSPATR(RI HI)                                     CAAA03
     A                                      DSPATR(HI)
     A  31                                  DSPATR(RI)
      ** End of Change CAAA03
     A                                      OVRDTA
     A                                 21  2'F3=Exit   Roll-up   Roll-down'
     A                                 22  2'Enter Account'
     A            CNUMR          6A  B 22 16
     A N30N65                               DSPATR(HI)
     A  65
     AO 30                                  DSPATR(RI PC)
     A                                      OVRDTA
     A            CURRY          3A  B 22 23
     A N30N65                               DSPATR(HI)
     A  65
     AO 30                                  DSPATR(RI)
     A                                      OVRDTA
     A**Start of change CGL029
     A**********  ACCOD          4A  B 22 27                                                  CGL029
     A            ACCOD         10A  B 22 27
     A**End of change CGL029
     A N30N65                               DSPATR(HI)
     A  65
     AO 30                                  DSPATR(RI)
     A                                      OVRDTA
     A**Start of change CGL029
     A**********  ACSEQ          2A  B 22 32                                                  CGL029
     A            ACSEQ          2A  B 22 38
     A**End of change CGL029
     A N30N65                               DSPATR(HI)
     A  65
     AO 30                                  DSPATR(RI)
     A                                      OVRDTA
     A**Start of change CGL029
     A**********  BRNCH          3A  B 22 35                                                  CGL029
     A            BRNCH          3A  B 22 41
     A**End of change CGL029
     A N30N65                               DSPATR(HI)
     A  65
     AO 30                                  DSPATR(RI)
     A N20                                  DSPATR(ND)
     A N20                                  DSPATR(PR)
     A                                      OVRDTA
     A**Start of change CGL029
     A**********                       22 41'Or Retail A/c'                                   CGL029
     A                                 22 47'Or Retail A/c'
     A**End of change CGL029
     A N21                                  DSPATR(ND)
     A                                      OVRATR
     A**Start of change CGL029
     A**********  RTAC          10A  B 22 55                                                  CGL029
     A            RTAC          10A  B 22 61
     A**End of change CGL029
     A N65N33                               DSPATR(HI)
     A  33
     AO 65                                  DSPATR(RI)
     A N21                                  DSPATR(ND)
     A N21                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 23  2'Enter Start Date'
     A            STDT           6A  B 23 19
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A                                      OVRDTA
     A*
     A** Message subfile record format
     A*
     A          R GL8000S2                  TEXT('Message subfile format')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A** Message subfile control format
     A*
     A          R GL8000C2
     A                                      SFLCTL(GL8000S2)
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A N68                                  SFLCLR
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
     A*
