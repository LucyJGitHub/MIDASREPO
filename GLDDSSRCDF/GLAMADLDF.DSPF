      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL AMAD - Display File')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GLAMADLDF - GL Account Details                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *CREATE    Date 25Apr05               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
      *
      * --------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                         *
      * --------------------------------------------------------------*
      *
     A          R GLAMADF0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      KEEP
     A                                      OVERLAY
     A                                      CF03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A  40                                  CF10(10 'Delete')
     A                                      CF12(12 'Previous')
     A                                      PUTOVR
     A            PSUSER    R        O  1  2REFFLD(USER)
     A                                  1 13'GLAMADF1'
     A            PSPROCNAME    10A  H
     A                                  1 25'European Reporting - Luxembourg'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            BJMRDT    R        O  1 74REFFLD(RUNA)
      *
     A            PSJOBNAME R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  4  2'Correction account...........:'
     A            L6LXCOBR       3A  B  4 33OVRDTA
     A                                      OVRATR
     A N50N20                               DSPATR(HI)
     A  50                                  DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A            L6LXCOAC      21A  B  4 37OVRDTA
     A                                      OVRATR
     A N51N20                               DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                  5  2'Correction amount............:'
     A            L6LXCOAM      15A  B  5 33OVRDTA
     A                                      OVRATR
     A N52N20                               DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  52N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                  6  2'Lump-Sum Code................:'
     A            L6LXLUSU       2A  B  6 33OVRDTA
     A                                      OVRATR
     A N53N20                               DSPATR(HI)
     A  53                                  DSPATR(RI)
     A  53N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                  7  2'Consumer Credit Code.........:'
     A            L6LX1UAC       1A  B  7 33OVRDTA
     A                                      OVRATR
     A N54N20                               DSPATR(HI)
     A  54                                  DSPATR(RI)
     A  54N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                 23  2'F3=Main Menu'
     A                                      COLOR(BLU)
     A  35                             23 16'F5=Refresh'
     A                                      COLOR(BLU)
     A  40                             23 28'F10=Delete'
     A                                      COLOR(BLU)
     A                                 23 40'F12=Previous'
     A                                      COLOR(BLU)
      *
      * --------------------------------------------------------------*
      *   Message subfile format                                      *
      * --------------------------------------------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * --------------------------------------------------------------*
      *   Message subfile control                                     *
      * --------------------------------------------------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
