      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Account maintenance - primary display')
      *****************************************************************
      *                                                               *
      *  Midas - GENERAL LEDGER Module                                *
      *                                                               *
      *  DSPF/GLAMADPDF - Account Maintenance - Primary Display       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 222549             Date 06Jun06               *
      * Midas Release 4.01.03 ----------------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01.03 ----------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 185023             Date 13Oct00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP035  *CREATE    Date 19Mar99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  222549 - Introduced *IN36 to be able to handle linked enquiry*
      *           processing.                                         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  185023 - Replace DBA2 help tag references with DBA3 ones.    *
      *  CAP035 - Midas/ToF Interface                                 *
      *                                                               *
      *****************************************************************
      *                                                               *
      *     Function of Indicators                                    *
      *                                                               *
      *  29 - Retail Number is used                                   *
      *  42 - Confirm Delete Indcator                                 *
      *  48 - Retail A/c No. Required                                 *
      *  50-56 -  Field Error - highlight screen fields               *
      *  68 - Position cursor on action code (if it is blank)         *
      *  70 - Insert Mode                                             *
      *  73 - Delete Mode                                             *
      *  96 - Subfile control                                         *
      *  97 - Subfile end                                             *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      VLDCMDKEY(49 'ANY VALID KEY')
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      INDARA
      **Start of change 185023
      **End of change 185023
      *
      *****************************************************************
     A          R GLAMADS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      *****************************************************************
     A          R GLAMADC1                  SFLCTL(GLAMADS1)
     A                                      SFLSIZ(24)
     A                                      SFLPAG(1)
     A N96                                  SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      OVERLAY
     A            DDPGMQ                    SFLPGMQ
      *
      *****************************************************************
     A          R GLAMADF1                  TEXT('SCREEN FOOTER')
     A                                      OVERLAY
     A                                      CF03
     A N42 35                               CF05
     A*N35********                          CF07                                              222549
     A*N35********                          CF08                                              222549
     A N35N36                               CF07                                              222549
     A N35N36                               CF08                                              222549
     A  42 35                               CF10
     A                                      CF12
      *
     A                                 22  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
      *
     A                                 23 02'F3=Exit'
      *
     A  35N42                          23 11'F5=Refresh'
      *
     A*N35********                     23 11'F7=Prev'                                         222549
     A N35N36                          23 11'F7=Prev'                                         222549
      *
     A  42                             23 11'F10=Delete'
      *
     A*N35********                     23 20'F8=Next'                                         222549
     A N35N36                          23 20'F8=Next'                                         222549
      *
     A                                 23 29'F12=Cancel'
      *
     A*N35********                     23 42'Action E,1..>5 I A D'                            222549
     A N35N36                          23 42'Action E,1..>5 I A D'                            222549
      *
      *****************************************************************
     A          R GLAMADDK                  OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A N42                                  CF05
     A  42                                  CF10
      **Start of change 185023
      **********
      **********
      **********
      **********
      **********
      **********
      **********
      *
      *
      *
      *
      *
      *
      *
      **End of change 185023
      *
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A                                  1 13'GLAMADDK'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DDWSID    R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
      *
     A  70                              2 25'OPENING OF ACCOUNT'
     A                                      DSPATR(UL HI)
     A  73                              2 25'CLOSING OF ACCOUNT'
     A                                      DSPATR(UL HI)
      *
     A            DDTIME         6Y 0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
      *
     A                                  3  2'Action'
     A            DDACTN         1A  O  3 10TEXT('ACTION')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
      *
     A                                  6 02'Branch'
     A            DDBRCA    R        B  6 10REFFLD(BRCA)
     A                                      DSPATR(HI)
     A N50N42                               DSPATR(UL)
     A  50N42                               DSPATR(PC RI)
     A  42                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  6 15'Customer number'
     A            DDCUSN    R        B  6 32REFFLD(CNUMA)
     A                                      DSPATR(HI)
     A N51N42                               DSPATR(UL)
     A  51N42                               DSPATR(PC RI)
     A  42                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  6 40'Currency'
     A            DDCCY     R        B  6 50REFFLD(CCY)
     A                                      DSPATR(HI)
     A N52N42                               DSPATR(UL)
     A  52N42                               DSPATR(PC RI)
     A  42                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  6 55'A/c. code'
     A**Start of change CGL029
     A**********  DDACOD    R        B  6 66REFFLD(ACODA)                                     CGL029
     A            DDACOD        10   B  6 66
     A**End of change CGL029
     A                                      DSPATR(HI)
     A N53N42                               DSPATR(UL)
     A  53N42                               DSPATR(PC RI)
     A  42                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A**Start of change CGL029
     A**********                        6 72'Seq.'                                            CGL029
     A**********  DDACSQ    R        B  6 78REFFLD(ACSQA)                                     CGL029
     A                                  7  2'Seq.'
     A            DDACSQ    R        B  7 10REFFLD(ACSQA)
     A**End of change CGL029
     A                                      DSPATR(HI)
     A N54N42                               DSPATR(UL)
     A  54N42                               DSPATR(PC RI)
     A  42                                  DSPATR(PR)
     A                                      OVRDTA
      *
      ** IF RETAIL NUMBER NOT USED FIELD NOT DISPLAYED.
     A**Start of change CGL029
     A**48******                        7  2'Retail Number  '                                 CGL029
     A**********  DDACNO    R        B  7 19REFFLD(RETL)                                      CGL029
     A  48                              7 15'Retail Number  '
     A            DDACNO    R        B  7 32REFFLD(RETL)
     A**End of change CGL029
     A                                      DSPATR(HI)
     A N55N42                               DSPATR(UL)
     A  55N42                               DSPATR(PC RI)
     A N48
     AO 42                                  DSPATR(PR)
     A N48                                  DSPATR(ND)
     A                                      OVRDTA
     A*/COPY WNCPYSRC,GLAMADPD01
      *
     A  73                             09 10'Account name '
     A            DDANAM        20A  B 09 25
     A                                      DSPATR(HI)
     A N56N42                               DSPATR(UL)
     A  56N42                               DSPATR(PC RI)
     A N73
     AO 42                                  DSPATR(PR)
     A N73                                  DSPATR(ND)
     A                                      OVRDTA
      *
      *****************************************************************
     A          R GLAMADDP                  OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      CF07
     A                                      CF08
      **Start of change 185023
      **End of change 185023
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A                                  1 13'GLAMADD0'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DDWSID    R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A                                  2 29'ACCOUNT DETAILS'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDTIME         6Y 0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  3  2'Action code'
     A            DDACTN         1A  B  3 15TEXT('ACTION')
     A  36                                  DSPATR(PR)                                        222549
     A                                      DSPATR(HI)
     A N50                                  DSPATR(UL)
     A  68
     AO 50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  4  2'Account Number'
     A**Start of change CGL029
     A**********  DDACCN        18A  B  4 17TEXT('Action key')                                CGL029
     A            DDACCN        24A  B  4 17TEXT('Action key')
     A**End of change CGL029
     A  36                                  DSPATR(PR)                                        222549
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PC RI)
     A                                      OVRDTA
     A**Start of change CGL029
     A**********                        4 48'Review from'                                     CGL029
     A**********  DDRFRA        18A  B  4 60TEXT('REVIEW FROM KEY')                           CGL029
     A                                  4 43'Review from'
     A            DDRFRA        24A  B  4 55TEXT('REVIEW FROM KEY')
     A**End of change CGL029
     A  36                                  DSPATR(PR)                                        222549
     A                                      DSPATR(HI)
     A N52                                  DSPATR(UL)
     A  52                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
      *
