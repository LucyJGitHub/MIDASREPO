      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Collateral allocations maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GLCOLADDF - Collateral Allocations Maintenance (Subfile)*
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CGL011  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL011 - Collateral Processing for Midas                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Cancel')
     A                                      CF15(15 'Collateral Maintenance')
     A                                      CF16(16 'Collateral Narrative')
      *
      ** Header Format
      *
     A
     A          R GLCOLAH0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            SUSER         10A  O  1  2
     A                                  1 13'GLCOLAD'
     A                                  2 26'Collateral Allocations +
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'DATE'
     A            SMRDT          7A  O  1 73TEXT('RUN DATE')
     A            SJOB          10A  O  2  2
     A                                  2 66'TIME'
     A                                  2 73TIME
      *
      ** Subfile Format
      *
     A          R GLCOLAS0                  SFL
     A  42                                  SFLNXTCHG
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            SACTN          1A  B  7 13DSPATR(HI)
     A                                      TEXT('Action')
     A  30                                  DSPATR(PR)
     A  50                                  DSPATR(RI PC)
     A N50N30                               DSPATR(UL)
     A            SBRCA          3A  B  7 23DSPATR(HI)
     A                                      TEXT('Branch')
     A  31                                  DSPATR(PR)
     A  51                                  DSPATR(RI PC)
     A N51N31                               DSPATR(UL)
     A            SCUSN         10A  B  7 41DSPATR(HI)
     A                                      TEXT('Customer')
     A  32                                  DSPATR(PR)
     A  52                                  DSPATR(RI PC)
     A N52N32                               DSPATR(UL)
     A            SMODI          2A  B  7 62DSPATR(HI)
     A                                      TEXT('Module')
     A  33                                  DSPATR(PR)
     A  53                                  DSPATR(RI PC)
     A N53N33                               DSPATR(UL)
     A            SPRSQ          3A  B  7 77DSPATR(HI)
     A                                      TEXT('Priority')
     A  34                                  DSPATR(PR)
     A  54                                  DSPATR(RI PC)
     A N54N34                               DSPATR(UL)
     A            SFCTY1         6A  B  8 13DSPATR(HI)
     A                                      TEXT('Facility')
     A  35                                  DSPATR(PR)
     A  55                                  DSPATR(RI PC)
     A N55N35                               DSPATR(UL)
     A            SFCTY2         3A  B  8 20DSPATR(HI)
     A                                      TEXT('Facility')
     A  35                                  DSPATR(PR)
     A  55                                  DSPATR(RI PC)
     A N55N35                               DSPATR(UL)
     A            SFCTY3         2A  B  8 24DSPATR(HI)
     A                                      TEXT('Facility')
     A  35                                  DSPATR(PR)
     A  55                                  DSPATR(RI PC)
     A N55N35                               DSPATR(UL)
     A            SLNLM          6A  B  8 41DSPATR(HI)
     A                                      TEXT('Loan/Limit')
     A  36                                  DSPATR(PR)
     A  56                                  DSPATR(RI PC)
     A N56N36                               DSPATR(UL)
     A**Start of change CGL029
     A**********  SREAC         18A  B  8 58DSPATR(HI)
     A            SREAC         24A  B  8 57DSPATR(HI)
     A**End of change CGL029
     A                                      TEXT('A/c')
     A  37                                  DSPATR(PR)
     A  57                                  DSPATR(RI PC)
     A N57N37                               DSPATR(UL)
     A            SAMPC          1A  B  9 25DSPATR(HI)
     A                                      TEXT('Amount/Percentage')
     A  38                                  DSPATR(PR)
     A  58                                  DSPATR(RI PC)
     A N58N38                               DSPATR(UL)
     A            SAMNT         16A  B  9 37DSPATR(HI)
     A                                      TEXT('Amount')
     A  39                                  DSPATR(PR)
     A  59                                  DSPATR(RI PC)
     A N59N39                               DSPATR(UL)
     A            SPRCT          6A  B  9 72DSPATR(HI)
     A                                      TEXT('Percentage')
     A  40                                  DSPATR(PR)
     A  60                                  DSPATR(RI PC)
     A N60N40                               DSPATR(UL)
     A            SBLANK         1A  H
     A            SRC            3  0H
     A                                  6  2'----------------------------------+
     A                                      -----------------------------------+
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  2'Action...:'
     A                                  7 15'Branch:'
     A                                  7 29'Customer..:'
     A**Start of change CGL029
     A**********                        7 53'Module.:'                                        CGL029
     A                                  7 52'Module.:'
     A**End of change CGL029
     A                                  7 66'Priority.:'
     A                                  8  2'Facility.:'
     A                                  8 29'Loan/Limit:'
     A**Start of change CGL029
     A**********                        8 53'A/c:'                                            CGL029
     A                                  8 52'A/c:'
     A**End of change CGL029
     A                                  9  2'Amount/Percentage....:'
     A                                  9 29'Amount:'
     A                                  9 60'Percentage:'
      *
      ** Subfile Control
      *
     A          R GLCOLAC0                  TEXT('Subfile control')
     A                                      SFLCTL(GLCOLAS0)
     A                                      SFLPAG(4) SFLSIZ(5)
     A                                      OVERLAY
     A  41                                  SFLCLR
     A N41                                  SFLDSPCTL
     A N41 45                               SFLDSP
     A  43                                  SFLEND(*MORE)
      *
      ** Setoff error indicators for subfile
      *
     A                                      SETOF(50 'Invalid: SACTN')
     A                                      SETOF(51 'Invalid: SBRCA')
     A                                      SETOF(52 'Invalid: SCUSN')
     A                                      SETOF(53 'Invalid: SMODI')
     A                                      SETOF(54 'Invalid: SPRSQ')
     A                                      SETOF(55 'Invalid: SFCTY')
     A                                      SETOF(56 'Invalid: SLNLM')
     A                                      SETOF(57 'Invalid: SREAC')
     A                                      SETOF(58 'Invalid: SAMPC')
     A                                      SETOF(59 'Invalid: SAMNT')
     A                                      SETOF(60 'Invalid: SPRCT')
      *
      ** Command Keys
      *
     A N43                                  ROLLUP(44 'Next page.')
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
     A            SRLRN          4  0H      SFLRCDNBR
     A            SCOLREF       10A  O  3 27
     A                                  3  2'Collateral Reference...:'
     A            SOLENVL       16A  O  3 60
     A                                  3 41'Old Lending Value:'
     A            SLENVAL       16A  O  4 60
     A                                  4 41'New Lending Value:'
     A            SPERALO        6A  O  5 27
     A                                  5  2'Percentage Allocated...:'
     A            STOTALO       16A  O  5 60
     A                                  5 41'Total Allocated..:'
      *
      ** Format trailer
      *
     A          R GLCOLAF0
     A                                      OVERLAY
     A                                 24  2'F3 = Exit  F12 = Cancel'
     A                                 24 27'F15=Coll. Maint.  F16=Coll. Narr.'
     A                                 24 63'Rollup Rolldown'
      *
      ** Error message subfile record format
      *
     A          R GLCOLAS1                  SFL
     A                                      SFLMSGRCD(23)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Subfile Control for error messages
      *
     A          R GLCOLAC1                  SFLCTL(GLCOLAS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      OVERLAY
     A  50
     AON50                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
