      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Collateral Details Browser Display')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GLCOLLBDF - Collateral Details Browser Display          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CGL011  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL011 - Collateral Processing for Midas (Phase 1)           *
      *                                                               *
      *****************************************************************
      *
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(23)
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      VLDCMDKEY(40 'ANY VALID KEY')
     A                                      INDARA
     A                                      CF03
     A                                      CF09
     A  11                                  CF11
     A                                      CF12
      *****************************************************************
      *
      ** Browse Subfile
     A          R GLCOLLS1                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            DDACTN         1A  B  9  3TEXT('Action')
     A                                      DSPATR(UL)
     A            DDRECS         1A  O  9  5TEXT('Record Status')
     A  70                                  DSPATR(ND)
     A            DDCREF        10A  O  9  7TEXT('Collateral Ref')
     A                                      DSPATR(HI)
     A            DDENTY         1A  O  9 19TEXT('Entity Level')
     A            DDCMPY         3A  O  9 22TEXT('Company')
     A            DDBRCA         3A  O  9 27TEXT('Branch')
     A            DDCOLT         5A  O  9 31TEXT('Collateral Type')
     A            DDCUSN         6A  O  9 37TEXT('Collateral Customer')
     A            DDSTAT         3A  O  9 44TEXT('Status')
     A            DDCCY          3A  O  9 50TEXT('Currency')
     A            DDLAMT        16A  O  9 54TEXT('Lending Value Amount')
     A            DDGNSP         1A  O  9 71TEXT('General/Specific Ind')
     A            DDAUTH         4A  O  9 74TEXT('Authorise')
     A N70                                  DSPATR(ND)
     A            DDIUSR        10A  H      TEXT('Inserting User')
     A            DDAUSR        10A  H      TEXT('Amending User')
      *****************************************************************
      *
      ** Browse Subfile Control
     A          R GLCOLLC1                  SFLCTL(GLCOLLS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Select screen control')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
      *
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A                                  1 13'GLCOLLB'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWSID    R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A                                  1 26'Collateral Details Maintenance'
     A                                      DSPATR(HI)
     A                                  2 37'Browse'
     A                                      DSPATR(HI)
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  4  3'Review from...:'
     A            DDREVF        10A  B  4 19TEXT('Review Reference')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A                                  4 32'And/Or'
      *
     A                                  5  3'Select - Level:'
     A            DDSENT         1A  B  5 19TEXT('Review Level')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A                                  5 21'Company.:'
     A            DDSCMP         3A  B  5 31TEXT('Review Company')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A                                  5 35'Branch:'
     A            DDSBRC         3A  B  5 43TEXT('Review Branch')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A                                  5 48'Type:'
     A            DDSCOL         5A  B  5 54TEXT('Review Type')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A                                  5 60'Customer:'
     A            DDSCNU        10A  B  5 70TEXT('Review Customer')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
      *
     A  70                              6  3'    Req Auth..:'
     A            DDREQR         1A  B  6 19TEXT('Review Auth')
     A                                      DSPATR(HI UL)
     A N70                                  DSPATR(ND PR)
     A                                      OVRDTA
     A                                  6 21'Currency:'
     A            DDSCCY         3A  B  6 31TEXT('Review Ccy')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A                                  6 35'Value.:'
     A            DDSVAL        16A  B  6 43TEXT('Review Value')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A                                  6 60'G/S.....:'
     A            DDSGNS         1A  B  6 70TEXT('Review G/S')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
      *
     A                                  8  2'Actn'
     A                                  8  7'Collateral'
     A                                  8 18'Lvl'
     A                                  8 22'Cmpy'
     A                                  8 27'Brch'
     A                                  8 32'Type'
     A                                  8 37'Cust.'
     A                                  8 43'Status'
     A                                  8 50'Ccy'
     A                                  8 55'Lending Value'
     A                                  8 70'G/S'
     A  70                              8 74'Auth'
      *****************************************************************
      *
      ** Browse Subfile
     A          R GLCOLLS2                  SFL
     A            DDACTN         1A  B  9  3TEXT('Action')
     A                                      DSPATR(UL)
     A            DDFRNT        20A  O  9  6TEXT('Front Office Id')
     A            DDCREF        10A  O  9 27TEXT('Collateral Ref')
     A            DDENTY         1A  O  9 39TEXT('Entity Level')
     A            DDCMPY         3A  O  9 42TEXT('Company')
     A            DDBRCA         3A  O  9 47TEXT('Branch')
     A            DDCOLT         5A  O  9 52TEXT('Collateral Type')
     A            DDCCY          3A  O  9 58TEXT('Currency')
     A            DDLAMT        16A  O  9 62TEXT('Lending Value Amount')
     A            DDIUSR        10A  H      TEXT('Inserting User')
     A            DDAUSR        10A  H      TEXT('Amending User')
     A            DDCUSN         6A  H      TEXT('Collateral Customer')
     A            DDSTAT         3A  H      TEXT('Status')
     A            DDGNSP         1A  H      TEXT('General/Specific Ind')
     A            DDAUTH         4A  H      TEXT('Authorise')
      *****************************************************************
      *
      ** Browse Subfile Control
     A          R GLCOLLC2                  SFLCTL(GLCOLLS2)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Select screen control')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A                                  1 13'GLCOLLB'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWSID    R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A                                  1 26'Collateral Details Maintenance'
     A                                      DSPATR(HI)
     A                                  2 37'Browse'
     A                                      DSPATR(HI)
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  4  3'Review from...:'
     A            DDREVF        10A  B  4 19TEXT('Review Reference')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A                                  4 32'And/Or'
      *
     A                                  5  3'Select - Level:'
     A            DDSENT         1A  B  5 19TEXT('Review Level')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A                                  5 21'Company.:'
     A            DDSCMP         3A  B  5 31TEXT('Review Company')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A                                  5 35'Branch:'
     A            DDSBRC         3A  B  5 43TEXT('Review Branch')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A                                  5 48'Type:'
     A            DDSCOL         5A  B  5 54TEXT('Review Type')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
      *
     A  70                              6  3'    Req Auth..:'
     A            DDREQR         1A  B  6 19TEXT('Review Auth')
     A                                      DSPATR(HI UL)
     A N70                                  DSPATR(ND PR)
     A                                      OVRDTA
     A                                  6 21'Currency:'
     A            DDSCCY         3A  B  6 31TEXT('Review Ccy')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A                                  6 35'Value.:'
     A            DDSVAL        16A  B  6 43TEXT('Review Value')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
      *
     A                                  8  2'Actn'
     A                                  8  7'Front Office Id.'
     A                                  8 27'Collateral'
     A                                  8 38'Lvl'
     A                                  8 42'Cmpy'
     A                                  8 47'Brch'
     A                                  8 52'Type'
     A                                  8 58'Ccy'
     A                                  8 62'Lending Value'
      *****************************************************************
      *
      ** Browse Screen Footer
     A          R GLCOLLF1                  BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 24  2'F3 = Exit  F9 = Insert  +
     A                                       F12 = Initial Screen  Roll-up  +
     A                                       Roll-down'
     A*                                24  2'F3 = Exit,  F9 = Insert,'
     A*                                24 28'F12 = Initial Screen,  Roll-up,  +
     A*                                      Roll-down'
