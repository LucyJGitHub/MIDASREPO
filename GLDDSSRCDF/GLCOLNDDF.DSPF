      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GL Collateral Narrative Details Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GLCOLNDDF - Collateral Narrative Details Maintenance    *
      *                   (Subfile)                                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CGL011  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL011 - Collateral Processing for Midas                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Cancel')
     A                                      CF15(15 'Collateral Allocation')
     A                                      CF16(16 'Collateral Maintenance')
      *
      ** Header Format
      *
     A
     A          R GLCOLNH0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            SUSER         10A  O  1  2
     A                                  1 13'GLCOLND'
     A                                  2 23'Collateral Narrative De+
     A                                      tails Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'DATE'
     A            SMRDT          7A  O  1 73TEXT('RUN DATE')
     A            SJOB          10A  O  2  2
     A                                  2 66'TIME'
     A                                  2 73TIME
      *
      ** Subfile Format
      *
     A          R GLCOLNS0                  SFL
     A  30                                  SFLNXTCHG
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            SNARR         79A  B  6  2DSPATR(HI)
     A                                      TEXT('Narrative')
     A                                      CHECK(LC)
     A  31                                  DSPATR(PR)
     A N31                                  DSPATR(UL)
      *
      ** Subfile Control
      *
     A          R GLCOLNC0                  TEXT('Subfile control')
     A                                      SFLCTL(GLCOLNS0)
     A                                      SFLPAG(16) SFLSIZ(17)
     A                                      OVERLAY
     A  41                                  SFLCLR
     A N41                                  SFLDSPCTL
     A N41 44                               SFLDSP
     A  42                                  SFLEND(*MORE)
      *
      ** Command Keys
      *
     A N42                                  ROLLUP(43 'Next page.')
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
     A            SRLRN          3S 0H      SFLRCDNBR
     A            SACTN          1A  O  4 16
     A                                  4  2'Action......:'
     A            SCOLREF       10A  O  4 56
     A                                  4 30'Collateral Reference...:'
      *
      ** Format trailer
      *
     A          R GLCOLNF0
     A                                      OVERLAY
     A                                 24  2'F3 = Exit  F12 = Cancel'
     A                                 24 27'F15=Coll. Alloc.  F16=Coll. Maint.'
     A                                 24 64'Rollup Rolldown'
      *
      ** Error message subfile record format
      *
     A          R GLCOLNS1                  SFL
     A                                      SFLMSGRCD(23)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Subfile Control for error messages
      *
     A          R GLCOLNC1                  SFLCTL(GLCOLNS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      OVERLAY
     A  50
     AON50                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
