      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
      *****************************************************************
      *                                                               *
      *  Midas  - GL                                                  *
      *                                                               *
      *  PF/ACCNTBX0 - Midas GL Accounts Master Extension File        *
      *                                                               *
      ***(C)*MKI*LIMITED*1988,*1998                                   *                       LUC109
      *  (c) Misys International Banking Systems Ltd. 2016            *                       LUC109
      *                                                               *
      *  Last Amend No. LUC109             DATE 23Aug16               *
      *  Prev Amend No. LUC013             DATE 21MAY08               *
      *                 167412             DATE 25OCT99               *
      *                 MMI043     *Create DATE 27Nov98               *
      *                 Xxxxxx             DATE DdMmmYy               *
      *****************************************************************
      *                                                               *
      *  LUC109 - HO Reporting (Upgrade to FB Midas 2.1)              *
      *  LUC013 - Define MACOD as 10 long.                            *
      *  167412 - Calculate monthly averages differently              *
      *  MMI043 - Interface to Bank Of Italy Reporting                *
      *                                                               *
      *                                                               *
      *****************************************************************
     A          R ACCNBFX0                  TEXT('(C) Copyright MKI +
     A                                      International Ltd 1988, 1998')
      *
     A***         MCNUM          6S 0       COLHDG('Customer Number')                         LUC109
     A            MCNUM          6A         COLHDG('Customer Number')                         LUC109
      *
     A            MCCY           3A         COLHDG('Currency Code')
      *
     A***         MACOD          4S 0       COLHDG('Account Code')                        LUC013
     A            MACOD         10S 0       COLHDG('Account Code')                        LUC013
      *
     A            MACSQ          2S 0       COLHDG('Acct Seq Code')
      *
     A            MBRCA          3A         COLHDG('Branch Code')
      *
     A            MDAVX         15  7       COLHDG('DR Rate MTD')
      *
     A            MDAVR         15  7       COLHDG('DR Rate' +
     A                                             'Mthly Average')
      *
     A            MCAVX         15  7       COLHDG('CR Rate MTD')
      *
     A            MCAVR         15  7       COLHDG('CR Rate' +
     A                                             'Mthly Average')
      *
     A            MCPPV         15  0       COLHDG('Int on CR Bals at' +
     A                                             'end of prev mth')
      *
     A            MCPDM         15  0       COLHDG('Int on CR Bals'    +
     A                                             'paid this mth')
      *
     A            MDPPV         15  0       COLHDG('Int on DR Bals at' +
     A                                             'end of prev mth')
      *
     A            MDPDM         15  0       COLHDG('Int on DR Bals'    +
     A                                             'paid this mth')
      *
     A            MDCPV         15  0       COLHDG('Commission DR at'  +
     A                                             'end of prev mth')
      *
     A            MDCMM         15  0       COLHDG('Commission DR'     +
     A                                             'this mth')
      *
     A            MDGPV         15  0       COLHDG('Charges DR at'     +
     A                                             'end of prev mth')
      *
     A            MDCGM         15  0       COLHDG('Charges DR'        +
     A                                             'this mth')
      *
     A            MDALX         15  0       COLHDG('DR Acct Bal MTD')
      *
     A            MDALB         15  0       COLHDG('DR Acct Bal' +
     A                                             'Mthly Average')
      *
     A            MCALX         15  0       COLHDG('CR Acct Bal MTD')
      *
     A            MCALB         15  0       COLHDG('CR Acct Bal' +
     A                                             'Mthly Average')
      *
     A            MDACX         15  0       COLHDG('DR Avail Bal MTD')
      *
     A            MDACB         15  0       COLHDG('DR Avail Bal' +
     A                                             'Mthly Average')
      *
     A            MCACX         15  0       COLHDG('CR Avail Bal MTD')
      *
     A            MCACB         15  0       COLHDG('CR Avail Bal' +
     A                                             'Mthly Average')
      *
     A            MQXAB         15  0       COLHDG('Quarterly Max' +
     A                                             'DR Acct Bal')
      *
     A            MQMAV         15  0       COLHDG('Quarterly Min' +
     A                                             'DR Avail Bal')
      *
     A            MQXAV         15  0       COLHDG('Quarterly Max' +
     A                                             'DR Avail Bal')
      *
     A            MPQMA         15  0       COLHDG('Prev Qtrly Min' +
     A                                             'DR Avail Bal')
      *
     A            MPQXA         15  0       COLHDG('Prev Qtrly Max' +
     A                                             'DR Avail Bal')
      *
     A            MQDIX         15  0       COLHDG('DR Int Movements' +
     A                                             'at end of last' +
     A                                             'quarter')
      *
     A            MQDIM         15  0       COLHDG('DR Int Movements' +
     A                                             'this quarter')
      *
     A            MQCIX         15  0       COLHDG('CR Int Movements' +
     A                                             'at end of last' +
     A                                             'quarter')
      *
     A            MQCIM         15  0       COLHDG('CR Int Movements' +
     A                                             'this quarter')
      *
     A            MNOCX         15  0       COLHDG('Number of chqs' +
     A                                             'DR at end of' +
     A                                             'last quarter')
      *
     A            MNOCQ         15  0       COLHDG('Number of chqs' +
     A                                             'DR this quarter')
      *
     A            MAMCX         15  0       COLHDG('Amount of chqs' +
     A                                             'DR at end of' +
     A                                             'last quarter')
      *
     A            MAMCQ         15  0       COLHDG('Amount of chqs' +
     A                                             'DR this quarter')
      *
     A            MUNPD         15  0       COLHDG('Amount of unpaid' +
     A                                             'Chqs & Drafts')
      *
     A            MRVNO         15  0       COLHDG('Number of' +
     A                                             'Reverses')
      *
     A            MDRPX         15  0       COLHDG('Amt of DR Postgs' +
     A                                             'at end of last' +
     A                                             'quarter' )
      *
     A            MDRPO         15  0       COLHDG('Amt of DR Postgs' +
     A                                             'this quarter')
      *
     A            MCRPX         15  0       COLHDG('Amt of CR Postgs' +
     A                                             'at end of last' +
     A                                             'quarter' )
      *
     A            MCRPO         15  0       COLHDG('Amt of CR Postgs' +
     A                                             'this quarter')
      *
     A            MNDPX         15  0       COLHDG('No of DR Postgs' +
     A                                             'at end of last' +
     A                                             'quarter' )
      *
     A            MNDPO         15  0       COLHDG('No of DR Postngs' +
     A                                             'this quarter')
      *
     A            MNCPX         15  0       COLHDG('No of CR Postgs' +
     A                                             'at end of last' +
     A                                             'quarter' )
      *
     A            MNCPO         15  0       COLHDG('No of CR Postngs' +
     A                                             'this quarter')
      *
     A            MMTDY          3  0       COLHDG('MTD Working Days')
      *
      *
      ** Start of fix 167412
      *
     A            MCDIRX        15  0       COLHDG('Previous Interest' +
     A                                             'debit ')
      *
     A            MCCIRX        15  7       COLHDG('Previous Interest' +
     A                                             'credit ')
      *
     A            MLDBLX        15  0       COLHDG('Previous Ledger' +
     A                                           'balance ')
      *
     A            MCLBLX        15  7       COLHDG('Previous Cleared' +
     A                                           'balance  ')
      *
      ** End of fix 167412
      *
