      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas GL Customer Fee Charges')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PF/GLCHRGPD - Customer Fee Charges                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CSD011             Date 04Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD011 - Performance Incentive Fees.                         *
      *  CSD008 - Customer Service Fees Enhancements                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R GLCHRGD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            CHRECI    R               REFFLD(RECI)
      *
     A            CHREFE         8S 0       TEXT('Charge Reference')
     A                                      COLHDG('Charge' 'Ref')
      *
     A            CHFTYP         2A         TEXT('Fee Type')
     A                                      COLHDG('Fee' 'Type')
      *
     A            CHCNUM         6A         TEXT('Customer number')
     A                                      COLHDG('Cust''Number')
      *
     A            CHPORT         4A         TEXT('Portfolio')
     A                                      COLHDG('Portf')
      *
     A            CHGROU         2A         TEXT('Charge Group')
     A                                      COLHDG('Chgr' 'Grp')
      *
     A            CHCCY          3A         TEXT('Charge Currency')
     A                                      COLHDG('Chgr' 'Ccy')
      *
     A            CHAMT         15P         TEXT('Charge Amount')
     A                                      COLHDG('Charge' 'Amount')
      *
     A            CHBRCA         3A         TEXT('Charge Branch')
     A                                      COLHDG('Chgr' 'Bch')
      *
     A**Start of change CGL029
     A**********  CHSEAC        18A         TEXT('Settlement Account')                        CGL029
     A**********                            COLHDG('Settl' 'Account')                         CGL029
      *
     A            QQSEAC        18A
     A**End of change CGL029
     A            CHSLSD         5P         TEXT('Fee start/Last sett Date')
     A                                      COLHDG('Fee St' 'L sett Dte')
      *
     A            CHAPOS         1A         TEXT('Automatic posting')
     A                                      COLHDG('Auto' 'Pos')
      *
     A            CHPFTC         4A         TEXT('Profit Centre')
     A                                      COLHDG('Profit' 'Centre')
      *
     A            CHSEDA         2S 0       TEXT('Settl Gene Days')
     A                                      COLHDG('Sett' 'Gen')
      *
     A            CHFREQ         1A         TEXT('Settlement Frequency')
     A                                      COLHDG('Sett' 'Freq')
      *
     A            CHDAMO         2S 0       TEXT('Settl Day in Month')
     A                                      COLHDG('Sett' 'Day in Mht')
      *
     A            CHFNSD         5P         TEXT('First/Next settl date')
     A                                      COLHDG('F/N' 'Settl' 'Date')
      *
     A            CHFIND         5P         TEXT('Final settlement date')
     A                                      COLHDG('Final' 'Settl' 'Date')
      *
     A            CHACCR         1A         TEXT('Accrual Indicator')
     A                                      COLHDG('Accr' 'Ind')
      *
     A            CHNAR1        35A         TEXT('Narrative 1')
     A                                      COLHDG('Narrative 1')
      *
     A            CHNAR2        35A         TEXT('Narrative 2')
     A                                      COLHDG('Narrative 2')
      *
     A            CHNAR3        35A         TEXT('Narrative 3')
     A                                      COLHDG('Narrative 3')
      *
     A            CHNAR4        35A         TEXT('Narrative 4')
     A                                      COLHDG('Narrative 4')
      *
     A            CHSTAT         1A         TEXT('Status')
     A                                      COLHDG('St')
      *
     A            CHJOBN    R               REFFLD(JOJOB)
     A                                      TEXT('Locked by Job Name')
     A                                      COLHDG('Locked' 'Job' 'Name')
      *
     A            CHUSER    R               REFFLD(USER)
     A                                      TEXT('Locked by User Identifier')
     A                                      COLHDG('Locked' 'User' 'Ident.')
      *
     A            CHJNBR    R               REFFLD(JONBR)
      *
     A            CHCHTP    R               REFFLD(LCTP)
      *
     A            CHLCD     R               REFFLD(LCD)
      *
     A*                                                        /*Start CSD011*/
     A            CHHWMF         1A         TEXT('High Water Mark Fee')
     A                                      COLHDG('High Water' +
     A                                      'Mark Fee')
      *
     A            CHCFRE         1A         TEXT('Calculation Frequency')
     A                                      COLHDG('Calculation' +
     A                                      'Frequency')
      *
     A            CHCDMO         2S 0       TEXT('Calculation Day in Month')
     A                                      COLHDG('Calculation' +
     A                                      'Day in Month')
      *
     A            CHNCDA         5P 0       TEXT('Next Calculation Date')
     A                                      COLHDG('     Next' +
     A                                      'Calculation Date')
     A*                                                        /*End   CSD011*/
     A**Start of change CGL029
     A            CHSEAC        24A         TEXT('Settlement Account')
     A**End of change CGL029
