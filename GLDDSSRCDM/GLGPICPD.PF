      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas GL Generic Posting Interface Control Data')      *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PF/GLPICDPD - Generic Posting Interface Control Data         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. CGL160             Date 10Oct14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CGL047  *Create    Date 30Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL160 - Cheque Details Upload via Generic Posting Interface *
      *  CGL047 - Midas Generic Posting Interface                     *
      *                                                               *
      *****************************************************************
     A          R GLGPICD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A*---------------------------------------------------------------------
     A            GPSARN         6          TEXT('Unique Interface Ref.')
     A                                      COLHDG('Unique' 'Interface' +
     A                                      'Ref.')
     A            GPMIDF         1          TEXT('Midas Feature Y/N')
     A                                      COLHDG('Midas' 'Feature' +
     A                                      'Y/N')
     A                                      VALUES('Y' 'N')
     A            GPDESC        30          TEXT('Interface Description')
     A                                      COLHDG('Interface' 'Description')
     A            GPIFIL        10          TEXT('Interface File Name')
     A                                      COLHDG('Interface' 'File' +
     A                                      'Name')
     A            GPILIB        10          TEXT('Interface File Library')
     A                                      COLHDG('Interface' 'File' +
     A                                      'Library')
     A            GPTFIL        10          TEXT('Interface Control File Name')
     A                                      COLHDG('Interface' 'Control' +
     A                                      'File Name')
     A            GPTLIB        10          TEXT('Interface Control File +
     A                                       Library')
     A                                      COLHDG('Interface' 'Control' +
     A                                      'File Library')
     A            GPIDTQ        10          TEXT('Incoming Data Queue Name')
     A                                      COLHDG('Incoming' 'Data Queue' +
     A                                      'Name')
     A            GPODTQ        10          TEXT('Outgoing Data Queue Name')
     A                                      COLHDG('Outgoing' 'Data Queue' +
     A                                      'Name')
     A            GPWAIT         5P 0       TEXT('Wait Time in Seconds')
     A                                      COLHDG('Wait Time' 'in' +
     A                                      'Seconds')
     A            GPFAIL         1A         TEXT('Fail COB Flag Y/N')
     A                                      COLHDG('Fail' 'COB Flag' +
     A                                      'Y/N')
     A                                      VALUES('Y' 'N')
     A            GPCLRF         1A         TEXT('Clear File Flag Y/N')
     A                                      COLHDG('Clear' 'File'  +
     A                                      'Flag Y/N')
     A                                      VALUES('Y' 'N')
     A            GPSPOS         7A         TEXT('Source of Posting')
     A                                      COLHDG('Source' 'of' +
     A                                      'Posting')
     A            GPGETP         1A         TEXT('Generated Entry Type')
     A                                      COLHDG('Generated'  +
     A                                      'Entry' 'Type')
     A            GPIDAT         5P 0       TEXT('Last Interface Run Date')
     A                                      COLHDG('Last' 'Interface' +
     A                                      'Run Date')
     A            GPSUSP         1A         TEXT('Post Invalid Retail A/c +
     A                                      to Suspense Y/N')
     A                                      COLHDG('Post Invalid' 'Retail A/c' +
     A                                      'to Suspense Y/N')
     A            GPINVL         1A         TEXT('Fail COB if Invalid Items')
     A                                      COLHDG('Fail COB' 'if'  +
     A                                      'Invalid Items')
     A            GPPVDT         1A         TEXT('Post on Value Date')
     A                                      COLHDG('Post' 'on'  +
     A                                      'Value Date')
     A            GPRPTI         1A         TEXT('Produce Report of Incoming +
     A                                      Items')
     A                                      COLHDG('Produce Report' 'of' +
     A                                      'Incoming Items')
      ** Start of CGL160
     A            GPRIAC        10A         TEXT('Rejected Items Account Code')
     A                                      COLHDG('Rejected Items' +
     A                                      'Account Code')
     A            GPUITT         5A         TEXT('Unpaid Items Trans Type')
     A                                      COLHDG('Unpaid Items' +
     A                                      'Transaction Type')
     A            GPUIAC        10A         TEXT('Unpaid Items Account Code')
     A                                      COLHDG('Unpaid Items' +
     A                                      'Account Code')
     A            GPBLAC         1A         TEXT('Blocked Account')
     A                                      COLHDG('Blocked' +
     A                                      'Account')
     A            GPINAC         1A         TEXT('Inactive Account')
     A                                      COLHDG('Inactive' +
     A                                      'Account')
     A            GPRFAC         1A         TEXT('Refer DR/CR')
     A                                      COLHDG('Refer' +
     A                                      'DR/CR')
     A            GPBKAC         1A         TEXT('Bankrupt')
     A                                      COLHDG('Bankrupt')
     A            GPNSF          1A         TEXT('Insufficient Funds')
     A                                      COLHDG('Insufficient' +
     A                                      'Funds')
     A            GPCQST         1A         TEXT('Stopped Cheque')
     A                                      COLHDG('Stopped' +
     A                                      'Cheque')
     A            GPCQRG         1A         TEXT('Cheque out of Range')
     A                                      COLHDG('Cheque out' +
     A                                      'of Range')
     A            GPCQPR         1A         TEXT('Cheque Presented')
     A                                      COLHDG('Cheque' +
     A                                      'Presented')
     A            GPCQNX         1A         TEXT('Cheque Not Expected')
     A                                      COLHDG('Cheque Not' +
     A                                      'Expected')
     A            GPLCDT         5  0       TEXT('Last Change Date')
     A                                      COLHDG('Last Change' +
     A                                      'Date')
     A            GPLCTP         1          TEXT('Last Change Type')
     A                                      COLHDG('Last Change' +
     A                                      'Type')
      ** End of CGL160
     A*=====================================================================
