      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  REUSEDLT(*YES)                                               *                     MD021155
/*EXI *  TEXT('Midas GL Generic Posting Interface Forward Valued')    *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PF/GLGPIFPD - Forward Valued Interface Postings              *
      *                                                               *
      *  (c) Finastra International Limited 2006                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD021155           Date 08Jul13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CGL047 *C *CREATE  Date 30Jun06               *
      *                 Xnnnnn             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD021155 - COB Restructure Phase 1 remnants                  *
      *           - Reuse deleted records not done                    *
      *  CGL047 - Midas Generic Posting Interface                     *
      *                                                               *
      *****************************************************************
     A          R GLGPIFD0                  TEXT('(c) Copyright Finastra +
     A                                      International Limited 2006')
     A*---------------------------------------------------------------------
     A            BTBTNB         3          TEXT('Batch number')
     A                                      COLHDG('Batch' +
     A                                             'number')
     A            BTBINB         3S 0       TEXT('Batch item no.')
     A                                      COLHDG('Batch' +
     A                                             'item no.')
     A            BTIBCA         3          TEXT('Input Branch Code - Alpha')
     A                                      COLHDG('Input Branch' +
     A                                             'Code - Alpha')
     A            BTCUST         6          TEXT('Customer number')
     A                                      COLHDG('Customer' +
     A                                             'number')
     A            BTCYCD         3          TEXT('Currency code')
     A                                      COLHDG('Curr' +
     A                                             'Code')
     A            BTACCD        10          TEXT('Account code')
     A                                      COLHDG('Account' +
     A                                             'code')
     A            BTACSN         2          TEXT('Account sequence number')
     A                                      COLHDG('A/c' +
     A                                             'Seq' +
     A                                             'No.')
     A            BTNNBI         2          TEXT('Nostro number (Item)')
     A                                      COLHDG('Nostro' +
     A                                             'number (Item)')
     A            BTRACN        10S 0       TEXT('Retail account number')
     A                                      COLHDG('Retail' +
     A                                             'Account' +
     A                                             'Number')
     A            BTRINO         1          TEXT('Retail ind.: Override')
     A                                      COLHDG('Retail ind.:' +
     A                                             'Override')
     A                                      DFT('N')
     A            BTRINE         1          TEXT('Retail ind.: Error')
     A                                      COLHDG('Retail' +
     A                                             'ind.: Error')
     A                                      DFT('N')
     A            BTTRTY         5          TEXT('Transaction type')
     A                                      COLHDG('TRANS.' +
     A                                             'TYPE')
     A            BTPTAM        13S 0       TEXT('Posting amount')
     A                                      COLHDG('Posting' +
     A                                             'Amount')
     A            BTDCIN         1          TEXT('Debit/credit indicator')
     A                                      COLHDG('DR/' +
     A                                             'CR' +
     A                                             'IND')
     A            BTPTDT         5S 0       TEXT('Posting date')
     A                                      COLHDG('Posting' +
     A                                             'Date')
     A            BTVLDT         5S 0       TEXT('Value date')
     A                                      COLHDG('Value' +
     A                                             'Date')
     A            BTNVCD         2          TEXT('Narrative code')
     A                                      COLHDG('NARC')
     A            BTNRDC        30          TEXT('Narrative description')
     A                                      COLHDG('NARRATIVE' +
     A                                             'DESCRIPTION')
     A            BTACTY         1          TEXT('Account type')
     A                                      COLHDG('Acc' +
     A                                             'Typ')
     A            BTACNB         6          TEXT('Assoc. Customer number')
     A                                      COLHDG('Assoc.' +
     A                                             'Customer number')
     A            BTCQNB         8          TEXT('Cheque number')
     A                                      COLHDG('Cheque' +
     A                                             'Number')
     A            BTCOIN         1          TEXT('Customer originated ind.')
     A                                      COLHDG('C/O' +
     A                                             'IND')
     A            BTSWCR        16          TEXT('Swift common reference')
     A                                      COLHDG('Swift common' +
     A                                             'reference')
     A            BTPRCN         4          TEXT('Profit centre')
     A                                      COLHDG('Profit' +
     A                                             'Centre')
     A            BTBCBH         3          TEXT('Branch Code (Batch Headr)')
     A                                      COLHDG('Branch Code' +
     A                                             '(Batch Headr)')
     A            BTRADP         1          TEXT('Retail advice printed')
     A                                      COLHDG('Retail advice' +
     A                                             'printed')
     A            BTSHPI         1          TEXT('Shadow post indicator')
     A                                      COLHDG('Shadow post' +
     A                                             'indicator')
     A            BTRRNM         5S 0       TEXT('Relative rec no of memos rcd')
     A                                      COLHDG('Rel.rec' +
     A                                             'No.of' +
     A                                             'Mem.rcd')
     A            BTRNBE         1          TEXT('Retail Number Entered')
     A                                      COLHDG('Retail Number' +
     A                                             'Entered')
     A                                      DFT('N')
     A            BTRARC        10          TEXT('Retail Ac Referral Cond.')
     A                                      COLHDG('Retail Ac' +
     A                                             'Referral Cond.')
     A            BTTSTY        10          TEXT('Transaction (Sub - Type)')
     A                                      COLHDG('Transaction' +
     A                                             '(Sub - Type)')
     A            BTBKCD         2          TEXT('Book Code')
     A                                      COLHDG('Book' +
     A                                             'Code')
     A            BTSPFC         8S 0       TEXT('Spread Factor')
     A                                      COLHDG('Spread' +
     A                                             'Factor')
     A            BTRVDI         1          TEXT('Reverse/Void Indicator')
     A                                      COLHDG('Reverse/Void' +
     A                                             'Indicator')
     A            BTNITM         5S 0       TEXT('Number of Items')
     A                                      COLHDG('Number' +
     A                                             'of Items')
     A            BTOCCY         3          TEXT('Original Currency')
     A                                      COLHDG('Original' +
     A                                             'Currency')
     A            BTOAMT        15S 0       TEXT('Original amount')
     A                                      COLHDG('Original' +
     A                                             'Amount')
     A            BTSARN         6          TEXT('Unique Interface Ref.')
     A                                      COLHDG('Unique' 'Interface' +
     A                                      'Ref.')
     A*=====================================================================
