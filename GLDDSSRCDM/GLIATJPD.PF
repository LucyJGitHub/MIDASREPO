      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas GL IAT processed interface file - postg')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger                                       *
      *                                                               *
      *  PF/GLIATJPD - GL Inter-Account Transfer Processed            *
      *                   Interface file - postings                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 25Nov05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CGL009  *CREATE    Date 18May99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL009 - Midas-Fontis Inter-Account Transfers (IAT) and      *
      *           Third Party Payments (TPP) Interface                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R GLIATJD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            FDBTNB         3S 0       TEXT('Batch number')
     A                                      COLHDG('Batch number')
     A            FDBINB         3S 0       TEXT('Item number')
     A                                      COLHDG('Item number')
     A**Start of change CGL029
     A**********  FDACNO        18A         TEXT('Account number')                            CGL029
     A**********                            COLHDG('Account number ')                         CGL029
     A            QQACNO        18A
     A**End of change CGL029
     A            FDPSTA        13S 0       TEXT('Posting amount')
     A                                      COLHDG('Posting amount')
     A            FDDRCR         1A         TEXT('Debit/Credit Ind')
     A                                      COLHDG('Debit/Credit Ind')
     A            FDVDAT         8A         TEXT('Value Date')
     A                                      COLHDG('Value Date')
     A            FDNARR        30A         TEXT('Narrative')
     A                                      COLHDG('Narrative')
     A            FDIATT        10A         TEXT('IAT Type')
     A                                      COLHDG('IAT Type')
      *
     A            ZZ010     R
      *
     A            FDAFTP         1A         TEXT('A/c Format entered indicator')
     A                                      COLHDG('A/c Fmt')
     A            FDABRC         3A         TEXT('A/c Branch')
     A                                      COLHDG('A/c branch')
      * Begin /CSD027/
     A**********  FDACUS         6S 0       TEXT('A/c Customer')                              CSD027
     A            FDACUS         6A         TEXT('A/c Customer')
      * End /CSD027/
     A                                      COLHDG('A/c Cust')
     A            FDACCY         3A         TEXT('A/c Currency')
     A                                      COLHDG('A/c Ccy')
     A**Start of change CGL029
     A**********  FDACOD         4S 0       TEXT('A/c code')                                  CGL029
     A**********                            COLHDG('A/c code')                                CGL029
     A            QQACOD    R               REFFLD(ACODQQ)
     A**End of change CGL029
     A            FDACSQ         2S 0       TEXT('A/c Sequence')
     A                                      COLHDG('A/c seq')
     A            FDENAR        20A         TEXT('Error Narrative')
     A                                      COLHDG('Error Narrative')
     A            FDBREF        30A         TEXT('Batch Reference')
     A                                      COLHDG('Batch Reference')
      *
     A**Start of change CGL029
     A            FDACNO        24A         TEXT('Account number')
     A            FDACOD    R               REFFLD(ACOD)
     A                                      TEXT('A/c code')
     A**End of change CGL029
