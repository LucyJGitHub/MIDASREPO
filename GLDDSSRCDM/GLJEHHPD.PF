      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  REUSEDLT(*YES)                                               *                     MD021155
/*EXI *  TEXT('Midas GL Journal history header')                      *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PF/GLJEHHPD - Journal History Header                         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD021155           Date 08Jul13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP032             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD021155 - COB Restructure Phase 1 remnants                  *
      *           - Reuse deleted records not done                    *
      *  CAP032 - Conversion of Journal Batch Entry inputs into       *
      *           modular structure to use as APIs.  Added fields     *
      *           HHFRNT, HHAFRT and HHTMST.                          *
      *                                                               *
      *****************************************************************
      *                                              Begin /CAP032/
     A                                      REF(MFRFL)
      *                                              End   /CAP032/
     A          R GLJEHHD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*---------------------------------------------------------------------
     A            HHBEDT         5  0       TEXT('Batch Entry Date')
     A                                      COLHDG('Batch' +
     A                                             'Entry Date')
     A            HHBTNB         3          TEXT('Batch number')
     A                                      COLHDG('Batch' +
     A                                             'number')
     A            HHBCDA         3          TEXT('Branch Code - Alpha')
     A                                      COLHDG('Brch' +
     A                                             'Code')
     A            HHDPCD         3          TEXT('Dept Code')
     A                                      COLHDG('Dept' +
     A                                             'Code')
     A            HHWSID        10          TEXT('Workstation id.')
     A                                      COLHDG('Workstation' +
     A                                             'id.')
     A            HHHIIN        15  0       TEXT('Hash integer input')
     A                                      COLHDG('Hash integer' +
     A                                             'input')
     A            HHHDIN         3  0       TEXT('Hash decimal input')
     A                                      COLHDG('Hash decimal' +
     A                                             'input')
     A            HHHINI         3  0       TEXT('Hash item no.input')
     A                                      COLHDG('Hash item' +
     A                                             'no.input')
     A            HHSHPI         1          TEXT('Shadow post indicator')
     A                                      COLHDG('Shadow post' +
     A                                             'indicator')
     A            HHRAPI         1          TEXT('Retail advice print ind.')
     A                                      COLHDG('Retail advice' +
     A                                             'print ind.')
     A            HHMBRB         1          TEXT('Multiple Branch Batch')
     A                                      COLHDG('Multiple' +
     A                                             'Branch Batch')
     A            HHRVVD         1          TEXT('Reversal / Void Indicator')
     A                                      COLHDG('Reversal /' +
     A                                             'Void Indicator')
     A            HHBDES        30          TEXT('Batch Description')
     A                                      COLHDG('Batch Description')
      *                                              Begin /CAP032/
     A            HHFRNT    R               REFFLD(FOTRNID)
     A                                      TEXT('Front Office Trans Id')
     A                                      COLHDG('Front Office' 'Trans Id')
     A            HHAFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Assoc Front Office Trans Id')
     A                                      COLHDG('Associated' 'Front Office' +
     A                                             'Trans Id')
     A            HHTMST    R               REFFLD(TIMESTMP)
     A                                      TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
      *                                              End   /CAP032/
     A*=====================================================================
