      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  REUSEDLT(*YES)                                               *                     MD021155
/*EXI *  TEXT('Midas GL Journal History Posting   PF')                *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PF/GLJEHPPD - Journal History Posting                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD021155           Date 08Jul13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP032             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU013             Date 27Feb98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD021155 - COB Restructure Phase 1 remnants                  *
      *           - Reuse deleted records not done                    *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP032 - Conversion of Journal Batch Entry inputs into       *
      *           modular structure to use as APIs.  Added fields     *
      *           HPFRNT, HPAFRT and HPTMST.                          *
      *  CEU013 - EMU Account Posting Narrative changes               *
      *           Addition of HPOCCY and HPOAMT fields                *
      *                                                               *
      *****************************************************************
      *                                              Begin /CAP032/
     A                                      REF(MFRFL)
      *                                              End   /CAP032/
     A          R GLJEHPD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*---------------------------------------------------------------------
     A            HPBEDT         5  0       TEXT('Batch Entry Date')
     A                                      COLHDG('Batch' +
     A                                             'Entry Date')
     A            HPBTNB         3          TEXT('Batch number')
     A                                      COLHDG('Batch' +
     A                                             'number')
     A            HPBINB         3  0       TEXT('Batch item no.')
     A                                      COLHDG('Batch' +
     A                                             'item no.')
     A            HPIBCA         3          TEXT('Input Branch Code - Alpha')
     A                                      COLHDG('Input Branch' +
     A                                             'Code - Alpha')
     A            HPCUST         6          TEXT('Customer number')
     A                                      COLHDG('Customer' +
     A                                             'number')
     A            HPCYCD         3          TEXT('Currency code')
     A                                      COLHDG('Curr' +
     A                                             'Code')
     A**Start of change CGL029
     A**********  HPACCD         4          TEXT('Account code')                              CGL029
     A**********                            COLHDG('Account' +                                CGL029
     A**********                                   'code')                                    CGL029
     A            QQACCD         4A
     A**End of change CGL029
     A            HPACSN         2          TEXT('Account sequence number')
     A                                      COLHDG('A/c' +
     A                                             'Seq' +
     A                                             'No.')
     A            HPNNBI         2          TEXT('Nostro number (Item)')
     A                                      COLHDG('Nostro' +
     A                                             'number (Item)')
     A            HPRACN        10S 0       TEXT('Retail account number')
     A                                      COLHDG('Retail' +
     A                                             'Account' +
     A                                             'Number')
     A            HPRINO         1          TEXT('Retail ind.: Override')
     A                                      COLHDG('Retail ind.:' +
     A                                             'Override')
     A                                      DFT('N')
     A            HPRINE         1          TEXT('Retail ind.: Error')
     A                                      COLHDG('Retail' +
     A                                             'ind.: Error')
     A                                      DFT('N')
     A            HPTRTY         5          TEXT('Transaction type')
     A                                      COLHDG('TRANS.' +
     A                                             'TYPE')
     A            HPPTAM        13  0       TEXT('Posting amount')
     A                                      COLHDG('Posting' +
     A                                             'Amount')
     A            HPDCIN         1          TEXT('Debit/credit indicator')
     A                                      COLHDG('DR/' +
     A                                             'CR' +
     A                                             'IND')
     A            HPPTDT         5  0       TEXT('Posting date')
     A                                      COLHDG('Posting' +
     A                                             'Date')
     A            HPVLDT         5  0       TEXT('Value date')
     A                                      COLHDG('Value' +
     A                                             'Date')
     A            HPNVCD         2          TEXT('Narrative code')
     A                                      COLHDG('NARC')
     A            HPNRDC        30          TEXT('Narrative description')
     A                                      COLHDG('NARRATIVE' +
     A                                             'DESCRIPTION')
     A            HPACTY         1          TEXT('Account type')
     A                                      COLHDG('Acc' +
     A                                             'Typ')
     A            HPACNB         6          TEXT('Assoc. Customer number')
     A                                      COLHDG('Assoc.' +
     A                                             'Customer number')
     A            HPCQNB         8          TEXT('Cheque number')
     A                                      COLHDG('Cheque' +
     A                                             'Number')
     A            HPCOIN         1          TEXT('Customer originated ind.')
     A                                      COLHDG('C/O' +
     A                                             'IND')
     A            HPSWCR        16          TEXT('Swift common reference')
     A                                      COLHDG('Swift common' +
     A                                             'reference')
     A            HPPRCN         4          TEXT('Profit centre')
     A                                      COLHDG('Profit' +
     A                                             'Centre')
     A            HPBCBH         3          TEXT('Branch Code (Batch Headr)')
     A                                      COLHDG('Branch Code' +
     A                                             '(Batch Headr)')
     A            HPRADP         1          TEXT('Retail advice printed')
     A                                      COLHDG('Retail advice' +
     A                                             'printed')
     A            HPSHPI         1          TEXT('Shadow post indicator')
     A                                      COLHDG('Shadow post' +
     A                                             'indicator')
     A            HPRRNM         5  0       TEXT('Relative rec no of memos rcd')
     A                                      COLHDG('Rel.rec' +
     A                                             'No.of' +
     A                                             'Mem.rcd')
     A            HPRNBE         1          TEXT('Retail Number Entered')
     A                                      COLHDG('Retail Number' +
     A                                             'Entered')
     A                                      DFT('N')
     A            HPRARC        10          TEXT('Retail Ac Referral Cond.')
     A                                      COLHDG('Retail Ac' +
     A                                             'Referral Cond.')
     A            HPTSTY        10          TEXT('Transaction (Sub - Type)')
     A                                      COLHDG('Transaction' +
     A                                             '(Sub - Type)')
     A            HPBKCD         2          TEXT('Book Code')
     A                                      COLHDG('Book' +
     A                                             'Code')
     A            HPSPFC         8  0       TEXT('Spread Factor')
     A                                      COLHDG('Spread' +
     A                                             'Factor')
     A            HPRVDI         1          TEXT('Reverse/Void Indicator')
     A                                      COLHDG('Reverse/Void' +
     A                                             'Indicator')
     A            HPNITM         5  0       TEXT('Number of Items')
     A                                      COLHDG('Number' +
     A                                             'of Items')
      *                                              Begin /CEU013/
     A            HPOCCY         3          TEXT('Original Currency')
     A                                      COLHDG('Original' +
     A                                             'Currency')
     A            HPOAMT        15P 0       TEXT('Original Amount')
     A                                      COLHDG('Original' +
     A                                             'Amount')
      *                                              End   /CEU013/
      *                                              Begin /CAP032/
     A            HPFRNT    R               REFFLD(FOTRNID)
     A                                      TEXT('Front Office Trans Id')
     A                                      COLHDG('Front Office' 'Trans Id')
     A            HPAFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Assoc Front Office Trans Id')
     A                                      COLHDG('Associated' 'Front Office' +
     A                                             'Trans Id')
     A            HPTMST    R               REFFLD(TIMESTMP)
     A                                      TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
      *                                              End   /CAP032/
     A*=====================================================================
     A**Start of change CGL029
     A            HPACCD        10A         TEXT('Account code')
     A**End of change CGL029
