      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas GL Standing journal header')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PF/GLJESHPD - Standing Journal Header                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      *  Prev Amend No. 244686             Date 28Feb07               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP032             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  244686 - Added the missing fields which CAP086 and CSC024    *
      *           introduced to GZGLJENHPD in order for these 2 files *
      *           to be synchronized                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP032 - Conversion of Journal Batch Entry inputs into       *
      *           modular structure to use as APIs.  Added fields     *
      *           BRUSRI, BRUSRA, BRFRNT, BRAFRT, BRTMST, BRBHAI      *
      *           and BRVALI.                                         *
      *                                                               *
      *****************************************************************
      *                                              Begin /CAP032/
     A                                      REF(MFRFL)
      *                                              End   /CAP032/
     A          R @BRREMA                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*---------------------------------------------------------------------
     A            BRBTNB         3          TEXT('Batch number')
     A                                      COLHDG('Batch' +
     A                                             'number')
     A            BRBCDA         3          TEXT('Branch Code - Alpha')
     A                                      COLHDG('Brch' +
     A                                             'Code')
     A            BRDPCD         3          TEXT('Dept Code')
     A                                      COLHDG('Dept' +
     A                                             'Code')
     A            BRWSID        10          TEXT('Workstation id.')
     A                                      COLHDG('Workstation' +
     A                                             'id.')
     A            BRHICC        15  0       TEXT('Hash integer calculated')
     A                                      COLHDG('Hash integer' +
     A                                             'calculated')
     A            BRHDCC         3  0       TEXT('Hash decimal calculated')
     A                                      COLHDG('Hash decimal' +
     A                                             'calculated')
     A            BRHINC         3  0       TEXT('Hash item no.calculated')
     A                                      COLHDG('Hash item' +
     A                                             'no.calculated')
     A            BRHIIN        15  0       TEXT('Hash integer input')
     A                                      COLHDG('Hash integer' +
     A                                             'input')
     A            BRHDIN         3  0       TEXT('Hash decimal input')
     A                                      COLHDG('Hash decimal' +
     A                                             'input')
     A            BRHINI         3  0       TEXT('Hash item no.input')
     A                                      COLHDG('Hash item' +
     A                                             'no.input')
     A            BRBTSF         1          TEXT('Batch status flag')
     A                                      COLHDG('Batch' +
     A                                             'status flag')
     A            BRBIUF         1          TEXT('Batch in use flag')
     A                                      COLHDG('Batch in' +
     A                                             'use flag')
     A            BRSHPI         1          TEXT('Shadow post indicator')
     A                                      COLHDG('Shadow post' +
     A                                             'indicator')
     A            BRRAPI         1          TEXT('Retail advice print ind.')
     A                                      COLHDG('Retail advice' +
     A                                             'print ind.')
     A            BRMBRB         1          TEXT('Multiple Branch Batch')
     A                                      COLHDG('Multiple' +
     A                                             'Branch Batch')
     A            BRRVVD         1          TEXT('Reversal / Void Indicator')
     A                                      COLHDG('Reversal /' +
     A                                             'Void Indicator')
     A            BRBDES        30          TEXT('Batch Description')
     A                                      COLHDG('Batch Description')
     A            BRSPTT        10  0       TEXT('Spread Total')
     A                                      COLHDG('Spread' +
     A                                             'Total')
     A            BRNIST         3  0       TEXT('No. of Items to Spread to')
     A                                      COLHDG('No. of Items' +
     A                                             'to Spread to')
     A            BRSPRF         4          TEXT('Spread Profile Code')
     A                                      COLHDG('Spread' +
     A                                             'Profile Code')
     A            BRDCIN         1          TEXT('Debit/credit indicator')
     A                                      COLHDG('DR/' +
     A                                             'CR' +
     A                                             'IND')
     A            BRIBCF         3          TEXT('Input Branch Cd  Alloc Fm')
     A                                      COLHDG('Input Branch' +
     A                                             'Cd  Alloc Fm')
     A            BRCUSF         6          TEXT('Customer Number  Alloc Fm')
     A                                      COLHDG('Customer Number' +
     A                                             ' Alloc Fm')
     A            BRCYCF         3          TEXT('Currency Code    Alloc Fm')
     A                                      COLHDG('Currency Code' +
     A                                             '   Alloc Fm')
     A**Start of change CGL029
     A**********  BRACCF         4          TEXT('Account Code     Alloc Fm')                 CGL029
     A**********                            COLHDG('Account Code' +                           CGL029
     A**********                                   '    Alloc Fm')                            CGL029
     A            QQACCF         4A
     A**End of change CGL029
     A            BRACSF         2          TEXT('Account Seq No.  Alloc Fm')
     A                                      COLHDG('Account Seq' +
     A                                             'No.  Alloc Fm')
     A            BRNNBF         2          TEXT('Nostro No. (Item)Alloc Fm')
     A                                      COLHDG('Nostro No.' +
     A                                             '(Item)Alloc Fm')
     A            BRRACF        10  0       TEXT('Retail A/c No.   Alloc Fm')
     A                                      COLHDG('Retail A/c No.' +
     A                                             '  Alloc Fm')
     A            BRPRCF         4          TEXT('Profit Centre    Alloc Fm')
     A                                      COLHDG('Profit Centre' +
     A                                             '   Alloc Fm')
     A            BRAMTF        13  0       TEXT('Amount           Alloc Fm')
     A                                      COLHDG('Amount' +
     A                                             '    Alloc Fm')
     A            BRIBCT         3          TEXT('Input Branch Cd  Alloc To')
     A                                      COLHDG('Input Branch' +
     A                                             'Cd  Alloc To')
     A            BRCYCT         3          TEXT('Currency Code    Alloc To')
     A                                      COLHDG('Currency Code' +
     A                                             '   Alloc To')
     A            BRCNAT         6          TEXT('Customer Number  Alloc To')
     A                                      COLHDG('Customer Number' +
     A                                             ' Alloc To')
     A**Start of change CGL029
     A**********  BRACCT         4          TEXT('Account Code     Alloc To')                 CGL029
     A**********                            COLHDG('Account Code' +                           CGL029
     A**********                                   '    Alloc To')                            CGL029
     A            QQACCT         4A
     A**End of change CGL029
     A            BRASNT         2          TEXT('Account Seq No.  Alloc To')
     A                                      COLHDG('Account Seq' +
     A                                             'No.  Alloc To')
     A            BRNNBT         2          TEXT('Nostro No. (Item)Alloc To')
     A                                      COLHDG('Nostro No.' +
     A                                             '(Item)Alloc To')
     A            BRRACT        10  0       TEXT('Retail A/c No.   Alloc To')
     A                                      COLHDG('Retail A/c No.' +
     A                                             '  Alloc To')
     A            BRTOJE         1          TEXT('Type of Journal Entry')
     A                                      COLHDG('Type of' +
     A                                             'Journal Entry')
      *                                              Begin /CAP032/
     A            BRUSRI    R               REFFLD(USRP)
     A                                      TEXT('Last Insert User')
     A                                      COLHDG('Last Insert User')
     A            BRUSRA    R               REFFLD(USRP)
     A                                      TEXT('Last Amend User')
     A                                      COLHDG('Last Amend User')
     A            BRFRNT    R               REFFLD(FOTRNID)
     A                                      TEXT('Front Office Trans Id')
     A                                      COLHDG('Front Office' 'Trans Id')
     A            BRAFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Assoc Front Office Trans Id')
     A                                      COLHDG('Associated' 'Front Office' +
     A                                             'Trans Id')
     A            BRTMST    R               REFFLD(TIMESTMP)
     A                                      TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
     A            BRBHAI         1          TEXT('Batch Held/Accepted Ind.')
     A                                      COLHDG('Batch Held/Acc' 'Ind')
     A            BRVALI         1          TEXT('Valid Indicator')
     A                                      COLHDG('Valid Ind')
      *                                              End   /CAP032/
     A*=====================================================================
     A**Start of change CGL029
     A            BRACCF        10A         TEXT('Account Code     Alloc Fm')
     A            BRACCT        10A         TEXT('Account Code     Alloc To')
     A**End of change CGL029
     A**Start of change 244686
     A            BRMEAB         1          TEXT('Month end Adj. Batch')
     A                                      COLHDG('Month end Adj. Batch')
     A            BRAUTH         1A         TEXT('Automatic Authorisation')
     A                                      COLHDG('Automatic' 'Authorisation')
     A**End of change 244686
