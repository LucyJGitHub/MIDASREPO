      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Risk Weighted Assets Netting Groups')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PF/GLNETGPD - Risk Weighted Assets Netting Groups            *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 25Nov05               *
      *                 CGL029             Date 01Sep03               *
      *                 CGL018  *CREATE    Date 18Sep02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL018 - Risk Weighted Assets                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R GLNETGD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A            RWRECI         1A         TEXT('Record Id')
     A                                      COLHDG('Record' 'Id')
      *
     A            RWPRIM         1A         TEXT('Primary/Secondary Indicator')
     A                                      COLHDG('Primary/' 'Secondary' +
                                            'Indicator')
      *
     A            RWBRCA         3A         TEXT('Branch')
     A                                      COLHDG('Branch')
      *
      * Begin /CSD027/
     A**********  RWCUST         6S 0       TEXT('Customer Number')                           CSD027
     A            RWCUST         6A         TEXT('Customer Number')
      * End /CSD027/
     A                                      COLHDG('Customer' 'Number')
      *
     A            RWCCY          3A         TEXT('Currency')
     A                                      COLHDG('Currency')
      *
     A**Start of change CGL029
     A**********  RWACOD         4S 0       TEXT('Account Code')                              CGL029
     A**********                            COLHDG('Account' 'Code')                          CGL029
      *
     A            QQACOD    R               REFFLD(ACODQQ)
     A**End of change CGL029
     A            RWACSQ         2S 0       TEXT('Account Seq')
     A                                      COLHDG('Account' 'Sequence')
      *
     A            RWACNO        10S 0       TEXT('Retail Number')
     A                                      COLHDG('Retail' 'Number')
      *
     A            RWPBRCA        3A         TEXT('Primary Account Branch')
     A                                      COLHDG('Primary' 'Account' +
     A                                      'Branch')
      *
      * Begin /CSD027/
     A**********  RWPCUST        6S 0       TEXT('Primary Account Customer +                  CSD027
     A            RWPCUST        6A         TEXT('Primary Account Customer +
      * End /CSD027/
     A                                      Number')
     A                                      COLHDG('Primary Acc' 'Customer' +
     A                                      'Number')
      *
     A            RWPCCY         3A         TEXT('Primary Account Currency')
     A                                      COLHDG('Primary' 'Account' +
     A                                      'Currency')
      *
     A**Start of change CGL029
     A**********  RWPACOD        4S 0       TEXT('Primary Account Account Code')              CGL029
     A**********                            COLHDG('Primary Acc' 'Account Code')              CGL029
      *
     A            RWPACODQQ R               REFFLD(ACODQQ)
     A**End of change CGL029
     A            RWPACSQ        2S 0       TEXT('Primary Account Account Seq')
     A                                      COLHDG('Primary Acc' 'Account Seq')
      *
     A            RWPACNO       10S 0       TEXT('Primary Account Retail +
     A                                      Number')
     A                                      COLHDG('Primary Acc' 'Retail' +
     A                                      'Number')
      *
     A            RWCHTP         1A         TEXT('Primary Account Last +
     A                                      Change Type')
     A                                      COLHDG('Primary Acc' 'Last Change' +
     A                                      'Type')
      *
     A            RWPBAL        15P 0       TEXT('Primary Account Balance')
     A                                      COLHDG('Primary Acc' 'Balance')
      *
     A            RWSBAL        15P 0       TEXT('Secondary Account Balance')
     A                                      COLHDG('Secondary Acc' 'Balance')
      *
     A            RWCONF         1A         TEXT('Confirmed/Unconfirmed +
     A                                      Indicator')
     A                                      COLHDG('Confirmed/' 'Unconfirmed' +
     A                                      'Indicator')
      *
     A            RWLCD          5P 0       TEXT('Last Change Date')
     A                                      COLHDG('Last' 'Change' 'Date')
      *
     A**Start of change CGL029
     A            RWACOD    R               REFFLD(ACOD)
     A                                      TEXT('Account Code')
     A            RWPACOD   R               REFFLD(ACOD)
     A                                      TEXT('Primary Account Account Code')
     A**End of change CGL029
