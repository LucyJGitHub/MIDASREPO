      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas GL API account maint details ext file valid')    *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger ILE Module                            *
      *                                                               *
      *  PF/GLVAMBXPD - Valid Accounts Details Extension File 1       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL165             Date 23Feb15               *
      *                 CER055             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 25Nov05               *
      *                 CGL029             Date 01Sep03               *
      *                 208221             Date 11Dec02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CDE002             Date 02Feb00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP035  *CREATE    Date 28Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL165 - Dual Withholding Tax                                *
      *  CER055 - Penalty Interest on Exceeding Overdraft Limit       *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  208221 - Add fields for Margin Base Rates                    *
      *  CDE002 - CCRM Revenue Analysis - Addition of Margin Int. Flds*
      *  CAP035 - Midas/ToF Interface                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A          R GLVAMBXD0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
 
      * Begin /CSD027/
     A**********  BXCNUM    R               REFFLD(CNUM)                                      CSD027
     A            BXCNUM         6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            BXCCY     R               REFFLD(CCY)
     A**Start of change CGL029
     A**********  BXACOD    R               REFFLD(ACOD)                                      CGL029
     A            QQACOD    R               REFFLD(ACODQQ)
     A**End of change CGL029
     A            BXACSQ    R               REFFLD(ACSQ)
     A            BXCRST    R               REFFLD(ATCRST)
     A            BXSUBO    R               REFFLD(ATSUBO)
     A            BXITAX    R               REFFLD(MTAX)
     A            BXSTCI    R               REFFLD(ATSTCI)
     A            BXCHIN    R               REFFLD(ATCHIN)
     A            BXCHAM    R               REFFLD(ATCHAM)
     A            BXZZ100   R               REFFLD(ZZ100)
     A            BXBRCA    R               REFFLD(BRCA)
     A            BXAODR    R               REFFLD(AODR)
     A            BXITRD    R               REFFLD(ITRD)
     A            BXORTC    R               REFFLD(ORTC)
     A            BXOLVD    R               REFFLD(OLVD)
     A            BXPOLC    R               REFFLD(POLCD)
     A            BXPOLE    R               REFFLD(POLE)
      *
     A            BXFRNT    R               REFFLD(FOTRNID)
     A            BXAFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            BXREPA    R               REFFLD(RPRLOC)
     A            BXTMES    R               REFFLD(TIMESTMP)
      **Start of change CDE002
     A            BXACNO    R               REFFLD(ACNO)
     A                                      TEXT('Account Number')
     A                                      COLHDG('Account Number')
     A            BXOVRD    R               REFFLD(OVRC)
     A                                      TEXT('Debit Margin Overridden')
     A                                      COLHDG('DR Margin Overridden')
     A            BXOVRC    R               REFFLD(OVRC)
     A                                      TEXT('Credit Margin Overridden')
     A                                      COLHDG('CR Margin Overridden')
     A            BXDRMG        11P 7       TEXT('Debit Margin Rate')
     A                                      COLHDG('Debit Margin Rate')
     A            BXCRMG        11P 7       TEXT('Credit Margin Rate')
     A                                      COLHDG('Credit Margin Rate')
     A            BXDRMD         5P 0       TEXT('Debit Margin Change Date')
     A                                      COLHDG('DR Margin Chg Date')
     A            BXCRMD         5P 0       TEXT('Credit Margin Change Date')
     A                                      COLHDG('CR Margin Chg Date')
      **End of change CDE002
      * Start of change 208221
     A            BXDMBR         5A         TEXT('Debit Margin Base Rate')
     A            BXCMBR         5A         TEXT('Credit Margin Base Rate')
      * End of change 208221
     A**Start of change CGL029
     A            BXACOD    R               REFFLD(ACOD)
     A**End of change CGL029
      **Start of change CER055
     A            BXDOBE         5P 0       TEXT('Date of Overdraft Balance +
     A                                            Excess')
     A                                      COLHDG('Date of Overdraft' +
     A                                             'Balance Excess')
     A            BXNDYE         4P 0       TEXT('No. of Days in Excess')
     A                                      COLHDG('No.of Days in Excess')
     A            BXNDYO         4P 0       TEXT('No. of Days in Excess +
     A                                            since A/c Opening')
     A                                      COLHDG('No.of Days in Excess' +
     A                                             'since A/c Opening')
     A            BXPOLA        11P 0       TEXT('Previous Overdraft Limit')
     A                                      COLHDG('Previous Overdraft' +
     A                                             'Limit')
      **End of change CER055
     A**Start of change CGL165
     A            BXTXC1         2A         TEXT('Main Tax Code')
     A                                      COLHDG('Main' +
     A                                             'Tax Code')
     A            BXTXC2         2A         TEXT('Secondary Tax Code')
     A                                      COLHDG('Secondary' +
     A                                             'Tax Code')
     A**End of change CGL165
