      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas GL Valid JE Items')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PF/GLVBITPPD - Valid Journal Batch Entry Items               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. TCA102             Date 02Apr04               *
      *  Prev Amend No. CRE019             Date 12Jan04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP032  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  TCA102 - Various errors on Journal Entry input.              *
      *  CRE019 - Cheque Processing and Maintenance                   *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP032 - Conversion of Journal Batch Entry inputs into       *
      *           modular structure to use as APIs.                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R GLVBITPD0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            B2BTNB         3          TEXT('Batch number')
     A                                      COLHDG('Batch' +
     A                                             'number')
     A            B2BINB         3  0       TEXT('Batch item no.')
     A                                      COLHDG('Batch' +
     A                                             'item no.')
     A            B2IBCA         3          TEXT('Input Branch Code - Alpha')
     A                                      COLHDG('Input Branch' +
     A                                             'Code - Alpha')
     A            B2CUST         6          TEXT('Customer number')
     A                                      COLHDG('Customer' +
     A                                             'number')
     A            B2CYCD         3          TEXT('Currency code')
     A                                      COLHDG('Curr' +
     A                                             'Code')
     A**Start of change CGL029
     A**********  B2ACCD         4          TEXT('Account code')                              CGL029
     A**********                            COLHDG('Account' +                                CGL029
     A**********                                   'code')                                    CGL029
     A            QQACCD         4A
     A**End of change CGL029
     A            B2ACSN         2          TEXT('Account sequence number')
     A                                      COLHDG('A/c' +
     A                                             'Seq' +
     A                                             'No.')
     A            B2NNBI         2          TEXT('Nostro number (Item)')
     A                                      COLHDG('Nostro' +
     A                                             'number (Item)')
     A            B2RACN        10S 0       TEXT('Retail account number')
     A                                      COLHDG('Retail' +
     A                                             'Account' +
     A                                             'Number')
     A            B2RINO         1          TEXT('Retail ind.: Override')
     A                                      COLHDG('Retail ind.:' +
     A                                             'Override')
     A                                      DFT('N')
     A            B2RINE         1          TEXT('Retail ind.: Error')
     A                                      COLHDG('Retail' +
     A                                             'ind.: Error')
     A                                      DFT('N')
     A            B2TRTY         5          TEXT('Transaction type')
     A                                      COLHDG('TRANS.' +
     A                                             'TYPE')
     A            B2PTAM        13  0       TEXT('Posting amount')
     A                                      COLHDG('Posting' +
     A                                             'Amount')
     A            B2DCIN         1          TEXT('Debit/credit indicator')
     A                                      COLHDG('DR/' +
     A                                             'CR' +
     A                                             'IND')
     A            B2PTDT         5  0       TEXT('Posting date')
     A                                      COLHDG('Posting' +
     A                                             'Date')
     A            B2VLDT         5  0       TEXT('Value date')
     A                                      COLHDG('Value' +
     A                                             'Date')
     A            B2NVCD         2          TEXT('Narrative code')
     A                                      COLHDG('NARC')
     A            B2NRDC        30          TEXT('Narrative description')
     A                                      COLHDG('Narrative' +
     A                                             'Description')
     A            B2ACTY         1          TEXT('Account type')
     A                                      COLHDG('Acc' +
     A                                             'Typ')
     A            B2ACNB         6          TEXT('Assoc. Customer number')
     A                                      COLHDG('Assoc.' +
     A                                             'Customer number')
     A            B2CQNB         8          TEXT('Cheque number')
     A                                      COLHDG('Cheque' +
     A                                             'Number')
     A            B2COIN         1          TEXT('Customer originated ind.')
     A                                      COLHDG('C/O' +
     A                                             'IND')
     A            B2SWCR        16          TEXT('Swift common reference')
     A                                      COLHDG('Swift common' +
     A                                             'reference')
     A            B2PRCN         4          TEXT('Profit centre')
     A                                      COLHDG('Profit' +
     A                                             'Centre')
     A            B2BCBH         3          TEXT('Branch Code (Batch Headr)')
     A                                      COLHDG('Branch Code' +
     A                                             '(Batch Headr)')
     A            B2RADP         1          TEXT('Retail advice printed')
     A                                      COLHDG('Retail advice' +
     A                                             'printed')
     A            B2SHPI         1          TEXT('Shadow post indicator')
     A                                      COLHDG('Shadow post' +
     A                                             'indicator')
     A            B2RRNM         5  0       TEXT('Relative rec no of memos rcd')
     A                                      COLHDG('Rel.rec' +
     A                                             'No.of' +
     A                                             'Mem.rcd')
     A            B2RNBE         1          TEXT('Retail Number Entered')
     A                                      COLHDG('Retail Number' +
     A                                             'Entered')
     A                                      DFT('N')
     A            B2RARC        10          TEXT('Retail Ac Referral Cond.')
     A                                      COLHDG('Retail Ac' +
     A                                             'Referral Cond.')
     A            B2TSTY        10          TEXT('Transaction (Sub - Type)')
     A                                      COLHDG('Transaction' +
     A                                             '(Sub - Type)')
     A            B2BKCD         2          TEXT('Book Code')
     A                                      COLHDG('Book' +
     A                                             'Code')
     A            B2SPFC         8  0       TEXT('Spread Factor')
     A                                      COLHDG('Spread' +
     A                                             'Factor')
     A            B2RVDI         1          TEXT('Reverse/Void Indicator')
     A                                      COLHDG('Reverse/Void' +
     A                                             'Indicator')
     A            B2NITM         5  0       TEXT('Number of Items')
     A                                      COLHDG('Number' +
     A                                             'of Items')
     A            B2OCCY         3          TEXT('Original Currency')
     A                                      COLHDG('Original' +
     A                                             'Currency')
     A            B2OAMT        15P 0       TEXT('Original amount')
     A                                      COLHDG('Original' +
     A                                             'Amount')
     A            B2FRNT    R               REFFLD(FOTRNID)
     A                                      TEXT('Front Office Trans ID')
     A                                      COLHDG('Front Office' 'Trans Id')
     A            B2AFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Assoc Front Office Trans Id')
     A                                      COLHDG('Associated' 'Front Office' +
     A                                             'Trans Id')
     A            B2TMST    R               REFFLD(TIMESTMP)
     A                                      TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
     A            B2VALI         1          TEXT('Valid Indicator')
     A                                      COLHDG('Valid Ind')
     A**Start of change TCA102
     A            B2ZONE        10A         TEXT('Zone')
     A                                      COLHDG('Zone')
     A**End of change TCA102
      *
     A**Start of change CGL029
     A            B2ACCD        10A         TEXT('Account code')
     A**End of change CGL029
     A**Start of change CRE019
     A            B2DUPC         1A         TEXT('Duplicate Cheques +
     A                                           Flag')
     A                                      COLHDG('Duplicate Cheques' +
     A                                           'Flag')
     A            B2OUTR         1A         TEXT('Out of Range +
     A                                           Flag')
     A                                      COLHDG('Out of Range' +
     A                                           'Flag')
     A**End of change CRE019
