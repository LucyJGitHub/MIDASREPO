      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas GL API GL Account definition file')              *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger ILE Module                            *
      *                                                               *
      *  PF/GLACCNPD - API Incoming Message Definition File for       *
      *             GL Account Maintenance - ALL FIELD DETAILS     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER047             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CDE002             Date 01Feb00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CFT004             Date 09Nov99               *
      *                 CAP035  *CREATE    Date 15Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *  CLE025 - Credit Lines                                        *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDE002 - CCRM Revenue Analysis - Addition of Margin Int. Flds*
      *  CFT004 - Straight Through Processing Phase 2                 *
      *         - International Bank Account Numbers                  *
      *  CAP035 - Midas/ToF interface                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R GLACCND0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
      ** Message contents
      *
      *
      ** ACCOUNT MASTER DETAILS
      *
      ** Action code
     A            DDACTN         1A         TEXT('Action code')
      *
      ** Account Number
     A**Start of change CGL029
     A**********  DDACCN        18A         TEXT('Account number')                            CGL029
     A            DDACCN        24A         TEXT('Account number')
     A**End of change CGL029
      *
      ** Review from Account
     A**Start of change CGL029
     A**********  DDRFRA        18A         TEXT('Review from Account')                       CGL029
     A            DDRFRA        24A         TEXT('Review from Account')
     A**End of change CGL029
      *
      ** Branch Code
     A            DDBRCA         3A         TEXT('Branch Code')
      *
      ** Customer number
     A            DDCUSN         6A         TEXT('Customer number')
      *
      ** Currency
     A            DDCCY          3A         TEXT('Currency')
      *
      ** Account code
      **Start of change CGL029
     A**********  DDACOD         4A         TEXT('Account code')                              CGL029
     A            DDACOD        10A         TEXT('Account code')
      **End of change CGL029
      *
      ** Account Sequence
     A            DDACSQ         2A         TEXT('Account sequence')
      *
      *
      ** Retail Number
     A            DDACNO        10A         TEXT('Retail number')
      *
      ** Account Name
     A            DDANAM        20A         TEXT('Account Name')
      *
      ** Date Account Opened
     A            DDDACO         6A         TEXT('Date Account Opened')
      *
      ** Next statement date
     A            DDNSTD         6A         TEXT('Next statement date')
      *
      ** Frequency
     A            DDSTFQ         1A         TEXT('Frequency')
      *
      ** Number of Copies
     A            DDENOC         2A          TEXT('Number of Copies')
      *
      ** Next statement day number
     A            DDNSDN         2A          TEXT('Next statement day')
      *
      ** Statement Balance in Euros
     A            DDEUSB         1A          TEXT('Statement Bal. in Euros')
      *
      ** Print address Y/N
     A            DDPTAD         1A          TEXT('Print address Y/N')
      *
      ** Address References
     A            DDREF         10A          TEXT('Address References')
      *
      ** Refer all debits
     A            DDRAD          1A          TEXT('Refer all debits')
      *
      ** Refer all credits
     A            DDRAC          1A          TEXT('Refer all credits')
      *
      ** Block all debits
     A            DDBAD          1A          TEXT('Block all debits')
      *
      ** Block all credits
     A            DDBAC          1A          TEXT('Block all credits')
      *
      ** TI Account
     A            DDTIAC         1A         TEXT('TI Account')
      *
      ** TI Account Number
     A            DDTIAN         6A         TEXT('TI Account Number')
      *
      ** TI Account Suffix
     A            DDTIAS         3A         TEXT('TI Account Suffix')
      *
      ** Profit centre
     A            DDPRFC         4A         TEXT('Profit centre')
      *
      ** Archiving frequency
     A            DDMFFQ         1A         TEXT('Archiving frequency')
      *
      ** Portfolio Reference
     A            DDPTFR         4A         TEXT('Portfolio Reference')
      *
      ** Midas taxable indicator
     A            DDITAX         1A         TEXT('Midas taxable indicator')
      *
      ** Statement charge indicator
     A            DDSTCI         1A         TEXT('Statement charge indicator')
      *
      ** Subject to account charge
     A            DDCHIN         1A         TEXT('Subject to account charge')
      *
      ** Account charge amount
     A            DDCHAM         8A         TEXT('Account charge amount')
      *                                              Begin /CFT004/
      ** International Bank Account Number
     A            DDIBAN        47A         TEXT('Int. Bank Acc. No.')
      *                                              End   /CFT004/
      *
      ** DEBIT INTERESTS AND OVERDRAFT DETAILS
      *
      ** Base Rate Type
     A            DDDRIB         2A         TEXT('Base Rate Type')
      *
      ** Rate/Spread
     A            DDDRIS        12A         TEXT('Rate/Spread')
      *
      ** Rate Spread sign
     A            DDDSIG         1A         TEXT('Rate Spread Sign')
      *
      ** Interest Calculation Type
     A            DDDICT         2A         TEXT('Interest Calc.Type')
      *
      ** Interest Calculation Sub-type
     A            DDDCST         5A         TEXT('Interest Calc.Styp')
      *
      ** This Change Value Date
     A            DDDRCD         6A         TEXT('This Change Vl.date')
      *
      ** Calculation Basis
     A            DDDRIC         1A         TEXT('Calculation Basis')
      *
      ** Next Capitalisation Date
     A            DDNDID         6A         TEXT('Next Capitalis.Date')
      *
      ** Next Capitalisation Frequency
     A            DDDRIF         1A         TEXT('Next Capital.frequ.')
      *
      ** Next Capitalisation Day No.
     A            DDDNCD         2A         TEXT('Next Capital. Day n')
      *
      ** Alternative A/c to post Cr Interest - Branch
     A            DDDRBR         3A         TEXT('Alt.A/c Cr.Int. Br.')
      *
      ** Alternative A/c to post Cr Interest - Account
     A**Start of change CGL029
     A**********  DDDACP        15A         TEXT('Alt.A/c Cr.Int. Acc')                       CGL029
     A            DDDACP        21A         TEXT('Alt.A/c Cr.Int. Acc')
     A**End of change CGL029
      *
      ** Overdraft Line
     A            DDODLN        12A         TEXT('Overdraft Line')
      *
      ** OD Secured
     A            DDODSC         1A         TEXT('OD Secured')
      *
      ** Review Date (Line or Rate)
     A            DDITRD         6A         TEXT('Rev. Date (Line or Rate)')
      *
      ** Overdraft Line Charge Value Date
     A            DDOLVD         6A         TEXT('OD Line Charge Value Date')
      *
      ** Overdaft Line Expiry Date
     A            DDODED         6A         TEXT('OD Line Expiry Date')
      *
      ** Additional Overdraft Rate
     A            DDAODR        12A         TEXT('Additional Overdraft Rate')
      *
      ** Additional Overdarft Rate Sign
     A            DDAODRX        1A         TEXT('Additional OD Rate Sign')
      *
      ** Change Value Date, OD Rate
     A            DDORTC         6A         TEXT('Chg. Val. Date, OD Rate')
      *
      ** CREDIT INTERESTS AND CHARGES DETAILS
      *
      ** Base Rate Type
     A            DDCRIB         2A         TEXT('Base Rate Type')
      *
      ** Rate Spread
     A            DDCRIS        12A         TEXT('Rate Spread')
      *
      ** Rate Spread sign
     A            DDCSIG         1A         TEXT('Rate Spread Sign')
      *
      ** Interest Calculation Type
     A            DDCICT         2A         TEXT('Interest Calc.Type')
      *
      ** Interest Calculation Sub-type
     A            DDCCST         5A         TEXT('Interest Calc.Styp')
      *
      ** This Change Value Date
     A            DDCRCD         6A         TEXT('This Change Vl.date')
      *
      ** Calculation Basis
     A            DDCRIC         1A         TEXT('Calculation Basis')
      *
      ** Next Capitalisation Date
     A            DDNCID         6A         TEXT('Next Capitalis.Date')
      *
      ** Next Capitalisation Frequency
     A            DDCRIF         1A         TEXT('Next Capital.frequ.')
      *
      ** Next Capitalisation Day No.
     A            DDCNCD         2A         TEXT('Next Capital. Day n')
      *
      ** Alternative A/c to post Cr Interest - Branch
     A            DDCRBR         3A         TEXT('Alt.A/c Cr.Int. Br.')
      *
      ** Alternative A/c to post Cr Interest - Account
     A**Start of change CGL029
     A**********  DDCACP        15A         TEXT('Alt.A/c Cr.Int. Acc')                       CGL029
     A            DDCACP        21A         TEXT('Alt.A/c Cr.Int. Acc')
     A**End of change CGL029
      *
      ** Charges Calculation Type
     A            DDCHGT         2A         TEXT('Charges Calc. Type')
      *
      ** Charges Calculation Sub-type
     A            DDCHST         5A         TEXT('Charges Calc. S/Typ')
      *
      ** Alternative A/c to post Charges - Branch
     A            DDCHBR         3A         TEXT('Alt.A/c Charges Br.')
      *
      ** Alternative A/c to post Chargest - Account
     A**Start of change CGL029
     A**********  DDCHCP        15A         TEXT('Alt.A/c Charges Acc')                       CGL029
     A            DDCHCP        21A         TEXT('Alt.A/c Charges Acc')
     A**End of change CGL029
      *
      ** Transaction Charges Override
     A            DDTCHO         7A         TEXT('Transact.Charges Ov')
      *
      *
      *
      ** COMMISSIONS & OVERRIDE DETAILS ...
      *
      *
      ** Highest Overdraft Commission
     A            DDCEXA        11A         TEXT('Highest OD Commission')
      *
      ** Turnover Commission
     A            DDCEXB        11A         TEXT('Turnover Commission')
      *
      ** Debit interest Commission
     A            DDCEXC        11A         TEXT('Debit interest Commission')
      *
      ** Commission on inactive account
     A            DDCEXD         8A         TEXT('Comm. on inactive account')
      *
      ** Minimum CR. Balance Commission
     A            DDCEXE         8A         TEXT('Minimum CR. Bal. Comm.')
      *
      ** Extra Daily Statement Commission
     A            DDCEXF         8A         TEXT('Extra Daily Statemnt Comm')
      *
      ** Fixed Commission
     A            DDCEXG         8A         TEXT('Fixed Commission')
      *
      ** Number of Copies Chargeable
     A            DDNOCC         2A         TEXT('No. of Copies Chargeable')
      *
      **Start of change CDE002
      ** Debit Margin Overriden
     A            DDOVRD         1A         TEXT('Debit Margin Overridden')
      *
      ** Debit Margin Rate
     A            DDDRMG        12A         TEXT('Debit Margin Rate')
      *
      ** Credit Margin Overriden
     A            DDOVRC         1A         TEXT('Credit Margin Overridden')
      *
      ** Credit Margin Rate
     A            DDCRMG        12A         TEXT('Credit Margin Rate')
      **End of change CDE002
      *
      ** ACCOUNT OTHER DETAILS ...
      *
      ** Minimum Balance
     A            DDMINB        12A         TEXT('Minimum Balance')
      *
      ** BRT Category
     A            DDBRTCA        1A         TEXT('BRT Category')
      *
      ** Facility Type
     A            DDFACT         3A         TEXT('Facility Type')
      *
      ** Facility Number
     A            DDFCNO         2A         TEXT('Facility Number')
      *
      ** Encashment Facility
     A            DDEFAC         2A         TEXT('Encashment Facility')
      *
      ** Inactive Account Y/N
     A            DDINAI         1A         TEXT('Inactive Account Y/N')
      *
      ** Bankrupt/liquidated Y/N
     A            DDBRLI         1A         TEXT('Bankrupt/liquidated Y/N')
      *
      ** Bad Debt Y/N
     A            DDBADI         1A         TEXT('Bad Debt Y/N')
      *
      ** Print Transaction Advice Y/N
     A            DDPTRA         1A         TEXT('Print Trans. Advice Y/N')
      *
      ** Print Rate Change Advice Y/N
     A            DDPRCH         1A         TEXT('Prn Rate Chg Advice Y/N')
      *
      ** Passbooks Used on Savings A/c Y/N
     A            DDPBKU         1A         TEXT('Passbooks Used on A/c Y/N')
      *
      ** Savings A/c Password
     A            DDPSWD        15A         TEXT('Savings A/c Password')
      *
      **Start of change CLE025
     A            DDFCCU        10          TEXT('Facility Customer')
     A                                      COLHDG('Facility Customer')
      **End of change CLE025
      **Start of change CER047
     A            DDK24C         1A         TEXT('KWG @24c reporting Indicator')
     A                                      COLHDG('KWG @24c reporting' +
     A                                             'Indicator')
      **End of change CER047
