/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas GL Generic Posting Interface Account Keys')      *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PF/GLGPIKPD - Generic Posting Interface Account Keys         *
      *                                                               *
      *  (c) Misys Banking Systems Ltd. 2006                          *
      *                                                               *
      *  Last Amend No. CGL160             Date 11Nov14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CGL047A            Date 20Oct09               *
      *                 CGL047 *Create     Date 30Jun06               *
      *                 Xnnnnn             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CGL160 - Cheque Details Upload via Generic Posting Interface *
      *  CGL047A- Extended Deal Sub Type                              *
      *  CGL047 - Midas Generic Posting Interface                     *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A          R GLGPIKD0                  TEXT('(c) Copyright Misys +
     A                                      Banking Systems Ltd. 2006')
     A            RECI      R
     A*                                              Begin /CGL047A/
     A**********  AKEY      R
     A            QQAKEY        10A
     A*                                              End   /CGL047A/
     A            PREF      R
     A            CNUM           6A
     A            BRCA      R
     A            ACSQ      R
     A            EDAT      R
     A            ECCY      R
     A            EAMT      R
     A            SETP      R
     A            OSAC          18A
     A            OSBR      R
     A            VDAT      R
     A            PRFC      R
     A            BOKC      R
     A            MODC      R
     A            BCEQ      R
     A            BTBTNB         3          TEXT('Batch number')
     A                                      COLHDG('Batch' +
     A                                             'number')
     A            BTBINB         3S 0       TEXT('Batch item no.')
     A                                      COLHDG('Batch' +
     A                                             'item no.')
     A            BTPNAR    R               REFFLD(PNAR)
     A                                      TEXT('Posting narrative')
     A                                      COLHDG('Posting' +
     A                                             'Narrative')
     A            BTSWCR    R               REFFLD(SWCR)
     A                                      TEXT('Swift Common Reference')
     A                                      COLHDG('SWIFT Common' +
     A                                             'Reference')
     A            BTTRAT    R               REFFLD(TRAT)
     A                                      TEXT('Transaction Type')
     A                                      COLHDG('Transaction' +
     A                                             'Type')
     A            BTRINO         1          TEXT('Retail ind.: Override')
     A                                      COLHDG('Retail ind.:' +
     A                                             'Override')
     A            BTSARN         6          TEXT('Unique Interface Ref.')
     A                                      COLHDG('Unique' 'Interface' +
     A                                      'Ref.')
     A*                                              Begin /CGL047A/
     A            AKEY          14A         TEXT('Account Key')
     A                                      COLHDG('Account' 'Key')
     A*                                              End   /CGL047A/
      **Start of Change CGL160**
     A            BTCQNB         8A         TEXT('Cheque Number')
     A                                      COLHDG('Cheque No.')
      **End of Change CGL160**
