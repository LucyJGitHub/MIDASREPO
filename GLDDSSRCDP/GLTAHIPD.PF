      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas GL Todays Accrual on Holding Items')             *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PF/GLTAHIPD - Today's Accrual on Holding Items               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R GLTAHID0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            F2PSTD         5P 0       TEXT('Posting Date')
     A                                      COLHDG('Posting' 'Date')
      *
     A            F2BRCA         3A         TEXT('Branch Code')
     A                                      COLHDG('Branch' 'Code')
      *
     A            F2FEPT         2A         TEXT('Fee Processing Type')
     A                                      COLHDG('Fee' 'Proc' 'Type')
      *
     A            F2FETP         2A         TEXT('Fee Type')
     A                                      COLHDG('Fee' 'Type')
      *
     A            F2CNUM         6A         TEXT('Customer Number')
     A                                      COLHDG('Customer' 'Number')
      *
     A            F2PTFR         4A         TEXT('Portfolio Reference')
     A                                      COLHDG('Portfolio' 'Reference')
      *
     A            F2CHGR         2A         TEXT('Charge Group')
     A                                      COLHDG('Charge' 'Group')
      *
     A            F2RECI         1A         TEXT('Record type')
     A                                      COLHDG('Record' 'Type')
      *
     A            F2INST         3A         TEXT('Instrument' )
     A                                      COLHDG('Instrument')
      *
     A            F2SRHO         2A         TEXT('Source of Holding')
     A                                      COLHDG('Source' 'Of Holding')
      *
     A            F2HORF        10A         TEXT('Holding Reference')
     A                                      COLHDG('Holding' 'Reference')
      *
     A            F2HCCY         3A         TEXT('Holding Currency')
     A                                      COLHDG('Holding' 'Currency')
      *
     A            F2HOAM        15P 0       TEXT('Holding Amount')
     A                                      COLHDG('Holding' 'Amount')
      *
     A            F2HACH        15P 0       TEXT('Hldng Amount in Charging CCY')
     A                                      COLHDG('Holding Amount' 'In Charg +
     A                                      ing' 'Currency')
      *
     A            F2FRAT         9P 7       TEXT('Fee Rate (only %age)')
     A                                      COLHDG('Fee' 'Rate')
      *
     A            F2CCCY         3A         TEXT('Charging Currency')
     A                                      COLHDG('Charging' 'Currency')
      *
     A            F2CHAM        15P 0       TEXT('Charging Amount')
     A                                      COLHDG('Charging' 'Amount')
      *
     A            F2DPRF        10A         TEXT('Depot Reference')
     A                                      COLHDG('Depot' 'Reference')
      *
     A            F2SHSN        10A         TEXT('Security Shortname')
     A                                      COLHDG('Security' 'Shortname')
      *
     A            F2INVT         3A         TEXT('Investment Type')
     A                                      COLHDG('Investment' 'Type')
      *
     A            F2SCCY         3A         TEXT('Selection Currency')
     A                                      COLHDG('Selection' 'Currency')
      *
     A            F2NORM         1A         TEXT('Nominal or Market Indicator')
     A                                      COLHDG('Nominal' 'Or Market' 'Ind.')
      *
     A            F2CPHI         1A         TEXT('Cust/Port/Holding Ind.')
     A                                      COLHDG('Customer/' 'Portfolio/' -
     A                                       'Holding Ind')
      *
     A            F2FERC         2A         TEXT('Fee Rate Code')
     A                                      COLHDG('Fee' 'Rate' 'Code')
      *
     A            F2THTI         1A         TEXT('Fee Rate Thresh./Tiered Ind.')
     A                                      COLHDG('Fee Rate' 'Threshold/' -
     A                                        'Tiered')
      *
     A            F2MINA        15P 0       TEXT('Fee Rate Minimum Amount')
     A                                      COLHDG('Fee Rate' 'Minimum' -
     A                                        'Amount')
      *
     A            F2MAXA        15P 0       TEXT('Fee Rate Maximum Amount')
     A                                      COLHDG('Fee Rate' 'Maximum' -
     A                                        'Amount')
      *
     A            F2RANG        15P 0       TEXT('Amount For Charges')
     A                                      COLHDG('Amount' 'For Charges')
      *
     A**Start of change CGL029
     A**********  F2ADAC        18A         TEXT('Fee Adjustment Account')                    CGL029
     A**********                            COLHDG('Fee' 'Adjustment' 'Account')              CGL029
      *
     A            QQADAC        18A
     A**End of change CGL029
     A            F2ADAM        15P 0       TEXT('Fee Adjustment Amount')
     A                                      COLHDG('Fee' 'Adjustment' 'Amount')
      *
     A**Start of change CGL029
     A**********  F2ACAC        18A         TEXT('Fee Accruals Account')                      CGL029
     A**********                            COLHDG('Fee' 'Accruals' 'Account')                CGL029
      *
     A            QQACAC        18A
     A**End of change CGL029
     A            F2ACAM        15P 0       TEXT('Fee Accruals Amount')
     A                                      COLHDG('Fee' 'Accruals' 'Amount')
      *
     A            F2FRPE         5P 0       TEXT('Accruals Period - Start Date')
     A                                      COLHDG('Accruals' 'Period' 'Start')
      *
     A            F2TOPE         5P 0       TEXT('Accruals Period - End Date')
     A                                      COLHDG('Accruals' 'Period' 'End')
      *
      ***  Key list
      *
     A**Start of change CGL029
     A            F2ADAC        24A         TEXT('Fee Adjustment Account')
     A            F2ACAC        24A         TEXT('Fee Accruals Account')
     A**End of change CGL029
     A          K F2PSTD
     A          K F2BRCA
     A          K F2FEPT
     A          K F2FETP
     A          K F2CNUM
     A          K F2PTFR
     A          K F2CHGR
     A          K F2RECI
     A          K F2INST
     A          K F2SRHO
     A          K F2HORF
      *
