      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas GL Joined File for APOSTPD and GLACPHPD')        *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  LF/APOSTJ2 - Midas GL Joined File for APOSTPD and GLACPHPD   *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD021383A          Date 16Jun23               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 AR669447 *CREATE   Date 07Jan13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD021383A - Incorrect CFA for Retail Overdraft (CLE025).     *
      *              Logical file over APOSTPD and GLACPHPD by        *
      *              account and cleared balance date.                *
      *            - MD061267 - Credit Lines (CLE025) fixes           *
      *  MD046248 - Finastra Rebranding                               *
      *  AR669447 - Contingent entry for facility linked to retail    *
      *             account produces extra repayment account key.     *
      *             Introduce new LF to capture all postings from     *
      *             GLACPHPD and APOSTPD files.                       *
      *           - Applied for AR1023928A (Child: AR1023929A)        *
      *                                                               *
      *****************************************************************
     A**********R APOSTPDF                  PFILE(APOSTPD GLACPHPD)                        MD021383A
     A          R APOSTJD0                  PFILE(APOSTPD GLACPHPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited 2012')
      ** Start of Change MD021383A
      *
      ** Back/Current Valued Posting
      *
     A            CNUM      R
     A            CCY       R
     A            ACOD      R
     A            ACSQ      R
     A            BRCA      R
     A            PSTD      R
     A            VALD      R
     A            PSTA      R
     A            DRCR      R
     A            SPOS      R
      *
      ** End of Change MD021383A
      *
     A          K CNUM
     A          K CCY
     A          K ACOD
     A          K ACSQ
     A          K BRCA
     A          K PSTD
      *
      ** Start of Change MD021383A
     A          S VALD                      CMP(LE PSTD)
      *
     A          R APOSTJD1                  PFILE(APOSTPD GLACPHPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited 2023')
      *
      ** Forward Valued Posting
      *
     A            CNUM      R
     A            CCY       R
     A            ACOD      R
     A            ACSQ      R
     A            BRCA      R
     A            PSTD                      RENAME(VALD)
     A                                      TEXT('Posting Date')
     A                                      COLHDG('Posting' 'Date')
     A            VALD                      RENAME(PSTD)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value' 'Date')
     A            PSTA      R
     A            DRCR      R
      *
     A          K CNUM
     A          K CCY
     A          K ACOD
     A          K ACSQ
     A          K BRCA
     A          K PSTD
      *
     A          S PSTD                      CMP(GT VALD)
      *
      ** End of Change MD021383A
