      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas GL and RE account balances sorted')              *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  JLF/GLACCTJ0 - Retail and GL Account Balances Sorted         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 MD041343           Date 15Sep16               *
      *                 CRE073             Date 06Dec10               *
      *                 CRE075             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 BUG23967           Date 13May09               *
      *                 CER047             Date 19May08               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CAP086             Date 08Jun05               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 01Jun99               *
      *                 CAP035             Date 07May99               *
      *                 CFT004             Date 12Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU013             DATE 27FEB98               *
      *                 CTI001             Date 15JUL97               *
      *                 CGL002 *CREATE     Date 28OCT96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  MD041343 - Undefined record type found in file SSRACJL       *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CRE075 - Effective Date for Retail Accounts (Recompile)      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23967 - Add Local Industry Code Field. (Recompile)        *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *           (Recompile)                                         *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089            *
      *           to Midas Plus (Recompile)                           *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CAP086 - Introduce Auto Authorisation to the API's           *
      *           without it. (Recompile)                             *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
     A*  CPB001 - Meridian DBA Middleware Replication Customization.  *
     A*           Recompiled over amended ACCNTAB.                    *
     A*  CAP035 - Conversion of Midas inputs to modular API structure *
     A*           Recompiled over amended ACCNTAB.                    *
     A*  CFT004 - Straight Through Processing Phase 2                 *
     A*         - International Bank Account Numbers (Recompile)      *
     A*  CEU013 - EMU Account Postings Narrative                      *
     A*           Recompiled over amended ACCNTAB.                    *
      *  CTI001 - Midas / Trade Innovation Interface Phase 1          *
      *           Recompiled over ACCNTAB.                            *
      *  CGL002 - Block GL Accounts in Journal Entry Input            *
      *                                                               *
      *****************************************************************
      *
     A                                      JDFTVAL
     A                                      DYNSLT
      *
      **Start of Change MD041343
     A**********R GLACCTJF                  JFILE(ACCNTAB CLINTCB)                          MD041343
     A          R SSRACJF                   JFILE(ACCNTAB CLINTCB)
      **End of Change MD041343
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(CNUM CNUM)
      *
     A            RECI                      JREF(ACCNTAB)
     A            BRCA                      JREF(ACCNTAB)
     A            ATYP                      JREF(ACCNTAB)
     A            STYP                      JREF(ACCNTAB)
     A            CNUM                      JREF(ACCNTAB)
     A            CCY                       JREF(ACCNTAB)
     A            ACOD                      JREF(ACCNTAB)
     A            ACSQ                      JREF(ACCNTAB)
     A            ACNO                      JREF(ACCNTAB)
     A            CASK                      JREF(CLINTCB)
     A            CRNM                      JREF(CLINTCB)
     A            ACOC                      JREF(CLINTCB)
     A            EFAC                      JREF(ACCNTAB)
     A            RETB                      JREF(ACCNTAB)
     A            LRCR                      JREF(ACCNTAB)
     A            GRBL                      JREF(ACCNTAB)
     A                                      RENAME(LDBL)
     A            NETBL                     JREF(ACCNTAB)
     A                                      RENAME(CLBL)
     A            HELD                      JREF(ACCNTAB)
     A            MINB                      JREF(ACCNTAB)
     A            ODLN                      JREF(ACCNTAB)
     A            ODED                      JREF(ACCNTAB)
     A            DLIM                      JREF(ACCNTAB)
      *
     A            EPGB                      JREF(ACCNTAB)
     A                                      RENAME(EPLB)
     A            EPNB                      JREF(ACCNTAB)
     A                                      RENAME(EPCB)
     A            RTLB                      JREF(ACCNTAB)
     A            RTCB                      JREF(ACCNTAB)
     A            STFQ                      JREF(ACCNTAB)
     A            DACO                      JREF(ACCNTAB)
      *
      **  Key fields
      *
     A          K BRCA
     A          K CNUM
     A          K CCY
     A          K ACOD
     A          K ACSQ
      *
      **  Select fields
      *
     A          S RECI                      VALUES('D')
     A            ATYP                      VALUES('R' 'G')
