      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas GL and RE account balances sorted')              *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  JLF/GLACCTJ1 - Retail and GL Account Balances Sorted         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2017            *
      *                                                               *
      *  Last Amend No. MD039745A          Date 02Feb17               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD039745A - REC3760T2/00001 failur due to undefined record   *
      *              type. Create new LF to OVRDBF with SSRACJ0 with  *
      *              same record format                               *
      *                                                               *
      *****************************************************************
      *
     A                                      JDFTVAL
     A                                      DYNSLT
      *
     A          R SSRACJF                   JFILE(ACCNTAB CLINTCB)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(CNUM CNUM)
      *
     A            RECI                      JREF(ACCNTAB)
     A            BRCA                      JREF(ACCNTAB)
     A            ATYP                      JREF(ACCNTAB)
     A            STYP                      JREF(ACCNTAB)
     A            CNUM                      JREF(ACCNTAB)
     A            CCY                       JREF(ACCNTAB)
     A            ACOD                      JREF(ACCNTAB)
     A            ACSQ                      JREF(ACCNTAB)
     A            ACNO                      JREF(ACCNTAB)
     A            CASK                      JREF(CLINTCB)
     A            CRNM                      JREF(CLINTCB)
     A            ACOC                      JREF(CLINTCB)
     A            EFAC                      JREF(ACCNTAB)
     A            RETB                      JREF(ACCNTAB)
     A            LRCR                      JREF(ACCNTAB)
     A            GRBL                      JREF(ACCNTAB)
     A                                      RENAME(LDBL)
     A            NETBL                     JREF(ACCNTAB)
     A                                      RENAME(CLBL)
     A            HELD                      JREF(ACCNTAB)
     A            MINB                      JREF(ACCNTAB)
     A            ODLN                      JREF(ACCNTAB)
     A            ODED                      JREF(ACCNTAB)
     A            DLIM                      JREF(ACCNTAB)
      *
     A            EPGB                      JREF(ACCNTAB)
     A                                      RENAME(EPLB)
     A            EPNB                      JREF(ACCNTAB)
     A                                      RENAME(EPCB)
     A            RTLB                      JREF(ACCNTAB)
     A            RTCB                      JREF(ACCNTAB)
     A            STFQ                      JREF(ACCNTAB)
     A            DACO                      JREF(ACCNTAB)
      *
      **  Key fields
      *
     A          K BRCA
     A          K CNUM
     A          K CCY
     A          K ACOD
     A          K ACSQ
      *
      **  Select fields
      *
     A          S RECI                      VALUES('D')
     A            ATYP                      VALUES('R' 'G')
