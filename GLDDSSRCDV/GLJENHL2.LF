      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas GL Journal entry header branch/batch seq')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  LF/GLJENHL2 - Journal entry header branch/batch seq          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAP086             Date 08Jun05               *
      *  Prev Amend No. CSC024             Date 07Feb05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP032             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CGL008             Date 03Mar99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CAP086 - Introduce Auto Authorisation to the API's           *
      *           without it.                                         *
      *  CSC024 - Open Month End                                      *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CAP032 - Conversion of Journal Batch Entry inputs into       *
      *           modular structure to use as APIs.  Added fields     *
      *           BRFRNT, BRAFRT, BRTMST, BRBHAI and BRVALI.          *
      *  CGL008 - Security on Journal Entry.                          *
      *                                                               *
      *****************************************************************
     A          R @BRTHCJ                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PFILE(GLJENHPD)
     A*---------------------------------------------------------------------
     A            BRBTNB                    TEXT('Batch number')
     A            BRBCDA                    TEXT('Branch Code - Alpha')
     A            BRDPCD                    TEXT('Dept Code')
     A            BRWSID                    TEXT('Workstation id.')
     A            BRHICC                    TEXT('Hash integer calculated')
     A            BRHDCC                    TEXT('Hash decimal calculated')
     A            BRHINC                    TEXT('Hash item no.calculated')
     A            BRHIIN                    TEXT('Hash integer input')
     A            BRHDIN                    TEXT('Hash decimal input')
     A            BRHINI                    TEXT('Hash item no.input')
     A            BRBTSF                    TEXT('Batch status flag')
     A            BRBIUF                    TEXT('Batch in use flag')
     A            BRSHPI                    TEXT('Shadow post indicator')
     A            BRRAPI                    TEXT('Retail advice print ind.')
     A            BRMBRB                    TEXT('Multiple Branch Batch')
     A            BRRVVD                    TEXT('Reversal / Void Indicator')
     A            BRBDES                    TEXT('Batch Description')
     A            BRSPTT                    TEXT('Spread Total')
     A            BRNIST                    TEXT('No. of Items to Spread to')
     A            BRSPRF                    TEXT('Spread Profile Code')
     A            BRDCIN                    TEXT('Debit/credit indicator')
     A            BRIBCF                    TEXT('Input Branch Cd  Alloc Fm')
     A            BRCUSF                    TEXT('Customer Number  Alloc Fm')
     A            BRCYCF                    TEXT('Currency Code    Alloc Fm')
     A            BRACCF                    TEXT('Account Code     Alloc Fm')
     A            BRACSF                    TEXT('Account Seq No.  Alloc Fm')
     A            BRNNBF                    TEXT('Nostro No. (Item)Alloc Fm')
     A            BRRACF                    TEXT('Retail A/c No.   Alloc Fm')
     A            BRPRCF                    TEXT('Profit Centre    Alloc Fm')
     A            BRAMTF                    TEXT('Amount           Alloc Fm')
     A            BRIBCT                    TEXT('Input Branch Cd  Alloc To')
     A            BRCYCT                    TEXT('Currency Code    Alloc To')
     A            BRCNAT                    TEXT('Customer Number  Alloc To')
     A            BRACCT                    TEXT('Account Code     Alloc To')
     A            BRASNT                    TEXT('Account Seq No.  Alloc To')
     A            BRNNBT                    TEXT('Nostro No. (Item)Alloc To')
     A            BRRACT                    TEXT('Retail A/c No.   Alloc To')
     A            BRTOJE                    TEXT('Type of Journal Entry')
      *                                              Begin /CGL008/
     A            BRUSRI                    TEXT('Last Insert User')
     A            BRUSRA                    TEXT('Last Amend User')
      *                                              End   /CGL008/
      *                                              Begin /CAP032/
     A            BRFRNT                    TEXT('Front Office Trans Id')
     A            BRAFRT                    TEXT('Assoc Front Office Trans Id')
     A            BRTMST                    TEXT('Timestamp')
     A            BRBHAI                    TEXT('Batch Held/Accepted Ind.')
     A            BRVALI                    TEXT('Valid Indicator')
      *                                              End   /CAP032/
      **Start of change CSC024
     A            BRMEAB                    TEXT('Month end Adj. Batch')
      **End of change CSC024
     A**Start of change CAP086
     A            BRAUTH                    TEXT('Automatic Authorisation')
     A**End of change CAP086
     A*.....................................................................
     A* Key fields
     A          K BRBCDA
     A          K BRBTNB
     A*=====================================================================
