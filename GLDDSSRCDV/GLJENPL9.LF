      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas GL Journal entry postings in amount/DC seq')     *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  LF/GLJENPL9 - Journal entry postings in amount/DC seq        *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. MD059634 *CREATE   Date 21Mar22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD059634 - Batch postings records jumbled when it arrive in  *
      *             Midas. Batch items on Display Journal Enquiries   *
      *             inside Journal Entry based on Interface File are  *
      *             not sorted by posting amount and DR/CR indicator. *
      *                                                               *
      *****************************************************************
     A          R @BTREAL                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2022')
     A                                      PFILE(GLJENPPD)
      *
     A            BTBTNB                    TEXT('Batch number')
     A            BTBINB                    TEXT('Batch item no.')
     A            BTIBCA                    TEXT('Input Branch Code - Alpha')
     A            BTCUST                    TEXT('Customer number')
     A            BTCYCD                    TEXT('Currency code')
     A            BTACCD                    TEXT('Account code')
     A            BTACSN                    TEXT('Account sequence number')
     A            BTNNBI                    TEXT('Nostro number (Item)')
     A            BTRACN                    TEXT('Retail account number')
     A            BTRINO                    TEXT('Retail ind.: Override')
     A            BTRINE                    TEXT('Retail ind.: Error')
     A            BTTRTY                    TEXT('Transaction type')
     A            BTPTAM                    TEXT('Posting amount')
     A            BTDCIN                    TEXT('Debit/credit indicator')
     A            BTPTDT                    TEXT('Posting date')
     A            BTVLDT                    TEXT('Value date')
     A            BTNVCD                    TEXT('Narrative code')
     A            BTNRDC                    TEXT('Narrative description')
     A            BTACTY                    TEXT('Account type')
     A            BTACNB                    TEXT('Assoc. Customer number')
     A            BTCQNB                    TEXT('Cheque number')
     A            BTCOIN                    TEXT('Customer originated ind.')
     A            BTSWCR                    TEXT('Swift common reference')
     A            BTPRCN                    TEXT('Profit centre')
     A            BTBCBH                    TEXT('Branch Code (Batch Headr)')
     A            BTRADP                    TEXT('Retail advice printed')
     A            BTSHPI                    TEXT('Shadow post indicator')
     A            BTRRNM                    TEXT('Relative rec no of memos rcd')
     A            BTRNBE                    TEXT('Retail Number Entered')
     A            BTRARC                    TEXT('Retail Ac Referral Cond.')
     A            BTTSTY                    TEXT('Transaction (Sub - Type)')
     A            BTBKCD                    TEXT('Book Code')
     A            BTSPFC                    TEXT('Spread Factor')
     A            BTRVDI                    TEXT('Reverse/Void Indicator')
     A            BTNITM                    TEXT('Number of Items')
     A            BTOCCY                    TEXT('Original Currency')
     A            BTOAMT                    TEXT('Original amount')
     A            BTFRNT                    TEXT('Front Office Trans Id')
     A            BTAFRT                    TEXT('Assoc Front Office Trans Id')
     A            BTTMST                    TEXT('Timestamp')
     A            BTVALI                    TEXT('Valid Indicator')
     A            BTDUPC                    TEXT('Duplicate Cheques Flag')
     A            BTOUTR                    TEXT('Out of Range Flag')
     A            BTPDID                    TEXT('Pre-Deal Check ID')
      *
      ** Key fields
      *
     A          K BTBTNB
     A          K BTPTAM
     A          K BTDCIN
     A          K BTBINB
     A*=====================================================================
