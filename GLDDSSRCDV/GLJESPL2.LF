      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas GL Journal standing posting Btch/Ccy')           *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  LF/GLJESPL2 - Journal entry standing posting                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD027838           Date 02Jul14               *
      *                 AR761594           Date 24May11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP032             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 082118             Date 20Jun96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD027838 - Match the structure of GLJENPL2. Used as Override *
      *             in GLC1440.                                       *
      *  AR761594 - Match the structure of GLJESPPD and GLJESXPD with *
      *           GLJENPPD and GLJENXPD respectively.                 *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CAP032 - Conversion of Journal Batch Entry inputs into       *
      *           modular structure to use as APIs.  Added fields     *
      *           BTOCCY, BTOAMT, BTFRNT, BTAFRT, BTTMST and BTVALI.  *
      *  082118 - Key s/b the same as GLJENPL2, else 'Tried to        *
      *           write duplicate key' message may be received.       *
      *                                                               *
     A********************************************************************
     A          R @BTREBG                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      PFILE(GLJESPPD)
     A*---------------------------------------------------------------------
     A            BTBTNB                    TEXT('Batch number')
     A            BTBINB                    TEXT('Batch item no.')
     A            BTIBCA                    TEXT('Input Branch Code - Alpha')
     A            BTCUST                    TEXT('Customer number')
     A            BTCYCD                    TEXT('Currency code')
     A            BTACCD                    TEXT('Account code')
     A            BTACSN                    TEXT('Account sequence number')
     A            BTNNBI                    TEXT('Nostro number (Item)')
     A            BTRACN                    TEXT('Retail account number')
     A            BTRINO                    TEXT('Retail ind.: Override')
     A            BTRINE                    TEXT('Retail ind.: Error')
     A            BTTRTY                    TEXT('Transaction type')
     A            BTPTAM                    TEXT('Posting amount')
     A            BTDCIN                    TEXT('Debit/credit indicator')
     A            BTPTDT                    TEXT('Posting date')
     A            BTVLDT                    TEXT('Value date')
     A            BTNVCD                    TEXT('Narrative code')
     A            BTNRDC                    TEXT('Narrative description')
     A            BTACTY                    TEXT('Account type')
     A            BTACNB                    TEXT('Assoc. Customer number')
     A            BTCQNB                    TEXT('Cheque number')
     A            BTCOIN                    TEXT('Customer originated ind.')
     A            BTSWCR                    TEXT('Swift common reference')
     A            BTPRCN                    TEXT('Profit centre')
     A            BTBCBH                    TEXT('Branch Code (Batch Headr)')
     A            BTRADP                    TEXT('Retail advice printed')
     A            BTSHPI                    TEXT('Shadow post indicator')
     A            BTRRNM                    TEXT('Relative rec no of memos rcd')
     A            BTRNBE                    TEXT('Retail Number Entered')
     A            BTRARC                    TEXT('Retail Ac Referral Cond.')
     A            BTTSTY                    TEXT('Transaction (Sub - Type)')
     A            BTBKCD                    TEXT('Book Code')
     A            BTSPFC                    TEXT('Spread Factor')
     A            BTRVDI                    TEXT('Reverse/Void Indicator')
     A            BTNITM                    TEXT('Number of Items')
      *                                              Begin /CAP032/
     A            BTOCCY                    TEXT('Original Currency')
     A            BTOAMT                    TEXT('Original amount')
     A            BTFRNT                    TEXT('Front Office Trans Id')
     A            BTAFRT                    TEXT('Assoc Front Office Trans Id')
     A            BTTMST                    TEXT('Timestamp')
     A            BTVALI                    TEXT('Valid Indicator')
      *                                              End   /CAP032/
     A** Start of change AR761594
     A            BTDUPC                    TEXT('Duplicate Cheques Flag')
     A            BTOUTR                    TEXT('Out of Range Flag')
     A** End of change AR761594
     A** Start of Change MD027838
     A            BTPDID                    TEXT('Pre-Deal Check ID')
     A** End of Change MD027838
     A*.....................................................................
     A* Key fields
     A          K BTBTNB
     A          K BTCYCD
     A*                                              Begin 082118
     A          K BTIBCA
     A*                                              End   082118
     A*=====================================================================
