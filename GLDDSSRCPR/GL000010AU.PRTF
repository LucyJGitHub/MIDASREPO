      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL Price Type Update of Security Collaterals')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000010AU - Price Type Update of Security              *
      *                    Collaterals Audit                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL014  *CREATE    Date 20Oct03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL014 - Collaterals Processing                              *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Header
      *
     A          R HEADAU
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE GL000010AU'
     A            BJURPT    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            BJMRDT    R            105REFFLD(RUNA)
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      SPACEA(1)
     A                                      EDTWRD('0 :  :  ')
      *
     A                                    54'PRICE TYPE REPORT AUDIT'
     A                                      SPACEA(1)
      *
     A                                    54'-----------------------'
     A                                      SPACEA(3)
      *
     A                                    60'CALCULATED'
     A                                      SPACEA(1)
     A                                    60'----------'
     A                                      SPACEA(2)
      *
     A                                     6'FILE - PRICE TYPE TABLE +
     A                                      - LF/GLCOLLLE'
     A                                     6'------------------------+
     A                                      -------------'
     A                                      SPACEB(1)
     A                                      SPACEA(3)
     A          R CONTROL
     A                                    10'NUMBER OF PLEDGED COLLATERAL +
     A                                      ITEMS READ'
     A            RPCALC         4  0     63EDTWRD('  0 ')
     A                                      SPACEA(1)
     A                                    10'NUMBER OF SUCCESSFUL +
     A                                      UPDATES'
     A            RSCALC         4  0     63EDTWRD('  0 ')
     A                                      SPACEA(1)
     A                                    10'NUMBER OF UNSUCCESSFUL +
     A                                      UPDATES'
     A            RUCALC         4  0     63EDTWRD('  0 ')
      *
      ** No Details
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      ** Database Error Message
      *
     A          R DBERROR                   TEXT('DATABASE ERROR MSG')
     A                                      SPACEB(3)
     A                                    48 '***   PRINT TERMINATED   ***'
     A                                      SPACEA(2)
     A                                    45 'DATABASE ERROR'
     A            DBASE          3  0     60
     A                                    65 'FILE'
     A            DBFILE         8        70
     A                                      SPACEA(1)
     A                                    45 'KEY'
     A            DBKEY         29        49
