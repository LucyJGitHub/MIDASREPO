      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Unsuccessful Update of Security Coll')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000010P1 - Unsuccessful Update of Security Collaterals*
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL014  *CREATE    Date 20Oct03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL014 - Collaterals Processing                              *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Format
      *
     A          R GL0010R0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE GL000010P1'
     A            BJURPT    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    39'UNSUCCESSFUL UPDATE OF +
     A                                       SECURITY COLLATERALS'
     A                                      SPACEA(1)
     A                                    39'-----------------------+
     A                                       --------------------'
     A                                      SPACEA(3)
      *
     A                                     2'Collateral Reference'
     A                                    31'Branch'
     A                                    46'Security'
     A                                    66'Customer'
     A  25                                82'Portfolio'
     A                                    99'Price Type'
     A                                      SPACEA(1)
     A                                     2'--------------------'
     A                                    31'------'
     A                                    46'--------'
     A                                    66'--------'
     A                                    82'---------'
     A                                    99'----------'
     A                                      SPACEA(1)
      *
      ** Format to Output Detail Lines
      *
     A          R GL0010R1                  TEXT('DETAIL OUTPUT FMT')
      *
     A            RCOLRE        10A        6TEXT('Collateral Reference')
     A            RBRNCH         3A       32TEXT('Branch')
     A            RSCRTY        10A       46TEXT('Security')
     A            RCUSTR         6A       66TEXT('Customer')
     A  25        RPRTFL         4A       83TEXT('Portfolio')
     A            RPRTYP         5A      101TEXT('Price Type')
     A                                      SPACEA(1)
      *
      ** End of Report Format
      *
     A          R GL0010R2                  TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    52'*** END OF REPORT ***'
      *
