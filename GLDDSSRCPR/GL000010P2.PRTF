      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Successful Update of Security Collaterals')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000010P2 - Successful Update of Security Collaterals  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL014  *CREATE    Date 20Oct03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL014 - Collaterals Processing                              *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Format
      *
     A          R GL0010R0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE GL000010P2'
     A            BJURPT    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    40'SUCCESSFUL UPDATE OF +
     A                                       SECURITY COLLATERALS'
     A                                      SPACEA(1)
     A                                    40'---------------------+
     A                                       --------------------'
     A                                      SPACEA(3)
      *
     A                                     2'Collateral Reference'
     A                                    31'Branch'
     A                                    46'Security'
     A                                    66'Customer'
     A  25                                82'Portfolio'
     A                                    99'Prev Price Type'
     A                                      SPACEA(1)
     A                                     2'--------------------'
     A                                    31'------'
     A                                    46'--------'
     A                                    66'--------'
     A                                    82'---------'
     A                                    99'---------------'
     A                                      SPACEA(1)
      *
      ** Format to Output Detail Lines
      *
     A          R GL0010R1                  TEXT('DETAIL OUTPUT FMT')
      *
     A            RCOLRE        10A        7TEXT('Collateral Reference')
     A            RBRNCH         3A       32TEXT('Branch')
     A            RSCRTY        10A       45TEXT('Security')
     A            RCUSTR         6A       67TEXT('Customer')
     A  25        RPRTFL         4A       84TEXT('Portfolio')
     A            RPRTYP         5A      105TEXT('Price Type')
     A                                      SPACEA(1)
      *
      ** End of Report Format
      *
     A          R GL0010R2                  TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    52'*** END OF REPORT ***'
      *
