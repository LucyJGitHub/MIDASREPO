      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Accounts excluded from KWG @24C Reporting')   *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000100P1 - Accounts excluded from KWG @24C Report     *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG23958           Date 11May09               *
      *                 BUG23959           Date 09May09               *
      *                 CER047  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23958 - New field should use correct char §               *
      *  BUG23959 - Report not showing when accounts were opened      *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R GL000100R0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2008')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL000100P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    44'Accounts Excluded From +
      **Start of change BUG23958
     A**********                            @24C '                                          BUG23958
     A                                      §24C '
      **End of change BUG23958
      *
     A                                    72'Exception Report'
     A                                      SPACEA(1)
      *
     A                                    44'----------------------+
     A                                      ------'
      *
     A                                    72'----------------'
     A                                      SPACEA(2)
      *
     A                                     6'Customer'
      *
     A                                    58'Account'
      *
      **Start of change BUG23959
     A**********                          95'Balance'                                       BUG23959
     A                                    87'Balance'
      *
     A                                   105'Date Acc. Opened'
      **End of change BUG23959
     A                                      SPACEA(2)
      *
      ** Detail Record Format
      *
     A          R GL000100R1
      *
     A                                      SPACEA(001)
      *
     A  20        RCUST          6         6TEXT('Customer number')
      *
     A  20        RNAME         20        14TEXT('Customer name')
      *
     A            RCCY           3        40TEXT('Currency')
      *
     A            RACOD         10        46TEXT('Account Code')
     A
     A            RACSQ          2        58TEXT('Account Sequence +
     A                                          Number')
      *
     A            RBRCA          3        61TEXT('Branch code')
      *
     A            RACNO         10        70TEXT('Retail Account +
     A                                          Number')
      *
      **Start of change
     A**********  RBAL          22        90TEXT('Balance')                                 BUG23959
     A            RBAL          22        82TEXT('Balance')
      *
     A            RDACO          7       110TEXT('Date Acc. Opened')
      **End of change
      *
      ** Report Trailer
      *
     A          R GL000100R2
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
