     *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Transfer Events - Audit Report')
      *****************************************************************
      *                                                               *
      *  MIDAS - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000539AU - Transfer Events Audit Report               *
      *                                                               *
      *  @ Finastra International Limited 2006                        *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG12141           Date 05Oct06               *
      *                 CRE026  *CREATE    Date 24May06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG12141 - Word Equation should not appear on Consumer       *
      *             banking audit reports                             *
      *  CRE026 - Consumer Banking                                    *
      *                                                               *
      *****************************************************************
      *
      *----------------
      * REPORT HEADER *
      *----------------
      *
     A          R AU_HDR
     A                                      SKIPB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      'REFERENCE'
     A            AU_PGMNM      10A  O    11
     A            AU_RPTTL      53A  O    40
     A                                   +12
     A                                      'DATE'
     A            AU_DATE        7A  O    +1
     A                                    +4
     A                                      'PAGE'
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                    39
     A**********                            'Consumer Banking - Equation Events-            BUG12141
     A**********                             Extract Audit Report'                          BUG12141
     A                                      'Consumer Banking - Sub-ledger-                 BUG12141
     A                                       Events Extract Audit Report'                   BUG12141
     A                                      SPACEB(003)
     A                                    39
     A**********                            '-----------------------------------            BUG12141
     A**********                            ---------------------'                          BUG12141
     A                                      '------------------------------                 BUG12141
     A                                      ----------------------------'                   BUG12141
     A                                      SPACEB(001)
      *
      *----------------
      * REPORT DETAIL *
      *----------------
      *
     A          R AU_DTL
     A                                      SPACEB(001)
     A                                      SPACEA(003)
     A                                     7
     A                                      'RUN CONTROLS'
     A                                      SPACEB(004)
     A                                    60
     A                                      'NUMBER'
     A                                   +23
     A                                      'VALUE'
     A                                     7
     A                                      'EVNTXZZ'
     A                                      SPACEB(002)
     A                                    12
     A                                      'BEFORE EXTRACT'
     A                                      SPACEB(002)
     A            AU_NRBE        5S 0O    60
     A            AU_HTRBWNE    15S 0O   +14
     A            AU_HTRBDCE     3S 0O    +1
     A                                    12
     A                                      'EXTRACTED'
     A                                      SPACEB(002)
     A            AU_NREE        5S 0O    60
     A            AU_HTREWNE    15S 0O   +14
     A            AU_HTREDCE     3S 0O    +1
     A                                    12
     A                                      'OUTPUT TO PF/EVNTXZZ'
     A                                      SPACEB(002)
     A            AU_NRTE        5S 0O    60
     A            AU_HTRTWNE    15S 0O   +14
     A            AU_HTRTDCE     3S 0O    +1
     A                                     7
     A                                      'FPVNTZZ'
     A                                      SPACEB(002)
     A                                    12
     A                                      'BEFORE EXTRACT'
     A                                      SPACEB(002)
     A            AU_NRBF        5S 0O    60
     A            AU_HTRBWNF    15S 0O   +14
     A            AU_HTRBDCF     3S 0O    +1
     A                                    12
     A                                      'EXTRACTED'
     A                                      SPACEB(002)
     A            AU_NREF        5S 0O    60
     A            AU_HTREWNF    15S 0O   +14
     A            AU_HTREDCF     3S 0O    +1
     A                                    12
     A                                      'OUTPUT TO PF/FPVNTZZ'
     A                                      SPACEB(002)
     A            AU_NRTF        5S 0O    60
     A            AU_HTRTWNF    15S 0O   +14
     A            AU_HTRTDCF     3S 0O    +1
      *
      *----------------
      * NO DETAILS    *
      *----------------
      *
     A          R AU_NODTL
     A                                      SPACEB(001)
     A                                      SPACEA(009)
     A                                    53
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *----------------
      * DB ERROR      *
      *----------------
      *
     A          R AU_DBERR
     A                                      SPACEB(001)
     A                                      SPACEA(003)
     A                                    53
     A                                      '***** PRINT TERMINATED *****'
     A                                      SPACEB(004)
     A                                    53
     A                                      'DATABASE ERROR'
     A                                      SPACEB(002)
     A            DBASE          3S 0O    69
     A                                    82
     A                                      'FILE'
     A            DBFILE         8A  O    88
     A                                    53
     A                                      'KEY'
     A                                      SPACEB(001)
     A            DBKEY         29A  O    57
      *
      *----------------
      * END OF REPORT *
      *----------------
      *
     A          R AU_EORPT
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    54
     A                                      '***** END OF REPORT *****'
     A                                      SPACEB(003)
