      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL Balances Extract Report - audit')             *
      *****************************************************************
      *                                                               *
      *  MIDAS - General Ledger                                       *
      *                                                               *
      *  GL000570AU - Balances Extract Report - audit                 *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL127AP *CREATE   Date 06Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL127AP - COB Restructure - GL COB Optimisation Phase 1     *
      *                                                               *
      *****************************************************************
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2012')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A            RREFNAME      10A       16
      *
     A            RURPT         53A       37
     A                                   101'DATE'
     A            RMRDT          7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A          R CONTROL
     A                                    42'ACCOUNT DETAILS EXTRACT'
     A                                    68'RUN CONTROLS'
     A                                      SPACEA(1)
     A                                    42'-----------------------'
     A                                      SPACEA(3)
      *
     A                                      SKIPB(15)
      *
     A  41                                10'NUMBER OF RETAIL +
     A                                       ACCOUNT RECORDS READ'
     A  41        RRECCOUNT     10  0     56EDTCDE(3)
     A  41                                  SPACEA(2)
      *
     A                                    10'NUMBER OF RECORDS WRITTEN +
     A                                       TO OUTPUT FILES'
     A                                      SPACEA(2)
      *
     A  42                                30'INACTIVE/BLOCKED A/Cs +
     A                                       FILE'
     A  42                                69' - IBPA'
     A  42        RIBPACTR      10  0     86EDTCDE(1)
     A  42                                  SPACEA(2)
      *
     A  43                                30'A/Cs TO BE TRANSFERRED +
     A                                       TO INACTIVE FILE'
     A  43                                69' - AXFI'
     A  43        RAXFICTR      10  0     86EDTCDE(1)
     A  43                                  SPACEA(2)
      *
     A  44                                30'RETAIL A/C BALANCES FILE'
     A  44                                69' - RETAC'
     A  44        RRETACCTR     10  0     86EDTCDE(1)
     A  44                                  SPACEA(2)
      *
     A          R DBERROR
     A                                    20'DATABASE ERROR NO.-'
     A            RDBASE         3  0     39
     A                                    44'FILE-'
     A            RDBFILE       10A       49
     A                                    62'KEY-'
     A            RDBKEY        29A       66
     A                                      SPACEA(3)
