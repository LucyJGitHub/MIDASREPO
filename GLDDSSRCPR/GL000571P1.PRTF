      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL ATU Tranfer Criteria List')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000571P1 - Midas GL Account Transfer Utility Tranfer  *
      *                    Criteria Audit                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2010')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE GL000571P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'Date'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'Page'
     A                                   127
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(002)
      *
     A                                    39
     A                                      'Account Transfer Utility - Trans+
     A                                      fer Criteria List'
     A                                      SPACEA(001)
      *
     A                                    39
     A                                      '--------------------------------+
     A                                      -----------------'
     A                                      SPACEA(002)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     1'Reference'
     A                                    12'Description'
     A                                    29'Transfer Cr:'
     A                                    42'Branch'
     A                                    49'Customer Range'
     A                                    65'Ccy'
     A                                    69'Account Code Range'
     A                                    92'Seq'
     A                                    96'Retail Account # Range'
     A                                   119'Ofr'
     A                                   123'Type'
     A                                   128'Dom'
      *
     A                                      SPACEA(001)
      *
     A                                    12'Status'
     A                                    29'Transfer To:'
     A                                    42'Branch'
     A                                    49'Customer'
     A                                    69'Account Code'
     A                                    92'Seq'
      *
     A                                      SPACEA(001)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A
     A  04                                  SPACEB(001)
     A            P1RFNO        10         1TEXT('REFERENCE NO.')
     A            P1DESC        30        12TEXT('DESCRIPTION')
     A            P1BRCA         3        43TEXT('BRANCH')
     A            P1CNU1         6        49TEXT('CUSTOMER 1')
     A  54                                56'-'
     A            P1CNU2         6        58TEXT('CUSTOMER 2')
     A            P1CCY          3        65TEXT('CURRENCY CODE')
     A            P1ACO1        10        69TEXT('ACCOUNT CODE 1')
     A  55                                80'-'
     A            P1ACO2        10        82TEXT('ACCOUNT CODE 2')
     A            P1ACSQ         2        93TEXT('ACCOUNT SEQUENCE')
     A            P1ACN1        10        96TEXT('RETAIL ACCOUNT # 1')
     A  56                               107'-'
     A            P1ACN2        10       109TEXT('RETAIL ACCOUNT # 2')
     A            P1ACOC         2       120TEXT('ACCOUNT OFFICER')
     A            P1ATYP         1       125TEXT('ACCOUNT TYPE')
     A            P1COLC         2       129TEXT('DOMICILE CODE')
     A                                      SPACEA(001)
     A            P1STAT         2        12TEXT('STATUS')
     A            P1TBRC         3        43TEXT('TO BRANCH')
     A            P1TCNU         6        49TEXT('TO CUSTOMER')
     A            P1TACO        10        69TEXT('TO ACCOUNT CODE')
     A            P1TACS         2        93TEXT('TO ACCOUNT SEQUENCE')
     A                                    96'Create:'
     A            P1CRTD         9       104TEXT('CREATION DATE')
     A                                   114'Process:'
     A            P1PRCD         9       123TEXT('PROCESS DATE')
     A                                      SPACEA(001)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
