      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Account Transfer-Accnts to be Transferred')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000572AU - Midas GL Account Transfer - Accounts to be *
      *                    Transferred Audit                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADAU
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2010')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(003)
     A                                     6'REFERENCE GL000572AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    43'ACCOUNTS TO BE TRANSFERRED +
     A                                      REPORT - AUDIT'
     A                                      SPACEB(003)
      *
     A                                    43'---------------------------+
     A                                      --------------'
     A                                      SPACEB(001)
      *
      *---------------*
      ** File Control *
      *---------------*
      *
     A          R CONTROL
     A                                      SPACEA(001)
      *
     A                                    87'Calculated'
     A                                      SPACEA(001)
      *
     A                                    87'----------'
     A                                      SPACEA(001)
      *
     A                                    21'Total no. of records +
     A                                      processed - LF/GLSLACL8'
      *
     A            RCOUNT         9  0     88EDTCDE(3)
      *
      *-------------*
      ** No Details *
      *-------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(002)
     A                                    11'**********************************-
     A                                      ********************'
     A                                      SPACEB(002)
     A                                    11'*'
     A                                      SPACEB(001)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(001)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(001)
     A            DBKEY         29        30
     A                                    59'''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(001)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(001)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(001)
     A                                    64'*'
     A                                    11'**********************************-
     A                                      ********************'
     A                                      SPACEB(001)
      *
