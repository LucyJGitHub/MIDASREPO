      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Account Transfer-Accnts to be Transferred')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000572P1 - Midas GL Account Transfer - Accounts to be *
      *                    Transferred Report                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2010')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL000572P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    49'ACCOUNTS TO BE +
     A                                      TRANSFERRED REPORT'
      *
     A                                      SPACEA(1)
     A                                    49'---------------+
     A                                      ------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'Reference'
     A                                    19'GL Account'
     A                                    50'Retail A/c'
     A                                    63'Account'
     A                                    73'Account'
     A                                    83'Domicile'
     A                                    94'Creation'
     A                                   106'Transfer To'
     A                                      SPACEA(1)
      *
     A                                    63'Officer'
     A                                    73'Type'
     A                                    83'Code'
     A                                    94'Date'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            R1RFNO        10A  O     6
     A            R1FBRC         3A  O    19
     A            R1FCNU         6A  O    23
     A            R1CCY          3A  O    30
     A            R1FACO        10A  O    34
     A            R1FACS         2A  O    45
     A            R1ACNO        10A  O    50
     A            R1ACOC         2A  O    65
     A            R1ATYP         1A  O    75
     A            R1COLC         2A  O    85
     A            R1CRTD         9A  O    94
     A            R1TBRC         3A  O   106
     A            R1TCNU         6A  O   110
     A            R1TACO        10A  O   117
     A            R1TACS         2A  O   128
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
