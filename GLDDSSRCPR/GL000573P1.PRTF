      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Account Transfer - Transfer Audit List')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000573P1 - Midas GL Account Transfer - Transfer Audit *
      *                    List                                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2010')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE GL000573P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'Date'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'Page'
     A                                   127
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(002)
      *
     A                                    47
     A                                      'ACCOUNT TRANSFER UTILITY AUDIT RE+
     A                                      PORT'
     A                                      SPACEA(001)
      *
     A                                    47
     A                                      '--------------------------------+
     A                                      ----'
     A                                      SPACEA(002)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'Reference No.:'
     A            H1RFNO        10A       21TEXT('REFERENCE NUMBER')
     A                                    39'Authorised By:'
     A            H1USER        10A       54TEXT('USER ID')
     A                                    72'Date of Transfer:'
     A            H1PRCD         9A       90TEXT('DATE PROCESSED')
      *
     A                                      SPACEA(001)
      *
     A                                     6'------------ From ------+
     A                                       ------'
     A                                    39'------------- To ------+
     A                                       -------'
      *
     A                                      SPACEA(001)
      *
     A                                     6'Branch'
     A                                    13'Customer'
     A                                    22'Account'
     A                                    33'Seq'
     A                                    39'Branch'
     A                                    46'Customer'
     A                                    55'Account'
     A                                    66'Seq'
     A                                    72'File Name'
     A                                    85'Record Id'
     A                                      SPACEA(001)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A
     A  04                                  SPACEB(001)
     A            R1FBRC         3A        6TEXT('FROM BRANCH CODE')
     A            R1FCNU         6A       13TEXT('FROM CUSTOMER NUMBER')
     A            R1FACO        10A       22TEXT('FROM ACCOUNT CODE')
     A            R1FACS         2A       33TEXT('FROM ACCOUNT SEQUENCE')
     A            R1TBRC         3A       39TEXT('TO BRANCH CODE')
     A            R1TCNU         6A       46TEXT('TO CUSTOMER NUMBER')
     A            R1TACO        10A       55TEXT('TO ACCOUNT CODE')
     A            R1TACS         2A       66TEXT('TO ACCOUNT SEQUENCE')
     A            R1FILE        10A       72TEXT('FILE NAME')
     A            R1TEXT        50A       85TEXT('RECORD ID')
     A                                      SPACEA(001)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
