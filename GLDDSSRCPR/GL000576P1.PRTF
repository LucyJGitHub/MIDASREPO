      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL ATU Batch Update Audit Housekeep Report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000576P1 - Midas GL ATU Batch Update Audit            *
      *                    Housekeeping Report.                       *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL000576P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    43'ATU - BATCH UPDATE AUDIT +
     A                                      PURGED TODAY REPORT'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    43'-------------------------+
     A                                      -------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'Reference'
     A                                    20'File Name'
     A                                    33'Logical File Name'
     A                                    52'F/Brc'
     A                                    60'F/Cust'
     A                                    69'F/AC Code'
     A                                    81'F/AC Seq'
     A                                    92'T/Brc'
     A                                    99'T/Cust'
     A                                   108'T/AC Code'
     A                                   120'T/AC Seq'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RRFNO         10A  O     6
     A            RFILE         10A  O    20
     A            RLFIL         10A  O    36
     A            RFBRC          3A  O    53
     A            RFCNU          6A  O    60
     A            RFACO         10A  O    69
     A            RFACS          2A  O    84
     A            RTBRC          3A  O    93
     A            RTCNU          6A  O    99
     A            RTACO         10A  O   108
     A            RTACS          2A  O   123
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
