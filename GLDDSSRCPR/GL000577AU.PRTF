      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL ATU Selection Criteria File HousekepAudit')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000577AU - Midas GL ATU Selection Criteria File       *
      *                    Housekeeping Report - Audit                *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADAU
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2010')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(003)
     A                                     6'REFERENCE GL000577AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    38'ATU - SELECTION CRITERIA +
     A                                      PURGED TODAY REPORT - AUDIT'
     A                                      SPACEB(003)
      *
     A                                    38'-------------------------+
     A                                      ---------------------------'
     A                                      SPACEB(001)
      *
      *---------------*
      ** File Control *
      *---------------*
      *
     A          R CONTROL
     A                                      SPACEA(001)
      *
     A                                     6'TOTAL NUMBER OF RECORDS PURGED'
      *
     A            RCOUNT         9  0     60EDTCDE(3)
      *
      *-------------*
      ** No Details *
      *-------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(002)
     A                                    11'**********************************-
     A                                      ********************'
     A                                      SPACEB(002)
     A                                    11'*'
     A                                      SPACEB(001)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(001)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(001)
     A            DBKEY         29        30
     A                                    59'''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(001)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(001)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(001)
     A                                    64'*'
     A                                    11'**********************************-
     A                                      ********************'
     A                                      SPACEB(001)
      *
      *-----------------*
      ** End of Report  *
      *-----------------*
      *
     A          R ENDRPT
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(002)
