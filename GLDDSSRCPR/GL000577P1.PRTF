      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL ATU Selection Criteria File housekeep Rpt')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000577P1 - Midas GL ATU Selection Criteria File       *
      *                    Housekeeping Report.                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2010')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL000577P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    43'ATU - SELECTION CRITERIA +
     A                                      PURGED TODAY REPORT'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    43'-------------------------+
     A                                      -------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     1'Reference'
     A                                    12'Description'
     A                                    29'Transfer Cr:'
     A                                    42'Branch'
     A                                    49'Customer Range'
     A                                    65'Ccy'
     A                                    69'Account Code Range'
     A                                    92'Seq'
     A                                    96'Retail Account# Range'
     A                                   119'Ofr'
     A                                   123'Type'
     A                                   128'Dom'
     A                                      SPACEA(1)
      *
     A                                    12'Status'
     A                                    29'Transfer To:'
     A                                    42'Branch'
     A                                    49'Customer'
     A                                    69'Account Code'
     A                                    92'Seq'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RRFNO         10A  O     1
     A            RDESC         30A  O    12
     A            RBRCA          3A  O    43
     A            RCNU1          6A  O    49
     A                                    56'-'
     A            RCNU2          6A  O    58
     A            RCCY           3A  O    65
     A            RACO1         10A  O    69
     A                                    80'-'
     A            RACO2         10A  O    82
     A            RACSQ          2A  O    93
     A            RACN1         10A  O    96
     A                                   107'-'
     A            RACN2         10A  O   109
     A            RACOC          2A  O   120
     A            RATYP          1A  O   125
     A            RCOLC          2A  O   129
     A                                      SPACEA(1)
     A            RSTAT         10A  O    12
     A            RTBRC          3A  O    43
     A            RTCNU          6A  O    49
     A            RTACO         10A  O    69
     A            RTACS          2A  O    93
     A                                    96'Create:'
     A            RCRTD          9A  O   104
     A                                   114'Process:'
     A            RPRCD          9A  O   123
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
