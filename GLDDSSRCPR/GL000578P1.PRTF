      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL ATU Selected Accounts File Housekeep Rpt')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000578P1 - Midas GL ATU Selected Accounts File        *
      *                    Housekeeping Report.                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2010')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL000578P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    41'ATU - SELECTED ACCOUNTS +
     A                                      PURGED TODAY REPORT'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    41'------------------------+
     A                                      -------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'Reference'
     A                                    17'Description'
     A                                    50'Currency'
     A                                    59'Retail A/C#'
     A                                    71'Officer'
     A                                    79'Type'
     A                                    84'Domicile'
     A                                    94'From:'
     A                                   100'Brc'
     A                                   105'Cust'
     A                                   112'AC Code -'
     A                                   122'Sequence'
     A                                      SPACEA(1)
      *
     A                                    17'Status'
     A                                    24'Create Date'
     A                                    36'Process Date'
     A                                    94'To  :'
     A                                   100'Brc'
     A                                   105'Cust'
     A                                   112'AC Code -'
     A                                   122'Sequence'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RRFNO         10A  O     6
     A            RDESC         30A  O    17
     A            RCCY           3A  O    52
     A            RACNO         10A  O    59
     A            RACOC          2A  O    73
     A            RATYP          1A  O    81
     A            RCOLC          2A  O    87
     A            RFBRC          3A  O   100
     A            RFCNU          6A  O   105
     A            RFACO         10A  O   113
     A            RFACS          2A  O   126
     A                                      SPACEA(1)
      *
     A            RSTAT          2A  O    19
     A            RCRTD          9A  O    25
     A            RPRCD          9A  O    37
     A            RTBRC          3A  O   100
     A            RTCNU          6A  O   105
     A            RTACO         10A  O   113
     A            RTACS          2A  O   126
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
