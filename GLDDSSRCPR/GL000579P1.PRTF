      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL ATU Authorisation Log File Housekeep Rpt')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000579P1 - Midas GL ATU Authorisation Log File        *
      *                    Housekeeping Report.                       *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL000579P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    43'ATU - AUTHORISATION LOG +
     A                                      PURGED TODAY REPORT'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    43'------------------------+
     A                                      -------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'Creation Date'
     A                                    21'Narrative'
     A                                    88'Job No.'
     A                                    97'Job Name'
     A                                   107'Authorising User'
     A                                   126'Time'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RCRTD          9A  O     6
     A            RNARR         65A  O    21
     A            RJOBN          6A  O    88
     A            RJOB          10A  O    96
     A            RUSER         10A  O   109
     A            RTIME          4S  O   126
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
