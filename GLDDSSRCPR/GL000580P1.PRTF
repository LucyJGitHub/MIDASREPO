      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL ATU Accounts for Demand Interest Capitali')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000580P1 - Midas GL ATU Accounts for Demand Interest  *
      *                    Capitalisation Audit List                  *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CGL114A *CREATE    Date 12Mar10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL114A - CCR Interest Capitalisation                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2010')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE GL000580P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   127
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(002)
      *
     A                                    38
     A                                      'ACCOUNTS FOR DEMAND INTEREST CAPIT+
     A                                      ALISATION REPORT'
     A                                      SPACEA(001)
      *
     A                                    38
     A                                      '--------------------------------+
     A                                      ------------------'
     A                                      SPACEA(002)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'Reference'
     A                                    19'Account'
     A                                    50'Retail A/c'
     A                                    63'Account'
     A                                    73'Account'
     A                                    83'Domicile'
     A                                    94'Creation'
     A                                   106'Transfer To'
     A                                      SPACEA(001)
     A                                    63'Officer'
     A                                    73'Type'
     A                                    83'Code'
     A                                    94'Date'
     A                                      SPACEA(001)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A
     A            R1RFNO        10A        6TEXT('Reference')
     A            R1FBRC         3A       19TEXT('From Branch Code')
     A            R1FCNU         6A       23TEXT('From Customer No')
     A            R1CCY          3A       30TEXT('Currency')
     A            R1FACO        10A       34TEXT('From A/c Code')
     A            R1FACS         2A       45TEXT('From A/c Seq')
     A            R1ACNO        10A       50TEXT('Retail A/c')
     A            R1ACOC         2A       65TEXT('Account Officer')
     A            R1ATYP         1A       75TEXT('Account Type')
     A            R1COLC         2A       85TEXT('Domicile Code')
     A            R1CRTD         9A       94TEXT('Creation Date')
     A            R1TBRC         3A      106TEXT('To Branch Code')
     A            R1TCNU         6A      110TEXT('To Customer No')
     A            R1TACO        10A      117TEXT('To A/c Code')
     A            R1TACS         2A      128TEXT('To A/c Seq')
     A                                      SPACEA(001)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
