      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL FATCA High Value Review Report Audit')        *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL000622P1 - Midas GL FATCA High Value Account Review        *
      *               Report                                          *
      *                                                               *
      *  Last Amend No. CGL154  *CREATE    Date 13Oct14               *
      *                                                               *
      *  -------------------------------------------------------------*
      *                                                               *
      *  CGL154 - FATCA Reporting                                     *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      INDARA
      *
      ** Format for Whole Report
      *
     A          R CONTROL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2014')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE GL000622AU'
     A            RRTITL        53        36TEXT('TITLE')
     A                                   100'DATE'
     A            RRRUNA         7       105TEXT('RUNDATE')
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      SPACEA(1)
     A                                      EDTWRD('0 :  :  ')
      *
     A                                    46'FATCA YEAR END HIGH VALUE +
     A                                       REVIEW - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    46'--------------------------+
     A                                       --------------'
     A                                      SPACEA(3)
      *
     A                                    62'CALCULATED'
     A                                      SPACEA(1)
     A                                    62'----------'
     A                                      SPACEA(2)
      *
     A                                     6'FILE - FATCA Customer and +
     A                                      Non account holder table'
     A                                     6'--------------------------+
     A                                      ------------------------'
     A                                      SPACEB(1)
     A                                      SPACEA(3)
     A                                    10'NO. OF CUSTOMERS UPDATED'
     A                                    46':'
     A            RRCUST         4  0     68TEXT('CALC NO. OF RECORDS')
     A                                      EDTWRD('  0 ')
     A                                      SPACEA(1)
     A                                    10'NO. OF NON-ACCOUNT HOLDERS +
     A                                      UPDATED'
     A                                    46':'
     A            RRNAHO         4  0     68TEXT('CALC NO. OF RECORDS')
     A                                      EDTWRD('  0 ')
      *
     A          R TRAILER
      *
     A                                    48'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      ** Database Error Message
      *
     A          R DBERROR                   TEXT('DATABASE ERROR MSG')
     A                                      SPACEB(3)
     A                                    48 '***   PRINT TERMINATED   ***'
     A                                      SPACEA(2)
     A                                    45 'DATABASE ERROR'
     A            DBASE          3  0     60
     A                                    65 'FILE'
     A            DBFILE         8        70
     A                                      SPACEA(1)
     A                                    45 'KEY'
     A            DBKEY         29        49
      *
