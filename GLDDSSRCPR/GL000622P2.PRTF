      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL CRS High Value Review Report Detail')         *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL000622P2 - Midas GL FATCA High Value Account Review        *
      *               Report                                          *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
     A          R GL000622R3                TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited 2016')
      *
     A                                  1  6'REFERENCE GL000622P2'
     A            RRTITL        53      1 36TEXT('Title')
     A                                  1100'DATE'
     A            RRRUNA         7      1105TEXT('Date')
     A                                  1118'PAGE'
     A                                  1123PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  2100'TIME'
     A                                  2105TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A                                  3 41'CRS YEAR END HIGH VALUE +
     A                                       REVIEW REPORT'
     A                                  4 41'------------------------+
     A                                       -------------'
      *
     A                                  5  1'High Value Threshold (Individual):'
     A            X2HIWI        18A     5 36TEXT('High Value Threshold')
     A                                  6  1'High Value Threshold (Entity)    :'
     A            X2HIWE        18A     6 36TEXT('High Value Threshold')
     A                                  7  1'Low Value Threshold              :'
     A            X2LOGT        18A     7 36TEXT('Low Value Threshold')
     A                                  8  1'High Value Currency              :'
     A            X2HVCY         3A     8 36TEXT('High Value Currency')
     A                                 10  2'Cust No/Non'
     A                                 10 48'Report Name'
     A                                 10 76'Report'
     A                                 10 95'CRS'
     A                                 10112'High Value'
     A                                 11  3'A/C Holder'
     A                                 11 77'Town'
     A                                 11 91'Type'
     A                                 11 97'SubType'
     A                                 11110'Ind. Calculated'
     A                                 10 24'Shortname'
      *
      ** Record format for Details
     A          R GL000622R4
      *
     A            X2CUST        10A        5TEXT('Customer Number/Non +
     A                                            Account Holder')
     A                                      SPACEB(1)
     A            X2CSSN        10A       22TEXT('Customer Shortname')
     A            X2RPNM        20A       43TEXT('Report Name')
     A            X2RPTN        10A       74TEXT('Report Town')
     A            X2CUTP         1A       92TEXT('CRS Customer Type')
     A            X2CSTP         2A       98TEXT('CRS Customer Sub Type')
     A            X2HVIN         1A      108TEXT('High Value Indicator')
     A            X2HVAM        18A      110TEXT('High Value Calculated')
      *
     A          R GL000622R5
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(1)
      *
