      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL Report Management - Audit')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  GL000662AU - Midas GL Reports Management - Audit             *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL154  *CREATE    Date 13Oct14               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL154 - FATCA Reporting                                     *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      INDARA
      *
      ** Format for Whole Report
      *
     A          R CONTROL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2014')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE GL000662AU'
     A            RRTITL        53        36TEXT('TITLE')
     A                                   100'DATE'
     A            RRRUNA         7       105TEXT('RUNDATE')
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      SPACEA(1)
     A                                      EDTWRD('0 :  :  ')
      *
     A                                    29'Customers/Non-Account +
     A                                      Holders under Exception +
     A                                      Management - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    29'----------------------------------'
     A                                    63'------------------------------'
     A                                      SPACEA(3)
      *
     A                                     6'FILE - FATCA Customer and +
     A                                      Non account holder table'
     A                                    62'CALCULATED'
     A                                      SPACEA(1)
     A                                     6'--------------------------+
     A                                      ------------------------'
     A                                    62'----------'
     A                                      SPACEA(3)
     A                                    10'NO. OF CUSTOMERS REPORTED'
     A                                    46':'
     A            RRTCUS         4  0     68TEXT('CALC NO. OF RECORDS')
     A                                      EDTWRD('  0 ')
     A                                      SPACEA(1)
     A                                    10'NO. OF NON-ACCOUNT HOLDERS +
     A                                      REPORTED'
     A                                    46':'
     A            RRTNAH         4  0     68TEXT('CALC NO. OF RECORDS')
     A                                      EDTWRD('  0 ')
     A                                      SPACEA(1)
      *
     A          R TRAILER
      *
     A N54N80                             48'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      ** Database Error Message
      *
     A          R DBERROR                   TEXT('DATABASE ERROR MSG')
     A                                      SPACEB(3)
     A                                    48 '***   PRINT TERMINATED   ***'
     A                                      SPACEA(2)
     A                                    45 'DATABASE ERROR'
     A            DBASE          3  0     60
     A                                    65 'FILE'
     A            DBFILE         8        70
     A                                      SPACEA(1)
     A                                    45 'KEY'
     A            DBKEY         29        49
      *
