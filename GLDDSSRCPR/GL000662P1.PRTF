      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Report Management - Print')                   *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL000662P1 - Midas GL Reports Management - Print             *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD043131           Date 14Dec16               *
      *                 CGL154  *CREATE    Date 13Oct14               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD043131 - Incorrect Document Type being displayed in the    *
      *             printed report                                    *
      *  CGL154 - FATCA Reporting                                     *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Record Format
     A          R GL000661R5                TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2014')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE GL000662P1'
     A            RRTITL        53A       36TEXT('Title')
     A                                   100'DATE'
     A            RRRUNA         7A      105TEXT('Date')
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
      *
     A                                    50'FATCA Reports +
     A                                       Management'
     A                                      SPACEA(1)
     A                                    50'--------------+
     A                                       ----------'
     A                                      SPACEA(2)
      *
     A                                     2'Document Type:'
     A            RDOCTP         7A       17TEXT('Document Type')
     A                                    29'Report Type:'
     A            RDRTYP        18A       42TEXT('Report Type')
     A                                    65'Year:'
     A            RDRYER         4A       71TEXT('Year')
     A                                      SPACEA(2)
      *
     A                                     2'List of Customers/Non-Account +
     A                                      Holders under Exception +
     A                                      Management'
     A                                      SPACEA(1)
     A                                     2'----------------------------------'
     A                                    36'------------------------------'
     A                                      SPACEA(2)
      *
     A                                     2'Cust/NAH'
     A                                    16'Report Name'
     A                                    40'Report Town'
     A                                    55'Exception List'
     A                                      SPACEA(1)
      *
     A                                     2'----------'
     A                                    16'--------------------'
     A                                    40'----------'
     A                                    55'----------------------------------'
     A                                    89'------------------------------'
     A                                      SPACEA(1)
      *
      ** Record format for Details
     A          R GL000661D5
      *
     A            RRCSNH        10A        2TEXT('Customer/Non-Account Hldr')
     A            RRRPNM        20A       16TEXT('Report Name')
     A            RRRPTN        10A       40TEXT('Report Town')
     A            RREXLT        64A       55TEXT('Exception List')
     A                                      SPACEA(1)
      *
      ** Record format for no records
     A          R GL000661N5
      *
     A                                    52'*** END OF REPORT ***'
     A                                      SPACEB(2)
      ** Header format for Pool report
     A          R GL000662R0                TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2014')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE GL000662P1'
     A            RRTITL        53A       36TEXT('Title')
     A                                   100'DATE'
     A            RRRUNA         7A      105TEXT('Date')
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
      *
     A                                    50'FATCA Reports +
     A                                       Management'
     A                                      SPACEA(1)
     A                                    50'--------------+
     A                                       ----------'
     A                                      SPACEA(2)
      *
     A                                     2'Document Type:'
     A            RDOCTP         7A       17TEXT('Document Type')
     A                                    29'Report Type:'
     A            RDRTYP        18A       42TEXT('Report Type')
     A                                    65'Year:'
     A            RDRYER         4A       71TEXT('Year')
     A                                      SPACEA(2)
     A                                     2'Reporting Group/Pool Report'
     A                                       SPACEA(1)
     A                                     2'---------------------------'
     A                                       SPACEA(2)
     A                                     2'Document Ref. ID:'
     A            RRDRFID       35A       20 TEXT('Document Ref ID')
     A                                    60'Corrected Ref. ID:'
     A            RRCRFID       35A       79 TEXT('Corrected Ref. ID')
     A                                      SPACEA(1)
     A                                     2'Account Pool Report Type:'
     A            RACPRPT        8A       28 TEXT('Account Pool Report Type')
     A                                    39'Account Count:'
     A            RRCNCNT       18A       54 TEXT('Account Count')
     A                                    75'Pool Balance:'
     A            RRCNBAL       22A       89 TEXT('Pool Balance')
     A                                   114'Currency:'
     A            RRCNCCY        3A      124 TEXT('Currency')
     A                                      SPACEA(3)
     A                                     2'Cust/Nah'
     A                                    22'Report Name'
     A                                    52'Report Town'
     A                                    73'Total Balance'
     A                                      SPACEA(1)
     A                                     2'----------'
     A                                    22'--------------------'
     A                                    52'-----------'
     A                                    73'----------------------'
     A                                      SPACEA(2)
      ** Record format for Details
      **Pool Report Cust/Nah details format
     A          R GL000662D0
      *
     A            RCSNAH        10A        2TEXT('Customer/Non-Account Hldr')
     A            RCNRPN        20A       22TEXT('Report Name')
     A            RRPTTW        10A       52TEXT('Report Town')
     A            RTOTBL        22A       73TEXT('Total Balance')
     A                                      SPACEA(1)
 
      ** Account Report Page Header Record Format
     A          R GL000663R0                TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2014')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE GL000662P1'
     A            RRTITL        53        36TEXT('Title')
     A                                   100'DATE'
     A            RRRUNA         7       105TEXT('Date')
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
      *
     A                                    50'FATCA Reports Management'
     A                                      SPACEA(1)
     A                                    50'------------------------'
     A                                      SPACEA(2)
      *
     A                                     2'Document Type:'
     A            RRDTIN         7A       17 TEXT('Document Type')
     A                                    29'Report Type:'
     A            RRRTYP        18A       42 TEXT('Reporting Type')
     A                                    65'Year:'
     A            RRRYER         4A       71 TEXT('Calendar Year')
     A                                      SPACEA(2)
     A                                     2'Reporting Group/Account Report'
     A                                      SPACEA(1)
     A                                     2'------------------------------'
     A                                      SPACEA(1)
 
      ** Account Report Header Column Format
     A          R GL000663R1
     A                                     2'Account Holder - Header Details---'
     A                                    36'----------------------------------'
     A                                    70'----------------------------------'
     A                                   104'------------------------'
     A                                      SPACEA(1)
     A                                     2'Document Ref. ID:'
     A            R1DRID        31A       20TEXT('Document Ref.ID')
     A                                    54'Corrected Ref.ID:'
     A            R1CRID        31A       72TEXT('Corrected Ref.ID')
     A                                      SPACEA(1)
     A                                     2'Account Number..:'
     A            R1ACNO        26A       20TEXT('Account Number')
     A                                    54'Balance.........:'
 
     A            R1ACBL        21A       72TEXT('Account Balance')
 
     A                                    95'Currency.............:'
     A            R1ABCC         3A      118TEXT('Currency ISO Code')
     A                                      SPACEA(1)
     A                                     2'Customer/NAH....:'
     A            R1CNNH        10A       20TEXT('Customer/NAH')
     A                                    54'Report Name.....:'
     A            R1RPTN        20A       72TEXT('Report Name')
     A                                    95'Account Holder Type..:'
     A            R1ACHT         8A      118TEXT('Account Holder Type')
     A                                      SPACEA(1)
 
      ** Account Report Detail Format
     A          R GL000663R2
      *
     A                                     2'Account Holder - Individual or Org'
     A                                    36'anisation Details'
     A                                    53'-----------------'
     A                                    70'----------------------------------'
     A                                   104'------------------------'
     A                                      SPACEA(1)
     A                                     2'First Name................:'
     A            R2FNME        70A       30TEXT('First Name')
     A                                      SPACEA(1)
     A                                     2'Last Name.................:'
     A            R2LNME        70A       30TEXT('Corrected Ref.ID')
     A                                      SPACEA(1)
     A                                     2'Address...................:'
     A            R2ADD1        60A       30TEXT('Address Line 1')
     A                                      SPACEA(1)
     A            R2ADD2        60A       30TEXT('Address Line 2')
     A                                      SPACEA(1)
 
     A                                     2'Residing Country Code.....:'
 
     A            R2RECO         2A       30TEXT('Residence Country Code')
     A                                    54'Country Code..............:'
     A            R2COCO         2A       82TEXT('Country Code')
     A                                    95'Birth Date...........:'
 
     A            R2BRTD        10A      118TEXT('Birth Date')
 
     A                                      SPACEA(1)
     A                                     2'Tax Identification Numbers:'
     A            R2COT1         2A       30TEXT('Country of TIN 1')
     A            R2TIN1        25A       34TEXT('Tax Identification No.1')
     A                                      SPACEA(1)
 
 
      ** Account Report Substantial Owner Details
     A          R GL000663R3
      *
     A                                     2'Account Holder - Substantial Owner'
     A                                    36' Details--------------------------'
     A                                    70'----------------------------------'
     A                                   104'------------------------'
     A                                      SPACEA(1)
     A                                     2'First Name................:'
     A            R3FNME        70A       30TEXT('First Name')
     A                                      SPACEA(1)
     A                                     2'Last Name.................:'
     A            R3LNME        70A       30TEXT('Corrected Ref.ID')
     A                                      SPACEA(1)
     A                                     2'Address...................:'
     A            R3ADD1        60A       30TEXT('Address Line 1')
     A                                      SPACEA(1)
     A            R3ADD2        60A       30TEXT('Address Line 2')
     A                                      SPACEA(1)
 
     A                                     2'Residing Country Code.....:'
 
     A            R3RECO         2A       30TEXT('Residence Country Code')
     A                                    54'Country Code..............:'
     A            R3COCO         2A       82TEXT('Country Code')
     A                                    95'Birth Date...........:'
 
     A            R3BRTD        10A      118TEXT('Birth Date')
 
     A                                      SPACEA(1)
     A                                     2'Tax Identification Numbers:'
     A            R3COT1         2A       30TEXT('Country of TIN 1')
 
     A            R3TIN1        25A       34TEXT('Tax Identification No.1')
 
     A                                      SPACEA(1)
 
      ** Account Report Account Balance Payment Header
     A          R GL000663R4
      *
     A                                     2'Account Holder - Account Balance /'
     A                                    36' Payment Details------------------'
     A                                    70'----------------------------------'
     A                                   104'------------------------'
     A                                      SPACEA(1)
 
      ** Account Report Account Balance Payment Header
     A          R GL000663R5
      *
     A                                     2'FATCA Payment Type:'
     A            R4PAYT         8A       25TEXT('FATCA Payment Type')
     A                                    37'Currency:'
     A            R4PACC         3A       47TEXT('Currency')
     A                                    59'Payment Amount:'
     A            R4PSTA        15A       75TEXT('Payment Amount')
     A                                      SPACEA(1)
 
      ** Message Header and Reporting FI Main Header
     A          R GL000663R6                TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2014')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE GL000662P1'
     A            RRTITL        53        36TEXT('Title')
     A                                   100'DATE'
     A            RRRUNA         7       105TEXT('Date')
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
      *
     A                                    50'FATCA Reports Management'
     A                                      SPACEA(1)
     A                                    50'------------------------'
     A                                      SPACEA(2)
      *
     A                                     2'Document Type:'
      ** Start of change MD043131
     A**********  RRDTIN         7A       17 TEXT('Document Type')                          MD043131
     A            RDOCTP         7A       17 TEXT('Document Type')
      ** End of change MD043131
     A                                    29'Report Type:'
     A            RRRTYP        18A       42 TEXT('Reporting Type')
     A                                    65'Year:'
     A            RRRYER         4A       71 TEXT('Calendar Year')
     A                                      SPACEA(2)
     A                                     2'Report - Message Header and Report'
     A                                    36'ing Financial Institution'
     A                                      SPACEA(1)
     A                                     2'FATCA Schema Version......:'
     A            RROECD         5A       30TEXT('FATCA_OECD')
     A                                      SPACEA(1)
 
      ** Message Header and Reporting FI Details
     A          R GL000663R7
      *
     A                                     2'Message Header Details------------'
     A                                    36'----------------------------------'
     A                                    70'----------------------------------'
     A                                   104'------------------------'
     A                                      SPACEA(1)
     A                                     2'Sending Company IN........:'
     A            R7SNDC        25A       30TEXT('Sending Company IN')
     A                                      SPACEA(1)
     A                                     2'Transmitting Country......:'
     A            R7TRNC         2A       30TEXT('Transmitting Country')
     A                                      SPACEA(1)
     A                                     2'Receiving Country.........:'
     A            R7RCVC         2A       30TEXT('Receiving Country')
     A                                      SPACEA(1)
     A                                     2'Message Type..............:'
     A            R7MSGT        10A       30TEXT('Message Type')
     A                                      SPACEA(1)
     A                                     2'Warning...................:'
     A            R7WARN        50A       30TEXT('Warning')
     A                                      SPACEA(1)
     A                                     2'Message Reference ID......:'
     A            R7MRID        30A       30TEXT('Message Reference ID')
     A                                      SPACEA(1)
     A                                     2'Corrected Message Ref. ID.:'
     A            R7CMID        30A       30TEXT('Corrected Msg Ref. ID')
     A                                      SPACEA(1)
     A                                     2'Reporting Period..........:'
     A            R7RPTP        10A       30TEXT('Reporting Period')
     A                                      SPACEA(1)
     A                                     2'Timestamp.................:'
     A            R7TSMP        26A       30TEXT('Timestamp')
     A                                      SPACEA(2)
     A                                     2'Reporting - Financial Institution '
     A                                    36'Details---------------------------'
     A                                    70'----------------------------------'
     A                                   104'------------------------'
     A                                      SPACEA(1)
     A                                     2'Tax Identification Number.:'
     A            R7TINO        25A       30TEXT('Tax Identification No.')
     A                                      SPACEA(1)
     A                                     2'TIN (Issued By)...........:'
     A            R7TNIB        35A       30TEXT('TIN (Issued By)')
     A                                      SPACEA(1)
     A                                     2'Residence Country Code....:'
     A            R7RESC         2A       30TEXT('Residence Country Code')
     A                                      SPACEA(1)
     A                                     2'Customer Name.............:'
     A            R7NAME        70A       30TEXT('Customer Name')
     A                                      SPACEA(1)
     A                                     2'Country Code..............:'
     A            R7CTRY         2A       30TEXT('Country Code')
     A                                      SPACEA(1)
     A                                     2'Address...................:'
     A            R7ADD1        60A       30TEXT('Address 1')
     A                                      SPACEA(1)
     A            R7ADD2        60A       30TEXT('Address 2')
     A                                      SPACEA(1)
     A                                     2'Document Reference ID.....:'
     A            R7DRID        31A       30TEXT('Doc. Reference ID')
     A                                      SPACEA(1)
     A                                     2'Corrected Reference ID....:'
     A            R7CRID        31A       30TEXT('Corrected Ref. ID')
     A                                      SPACEA(1)
 
      *****************************************************************
