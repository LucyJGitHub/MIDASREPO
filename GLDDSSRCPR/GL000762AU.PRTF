      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL CRS Report Management - Audit')               *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  GL000762AU - Midas GL CRS Reports Management - Audit         *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      INDARA
      *
      ** Format for Whole Report
      *
     A          R CONTROL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2016')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE GL000762AU'
     A            RRTITL        53        36TEXT('TITLE')
     A                                   100'DATE'
     A            RRRUNA         7       105TEXT('RUNDATE')
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      SPACEA(1)
     A                                      EDTWRD('0 :  :  ')
      *
     A                                    29'Customers/Non-Account +
     A                                      Holders under Exception +
     A                                      Management - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    29'----------------------------------'
     A                                    63'------------------------------'
     A                                      SPACEA(3)
      *
     A                                     6'FILE - CRS Customer and +
     A                                      Non account holder table'
     A                                    62'CALCULATED'
     A                                      SPACEA(1)
     A                                     6'--------------------------+
     A                                      ----------------------'
     A                                    62'----------'
     A                                      SPACEA(3)
     A                                    10'NO. OF CUSTOMERS REPORTED'
     A                                    46':'
     A            RRTCUS         4  0     68TEXT('CALC NO. OF RECORDS')
     A                                      EDTWRD('  0 ')
     A                                      SPACEA(1)
     A                                    10'NO. OF NON-ACCOUNT HOLDERS +
     A                                      REPORTED'
     A                                    46':'
     A            RRTNAH         4  0     68TEXT('CALC NO. OF RECORDS')
     A                                      EDTWRD('  0 ')
     A                                      SPACEA(1)
      *
     A          R TRAILER
      *
     A N54N80                             48'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      ** Database Error Message
      *
     A          R DBERROR                   TEXT('DATABASE ERROR MSG')
     A                                      SPACEB(3)
     A                                    48 '***   PRINT TERMINATED   ***'
     A                                      SPACEA(2)
     A                                    45 'DATABASE ERROR'
     A            DBASE          3  0     60
     A                                    65 'FILE'
     A            DBFILE         8        70
     A                                      SPACEA(1)
     A                                    45 'KEY'
     A            DBKEY         29        49
      *
