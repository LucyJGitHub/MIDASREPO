      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL Amt Amortisation Upd & Entry Gen Audit')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL001141AU - Midas GL Amount Amortisation Update &      *
      *                    Entry Generation Audit Report              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE GL001141AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    39'AMOUNT AMORTISATION UPDATE +
     A                                      AND ENTRY GENERATION +
     A                                      RUN CONTROLS'
     A                                      SPACEA(1)
     A                                    39'---------------------------+
     A                                      --------------------'
     A                                      SPACEA(3)
      *
     A          R CONTROL
     A                                    53'------------ PROGRAM +
     A                                      -----------'
     A                                      SPACEA(2)
      *
     A                                    53'NUMBER'
     A                                    80'VALUE'
     A                                      SPACEA(1)
      *
     A                                    12'S/GLAMRTPD - AMOUNT AMORTISATION'
     A                                    45'MASTER'
     A                                      SPACEA(2)
      *
     A                                     9'NUMBER OF LIVE RECORDS'
     A                                    32'ON FILE'
     A            RNOREL    R             54REFFLD(NORE) EDTCDE(3)
     A            RTOTLW    R             66REFFLD(TTAM) EDTCDE(Z)
     A            RTOTLD    R             81REFFLD(NORE3)
     A                                      SPACEA(2)
      *
     A                                     9'NUMBER OF ACTIVE RECORDS'
     A                                    34'ON FILE'
     A            RNOREA    R             54REFFLD(NORE) EDTCDE(3)
     A            RTOTAW    R             66REFFLD(TTAM) EDTCDE(Z)
     A            RTOTAD    R             81REFFLD(NORE3)
     A                                      SPACEA(2)
      *
     A                                    12'S/GLNAMRPD - NEW AMOUNT'
     A                                    36'AMORTISATION MASTER'
     A                                      SPACEA(2)
      *
     A                                     9'NUMBER OF LIVE RECORDS'
     A                                    32'ON FILE'
     A            RNORLO    R             54REFFLD(NORE) EDTCDE(3)
     A            RTOLWO    R             66REFFLD(TTAM) EDTCDE(Z)
     A            RTOLDO    R             81REFFLD(NORE3)
     A                                      SPACEA(2)
      *
     A                                     9'NUMBER OF ACTIVE RECORDS'
     A                                    34'ON FILE'
     A            RNORAO    R             54REFFLD(NORE) EDTCDE(3)
     A            RTOAWO    R             66REFFLD(TTAM) EDTCDE(Z)
     A            RTOADO    R             81REFFLD(NORE3)
     A                                      SPACEA(2)
      *
     A                                    12'S/GLAMRTPD - GENERATED'
     A                                    35'ENTRIES'
     A                                      SPACEA(2)
      *
     A                                     9'NUMBER OF RECORDS ON'
     A                                    30'FILE'
     A            RNOREG    R             54REFFLD(NORE) EDTCDE(3)
     A            RTOTGW    R             66REFFLD(TTAM) EDTCDE(Z)
     A            RTOTGD    R             81REFFLD(NORE3)
     A                                      SPACEA(8)
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
