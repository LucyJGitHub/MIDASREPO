      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL RWA Netting Groups Audit - Audit')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Moule                                 *
      *                                                               *
      *  PRTF/GL001247AU - RWA Netting groups input and amended today *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL018  *CREATE    Date 18Sep02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL018 - Risk Weighted Assets                                *
      *                                                               *
      *****************************************************************
     A                                      REF(GLNETGPD)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL001247'
     A            BJURPT        53A       37
     A                                   101'DATE'
     A            BJMRDT         7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    54'RWA NETTING GROUPS - +
     A                                             AUDIT'
     A                                      SPACEA(1)
      *
     A                                    54'--------------------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    50'INSERTED'
     A                                    60'AMENDED'
     A                                    75'TOTAL IN FILE'
     A                                      SPACEA(1)
     A                                    49'-----------------------+
     A                                       -----------------'
     A                                      SPACEA(3)
      *
     A                                     6'GLNETGPD +
     A                                      - PF/(File)'
      *
     A            ICOUNT         3  0     50EDTCDE(3)
     A            ACOUNT         3  0     60EDTCDE(3)
     A            RCOUNT         5  0     80EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'----------------------+
     A                                      -----------'
     A                                      SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
      ***  Example /COPY.
     A*/COPY WNCPYSRC,MMnnnnAU01
      *
