      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL RWA Netted Balances Audit Report')
      *****************************************************************
      *                                                               *
      *  Midas - GL Module                                            *
      *                                                               *
      *  PRTF/GL001248P1 Midas GL - RWA Netted Balances Report - List *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. CGL018  *CREATE    Date 08Oct02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL018 - Risk Weighted Assets                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-----------*
      ***  HEADP1 *
      *-----------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2002')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(002)
     A                                     1
     A                                      'REFERENCE'
     A                                    11
     A                                      'GL001248'
     A            BJURPT    R             34
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
      *
     A                                    47
     A                                      'RWA Netted Balances Report'
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
     A                                    47'--------------------------'
     A                                      SPACEA(002)
     A                                     5
     A                                      'Set Code  --'
     A            RRSET          1A  O    18
     A                                    20
     A                                      '--'
     A                                    61
     A                                      'Balance'
     A                                      SPACEB(001)
     A                                    79
     A                                      'Netted Balance'
     A                                   108
     A                                      'RWA Balance'
      *
      *-----------------------*
      ***  PRIMARY ACCOUNT    *
      *-----------------------*
      *
     A          R DETPRI
     A                                     5
     A                                      'Primary Account:'
     A                                      HIGHLIGHT
     A                                      SPACEB(002)
     A**Start of change CGL029
     A**********  RRPACC        22A  O    22HIGHLIGHT                                         CGL029
     A**********  RRBAL1        21A  O    47                                                  CGL029
     A            RRPACC        28A  O    22HIGHLIGHT
     A            RRBAL1        21A  O    53
     A**End of change CGL029
     A            RRDRCR         2A  O    +0
      *
      *--------------------------*
      ***  SECONDARY ACCOUNT 1   *
      *--------------------------*
      *
     A          R DETSE1
     A                                     5
     A                                      'Secondary Accounts'
     A                                      SPACEB(001)
      *
      *--------------------------*
      ***  SECONDARY ACCOUNT 2   *
      *--------------------------*
      *
     A          R DETSE2
     A**Start of change CGL029
     A**********  RRSACC        22A  O    24                                                  CGL029
     A            RRSACC        28A  O    24
     A**End of change CGL029
     A                                      SPACEB(001)
     A**Start of change CGL029
     A**********  RRSBA1        21A  O    47                                                  CGL029
     A            RRSBA1        21A  O    53
     A**End of change CGL029
     A            RRDRCR         2A  O    +0
      *
      *--------------------------*
      ***  SECONDARY ACCOUNT 3   *
      *--------------------------*
      *
     A          R DETSE3
     A            RRSBA2        21A  O    72
     A                                      SPACEB(001)
     A            RRDRCR         2A  O    +0
     A            RRSBA3        21A  O    98
     A            RRDRC1         2A  O    +0
      *
      *-------------*
      ***  NORECORD *
      *-------------*
     A          R NORECORD
     A                                      SPACEB(001)
     A                                    46
     A                                      '***  NO RECORDS TO REPORT ***'
     A                                      SPACEA(001)
      *
      *------------*
      ***  TRAILP1 *
      *------------*
      *
     A          R TRAILP1
     A                                    49
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
