      *****************************************************************
/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Risk Weighted Assets Balances Report (P1) PRTF')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL001249P1 - Risk Weighted Assets Balances Report       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. CGL018  *CREATE    Date 23Sep02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL018 - Risk Weighted Assets                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL001249P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    56'RWA Balances Report'
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
      *
     A                                    56'-------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
     A                                     1'SET'
     A            RWSETN         1A        5
     A                                     8'Calculation method'
     A            RWCALC         1A       27
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
     A                                     1'MODULE'
     A                                     8'TRANSACTION'
     A                                    38'ASSET'
     A                                    48'CCY'
     A                                    52'WEIGHTING'
     A                                    62'RWA'
     A                                    66'WEIGHTING'
     A                                   100'RISK ADJUSTED'
     A                                   125'RWA'
     A                                      SPACEA(1)
      *
     A                                    12'No.'
     A                                    37'BALANCE'
     A                                    53'SOURCE'
     A                                    62'CODE'
     A                                    70'%'
     A                                    84'AMOUNT'
     A                                   104'AMOUNT'
     A                                   123'BALANCE'
     A                                      SPACEA(1)
      *
      *--------------------------*
      ***  Detail Record Formats *
      *--------------------------*
      *
     A          R DETAIL1                   SPACEB(2)
     A            RWMOD          2A        2
     A**Start of change CGL029
     A**********  TRANO         22A        5                                                  CGL029
     A**30******  AMNT0         15  0     28EDTCDE(1)                                         CGL029
     A**31******  AMNT1         15  1     27EDTCDE(1)                                         CGL029
     A**32******  AMNT2         15  2     27EDTCDE(1)                                         CGL029
     A**33******  AMNT3         15  3     28EDTCDE(1)                                         CGL029
     A**********  RWCCY          3A       48                                                  CGL029
     A            TRANO         28A        5
     A  30        AMNT0         15  0     34EDTCDE(1)
     A  31        AMNT1         15  1     33EDTCDE(1)
     A  32        AMNT2         15  2     33EDTCDE(1)
     A  33        AMNT3         15  3     34EDTCDE(1)
     A            RWCCY          3A       54
     A**End of change CGL029
      *
     A          R DETAIL2                   SPACEB(1)
     A            COLNAR        27A        5
     A            RWWSRC        10A       52
     A            RWCODE         2A       63
     A            PERCO          5  2     66EDTCDE(3)
     A                                    72'%'
     A  30        RIAMT0        15  0     74EDTCDE(1)
     A  31        RIAMT1        15  1     73EDTCDE(1)
     A  32        RIAMT2        15  2     73EDTCDE(1)
     A  33        RIAMT3        15  3     74EDTCDE(1)
     A  30        RADJA0        15  0     94EDTCDE(1)
     A  31        RADJA1        15  1     93EDTCDE(1)
     A  32        RADJA2        15  2     93EDTCDE(1)
     A  33        RADJA3        15  3     94EDTCDE(1)
      *
     A          R DETTOT                    SPACEB(1)
     A  30        RABAL0        15  0    114EDTCDE(1)
     A  31        RABAL1        15  1    113EDTCDE(1)
     A  32        RABAL2        15  2    113EDTCDE(1)
     A  33        RABAL3        15  3    114EDTCDE(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
