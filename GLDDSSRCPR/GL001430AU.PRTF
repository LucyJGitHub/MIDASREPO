      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL MT940 File Extract - Audit Report')           *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL001430AU - Midas GL MT940 File Extract - Audit Report *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      *  Prev Amend No. CGL013  *CREATE    Date 23Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL013 - MT94x Message Generation                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE GL001430AU'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                    41
     A                                      'Midas GL MT940 File Extract - Audi-
     A                                      t Report'
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
     A                                    42
     A                                      '-----------------------------------
     A                                      --------'
      *
      *-----------------*
      ***  Sub-Title  ***
      *-----------------*
      *
     A          R SUBTL
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    13
     A                                      'Network'
     A                                      SPACEB(001)
     A                                    +4
     A                                      'Account'
     A                                   +25
     A                                      'Destination'
     A                                    +5
     A                                      'Comments'
     A                                    13
     A                                      '-------'
     A                                      SPACEB(001)
     A                                    24
     A                                      '-------'
     A                                   +25
     A                                      '-----------'
     A                                    +5
     A                                      '--------'
      *
      *--------------*
      ***  Detail  ***
      *--------------*
      *
     A          R DETAIL
     A                                      SPACEB(001)
     A            N4NWRK    R        O    13
     A                                      REFFLD(GLNW94D0/N4NWRK *LIBL/GLNW94-
     A                                      PD)
     A            N4BRCA    R        O    +5
     A                                      REFFLD(GLNW94D0/N4BRCA *LIBL/GLNW94-
     A                                      PD)
     A                                    +0
     A                                      '-'
      * Begin /CSD027/
     A**********  N4CNUM    R        O    +0                                                  CSD027
     A**********                            REFFLD(GLNW94D0/N4CNUM *LIBL/GLNW94-              CSD027
     A**********                            PD)                                               CSD027
     A            N4CNUM         6A  O    +0TEXT('Account Customer')
      * End /CSD027/
     A                                    +0
     A                                      '-'
     A            N4CCY     R        O    +0
     A                                      REFFLD(GLNW94D0/N4CCY *LIBL/GLNW94P-
     A                                      D)
     A                                    +0
     A                                      '-'
     A            N4ACCD    R        O    +0
     A                                      REFFLD(GLNW94D0/N4ACCD *LIBL/GLNW94-
     A                                      PD)
     A                                    +0
     A                                      '-'
     A            N4ACSQ    R        O    +0
     A                                      REFFLD(GLNW94D0/N4ACSQ *LIBL/GLNW94-
     A                                      PD)
     A*/COPY WNCPYSRC,GLH00390
     A            N4DSTN    R        O   +10
     A*/COPY WNCPYSRC,GLH00391
     A                                      REFFLD(GLNW94D0/N4DSTN *LIBL/GLNW94-
     A                                      PD)
     A            P4CMNT        50A  O    +5
     A                                      TEXT('Comments')
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                      SPACEB(002)
     A                                    52
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *---------------------*
      ***  End of Report  ***
      *---------------------*
      *
     A          R ENDRPT
     A                                      SPACEB(002)
     A                                    55
     A                                      '*** END OF REPORT ***'
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEB(002)
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(002)
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    FILE NAME    '''
     A                                      SPACEB(001)
     A            DBFILE         8A       30
     A                                    38
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    KEY          '''
     A                                      SPACEB(001)
     A            DBKEY         29        30
     A                                    59
     A                                      '''    *'
     A                                    11
     A                                      '*    PROGRAM NAME '''
     A                                      SPACEB(001)
     A            DBPGM         10A       30
     A                                    40
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    AT           '''
     A                                      SPACEB(001)
     A            DBASE          3  0     30
     A                                    33
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(001)
      *
