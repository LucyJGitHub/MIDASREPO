      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL CGL013 Initialiation Report -DETAIL')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PRTF/GL001450P1 - GL CGL013 Initialisation Report - DETAIL   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. CGL013  *CREATE    Date 25May02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL013 - MT94x Message Generation                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE CGL001450P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    35'Upgrade Utility Report for +
     A                                      MT94x Message Generation'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    35'---------------------------+
     A                                      ------------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading1:
      ***  ------------
      *
     A          R HEAD1
      *
     A                                      SPACEB(1)
     A                                    10'PF / SDNWRKPD'
     A                                      SPACEA(2)
     A                                    10'NETWORK'
     A                                    38'STATUS'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                      SPACEA(1)
     A            RNWRK          6        10TEXT('Network')
     A                                    34'RECORD UPDATED'
      *
      *
      ***  Sub-Heading2:
      ***  ------------
      *
     A          R HEAD2
      *
     A                                      SPACEB(1)
     A                                    10'PF / GLNWACPD & GLNW94PD'
     A                                      SPACEA(2)
     A                                     5'NETWORK'
     A**Start of change CGL029
     A**********                          15'ACCOUNT'                                         CGL029
     A                                    14'ACCOUNT'
     A**End of change CGL029
     A                                    40'GEN. MT950?'
     A                                    55'GEN. MT940?'
     A                                    70'GEN. MT941?'
     A                                    85'GEN. MT942?'
     A                                   102'DESTINATION'
     A                                   118'No. Of Postings'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL2
      *
     A                                      SPACEA(1)
     A            RNETWORK       6         5TEXT('Network')
     A**Start of change CGL029
     A**********  RACCOUNT      18        15TEXT('GL Account Number')                         CGL029
     A            RACCOUNT      24        14TEXT('GL Account Number')
     A**End of change CGL029
     A            RMT950         1        46TEXT('Generate MT950?')
     A            RMT940         1        60TEXT('Generate MT940?')
     A            RMT941         1        75TEXT('Generate MT941?')
     A            RMT942         1        90TEXT('Generate MT942?')
     A            RDEST         11       102TEXT('Destination')
     A            RPOST          5  0    123TEXT('Posting Transferred')
      *
      *---------------*
      ***  REMINDER   *
      *---------------*
      *
     A          R REMIND
     A                                    42'***  PLEASE REVIEW THE NETWORK +
     A                                      DETAILS  ***'
     A                                      SPACEB(1)
     A                                    42'***  AND NETWORK ACCOUNT DETAILS +
     A                                      AFTER  ***'
     A                                      SPACEB(1)
     A                                    42'*** RUNNING THIS INITIALISATION +
     A                                      PROGRAM ***'
     A                                      SPACEB(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
