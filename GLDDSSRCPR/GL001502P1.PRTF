      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Collateral Entry - Missing Account Keys')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL001502P1 - Collateral Entry - Missing Account Keys    *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CGL014  *CREATE    Date 20Oct03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CGL014 - Collaterals Processing                              *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Format
      *
     A          R GL1502R0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE GL001502P1'
     A            BJURPT    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    40'COLLATERAL ENTRY - +
     A                                       MISSING ACCOUNT KEYS'
     A                                      SPACEA(1)
     A                                    40'-------------------+
     A                                       --------------------'
     A                                      SPACEA(3)
      *
     A                                     7'Account Key'
     A                                    24'Collateral Ref'
     A                                    44'Cust No.'
     A                                    58'Event Date'
     A                                    74'Event Ccy'
     A                                    89'Event Amount'
     A                                      SPACEA(1)
     A                                     7'-----------'
     A                                    24'--------------'
     A                                    44'--------'
     A                                    58'----------'
     A                                    74'---------'
     A                                    89'------------'
     A                                      SPACEA(1)
      *
      ** Format to Output Detail Lines
      *
     A          R GL1502R1                  TEXT('DETAIL OUTPUT FMT')
      *
     A            RAKEY         10A        7TEXT('Account Key')
     A            RCREF         10A       26TEXT('Collateral Reference')
     A            RCNUM          6A       45TEXT('Customer Number')
     A            REDAT          7A       59TEXT('Event Date')
     A            RECCY          3A       77TEXT('Event Currency')
     A            REAMT         21A       86TEXT('Event Amount')
     A                                      SPACEA(1)
      *
      ** End of Report Format
      *
     A          R GL1502R2                  TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    52'*** END OF REPORT ***'
      *
