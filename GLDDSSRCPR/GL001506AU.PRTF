      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL Collateral Entry Generation - Audit')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL001502AU - Generate Account Keys Header - Audit       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 240858  *CREATE    Date 11Jul06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  240858 - It will generate the Header record of Collateral    *
      *           Account Keys Header.                                *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Format
      *
     A          R HEADAU
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE GL001506AU'
     A            BJURPT    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    39'COLLATERAL ACCOUNT KEY HEADER +
     A                                       - AUDIT'
     A                                      SPACEA(1)
     A                                    39'----------------------------+
     A                                       ---------'
     A                                      SPACEA(3)
      *
      ** Format to Output Detail Lines
      *
     A          R CONTROL
     A                                     6'NO. OF RECORDS ON FILE +
     A                                      - PF/GLCOAKPD'
     A            RNORE          5  0     65EDTCDE(3)
     A                                      SPACEA(2)
     A                                     6'GENERATED COLLATERAL ACCOUNT +
     A                                       KEY HEADER - PF/GLCOAHPD'
     A                                      SPACEA(1)
     A                                    16'RECORDS AT START OF I/C '
     A            RNREC          5  0     65EDTCDE(3)
     A                                      SPACEA(1)
     A                                    16'NO. OF RECORDS INSERTED '
     A            RNINS          5  0     65EDTCDE(3)
     A                                      SPACEA(1)
     A                                    16'NO. OF RECORDS DELETED  '
     A            RNDEL          5  0     65EDTCDE(3)
     A                                      SPACEA(1)
     A                                    16'NO. OF RECORDS AMENDED  '
     A            RNAMD          5  0     65EDTCDE(3)
     A                                      SPACEA(1)
     A                                    16'NO. OF A/C KEY RECORD   '
     A            RNRC1          5  0     65EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                    48'*** END OF REPORT ***'
      *
      ** No Records In File
      *
     A          R NOREC
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      ** Database Error
      *
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
      *                                      Begin /S01117/
     A            DBFILE         8        30
     A                                    38''''
      *                                      End   /S01117/
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
