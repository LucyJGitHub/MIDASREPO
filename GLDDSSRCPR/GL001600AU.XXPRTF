      *****************************************************************
/*S*D ***PRTFBASE******************************************************                       CCB023
/*E*S ***PR_AU60*******************************************************                       CCB023
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL001600AU - Transfer Equation Accounting Information   *
      *                    Audit Report                               *
      *                                                               *
      *  @ Misys International Banking Systems Ltd. 2006              *
      *                                                               *
      *  Last Amend No. CCB023 *REDUNDANT  Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG12141           Date 05Oct06               *
      *                 CRE026  *CREATE    Date 24May06               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCB023 - COB Restructure - Input Cycle Termination           *
      *           Restructuring                                       *
      *  BUG12141 - Word Equation should not appear on Consumer       *
      *             banking audit reports                             *
      *  CRE026 - Consumer Banking                                    *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU
     A                                      TEXT('@ MISYS INTERNATIONAL BANKI-
     A                                      NG SYSTEMS LTD. 2006')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(003)
     A            BJURPT    R             40
     A                                      REFFLD(URPT)
     A                                   104
     A                                      'DATE'
     A                                     6
     A                                      'REFERENCE GL001600AU'
     A            BJMRDT    R            110
     A                                      REFFLD(MRDT)
     A                                   121
     A                                      'PAGE'
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   104
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   110
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    31
     A**********                            'CONSUMER BANKING - TRANSFER EQUATI-            BUG12141
     A**********                            ON ACCOUNTING INFORMATION AUDIT REP-            BUG12141
     A**********                            ORT'                                            BUG12141
     A                                      'CONSUMER BANKING - TRANSFER-                   BUG12141
     A                                       SUB-LEDGER ACCOUNTING INFORMATION-             BUG12141
     A                                       AUDIT REPORT'                                  BUG12141
     A                                      SPACEB(001)
     A                                    31
     A**********                            '-----------------------------------            BUG12141
     A**********                            ------------------------------------            BUG12141
     A**********                            ---'                                            BUG12141
     A                                      '----------------------------                   BUG12141
     A                                      -----------------------------------             BUG12141
     A                                      -------------'                                  BUG12141
     A                                      SPACEB(001)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                      SPACEA(002)
     A                                     9
     A                                      'RUN CONTROLS'
     A                                   +38
     A                                      'NUMBER'
     A                                   +26
     A                                      'VALUE'
     A                                    58
     A                                      '--------'
     A                                      SPACEB(001)
     A                                   +25
     A                                      '------------------'
     A                                     6
     A                                      'OUTPUT TO CONSUMER BANKING POSTING'
     A                                      SPACEB(002)
     A                                     6
     A                                      'TRAILER FILE'
     A                                      SPACEB(001)
     A                                   +10
     A                                      '- PF/GECBZZ'
      *
     A            PRCOUNT        5  0     60
     A                                      EDTCDE(3)
     A            PRHRWN        13  0    +26
     A                                      EDTCDE(3)
     A            PRHRDC         4  0     +1
     A                                      EDTCDE(3)
     A            PRSIGN         1A  O    +0
      *
     A                                     6
     A                                      '---------------------------------'
     A                                      SPACEB(001)
      *
      *---------------*
      ***  NO Details *
      *---------------*
      *
     A          R NODTLS
     A                                    53
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                      SPACEA(001)
     A                                    53
     A                                      '***** PRINT TERMINATED *****'
     A                                      SPACEB(002)
     A                                    55
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEB(001)
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(002)
     A                                    11
     A                                      '*    FILE NAME    '''
     A                                      SPACEB(001)
     A            DBFILE         8A       30
     A                                    38
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    KEY          '''
     A                                      SPACEB(001)
     A            DBKEY         29        30
     A                                    59
     A                                      '''    *'
     A                                    11
     A                                      '*    PROGRAM NAME '''
     A                                      SPACEB(001)
     A            DBPGM         10A       30
     A                                    40
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    AT           '''
     A                                      SPACEB(001)
     A            DBASE          3  0     30
     A                                    33
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(001)
     A                                     1
     A                                      'RECORD:'
     A                                      SPACEB(001)
     A            PRFOTRANID    20A  O    +1
     A            PRFOASOCID    20A  O    +1
     A            PRCNFVALFO     1A  O    +1
     A            PRRPRLOCN      1A  O    +1
     A            PRRETCODE     10A  O    +1
     A            PRMIDUSR      10A  O    +1
     A            PRACID        10A  O    +1
     A            PRCYCD         3A  O    +1
     A            PRACCD        10A  O    +1
     A            PRACSN         2A  O    +1
     A            PRIBCA         3A  O    +1
     A            PRPTAM        14A  O    +1
     A            PRDCIN         1A  O    +1
     A            PRNARR        30A  O     9
     A                                      SPACEB(001)
     A            PRTRTY         5A  O    +1
     A            PRTSTY        10A  O    +1
     A            PRVLDT         6A  O    +1
     A            PRCQNB         8A  O    +1
     A            PRSWCR        16A  O    +1
     A            PRPRCN         4A  O    90
     A            PRACNB        10A  O    95
     A            PRBKCD         2A  O   106
     A            PRRVDI         1A  O   109
     A            PRNITM         5A  O   111
     A            PRRINO         1A  O   117
     A            PROCCY         3A  O   119
     A            PROAMT        16A  O     9
     A                                      SPACEB(001)
      *------------------*
      ***  End Of Report *
      *------------------*
      *
     A          R FOOTAU
     A                                    54
     A                                      '***** END OF REPORT *****'
     A                                      SPACEB(003)
      *
