      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL External Batch Headers Report')               *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      ***PRTF/GL001600P1*-*Midas*GL*External*Batch*Headers*Report******                       CER059
      *  PRTF/GL001601P1 - Midas GL External Batch Headers Report     *                       CER059
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER046  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER046 - German Features LF033-00 Payments & Clearing        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADER1                   TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2008')
     A                                      SKIPB(001)
      *
      **Start of Change CER059
     A**********                           6'REFERENCE GL001600P1'                            CER059
     A                                     6'REFERENCE GL001601P1'
      **End of Change CER059
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(003)
      *
     A                                    47
     A                                      'EXTERNAL BATCH +
     A                                      HEADERS REPORT'
     A                                      SPACEA(001)
     A                                    47
     A                                      '---------------+
     A                                      --------------'
     A                                      SPACEA(002)
      *
     A                                     2'Batch'
     A                                    10'New Batch'
     A                                    24'Department'
     A                                    73'Number of Items'
     A                                      SPACEA(001)
      *
     A                                     3'No.'
     A                                    13'No.'
     A                                    27'Code'
     A                                    42'Batch Description'
     A                                    73'Input  Computed'
     A                                   100'Status'
     A                                      SPACEA(001)
      *
     A                                     2'-----'
     A                                    10'----------'
     A                                    24'----------'
     A                                    36'-------------------+
     A                                      -----------'
     A                                    72'------- --------'
     A                                    91'-------------------+
     A                                      ------'
     A                                      SPACEA(001)
      *
      ** Detail Record Format
      *
     A          R DEATIL1
      *
     A N30        RBBTNB         3         3TEXT('Batch number')
     A N30        RBNBTN         3        13TEXT('New Batch number')
     A N30        RBDPCD         3        27TEXT('Dept Code')
     A N30        RBBDES        30        36TEXT('Batch Description')
     A N30        RBHINI         3        75TEXT('Item no.input')
     A N30        RBHINC         3        83TEXT('item no.calculated')
     A            RBSTAT        25        91TEXT('Status')
     A                                      SPACEA(1)
      *
