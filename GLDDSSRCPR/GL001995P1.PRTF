      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Amount Amortisation Audit List - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL001995P1 - Amount Amortisation Audit List             *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG13042           Date 16Jan07               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAS011             Date 16May05               *
      *                 CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG13042 - Two different reports having same title           *
      *  CAS011 - EIR/AC Accounting Upgrade to MP1                    *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International -
     A                                      Limited 2004')
      *
     A                                      SKIPB(003)
      *
     A                                     6'REFERENCE GL001995P1'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(002)
      *
      ** Start of change BUG13042
     A**********                          50'AMOUNT AMORTISATION REPORT'                    BUG13042
     A  35                                46'AMOUNT AMORTISATION REPORT - AUDIT'
     A  35                                81'LIST'
     A                                      SPACEA(001)
     A N35                                46'AMOUNT AMORTISATION REPORT - FULL'
     A N35                                80'LIST'
      ** End of change BUG13042
     A                                      SPACEA(001)
      ** Start of change BUG13042
     A**********                          50'--------------------------'                    BUG13042
     A                                    46'----------------------------------'
     A                                    80'----'
      ** End of change BUG13042
     A                                      SPACEA(002)
     A  37                                 6'BRANCH:'
     A  37        RBRCA1         3        +1TEXT('BRANCH CODE')
     A  37        RBRNM1        30        +2TEXT('BRANCH NAME')
     A  37                                  SPACEA(002)
      *
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
     A                                      SPACEB(002)
      *
     A  01                                 6'CURRENCY'
     A  01        RCCY           3        16TEXT('CURRENCY CODE')
     A  01        RCYNM         14        22TEXT('CURRENCY NAME')
     A                                      SPACEA(002)
     A                                     6'REFERENCE NO.'
     A            RAREF          6        21TEXT('TRANSACTION REF')
     A                                    32'NARRATIVE'
     A            RNARR         30        43TEXT('POSTING NARRATIVE')
      *
     A                                     6'START DATE'
     A                                      SPACEB(001)
     A            RSTDT          7        18TEXT('START DATE')
     A                                    32'MATURITY DATE'
     A            RMDAT          7        47TEXT('MATURITY DATE')
     A                                    60'CALCULATION BASIS'
     A            RCLBS          1        79TEXT('CALCULATION BASIS')
     A                                    83'EFFECTIVE YIELD'
     A            REFYL         16       100TEXT('EFFECTIVE YIELD')
     A                                   120'METHOD'
     A            RAMMD          1       128TEXT('METHOD')
      *
     A                                    15'DR A/C'
     A                                      SPACEB(002)
     A                                    31'CR A/C'
     A                                    50'DISCOUNTED'
     A                                    76'PAR'
     A                                    91'AMOUNT AMORTISED'
     A                                   112'AMOUNT UNAMORTISED'
      *
     A                                    52'AMOUNT'
     A                                      SPACEB(001)
     A                                    74'AMOUNT'
     A                                    92'POSTED TO DATE'
     A                                   114'POSTED TO DATE'
      *
      ** Start of change CAS011
     A**********  RDRCN          6        11TEXT('DEBIT CUSTOMER')                            CAS011
     A**********                            SPACEB(001)                                       CAS011
     A**********                          17'-'                                               CAS011
     A**********  RDRCO          4        18TEXT('DEBIT A/C CODE')                            CAS011
     A**********                          22'-'                                               CAS011
     A**********  RDRSQ          2        23TEXT('DEBIT A/C SEQ')                             CAS011
     A**********  RCRCN          6        27TEXT('CREDIT CUSTOMER')                           CAS011
     A**********                          33'-'                                               CAS011
     A**********  RCRCO          4        34TEXT('CREDIT A/C CODE')                           CAS011
     A**********                          38'-'                                               CAS011
     A**********  RCRSQ          2        39TEXT('CREDIT SEQ. NO.')                           CAS011
     A            RDRCN          6         5TEXT('DEBIT CUSTOMER')
     A                                      SPACEB(001)
     A                                    11'-'
     A            RDRCO         10        12TEXT('DEBIT A/C CODE')
     A                                    22'-'
     A            RDRSQ          2        23TEXT('DEBIT A/C SEQ')
     A            RCRCN          6        26TEXT('CREDIT CUSTOMER')
     A                                    32'-'
     A            RCRCO         10        33TEXT('CREDIT A/C CODE')
     A                                    43'-'
     A            RCRSQ          2        44TEXT('CREDIT SEQ. NO.')
      ** End of change CAS011
     A            RDSAM         16        47TEXT('DISCOUNTED AMOUNT')
     A            RPRAM         16        69TEXT('PAR AMOUNT')
     A            RAATD         16        91TEXT('AMOUNT AMORTISED POSTED TO +
     A                                      DATE')
     A            RUNAM         16       113TEXT('AMOUNT UNAMORTISED TO DATE')
      *
      *------------------------------------*
      ***  Report Ender Record Format      *
      *------------------------------------*
      *
     A          R ENDER1
      *
     A                                      SPACEB(002)
      *
     A                                    70'CURRENCY TOTAL'
     A            RTOTA         16        91TEXT('TOTAL AMORTISED POSTED TO +
     A                                      DATE')
     A            RTOTU         16       113TEXT('TOTAL UNAMORTISED TO DATE')
     A                                      SPACEA(001)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
      *
     A                                      SPACEB(002)
      *
     A N37                                51'*** END OF REPORT ***'
     A  37                                48'*** END OF REPORT FOR BRANCH'
     A  37        RBRCA2         3        +1
     A  37                                +1'***'
