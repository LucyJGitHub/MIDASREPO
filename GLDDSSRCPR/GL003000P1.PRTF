      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Account Transfer Housekeeping Report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL003000P1 - GL Account Transfer Housekeeping Report    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CAP204  *CREATE    Date 11Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP204 - Retail Account Transfer                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2010')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE GL003000P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    41'ACCOUNT TRANSFER PURGED RECORDS - +
     A                                      LIST REPORT'
     A                                      SPACEB(001)
     A                                   132' '
     A                                    41'-----------------------------------
     A                                      -----------'
     A                                      SPACEB(001)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'Brch'
     A                                      SPACEB(002)
     A                                    11'Transaction'
     A                                    24'Transfer'
     A                                    33'Trans'
     A                                    43'Debit Account'
     A                                    65'Debit Retail'
     A                                    79'Trans'
     A                                    88'Credit Account'
     A                                   109'Credit Retail'
     A                                   124'Trans'
     A                                    11'Reference'
     A                                      SPACEB(001)
     A                                    26'Type'
     A                                    33'Subtype'
     A                                    66'Account'
     A                                    79'Type'
     A                                   111'Account'
     A                                   124'Type'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A                                      SPACEB(001)
     A            RABRCA         3A  O     6
     A            RATREF        15A  O    11
     A            RATRTY         2A  O    27
     A            RATRST         5A  O    33
     A            RADBAC        24A  O    40
     A            RADBRE        10A  O    66
     A            RADRTT         5A  O    79
     A            RACRAC        24A  O    86
     A            RACRRE        10A  O   111
     A            RACRTT         5A  O   124
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    52'***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A
