      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Account Transfer A/cs Gen Successfully Rpt')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL003300P1 - Midas GL ATU Accounts Generated Successful *
      *                    Report.                                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2010')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL003300P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    47'Accounts to be +
     A                                      Transferred - Successful'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    47'---------------+
     A                                      ------------------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     1'Reference'
     A                                    18'From Account'
     A                                    47'Retail'
     A                                    58'Officer   Type'
     A                                    74'Domicile'
     A                                    91'To Account'
     A                                   116'Status'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAILP1
      *
     A            P1RFNO    R              1REFFLD(SLRFNO GLSLACPD)
     A            P1FCNU    R             14REFFLD(SLFCNU GLSLACPD)
     A            P1FCCY    R             +1REFFLD(SLCCY  GLSLACPD)
     A            P1FACO    R             +1REFFLD(SLFACO GLSLACPD)
     A            P1FACS    R             +1REFFLD(SLFACS GLSLACPD)
     A            P1FBRC    R             +1REFFLD(SLFBRC GLSLACPD)
     A            P1ACNO    R             45REFFLD(SLACNO GLSLACPD)
     A            P1ACOC    R             60REFFLD(SLACOC GLSLACPD)
     A            P1ATYP    R             +7REFFLD(SLATYP GLSLACPD)
     A            P1COLC    R             +7REFFLD(SLCOLC GLSLACPD)
     A            P1TCNU    R             +6REFFLD(SLTCNU GLSLACPD)
     A            P1TCCY    R             +1REFFLD(SLCCY  GLSLACPD)
     A            P1TACO    R             +1REFFLD(SLTACO GLSLACPD)
     A            P1TACS    R             +1REFFLD(SLTACS GLSLACPD)
     A            P1TBRC    R             +1REFFLD(SLTBRC GLSLACPD)
     A            P1STAT    R             +5REFFLD(SLSTAT GLSLACPD)
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    46'NUMBER OF ACCOUNTS SELECTED:'
     A                                      SPACEB(2)
     A            P1NOAC         9  0     +2
     A                                      EDTCDE(Z)
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
