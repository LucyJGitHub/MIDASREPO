      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL ATU Accounts Reversed Failed Report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL003400P2 - Midas GL ATU Accounts Reversed Failed      *
      *                    Report.                                    *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL003400P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'Accounts to be +
     A                                      Reversed - Failed'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'---------------+
     A                                      -----------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     1'Reference'
     A                                    18'From Account'
     A                                    47'Retail'
     A                                    58'Officer   Type'
     A                                    74'Domicile'
     A                                    91'To Account'
     A                                   116'Status'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAILP2
      *
     A            P2RFNO    R              1REFFLD(SLRFNO GLSLACPD)
     A            P2FCNU    R             14REFFLD(SLFCNU GLSLACPD)
     A            P2FCCY    R             +1REFFLD(SLCCY  GLSLACPD)
     A            P2FACO    R             +1REFFLD(SLFACO GLSLACPD)
     A            P2FACS    R             +1REFFLD(SLFACS GLSLACPD)
     A            P2FBRC    R             +1REFFLD(SLFBRC GLSLACPD)
     A            P2ACNO    R             45REFFLD(SLACNO GLSLACPD)
     A            P2ACOC    R             60REFFLD(SLACOC GLSLACPD)
     A            P2ATYP    R             +7REFFLD(SLATYP GLSLACPD)
     A            P2COLC    R             +7REFFLD(SLCOLC GLSLACPD)
     A            P2TCNU    R             +6REFFLD(SLTCNU GLSLACPD)
     A            P2TCCY    R             +1REFFLD(SLCCY  GLSLACPD)
     A            P2TACO    R             +1REFFLD(SLTACO GLSLACPD)
     A            P2TACS    R             +1REFFLD(SLTACS GLSLACPD)
     A            P2TBRC    R             +1REFFLD(SLTBRC GLSLACPD)
     A            P2STAT    R             +5REFFLD(SLSTAT GLSLACPD)
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP2
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
