      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Account Transfer-Delete Selected Acct Report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL003500P1 - Midas Account Transfer-Delete Selected     *
      *                    Accounts Report.                           *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL003500P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    53'Deleted Selected Accounts'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    53'-------------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     1'Reference'
     A                                    17'Description'
     A                                    31'Status'
     A                                    38'Creation'
     A                                    51'From Account'
     A                                    79'Retail'
     A                                    86'Officer Type'
     A                                    +1'Domicile'
     A                                   113'To Account'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAILP1
      *
     A            P1RFNO    R              1REFFLD(SLRFNO GLSLACPD)
     A            P1DESC    R   20        12REFFLD(SLDESC GLSLACPD)
     A            P1STAT    R             33REFFLD(SLSTAT GLSLACPD)
     A            P1CRTD    R             37REFFLD(SLCRTD GLSLACPD)
     A            P1FCNU    R             47REFFLD(SLFCNU GLSLACPD)
     A            P1CCY     R             +1REFFLD(SLCCY  GLSLACPD)
     A            P1FACO    R             +1REFFLD(SLFACO GLSLACPD)
     A            P1FACS    R             +1REFFLD(SLFACS GLSLACPD)
     A            P1FBRC    R             +1REFFLD(SLFBRC GLSLACPD)
     A            P1ACNO    R             +2REFFLD(SLACNO GLSLACPD)
     A            P1ACOC    R             89REFFLD(SLACOC GLSLACPD)
     A            P1ATYP    R             96REFFLD(SLATYP GLSLACPD)
     A            P1COLC    R            102REFFLD(SLCOLC GLSLACPD)
     A            P1TCNU    R            106REFFLD(SLTCNU GLSLACPD)
     A            P1TACO    R             +1REFFLD(SLTACO GLSLACPD)
     A            P1TACS    R             +1REFFLD(SLTACS GLSLACPD)
     A            P1TBRC    R             +1REFFLD(SLTBRC GLSLACPD)
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
