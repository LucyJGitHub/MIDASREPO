      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Generated Entries Account Transfer-Detail')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL004601P1 - Midas GL Generated Entries Account         *
      *                    Transfer - Detail                          *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD057038           Date 11Nov20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CAP204 *CREATE     Date 05Feb10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD057038 - Additional of Debit/Credit Associated Customer.   *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP204 - Teller Related APIs - Account Transfer              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL004601P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    42'ACCOUNT TRANSFER INPUT +
     A                                      TODAY RUN CONTROLS'
      *
     A                                    89'BRANCH -'
     A            RRBRCD    R             98REFFLD(BRCA)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    42'-----------------------+
     A                                      ------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
      **Start of change MD057038
     A**********                           2'TRANSACTION'
     A**********                          19'CUSTOMER NAME'
     A**********                          45'ACCOUNT NO'
     A**********                          60'CHEQUE NO'
     A**********                          73'NARRATIVE'
     A**********                         105'CCY'
     A**********                         109'AMOUNT'
     A                                     2'TRANSACTION'
     A                                    18'CUSTOMER NAME'
     A                                    44'ACCOUNT NO'
     A                                    55'ASSOC CUST'
     A                                    66'CHEQUE NO'
     A                                    77'NARRATIVE'
     A                                   108'CCY'
     A                                   112'AMOUNT'
      **End of change MD057038
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A                                      SPACEA(2)
      *
      **Start of change MD057038
     A**********  RRTREF        15A        2TEXT('Transaction Ref')
     A**********  RRCRNM        20A       19TEXT('Customer Name')
     A*N31******                                40'-DR-'
     A**31******                                40'-CR-'
     A**********  RRACNO        10A       47TEXT('Account No.')
     A**********  RRCHNO        10A       60TEXT('Cheque No.')
     A**********  RRNARR        30A       73TEXT('Narrative')
     A**********  RRCCY          3A      105TEXT('Currency')
     A**********  RRAMT         20A      109TEXT('Amount')
     A            RRTREF        15A        2TEXT('Transaction Ref')
     A            RRCRNM        20A       18TEXT('Customer Name')
     A N31                                39'-DR-'
     A  31                                39'-CR-'
     A            RRACNO        10A       44TEXT('Account No.')
     A            RRASOC         9A       55TEXT('Associated Customer')
     A            RRCHNO        10A       66TEXT('Cheque No.')
     A            RRNARR        30A       77TEXT('Narrative')
     A            RRCCY          3A      108TEXT('Currency')
     A            RRAMT         20A      112TEXT('Amount')
      **End of change MD057038
      *
      *-----------------------------*
      ***  Fee Detail Record Format *
      *-----------------------------*
     A          R DETAIL2
     A                                      SPACEA(2)
     A N30                                40'FEE'
     A N30        RRFEE          1A       44TEXT('Fee Number')
      **Start of change MD057038
     A*N30*****   RRFCCY         3A      105TEXT('Fee Currency')
     A*N30*****   RRFAMT        20A      109TEXT('Fee Amount')
     A N30        RRFCCY         3A      108TEXT('Fee Currency')
     A N30        RRFAMT        20A      112TEXT('Fee Amount')
      **End of change MD057038
     A  30                                19'EXCHANGE RATE'
     A  30        RREXRT        13  8     47TEXT('Exchange Rate')
     A                                      EDTCDE(3)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'NO OF ACCOUNT TRANSFERS'
     A                                      SPACEB(3)
     A            NORE           5  0     75EDTCDE(3)
     A                                    46'***  END OF REPORT FOR +
     A                                      BRANCH'
     A                                      SPACEB(2)
     A            RRBRCD    R             76REFFLD(BRCA)
     A                                    80'***'
      *
