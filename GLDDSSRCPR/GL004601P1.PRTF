      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Generated Entries Account Transfer-Detail')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL004601P1 - Midas GL Generated Entries Account         *
      *                    Transfer - Detail                          *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD057038           Date 11Nov20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CAP204 *CREATE     Date 05Feb10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD057038 - Added field for Associated Customer.              *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP204 - Teller Related APIs - Account Transfer              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL004601P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    42'ACCOUNT TRANSFER INPUT +
     A                                      TODAY RUN CONTROLS'
      *
     A                                    89'BRANCH -'
     A            RRBRCD    R             98REFFLD(BRCA)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    42'-----------------------+
     A                                      ------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A**********                           2'TRANSACTION'                                  MD057038
     A**********                          19'CUSTOMER NAME'                                MD057038
     A**********                          45'ACCOUNT NO'                                   MD057038
     A**********                          60'CHEQUE NO'                                    MD057038
     A**********                          73'NARRATIVE'                                    MD057038
     A**********                         105'CCY'                                          MD057038
     A**********                         109'AMOUNT'                                       MD057038
     A                                     2'TRANSACTION'                                  MD057038
     A                                    18'CUSTOMER NAME'                                MD057038
     A                                    44'ACCOUNT NO'                                   MD057038
     A                                    55'ASSOC CUST'                                   MD057038
     A                                    66'CHEQUE NO'                                    MD057038
     A                                    77'NARRATIVE'                                    MD057038
     A                                   108'CCY'                                          MD057038
     A                                   112'AMOUNT'                                       MD057038
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A                                      SPACEA(2)
      *
     A**********  RRTREF        15A        2TEXT('Transaction Ref')                        MD057038
     A**********  RRCRNM        20A       19TEXT('Customer Name')                          MD057038
     A*N31******                                40'-DR-'                                   MD057038
     A**31******                                40'-CR-'                                   MD057038
     A**********  RRACNO        10A       47TEXT('Account No.')                            MD057038
     A**********  RRCHNO        10A       60TEXT('Cheque No.')                             MD057038
     A**********  RRNARR        30A       73TEXT('Narrative')                              MD057038
     A**********  RRCCY          3A      105TEXT('Currency')                               MD057038
     A**********  RRAMT         20A      109TEXT('Amount')                                 MD057038
     A            RRTREF        15A        2TEXT('Transaction Ref')                        MD057038
     A            RRCRNM        20A       18TEXT('Customer Name')                          MD057038
     A N31                                39'-DR-'                                         MD057038
     A  31                                39'-CR-'                                         MD057038
     A            RRACNO        10A       44TEXT('Account No.')                            MD057038
     A            RRASOC         9A       55TEXT('Associated Customer')                    MD057038
     A            RRCHNO        10A       66TEXT('Cheque No.')                             MD057038
     A            RRNARR        30A       77TEXT('Narrative')                              MD057038
     A            RRCCY          3A      108TEXT('Currency')                               MD057038
     A            RRAMT         20A      112TEXT('Amount')                                 MD057038
      *
      *-----------------------------*
      ***  Fee Detail Record Format *
      *-----------------------------*
     A          R DETAIL2
     A                                      SPACEA(2)
     A N30                                40'FEE'
     A N30        RRFEE          1A       44TEXT('Fee Number')
     A*N30*****   RRFCCY         3A      105TEXT('Fee Currency')                          MD057038
     A*N30*****   RRFAMT        20A      109TEXT('Fee Amount')                            MD057038
     A N30        RRFCCY         3A      108TEXT('Fee Currency')                          MD057038
     A N30        RRFAMT        20A      112TEXT('Fee Amount')                            MD057038
     A  30                                19'EXCHANGE RATE'
     A  30        RREXRT        13  8     47TEXT('Exchange Rate')
     A                                      EDTCDE(3)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'NO OF ACCOUNT TRANSFERS'
     A                                      SPACEB(3)
     A            NORE           5  0     75EDTCDE(3)
     A                                    46'***  END OF REPORT FOR +
     A                                      BRANCH'
     A                                      SPACEB(2)
     A            RRBRCD    R             76REFFLD(BRCA)
     A                                    80'***'
      *
