      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL Account Tranfer Events Generation Audit')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL004602AU - Midas GL Account Transfer Events           *
      *                    Generation Audit Report                    *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CAP204  *CREATE    Date 09Feb10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP204 - Teller Related APIs - Account Transfer              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL004602AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A                                    46'ACCOUNT TRANSFER EVENTS FILE +
     A                                       CREATION - AUDIT'
     A                                      SPACEA(1)
     A                                    46'-----------------------------+
     A                                      ------------------'
     A                                      SPACEA(2)
     A*
      *------------------*
      ***  File Control  *
      *------------------*
     A          R CONTROL
     A                                    58'CALCULATED'
     A                                    80'INTEGER'
     A                                    97'DECIMAL'
     A                                      SPACEA(1)
     A                                    58'----------'
     A                                    80'-------'
     A                                    97'-------'
     A                                      SPACEA(3)
 
     A                                    10'Account Transfer - PF/GLATMIPD'
     A
     A            NORE           5  0     60EDTCDE(3)
     A            HRWN          15  0     77EDTCDE(3)
     A            HRDC           3  0     99EDTCDE(3)
 
     A                                      SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A*
      *
      *-------------------*
      ***  Database Error *
      *------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8        30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
      *------------------*
      ***  End of Report *
      *------------------*
      *
     A          R ENDRPT
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(2)
      *
