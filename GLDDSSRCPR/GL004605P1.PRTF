      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Failed Account Transfer Report - w/error')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL004605P1 - Midas GL Failed Account Transfer Report    *
      *                    with Error                                 *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. BUG27831A *CREATE  Date 03Aug10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG27831A - Forward Account Transfer Processing              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL004605P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    50'LIST OF FAILED ACCOUNT +
     A                                      TRANSFER'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    50'-----------------------+
     A                                      --------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
     A                                      SPACEB(2)
      *
     A                                     3'Transaction Reference:'
     A            RTREF         15A       26TEXT('Transaction Ref')
     A                                    48'Execution Date:'
     A            REXDT          7A       64TEXT('Execution Date')
     A                                      SPACEA(1)
     A                                     6'Type/Subtype:'
     A            RTRTY          2A       20TEXT('Type')
     A            RTRST          5A       23TEXT('Subtype')
     A                                    33'Debit Account:'
     A            RDBBR          3A       48TEXT('DR Branch')
     A                                    51'-'
     A            RDBCU          6A       52TEXT('DR Customer')
     A                                    58'-'
     A            RDBCY          3A       59TEXT('DR Currency')
     A                                    62'-'
     A            RDBAC         10A       63TEXT('DR Account Code')
     A                                    73'-'
     A            RDBSQ          2A       74TEXT('DR Account Seq No')
     A                                    81'Credit Account:'
     A            RCRBR          3A       97TEXT('CR Branch')
     A                                   100'-'
     A            RCRCU          6A      101TEXT('CR Customer')
     A                                   107'-'
     A            RCRCY          3A      108TEXT('CR Currency')
     A                                   111'-'
     A            RCRAC         10A      112TEXT('CR Account Code')
     A                                   122'-'
     A            RCRSQ          2A      123TEXT('CR Account Seq No')
      *
      *-------------------------------*
      ***  Error Detail Record Format *
      *-------------------------------*
     A          R DETAILE
     A                                      SPACEB(1)
     A  30                                 6'Error Description/s:'
     A            RERRM        100A       27TEXT('Error Message')
      *
      *--------------------------*
      ***  No Details to Report  *
      *--------------------------*
      *
     A          R NODTLP1
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(001)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    55'***  END OF REPORT  ***'
     A                                      SPACEB(2)
      *
