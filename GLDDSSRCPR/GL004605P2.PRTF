      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Failed Account Transfer Report - No Resp')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL004605P2 - Midas GL Failed Account Transfer Report    *
      *                    No Response                                *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD057038           Date 11Nov20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 BUG27831A *CREATE  Date 03Aug10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD057038 - Additional of Debit/Credit Associated Customer.   *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG27831A - Forward Account Transfer Processing              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL004605P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    44'LIST OF FAILED ACCOUNT +
     A                                      TRANSFER - NO RESPONSE'
     A                                      SPACEA(1)
      *
     A                                    44'-----------------------+
     A                                      ----------------------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL2
     A                                      SPACEB(1)
      *
     A  30                                 3'Transaction Reference'
     A  30                                29'Execution'
     A  30                                43'Transaction'
     A  30                                59'Debit Account'
     A  30                                92'Credit Account'
     A  30                                  SPACEA(1)
     A  30                                29'Date'
     A  30                                43'Type/Subtype'
      *
     A                                      SPACEA(1)
     A            RTREF2        15A        3TEXT('Transaction Ref')
     A            REXDT2         7A       29TEXT('Execution Date')
     A            RTRTY2         2A       43TEXT('Type')
     A                                    45'-'
     A            RTRST2         5A       46TEXT('Subtype')
     A            RDBBR2         3A       59TEXT('DR Branch')
     A                                    62'-'
     A            RDBCU2         6A       63TEXT('DR Customer')
     A                                    69'-'
     A            RDBCY2         3A       70TEXT('DR Currency')
     A                                    73'-'
     A            RDBAC2        10A       74TEXT('DR Account Code')
     A                                    84'-'
     A            RDBSQ2         2A       85TEXT('DR Account Seq No')
     A            RCRBR2         3A       92TEXT('CR Branch')
     A                                    95'-'
     A            RCRCU2         6A       96TEXT('CR Customer')
     A                                   102'-'
     A            RCRCY2         3A      103TEXT('CR Currency')
     A                                   106'-'
     A            RCRAC2        10A      107TEXT('CR Account Code')
     A                                   117'-'
     A            RCRSQ2         2A      118TEXT('CR Account Seq No')
      **Start of change MD057038
     A                                      SPACEA(1)
     A  30                                59'Debit Associated Customer'
     A  30                                92'Credit Associated Customer'
     A                                      SPACEA(1)
     A            RDASC2         6A       59TEXT('Debit Associated Customer')
     A            RCASC2         6A       92TEXT('Credit Associated Customer')
      **Start of change MD057038
      *
      *--------------------------*
      ***  No Details to Report  *
      *--------------------------*
      *
     A          R NODTLP2
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(001)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP2
     A                                    55'***  END OF REPORT  ***'
     A                                      SPACEB(2)
      *
