      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL Account Transfer Report - Audit')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL004610AU - Midas GL Account Transfer Report - Audit   *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CAP204  *CREATE    Date 18Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP204 - Retail Account Transfer                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
 
      **-------------------------------*
      **  Report Header Record Format  *
      **-------------------------------*
 
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
 
     A                                      SKIPB(3)
 
     A                                     6'REFERENCE GL004610AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                    +1PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
 
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
 
     A  01                                47'TRANSFERS CHANGED +
     A                                       TODAY REPORT - AUDIT'
     A  01                                  SPACEB(1)
 
     A  01                                47'------------------+
     A                                       --------------------'
     A  01                                  SPACEB(1)
 
     A  02                                50'ACCOUNT TRANSFERS LIST - +
     A                                       AUDIT'
     A  02                                  SPACEB(1)
 
     A  02                                50'-------------------------+
     A                                       -----'
     A  02                                  SPACEB(1)
 
     A  03                                46'HISTORIC ACCOUNT TRANSFERS LIST - +
     A                                       AUDIT'
     A  03                                  SPACEB(1)
 
     A  03                                46'----------------------------------+
     A                                       -----'
     A  03                                  SPACEB(1)
 
      **---------------------*
      **  File Controls      *
      **---------------------*
 
     A          R CONTROL
     A                                    46'CALCULATED'
     A                                      SPACEB(4)
     A                                    46'----------'
     A                                      SPACEB(1)
 
     A                                     7'TOTAL NUMBER OF RECORDS READ    :'
     A                                      SPACEB(1)
     A            RCOUNT         8  0     48EDTCDE(3)
 
     A                                     7'TOTAL NUMBER OF RECORDS INSERTED:'
     A                                      SPACEB(1)
     A            RINSRT         8  0     48EDTCDE(3)
 
     A                                     7'TOTAL NUMBER OF RECORDS AMENDED :'
     A                                      SPACEB(1)
     A            RAMEND         8  0     48EDTCDE(3)
 
     A                                     7'TOTAL NUMBER OF RECORDS DELETED :'
     A                                      SPACEB(1)
     A            RDELET         8  0     48EDTCDE(3) SPACEA(1)
 
      **--------------*
      **  No Details  *
      **--------------*
 
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
 
      **---------------------*
      **  Database Error     *
      **---------------------*
 
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
      *------------------*
      ***  End of Report *
      *------------------*
      *
     A          R ENDRPT
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(2)
      *
