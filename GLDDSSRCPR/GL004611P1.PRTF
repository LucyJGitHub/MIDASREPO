      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas GL HouseKeeping-GLMEMOT2 Report')                *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL004611P1 - Midas GL HouseKeeping-GLMEMOT2 Report      *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. MD060909  *CREATE  Date 14Feb23               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD060909 - After COB job FTC0630 go in MSGW.                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R GL004611H1                TEXT('(c) Finastra Internatio +
     A                                      nal Limited 2023')
     A                                      SKIPB(1)
     A                                     6'REFERENCE GL004611P1'
     A                                   101'DATE:'
     A                                   106DATE
     A                                      EDTWRD('  /  /  ')
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                    30'Housekeeping of MEMOS Shadow file
     A                                       - GLMEMOT2'
     A                                      SPACEB(1)
     A                                   101'TIME:'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    30'----------------------------------
     A                                      -----------'
     A                                      SPACEA(2)
      *
     A          R GL004611H2
     A                                     1'RETAIL NO'
     A                                    12'CUST NO'
     A                                    21'CCY'
     A                                    26'ACT SEQ'
     A                                    34'BRANCH'
     A                                    43'VALUE DATE'
     A                                    58'AMOUNT'
     A                                    80'TIMESTAMPS'
     A                                      SPACEA(1)
      *
     A          R GL004611D1
     A            PACNO         10S        1 EDTCDE(Z)
     A            PCNUM          6A       12
     A            PCCY           3A       21
     A            PACSQ          2S 0     26 EDTCDE(Z)
     A            PBRCA          3A       34
     A            PVDATEA        7A       43
     A            PAMT          15S 0     58 EDTCDE(J)
     A            PTMST           Z       80
     A                                      SPACEA(1)
      *
     A          R GL004611T
     A                                      SPACEB(1)
     A                                    10'NUMBER OF RECORDS WERE DELETED'
     A                                    43
     A                                      '='
     A            PCTR           6S 0O    45
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
     A          R GL004611E
     A                                      SPACEB(1)
     A                                    54'*** END OF REPORT ***'

     A*%%RS+<record-sequences>
     A*%%RS+ <sequence name="Untitled">
     A*%%RS+  <device type="printer" width="132" height="66" />
     A*%%RS+  <record-write record-format="GL004611H1" />
     A*%%RS+ </sequence>
     A*%%RS </record-sequences>
