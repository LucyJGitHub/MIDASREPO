      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas GL Stamp Tax Posting Summary Detail Report')     *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger                                       *
      *                                                               *
      *  PRTF/GL008111P1 - Stamp Tax Posting Summary Report           *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER049 *Create     Date 06Dec10               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
      **
     A                                      REF(MFRFL)
      **   *******************
      **   Page Headings
      **   *******************
      **
     A          R HEADP1                    TEXT('(C) Finastra +
     A                                      International +
     A                                      Limited 2010')
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL008111P1'
     A            BJURPT    R             39REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z) SPACEA(2)
      *
     A                                    40'STAMP TAX POSTING'
     A                                    59' SUMMARY - DETAIL REPORT'
     A                                      SPACEA(1)
     A                                    40'-----------------'
     A                                    57'------------------------'
     A                                      SPACEA(2)
     A                                    06'BRANCH'
     A                                    14'BRANCH CUST REF'
     A                                    30'CCY'
     A                                    35'A/C CODE'
     A                                    44'A/C SEQ'
     A                                    56'NARRATIVE   '
     A                                    91'DEBIT/CREDIT'
     A                                   106'POSTING AMOUNT'
     A                                      SPACEA(2)
      **
      **   ***********************
      **   Detail Part Of Report 1
      **   ***********************
      **
     A          R DETLP1
     A            BRCA      R             07
     A            CNUM           6        17
     A            CCY       R             30
     A            ACOD      R             34
     A            ACSQ      R             46
     A            PNAR      R             52
     A            DRCR      R             96
     A            PSTAR         13       107
     A                                      SPACEA(1)
      **
      **   ***********************
      **   ACOD Level Break Line
      **   ***********************
      **
     A          R TOTAL
     A                                   107'----------------'
     A                                      SPACEA(1)
     A            TOTAR         13       107
     A            SIGN           2       121
     A                                      SPACEA(2)
      **   *******************
      **   Report Trailer
      **   *******************
      **
     A          R TRAILP1                   SPACEB(2)
     A                                    56'*** END OF REPORT ***'
      **   **************
      **   Database Error
      **   **************
      **
     A          R ERRAP                     SPACEB(2)
     A                                    56'*** DATABASE ERROR ***'
