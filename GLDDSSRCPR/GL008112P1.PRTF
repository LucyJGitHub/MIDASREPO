      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas GL Stamp Tax A/c Keys & Generated Ents Rept')    *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger                                       *
      *                                                               *
      *  PRTF/GL008112P1 - Stamp Tax A/c Keys & Generated Ents Report *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      *                 CER049 *C *CREATE  Date 06Dec10               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **   *******************
      **   PAGE HEADINGS
      **   *******************
      **
     A          R HEADP1                    SKIPB(3)
     A                                     6'REFERENCE GL008112P1'
     A            BJURPT        53A       39
     A                                   101'DATE'
     A            BJMRDT         7A      106
     A                                   119'PAGE'
     A            PAGE2        004S 0    125EDTCDE(Z) SPACEA(2)
      *
     A                                    30'STAMP TAX '
     A                                    40'A/C KEYS AND GENERATED EN'
     A                                    65'TRIES'
      *
     A                                      SPACEA(1)
     A                                    30'----------'
     A                                    40'-------------------------'
     A                                    65'-----'
     A                                      SPACEA(2)
     A                                    04'ACCOUNT KEY'
     A                                    20'ORIGINAL ACCOUNT'
     A                                    49'LOAN  '
     A                                    57'DEAL  '
     A                                    63'MISSING'
     A                                    72'EVENT'
     A                                    79'EVENT'
     A                                    86'EVENT AMOUNT'
     A                                   106'REV'
     A                                      SPACEA(1)
     A                                    49'NUMBER'
     A                                    57'NUMBER'
     A                                    63'IND'
     A                                    72'DATE'
     A                                    79'CCY'
     A                                   106'IND'
      **
      **   *****************************************
      **   DETAIL PART OF REPORT 2 - A/C KEY
      **   *****************************************
      **
     A          R DETLP21                   SPACEB(2)
     A            AKEY          14A       04
     A            ORIGAC        28A       20
      **Start of change CLE148
     A**********  LNNO           6S 0     49                                                  CLE148
     A            LNNO           6A       49
      **End of change CLE148
     A            DLNO           6S 0     57
     A            SMISS          1A       66
     A            YEVDT          7A       71
     A            EVCY           3A       80
     A            EDEVAM        21A       84
     A            RVRS           1A      107
      **
      **   *****************************************
      **   DETAIL PART OF REPORT 3 - GENERATED ENTRY
      **   *****************************************
      **
     A          R DETLP31                   SPACEB(1)
     A            P1BRCA    R             10REFFLD(BRCA)
     A            P1CNUM    R             15REFFLD(CNUMA)
     A            P1CCY     R             23REFFLD(CCY)
     A            P1ACOD    R             28REFFLD(ACOD)
     A            P1ACSQ    R             34REFFLD(ACSQ)
     A            PVALD          7A       48
     A            PPSTD          7A       58
     A            PNAR      R             68
     A            PPSTA         21A      100
     A            PDRCR          2A      121
      **   *******************
      **   REPORT TRAILER
      **   *******************
      **
     A          R TRAILP1                   SPACEB(2)
     A                                    56'*** END OF REPORT ***'
