     A*****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas GL Stamp Tax RE Dr Int/Avg Balance Detail')      *
     A*****************************************************************
     A*                                                               *
     A*   Midas - General Ledger                                      *
     A*                                                               *
     A*   PRTF/GL008131P1 - Stamp Tax on Debit Interest & Average     *
     A*                     Balances - Detail                         *
     A*                                                               *
     A*  (c) Finastra International Limited 2010                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER049  *CREATE    Date 06Dec10               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CER049 - Stamp Tax                                           *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited 2010')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'GL008131P1'
     A            BJURPT        53        30
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A            TITLE         40A       46
     A                                    38'STAMP TAX ON AVERAGE BALANCES AND '
     A                                    72'ON RETAIL DEBIT INTEREST'
     A*
     A                                     4'Account Key'
     A                                      SPACEB(3)
     A                                    26'Reference Key'
     A                                    55'Posting Date'
     A                                    69'Value Date'
     A                                    81'Ccy'
     A                                    86'Orig. Key/Avg Balance'
     A                                     4'Tax Amount'
     A                                      SPACEB(1)
     A                                    27'Rate'
     A                                    35'Tax Debit Acc'
     A                                    65'Rev'
     A            TRATT        132A        1
     A                                      SPACEB(1)
     A                                      SPACEA(1)
     A*****************************************************************
     A          R DETDP1                    SPACEB(1)
     A            @AKEY         20A        4
     A            @KEYRF        28A       26
     A            @PSTDT         7A       56
     A            @VALDT         7A       70
     A            @CCY           3A       81
     A            @EAMT         21A       86
     A            @TAXO         21A        4
     A                                      SPACEB(1)
     A  71        @TAXR          5S 3     27EDTWRD(' 0.   ')
     A            @DBACC        28A       35
     A            @REVIN         1A       65
     A
     A*****************************************************************
     A          R ENDDP1                    SPACEB(3)
     A                                    57'** END OF REPORT **'
