     A*****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas GL Stamp Tax DL/LE/RE Up To Date Avg Bal Det')   *
     A*****************************************************************
     A*                                                               *
     A*   Midas - General Ledger                                      *
     A*                                                               *
     A*   PRTF/GL008131P2 - Stamp Tax Lending, Dealing and Retail Up  *
     A*                     To Date Average Balance Report            *
     A*                                                               *
     A*  (c) Finastra International Limited 2010                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER049  *CREATE    Date 06Dec10               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CER049 - Stamp Tax                                           *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R HEADP2                    TEXT('(c) Finastra International +
     A                                      Limited 2011')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'GL008131P2'
     A            BJURPT        53        30
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    80'UP TO DATE AVERAGE BALANCE REPORT'
     A*
     A                                     4'Reference'
     A                                      SPACEB(3)
     A                                    33'Start Date'
     A                                    44'End Date'
     A                                    53'      Opening Balance'
     A                                    75'      Closing Balance'
     A                                    97'      Average Balance'
     A                                     4'           Tax posted'
     A                                      SPACEB(1)
     A                                    26'       Tax/Adjustment'
     A                                    48'Posting Date'
     A            TRATT2       132A        1
     A                                      SPACEB(1)
     A*****************************************************************
     A          R DETDP2                    SPACEB(1)
     A*
     A            @OREFR        28A        4
     A*
     A            @OSTDT         7A       33
     A            @OENDT         7A       44
     A            @OOPNB        21A       53
     A            @OCURB        21A       75
     A            @OAVGB        21A       97
     A            @OCALT        21A        4
     A                                      SPACEB(1)
     A            @OTAXE        21A       26
     A            @OPSDT         7A       48
     A*
     A*****************************************************************
     A          R DETDP3                    SPACEB(1)
     A*
     A*****************************************************************
     A          R TOTCCH                    SKIPB(3)
     A*
     A                                     6'REFERENCE'
     A                                    16'GL008131P2'
     A            BJURPY        53        30
     A                                   100'DATE'
     A            BJMRDY         7       105
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    80'UP TO DATE AVERAGE BALANCE REPORT'
     A*
     A                                    51'Ccy'
     A                                       SPACEB(3)
     A                                    57'Date'
     A                                    65'           Tax Posted'
     A                                    88'       Tax Adjustment'
     A                                   111'       Spot Rate'
     A*
     A                                    51'----------------------------------'
     A                                       SPACEB(1)
     A                                    85'----------------------------------'
     A                                   119'--------'
     A*****************************************************************
     A          R TOTCCD                    SPACEB(1)
     A*
     A            @O2CCY         3        51
     A            @O2CDT         7        56
     A            @O2TXP        21        65
     A            @O2TXA        21        88
     A            @O2SPR        16       111
     A*
     A*****************************************************************
     A          R ENDDP2                    SPACEB(3)
     A                                    90'** END OF REPORT **'
