      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Stamp Tax Retail Movements Report')           *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL008162P1 - Stamp Tax Movements Report - Retail        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd 2010             *
      *                                                               *
      *  Last Amend No. CER049  *CREATE    Date 06Dec10               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2010')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'GL008162P1'
     A            BJURPT        53        30
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A            TITLE         40A       46
     A                                    40'STAMP TAX ON AVERAGE BALANCES/RETA-
     A                                      IL DEBIT INTEREST'
     A                                      SPACEA(1)
     A                                    40'-----------------------------------
     A                                      -----------------'
     A                                      SPACEA(1)
      *
     A                                     1'Account:'
     A                                      SPACEB(2)
     A            W#BRCA         3        10
     A                                    13'-'
     A            W#CUST         6        14
     A                                    20'-'
     A            W#CCY          3        21
     A                                    24'-'
     A            W#ACOD        10        25
     A                                    35'-'
     A            W#ACSQ         2        36
      *
     A                                     1'Stamp Tax Debit Account:'
     A                                      SPACEB(2)
      *
     A            WDBRCA         3        27
     A                                    30'-'
     A            WDCUST         6        31
     A                                    37'-'
     A            WDCCY          3        38
     A                                    41'-'
     A            WDACOD        10        42
     A                                    52'-'
     A            WDACSQ         2        53
      *
     A                                     1'From:'
     A                                      SPACEB(2)
      *
     A            STDAT          6        10
     A                                    18'To:'
     A            ENDAT          6        23
      *
     A                                     1'Posting'
     A                                      SPACEB(4)
     A                                    10' Value '
     A                                    19'     Opening Balance '
     A                                    41'     Closing Balance '
     A  20                                63'    Average Balance  '
     A N20                                63'      Debit Interest '
     A                                    85'          Tax Amount '
     A                                   108'Ccy'
     A                                   112'             Tax Rate'
      *
     A            TRATT        132         1
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *
     A          R DETDP1                    SPACEB(1)
     A            OPSTD          7         1
     A            OVALD          7        10
     A            OOPNBL        21        19
     A            OCLOBL        21        41
     A            OGROSS        21        63
     A            OTAXAM        21        85
     A            OCCY           3       108
     A            OTAXRT        21       112
      *
      ** Interest Header
      *
     A          R HEADP2                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2010')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'GL008162P1'
     A            BJURPI        53        30
     A                                   100'DATE'
     A            BJMRDI         7       105
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A            TITLEI        40A       46
     A                                    53'STAMP TAX ON DEBIT INTEREST'
     A                                      SPACEA(1)
      *
     A                                    53'---------------------------'
     A                                      SPACEA(1)
      *
     A                                     1'Account:'
     A                                      SPACEB(2)
     A            W#BRCI         3        10
     A                                    13'-'
     A            W#CUSI         6        14
     A                                    20'-'
     A            W#CCYI         3        21
     A                                    24'-'
     A            W#ACOI        10        25
     A                                    35'-'
     A            W#ACSI         2        36
      *
     A                                     1'Stamp Tax Debit Account:'
     A                                      SPACEB(2)
      *
     A            WDBRCI         3        27
     A                                    30'-'
     A            WDCUSI         6        31
     A                                    37'-'
     A            WDCCYI         3        38
     A                                    41'-'
     A            WDACOI        10        42
     A                                    52'-'
     A            WDACSI         2        53
      *
     A                                     1'From:'
     A                                      SPACEB(2)
      *
     A            STDATI         6        10
     A                                    18'To:'
     A            ENDATI         6        23
      *
     A                                     1'Posting'
     A                                      SPACEB(4)
     A                                    10' Value '
     A                                    19'      Debit Interest '
     A                                    42'          Tax Amount '
     A                                    65'Ccy'
     A                                    70'             Tax Rate'
      *
     A            TRATTI       132         1
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *
     A          R DETDP2                    SPACEB(1)
     A            OPSTI          7         1
     A            OVALI          7        10
     A            OGROSI        21        19
     A            OTAXAI        21        42
     A            OCCYI          3        65
     A            OTAXRI        21        70
      *
      ** Interest Header - Songle Movement
      *
     A          R HEADP3                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2010')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'GL008162P1'
     A            BJURP3        53        30
     A                                   100'DATE'
     A            BJMRD3         7       105
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A            TITLE3        40A       46
     A                                    54'STAMP TAX - RETAIL ACCOUNT'
     A                                      SPACEA(1)
      *
     A                                    54'--------------------------'
     A                                      SPACEA(1)
      *
     A                                     3'Account:'
     A                                      SPACEB(2)
      *
     A            W#CUS3         6        12
     A                                    18'-'
     A            W#BRC3         3        19
     A                                    22'-'
     A            W#CCY3         3        23
     A                                    26'-'
     A            W#ACO3        10        27
     A                                    37'-'
     A            W#ACS3         2        38
     A            BBCRN3        20        41
      *
     A                                    64'Stamp Tax Debit Account:'
      *
     A            WDBRC3         3        89
     A                                    92'-'
     A            WDCUS3         6        93
     A                                    99'-'
     A            WDCCY3         3       100
     A                                   103'-'
     A            WDACO3        10       104
     A                                   114'-'
     A            WDACS3         2       115
      *
     A                                     3'Posting Date:'
     A                                      SPACEB(4)
     A            OPST3          7        17
     A                                     3'Value Date:'
     A                                      SPACEB(1)
     A            OVAL3          7        17
      *
     A  20                                 3'Opening Date:'
     A  20                                  SPACEB(4)
     A  20        OSTD3          7        17
     A  20                                 3'Closing Date:'
     A  20                                  SPACEB(1)
     A  20        OCLO3          7        17
      *
     A  20                                 3'Opening balance:'
     A  20                                  SPACEB(3)
     A  20        OOPNCL        21        34
      *
     A  20                                 3'Closing Balance:'
     A  20                                  SPACEB(1)
     A  20        OCLOBL        21        34
      *
     A  20                                 3'Average Balance:'
     A  20                                  SPACEB(1)
     A N20                                 3'Debit Interest:'
     A N20                                  SPACEB(3)
     A            OGROS3        21        34
     A                                     3'Tax Rate:'
     A                                      SPACEB(2)
     A            OTXRT3        21        34
      *
     A                                     3'Tax Amount:'
     A                                      SPACEB(3)
     A            OTAXE3        21        34
     A            OCCY3          3        56
      *
     A                                     3'Tax Amount in Dr Account Ccy:'
     A                                      SPACEB(1)
     A            OTAXD3        21        34
     A            OCCD3          3        56
      *
     A                                     3'Tax Amount in Stamp Tax Ccy:'
     A                                      SPACEB(1)
     A            OTAXS3        21        34
     A            OCCS3          3        56
      *
     A          R ENDDP1                    SPACEB(3)
     A                                    57'** END OF REPORT **'
