      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas Stamp Tax Report')                               *
      *****************************************************************
      *                                                               *
      *  Midas - Stamp tax                                            *
      *                                                               *
      *   PRTF/GL008170P1 - Portuguese Stamp Tax Report               *
      *                                                               *
      *  (C) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. CER049   *CREATE   Date 06Dec10               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R HEADP1                    TEXT('(c) Misys     +
     A                                      International +
     A                                      Banking Systems LTD 2010')
     A                                      SKIPB(6)
     A            OBBCN1        35        45
     A            OBBCN3        35        45
     A                                      SPACEB(1)
     A                                    15'Customer'
     A                                      SPACEB(1)
     A            O#CNUM         6        24
     A            OBBCN4        35        45
      *
     A                                    15'Date'
     A                                      SPACEB(1)
     A            BJMRDT         7        24
      *
     A                                    10'Debit Advice on the '
     A                                      SPACEB(3)
     A                                    30'Calculation of Stamp Tax for'
     A                                    10'Credit Transaction Account or Term'
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A                                    45'Not Determined'
     A                                      HIGHLIGHT
      *
     A                                    10'Transaction Account'
     A                                      SPACEB(2)
     A            OREFT         28        32
     A                                    10'Debit Account'
     A                                      SPACEB(1)
     A            OREFD         28        32
     A                                    10'Please be informed '
     A                                      SPACEB(1)
     A                                    29'that we debited the following'
     A                                    10'on the above mentioned account'
     A                                      SPACEB(1)
     A                                    10'Details'
     A                                      SPACEB(2)
     A                                    10'Start Date'
     A                                      SPACEB(1)
     A            ODATI          7        45
     A                                    10'End Date'
     A                                      SPACEB(1)
     A            ODATF          7        45
     A                                    10'Stamp Tax Rate:'
     A                                      SPACEB(1)
     A            W#TXRT        21        38
     A                                    10'Gross Amount'
     A                                      SPACEB(1)
     A            W#GROS        21        31
     A            #2GCCY         3        53
     A                                    10'Stamp Tax in Account Ccy'
     A                                      SPACEB(1)
     A            W#TAXE        21        31
     A            W#CCYE         3        53
     A                                    10'Posting Date'
     A                                      SPACEB(1)
     A            W#PSDT         7        23
     A                                    34'Value Date'
     A            W#VLDT         7        45
     A                                    10'Debit Account Details'
     A                                      SPACEB(2)
      *****************************************************************
     A                                    10'_________________________________'
     A                                      SPACEB(1)
     A                                    10'Account Reference'
     A                                      SPACEB(1)
     A            #2DRAC        28        32
     A                                    10'Tax Amount'
     A                                      SPACEB(1)
     A            W#CCYD         3        32
     A            W#TAXD        21        37
      *****************************************************************
