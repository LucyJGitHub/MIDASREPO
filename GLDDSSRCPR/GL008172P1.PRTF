      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas GL Stamp Tax Movements Report')                  *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger                                       *
      *                                                               *
      *  PRTF/GL008172P1 - Stamp Tax Movements Report                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd 2010             *
      *                                                               *
      *  Last Amend No. CER049  *CREATE    Date 06Dec10               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2010')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'GL008172P1'
     A            BJURPT        53        30
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A            TITLE         40A       46
     A                                    74'STAMP TAX MOVEMENTS'
     A                                      SPACEA(1)
     A                                    74'-------------------'
     A                                      SPACEA(1)
      *
     A                                     1'Value Date:'
     A                                      SPACEB(2)
     A                                    22'From'
     A            O#VDATF        7        27
     A                                    39'To'
     A            O#VDATT        7        42
     A                                     1'Posting Date:'
     A                                      SPACEB(1)
     A                                    22'From'
     A            O#PDATF        7        27
     A                                    39'To'
     A            O#PDATT        7        42
      *
     A                                     1'Module:'
     A                                      SPACEB(1)
     A            O#MODU        10        22
      *
     A            OSELE1        15         1
     A                                      SPACEB(2)
     A            OSELE2        31        22
      *
     A                                     4'Transaction'
     A                                      SPACEB(2)
     A                                    37'Account Debited'
     A                                    67'Post Date'
     A                                    76'Val Date'
     A                                    85'Event Amount'
     A                                   108'Ccy'
     A                                     4'Event Type'
     A                                      SPACEB(1)
     A                                    16'Tax Rate'
     A                                    29'Tax Amount in Dr ccy'
     A                                    51'Rev'
     A                                    55'Module'
      *
     A            TRATT        132         1
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *
      *****************************************************************
     A          R DETDP1                    SPACEB(1)
     A            OASTER         1         2
     A            OREFT         31         4
     A            OREFD         28        37
     A            OPSTD          7        67
     A            OVALD          7        76
     A            OGROSS        21        85
     A            OCCY           3       108
     A            OEVENT        10         4
     A                                      SPACEB(1)
     A            OTAXRT        11        16
     A            OTAXAM        21        29
     A            OKREV          1        52
     A            OMODUL         9        55
      *
      *****************************************************************
     A          R ENDDP1                    SPACEB(3)
     A                                    57'** END OF REPORT **'
