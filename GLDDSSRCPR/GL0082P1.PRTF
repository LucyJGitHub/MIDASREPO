      *****************************************************************
/*STD *: PRTFP1                                                     : *
/*EXI *  TEXT('Journal Entry Detail Report')                          *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger                                       *
      *                                                               *
      *  PRTF/GL0082P1 - Journal Entry Detail Report')                *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. CGL082  *CREATE    Date 02Nov21               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL082 - Journal Entry User Audit                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
      *
     A          R HEADPY                    TEXT('(c) Finatrsa International +
     A                                      Limited 2021')
      *
      *
     A                                      SKIPB(3)
     A                                     1'REFERENCE GL0082P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A  30                               101'TIME'
     A  30                               106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  30                                37'**INTERIM**'
     A                                    49'JOURNAL ENTRY AUDIT +
     A                                      REPORT'
      *
     A  37                                 1'BRANCH - '
     A  37        A8BRCD    R             10REFFLD(BRCA)
     A  37        A8BRNM    R             14REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A  30                                37'------------'
     A                                    49'--------------------+
     A                                      ------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R HEADDT
     A                                     7'BATCH NUMBER'
     A                                    23'BATCH DESCRIPTION'
     A                                    58'BRANCH'
     A                                    69'DEPARTMENT'
     A                                    85'ACTION'
     A                                   100'USER ID'
     A                                   120'TIME'
     A                                      SPACEA(1)
     A                                     7'------------'
     A                                    23'-----------------'
     A                                    58'------'
     A                                    69'----------'
     A                                    85'------'
     A                                   100'-------'
     A                                   120'----'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            L#BTNB         3A       11
     A            L#BDES        30A       23
     A            L#BRCA         3A       60
     A            L#DPCD         3A       73
     A            L#ACTN         6A       85
     A            L#USID        10A      100
     A            L#HH           2A      118
     A                                   120':'
     A            L#MM           2A      121
     A                                   123':'
     A            L#SS           2A      124
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Blank Record Format  *
      *-------------------------*
      *
     A          R DETBLK
     A                                     1' '
     A                                      SPACEA(1)
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)

