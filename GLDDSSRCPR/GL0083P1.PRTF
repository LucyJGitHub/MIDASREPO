      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*STD *  PR_STD66                                                     *                     MD012161
/*EXI *  TEXT('Midas GL Automatic Account Closure Report')            *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL0083P1 - Midas GL Automatic Account Closure Report    *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. MD012161           Date 18Aug23               *
      *  Prev Amend No. CGL083 *Create     Date 18Aug23               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD012161 - GLC0083 in MSGW due to printer overflow. Add      *
      *             standard creation paramater for overflow 66.      *
      *  CGL083 - Automatic Account Closure                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  STATEMENT HEADER            *
      *--------------------------------*
      *
     A          R PGHEADER                  TEXT('(c) Finastra Internation+
     A                                      al Limited 2023')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL0083P1'
     A            BJURPT    R             39REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    45'AUTOMATIC ACCOUNT CLOSURE REPORT'
     A                                      SPACEA(1)
     A                                    45'--------------------------------'
     A                                      SPACEA(2)
      *
      *
      *------------------------*
      ***  STATEMENT HEADER    *
      *------------------------*
      *
     A          R DTHEADER
     A                                    10'Branch'
     A                                    21'Customer'
     A                                    31'Curr'
     A                                    37'Account'
     A*********************************** 47'A/C Seq'
     A                                    49'A/C Seq'
     A                                    58'Account'
     A                                    83'Last Movement'
     A                                    99'Date Account'
     A                                      SPACEA(1)
     A                                    11'Code'
     A                                    22'Number'
     A                                    31'Code'
     A                                    37'Code'
     A*********************************** 47'Number'
     A                                    49'Number'
     A                                    58'Name'
     A                                    86'Date'
     A                                   102'Opened'
     A                                      SPACEA(1)
     A                                    10'------'
     A                                    21'--------'
     A                                    31'----'
     A                                    37'-------'
     A*********************************** 47'-------'
     A                                    49'-------'
     A                                    58'--------------------'
     A                                    83'-------------'
     A                                    99'------------'
     A                                      SPACEA(2)
      *
      *------------------------*
      ***  STATEMENT DETAIL    *
      *------------------------*

     A          R R1DETAIL
      *
     A            P1BRCA         3        11
     A            P1CNUM         6        22
     A            P1CCY          3        31
     A            P1ACOD        10        37
     A*********** P1ACSQ         2        49
     A            P1ACSQ         2        51
     A            P1ANAM        20        58
     A            P1LMVD         7        86
     A            P1DACO         7       102
      *
     A                                      SPACEA(1)
      *
      *------------------------*
      ***  REPORT TRAILER      *
      *------------------------*
      *
     A          R TRAILER
      *
     A                                      SPACEB(5)
      *
     A  50                                51'NO RECORDS TO REPORT'
     A  50                                  SPACEA(5)
      *
      *
     A                                    49'***  END OF REPORT  ***'
      *
      *
