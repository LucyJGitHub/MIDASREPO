      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL Corrective Batch Reg Housekeeping - Audit')   *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL008901AU - Midas GL Corrective Batch Register         *
      *                    Housekeeping - Audit                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CSC054  *CREATE    Date 23Feb12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSC054 - Period End Adjustments                              *
      *                                                               *
      *****************************************************************
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE GL008901AU'
     A            BJURPT        53A       39
     A                                   101'DATE'
     A            BJMRDT         7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    39'Corrective Batch Register +
     A                                      File Housekeeping +
     A                                      - Audit'
     A                                      SPACEA(1)
     A                                    39'---------------------------+
     A                                      ------------------------'
     A                                      SPACEA(3)
      *
     A          R CONTROL
     A                                    39'GLCORBPD - Corrective Batch +
     A                                       Register File'
     A                                      SPACEA(2)
      *
     A                                    39'NUMBER OF RECORDS READ......:'
     A            RNREAD         5  0     70EDTCDE(3)
     A                                      SPACEA(1)
     A                                    39'NUMBER OF RECORDS PURGED....:'
     A            RNDELT         5  0     70EDTCDE(3)
     A                                      SPACEA(3)
      *
     A          R DBERROR
     A                                    48'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    35'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    35'*'
     A                                      SPACEB(1)
     A                                    88'*'
     A                                    35'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       54
     A                                    62''''
     A                                    88'*'
     A                                    35'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        54
     A                                    83 '''    *'
     A                                    35'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     54
     A                                    57''''
     A                                    88'*'
     A                                    35'*'
     A                                      SPACEB(1)
     A                                    88'*'
     A                                    35'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
