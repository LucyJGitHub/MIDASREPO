      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL External Batch Details Report')               *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL0150P1 - Midas GL External Batch Details Report       *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER046  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER046 - German Features LF033-00 Payments & Clearing        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADER01
     A                                      TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2008')
     A                                      SKIPB(001)
      *
     A                                     6'REFERENCE GL0150P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(003)
      *
     A                                    47'EXTERNAL BATCH +
     A                                      DETAILS REPORT'
     A                                      SPACEA(001)
     A                                    47
     A                                      '---------------+
     A                                      --------------'
     A                                      SPACEA(002)
      *
     A                                     2'Batch'
     A                                    10'Batch'
     A                                    18'Branch'
     A                                    26'Customer'
     A                                    76'Dr/'
     A                                    92'Narrative'
     A                                      SPACEA(001)
      *
     A                                     3'No.'
     A                                     9'Item No'
     A                                    19'Code'
     A                                    27'Number'
     A                                    37'Posting Amount'
     A                                    60'Account'
     A                                    76'Cr'
     A                                    80'Value Date'
     A                                    94'Code'
     A                                   114'Status'
     A                                      SPACEA(001)
      *
     A                                     2'-----'
     A                                     9'-------'
     A                                    18'-------'
     A                                    26'---------'
     A                                    36'----------------'
     A                                    54'--------------------'
     A                                    76'---'
     A                                    80'----------'
     A                                    92'----------'
     A                                   104'----------------+
     A                                      ---------'
     A                                      SPACEA(001)
      *
      ** Detail Record Format
      *
     A          R DETAIL01
      *
     A  87        RBBTNB         3  0      3TEXT('Batch number')
     A  87        RBBINB         3  0     11TEXT('Batch item no.')
     A  87        RBIBCA         3        20TEXT('Input Branch Code')
     A  87        RBCUST         6        27TEXT('Customer number')
     A  87        RBPTAM        16        36TEXT('Posting amount')
     A  87        RBACCD        21        54TEXT('Account code')
     A  87        RBDCIN         1        77TEXT('Dr/Cr indicator')
     A  87        RBVLDT        10        80TEXT('Value Date')
     A  87        RBNVCD         2        95TEXT('Narrative code')
     A            RBSTAT        25       104TEXT('Status')
     A                                      SPACEA(001)
      *
