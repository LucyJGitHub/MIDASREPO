      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Interest tax not in base ccy report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL0201P1 - Interest Tax not in Base Currency Report     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL013  *CREATE    Date 08Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL013 - Tiered Withholding Tax                              *
      *           Print details of tax deducted from interest today   *
      *           for MM deals & Retail accounts not in Base Currency *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **  Report Header
     A          R HEADER                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL0201P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    53'TAX NOT IN BASE CURRENCY'
     A                                      SPACEA(1)
     A                                    53'------------------------'
     A                                      SPACEA(2)
     A                                     2'Branch'
     A                                    12'Deal No./Account No.'
     A                                    36'Customer/Account Shortname'
     A                                    68'Value Date'
     A                                    83'Tax Amount (Ccy)'
     A                                   106'Tax Amount (Base)'
      *
      *---------------------------------------------------------------*
      *
      **  Currency header
     A          R CURHDR
     A                                      SPACEB(2)
     A                                     2'CURRENCY:'
     A            ECURR     R             12REFFLD(CYCD)
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Report details
     A          R DETAIL
     A  67                                  SPACEB(1)
     A  67        EBRCA     R              2REFFLD(BRCA)
     A  66        WDLNUM         6  0     12
     A  66 68     WCUST1         6        36
     A N66        EDLAC     R             12REFFLD(ACNO)
     A N66        WCUST2         6        36
     A N66                                42'-'
     A N66        EACCD     R             43REFFLD(ZZ010)
     A N66                                47'-'
     A N66        EACSQ     R             48REFFLD(ACSQ)
     A     68     BBCSSN        10        53
     A            CVDAT          7        69
     A            CURTAX        22        79
     A            BASTAX        22       103
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Report footer
     A          R FOOTER
     A                                      SPACEB(3)
     A                                    50'*** END OF REPORT ***'
