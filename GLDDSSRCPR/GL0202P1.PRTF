      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('WHT Deductions by Customer report')
     ******************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL0202P1 - WHT Deductions by Customer Report            *
      *                                                               *
      *  (c) Finastra International Limited 2006                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL013  *CREATE    Date 08Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL013 - Tiered Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **  Report Header
     A          R HEADER                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2006')
     A                                     6'REFERENCE GL0202P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(4)
     A                                      SPACEA(2)
     A                                    39'WITHHOLDING TAX DEDUCTIONS'
     A                                    66'BY CUSTOMER'
     A                                    82'BRANCH -'
     A            EBRCA     R             91REFFLD(BRCA)
     A                                      SPACEA(1)
     A                                    39'-------------------------'
     A                                    64'-------------'
     A                                      SPACEA(2)
     A                                     2'Customer'
     A                                    12'Deal/Account No.'
     A                                    30'Ccy'
     A                                    36'CR Int. this month'
     A                                    60'WHT this month'
     A                                    77'CR Int. year to date'
     A                                   102'WHT year to date'
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Report details
     A          R DETAIL
     A  67                                  SPACEB(1)
     A  67        ECUST     R              2REFFLD(ZZ006)
     A N65        EDLAC     R             14REFFLD(ACNO)
     A  65        WDLNUM         6  0     18
     A  68        ECURR     R             30REFFLD(CYCD)
     A            PCRTM         19        37
     A            PWTTM         19        57
     A            PCRYD         19        80
     A            PWTYD         19       101
     A            STAR1          1       119
     A            STATUS         7       121
     A            STAR2          1       129
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Currency Total
     A          R CCYTTL
     A                                    36'-------------------------'
     A                                    61'-------------------------'
     A                                    86'-------------------------'
     A                                   111'-------'
     A                                      SPACEA(1)
     A                                    16'TOTAL FOR CCY'
     A            OLDCCY         3        30
     A            PTINTM        21        35
     A            PTWHTM        21        55
     A            PTINTY        21        78
     A            PTWHTY        21        99
     A                                      SPACEA(1)
     A                                    36'-------------------------'
     A                                    61'-------------------------'
     A                                    86'-------------------------'
     A                                   111'-------'
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Branch footer
     A          R BCHFOOT
     A                                      SPACEB(1)
     A                                    49'*** END OF REPORT'
     A                                    67'FOR BRANCH'
     A            OLDBCH         3        78
     A                                    82'***'
      *
      *---------------------------------------------------------------*
      *
      **  Report footer
     A          R FOOTER
     A                                      SPACEB(3)
     A                                    56'*** END OF REPORT ***'
