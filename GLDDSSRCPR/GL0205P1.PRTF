      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('WHT ICD Installation report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL0205P1 - WHT ICD Installation report                  *
      *                                                               *
      *  (c) Finastra International Limited 2006                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL013  *CREATE    Date 08Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL013 - Tiered Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **  Report Header
     A          R HEADER                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2006')
     A                                     6'REREFENCE GL0205P1'
     A            BJURPT        53A  O    37TEXT('User Report Title')
     A                                   101'DATE'
     A            BJMRDT         7A  O   106TEXT('Midas Run Date')
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(4)
     A                                      SPACEA(2)
      *
      *----------------------------------------------------------------
      *
      ** Print Sub-heading if CAR is ON
     A          R SUBHD1
     A                                    32'WITHHOLDING TAX ICD INSTALLATION R-
     A                                      EPORT - TIERED WHT SWITCHED ON'
     A                                      SPACEA(1)
     A                                    32'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-
     A                                      ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'
     A                                      SPACEA(2)
     A                                    44'WHT RECORD'
     A                                    70'BEFORE UPDATE'
     A                                   102'AFTER UPDATE'
     A                                      SPACEA(1)
     A                                    19'FILE NAME'
     A                                    47'TYPE'
     A                                    65'TIERED TAX RATE LEVEL 1'
     A                                    97'TIERED TAX RATE LEVEL 1'
     A                                      SPACEA(2)
      *
      *----------------------------------------------------------------
      *
      ** Report Details if CAR is ON
     A          R DET1
     A            DBFILE    R             19TEXT('Database File')
     A            WTYPE         10        46TEXT('WHT Record Type')
     A            UETXR1         5  2     75TEXT('Before Tiered WHT Level 1')
     A                                      EDTCDE(J)
     A            TXWTRR         5  2    104TEXT('After Tiered WHT Level 1')
     A                                      EDTCDE(J)
      *
      *----------------------------------------------------------------
      *
      ** Print Sub-heading if CAR is OFF
     A          R SUBHD2
     A                                    32'WITHHOLDING TAX ICD INSTALLATION R-
     A                                      EPORT - TIERED WHT SWITCHED OFF'
     A                                      SPACEA(1)
     A                                    32'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-
     A                                      ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'
     A                                      SPACEA(2)
      *
      *----------------------------------------------------------------
      *
      ** Report column headings & details if CAR is OFF
     A          R DET2
     A                                    44'WHT RECORD'
     A                                    64'BEFORE UPDATE'
     A                                    86'AFTER UPDATE'
     A                                      SPACEA(1)
     A                                    27'FILE NAME'
     A                                    47'TYPE'
     A  60                                63'RETAIL WHT RATE'
     A  60                                85'RETAIL WHT RATE'
     A  60                                  SPACEA(1)
     A  61                                64'W/H TAX RATE'
     A  61                                86'W/H TAX RATE'
     A  61                                  SPACEA(1)
     A            DBFILE    R             27TEXT('Database File')
     A            WTYPE         10        47TEXT('WHT Record Type')
     A  60        UTXWTR         5  2     68TEXT('Before Retail WHT Rate')
     A                                      EDTCDE(J)
     A  61        UBNHKT         5  2     68TEXT('Before WHT Rate')
     A                                      EDTCDE(J)
     A            ETXR1          5  2     89TEXT('After Retail/WHT Rate')
     A                                      EDTCDE(J)
     A                                      SPACEA(2)
      *
      *---------------------------------------------------------------*
      *
      **  Report footer
     A          R FOOTER                    SPACEB(3)
     A                                    56'*** END OF REPORT ***'
