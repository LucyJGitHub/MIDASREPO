      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Account codes installation report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL0206P1 - WHT Extension File Update Report             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL013  *CREATE    Date 08Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL013 - Tiered Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **  Report Header
     A          R HEADER                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                     6'REFERENCE GL0206P1'
     A            BJURPT        53A  O    37TEXT('User Report Title')
     A                                   101'DATE'
     A            BJMRDT         7A  O   106TEXT('Midas Run Date')
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(4)
     A                                      SPACEA(2)
     A                                    39'WITHHOLDING TAX ACCOUNT CODES - AM-
     A                                      ENDED RECORDS'
     A                                      SPACEA(1)
     A                                    39'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-
     A                                      ~~~~~~~~~~~~~'
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      * Sub-heading
     A          R SUB1                      TEXT('CDL013 installed')
     A                                    42'TIERED WITHHOLDING TAX FEATURE SWI-
     A                                      TCHED ON'
     A                                      SPACEA(1)
     A                                    42'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-
     A                                      ~~~~~~~~'
     A                                      SPACEA(2)
     A                                    50'WHT'
     A                                    63'UPDATED RETAIL'
     A                                    84'UPDATED CORP.'
     A                                      SPACEA(1)
     A                                    29'ACCOUNT CODE'
     A                                    48'A/C CODE'
     A                                    65'A/C CODE'
     A                                    86'A/C CODE'
     A                                      SPACEA(1)
     A                                    29'~~~~~~~~~~~~'
     A                                    48'~~~~~~~~~'
     A                                    63'~~~~~~~~~~~~~~'
     A                                    84'~~~~~~~~~~~~~'
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      * Sub-heading
     A          R SUB2                      TEXT('CDL013 not installed')
     A                                    42'TIERED WITHHOLDING TAX FEATURE SWI-
     A                                      TCHED OFF'
     A                                      SPACEA(1)
     A                                    42'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-
     A                                      ~~~~~~~~~'
     A                                      SPACEA(2)
     A                                    69'UPDATED WHT'
     A                                      SPACEA(1)
     A                                    29'ACCOUNT CODE'
     A                                    48'RETAIL A/C CODE'
     A                                    70'A/C CODE'
     A                                      SPACEA(1)
     A                                    29'~~~~~~~~~~~~'
     A                                    48'~~~~~~~~~~~~~~~'
     A                                    69'~~~~~~~~~~~~'
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Report details
     A          R DET1                      TEXT('CDL013 installed')
     A            XRACCD        10        33TEXT('Account Code')
     A            XRWTAC        10        50TEXT('WHT A/C Code - Main File')
     A            XRWTRA        10        68TEXT('Updated WHT Reatil A/C Code')
     A            XRWTCA        10        88TEXT('Updated WHT Corp. A/C Code')
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Report details
     A          R DET2                      TEXT('CDL013 not installed')
     A            XRACCD        10        33TEXT('Account Code')
     A            XRWTRC        10        53TEXT('WHT Retail A/C Code')
     A            XRWHTA        10        72TEXT('Updated WHT A/C Code')
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Report footer
     A          R FOOTER
     A                                      SPACEB(3)
     A                                    56'*** END OF REPORT ***'
