      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Account keys installation report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL0207P1 - WHT Account Keys - Corporate Customers       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL013  *CREATE    Date 08Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL013 - Tiered Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(ACKEY)
      *
      **  Report Header
     A          R HEADER                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                     6'REFERENCE GL0207P1'
     A            BJURPT        53A  O    37TEXT('User Report Title')
     A                                   101'DATE'
     A            BJMRDT         7A  O   106TEXT('Midas Run Date')
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(4)
     A                                      SPACEA(2)
     A                                    25'WITHHOLDING TAX ACCOUNT KEYS CREAT-
     A                                      ED FOR CORPORATE CUSTOMERS - TIERED-
     A                                       WHT SWITCHED ON'
     A                                      SPACEA(1)
     A                                    25'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-
     A                                      ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-
     A                                      ~~~~~~~~~~~~~~~'
     A                                      SPACEA(2)
     A                                    15'NEW ACCOUNT KEY'
     A                                    41'* ----- DEBIT DETAILS ----- *'
     A                                    82'* ----- CREDIT DETAILS -----*'
     A                                      SPACEA(1)
     A                                    43'ONE        TWO       THREE'
     A                                    83'ONE         TWO       THREE'
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Report Details
     A          R DETAIL
     A            AKEY      R             16TEXT('Account Code Key')
     A            ACD1      R             41TEXT('Account Code 1')
     A            NCD1      R             46TEXT('Narrative Code 1')
     A            PRF1      R             49TEXT('Posting Reference 1')
     A            ACD2      R             52TEXT('Account Code 2')
     A            NCD2      R             57TEXT('Narrative Code 2')
     A            PRF2      R             60TEXT('Posting Reference 2')
     A            ACD3      R             63TEXT('Account Code 3')
     A            NCD3      R             68TEXT('Narrative Code 3')
     A            PRF3      R             71TEXT('Posting Reference 3')
     A            ACD4      R             82TEXT('Account Code 4')
     A            NCD4      R             87TEXT('Narrative Code 4')
     A            PRF4      R             90TEXT('Posting Reference 4')
     A            ACD5      R             93TEXT('Account Code 5')
     A            NCD5      R             98TEXT('Narrative Code 5')
     A            PRF5      R            101TEXT('Posting Reference 5')
     A            ACD6      R            104TEXT('Account Code 6')
     A            NCD6      R            109TEXT('Narrative Code 6')
     A            PRF6      R            112TEXT('Posting Reference 6')
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Report footer
     A          R FOOTER
     A                                      SPACEB(3)
     A                                    55'*** END OF REPORT ***'
