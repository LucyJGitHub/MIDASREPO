      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Customer details amendment report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL0208P1 - Customer Details Amendments for Tiered       *
      *                  Withholding Tax                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL013  *CREATE    Date 08Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL013 - Tiered Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(SDCUSTPD)
      *
      **  Report Header
     A          R HEADER                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                     6'REFERENCE GL0208P1'
     A            BJURPT        53A  O    37TEXT('User Report Title')
     A                                   101'DATE'
     A            BJMRDT         7A  O   106TEXT('Midas Run Date')
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(4)
     A                                      SPACEA(2)
      *
      *----------------------------------------------------------------
      *
      ** Print Sub-heading if CAR is ON
     A          R SUBHD1
     A                                    38'CUSTOMER DETAILS AMENDED - TIERED -
     A                                      WHT SWITCHED ON'
     A                                      SPACEA(1)
     A                                    38'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-
     A                                      ~~~~~~~~~~~~~~~'
     A                                      SPACEA(2)
     A                                    14'CUSTOMER NUMBER'
     A                                    33'CUSTOMER NAME'
     A                                    65'BEFORE UPDATE'
     A                                    98'AFTER UPDATE'
     A                                      SPACEA(1)
     A                                    58'CUSTOMER TYPE'
     A                                    76'TAX/NON-TAX'
     A                                    91'CUSTOMER TYPE'
     A                                   108'TAX/NON-TAX'
     A                                      SPACEA(1)
      *
      *----------------------------------------------------------------
      *
      ** Print Sub-heading if CAR is OFF
     A          R SUBHD2
     A                                    38'CUSTOMER DETAILS AMENDED - TIERED -
     A                                      WHT SWITCHED OFF'
     A                                      SPACEA(1)
     A                                    38'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-
     A                                      ~~~~~~~~~~~~~~~~'
     A                                      SPACEA(2)
     A                                    14'CUSTOMER NUMBER'
     A                                    33'CUSTOMER NAME'
     A                                    65'BEFORE UPDATE'
     A                                    98'AFTER UPDATE'
     A                                      SPACEA(1)
     A                                    58'CUSTOMER TYPE'
     A                                    76'TAX/NON-TAX'
     A                                    91'CUSTOMER TYPE'
     A                                   108'TAX/NON-TAX'
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Report Details
     A          R DETAIL
     A            BBCUST    R             18TEXT('Customer Number')
     A            BBCSSN    R             33TEXT('Customer Name')
     A            UBCSTY    R             64REFFLD(BBCSTY)
     A                                      TEXT('Cust. Type - Before')
     A            UBTAIN    R             81REFFLD(BBTAIN)
     A                                      TEXT('Tax Ind. - Before')
     A            BBCSTY    R             97TEXT('Cust. Type - After')
     A            BBTAIN    R            113TEXT('Tax Ind. - After')
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Report footer
     A          R FOOTER
     A                                      SPACEB(3)
     A                                    50'*** END OF REPORT ***'
