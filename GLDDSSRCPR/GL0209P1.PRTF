      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Set up tax indicator on deals report             3')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL0209P1 - Money Market Deal Updates for Tiered         *
      *                  Withholding Tax                              *
      *                                                               *
      *  (c) Finastra International Limited 2006                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL013  *CREATE    Date 08Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL013 - Tiered Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(DEALS)
      *
      **  Report Header
     A          R HEADER                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2006')
     A                                     6'REFERENCE GL0209P1'
     A            BJURPT        53A  O    37TEXT('User Report Title')
     A                                   101'DATE'
     A            BJMRDT         7A  O   106TEXT('Midas Run Date')
     A                                   119'PATE'
     A                                   124PAGNBR
     A                                      EDTCDE(4)
     A                                      SPACEA(2)
     A  60                                38'MONEY MARKET DEALS AMENDED - TIERE-
     A                                      D WHT SWITCHED ON'
     A                                      SPACEA(1)
     A  60                                38'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-
     A                                      ~~~~~~~~~~~~~~~~~'
     A                                      SPACEA(2)
     A  61                                38'MONEY MARKET DEALS AMENDED - TIERE-
     A                                      D WHT SWITCHED OFF'
     A                                      SPACEA(1)
     A  61                                38'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-
     A                                      ~~~~~~~~~~~~~~~~~~'
     A                                      SPACEA(2)
     A                                     9'DEAL NUMBER'
     A                                    30'CUSTOMER'
     A                                    44'DEAL TYPE/'
     A                                    62'BEFORE UPDATE'
     A                                    95'AFTER UPDATE'
     A                                      SPACEA(1)
     A                                    31'NUMBER'
     A                                    44'SUB TYPE'
     A                                    63'TAX/NON-TAX'
     A                                    95'TAX/NON-TAX'
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Report Details
     A          R DETAIL
     A            DLNO      R             12TEXT('Deal Number')
     A            CNUM      R             33TEXT('Customer Number')
     A            DTYP      R             46TEXT('Deal Type')
     A            DLST      R             49TEXT('Sub Type')
     A            #ETAXI         1        68TEXT('Tax/Non-Tax - Before')
     A            TAXI      R            100TEXT('Tax/Non Tax - After')
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Report footer
     A          R FOOTER
     A                                      SPACEB(3)
     A                                    50'*** END OF REPORT ***'
