      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Set up tax indicator on accounts report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL0210P1 - Account Details Extension File Update        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL013  *CREATE    Date 08Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL013 - Tiered Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(ACCNTBY0)
      *
      **  Report Header
     A          R HEADER                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                     6'REFERENCE GL0210P1'
     A            BJURPT        53A  O    37TEXT('User Report Title')
     A                                   101'DATE'
     A            BJMRDT         7A  O   106TEXT('Midas Run Date')
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(4)
     A                                      SPACEA(2)
     A  60                                41'ACCOUNT DETAILS AMENDED - TIERED W-
     A                                      HT SWITCHED ON'
     A                                      SPACEA(1)
     A  60                                41'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-
     A                                      ~~~~~~~~~~~~~~'
     A                                      SPACEA(2)
     A  61                                41'ACCOUNT DETAILS AMENDED - TIERED W-
     A                                      HT SWITCHED OFF'
     A                                      SPACEA(1)
     A  61                                41'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-
     A                                      ~~~~~~~~~~~~~~~'
     A                                      SPACEA(2)
     A                                    19'ACCOUNT NUMBER'
     A                                    62'BEFORE UPDATE'
     A                                    95'AFTER UPDATE'
     A                                      SPACEA(1)
     A                                    63'TAX/NON-TAX'
     A                                    95'TAX/NON-TAX'
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Report Details
     A          R DETAIL
     A            ATBRCA    R             16TEXT('Branch code')
     A            ATCNUM    R             20TEXT('Customer no')
     A            ATCCY     R             27TEXT('Ccy code')
     A            ATACOD    R             31TEXT('Acc code')
     A            ATACSQ    R             36TEXT('Acc seq no')
     A            #ETAXI         1        68TEXT('Tax/Non-Tax - Before')
     A            ATITAX    R            100TEXT('Tax/Non Tax - After')
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *
      **  Report footer
     A          R FOOTER
     A                                      SPACEB(3)
     A                                    50'*** END OF REPORT ***'
