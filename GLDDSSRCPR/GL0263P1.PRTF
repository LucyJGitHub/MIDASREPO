      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Gener. Interbranch Ent. at Item Level')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL0263P1 - Batch Transaction File Split                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CCB003 *CREATE     Date 11OCT96               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CCB003 - COB Performance Enhancements - Task Split           *
      *                                                               *
      *****************************************************************
      **
     A          R H001                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      **
     A                                 03  6'REFERENCE GL0263P1'
     A            BJURPT        53A    03 37
     A                                 03100'DATE'
     A            BJMRDT         7A    03105
     A                                 03119'PAGE'
     A            PAGE1          4S 0  03124EDTCDE(Z)
      **
     A                                 05 50'GENERATED INTERBRANCH ENTRIES'
     A                                      UNDERLINE
     A          R D001                      SPACEB(2)
     A                                     6'BATCH NO.:'
     A                                      UNDERLINE
     A            BNUM           3A       17UNDERLINE
     A                                    21'(BATCH BRANCH :'
     A            BRBCDA         3A       37
     A                                    40')'
     A          R D002                      SPACEB(2)
     A                                    19'BRANCH :'
     A            BTIBCA         3A       28
     A                                    39'ITEM:'
     A            ITEM           3S 0     48
     A          R D003                      SPACEB(1)
     A                                    23'ENTRIES :'
     A            BRCA           3A       33
     A                                    36'-'
     A            CNUMA          6A       37
     A                                    43'-'
     A            CCY            3A       44
     A                                    47'-'
     A**Start of change CGL029
     A**********  ACODA          4A       48                                                  CGL029
     A**********                          52'-'                                               CGL029
     A**********  ACSQA          2A       53                                                  CGL029
     A**********  PNAR          30A       60                                                  CGL029
     A**40******  PSTAA         15S       95EDTCDE(J)                                         CGL029
     A**41******  PSTAB         15S 1     94EDTCDE(J)                                         CGL029
     A**44******  PSTAC         15S 2     94EDTCDE(J)                                         CGL029
     A**43******  PSTAD         15S 3     95EDTCDE(J)                                         CGL029
     A**77******                         114'DR'                                              CGL029
     A*N77******                         114'CR'                                              CGL029
     A            ACODA         10A       48
     A                                    58'-'
     A            ACSQA          2A       59
     A            PNAR          30A       66
     A  40        PSTAA         15S      101EDTCDE(J)
     A  41        PSTAB         15S 1    100EDTCDE(J)
     A  44        PSTAC         15S 2    100EDTCDE(J)
     A  43        PSTAD         15S 3    101EDTCDE(J)
     A  77                               120'DR'
     A N77                               120'CR'
     A**End of change CGL029
     A          R T001                      SPACEB(2)
     A                                     6'TOTAL OF INTERBRANCH ENTRIES FOR'
     A                                      UNDERLINE
     A                                    41'BATCH BRANCH :'
     A            BRCBX          3A       57
     A          R T002                      SPACEB(2)
     A                                     6'WITH BRANCH :'
     A                                      UNDERLINE
     A            BRCA           3A       20
     A                                    26'CURRENCY :'
     A                                      UNDERLINE
     A            CCY            3A       37
     A                                    52'DEBIT :'
     A  40        TDRAA         15S       61EDTCDE(J)
     A  41        TDRAB         15S 1     60EDTCDE(J)
     A  44        TDRAC         15S 2     60EDTCDE(J)
     A  43        TDRAD         15S 3     60EDTCDE(J)
     A                                    85'CREDIT :'
     A  40        TCRAA         15S       95EDTCDE(J)
     A  41        TCRAB         15S 1     94EDTCDE(J)
     A  44        TCRAC         15S 2     94EDTCDE(J)
     A  43        TCRAD         15S 3     95EDTCDE(J)
     A          R F001                      SPACEB(2)
     A                                     6'TOTAL OF INTERBRANCH ENTRIES'
     A                                      UNDERLINE
     A          R F002                      SPACEB(2)
     A                                     6'CURRENCY :'
     A                                      UNDERLINE
     A            CCY            3A       17
     A                                    52'DEBIT :'
     A  40        FDRAA         15S       61EDTCDE(J)
     A  41        FDRAB         15S 1     60EDTCDE(J)
     A  44        FDRAC         15S 2     60EDTCDE(J)
     A  43        FDRAD         15S 3     60EDTCDE(J)
     A                                    85'CREDIT :'
     A  40        FCRAA         15S       95EDTCDE(J)
     A  41        FCRAB         15S 1     94EDTCDE(J)
     A  44        FCRAC         15S 2     94EDTCDE(J)
     A  43        FCRAD         15S 3     95EDTCDE(J)
