      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Batch Postings on Comp. Susp. A/C report')    *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL0280P1 - GL Batch Postings diverted to Computer       *
      *                  Suspense Account Report                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. MD022919           Date 19Aug14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU013  *CREATE    Date 17APR98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD022919 - GL0280P1 report being generated without a header. *
      *             Created a new format for "No Details to Report".  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CEU013 - EMU Account Postings Narrative                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R GL0280F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL0280P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    39'BATCH POSTINGS DIVERTED +
     A                                      TO COMPUTER SUSPENSE ACCO+
     A                                      UNT'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    39'------------------------+
     A                                      -------------------------+
     A                                      ---'
      *
     A                                     6'Batch'
     A                                      SPACEB(2)
     A            #BTNB          3        12
     A                                    17'Branch'
     A            #BCDA          3        24
     A            #DPCD          3        28
     A            #BDES         30        32
      *
     A          R GL0280F2
      *
     A                                     6'Item no.'
     A                                      SPACEB(2)
     A            #BINB          3        15
      *
     A                                     6'Account No.'
     A                                      SPACEB(1)
     A                                    40'Amount'
     A                                    70'Narrative'
      *
     A            #BICN         10         6
     A                                      SPACEB(1)
     A            #CYCD          3        17
     A**Start of change CGL029
     A**********  #ACCD          4        21                                                  CGL029
     A**********  #ACSN          2        26                                                  CGL029
     A**********  #PTAM         23        32                                                  CGL029
     A**********  #DCIN          1        55                                                  CGL029
     A**********  #PNAR         30        58                                                  CGL029
     A            #ACCD         10        21
     A            #ACSN          2        32
     A            #PTAM         23        38
     A            #DCIN          1        61
     A            #PNAR         30        64
     A**End of change CGL029
      *
     A                                     6'Type'
     A                                      SPACEB(1)
     A            #TRTY          5        11
     A                                    18'Sub-Type'
     A            #TSTY         10        27
     A                                    38'Posting Date'
     A            #PTDT          7        51
     A                                    59'Value Date'
     A            #VLDT          7        70
     A                                    78'Assoc Cust'
     A            #ACNB          6        91
      *
     A                                     6'Cheque No.'
     A                                      SPACEB(1)
     A            #CQNB          8        17
     A                                    26'Swift Add.'
     A            #SWCR         16        37
     A                                    54'Prof. Centre'
     A            #PRCN          4        67
     A                                    72'Book Code'
     A            #BKCD          2        82
      *
      **Start of change MD022919
     A          R GL0280NR
     A                                    52'*** NO DETAILS TO REPORT +
     A                                      ***'
     A                                      SPACEB(3)
      **End of change MD022919
     A          R GL0280F3
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(2)
      *
      *
