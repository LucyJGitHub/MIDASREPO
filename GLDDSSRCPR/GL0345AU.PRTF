      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL Generated entries audit report')
      *****************************************************************
      *                                                               *
      *  Midas General Ledger Module                                  *
      *                                                               *
      *  PRTF/GL0345AU - Generated entries audit report               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. 153640  *CREATE    Date 12May00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  153640 - New file.                                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL0345AU'
     A            BJURPT        53A       37
     A                                   101'DATE'
     A            BJMRDT         7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    42'GENERATED ENTRIES BY +
     A                                      FILE - AUDIT'
     A  41                                75' - RE/GL'
     A  42                                75' - DL/LE/FT'
      *
     A                                    42'---------------------+
     A                                      ------------'
     A                                      SPACEB(1)
     A  41                                75'--------'
     A  42                                75'-----------'
      *
     A                                     3'File      Source of +
     A                                      posting'
     A                                      SPACEB(3)
     A                                    50'No of'
     A                                    67'Total value'
     A                                      SPACEA(1)
     A                                    50' Recs'
     A                                    67' of records'
     A                                      SPACEA(2)
      *
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R DETAIL
     A            TEXT          40A        3
     A                                    42'Actual'
     A            FNORE          5  0     50EDTCDE(3)
     A            FPSTA         18  0     60EDTCDE(3)
     A                                      SPACEA(1)
     A  50                                13'*** DIFFERENCE ***'
     A                                    42'Trailer'
     A            TNORE          5  0     50EDTCDE(3)
     A            TPSTA         18  0     60EDTCDE(3)
     A                                      SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
      ***  Example /COPY.
     A*/COPY WNCPYSRC,MMnnnnAU01
      *
