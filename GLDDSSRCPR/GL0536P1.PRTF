      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Future Postings')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL0536P1 - Future Postings Report                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CGL010 *C *CREATE  Date 08Nov99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL010 - Future Value Dated Journal Entries for Non-Retail   *
      *           Accounts.                                           *
      *                                                               *
      *****************************************************************
      *                                                               *
      *   File Indicators                                             *
      *   ---------------                                             *
      *                                                               *
      *   Records, Fields and Indicators                              *
      *   ------------------------------                              *
      *                                                               *
      *   HEADP1   - Page Header                                      *
      *      BJURPT    : User Report Name                             *
      *      BJMRDT    : Midas Run Date (Alpha)                       *
      *      A8BRCD    : Branch                                       *
      *      A8BRCD    : Branch Name                                  *
      *      RCCY      : Currency Code                                *
      *      RTRAC     : Transitory Account Code                      *
      *                                                               *
      *   DETAIL   - Future Posting Details                           *
      *      RCNUM     : Customer Number                              *
      *      RCCY      : Currency Code                                *
      *      RACOD     : Account Code                                 *
      *      RACSQ     : Account Sequence Number                      *
      *      RPSTD     : Posting Date (Alpha)                         *
      *      RVALD     : Value Date   (Alpha)                         *
      *      RPSTA     : Posting Amount                               *
      *                                                               *
      *   TOTAL1   - Total for Transitory Code                        *
      *      RTRAW     : Saved Transitory Account Code                *
      *      RTRAT     : Total Amount for Transitory Account Code     *
      *                                                               *
      *   TOTAL2   - Total for Currency                               *
      *      RCCYW     : Saved Currency Code                          *
      *      RCCYT     : Total Amount for Currency                    *
      *                                                               *
      *   TOTAL3   - Total for Branch                                 *
      *      RBRCAW    : Saved Branch                                 *
      *      RBRCAT    : Total Amount for Branch                      *
      *                                                               *
      *   TRAILP1  - End Report                                       *
      *                                                               *
      *    76          : BRCA, CCY & TRAC Continue on the Next Page   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL0536P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'FUTURE POSTINGS REPORT'
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                      SPACEA(1)
     A                                    46'----------------------'
     A                                      SPACEA(2)
      *
      ** Sub-Heading
      *
     A                                     6'Currency:'
     A            RCCY           3        16
     A                                    26'Transitory Acc.:'
     A**Start of change CGL029
     A**********  RTRAC          4        43                                                  CGL029
     A**76******                          54'(Continued)'                                     CGL029
     A            RTRAC         10        43
     A  76                                57'(Continued)'
     A**End of change CGL029
      *
     A                                      SPACEA(1)
      *
      ** Detail Sub-Headings
      *
     A                                    21'Account number'
     A                                      SPACEB(2)
     A                                    44'Posting date'
     A                                    59'Value date'
     A                                    80'Posting amount'
     A                                   100'Posting narrative'
     A                                      SPACEA(2)
      *
      ** Detail Record Format
      *
     A          R DETAIL
      *
     A**Start of change CGL029
     A**********  RCNUM          6        21                                                  CGL029
     A**********  RCCY           3        28                                                  CGL029
     A**********  RACOD          4        32                                                  CGL029
     A            RCNUM          6        15
     A            RCCY           3        22
     A            RACOD         10        26
     A**End of change CGL029
     A            RACSQ          2        37
     A            RPSTD          8A       44
     A            RVALD          8A       59
     A            RPSTA         22        72
     A            RPNAR         30       100
     A*/COPY WNCPYSRC,GL0536P101
     A                                      SPACEA(1)
      *
      ** Total by transitory posting
      *
     A          R TOTAL1                    TEXT('Total for TRAC')
     A                                    26'Total amt for    , transitory acc.'
     A                                      SPACEB(2)
     A            RCCYW          3        40
     A**Start of change CGL029
     A**********  RTRACW         4        61                                                  CGL029
     A            RTRACW        10        61
     A**End of change CGL029
     A            RTRACT        22        72
      *
      ** Total for currency
      *
     A          R TOTAL2                    TEXT('Total for CCY')
     A                                    47'Total amt for'
     A                                      SPACEB(2)
     A            RCCYW          3        61
     A            RCCYT         22        72
      *
      ** Total for branch
      *
     A          R TOTAL3                    TEXT('Total for BRCA')
     A                                    47'Total amt for'
     A                                      SPACEB(2)
     A            RBRCAW         3        61
     A            RBRCAT        22        72
      *
      ** Report Trailer
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT FOR BRANCH  ***'
     A                                      SPACEB(3)
      *
