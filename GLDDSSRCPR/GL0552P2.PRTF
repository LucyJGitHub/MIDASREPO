      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Daily Events by Ccy for IPP - by Company')
      *****************************************************************
      *                                                               *
      *  Midas - General Lendger Module                               *
      *                                                               *
      *  PRTF/GL0552P2 - Daily Events by Currency for Internal        *
      *                  Parts Purchased - by Company                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CLE004 *Create     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE004 - Customer Lending Enhancements - Syndications.       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL0551P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'DAILY EVENTS BY CURRENCY +
     A                                      FOR INTERNAL PARTS PURCHASED'
     A                                      SPACEA(1)
     A                                    40'-------------------------+
     A                                      ----------------------------'
     A                                      SPACEA(1)
     A                                     9'COMPANY'
     A            RECPY          3        17
     A            RCMNM         30        21
     A                                      SPACEA(2)
     A            RCYNM         14         9
     A                                      SPACEA(1)
      *
      *-----------------*
      ***  Sub-Heading: *
      *-----------------*
      *
     A          R COLMP2
     A                                    41'LOAN'
     A                                    84'VAL/MAT'
     A                                      SPACEA(1)
     A                                     7'CUSTOMER'
     A                                    41'NUMBER'
     A                                    51'EVENT DATE'
     A                                    65'EVENT'
     A                                    86'DATE'
     A                                   106'AMOUNT'
     A                                   121'RATE'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETLP2
     A            RCRNM         20         7
     A            RCTWN         10        28
     A            RLNRF          6        41
     A            REDAT          7        52
     A            RENAR         15        65
     A            ROTHD          7        84
     A            REAMT         18        95
     A            RINTR         13       116
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP2                   SPACEB(3)
     A                                    51'***  END OF REPORT FOR +
     A                                      COMPANY ***'
      *
