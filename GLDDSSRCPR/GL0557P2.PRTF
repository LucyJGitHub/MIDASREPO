      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Events by Currency with Running Totals IPP Consolidat')*
      *****************************************************************
      *                                                               *
      *  Midas - General Lendger Module                               *
      *                                                               *
      *  PRTF/GL0557P2 - Events by Currency with Running Totals       *
      *                  Internal Parts Purchased - Consolidated      *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. CGL091  *Create    Date 02Nov21               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * CGL091 - Events by Currency with Running Totals Report        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2021')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL0557P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    34'EVENTS BY CURRENCY WITH RUNNING +
     A                                      TOTALS - TO'
     A N56                                78'END OF BOOKS'
     A N56                                  SPACEA(1)
     A  56        #DAYS          3  0     78
     A  56                                82'DAYS FORWARD'
     A  56                                  SPACEA(1)
     A  99N99     RCYNM         14         9
     A                                    34'-------------------------------+
     A                                      -------------'
     A N56                                78'------------'
     A N56                                  SPACEA(1)
     A  56                                78'----'
     A  56                                82'------------'
     A  56                                  SPACEA(1)
     A                                    46'FOR INTERNAL PARTS PURCHASED'
     A                                      SPACEA(1)
     A                                    46'----------------------------'
     A                                      SPACEA(1)
     A                                     9'CONSOLIDATED'
     A                                      SPACEA(1)
      *
      *-----------------*
      ***  Sub-Heading: *
      *-----------------*
      *
     A          R COLMP2
     A                                    41'LOAN'
     A                                    84'VAL/MAT'
     A                                      SPACEA(1)
     A                                     7'CUSTOMER'
     A                                    41'NUMBER'
     A                                    51'EVENT DATE'
     A                                    65'EVENT'
     A                                    86'DATE'
     A                                   106'AMOUNT'
     A                                   121'RATE'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETLP2
     A            RCRNM         20         7
     A            RCTWN         10        28
     A            RLNRF          6        41
     A            REDAT          7        52
     A            RENAR         15        65
     A            ROTHD          7        84
     A            REAMT         18        95
     A            RINTR         13       116
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP2                   SPACEB(3)
     A  77                                51'***  END OF REPORT  ***'
     A N77                                48'***  NO DETAILS TO +
     A                                      REPORT ***'
      *
