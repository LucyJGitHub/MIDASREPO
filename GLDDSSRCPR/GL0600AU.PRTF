      *****************************************************************
/*S*D****PRTFBASENS****************************************************                     MD021994
/*STD *  PRTFBASE                                                     *                     MD021994
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL Accounts Post')                               *
     A*****************************************************************
     A*                                                               *
     A*  Midas - General Ledger Module
     A*                                                               *
     A*  PRTF/GL0600AU - FOR ACCOUNT POSTING                          *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD021994           Date 30Aug13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01413             Date 13Apr93               *
     A*                 000000                  DDMMMYY               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  MD021994 - GLC0600 failed trying to allocate REIAC record    *
     A*  S01413  -  Retail 3 Incorporation                            *
     A*  Dummy src member created for printer file which has no DDS   *
     A*                                                               *
     A*****************************************************************
      **Start of Change MD021994
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
      *
     A                                      SKIPB(3)
     A                                     6'Reference GL0600AU'
      *
     A            BJURPT        53        37
     A                                   101'Date'
     A            RUNA           7       106
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    53'ACCOUNTS POST'
     A                                    69'RUN CONTROLS'
     A                                      SPACEA(1)
      *
     A                                    53'-------------'
     A                                      SPACEA(3)
     A          R CONTROL                   TEXT('GL0600 Audit Details')
     A                                      SKIPB(20)
     A                                     1'ACCOUNTS POST CONTROLS'
     A                                      SPACEA(2)
      *
     A                                     9'-NUMBER OF ACCOUNT +
     A                                      RECORDS READ'
     A            TNRR           5  0     54EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     9'-NUMBER OF ACCOUNTS +
     A                                      OPENED BY THE SYSTEM'
     A            TNAO           5  0     54EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     9'-NUMBER OF ACCOUNTS +
     A                                      RE-OPENED BY THE SYSTEM'
     A            TNAR           5  0     54EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     9'-NUMBER OF ACCOUNT +
     A                                      RECORDS WRITTEN'
     A            TNRW           5  0     54EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                     9'-NUMBER OF POSTINGS +
     A                                      PROCESSED IN C.O.B.'
     A            TNPRC          5  0     54EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                     9'-NUMBER OF POSTINGS +
     A                                      PROCESSED IN I.C.'
     A            TNPRI          5  0     54EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                     9'-NUMBER OF POSTINGS +
     A                                      IN CLEARING'
     A            TNPIC          5  0     54EDTCDE(3)
     A                                      SPACEA(2)
      *
     A          R DBERROR                   TEXT('Database Error')
      *
     A                                    30'*** DATABASE ERROR'
     A            DBASE          3  0     49
      *
     A                                    52'-FILE-S/'
     A            DBFILE         8A       60
      *
     A                                    73'KEY-'
     A            DBKEY         29A       77
      *
     A                                   107'***'
      **End Of Change MD021994
