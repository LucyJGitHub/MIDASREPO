      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL Balance Sheet / P & L Reporting')
     A********************************************************************
     A**                                                                **
     A*  Midas   GENERAL LEDGER MODULE
     A**                                                                **
     A**     GL0761AU - BALANCE SHEET / PROFIT + LOSS REPORT            **
     A**                                                                **
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A**                                                                **
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A**     Last Amend No. 124974           DATE 20May98             *
     A**     Prev Amend No. S10736           DATE 16Oct92             *
     A**                    RT0086           DATE 15May91             *
     A**                                                                **
     A**--------------------------------------------------------------- **
     A**                                                                **
     A**     124974 - ADDHED field added for extra heading needed for *
     A**              BSPL reports. Applied fix 063148.               *
     A**     S10736 - Print Management Accounts Report Options
     A**     RT0086 - DO NOT PRINT 'END OF REPORT' ON AU.               **
     A**                                                                **
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      ** Header Format
      *
     A          R GL0761AH                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'GL0761AU'
     A            BJURPT        53        40
     A                                   100'DATE'
     A            BJMRDT         7A      105
     A                                   120'PAGE'
     A                                   125PAGNBR
     A                                      EDTCDE(J) SPACEA(1)
      *
     A                                   100'TIME'
     A                                   105TIME EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
      *                                              Begin /124974/
     A*********** G5RSNM        30A       48 SPACEA(1)
     A            G5RSNM        30A       48
     A            ADDHED        35A       78 SPACEA(1)
      *                                              End   /124974/
     A            NMUL          30A       48 SPACEA(1)
      *
     A            PRTDT         50A       50SPACEA(1)
     A            DTUL          50A       50SPACEA(2)
      *
     A                                    11'REPORT OPTIONS' SPACEA(1)
     A                                    11'--------------' SPACEA(2)
     A            RPOP1         80A       26SPACEA(1)
      *                                                      /*S10736*/
     A************RPOP2         80A       26SPACEA(2)
     A            RPOP2         80A       26SPACEA(1)
     A*/COPY WNCPYSRC,GL0761AU01
      ** Extra Report Options displayed for Management Accounts
      *
     A  76                                26'Control Group selected is'
     A  76        PRGRP          2A       52
     A  76                                55'-'
     A  76        PRCGDS        30A       57SPACEA(1)
     A  76                                26'Relative Period Number is'
     A  76        PRRPNC         4A       52SPACEA(1)
     A  76                                26'Term selected is'
     A  76        PRTRM         15A       43SPACEA(1)
     A  76                                26'Type selected is'
     A  76        PRTYP          9A       43SPACEA(1)
     A  76                                26'Analysis by'
     A  76        AULEH         90A       38SPACEA(1)
      *                                                      /*S10736*/
      ** Detail Format
      *
     A          R GL0761AD                  TEXT('Detail Format')
     A                                      SPACEB(7)
     A                                    11'REPORT FILE GLBSRPPD'
     A                                      SPACEA(1)
     A                                    11'--------------------'
     A                                      SPACEA(5)
     A                                    11'RECORDS READ   '
     A            NRRD          15S 0     60EDTCDE(3)
     A                                      SPACEA(2)
     A                                    11'DEBIT TOTAL   '
     A            HWNDR         15  0     57EDTCDE(Z)
     A            HDCDR          3  0     72
     A                                      SPACEA(2)
     A                                    11'CREDIT TOTAL  '
     A            HWNCR         15  0     57EDTCDE(Z)
     A            HDCCR          3  0     72
     A                                      SPACEA(2)
      *
      ** Error Format
      *
     A          R GL0761AE                  TEXT('Error Format')
     A                                      SPACEB(7)
     A                                    40'*** Database in Error ***'
      *
     A                                      SPACEA(1)
     A                                    44'File'
     A                                    55':-'
     A            DBFILE         8        59
      *
     A                                      SPACEA(1)
     A                                    44'Key'
     A                                    55':-'
     A            DBKEY         29        59
      *
     A                                      SPACEA(1)
     A                                    44'In Program'
     A                                    55':-'
     A            DBPGM         10        59
      *
     A                                      SPACEA(1)
     A                                    44'At point'
     A                                    55':-'
     A            DBASE          3S 0     59
      *
      ** No Details to report
      *
     A          R GL0761AN                  TEXT('End of Report Format')
     A                                      SPACEB(5)
     A                                    40'*** No Details to Report ***'
      *
      **                                                          /*RT0086*/
      ***End of Report Format
      ******
     A*****     R GL0761AT                  TEXT('End of Report Format')
     A*****                                 SKIPB(50)
     A*****                               56'*** End of Report ***'
      **                                                          /*RT0086*/
