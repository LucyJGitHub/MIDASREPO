      ********************************************************************
/*STD *  PRTFAU                                                          *
/*EXI *  TEXT('Midas Generic Post Interface Referred Items')          *
      ********************************************************************
      *                                                                  *
      *  Midas - General Ledger Module                                   *
      *                                                                  *
      *  PRTF/GL1721P3 - Generic Posting Interface Referred Items        *
      *                                                                  *
      *  (c) Misys Banking Systems Ltd. 2014                             *
      *                                                                  *
      *  Last Amend No. CGL160  *CREATE    Date 16Oct14                  *
      *                                                                  *
      ********************************************************************
      *                                                                  *
      *  CGL160 - Cheque Details Upload via Generic Posting Interface    *
      *                                                                  *
      ********************************************************************
      *  C R E A T I O N     P A R A M E T E R S                         *
      *                                                                  *
      ********************************************************************
      *
      ** Header Format
     A          R GL1721H3                  TEXT('(c) Copyright Misys +
     A                                      Banking Systems Ltd. 2014')
      *
     A                                  3  6'Reference GL1721P3'
     A            BJURPT        53A     3 39
     A                                  3100'Date'
     A            BJMRDT         7A     3105
     A                                  3120'Page'
     A            PAGE           4S 0   3125EDTCDE(Z)
      *
     A                                  5 25'Generic Postings Interface +
     A                                      Referred Items Report:'
     A            ##DESC        30A     5 75
     A                                  6 25'---------------------------+
     A                                      ----------------------'
      *
     A                                  8 01'Batch'
     A                                  8 38'Retail'
     A                                  8 49'Posting'
     A                                  8 58'Value'
     A                                  8 86'Dr'
     A                                  8 91'Trans.'
     A                                  8 99'Profit'
     A                                  8106'Book'
     A                                  8111'Cheque'
      *
     A                                  9 01'Ref.'
     A                                  9 09'Account Ref.'
     A                                  9 38'Account No.'
     A                                  9 50'Date'
     A                                  9 58'Date'
     A                                  9 70'Posting Amount'
     A                                  9 86'Cr'
     A                                  9 91'Type'
     A                                  9 99'Centre'
     A                                  9106'Code'
     A                                  9111'Number'
      *
     A                                 10 01'----'
     A                                 10 09'------------'
     A                                 10 38'-----------'
     A                                 10 50'----'
     A                                 10 58'----'
     A                                 10 70'--------------'
     A                                 10 86'--'
     A                                 10 91'----'
     A                                 10 99'------'
     A                                 10106'----'
     A                                 10111'------'
      *
      *.....................................................................
      *
     A          R GL1721D3                  TEXT('Posting Details')
     A                                      SPACEB(2)
     A            BTBTNB         3A       01
     A            BTBINB         3S 0     05
     A            BTIBCA         3A       09
     A                                    12'-'
     A            BTCUST         6A       13
     A                                    19'-'
     A            BTCYCD         3A       20
     A                                    23'-'
     A            BTACCD        10A       24
     A                                    34'-'
     A            BTACSN         2A       35
     A            BTRACN        10S 0     38
     A            XRPDTP         8A       49
     A            XRVDTP         8A       58
     A            XRPAMP        18A       67
     A            BTDCIN         1A       86
     A            BTTRTY         5A       91
     A            BTPRCN         4A      100
     A            BTBKCD         2A      107
     A            BTCQNB         8A      111
      *
      *.....................................................................
      *
     A          R GL1721R3                  TEXT('POSTING NARR ')
     A                                      SPACEB(1)
     A N50                                01'Referrals:'
     A            RFNAR         60        12
      *
      *.....................................................................
      *
     A          R GL1721T3                  TEXT('END OF REPORT')
     A                                      SPACEB(3)
     A                                    55'***** End of Report ****'
      *
