      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas GL Post Summary Detail Report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL1724P1  - Post Summary Detail Report                  *
      *                                                               *
      *  (C) Misys Banking Systems Ltd. 2006                          *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CGL047 *Create     Date 30Jun06               *
      *  Prev Amend No. Xnnnnn             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CGL047 - Midas Generic Posting Interface                     *
      *                                                               *
      *****************************************************************
      **
     A                                      REF(MFRFL)
      **   *******************
      **   Page Headings
      **   *******************
      **
     A          R HEADP1                    TEXT('(c) Copyright Misys +
     A                                      Banking Systems Ltd. 2006')
     A                                      SKIPB(3)
     A                                     6'Reference GL1724P1'
     A            BJURPT    R             39REFFLD(TITL)
     A                                   101'Date'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'Page'
     A                                   124PAGNBR EDTCDE(Z) SPACEA(2)
      *
     A                                    40'Posting Summary  - '
     A                                    59' Details Report     '
     A                                      SPACEA(1)
     A                                    40'-------------------'
     A                                    59'---------------     '
     A                                      SPACEA(2)
     A                                    06'Branch'
     A                                    14'Branch Cust No.'
     A                                    30'Ccy'
     A                                    34'A/c Code'
     A                                    49'A/c Seq'
     A                                    61'Narrative   '
     A                                    96'Debit/Credit'
     A                                   111'Posting Amount'
     A                                      SPACEA(1)
     A                                    06'------'
     A                                    14'---------------'
     A                                    30'---'
     A                                    34'--------'
     A                                    49'-------'
     A                                    61'---------   '
     A                                    96'------------'
     A                                   111'--------------'
     A                                      SPACEA(2)
      **
      **   ***********************
      **   Detail Part Of Report 1
      **   ***********************
      **
     A          R DETLP1
     A            BRCA      R             07
     A            CNUM           6A       17
     A            CCY       R             30
     A            ACOD      R             36
     A            ACSQ      R             51
     A            PNAR      R             57
     A            DRCR      R            101
     A            PSTAR         13       112
     A                                      SPACEA(1)
      **
      **   ***********************
      **   ACOD Level Break Line
      **   ***********************
      **
     A          R TOTAL
     A                                   106'----------------'
     A                                      SPACEA(1)
     A            TOTAR         13       106
     A            SIGN           2       120
     A                                      SPACEA(2)
      **   *******************
      **   Report Trailer
      **   *******************
      **
     A          R TRAILP1                   SPACEB(2)
     A                                    56'*** END OF REPORT ***'
      **   **************
      **   Database Error
      **   **************
      **
     A          R ERRAP                     SPACEB(2)
     A                                    56'*** DATABASE ERROR ***'
