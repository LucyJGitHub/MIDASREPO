      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Reconciliation Customer Fees Files Report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL1930P1 - Customer Service Fees - Report               *
      *                  Reconciliation Customer Fees Files           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CSD008   *CREATE   Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE GL1930P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    39'Reconciliation Customer Service +
     A                                       Fees Files - Full List'
     A                                      SPACEB(001)
      *
     A                                    39'----------------------------------+
     A                                      -----------------------'
     A                                      SPACEB(001)
      *
      ** Sub Heading For Fee Definition Reactivated
      *
     A          R SUBHFD
     A                                     9'Fee Type   '
     A                                      SPACEB(2)
     A                                    25'Reason of'
      *
     A                                     9'Reactivated'
     A                                      SPACEB(1)
     A                                    25'Reactivation'
      *
     A                                     9'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      SPACEB(001)
      *
      ** Detail Record Format for Fee definition Reactivation
      ** *IN59 : Problem during deletion of Spec. Fee Defin.
      ** *IN60 : Print Fee type/charge group, new reason descr.
      ** *IN61 : Error in Definition file
      ** *IN62 : Error in Extension details
      ** *IN63 : Error in Fee Charge Group/Fee Rate Table
      ** *IN64 : Error due to  Accruals not yet settled
      ** *IN65 : Error due To Settlements not yet posted
      *
     A          R DETLFD
      *
     A  60        RFETP          2         9
     A  60                                  SPACEB(001)
     A  60                                +0'/'
     A  60        RCHGR          2        +0
      *
     A  60 61                             25'Default for Fee Type Definition'
     A  61        RFETP1         2        +1
     A N60 61                               SPACEB(001)
     A  61        RCHGR1         2        +1
      *
     A  60 62                             25'Fee applicable for Customer-
     A                                      /Portfolio extension'
     A  62        RCNUM2         6        +1
     A N60 62                               SPACEB(001)
     A  62        RPTFR2         4        +1
      *
     A  60 63                             25'Fee applicable for Charge Grou-
     A                                      p/Rate code :'
     A  63        RFETP3         2        +1
     A N60 63                               SPACEB(001)
     A  63        RCHGR3         2        +1
     A  63        RDDPT3        10        +1
     A  63        RSESN3        10        +1
     A  63        RINVT3         3        +1
     A  63        RCCCY3         3        +1
     A  63        RNOMA3         1        +1
     A  63        RINST3         3        +1
     A  63        RPCHO3         1        +1
     A  63        RRTCD3         2        +1
      *
     A  60 64                             25'Accruals not settled for this fee'
     A  64        RBRCH4         3        +2
     A N60 64                               SPACEB(001)
     A  64        RCNUM4         6        +1
     A  64        RPTFR4         4        +1
      *
     A  60 65                             25'Settlements not posted for th-
     A                                      is fee'
     A  65        RBRCH5         3        +2
     A N60 65                               SPACEB(001)
     A  65        RCNUM5         6        +1
     A  65        RPTFR5         4        +1
      *
     A  60 59                             25'Used as Default of not deletab-
     A                                      le fee.'
     A N60 59                               SPACEB(001)
      *
     A
      *
      ** Sub Heading For VAT Code Reactivated
      *
     A          R SUBHVA
     A                                     2'VAT Code'
     A                                      SPACEB(2)
     A                                    35'Reason of'
      *
     A                                     2'Reactivated'
     A                                      SPACEB(1)
     A                                    35'Reactivation'
      *
     A                                     2'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------'
     A                                      SPACEB(001)
      *
      ** Detail Record Format  for VAT Code Reactivation
      ** *IN66 : Print Fee type/charge group, new reason descr.
      ** *IN67 : Error in Fee Type Definition File
      *
     A          R DETLVA
      *
     A  66        RVATC          1         2
     A  66                                  SPACEB(001)
     A  66        RNARR6        30        +1
      *
     A  66 67                             35'Applicable for Fee Type Definition'
     A  67        RFETP7         2        +1
     A N66 67                               SPACEB(001)
     A  67        RCHGR7         2        +1
     A  67        RNARR7        30        +1
      *
      ** Sub Heading For Rate code Reactivated
      *
     A          R SUBHRT
     A                                     2'Rate Code  '
     A                                      SPACEB(2)
     A                                    40'Reason of'
      *
     A                                     2'Reactivated'
     A                                      SPACEB(1)
     A                                    40'Reactivation'
      *
     A                                     2'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------'
     A                                      SPACEB(001)
      *
      ** Detail Record Format Rate Code Reactivated
      ** *IN68 : Print Rate Code, reason of reactivation
      ** *IN69 : Error in Fee Charge Group/Fee Rate
      *
     A          R DETLRC
      *
     A  68        RRTCD8         1         2
     A  68                                  SPACEB(001)
     A  68        RCCY8          3        +1
     A  68        RNARR8        30        +1
      *
     A  68 69                             40'Used by Fee Charge Group/Rate Code'
     A  69        RFETP9         2        +1
     A N68 69                               SPACEB(001)
     A  69        RCHGR9         2        +1
     A  69        RRTCD9         2        +1
     A  69        RDDPT9        10        +1
     A  69        RSESN9        10        +1
     A  69        RINVT9         3        +1
     A  69        RCCCY9         3        +1
     A  69        RNOMA9         1        +1
     A  69        RINST9         3        +1
     A  69        RPCHO9         1        +1
      *
      ** Report Trailer
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
