      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Cust. Serv. Fees - Accruals Adjust. RPT')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL1980P1 - Customer Service Fees                        *
      *                - Accruals Adjustments Report                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **  Report Header Record Format
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE GL1980P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    42'Accruals Adjustments'
     A                                      SPACEB(001)
     A N70                                62
     A                                      ' - Input Today'
     A  70                                62
     A                                      ' - Full List'
     A                                    77'Branch'
     A            RBRCA          3        +1
      *
     A                                    42
     A                                      '--------------------'
     A                                      SPACEB(001)
     A N70                                62
     A                                      '--------------'
     A  70                                62
     A                                      '------------'
     A                                    77
     A                                      '-----------'
      *
      **  Sub-Heading:
      *
     A                                     2'Account Officer :'
     A                                      SPACEB(2)
     A            RACOC          2        +1
     A            RACON         30        +1
     A                                     2'----------------------------------+
     A                                      -----------------------------------+
     A                                      -----------------------------------+
     A                                      ----------------------------'
     A                                      SPACEB(002)
 
      *
      **  Detail Record Format
      *
     A          R DETAIL                    SPACEA(001)
     A                                     2'Client/Portfolio :'
     A                                      SPACEB(1)
     A            RCUST          6        +1
     A                                    +0'/'
     A            RPTFR          4        +0
     A            RCRNM         20        35
     A  70                                70'Last Change :'
     A  70        RLCTY          6        +1
     A  70        RLCD           7        +1
     A  70        RTIME          6  0     +1EDTWRD('  :  :  ')
      *
     A                                     2'Fee Type         :'
     A                                      SPACEB(001)
     A            RFETP          2        +1
     A                                    +0'/'
     A            RCHGR          2        +0
     A            RNARR         30        35
      *
     A                                     2'Accrued Charges  :'
     A                                      SPACEB(001)
     A                                    21'System Accruals :'
     A            RACDT         16        +1
     A            RACC1          3        +3
      *
     A                                    21'Adjustm. Posted :'
     A                                      SPACEB(001)
     A            RMACP         16        +1
     A            RMACS          1        +1
     A            RACC2          3        +1
      *
     A                                    21'Oustanding Adj. :'
     A                                      SPACEB(001)
     A            ROTAJ         16        +1
     A            ROTSG          1        +1
     A            RACC3          3        +1
      *
     A                                    38'-------------------'
     A                                      SPACEB(001)
     A  71                                70'Settlement Account :'
     A**Start of change CGL029
     A**71******  RSTAC         18        +5                                                  CGL029
     A  71        RSTAC         24        +3
     A**End of change CGL029
      *
     A                                    21'Amount to Settle:'
     A                                      SPACEB(001)
     A            RTOT1         16        +1
     A            RACC4          3        +3
     A  72                                70'Projected settlement :'
     A  72        RSTTL         16        +1
     A  72        RSCCY          3        +1
      *
      **  Report Trailer
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
