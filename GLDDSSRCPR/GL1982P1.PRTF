      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Cust. Serv. Fees - Fees Accrued Today -P1')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL1982P1 - Customer Service Fees - Fees Accrued Today   *
      *                  Report                                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **  Report Header Record Format
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE GL1982P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    46'Fees Accrued Today Details Report'
     A                                      SPACEB(001)
     A                                    85'Branch'
     A            RBRCA          3        93
      *
     A                                    46'---------------------------------'
     A                                      SPACEB(001)
     A                                    85'-----------'
      *
      **  Sub-Heading:
      *
     A                                     9'Account Officer'
     A                                      SPACEB(2)
     A            RACOC          2        30
     A            RACON         30        33
     A                                     9'Fee Processing Type'
     A                                      SPACEB(1)
     A            RFEPT          2        30
     A            RPRTN         20        33
     A                                     9'Fee Type'
     A                                      SPACEB(1)
     A            RFETP          2        30
     A            RNARR         30        33
     A                                     9'Cust.'
     A                                      SPACEB(2)
     A                                    16'Port.'
     A                                    21'Chg'
     A                                    25'Inst'
     A                                    30'Holding'
     A                                    41'Hold Holding'
     A                                    62'Holding Amount'
     A                                    80'Charg. Charging'
     A                                   104'Fee Rate'
     A                                   116'Security'
      *
     A                                      SPACEB(1)
     A                                    21'Grp'
     A                                    30'Reference'
     A                                    41'Ccy  Amount'
     A                                    62'in Charg. Ccy'
     A                                    80'Ccy    Amount'
     A                                     9'----------------------------------+
     A                                      -----------------------------------+
     A                                      -----------------------------------+
     A                                      --------------'
     A                                      SPACEB(001)
      *
      **  Detail Record Format
      *
     A          R DETAIL
      *
     A            RCUST          6         9SPACEB(1)
     A            RPTFR          4        16
     A            RCHGR          2        21
     A            RINST          3        25
     A            RHORF         10        30
     A            RHCCY          3        41
     A            RHOAM         16        45
     A            RHACH         16        62
     A            RCCCY          3        80
     A            RCHAM         16        84
     A            RFRAT         10       104
     A            RSHSN         10       116
      *
      **  Detail Criteria Record Format
      *
     A          R DETAIL2
      *
     A                                    16'Selection criteria'
     A                                      SPACEB(002)
     A                                    60'Rate details'
     A                                    16'Depot reference'
     A                                      SPACEB(001)
     A            RDPRF         10        35
     A                                    60'Fee rate code'
     A            RFERC          2        85
     A                                    16'Security shortname'
     A                                      SPACEB(001)
     A            RSHSN         10        35
     A                                    60'Threshold/Tiered ind.'
     A            RTHTI          1        85
     A                                    16'Investment type'
     A                                      SPACEB(001)
     A            RINVT          3        35
     A                                    60'Minimum Amount'
     A            RMINA         16        85
     A                                    16'Currency'
     A                                      SPACEB(001)
     A            RSCCY          3        35
     A                                    60'Maximum Amount'
     A            RMAXA         16        85
     A                                    16'Nominal or Market ind.'
     A                                      SPACEB(001)
     A            RNORM          1        41
     A                                    60'Amount for Charges'
     A            RRANG         16        85
     A                                    16'Cust/Portf/Holding ind.'
     A                                      SPACEB(001)
     A            RCPHI          1        41
     A                                    60'Percentage/Flat'
     A            RPERF          1        85
     A                                      SPACEA(001)
      *
      **  Total Record Format
      *
     A          R TOTAL
      *
      **  RECI EQ 2
      *
     A                                    20'Accruals adjustment'
     A                                      SPACEB(002)
     A**Start of change CGL029
     A**********  RADAC         18        54                                                  CGL029
     A**********  RADCY          3        75                                                  CGL029
     A**********  RADAM         16        79                                                  CGL029
     A            RADAC         24        54
     A            RADCY          3        80
     A            RADAM         16        84
     A**End of change CGL029
      *
      **  RECI EQ 1
      *
     A                                    20'Accrued amount'
     A                                      SPACEB(001)
     A**Start of change CGL029
     A**********  RACAC         18        54                                                  CGL029
     A**********  RACCY          3        75                                                  CGL029
     A**********  RACAM         16        79                                                  CGL029
     A            RACAC         24        54
     A            RACCY          3        80
     A            RACAM         16        84
     A**End of change CGL029
      *
      **  RECI EQ 3
      *
     A                                    20'Total for period'
     A                                      SPACEB(001)
     A            RFRPE          8        54
     A                                    +1'-'
     A            RTOPE          8        +1
     A**Start of change CGL029
     A**********  RTOCY          3        75                                                  CGL029
     A**********  RTOTA         16        79                                                  CGL029
     A            RTOCY          3        80
     A            RTOTA         16        84
     A**End of change CGL029
      *
      **  Accruals Due to Settlement Generation Record Format
      *
     A          R ACSEGEN
      *
      **  RECI EQ 4
      *
     A                                    20'Settl. Gener. Outst. Accruals '
     A                                      SPACEB(001)
     A**Start of change CGL029
     A**********  RADAC         18        54                                                  CGL029
     A**********  RACCY          3        75                                                  CGL029
     A**********  RADAM         16        79                                                  CGL029
     A            RADAC         24        54
     A            RACCY          3        80
     A            RADAM         16        84
     A**End of change CGL029
      *
      **  "Out of Settlement Range" Accruals Record Format
      *
     A          R ACOTADJ
      *
      **  RECI EQ 5
      *
     A                                    20'"Out of Settl. Range" Accruals '
     A                                      SPACEB(001)
     A**Start of change CGL029
     A**********  RADAC         18        54                                                  CGL029
     A**********  RACCY          3        75                                                  CGL029
     A**********  RADAM         16        79                                                  CGL029
     A            RADAC         24        54
     A            RACCY          3        80
     A            RADAM         16        84
     A**End of change CGL029
      *
      **  Outstanding Settlements Adjustments Accruals Record Format
      *
     A          R ACSTADJ
      *
      **  RECI EQ 6
      *
     A                                    20'Outst. Settl. Adjust. Accruals'
     A                                      SPACEB(001)
     A**Start of change CGL029
     A**********  RADAC         18        54                                                  CGL029
     A**********  RACCY          3        75                                                  CGL029
     A**********  RADAM         16        79                                                  CGL029
     A            RADAC         24        54
     A            RACCY          3        80
     A            RADAM         16        84
     A**End of change CGL029
      *
      **  Report Trailer
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
