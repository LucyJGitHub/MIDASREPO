      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL CS Fees Settlements Totals Report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL1988P1 - Customer Service Fees Settlements Totals     *
      *                  Report                                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE GL1988P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    43'Settlements Totals Full List'
     A                                      SPACEB(001)
     A                                    77'Branch'
     A            RRBRCA         3        84
     A                                   132' '
     A                                    43'----------------------------'
     A                                      SPACEB(001)
     A                                    77'------'
     A                                    84'---'
      *
      ***  ------------
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     5'Settlement Value date :'
     A                                      SPACEB(2)
     A            RRSTVD         7        +1
     A                                     4'   Amounts to Settle in Cha-
     A                                      rging CCY'
     A                                      SPACEB(2)
     A                                    +6'   Cross Rate    '
     A                                    +6'      Projected Settlements       '
     A                                     4'      Total      CCY      A-
     A                                      uthorised'
     A                                      SPACEB(1)
     A                                    +6'Charges - Settl. '
     A                                    +6'      Total      CCY      A-
     A                                      uthorised'
     A                                    02'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----------------------'
     A                                      SPACEB(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RRTAMT        16         4SPACEB(001)
     A            RRACCY         3        +1
     A            RRAAMT        16        +1
     A            RRFXRT        14        +7
     A N80        RRTSTT        16        +7
     A N80        RRSCCY         3        +1
     A N80        RRASTT        16        +1
      *
      *------------------------*
      ***  Total Record Format *
      *------------------------*
      *
     A          R TOTAL                     SPACEA(001)
      *
     A                                    69'-----------------------------------
     A                                      ----'
     A                                      SPACEB(1)
     A                                    50'Total Amount Due :'
     A                                      SPACEB(001)
     A            RRTTAT        16        69
     A            RRFCCY         3        +1
     A            RRATAT        16        +1
      *
     A          R BLKLIN                    SPACEA(001)
     A                                    69'-----------------------------------
     A                                      ----'
     A                                      SPACEB(1)
     A                                    50'Total Amount Due :'
     A                                      SPACEB(001)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
