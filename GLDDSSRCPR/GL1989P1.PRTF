      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Holdings by portflio & customer')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL1989P1 - Customer Service Fees - Customer Holdings    *
      *                  Report                                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **  Report Header Record Format
      *
     A          R HEADPY                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL1989P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'HOLDINGS BY PORTFOLIO & +
     A                                      CUSTOMER'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'------------------------+
     A                                      --------'
     A                                      SPACEA(2)
      *
      **  Sub-Heading:
      *
     A          R DETAIL
     A                                      SPACEB(2)
     A                                     9'Customer'
     A                                    19'Portfolio'
     A                                    30'Currency'
     A                                    41'Instrument'
     A                                    53'Source'
     A                                    65'Transaction'
     A                                    85'Depot'
     A                                    95'Security'
     A                                   107'Investment'
     A                                      SPACEA(1)
      *
     A                                     9'Number'
     A                                    19'Reference'
     A                                    30'Code'
     A                                    41'Type'
     A                                    53'Holding'
     A                                    65'Reference'
     A                                    85'Reference'
     A                                    95'Shortname'
     A                                   107'Type'
     A                                      SPACEA(2)
      *
     A            R8CNUM         6A        9TEXT('Customer Number')
     A            R8PTFR         4A       19TEXT('Portfolio Reference')
     A            R8CCY          3A       30TEXT('Currency Code')
     A            R8INST         3A       41TEXT('Instrument Type')
     A            R8SRHO         2A       53TEXT('Source of Holding')
     A            R8TREF        18A       65TEXT('Transaction Reference')
     A            R8DPRF        10A       85TEXT('Depot Reference')
     A            R8SESN        10A       95TEXT('Security Shortname')
     A            R8INVT         3A      107TEXT('Investment Type')
     A                                      SPACEA(2)
     A*
     A N80                                21'Assets'
     A N80                                49'Liabilities'
     A N80                                80'Assets by Currency'
     A N80                               102'Liabilities by Currency'
     A                                      SPACEA(1)
      *
     A N81                                 9'Nominal'
     A N81        R8ASNV        21        21TEXT('Assets Nominal')
     A N81        R8LINV        21        49TEXT('Liabilities Nominal')
     A N81        RRASNV        21        80TEXT('Assets Nominal')
     A N81        RRLINV        21       102TEXT('Liabilities Nominal')
     A N81                                  SPACEA(1)
      *
     A N82                                 9'Value'
     A N82        R8ASMV        21        21TEXT('Assets Market Value')
     A N82        R8LIMV        21        49TEXT('Liabilities Market Value')
     A N82        RRASMV        21        80TEXT('Assets Market Value')
     A N82        RRLIMV        21       102TEXT('Liabilities Market Value')
     A N82                                  SPACEA(1)
      *
     A N83                                 9'Forward FX'
     A N83        R8ASFX        21        21TEXT('Assets Forward FX')
     A N83        R8LIFX        21        49TEXT('Liabilities Forward FX')
     A N83        RRASFX        21        80TEXT('Assets Forward FX')
     A N83        RRLIFX        21       102TEXT('Liabilities Forward FX')
     A N83                                  SPACEA(1)
      *
     A N84                                 9'Accrued on'
     A N84        R8ASAC        21        21TEXT('Assets Accrued on')
     A N84        R8LIAC        21        49TEXT('Liabilities Accrued on')
     A N84        RRASAC        21        80TEXT('Assets Accrued on')
     A N84        RRLIAC        21       102TEXT('Liabilities Accrued on')
     A N84                                  SPACEA(1)
      *
      **  Report Trailer
      *
     A          R TRAILPY
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
