      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Drop of Expired Charges Report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL1991P1 - GL Drop of Expired Charges                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD008 - Customer Service Fees Enhancements                  *
      *                                                               *
      *****************************************************************
     A                                               REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE GL1991P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    51'GL Drop of Expired Charges'
      *
     A                                      SPACEA(1)
     A                                    51'--------------------------'
     A                                      SPACEA(3)
      *
      *----------------*
      ***  Sub Heading *
      *----------------*
      *
     A                                     3'Charge'
     A                                    15'Fee'
     A                                    23'Charge'
     A                                    36'Customer'
     A                                    50'Portfolio'
     A                                    65'Final Settlement'
     A                                    86'Charge'
     A                                   110'Settlement'
     A                                      SPACEA(1)
     A                                     2'Reference'
     A                                    15'Type'
     A                                    23'Group'
     A                                    31'Number  Shortname'
     A                                    50'Reference'
     A                                    71'Date'
     A                                    82'Currency        Amount'
     A                                   111'Account'
     A                                      SPACEA(1)
     A                                     2'---------'
     A                                    15'----'
     A                                    23'------'
     A                                    31'-----------------'
     A                                    50'---------'
     A                                    65'----------------'
     A                                    82'----------------------'
     A                                   107'------------------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL                    SPACEA(001)
      *
     A            PPREFE         8A        2TEXT('Reference')
     A            PPFTYP         2A       16TEXT('Fee Type')
     A            PPGROU         2A       25TEXT('Charge Group')
     A            PPCNUM         6A       31TEXT('Customer Number')
     A            PPCSSN        10A       38TEXT('Cust. Shortname')
     A            PPPORT         4A       52TEXT('Portfolio Ref.')
     A            PPFIND         7A       70TEXT('Final Settlement Date')
     A            PPCCY          3A       82TEXT('Charging Currency')
     A            PPAMT         16A       88TEXT('Charging Amount')
     A**Start of change CGL029
     A**********  PPSEAC        18A      107TEXT('Settlement Account')                        CGL029
     A            PPSEAC        24A      107TEXT('Settlement Account')
     A**End of change CGL029
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'*** END OF REPORT ***'
     A                                      SPACEB(1)
