      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Customer Service Fees Charges Report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL1993P1 - Customer Service Fees Charges Report         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSD011             Date 04Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD011 - Performance Incentive Fees.                         *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL1993P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    55'FEES CHARGES REPORT'
      *
     A                                      SPACEA(1)
     A                                    55'-------------------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAP1
     A                                     5'Charge Reference:'
     A            R1REFE         8        +1
     A                                    35'Status:'
     A            R1STAT        20        +1
     A                                      SPACEA(1)
     A                                     5'Fee Type:'
     A            R1FTYP         2        +1
     A                                    35'Customer number:'
     A            R1CNUM         6        +1
     A**Start of change CGL029
     A**********                          75'Portfolio:'
     A                                    81'Portfolio:'
     A**End of change CGL029
     A            R1PORT         4        +1
      * Start of change CSD011
     A  57                               105'High water mark fee:'
     A  57        R1HWMF         1        +1
      * End of change CSD011
     A                                      SPACEA(1)
     A                                     5'Charge Group:'
     A            R1GROU         2        +1
     A                                    35'Charge currency:'
     A            R1CCY          3        +1
     A**Start of change CGL029
     A**********                          75'Charge amount:'                                  CGL029
     A                                    81'Charge amount:'
     A**End of change CGL029
     A            R1AMT         19        +1
     A                                      SPACEA(1)
     A                                     5'Charge Branch:'
     A            R1BRCA         3        +1
     A                                    35'Settlement account:'
     A**Start of change CGL029
     A**********  R1SEAC        18        +1                                                  CGL029
     A**********                          75'Fee Start/Last settlement date:'                 CGL029
     A            R1SEAC        24        +1
     A                                    81'Fee Start/Last settlement date:'
     A**End of change CGL029
     A            R1SLSD         8        +1
     A                                      SPACEA(1)
     A                                     5'Automatic posting:'
     A            R1APOS         1        +1
     A                                    35'Settement generation days:'
     A            R1SEDA         2        +1
     A**Start of change CGL029
     A**********                          75'Settlement frequency:'                           CGL029
     A                                    81'Settlement frequency:'
     A**End of change CGL029
     A            R1FREQ         1        +1
     A                                   105'Settlement day in month:'
     A            R1DAMO         2        +1
     A                                      SPACEA(1)
     A                                     5'First/Next settlement date:'
     A            R1FNSD         8        +1
     A**Start of change CGL029
     A**********                          75'Final settlement date:'                          CGL029
     A                                    81'Final settlement date:'
     A**End of change CGL029
     A            R1FIND         8        +1
     A                                      SPACEA(1)
      * Start of change CSD011
     A  57                                 5'Calculation frequency:'
     A  57        R1CFRE         1        +1
     A  57                                35'Calculation day in month:'
     A  57        R1CDMO         2        +1
     A**Start of change CGL029
     A**57******                          75'Calculation date:'
     A  57                                81'Calculation date:'
     A**End of change CGL029
     A  57        R1NCDA         8        +1
      * End of change CSD011
     A  57                                  SPACEA(1)
     A                                     5'Narrative 1:'
     A            R1NAR1        35        +1
     A**Start of change CGL029
     A**********                          75'Narrative 2:'                                    CGL029
     A                                    81'Narrative 2:'
     A**End of change CGL029
     A            R1NAR2        35        +1
     A                                      SPACEA(1)
     A                                     5'Narrative 3:'
     A            R1NAR3        35        +1
     A**Start of change CGL029
     A**********                          75'Narrative 4:'                                    CGL029
     A                                    81'Narrative 4:'
     A**End of change CGL029
     A            R1NAR4        35        +1
     A                                      SPACEA(1)
     A                                     5'Accrual indicator:'
     A            R1ACCR         1        +1
     A                                    35'Last action type:'
     A            R1CHTP         6        +1
     A**Start of change CGL029
     A**********                          75'Last change date:'                               CGL029
     A                                    81'Last change date:'
     A**End of change CGL029
     A            R1LCD          8        +1
      *
     A                                      SPACEA(3)
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
