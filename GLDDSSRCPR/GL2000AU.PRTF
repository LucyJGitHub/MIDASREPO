      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Turnover statements - audit report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - General Ledger Module
     A*                                                               *
     A*  GL2000AU - TURNOVER STATEMENTS AUDIT REPORT                  *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. S01413 *CREATE     DATE 13APR93               *
     A*                                                               *
     A*  Note: *NONE                                                  *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  S01413  -  Retail 3 Incorporation                            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *************
     A**   AUDIT  HEADER
     A**   *************
     A**
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'GL2000AU'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE    1 '
     A                                      SPACEA(2)
     A                                    48'TURNOVER STATEMENTS - AUDIT REPORT'
     A                                      SPACEA(1)
     A                                    48'----------------------------------'
     A                                      SPACEA(2)
     A**
     A**   ************
     A**   AUDIT DETAIL
     A**   ************
     A**
     A          R DETAU                     SKIPB(10)
     A                                    30'NUMBER OF ACCOUNTS GENERATING STAT-
     A                                      EMENTS :'
     A            P2NOAC        15  0O    74EDTCDE(3)
     A                                      SPACEA(3)
     A                                    30'TOTAL NUMBER OF STATEMENTS GENERAT-
     A                                      ED :'
     A            P2TSTM        15  0O    74EDTCDE(3)
     A**
     A**   ********************
     A**   AUDIT DATABASE ERROR
     A**   ********************
     A**
     A          R ERRAU
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE    R             30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY     R             30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM     R             30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3S 0     30
     A                                    33''''
     A                                    64'*'
     A  90                                11'*    STATUS       '''
     A  90                                  SPACEB(1)
     A  90        DBSTAT         5        30
     A  90                                35''''
     A  90                                64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
