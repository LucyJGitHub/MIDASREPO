      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Turnover statements')
     A*****************************************************************
     A*                                                               *
     A*  Midas - General Ledger
     A*                                                               *
     A*  PRTF/GL2000P1 - TURNOVER STATEMENTS                          *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. EML102             Date 28Nov19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD053             Date 01Jun06               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 063609             Date 30Nov93               *
      *                 S01413 *CREATE     Date 13Apr93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  EML102 - Upgrade of UNL007 to FB2.1 SP21                     *
     A*           UNL007 - Extended Narratives                        *
     A*                Used GL2000P101)                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD053 - Correspondence Manager Multilanguage Upgrade        *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  063609 - Amounts are notated in reverse to rest of system.   *
     A*           i.e. 3,000.00 is printed as 3.000,00.               *
     A*  S01413 - Retail 3 Incorporation                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A*
     A** REPORT HEADER
     A*
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'GL2000P1'
     A            TITL      R             37
     A*/COPY WNCPYSRC,GL2000P103
     A                                   101'DATE'
     A            RUNA      R            106
     A*/COPY WNCPYSRC,GL2000P104
     A                                   119'PAGE'
     A            PAGE           5  0    124EDTCDE(Z) SPACEA(2)
     A                                    56'TURNOVER STATEMENTS'
     A                                      SPACEA(1)
     A                                    55'-------------------'
     A*
     A*****************************************************************
     A          R GL2000FA
     A*
     A** STATEMENT HEADING DETAILS
     A*
     A                                      SKIPB(1)
     A**Start of change CSD053
     A**********  P1CNA1        35   O     1SPACEA(1)                                         CSD053
     A            P1CNA1        35O  O     1SPACEA(1)
     A**End of change CSD053
     A            P1STAT        25A  O    88
     A*/COPY WNCPYSRC,GL2000P105
     A            P1STDT    R            124REFFLD(RUNA)
     A*/COPY WNCPYSRC,GL2000P106
     A**Start of change CSD053
     A**********  P1CNA2        35   O     1SPACEA(1)                                         CSD053
     A**********  P1CNA3        35   O     1SPACEA(1)                                         CSD053
     A**********  P1CNA4        35   O     1                                                  CSD053
     A            P1CNA2        35O  O     1SPACEA(1)
     A            P1CNA3        35O  O     1SPACEA(1)
     A            P1CNA4        35O  O     1
     A**End of change CSD053
     A*
     A**Start of change CGL029
     A**58******  P1BRCA         3   O    46                                                  CGL029
     A**58******                          49'-'                                               CGL029
     A**********  P1CNUM         6   O    50                                                  CGL029
     A**********                          56'-'                                               CGL029
     A**********  P1CCY          3   O    57                                                  CGL029
     A**********                          60'-'                                               CGL029
     A**********  P1ACOD         4   O    61                                                  CGL029
     A  58        P1BRCA         3   O    40
     A  58                                43'-'
     A            P1CNUM         6   O    44
     A                                    50'-'
     A            P1CCY          3   O    51
     A                                    54'-'
     A            P1ACOD        10   O    55
     A**End of change CGL029
     A                                    65'-'
     A            P1ACSQ         6   O    66
     A*
     A  21        P1ACNO        10   O    71
     A*
     A  58                                88'BRANCH -'
     A  58        P1BRNM        30   O    97
     A            PAGE           5  0O   126SPACEB(1)
     A                                      EDTCDE(Z)
     A            P1ANAM        20   O    14SPACEB(2)
     A            P1CYNM        15   O    50
     A            RUNA           7   O    80
     A*/COPY WNCPYSRC,GL2000P107
     A            P1STNO         3  0O   104EDTCDE(Z)
     A            P1STSQ         5  0O   110EDTCDE(Z)
     A*
     A*****************************************************************
     A          R GL2000FB
     A*
     A** BROUGHT FORWARD BALANCE DETAILS
     A*
     A                                      SPACEB(6)
     A            P1LSTD         8A  O     1
     A  20        P1COPY        10A  O    18
     A                                    56'B/F'
     A  30        P1BFW0        15S 0O   108EDTWRD('   ,   ,   ,   , 0 ')
     A  31        P1BFW1        15S 0O   107EDTWRD('  ,   ,   ,   , 0 . ')
     A  32        P1BFW2        15S 0O   107EDTWRD(' ,   ,   ,   , 0 .  ')
     A  33        P1BFW3        15S 0O   108EDTWRD('   ,   ,   , 0 .   ')
     A            P1OPNB         6A  O   127
     A                                      SPACEA(1)
     A*/COPY WNCPYSRC,GL2000P108
     A*****************************************************************
     A          R GL2000FC
     A*
     A** CARRIED FORWARD BALANCE DETAILS
     A*
     A                                    56'C/F'
     A  30        P1CFW0        15S 0O   108EDTWRD('   ,   ,   ,   , 0 ')
     A  31        P1CFW1        15S 0O   107EDTWRD('  ,   ,   ,   , 0 . ')
     A  32        P1CFW2        15S 0O   107EDTWRD(' ,   ,   ,   , 0 .  ')
     A  33        P1CFW3        15S 0O   108EDTWRD('   ,   ,   , 0 .   ')
     A            P1CLOB         6A  O   127
     A*
     A*****************************************************************
     A          R GL2000F1
     A*
     A** ACCOUNT POSTINGS
     A*
     A            P1PSTD    R              2REFFLD(RUNA)
     A            P1VALD    R             12REFFLD(RUNA)
     A            P1CHQN         8A  O    22
     A            P1PNAR        30A  O    31
     A            P1PNUM         3A  O    62
     A*
     A  30N50     P1DRA0        13S 0O    65EDTWRD(' ,   ,   ,   , 0 ')
     A  31N50     P1DRA1        13S 0O    65EDTWRD('   ,   ,   , 0 . ')
     A  32N50     P1DRA2        13S 0O    65EDTWRD('  ,   ,   , 0 .  ')
     A  33N50     P1DRA3        13S 0O    65EDTWRD(' ,   ,   , 0 .   ')
     A  30 50     P1CRA0        13S 0O    87EDTWRD(' ,   ,   ,   , 0 ')
     A  31 50     P1CRA1        13S 0O    87EDTWRD('   ,   ,   , 0 . ')
     A  32 50     P1CRA2        13S 0O    87EDTWRD('  ,   ,   , 0 .  ')
     A  33 50     P1CRA3        13S 0O    87EDTWRD(' ,   ,   , 0 .   ')
     A                                   132' '
     A                                      SPACEA(1)
     A*****************************************************************
     A*/COPY WNCPYSRC,GL2000P101
     A          R GL2000F3
     A*
     A** BLANK LINE FORMAT
     A                                   132' '
     A                                      SPACEB(1)
     A*/COPY WNCPYSRC,GL2000P102
     A*****************************************************************
     A          R ENDP1
     A*
     A** REPORT END
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(3)
     A*****************************************************************
     A          R ERRP1
     A*
     A** REPORT DATABASE ERROR
     A                                      SPACEB(02)
     A                                     9'REFERENCE GL2000'
     A                                    45'PRINT TERMINATED, '
     A                                    64'DATABASE IN ERROR'
