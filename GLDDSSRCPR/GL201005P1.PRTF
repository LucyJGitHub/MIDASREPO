      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Head office foreign ccy I/E to base ccy')     *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL201005P1 - Head Office Foreign Currency Income and    *
      *                    Expense Transfer to Base Currency report   *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. CGL201  *CREATE    Date 22Sep23               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL201 - New Reports for Daily Transfer of Foreign Exchange  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra Internation+
     A                                      al Limited 2023')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL201005P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    06'SUMMARY FOR HEAD OFFICE -'
     A                                    40'FOREIGN CURRENCY INCOME +
     A                                      AND EXPENSE'
      *
     A  37                                89'BRANCH'
     A  37        A8BRCD    R             96REFFLD(BRCA)
     A  37                                99'-'
     A  37        A8BRNM    R            101REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(3)
      *
      ** Sub-Heading:
      ** ------------
      *
     A                                     8'Value'
     A                                   100'EUR Equivalent'
     A                                   119'FX Spot'
      *
     A                                      SPACEA(1)
      *
     A                                     1'Branch'
     A                                     8'Date'
     A                                    16'Acct code'
     A                                    27'Ccy'
     A                                    31'Today Transfer'
     A                                    52'Current Mth'
     A                                    76'YTD Balance'
     A                                   100'Today Transfer'
     A                                   119'Rates'
      *
     A                                      SPACEA(2)
      *
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
      *
     A          R DETAILP1
      *
     A            R1BRCH         3A        2
     A            R1VDAT         7A       +3
     A            R1ACOD        10A       +1
     A            R1CCY          3A       +1
     A            R1PSTA        17A       +1
     A            R1DRCR1        2A       +1
     A            R1MTDA        20A       +1
     A            R1DRCR2        2A       +1
     A            R1YTDA        20A       +1
     A            R1DRCR3        2A       +1
     A            R1BPSTA       17A       +1
     A            R1SPOT        14A       +1
     A                                      SPACEA(1)
      *
      *-------------*
      ** Blank line *
      *-------------*
      *
     A          R BLANKP1
     A                                   132' '
     A                                      SPACEA(1)
      *
      *-----------------*
      ** Report Trailer *
      *-----------------*
      *
     A          R TRAILP1
     A                                      SPACEB(3)
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH'
     A  37        A8BRCD    R             +1REFFLD(BRCA)
     A  37                                +1'***'
     A N37                                51'***  END OF REPORT  ***'
      *
