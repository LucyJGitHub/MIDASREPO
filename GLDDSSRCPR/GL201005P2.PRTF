      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Local branch foreign ccy I/E to base ccy')    *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL201005P2 - Local Branch Foreign Currency Income and    *
      *                    Expense Transfer to Base Currency report   *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. AR854120           Date 22Sep23               *
      *  Prev Amend No. CGL201  *CREATE    Date 22Sep23               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR854120 - Seoul reports with SUMMARY FOR SINGAPORE BRANCH - *
      *             Added variable field to pass correct header       *
      *             statement for the Country Code (CHILD: AR854120)  *
      *  CGL201 - New Reports for Daily Transfer of Foreign Exchange  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADP2                    TEXT('(c) Finastra Internation+
     A                                      al Limited 2023')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL201005P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
      ** Start of change AR854120
     A            R2HSUM        30A        6
      ** End of change AR854120
     A                                    40'FOREIGN CURRENCY INCOME +
     A                                      AND EXPENSE'
      *
     A  37                                89'BRANCH'
     A  37        A8BRCD    R             96REFFLD(BRCA)
     A  37                                99'-'
     A  37        A8BRNM    R            101REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(3)
      *
      ** Sub-Heading:
      ** ------------
      *
     A                                     8'Value'
     A                                   100'EUR Equivalent'
     A                                   119'FX Spot'
     A                                      SPACEA(1)
      *
     A                                     1'Branch'
     A                                     8'Date'
     A                                    16'Acct code'
     A                                    27'Ccy'
     A                                    31'Today Transfer'
     A                                    52'Current Mth'
     A                                    76'YTD Balance'
     A                                   100'Today Transfer'
     A                                   119'Rates'
     A                                      SPACEA(2)
      *
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
      *
     A          R DETAILP2
      *
     A            R2BRCH         3A        2
     A            R2VDAT         7A       +3
     A            R2ACOD        10A       +1
     A            R2CCY          3A       +1
     A            R2PSTA        17A       +1
     A            R2DRCR1        2A       +1
     A            R2MTDA        20A       +1
     A            R2DRCR2        2A       +1
     A            R2YTDA        20A       +1
     A            R2DRCR3        2A       +1
     A            R2BPSTA       17A       +1
     A            R2SPOT        14A       +1
     A                                      SPACEA(1)
      *
      *-------------*
      ** Blank line *
      *-------------*
      *
     A          R BLANKP2
     A                                   132' '
     A                                      SPACEA(1)
      *
      *--------*
      ** Total *
      *--------*
      *
     A          R TOTALP2
     A                                      SPACEB(1)
      *
     A                                    31'--------------------'
     A                                   100'-----------------'
     A                                      SPACEA(1)
      *
     A                                     2'Total'
      *
     A            R2CCY          3A       27
     A            R2CCYPSTA     17A       31
     A            R2DRCR         2A       49
     A            R2CCYBPSTA    17A      100
     A                                      SPACEA(1)
      *
     A                                    31'--------------------'
     A                                   100'-----------------'
     A                                      SPACEA(2)
      *
      *-----------------*
      ** Report Trailer *
      *-----------------*
      *
     A          R TRAILP2
     A                                      SPACEB(3)
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH'
     A  37        A8BRCD    R             +1REFFLD(BRCA)
     A  37                                +1'***'
     A N37                                51'***  END OF REPORT  ***'
      *
