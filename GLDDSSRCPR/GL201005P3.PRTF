      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Audit trail for foreign currency I/E')        *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL201005P3 - Audit Trail for Foreign Currency Income    *
      *                    and Expense                                *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. CGL201  *CREATE    Date 22Sep23               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL201 - New Reports for Daily Transfer of Foreign Exchange  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADP3                    TEXT('(c) Finastra Internation+
     A                                      al Limited 2023')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL201005P3'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    16'AUDIT TRAIL -'
     A                                    40'FOREIGN CURRENCY INCOME +
     A                                      AND EXPENSE'
      *
     A  37                                89'BRANCH'
     A  37        A8BRCD    R             96REFFLD(BRCA)
     A  37                                99'-'
     A  37        A8BRNM    R            101REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(3)
      *
      ** Sub-Heading:
      ** ------------
      *
     A                                    51'Dr/'
     A                                    56'Source'
     A                                    71'Original'
     A                                    84'Original'
     A                                    97'Original'
     A                                      SPACEA(1)
      *
     A                                     2'Posting'
     A                                    11'Customer'
     A                                    21'Currency'
     A                                    32'Posting'
     A                                    51'Cr'
     A                                    56'Of'
     A                                    65'Brch'
     A                                    71'Transaction'
     A                                    84'Transaction'
     A                                    97'Transaction'
     A                                   114'Account'
     A                                   126'Account'
     A                                      SPACEA(1)
      *
     A                                     2'Date'
     A                                    11'Number'
     A                                    21'Code'
     A                                    32'Amount'
     A                                    51'Ind'
     A                                    56'Posting'
     A                                    65'Code'
     A                                    71'Type'
     A                                    84'Sub-type'
     A                                    97'Reference'
     A                                   114'Code'
     A                                   126'Seq.'
     A                                      SPACEA(2)
      *
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
      *
     A          R DETAILP3
      *
     A            R3PSTD         7A        2
     A            R3CNUM         6A       13
     A            R3CCY          3A       21
     A            R3PSTA        17A       32
     A            R3DRCR         2A       51
     A            R3SPOS         7A       56
     A            R3BRCA         3A       65
     A            R3TRTY         2A       71
     A            R3TRSU         2A       84
     A            R3TREF        15A       97
     A            R3ACOD        10A      114
     A            R3ACSQ         2A      126
     A                                      SPACEA(1)
      *
      *--------*
      ** Total *
      *--------*
      *
     A          R ACDTOTALP3
     A                                      SPACEB(1)
      *
     A                                    21'TOTAL'
     A            R3ACDPSTA     17A       32
     A            R3ACDDRCR2     2A       51
     A                                      SPACEA(2)
      *
      *-----------------*
      ** Currency Total *
      *-----------------*
      *
     A          R CCYTOTALP3
      *
     A                                    21'CCY TOTAL'
     A            R3CCYPSTA     17A       32
     A            R3ACDDRCR3     2A       51
     A                                      SPACEA(2)
      *
      *-----------------*
      ** Report Trailer *
      *-----------------*
      *
     A          R TRAILP3
     A                                      SPACEB(3)
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH'
     A  37        A8BRCD    R             +1REFFLD(BRCA)
     A  37                                +1'***'
     A N37                                51'***  END OF REPORT  ***'
      *
