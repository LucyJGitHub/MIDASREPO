      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Transfer Batch entries to History')
      *****************************************************************
      *                                                               *
      *  Midas  General Ledger Module                                 *
      *                                                               *
      *      GL2560AU - TRANSFER BATCHES TO HISTORY AUDIT REPORT      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL013             Date 20Feb02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. xxxxxx  *CREATE    Date ddMmmyy               *
      *                                                               *
      **--------------------------------------------------------------*
      *                                                               *
      *  CGL013 - MT94x Message Generation.                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Header Format
      *
     A          R GL2560H                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL2560AU'
     A            BJURPT        53        40
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   120'PAGE'
     A                                   125PAGNBR
     A                                      EDTCDE(J)
     A                                      SPACEA(2)
     A                                    50'BATCH ENTRY HISTORY FILE'
     A                                    75'TRANSFER'
      *
      ** Detail Format
      *
     A          R GL2560D                   TEXT('Detail Format')
     A                                      SPACEB(11)
     A                                    11'HISTORIC BATCH HEADER +
     A                                      FILE GLJEHHPD'
     A                                      SPACEA(2)
     A                                    11'RECORDS WRITTEN'
     A                                    54'-'
     A            NHRW          15S 0     57EDTCDE(Z)
      *
     A                                      SPACEA(3)
     A                                    11'HISTORIC BATCH ENTRY +
     A                                      FILE GLJEHPPD'
     A                                      SPACEA(2)
     A                                    11'RECORDS WRITTEN'
     A                                    54'-'
     A            NPRW          15S 0     57EDTCDE(Z)
     A*/COPY WNCPYSRC,GL2560AU01
      *
     A                                      SPACEA(3)
      **Start of change CGL013
     A  35                                11'HISTORIC BATCH +
     A                                      ADDITIONAL INFORMATION +
     A                                      FILE GLJEHXPD'
     A  35                                  SPACEA(2)
     A  35                                11'RECORDS WRITTEN'
     A  35                                54'-'
     A  35        NXRW          15S 0     57EDTCDE(Z)
     A  35                                  SPACEA(3)
      **End of change CGL013
     A                                    11'HISTORIC BATCH SUMMARY +
     A                                      FILE GLJEHSPD'
     A                                      SPACEA(2)
     A                                    11'RECORDS WRITTEN'
     A                                    54'-'
     A            NSRW          15S 0     57EDTCDE(Z)
      *
      ** Error Format
      *
     A          R GL2560E                   TEXT('Error Format')
     A                                      SKIPB(20)
     A                                    40'*** Database in Error ***'
      *
     A                                      SPACEA(1)
     A                                    44'File'
     A                                    55':-'
     A            DBFILE         8        59
      *
     A                                      SPACEA(1)
     A                                    44'Key'
     A                                    55':-'
     A            DBKEY         29        59
      *
     A                                      SPACEA(1)
     A                                    44'In Program'
     A                                    55':-'
     A            DBPGM         10        59
      *
     A                                      SPACEA(1)
     A                                    44'At point'
     A                                    55':-'
     A            DBASE          3S 0     59
      *
      ** End of Report Format
      *
     A          R GL2560T                   TEXT('End of Report Format')
     A                                      SPACEB(10)
     A                                    56'*** END OF REPORT ***'
