      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas-Fontis I/face - daily statement generation')
      *****************************************************************
      *                                                               *
      *  Midas-Fontis Interface                                       *
      *                                                               *
      *  PRTF/GL2804AU - Daily Statement Generation - Audit           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CGL004 *CREATE      Date 08JAN97              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL004 - Midas-Fontis Interface                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R GL2804A1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                      SPACEA(2)
     A                                     2'Reference GL2804AU'
     A            BJURPT        53        35
     A                                    93'DATE'
     A            BJMRDT         7        98
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    35'DAILY STATEMENT GENERATION +
     A                                       REPORT'
     A                                      SPACEA(1)
     A                                    35'~~~~~~~~~~~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~'
     A                                      SPACEA(1)
      *
     A          R GL2804A2                  SPACEB(5)
     A                                    25'FILE : GLFONAPD'
     A                                      SPACEA(1)
     A                                    25'~~~~~~~~~~~~~~~'
     A                                      SPACEA(1)
     A                                    30'No. of Accounts'
     A                                    46'Read :'
     A            W@RED          3  0     59
     A                                      SPACEA(1)
     A                                    46'Updated :'
     A            W@UPD          3  0     59
     A                                      SPACEA(1)
     A                                    46'Deleted :'
     A            W@DEL          3  0     59
     A                                      SPACEA(4)
     A                                    25'FILE : GLFDTAPD'
     A                                      SPACEA(1)
     A                                    25'~~~~~~~~~~~~~~~'
     A                                      SPACEA(1)
     A                                    30'No. of Accounts'
     A                                    46'Written :'
     A            W@WRI          3  0     59
      *
     A          R GL2804A3                  SPACEB(5)
     A                                    25'WARNING'
     A                                      SPACEA(1)
     A                                    25'======='
     A                                      SPACEA(1)
     A                                    30'Account No.'
     A**Start of change CGL029
     A**********  W@ACC         18        44                                                  CGL029
     A            W@ACC         24        44
     A**End of change CGL029
     A                                      SPACEA(1)
     A                                    44'Warning : Discrepancy found +
     A                                      in account balance'
      *
     A          R GL2804A4                  SPACEB(20)
     A
     A                                    50'*** NO MESSAGES TO REPORT ***'
     A                                      SPACEA(1)
      *
     A          R GL2804A5
     A                                    19'DAILY STATEMENT GENERATION +
     A                                      DATABASE ERROR REPORT.'
     A                                      SPACEA(12)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
      *
     A          R GL2804A6                  SPACEB(5)
     A
     A                                    45'*** END OF REPORT ***'
