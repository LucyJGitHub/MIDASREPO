      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Fontis Narrative Codes Table Printer file')
      *****************************************************************
      *                                                               *
      *  Midas - Midas-Fontis Interface                               *
      *                                                               *
      *  PRTF/GL2811P1 - Fontis Narrative Tables Codes Printer File   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No.  CGL004 *CREATE   Date 10MAR97                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL004 - Midas-Fontis Interface                              *
      *                                                               *
     A*****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A                                    12'GL2811P1'
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('Midas Rundate')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##ARTL        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##UNDL        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SPACEB(3)
     A                                      SPACEA(1)
      *.........................................................................
     A                                    10'Trans.'
     A                                    18'Funds'
     A                                    27'Language'
     A                                    37'Narrative'
     A                                    73'SWIFT Trans.'
     A                                    86'Midas Mod.'
     A                                    99'Last Change'
     A                                   113'Last Change'
      *
     A                                      SPACEA(1)
     A                                    11'Type'
     A                                    17'Transfer'
     A                                    29'Code'
     A                                    77'Type'
     A                                    89'ID'
     A                                   102'Date'
     A                                   116'Type'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
     A            XRPMTT         2        12TEXT('Trans Type')
     A            XRFTTR         1        20TEXT('Funds Transfer')
     A            XRLANC         2        30TEXT('Language Code')
     A            XRNATV        34        37TEXT('Narrative')
     A            XRSTTI         3        77TEXT('SWIFT Trans. Type')
     A            XRTRMD         1        90TEXT('Midas Module ID')
     A            XRACDT         7       101TEXT('alpha change date')
     A            XRALCH         6       115TEXT('alpha of last change')
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
