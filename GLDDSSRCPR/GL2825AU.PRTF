      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL JE batches converted from Fontis - audit')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL2825AU - Journal Entry Interface Batches Converted    *
      *                  from Fontis IATs - Audit                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *  Last Amend No. CGL009  *Create    Date 18May99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL009 - Midas-Fontis Inter-Account Transfers (IAT) and      *
      *           Third Party Payments (TPP) Interface                *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL2825AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    30'Journal Entry Interface Batches +
     A                                      Converted from Fontis IATs - audit +
     A                                      report'
     A                                      SPACEA(1)
      *
     A                                    30'--------------------------------+
     A                                      -----------------------------------+
     A                                      ------'
     A                                      SPACEA(3)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    33'No posting entries found for IAT +
     A                                       batch'
     A                                      SPACEB(3)
     A            RNOBAT         3        72
     A                                    75'.  Batch not processed.'
      *
      *-----------------------------*
      ***  ICD range used up, reset *
      *-----------------------------*
      *
     A          R RESET
     A                                    36'The upper limit on the ICD batch +
     A                                       number range has been reached.'
     A                                      SPACEB(3)
     A                                    34'Batch number allocation has been +
     A                                       reset from the lowest available +
     A                                       number'
     A                                      SPACEB(1)
      *---------------------------------*
      ***  Whole range has been used up *
      *---------------------------------*
      *
     A          R LIMITS
     A                                    36'All batch numbers (001-999) have  +
     A                                       already been used up.'
     A                                      SPACEB(3)
     A                                    34'No further IAT batches may be +
     A                                       processed for the day.'
     A                                      SPACEB(1)
      *-------------------------------*
      ***  Successful output of Batch *
      *-------------------------------*
      *
     A          R SUCCES
     A                                    36'Fontis batch'
     A                                      SPACEB(3)
     A            SFBTCH         3        49
     A                                    53'has been added to the Journal Entr+
     A                                      y Interface file with batch number'
     A            SIBTCH         3       122
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBEROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
