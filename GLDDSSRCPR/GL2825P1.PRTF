      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL JE batches converted from Fontis')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL2825P1 - Journal Entry Interface Batches Converted    *
      *                  from Fontis IATs                             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CGL009  *CREATE    Date 18May99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL009 - Midas-Fontis Inter-Account Transfers (IAT) and      *
      *           Third Party Payments (TPP) Interface                *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL2825P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    37'Journal Entry Interface Batches +
     A                                      Converted from Fontis IATs'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    37'--------------------------------+
     A                                      --------------------------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Branch Header Format *
      *-------------------------*
      *
     A          R HEADP2
      *
     A                                     2'Batch Branch Code ...:'
     A                                      SPACEB(2)
     A            R1BCBH         3        25TEXT('Branch Code - Batch Header')
     A            R1BNMD        30        29TEXT('Branch name - display')
      *
      *------------------------*
      ***  Batch Header Format *
      *------------------------*
      *
     A          R HEADP3
      *
     A                                     2'Batch................:'
     A                                      SPACEB(2)
     A            RBATCH         3        25TEXT('Batch number')
     A                                    90'Description:'
     A            RDESCR        30       103TEXT('Batch Description')
     A                                     2'Original Fontis Batch:'
     A                                      SPACEB(1)
     A            ROFONT         3        25TEXT('Original Fontis Batch Number')
     A                                     2'Department...........:'
     A                                      SPACEB(1)
     A            RDCODE         3        25TEXT('Department Code')
     A            RDNAME        30        29TEXT('Department Name')
     A                                    90'Total: Input......:'
     A            RINTOT        23       110TEXT('Formatted Amount 1')
     A N80                                 2'Multiple Branch Batch:'
     A                                      SPACEB(1)
     A N80        RMBRCH         1        25TEXT('Multiple Branch Indicator')
     A                                    98'Calculated:'
     A            RCALCA        23       110TEXT('Formatted Amount 2')
     A                                     2'Terminal.............:'
     A                                      SPACEB(1)
     A            RTERMI        10        25TEXT('Terminal I.D.')
     A                                    90'Items: Input......:'
     A            RINTEM         3  0    130TEXT('Items Input')
     A                                      EDTCDE(3)
     A                                    98'Calculated:'
     A                                      SPACEB(1)
     A            RCALCI         3  0    130TEXT('Items Calculated')
     A                                      EDTCDE(3)
      *
     A**Start of change CGL029
     A**80******                           2'Item        Account number        -              CGL029
     A  80                                 2'Item      Account number          -
     A**End of change CGL029
     A                                                       Amount            -
     A                                                 Narrative               -
     A                                       Type    Retail A/C'
     A                                      SPACEB(2)
     A**Start of change CGL029
     A**80******                           2'----      ------------------      -              CGL029
     A  80                                 2'---- ------------------------     -
     A**End of change CGL029
     A                                              -------------------------  -
     A                                         -----------------------------   -
     A                                       -----   ----------'
     A                                      SPACEB(1)
      *
     A**Start of change CGL029
     A*N80******                           2'Item        Account number     Bra-              CGL029
     A N80                                 2'Item      Account number       Bra-
     A**End of change CGL029
     A                                      nch              Amount            -
     A                                                 Narrative               -
     A                                       Type    Retail A/C'
     A                                      SPACEB(2)
     A**Start of change CGL029
     A*N80******                           2'----      ------------------   ----              CGL029
     A N80                                 2'---- ------------------------  ----
     A**End of change CGL029
     A                                      ---     -------------------------  -
     A                                         -----------------------------   -
     A                                       -----   ----------'
     A                                      SPACEB(1)
      *
      *-------------------*
      ***  Report Details *
      *-------------------*
      *
     A          R DETAIL
      *
     A                                      SPACEB(1)
     A            RDBINB         3  0      2TEXT('Batch item no.')
     A                                      EDTCDE(4)
     A**Start of change CGL029
     A**********  RDCUST         6        12TEXT('Customer number')                           CGL029
     A**********  RDCYCD         3        19TEXT('Currency code')                             CGL029
     A**********  RDACCD         4        23TEXT('Account code')                              CGL029
     A**********  RDACSN         2        28TEXT('Account sequence number')                   CGL029
     A*N80******  RDIBCA         3        34TEXT('Input Branch Code - Alpha')                 CGL029
     A            RDCUST         6         7TEXT('Customer number')
     A            RDCYCD         3        14TEXT('Currency code')
     A            RDACCD        10        18TEXT('Account code')
     A            RDACSN         2        29TEXT('Account sequence number')
     A N80        RDIBCA         3        34TEXT('Input Branch Code - Alpha')
     A**End of change CGL029
     A            RDPAMP        23        44TEXT('Posting Amount')
     A            RDDCIN         1        68TEXT('Debit/credit indicator')
     A            RDNRTV        30        73TEXT('Narrative')
     A            RDTRTY         5       107TEXT('Transaction type')
     A N72        RDRACN        10  0    115TEXT('Retail account number')
      *
     A N73                                12'Value Date.......:'
     A                                      SPACEB(1)
     A N73        RDVDTP         8        31TEXT('Value Date')
     A N74                                44'Profit centre.:'
     A N74        RDPRCN         4        60TEXT('Profit centre')
     A N75                                67'Book Code.:'
     A N75        RDBKCD         2        79TEXT('Book Code')
     A N76                                84'Original Ccy/Amount.:'
     A N76        RDOCCY         3       106TEXT('Original Currency')
     A N76        RDOAMT        23       110TEXT('Original Amount')
      *
     A  31                                49'Account number is not recognised.'
     A                                      SPACEB(1)
      *
     A  32                                40'The matching spot trade account +
     A                                      posting is missing.'
     A                                      SPACEB(1)
      *
     A  33                                43'The matching client account post+
     A                                      ing is missing.'
     A                                      SPACEB(1)
      *
     A  34                                13'Spot trade account number not comp+
     A                                      letely determined as matching clien+
     A                                      t account posting is not recognised+
     A                                      .'
     A                                      SPACEB(1)
      *
     A                                    12'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----------------'
     A                                      SPACEB(1)
      *
      *---------------------*
      ***  Report Imbalance *
      *---------------------*
      *
     A          R IMBALN
      *
     A                                    48'**** IMBALANCE IN CURRENCY'
     A                                      SPACEB(3)
     A            RDCURR         3        75
     A                                    79'****'
      *
