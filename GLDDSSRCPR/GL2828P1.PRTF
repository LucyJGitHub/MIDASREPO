      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Gen base ccy equiv entries for Fontis')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL2828P1 - GL Generated Base Currency Equivalent        *
      *                  Entries for Fontis IATs                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *  Prev Amend No. CGL009  *Create    Date 18May99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL009 - Midas-Fontis Inter-Account Transfers (IAT) and      *
      *           Third Party Payments (TPP) Interface                *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL2828P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    38'Generated Base Currency Equivale+
     A                                      nt Entries for Fontis IATs'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    38'--------------------------------+
     A                                      --------------------------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Branch Header Format *
      *-------------------------*
      *
     A          R HEADP2
      *
     A                                     2'Batch Branch Code ...:'
     A                                      SPACEB(2)
     A            R1BCBH         3        25TEXT('Branch Code - Batch Header')
     A            R1BNMD        30        29TEXT('Branch name - display')
      *
      *------------------------*
      ***  Batch Header Format *
      *------------------------*
      *
     A          R HEADP3
      *
     A                                     2'Batch................:'
     A                                      SPACEB(2)
     A            RBATCH         3        25TEXT('Batch number')
     A                                    90'Description:'
     A            RDESCR        30       103TEXT('Batch Description')
     A                                     2'Department...........:'
     A                                      SPACEB(1)
     A            RDCODE         3        25TEXT('Department Code')
     A            RDNAME        30        29TEXT('Department Name')
     A                                    90'Total: Input......:'
     A            RINTOT        23       110TEXT('Formatted Amount 1')
     A N80                                 2'Multiple Branch Batch:'
     A                                      SPACEB(1)
     A N80        RMBRCH         1        25TEXT('Multiple Branch Indicator')
     A                                    98'Calculated:'
     A            RCALCA        23       110TEXT('Formatted Amount 2')
     A                                     2'Terminal.............:'
     A                                      SPACEB(1)
     A            RTERMI        10        25TEXT('Terminal I.D.')
     A                                    90'Items: Input......:'
     A            RINTEM         3  0    130TEXT('Items Input')
     A                                      EDTCDE(3)
     A                                    98'Calculated:'
     A                                      SPACEB(1)
     A            RCALCI         3  0    130TEXT('Items Calculated')
     A                                      EDTCDE(3)
      *
     A  80                               114'Profit       Book'
     A                                      SPACEB(2)
     A  80                                 2'Item      Account number          -
     A                                               Amount                    -
     A                                      Narrative                 Value Dat-
     A                                      e       centre       code'
     A                                      SPACEB(1)
     A**Start of change CGL029
     A**80********                         2'----    ------------------        -
     A  80                                 2'---- ------------------------     -
     A**End of change CGL029
     A                                          --------------------   ---------
     A                                      ---------------------   ------------
     A                                      ---   ----------   --------'
     A                                      SPACEB(1)
      *
     A N80                               114'Profit       Book'
     A                                      SPACEB(2)
     A**Start of change CGL029
     A*N80******                           2'Item      Account number     Branc-              CGL029
     A**********                            h        Amount                    -              CGL029
     A N80                                 2'Item      Account number      Bran-
     A                                      ch       Amount                    -
     A**End of change CGL029
     A                                      Narrative                 Value Dat-
     A                                      e       centre       code'
     A                                      SPACEB(1)
     A**Start of change CGL029
     A*N80******                           2'----    ------------------   ------              CGL029
     A**********                            -   --------------------   ---------              CGL029
     A N80                                 2'---- ------------------------ -----
     A                                      --  --------------------   ---------
     A**End of change CGL029
     A                                      ---------------------   ------------
     A                                      ---   ----------   --------'
     A                                      SPACEB(1)
      *
      *-------------------*
      ***  Report Details *
      *-------------------*
      *
     A          R DETAIL
      *
     A                                      SPACEB(1)
     A            RDBINB         3  0      2TEXT('Batch item no.')
     A                                      EDTCDE(4)
     A**Start of change CGL029
     A**********  RDCUST         6        10TEXT('Customer number')                           CGL029
     A**********  RDCYCD         3        17TEXT('Currency code')                             CGL029
     A**********  RDACCD         4        21TEXT('Account code')                              CGL029
     A**********  RDACSN         2        26TEXT('Account sequence number')                   CGL029
     A*N80******  RDIBCA         3        32TEXT('Input Branch Code - Alpha')                 CGL029
     A            RDCUST         6         7TEXT('Customer number')
     A            RDCYCD         3        14TEXT('Currency code')
     A            RDACCD        10        18TEXT('Account code')
     A            RDACSN         2        29TEXT('Account sequence number')
     A N80        RDIBCA         3        33TEXT('Input Branch Code - Alpha')
     A**End of change CGL029
     A            RDPAMP        18        40TEXT('Posting Amount')
     A            RDDCIN         1        59TEXT('Debit/credit indicator')
     A            RDNRTV        30        63TEXT('Narrative')
     A N61        RDVDTP         8        98TEXT('Value Date')
     A            RDPRCN         4       115TEXT('Profit centre')
     A            RDBKCD         2       128TEXT('Book Code')
      *
     A                                    10'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -------------------'
     A                                      SPACEB(2)
      *
      *---------------------*
      ***  Report Imbalance *
      *---------------------*
      *
     A          R IMBALN
      *
     A                                    30'**** Base currency posting generat-
     A                                      ed for batch'
     A                                      SPACEB(3)
     A            RDBTCH         3        77
     A                                    81'is out of balance ****'
      *
      *---------------------*
      ***  Report Imbalance *
      *---------------------*
      *
     A          R NODTLS
      *
     A                                    37'No Base Currency Equivalent Entrie-
     A                                      s generated for batch'
     A                                      SPACEB(3)
     A            RNOBAT         3        93
      *
