      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Collateral details report - detail')          *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL3300P1 - Collateral Details Report - Detail           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CER073C            Date 09Oct17               *
      *  Prev Amend No. CER073A            Date 05Sep17               *
      *                 CER073             Date 10Jul17               *
      *                 AR1008615          Date 25Nov12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 BUG17000           Date 11Apr08               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL014             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 CGL018             Date 28Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CGL011  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER073C - CER073 Change Control 3                            *
      *  CER073A - CER073 Change Control 1                            *
      *  CER073 - AnaCredit Reporting                                 *
      *  AR1008615 - Input 'no. of units' is missing (Child:AR1008618)*
      *  BUG17000 - Incorrect details in Collateral Details Report    *
      *  CGL014 - Collaterals Processing                              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL018 - Risk Weighted Assets                                *
      *  CGL011 - Collateral Processing for Midas                     *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
      *
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE GL3300P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A  20                                51
     A                                      'Collateral Details Report - AUDIT'
     A  20 28                             87'BRANCH'
     A  20        RBRCA     R             94
     A                                      REFFLD(BBRC)
     A  20 28                             97'-'
     A  20        RBRNA     R             98
     A                                      REFFLD(DBRNM)
     A                                      SPACEA(001)
      *
     A  20                                51
     A                                      '---------------------------------'
     A                                      SPACEA(003)
      *
     A N20                                49
     A                                      'Collateral Details Report - FULL -
     A                                      List'
     A N20 28                             87'BRANCH'
     A N20        RBRCB     R             94
     A                                      REFFLD(BBRC)
     A N20 28                             97'-'
     A N20        RBRNB     R             98
     A                                      REFFLD(DBRNM)
     A                                      SPACEA(001)
      *
     A N20                                49
     A                                      '----------------------------------
     A                                      ----'
     A                                      SPACEA(003)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'Collateral Reference:'
     A            RCREF         10        28
     A                                    42'Description:'
     A            RDESC         30        55
     A                                    89'Action:'
     A            RTYLC          1        97
     A                                      SPACEA(002)
     A
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A                                     6'Entity Level....:'
     A            RENTY          1        24TEXT('Entity Level')
     A                                    33'Company........:'
     A            RCMPY          3        50TEXT('Company')
     A                                    60'Collateral Type:'
     A            RCOLT          5        77TEXT('Collateral Type')
     A                                    87'Collateral Customer.:'
     A            RCNUM         10       109TEXT('Collateral Customer')
     A                                      SPACEA(001)
      *
     A                                     6'General/Specific:'
     A            RGNSP          1        24TEXT('General/Specific Indicator')
     A                                    33'Cross Reference:'
     A            RXREF         20        50TEXT('Collateral Cross Reference')
     A                                    87'Status..............:'
     A            RSTAT          1       109TEXT('Status')
     A                                      SPACEA(001)
      *
     A                                     6'Signing Date....:'
     A            RSDAT          7        24TEXT('Signing Date')
     A                                    33'Value Date.....:'
     A            RVDAT          7        50TEXT('Value Date')
     A                                    60'End Date.......:'
     A            REDAT          7        77TEXT('End Date')
     A                                      SPACEA(001)
      *
     A                                     6'Renewable.......:'
     A            RRNEW          1        24TEXT('Renewable Indicator')
     A                                    33'Review Date....:'
     A            RRVDT          7        50TEXT('Review Date')
     A                                    60'Freq/Day No....:'
     A            RRFRQ          1        77TEXT('Review Frequency')
     A                                    78'/'
     A            RRDNO          2S 0     79TEXT('Review Day Number')
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                     6'Currency........:'
     A            RDCCY          3        24TEXT('Currency')
     A                                    33'Amount/Percentage Indicator.......-
     A                                      ........:'
     A            RAPCT          1        77TEXT('Amount/Percentage Indicator')
      ** Start of change AR1008615
     A**********                          87'Units...............:'                        AR1008615
     A                                    87'Unit Price..........:'
      ** End of change AR1008615
     A**Start of change BUG17000
     A**Start*of*change*CGL014                                                              BUG17000
     A**********  RUNIT         13S 0    109TEXT('Number of Units')                           CGL014
     A**********  RUNIT         15S 0    109TEXT('Number of Units')                         BUG17000
     A**End*of*change*CGL014                                                                BUG17000
     A**********                            EDTCDE(Z)                                       BUG17000
      ** Start of change AR1008615
     A**********  RUNIT         23       109TEXT('Number of Units')                        AR1008615
     A            RUNIT         24       108TEXT('Number of Units')
      ** End of change AR1008615
     A**End of change BUG17000
     A                                      SPACEA(001)
      *
     A                                     6'Percentage......:'
     A            RPCNT          6        24TEXT('Percentage')
      ** Start of change AR1008615
     A**********                          33'Unit Amount....:'                             AR1008615
     A N50                                33'Nominal/Amount.:'
     A  50                                33'No. of Units...:'
      ** End of change AR1008615
     A            RUAMT         23        50TEXT('Unit Amount')
     A                                    87'Maximum Amount......:'
     A            RMAMT         23       109TEXT('Maximum Amount')
     A                                      SPACEA(001)
      *
      **Start of change BUG17000
     A                                     6'Blocked Collateral:'
     A            RBLCL          1        26TEXT('Blocked Collateral')
     A                                    33'Minimum Amount.:'
     A            RMNNA         23        50TEXT('Minimum Amount')
     A                                      SPACEA(001)
      **End of change BUG17000
      *
     A                                     6'Valuation.......:'
     A            RVAMT         23        24TEXT('Valuation Amount')
     A                                    51'Valuer..........:'
     A            RVCNM         10        69TEXT('Valuer Customer')
     A                                      SPACEA(001)
      *
     A                                     6'Valuation Date..:'
     A            RVLDT          7        24TEXT('Valuation Date')
     A                                    33'Next Valuation Date:'
     A            RNVDT          7        54TEXT('Next Valuation Date')
     A                                    69'Freq/Day No.....:'
     A            RVFRQ          1        87TEXT('Valuation Frequency')
     A                                    88'/'
     A            RVDNO          2S 0     89TEXT('Valuation Day Number')
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                     6'Margin..........:'
     A            RMARG          6        24TEXT('Collateral Margin')
     A                                    33'Holding Customer...:'
     A            RHCNM         10        54TEXT('Holding Customer')
     A                                    69'Location........:'
     A            RCOLO          3        87TEXT('Collateral Location')
     A                                    94'Lending Value:'
     A            RLAMT         23       109TEXT('Lending Value Amount')
     A                                      SPACEA(001)
      *
     A                                     6'Provider Details:'
     A            RPR01         10        24TEXT('Provider Customer  1')
     A            RPR02         10        37TEXT('Provider Customer  2')
     A            RPR03         10        50TEXT('Provider Customer  3')
     A            RPR04         10        63TEXT('Provider Customer  4')
     A            RPR05         10        76TEXT('Provider Customer  5')
     A            RPR06         10        89TEXT('Provider Customer  6')
     A                                      SPACEA(001)
      *
     A            RPR07         10        24TEXT('Provider Customer  7')
     A            RPR08         10        37TEXT('Provider Customer  8')
     A            RPR09         10        50TEXT('Provider Customer  9')
     A            RPR10         10        63TEXT('Provider Customer 10')
     A            RPR11         10        76TEXT('Provider Customer 11')
     A            RPR12         10        89TEXT('Provider Customer 12')
     A                                      SPACEA(001)
      *
     A  23                                 6'Price Feed Link.:'
     A  23        RPFLK          1        24TEXT('Price Feed Link')
      **Start of change CGL014
     A**********                          33'Accrued Interest:'                               CGL014
     A**********  RACCR         23        51TEXT('Accrued Interest')                          CGL014
     A N24 29                               SPACEA(001)
     A N29                                33'Accrued Interest:'
     A N29        RACCR         23        51TEXT('Accrued Interest')
      **End of change CGL014
     A N24                                  SPACEA(001)
     A  24                                76'Income Rate/Amount.............:'
     A  24        RIAMT         23       109TEXT('Income Rate/Amount')
     A                                      SPACEA(001)
      *
     A  25                                 6'Module Secured..:'
     A  25        RMODS          2        24TEXT('Module Secured')
     A  25                                33'Facility........:'
     A  25        RFCUS          6        51TEXT('Secured Facility Customer')
     A  25        RFTYP          3        58TEXT('Secured Facility Type')
     A  25        RFSEQ          2        62TEXT('Secured Facility Sequence No')
     A  25                                69'Loan/Limit........:'
     A  25        RLNLM          6        89TEXT('Secured Loan Limit')
     A  25                                96'Retail A/C.:'
     A**Start of change CGL029
     A**25******  RSACN         18       109TEXT('Secured Retail Account No.')                CGL029
     A  25        RSACN         24       109TEXT('Secured Retail Account No.')
     A**End of change CGL029
     A                                      SPACEA(001)
      *
     A  26                                 6'Collateral Transaction/Account....-
     A                                      .........:'
     A**Start of change CGL029
     A**26******  RCACN         20        51TEXT('Collateral Transaction/Accnt')              CGL029
     A  26        RCACN         26        51TEXT('Collateral Transaction/Accnt')
     A**End of change CGL029
      **Start of change CGL014
     A**26******                          79'Inclusive/Exclusive of Interest-                 CGL014
     A**********                            /Charges.:'                                       CGL014
     A**26******  RINEX          1       121TEXT('Inc/Exc of Interest/Charges')               CGL014
     A**********                            SPACEA(001)                                       CGL014
     A  26 29                               SPACEA(001)
     A  26N29                             79'Inclusive/Exclusive of Interest-
     A                                      /Charges.:'
     A  26N29     RINEX          1       121TEXT('Inc/Exc of Interest/Charges')
     A  26N29                               SPACEA(001)
      **End of change CGL014
      *
     A  27                                 6'Image Reference.:'
     A  27        RIRF1         20        24TEXT('Image Reference 1')
     A  27        RIRF2         20        47TEXT('Image Reference 2')
     A  27        RIRF3         20        70TEXT('Image Reference 3')
     A                                      SPACEA(001)
      *
     A  27        RIRF4         20        24TEXT('Image Reference 4')
     A  27        RIRF5         20        47TEXT('Image Reference 5')
     A  27        RIRF6         20        70TEXT('Image Reference 6')
     A                                      SPACEA(001)
      *
     A                                     6'Insurer.........:'
     A            RINSR         10        24TEXT('Insurer Customer Number')
     A                                    42'Insurance Policy Reference:'
     A            RIPLY         20        70TEXT('Insurance Policy Reference')
     A                                      SPACEA(001)
      *
     A                                     6'Policy Location.:'
     A            RIPLO          3        24TEXT('Insurance Policy Location')
     A N27                                  SPACEA(001)
     A  27                                33'Document Reference....:'
     A  27        RDORF         20        57TEXT('Document Reference')
     A                                      SPACEA(001)
      *
     A                                     6'Restrictions....:'
     A            RIRST          1        24TEXT('Insurance Policy Restriction')
     A                                    33'Insurance Valuation...:'
     A            RIVAL         23        57TEXT('Insurance Valuation')
     A                                    81'Insurance Date....:'
     A            RINDT          7       101TEXT('Insurance Date')
     A                                   114'Freq/Day No:'
     A            RINFQ          1       127TEXT('Insurance Frequency')
     A                                   128'/'
     A            RINDN          2S 0    129TEXT('Insurance Day Number')
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                     6'Registered......:'
     A            RRGTR          1        24TEXT('Registered')
     A                                    33'Value of Prior Charges:'
     A            RVPCH         23        57TEXT('Value of Prior Charges')
     A                                    81'Subsequent Chargee:'
     A            RSCHC         12       101TEXT('Subsequent Chargee')
     A                                      SPACEA(001)
      **Start of change CGL014
     A  29                                 6'Price Type......:'
     A  29        RXPRT          1        24TEXT('Price Type')
     A  29                                33'Generate Accounting...:'
     A  29        RXGAF          1        57TEXT('Generate Accounting')
     A  29                                81'Collateral Purpose:'
     A  29        RXCPU          4       101TEXT('Collateral Purpose')
     A  29                               110'Weighting Code :'
     A  29        RXWEC          2       127TEXT('Weighting Code')
     A  29                                  SPACEA(001)
      *
     A  29                                 6'Block Contents..:'
     A  29        RXBCO          1        24TEXT('Block Contents')
     A  29                                33'Release Date/Customer :'
     A  29        RXRDT          7        57TEXT('Release Date')
     A  29                                64'/'
     A  29        RXRCU         10        65TEXT('Release Customer')
     A  29                                  SPACEA(001)
      **End of change CGL014
      * Start of change CGL018
     A  81                                 6'RWA Code/SET IDs:'
     A  81        RWASE         90        24TEXT('RWA CODE/SET ID')
     A  81                                  SPACEA(1)
     A  81        RWASEC        90        24TEXT('RWA CODE SET ID SECOND LINE')
     A  81                                  SPACEA(1)
      * End of change CGL018
      **Start of change CER073
     A  82                                 6'AnaCredit Details'
     A                                      SPACEB(002)
      **Start of change CER073A
     A  82                                 6
     A                                      'Type of Protection Value..........-
     A                                      ..................:'
     A                                      SPACEB(001)
     A  82        RTOPV          2        61
     A  82                                 6
     A                                      'Protection Valuation Approach.....-
     A                                      ..................:'
     A                                      SPACEB(001)
     A  82        RPVAP          1        61
     A  82                                 6
     A                                      'Original Protection Value.........-
     A                                      ..................:'
     A                                      SPACEB(001)
     A  82        RORPV         15  2     61
     A**********                            EDTCDE(2)                                        CER073C
     A                                      EDTCDE(K)                                        CER073C
     A  82                                 6
     A                                      'Date of Original Protection Value.-
     A                                      ..................:'
     A                                      SPACEB(001)
     A  82        RDOPV          7        61
      **End of change CER073A
     A  82                                 6'Third Party Priority Claims -
     A                                      against the Protection..:'
     A                                      SPACEB(001)
     A  82        RTPPC         15  2     61
     A**********                            EDTCDE(2)                                        CER073C
     A                                      EDTCDE(K)                                        CER073C
     A  82                                 6
     A                                      'Probability of Default......-
     A                                      ........................:'
     A                                      SPACEB(001)
     A  82        RPROD          7  6     61
     A                                      EDTCDE(2)
     A  82                                 6
     A                                      'Default Status of the Counterparty-
     A                                      ..................:'
     A                                      SPACEB(001)
     A  82        RDSCP          2        61
     A  82                                 6
     A                                      'Date of the Default Status of the -
     A                                      Counterparty......:'
     A                                      SPACEB(001)
     A  82        RDTDS          7        61
      **End of change CER073
      *
      *--------------------------------------*
      ***  Narratives and Allocations Header *
      *--------------------------------------*
      *
     A          R HEADP2
      *
     A  21                                 6'COLLATERAL NARRATIVES:'
     A                                      SPACEB(003)
     A                                      SPACEA(001)
     A  21                                 6'----------------------'
     A                                      SPACEA(002)
      *
     A  22                                 6'COLLATERAL ALLOCATIONS:'
     A                                      SPACEB(003)
     A                                      SPACEA(001)
     A  22                                 6'-----------------------'
     A                                      SPACEA(002)
      *
     A  22                                85'Amount/'
     A                                      SPACEA(001)
     A  22                                 8'Customer   Module  Priority    Fac-
     A                                      ility    Loan/Limit      Account Co-
     A                                      de     Percentage          Amount  -
     A                                             Percentage'
     A                                      SPACEA(002)
      *
      *----------------------------*
      ***  Narrative Record Format *
      *----------------------------*
      *
     A          R NARRATE
      *
     A            RNARR         79        10TEXT('Collateral Narrative')
     A                                      SPACEA(001)
      *
      *------------------------------*
      ***  Allocations Record Format *
      *------------------------------*
      *
     A          R ALLOCAT
      *
     A            RCNUMA        10         7TEXT('Collateral Alloc Customer')
     A            RMODSA         2        21TEXT('Module Secured')
     A            RPRTYA         3S 0     29TEXT('Allocation Priority')
     A                                      EDTCDE(Z)
     A            RFCUSA         6        37TEXT('Secured Facility Customer')
     A            RFTYPA         3        44TEXT('Secured Facility Type')
     A            RFSEQA         2        48TEXT('Secured Facility Sequence No')
     A            RLNLMA         6        53TEXT('Secured Loan Limit')
     A**Start of change CGL029
     A**********  RACCDA        18        64TEXT('Secured Account Code')                      CGL029
     A            RACCDA        24        61TEXT('Secured Account Code')
     A**End of change CGL029
     A            RAPCTA         1        88TEXT('Allocation Amt/Percent Ind')
     A            RAAMTA        21        97TEXT('Allocation Amount')
     A            RPCNTA         6       121TEXT('Allocation Percentage')
     A                                      SPACEA(001)
      *
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
      *
     A                                    55'***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
