      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Unauthorised collateral items print')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL3310P1 - Unauthorised Collateral Items Print          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL018             Date 28Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CGL012  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL018 - Risk Weighted Assets                                *
      *  CGL012 - Collateral Authorisation                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL3310P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'UNAUTHORISED COLLATERAL +
     A                                      DETAILS REPORT'
     A                                      SPACEA(1)
      *
     A                                    46'------------------------+
     A                                      --------------'
     A                                      SPACEA(2)
      *
     A                                     6'COLLATERAL'
     A                                    17'ENTITY'
     A                                    24'COMPANY'
     A                                    32'BRANCH'
     A                                    39'CUSTOMER'
     A                                    48'TYPE'
     A                                    54'SIGNING'
     A                                    62'VALUE'
     A                                    70'END'
     A                                    76'CCY'
     A                                    85'LENDING VALUE'
     A                                   106'INSERT'
     A                                   118'AMEND'
     A                                      SPACEA(1)
      *
     A                                     6'REFERENCE'
     A                                    17'LEVEL'
     A                                    25'CODE'
     A                                    33'CODE'
     A                                    40'NUMBER'
     A                                    55'DATE'
     A                                    63'DATE'
     A                                    70'DATE'
     A                                   107'USER'
     A                                   118'USER'
     A                                      SPACEA(1)
      *
     A                                     6'----------'
     A                                    17'------'
     A                                    24'-------'
     A                                    32'------'
     A                                    39'--------'
     A                                    48'-----'
     A                                    54'-------'
     A                                    62'------'
     A                                    69'------'
     A                                    76'---'
     A                                    80'-----------------------'
     A                                   104'----------'
     A                                   115'----------'
     A                                      SPACEA(2)
      *
     A          R DETAIL
      *
     A            RCREF         10         6TEXT('Collateral Reference')
     A            RENTY          1        19TEXT('Entity Level')
     A            RCMPY          3        26TEXT('Company Code')
     A            RBRCA          3        33TEXT('Branch Code')
     A            RCNUM          6        40TEXT('Customer Number')
     A            RCOLT          5        48TEXT('Type')
     A            RSDAT          6        54TEXT('Signing Date')
     A            RVDAT          6        62TEXT('Value Date')
     A            REDAT          6        69TEXT('End Date')
     A            RCCY           3        76TEXT('Currency')
     A            RLAMT         23        80TEXT('Lending Value')
     A            RIUSR         10       104TEXT('Insert User')
     A            RAUSR         10       115TEXT('Amend User')
     A                                      SPACEA(1)
      * Start of change CGL018
     A  81                                 6'RWA Code/SET IDs:'
     A  81        RWASE         90        24TEXT('RWA CODE/SET ID')
     A  81                                  SPACEA(1)
     A  81        RWASEC        90        24TEXT('RWA CODE SET ID SECOND LINE')
     A  81                                  SPACEA(1)
      * End of change CGL018
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
