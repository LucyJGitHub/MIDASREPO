      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL Collateral details file purge - audit')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL3350AU - Collateral Details File Purge Audit Report   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL018             Date 04Nov02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CGL011  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL018 - Risk Weighted Assets                                *
      *  CGL011 - Collateral Processing for Midas DBA                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GL3350AU'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    45'Collateral Details File +
     A                                      Purge Audit Report'
     A                                      SPACEA(1)
      *
     A                                    45'------------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    29'No. of records purged +
     A                                      from Collateral +
     A                                      Details     (GLCOLLPD) +
     A                                      .....'
     A                                      SPACEB(2)
     A            RCOLL          5  0     97EDTCDE(3)
      *
     A                                    29'No. of records purged +
     A                                      from Collateral +
     A                                      Allocations (GLCOLAPD) +
     A                                      .....'
     A                                      SPACEB(1)
     A            RCOLA          5  0     97EDTCDE(3)
      *
     A                                    29'No. of records purged +
     A                                      from Collateral +
     A                                      Narratives  (GLCOLNPD) +
     A                                      .....'
     A                                      SPACEB(1)
     A            RCOLN          5  0     97EDTCDE(3)
      *
      * Start of change CGL018
     A  60                                29'No. of records purged +
     A                                      from Collateral RWA +
     A                                      Codes   (GLORWAPD) +
     A                                      .....'
     A                                      SPACEB(1)
     A  60        RCOLR          5  0     97EDTCDE(3)
      * End of change CGL018
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
