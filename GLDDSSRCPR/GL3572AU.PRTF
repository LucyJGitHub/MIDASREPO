      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Account Master Details audit report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module
     A*                                                               *
     A*  PRTF/GL3572AU - ACCOUNT MASTER DETAILS AUDIT REPORT          *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. S01413 *CREATE     DATE 13APR93               *
     A*                 XXXXXX             DATE DDMMMYY               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  S01413 - Retail 3 Incorporation                              *
     A*                                                               *
     A*****************************************************************
     A                                       REF(MFRFL)
     A          R GL3572AH
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                  2  7'REFERENCE GL3572AU'
     A            BJURPT    R           2 37 REFFLD(TITL)
     A                                  2101'DATE'
     A            RUNA      R           2108
     A                                  2120'PAGE'
     A                                  2125PAGNBR EDTCDE(Z)
     A  30                              4 38'INTERIM'
     A                                  4 46'ACCOUNT MASTER DETAIL AUDIT REPORT'
     A  30        MTIME     R           4 82EDTWRD('  :  :  ')
     A  30                              5 38'--------'
     A                                  5 46'----------------------------------'
     A  30                              5 80'-----------'
      *-------------------------------------------------------------------------
     A          R GL3572A1
      *
     A                                 18 12'FILE CONTROLS -'
     A N78                             21 20'NO OF RECORDS READ -'
     A N78                             21 41'INPUT -'
     A N78        NORE      R          21 49EDTCDE(3)
     A N78                             21 58'- CALCULATED -'
     A  78                             21 45'NO OF RECORDS '
     A  78                             21 60'CALCULATED -'
     A            NREC      R          21 73REFFLD(NORE)
     A                                      EDTCDE(3)
      *-------------------------------------------------------------------------
     A          R GL3572A2
      *
     A                                  7 30'** UPDATE CONTROL TOTALS **'
     A                                  9 20'INPUT CYCLE'
     A                                  9 57'INPUT CYCLE'
     A                                  9 75'PROCESSING'
     A                                  9 91'ACCUMULATED'
     A                                  9105'CALCULATED'
     A                                  9120'DIFFERENCE'
     A                                 11  2'A/C MASTER FILE - NO. OF ACCOUNTS -
     A                                      OPENED'
     A            NINU      R          11 59REFFLD(NORE)
     A                                      EDTCDE(1)
     A            NINP      R          11 77REFFLD(NORE)
     A                                      EDTCDE(1)
     A            NOOP      R          11 93REFFLD(NORE)
     A                                      EDTCDE(1)
     A            CNINU     R          11107REFFLD(NORE)
     A                                      EDTCDE(1)
     A  22                             11121'*****'
     A                                 12 20'NO. OF ACCOUNTS AMENDED'
     A            NOAM      R          12 59EDTCDE(1)
     A            NAMP      R          12 77EDTCDE(1)
     A            NOOM      R          12 93REFFLD(NOAM)
     A                                      EDTCDE(1)
     A            CNOAM     R          12107REFFLD(NOAM)
     A                                      EDTCDE(1)
     A  23                             12121'*****'
     A                                 13 20'NO. OF ACCOUNTS CLOSED'
     A            NOCL      R          13 59EDTCDE(1)
     A            NOCL1     R          13 93REFFLD(NOCL)
     A                                      EDTCDE(1)
     A            CNOCL     R          13107REFFLD(NOCL)
     A                                      EDTCDE(1)
     A  24                             13121'*****'
     A                                 15 20'NO. OF CLOSE TRAN FAILURES'
     A            NOCF      R          15 77EDTCDE(1)
     A            NOCF1     R          15 93REFFLD(NOCF)
     A                                      EDTCDE(1)
     A            CNOCF     R          15107REFFLD(NOCF)
     A                                      EDTCDE(1)
     A  25                             15121'*****'
     A                                 16 20'NO. OF ACCOUNTS RE-OPENED'
     A            NORD      R          16 77EDTCDE(1)
     A            NORD1     R          16 93REFFLD(NORD)
     A                                      EDTCDE(1)
     A            CNORD     R          16107REFFLD(NORD)
     A                                      EDTCDE(1)
     A  26                             16121'*****'
     A                                 18 20'NO. OF ACCOUNTS BEFORE UPDATE'
     A            NOOA      R          18 59EDTCDE(1)
     A            NOOA1     R          18 93REFFLD(NOOA)
     A                                      EDTCDE(1)
     A                                 19 20'NO. OF ACCOUNTS AFTER UPDATE'
     A  56        NOAU      R          19 59EDTCDE(1)
     A N56        NAUP      R          19 77EDTCDE(1)
     A            NAUP1     R          19 93REFFLD(NAUP)
     A                                      EDTCDE(1)
     A  56        NOAU1     R          19107REFFLD(NOAU)
     A                                      EDTCDE(1)
     A  61                             21  1'A/C NUMBERS FILE - NO. OF RETAIL A-
     A                                      CCOUNTS OPENED'
     A  61        CINUA1    R          21 59REFFLD(NINU)
     A                                      EDTCDE(1)
     A  61        CINUA2    R          21 93REFFLD(NINU)
     A                                      EDTCDE(1)
     A  61        CINUA3    R          21107REFFLD(NINU)
     A                                      EDTCDE(1)
     A  61                             22 20'NO. OF RETAIL ACCOUNTS CLOSED'
     A  61        COCLA1    R          22 59REFFLD(NOCL)
     A                                      EDTCDE(1)
     A  61        COCLA2    R          22 93REFFLD(NOCL)
     A                                      EDTCDE(1)
     A  61        COCLA3    R          22107REFFLD(NOCL)
     A                                      EDTCDE(1)
     A  61                             24 20'NO. OF RETAIL CLOSE FAILURES'
     A  61        COCFA1    R          24 77REFFLD(NOCF)
     A                                      EDTCDE(1)
     A  61        COCFA2    R          24 93REFFLD(NOCF)
     A                                      EDTCDE(1)
     A  61        COCFA3    R          24107REFFLD(NOCF)
     A                                      EDTCDE(1)
      *-------------------------------------------------------------------------
     A          R GL3572A3
     A                                      SPACEB(5)
     A                                    10'DATABASE ERROR NO.-'
     A            DBASE          3S 0     30
     A                                    35'FILE-'
     A            DBFILE    R             40
     A                                    60'KEY-'
     A            DBKEY     R             66
      *-------------------------------------------------------------------------
     A          R NODTLT
     A                                      SPACEB(5)
     A                                    53'*** NO DETAILS TO REPORT ***'
      *-------------------------------------------------------------------------
     A          R GL3572AT
     A                                      SPACEB(5)
     A                                    55'*** END OF REPORT ***'
