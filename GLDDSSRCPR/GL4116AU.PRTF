      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GL Daily Jrnl Summary Audit Layout')
     A*****************************************************************
     A*                                                               *
     A*  Midas - General Ledger Module                            *
     A*                                                               *
     A*  PRTF/GL4116AU - Daily Journal Summary Audit Layout           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. CRT001  *CREATE    DATE 28FEB95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CRT001 - Retail Teller System                                *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A*-------------------*
     A**   REPORT HEADING *
     A*-------------------*
     A*
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE GL4116AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A                                    46'DAILY JOURNAL SUMMARY - AUDIT'
     A                                      SPACEA(1)
     A                                    46'-----------------------------'
     A                                      SPACEA(2)
     A*
     A*------------------*
     A**  RUN CONTROL    *
     A*------------------*
     A*
     A          R CONTROL
     A                                    11'RUN CONTROLS'
     A                                    71'INPUT'
     A                                    91'CALCULATED'
     A                                      SPACEA(1)
     A                                    71'-----'
     A                                    91'----------'
     A                                      SPACEA(2)
     A*
     A                                    11'NUMBER OF RECORDS ON POSTINGS'
     A                                    41'EXTRACT FILE'
     A            NORE      R             70EDTWRD('   0 ')
     A            COUNT          5  0     95EDTWRD('   0 ')
     A  71                               103'*** DIFFERENCE ***'
     A*
     A                                    11'VALUE OF RECORDS ON POSTINGS'
     A                                      SPACEB(2)
     A                                    40'EXTRACT FILE'
     A            HRWN          15  0     57EDTCDE(3)
     A            HRDC           3  0     72
     A            TOTI          15  0     82EDTCDE(3)
     A            TOTD           3  0     97
     A  70                               103'*** DIFFERENCE ***'
     A*
     A*---------------*
     A**   No Details *
     A*---------------*
     A*
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A*
     A*-------------------*
     A**   DATABASE ERROR *
     A*------------------*
     A*
     A          R ERROR
     A                                      TEXT('DATABASE ERROR')
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8        30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
