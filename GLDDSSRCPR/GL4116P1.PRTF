      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Daily Journal Summary Rpt Layout')
     A*****************************************************************
     A*                                                               *
     A*  Midas - General Ledger Module                                *
     A*                                                               *
     A*  PRTF/GL4116P1 - Daily Journal Summary Report Layout          *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSD005             Date 11Mar99               *
      *                 CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CSD005 - Switchable feature- if account narrative feature    *  *
     A*           is active the current account description shall be  *  *
     A*           replaced with the account narrative.                *  *
     A*  CRT001  -  Retail Teller System                              *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A*
     A*-------------------*
     A***  PAGE HEADING 1 *
     A*-------------------*
     A*
     A          R HEADP1                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE GL4116'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A                                    46'DAILY JOURNAL SUMMARY'
     A  32                                89'BRANCH'
     A  32        ORBR      R             98
     A  32        PBRNM         30       102TEXT('BRANCH NAME')
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'---------------------'
     A                                      SPACEA(2)
     A*
     A*-------------------*
     A*    PAGE HEADING 2 *
     A*-------------------*
     A*
     A          R HEADP2                    TEXT('REPORT HEADER2')
     A                                     7'***                ***'
     A            CCNM      R             11
     A*
     A                                      SPACEA(3)
     A                                     8'A/C               A/C'
     A                                    48'SOURCE OF'
     A                                    71'DEBIT'
     A                                    91'CREDIT'
     A                                   112'BALANCE'
     A*
     A                                      SPACEA(1)
     A                                     7'CODE'
     A                                    23'DESCRIPTION'
     A                                    49'POSTING'
     A                                    70'AMOUNT'
     A                                    91'AMOUNT'
     A                                      SPACEA(2)
     A*
     A*-------------------*
     A***  PAGE HEADING 3 *
     A*-------------------*
     A*
     A          R HEADP3                    TEXT('REPORT HEADER3')
     A**Start of change CGL029
     A**********  ACOD      R              7                                                  CGL029
     A            ACOD      R              1
     A**End of change CGL029
      *************                                       Begin/CSD005/
     A********    ACKN      R             14
     A********                              SPACEA(2)
     A N73        ACKN      R             14
     A N73                                  SPACEA(2)
     A  73        W#NARR        50        14
     A  73                                  SPACEA(2)
      **************                                        End/CSD005/
     A*
     A*-------------------*
     A***  ACOD DETAILS   *
     A*-------------------*
     A*
     A          R DETAIL                    TEXT('REPORT DETAIL')
     A                                      SPACEA(2)
     A  40                                44'BRANCH'
     A  40        BRCA      R             51
     A  41                                44'SYSTEM'
     A  60        DRTOT0        13  0     65EDTWRD(' ,   ,   ,   , 0 ')
     A  61        DRTOT1        13  0     65EDTWRD('   ,   ,   , 0 . ')
     A  62        DRTOT2        13  0     65EDTWRD('  ,   ,   , 0 .  ')
     A  63        DRTOT3        13  0     65EDTWRD(' ,   ,   , 0 .   ')
     A*
     A  60        CRTOT0        13  0     86EDTWRD(' ,   ,   ,   , 0 ')
     A  61        CRTOT1        13  0     86EDTWRD('   ,   ,   , 0 . ')
     A  62        CRTOT2        13  0     86EDTWRD('  ,   ,   , 0 .  ')
     A  63        CRTOT3        13  0     86EDTWRD(' ,   ,   , 0 .   ')
     A*
     A  60        BRTOT0        13  0    107EDTWRD(' ,   ,   ,   , 0 CR')
     A  61        BRTOT1        13  0    107EDTWRD('   ,   ,   , 0 . CR')
     A  62        BRTOT2        13  0    107EDTWRD('  ,   ,   , 0 .  CR')
     A  63        BRTOT3        13  0    107EDTWRD(' ,   ,   , 0 .   CR')
     A*
     A*-------------------*
     A***  ACOD TOTALS    *
     A*-------------------*
     A*
     A          R TOTAL                     TEXT('REPORT TOTAL')
     A                                      SPACEB(2)
     A                                      SPACEA(3)
     A**Start of change CGL029
     A**********                          39'TOTAL FOR A/C CODE'                              CGL029
     A**********  ACOD      R             58                                                  CGL029
     A                                    32'TOTAL FOR A/C CODE'
     A            ACOD      R             51
     A**End of change CGL029
     A  60        DTOTA0        13  0     65EDTWRD(' ,   ,   ,   , 0 ')
     A  61        DTOTA1        13  0     65EDTWRD('   ,   ,   , 0 . ')
     A  62        DTOTA2        13  0     65EDTWRD('  ,   ,   , 0 .  ')
     A  63        DTOTA3        13  0     65EDTWRD(' ,   ,   , 0 .   ')
     A*
     A  60        CTOTA0        13  0     86EDTWRD(' ,   ,   ,   , 0 ')
     A  61        CTOTA1        13  0     86EDTWRD('   ,   ,   , 0 . ')
     A  62        CTOTA2        13  0     86EDTWRD('  ,   ,   , 0 .  ')
     A  63        CTOTA3        13  0     86EDTWRD(' ,   ,   , 0 .   ')
     A*
     A  60        ACTOT0        13  0    107EDTWRD(' ,   ,   ,   , 0 CR')
     A  61        ACTOT1        13  0    107EDTWRD('   ,   ,   , 0 . CR')
     A  62        ACTOT2        13  0    107EDTWRD('  ,   ,   , 0 .  CR')
     A  63        ACTOT3        13  0    107EDTWRD(' ,   ,   , 0 .   CR')
     A                                      SPACEA(3)
      *
      ** Database error reporting line
     A          R ERRLINE                   TEXT('ERROR PRINT LINE')
     A                                      SPACEB(2)
     A                                     7'DATABASE ERROR IN '
     A                                    26'FILE '
     A            DBFILE         8        31
     A                                    40'KEY '
     A            DBKEY         29        44
     A                                    70'IN '
     A            DBPGM         10        73
     A                                    84'AT '
     A            DBASE          3  0     87
     A*
     A*-------------------*
     A***  Report Trailer *
     A*-------------------*
     A*
     A          R LAST
     A  32                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N32                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
     A**
     A*-------------------*
     A***  NO RECORDS     *
     A*-------------------*
     A*
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
