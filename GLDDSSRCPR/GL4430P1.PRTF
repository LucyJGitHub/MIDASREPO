      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Report Today's Proj. Movements')
     A*****************************************************************
     A*                                                               *
     A*  Midas - General Ledger Module
     A*                                                               *
     A*  PRTF/GL4430P1 - Project Movements Print                      *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. 058468             Date 21Jul93               *
     A*                 041693             Date 05Jan93               *
     A*                 045014             DATE 01DEC92               *
     A*                 042779             DATE 19NOV92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  058468 - Change F1PST* to F2PST* on Detail 2 print record.   *
     A*                                                               *
     A*  041693 - Delivered as part of fix.                           *
     A*                                                               *
     A*  045014 - Add database error format                           *
     A*                                                               *
     A*  042779 - New program to report Shadow Post Movements         *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R F1HDR1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                  3  6'REFERENCE'
     A            #PRGM         10      3 16
     A            BJURPT    R           3 37REFFLD(URPT)
     A                                  3100'DATE'
     A            BJMRDT    R           3105REFFLD(RUNA)
     A                                  3119'PAGE'
     A            PAGE1          4  0   3124EDTCDE(Z)
     A                                  5 57'Projected Movements Report'
     A                                      UNDERLINE
      *
      *  Account Information
      *
     A          R F1HDR2                    TEXT('Account Information')
     A                                      SPACEB(2)
     A            F1BRCD         3         7
     A                                    10'-'
     A            F1CNUM         6        11
     A                                    17'-'
     A            F1CCY          3        18
     A                                    21'-'
     A**Start of change CGL029
     A**********  F1ACOD         4        22                                                  CGL029
     A**********                          26'-'                                               CGL029
     A**********  F1ACSQ         2        27                                                  CGL029
     A**********  F1ANAM        30        35                                                  CGL029
     A            F1ACOD        10        22
     A                                    32'-'
     A            F1ACSQ         2        33
     A            F1ANAM        30        41
     A**End of change CGL029
     A                                     6'Start of Day'
     A                                      SPACEB(2)
     A                                    20'General Ledger balance'
     A                                      SPACEB(1)
     A  40        F1LDB0        15  0     44EDTCDE(J)
     A  41        F1LDB1        15  1     44EDTCDE(J)
     A  42        F1LDB2        15  2     44EDTCDE(J)
     A  43        F1LDB3        15  3     44EDTCDE(J)
     A     37                             20'Cleared Ledger balance'
     A                                      SPACEB(1)
     A  40 37     F1CLB0        15  0     44EDTCDE(J)
     A  41 37     F1CLB1        15  1     44EDTCDE(J)
     A  42 37     F1CLB2        15  2     44EDTCDE(J)
     A  43 37     F1CLB3        15  3     44EDTCDE(J)
      *
      *  Projected balance information for Nostros
      *
     A          R F1HDR3                    TEXT('Nostro Information')
     A                                     6'Projected Balance +
     A                                       Information'
     A                                      SPACEB(2)
     A                                     6'Dates & Balances'
     A                                      SPACEB(1)
     A            F1DAT1         7        19
     A                                      SPACEB(1)
     A            F1DAT2         7        43
     A            F1DAT3         7        67
     A            F1DAT4         7        91
     A            F1DAT5         7       115
     A  40        F1BL10        15  0      6EDTCDE(J)
     A                                      SPACEB(1)
     A  41        F1BL11        15  1      6EDTCDE(J)
     A                                      SPACEB(1)
     A  42        F1BL12        15  2      6EDTCDE(J)
     A                                      SPACEB(1)
     A  43        F1BL13        15  3      6EDTCDE(J)
     A                                      SPACEB(1)
     A  40        F1BL20        15  0     30EDTCDE(J)
     A  41        F1BL21        15  1     30EDTCDE(J)
     A  42        F1BL22        15  2     30EDTCDE(J)
     A  43        F1BL23        15  3     30EDTCDE(J)
     A  40        F1BL30        15  0     54EDTCDE(J)
     A  41        F1BL31        15  1     54EDTCDE(J)
     A  42        F1BL32        15  2     54EDTCDE(J)
     A  43        F1BL33        15  3     54EDTCDE(J)
     A  40        F1BL40        15  0     78EDTCDE(J)
     A  41        F1BL41        15  1     78EDTCDE(J)
     A  42        F1BL42        15  2     78EDTCDE(J)
     A  43        F1BL43        15  3     78EDTCDE(J)
     A  40        F1BL50        15  0    102EDTCDE(J)
     A  41        F1BL51        15  1    102EDTCDE(J)
     A  42        F1BL52        15  2    102EDTCDE(J)
     A  43        F1BL53        15  3    102EDTCDE(J)
      *
      *  Projected balance information for Retail
      *
     A          R F1HDR4                    TEXT('Retail Information')
     A                                      SPACEB(2)
     A                                     6'Projected Balance +
     A                                       Information'
     A                                     6'At present'
     A                                      SPACEB(1)
     A                                    20'General Ledger balance'
     A                                      SPACEB(1)
     A  40        F1LDP0        15  0     44EDTCDE(J)
     A  41        F1LDP1        15  1     44EDTCDE(J)
     A  42        F1LDP2        15  2     44EDTCDE(J)
     A  43        F1LDP3        15  3     44EDTCDE(J)
     A     37                             20'Cleared Ledger balance'
     A                                      SPACEB(1)
     A  40 37     F1CLP0        15  0     44EDTCDE(J)
     A  41 37     F1CLP1        15  1     44EDTCDE(J)
     A  42 37     F1CLP2        15  2     44EDTCDE(J)
     A  43 37     F1CLP3        15  3     44EDTCDE(J)
      *
      *  Posting Information Column headings
      *
     A          R F1HDR5                    TEXT('Column Headings 1')
     A                                      SPACEB(2)
     A                                    11'Value'
     A                                    20'Posting'
     A                                    29'Type'
     A                                    35'Narrative'
     A                                    70'Cheque'
     A                                    86'Posting Amount'
     A                                   105'Balance Information'
     A                                    11'Date'
     A                                      SPACEB(1)
     A                                    22'Date'
     A                                    70'Number'
     A                                     2'=========='
     A                                      SPACEB(1)
     A                                    12'=========='
     A                                    22'=========='
     A                                    32'=========='
     A                                    42'=========='
     A                                    52'=========='
     A                                    62'=========='
     A                                    72'=========='
     A                                    82'=========='
     A                                    92'=========='
     A                                   102'=========='
     A                                   112'=========='
     A                                   122'========='
     A                                      SPACEA(1)
      *
      *  No Details to report
      *
     A          R F1DET1                    TEXT('Print balance +
     A                                      Information')
     A                                      SPACEB(1)
     A            F1VALD         7        10
     A  38                                55'Carried Forward Balance'
     A  39                                55'Brought Forward Balance'
     A* 058468 Start
     A**40********F1PST0        15  0    100EDTCDE(J)
     A**41********F1PST1        15  1    100EDTCDE(J)
     A**42********F1PST2        15  2    100EDTCDE(J)
     A**43********F1PST3        15  3    100EDTCDE(J)
     A  40        F2PST0        15  0    100EDTCDE(J)
     A  41        F2PST1        15  1    100EDTCDE(J)
     A  42        F2PST2        15  2    100EDTCDE(J)
     A  43        F2PST3        15  3    100EDTCDE(J)
     A* 058468 End
      *
      *  Posting detail information
      *
     A          R F1DET2                    TEXT('Posting Information')
     A                                      SPACEB(1)
     A            F1VALD         7        10
     A            F1PSTD         7        20
     A            F1TRYP         2        30
     A            F1PNAR        30        35
     A            F1CHQN         8        70
     A  40        F1PST0        15  0     80EDTCDE(J)
     A  41        F1PST1        15  1     80EDTCDE(J)
     A  42        F1PST2        15  2     80EDTCDE(J)
     A  43        F1PST3        15  3     80EDTCDE(J)
      *
      *  No Details to report
      *
     A          R F1DET3                    TEXT('No Details to Report')
     A                                 25 53'*** No Details -
     A                                      To Report ***'
      *
      *  No Retail or Nostro Information to report
      *
     A          R F1DET4                    TEXT('No Retail or Nostro +
     A                                      Information')
     A  44                             25 55'*** No Nostro or Retail +
     A                                      Information ***'
     A  45                             25 55'*** No Nostro +
     A                                      Information ***'
     A  46                             25 55'*** No Retail +
     A                                      Information ***'
      *
      *  End of Report
      *
     A          R F1TRL1                    TEXT('End of Report')
     A                                      SPACEB(2)
     A                                    56'*** End of Report ***'
      * 045014 Start
      *
      *  Database Error Detail
      *
     A          R F1DBERR                   TEXT('Database Error')
     A                                      SPACEB(1)
     A                                    30'Database Error'
     A                                      SPACEB(2)
     A                                    30'Error in Program..:'
     A                                      SPACEB(2)
     A            DBPGM         10        50
     A                                    30'File..............:'
     A                                      SPACEB(2)
     A            DBFILE        10        50
     A                                    30'Key...............:'
     A                                      SPACEB(2)
     A            DBKEY         30        50
     A                                    30'Subroutine........:'
     A                                      SPACEB(2)
     A            DBSTK         10        50
     A                                    30'Error number......:'
     A                                      SPACEB(2)
     A            DBERNB         5  0     50EDTCDE(J)
     A                                    30'Narrative.........:'
     A                                      SPACEB(2)
     A            DBNARR        80        50
     A                                    30'Primary reference.:'
     A                                      SPACEB(2)
     A            DBPREF        30        50
      * 045014 End
