      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Batch History File Purge Audit')
      *****************************************************************
      *                                                               *
      *  Midas GENERAL LEDGER MODULE                                  *
      *                                                               *
      *      PRTF/GL4460AU - BATCH HISTORY FILE PURGE REPORT          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL013             Date 20Feb02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. Xnnnnn  *CREATE    Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CGL013 - MT94x Message Generation.                           *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      ** Header Format
      *
     A          R GL4460AH                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                  3  6'REFERENCE GL4460AU'
     A            RURPT     R           3 40REFFLD(URPT)
     A                                  3100'DATE'
     A            RMRDT     R           3105REFFLD(MRDT)
     A                                  3120'PAGE'
     A                                  3125PAGNBR
     A                                      EDTCDE(J)
     A                                  5 51'BATCH HISTORY FILE'
     A                                  5 70'PURGE REPORT'
      *
      ** Detail Format
      *
     A          R GL4460AD                  TEXT('Detail Format')
     A*
     A                                 10  5'PURGE OF BATCH HISTORY'
     A                                 10 28'FILES:'
     A                                 12 11'RECORDS DELETED'
     A                                 12 28'LF/GLJEHHL1 -'
     A            NHDL          15S 0  12 43EDTCDE(3)
     A                                 14 28'LF/GLJEHPL1 -'
     A            NPDL          15S 0  14 43EDTCDE(3)
     A                                 16 28'LF/GLJEHSL1 -'
     A            NSDL          15S 0  16 43EDTCDE(3)
      **Start of change CGL013
     A  11                             18 28'LF/GLJEHXL2 -'
     A  11        NXDL          15S 0  18 43EDTCDE(3)
      **End of change CGL013
     A*/COPY WNCPYSRC,GL4460AU01
      *
      ** Error Format
      *
     A          R GL4460AE                  TEXT('Error Format')
     A                                      SPACEB(9)
     A                                    11'*** DATABASE IN ERROR ***'
      *
     A                                    49'File'
     A                                    60':-'
     A            DBFILE         8        64
      *
     A                                      SPACEA(1)
     A                                    49'Key'
     A                                    60':-'
     A            DBKEY         29        64
      *
     A                                      SPACEA(1)
     A                                    49'In Program'
     A                                    60':-'
     A            DBPGM         10        64
      *
     A                                      SPACEA(2)
     A                                    49'At point'
     A                                    60':-'
     A            DBASE          3S 0     64
      *
      *
      ** End of Report Format
      *
     A          R GL4460AT                  TEXT('End of Report Format')
     A                                      SPACEB(7)
     A                                    56'*** END OF REPORT ***'
