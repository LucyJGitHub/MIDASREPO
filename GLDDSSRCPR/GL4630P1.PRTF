      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STMT66                                                    *
/*EXI *  TEXT('Midas GL Confirmation of Balances Report')             *
/*ADD *  CHGPRTF FILE(GL4630P1) PAGESIZE(66 100) FORMTYPE(*STD)       *
/*ADD *  CHGPRTF FILE(GL4630P1) PAGRTT(180)                           *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL4630P1 - Certificate of Balance Report                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. AR787886           Date 15Jun11               *
      *  Prev Amend No. AR790145A          Date 04Jul11               *
      *                 AR798241           Date 13Jul11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL024  *CREATE    Date 31Jul03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR787886 - Account description is taken from ANAM (defined   *
      *             as 20A) of ACCNTAB, but GL4630P1 uses RANAM which *
      *             has a length of 19A. Need to declare RANAM as 20A.*
      *             (Child: AR787887)                                 *
      *  AR790145A - Client is requesting 'Ref: SIN:DEP:' on header   *
      *              to be removed as per business practice.          *
      *              Commented out 'Ref: SIN:DEP:'. (Child: AR790146A)*
      *  AR798241 - Customer Name and Address is truncated in Balance *
      *             Confirmation. Amended the program such that RCNA1,*
      *             RCNA2, RCNA3 & RCNA4 have a length of 35A.        *
      *             (Child: AR798242)                                 *
      *  CGL024 - Certificates and Confirmations of Balances.         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A                                      SKIPB(11)
      ** Start of change AR790145A
     A**50******                           6'Ref: SIN:DEP:'                                AR790145A
      ** End of change AR790145A
     A  50                                62'Date:'
     A  50        RMRDT     R        O    68REFFLD(MRDT)
     A  50                                  SPACEA(2)
     A  50                                 6'To'
      ** Start of change AR798241
     A**50******  RCNA1         30A  O     6                                                AR798241
     A**50******                            SPACEB(002)                                     AR798241
     A**50******  RCNA2         30A  O     6                                                AR798241
     A**50******                            SPACEB(001)                                     AR798241
     A**50******  RCNA3         30A  O     6                                                AR798241
     A**50******                            SPACEB(001)                                     AR798241
     A**50******  RCNA4         30A  O     6                                                AR798241
     A**50******                            SPACEB(001)                                     AR798241
     A  50        RCNA1         35A  O     6
     A  50                                  SPACEB(002)
     A  50        RCNA2         35A  O     6
     A  50                                  SPACEB(001)
     A  50        RCNA3         35A  O     6
     A  50                                  SPACEB(001)
     A  50        RCNA4         35A  O     6
     A  50                                  SPACEB(001)
      ** End of change AR798241
      *
     A N50N51                              6'Continued...'
     A N50N51                               SPACEA(012)
      *
     A  50                                29'Customer No.:'
     A  50                                  SPACEB(2)
     A  50        RCNUM          6A  O    43
     A  50                                29'--------------------'
     A  50                                  SPACEB(1)
     A  50                                16'Confirmation of outstanding balanc-
     A                                      es/transactions'
     A  50                                  SPACEB(1)
     A  50                                16'-----------------------------------
     A                                      ---------------'
     A  50                                  SPACEB(1)
     A  50                                  SPACEA(1)
      *
     A          R SUBH1
     A            RTEXT         69A  O     6
     A                                      SPACEB(001)
      *
     A          R SUBH2
     A                                      SPACEA(001)
     A                                     6'-----------------------------------
     A                                      -----------------------------------'
     A                                      SPACEB(001)
      *
     A                                    10'A/c Code'
     A                                      SPACEB(001)
     A                                    22'A/c'
     A                                    27'Account Description'
     A                                    48'A/c'
     A                                    53'Balance in A/c Ccy'
     A                                    22'Ccy'
     A                                      SPACEB(001)
     A                                    48'Seq'
     A                                    53'Balance in Base Ccy'
     A                                     6'-----------------------------------
     A                                      -----------------------------------'
     A                                      SPACEB(001)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A                                      SPACEA(1)
     A            RNUM           3A  O     6
     A                                      SPACEB(1)
     A            RACOD         10A  O    10
     A            RCYCD          3A  O    22
      ** Start of change AR787886
     A**********  RANAM         19A  O    27                                                AR787886
     A            RANAM         20A  O    27
      ** End of change AR787886
     A            RSEQN          2A  O    48
     A            RBALA         22A  O    53
     A            RBALB         22A  O    53
     A                                      SPACEB(1)
