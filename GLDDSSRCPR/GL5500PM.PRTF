      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL  Spread profile report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL5500PM - Spread Profile Report                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. 164074             Date 14Oct99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                         *CREATE    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('Midas Run Date')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##AURT        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##BJNA        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     2'Spread'
     A                                    19'Spread Profile'
     A                                    54' Control'
     A                                    69'Number of'
     A                                   111'Last'
     A                                   123'Change'
     A                                     2'Profile Code'
     A                                      SPACEB(1)
     A                                    19'Description'
     A                                    54' Total'
     A                                    69'profit Centres'
     A                                   111'Change'
     A                                   123'Type'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
      *.........................................................................
     A            XRSPRF         4         4TEXT('Spread Profile Code')
     A            XRSPDC        30        19TEXT('Spread Profile Descr.')
     A            XRCTTL        10  0     54TEXT('Control Total')
     A                                      EDTCDE(3)
     A            XRNUPC         2  0     73TEXT('No. of Profit Centres')
     A                                      EDTCDE(3)
     A            XRACDT         7       111TEXT('alpha change date')
     A            XRALCH         6       123TEXT('alpha of last change')
     A N79        XRWNFF       131         2TEXT('Window Function Field')
     A                                      SPACEB(1)
     A N78        XRWNF1       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A N77        XRWNF2       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      *                                              Begin /164074/
      *=========================================================================
     A          R DTLHEAD                   TEXT('Spread details header    ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     3'Profit'
     A                                    19'Amount'
     A                                    35' Profit Centre'
     A                                     3'Centre'
     A                                      SPACEB(1)
     A                                    35' Description'
     A                                      SPACEA(1)
      *=========================================================================
     A          R SPREDDTL                  TEXT('Spread details line      ')
     A                                      SPACEB(1)
      *.........................................................................
     A            D9SPRF         4A        4TEXT('Profit centre')
     A            D9AMOT         8  0     19TEXT('Amount')
     A                                      EDTCDE(3)
     A            DSPCDS        30A       36TEXT('Profit centre desc.')
      *                                                End /164074/
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A  76                                60'*** NO DETAILS TO REPORT ***'
      *                                              Begin /164074/
     A*N76******                          60'No. of Records Printed:'
     A N76                                52'No. of Spread Profiles Printed:'
      *                                                End /164074/
     A                                      SPACEB(1)
     A N76        ZTNCRC         5  0     84TEXT('No of Calculated Recs')
     A                                      EDTCDE(4)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
