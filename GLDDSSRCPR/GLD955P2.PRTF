      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GLD955P2 - Daily Events by Currency for Internal        *
      *                  Parts Purchased - Consolidated - I/C         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. DUG503             Date 08Apr18               *
      *  Prev Amend No. DUG405             Date 30Jul04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CLE004 *Create     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  DUG503 - UGB FED CARS Upgrade                                *
      *           DUG405 - EVENTS BY VALUE DATE REPORT               **
      *               NEW - RENAMED FROM GL0550P2                    **
      *  CLE004 - Customer Lending Enhancements - Syndications.       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A****                                 6'REFERENCE GL0550P2'                DUG405
     A                                     6'REFERENCE GLD955P2'                DUG405
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'DAILY EVENTS BY CURRENCY +
     A                                      FOR INTERNAL PARTS PURCHASED'
     A                                      SPACEA(1)
     A            RCYNM         14         9
     A                                    40'-------------------------+
     A                                      ----------------------------'
     A                                      SPACEA(2)
     A                                     9'CONSOLIDATED'
     A                                      SPACEA(1)
      *
      *-----------------*
      ***  Sub-Heading: *
      *-----------------*
      *
     A          R COLMP2
     A                                    41'LOAN'
     A                                    84'VAL/MAT'
     A                                      SPACEA(1)
     A                                     7'CUSTOMER'
     A                                    41'NUMBER'
     A                                    51'EVENT DATE'
     A                                    65'EVENT'
     A                                    86'DATE'
     A                                   106'AMOUNT'
     A                                   121'RATE'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETLP2
     A            RCRNM         20         7
     A            RCTWN         10        28
     A            RLNRF          6        41
     A            REDAT          7        52
     A            RENAR         15        65
     A            ROTHD          7        84
     A            REAMT         18        95
     A            RINTR         13       116
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP2                   SPACEB(3)
     A  77                                51'***  END OF REPORT  ***'
     A N77                                48'***  NO DETAILS TO +
     A                                      REPORT ***'
      *
