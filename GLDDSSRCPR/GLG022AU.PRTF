      *****************************************************************
/**STD****PRTFBASE****                                                *                      BUG3632
/*STD *  PRTFAU                                                       *                      BUG3632
/*EXI *  TEXT('Midas GL Customer Sanctioned Limits - Audit')
/**EXS***PR_STD60*****                                                *                      BUG3632
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GLG022AU - Customer Sanctioned Limits Listing           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG3632            Date 13Jul04               *
      *  Prev Amend No. GBO001  *CREATE    Date 09Apr03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG3632- Change creation parameter to output spoolfile to    *
      *           MPRINTAU.                                           *
      *  GBO001 - Sanctioned Limits Maintenance & Customer Position   *
      *           Memo Report.                                        *
      *         - Upgrade BOI001 to Midas Release 5.0                 *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *
     A          R AUHDR1
      *
     A                                      TEXT('Standard report header   ')
     A                                     2
     A                                      'Reference'
     A                                      SKIPB(001)
     A                                      SPACEB(002)
     A            ##PGM         10   O    12
     A                                      TEXT('*PROGRAM')
     A            ##URPT        53   O    45
     A                                      TEXT('User Report Title')
     A                                   110
     A                                      'Date'
     A            ##MRDT         7   O   115
     A                                      TEXT('midas rundate')
     A                                   124
     A                                      'Page'
     A                                   129
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                   110
     A                                      'Time'
     A                                      SPACEB(001)
     A                                   115
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    45
     A                                      SPACEB(001)
     A                                      'Customer Sanctioned Limits Listing-
     A                                       - Control Report'
     A                                    45
     A                                      SPACEB(001)
     A                                      '-----------------------------------
     A                                      -----------------'
     A***************************************************************  *********
     A          R AUDET1
     A***************************************************************  *********
     A                                     2
     A                                      SPACEB(004)
     A                                      'Number of Customer records printed-
     A                                      . . :'
     A            PNOREC         7S  O    +2
     A                                      EDTCDE(1)
     A***************************************************************  *********
     A          R AUDET2
     A***************************************************************  *********
     A                                     2
     A                                      SPACEB(004)
     A                                      'Data Base Error in File'
     A            AUDBS         10A  O    +2
     A                                    22
     A                                      SPACEB(001)
     A                                      'key'
     A            AUDBK          5A  O    +2
     A                                    23
     A                                      SPACEB(001)
     A                                      'at'
     A            AUDBL          3S  O    +2
     A                                    15
     A                                      SPACEB(001)
     A                                      'in program'
     A            AUDBP         10A  O    +2
     A***************************************************************  *********
