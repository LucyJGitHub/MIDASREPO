     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Chart of Accounts Report - retail')
     F*****************************************************************
     F*                                                               *
     F*  Midas General Ledger Module
     F*                                                               *
     F*  GL0037 - Chart of Accounts Report - retail                   *
     F*                                                               *
     F*  Function: This program will will produce a Chart of Accounts *
     F*  I/C Int   detailing retail accounts only.                    *
     F*  COB                                                          *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*     LAST AMEND NO. CSD005             DATE 22FEB99            *
      *     PREV AMEND NO. CTI002             DATE 23/09/98           *
     F*                    063184             DATE 04FEB98            *
     F*                    XXXXXX             DATE 00XXX00            *
     F*                                                               *
     F*                                                               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
     F*  CSD005 - Switchable feature- if account narrative feature    *  *
     F*           is active the current account description shall be  *  *
     F*           replaced with the account narrative.                *  *
      *  CTI002 - Recompiled : File SDACODPD changed                  *
     F*  063184 - Do not convert a/c titles to lower case             *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FSDACODL1IF  E           K        DISK
     FGL0037P1O   E             09     PRINTER      KINFDS SPOOL
     FGL0037AUO   E                    PRINTER      KINFDS SPOOLU     UC
      /EJECT
     F*****************************************************************
     F* F U N C T I O N  O F  I N D I C A T O R S                     *
     F*                                                               *
     F*  09 =    Overflow indicator                                   *
     F*                                                               *
     F*  10      End of file for SDACODL1                             *
     F*  20      End of file for SDBANKPD                             *
     F*                                                               *
     F*  21      ZSFILE ended in error                                *
     F*                                                               *
     F***50******Capital letter exists in array alphabet              *   063184
     F*                                                               *
     F*  73      Conditions field for switchable feature Account      *   CSD005
     F*          Narrative                                            *   CSD005
     F*                                                               *
     C*****************************************************************
      /SPACE 3
     F*****************************************************************
     F*                                                               *
     F*           INDEX TO SUBROUTINES                                *
     F*                                                               *
     F*******1.   SUB01 - Convert account title to lower case.        *   063184
     F*      3.   *PSSR - Database error processing.                  *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E**********          ALPHBT 26  26  1                                063184
     E                    ACCN       25  1
      *
     E                    CPY@    1   1 80
      * Copyright Array
     E/EJECT
     I            DS
     I                                        1  25 ACCN
     I                                        1  25 A5ACCN
     ICPYR@#      DS
      * Data Structure For Compilation  - Copyright Insertion
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
     ISPOOL       DS
      * P1 printer file information data structure
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
     I                                    B 152 1530SFLIN
      *
     ISPOOLU      DS
      * AU printer file information data structure
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     ISDBANK    E DSSDBANKPD
      * Bank Details
      *
     ISCSARD    E DSSCSARDPD                                              CSD005
      * Switchable Features File Details                                  CSD005
      *                                                                   CSD005
     IDSFDY     E DSDSFDY
      * First DS for Access Programs, Short Data Structure
      *
     ILDA         DS                            256
      * Local Data Area
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I****************************************************************
     I/EJECT
      *
      * Receive entry parameter
     C           *ENTRY    PLIST
     C                     PARM           SEQNO   5
      *
      * Set up work fields
     C                     MOVE 'Y'       RUN1    1
     C                     MOVE 'N'       RECORD  1
      *                                                                   CSD005
      ***  Initialise swichable features indicator                        CSD005
      *                                                                   CSD005
     C                     MOVE *OFF      *IN73                           CSD005
      *
      * Use access program AOBANKR0 to retrieve header details
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      * If error in access program, exit via *PSSR subroutine
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'GL0037'  ERRPGM 10
     C                     MOVE *BLANKS   ERRKEY  4
     C                     MOVEL'SDBANKPD'ERRFIL 10        ***************
     C                     Z-ADD001       ERRBAS  30       * DBERROR 001 *
     C                     EXSR *PSSR                      ***************
     C                     END
      *
     C                     MOVE BJURPT    RURPT
     C                     MOVE BJMRDT    RMRDT
      *
     C                     MOVEACPY@      BIS@   80
      *
      * Ensure totals spool file recorded by RCF
     C                     Z-ADDSFNUM     ZSNUM   60
      *
      * Call ZSFILE for GL0037P1
     C                     CALL 'ZSFILE'
     C                     PARM           SEQNO
     C                     PARM *BLANKS   ENTY    3
     C                     PARM           SFILE
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      * If error in ZSFILE, exit via *PSSR subroutine
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     21
     C                     EXSR *PSSR
     C                     END
      *                                                                   CSD005
      *** Use access program AOSARDR0 to retrieve switchable features     CSD005
      *                                                                   CSD005
     C                     CALL 'AOSARDR0'                                CSD005
     C                     PARM *BLANKS   @RTCD   7                       CSD005
     C                     PARM '*VERIFY' @OPTN   7                       CSD005
     C                     PARM 'CSD005'  @KEY    6                       CSD005
     C           SCSARD    PARM SCSARD    DSFDY                           CSD005
      *                                                                   CSD005
      *** Switch on Account Narrative feature if appropriate              CSD005
      *                                                                   CSD005
     C                     SELEC                                          CSD005
     C           @RTCD     WHEQ *BLANKS                                   CSD005
     C                     MOVE *ON       *IN73                           CSD005
      *                                                                   CSD005
      *** If error in access program, exit via *PSSR subroutine           CSD005
      *                                                                   CSD005
     C           @RTCD     WHNE '*NRF'                                    CSD005
     C           *LOCK     IN   LDA                                       CSD005
     C                     MOVEL'GL0037'  ERRPGM 10                       CSD005
     C                     MOVE *BLANKS   ERRKEY  4                       CSD005
     C                     MOVEL'SCSARDPD'ERRFIL 10        ***************CSD005
     C                     Z-ADD002       ERRBAS  30       * DBERROR 002 *CSD005
     C                     EXSR *PSSR                      ***************CSD005
     C                     ENDSL                                          CSD005
      *
      *   Read File SDACODL1
     C                     READ @A5REA6                  10
      *
      *   Print page headings and details
     C                     WRITEPAGHDG
     C                     WRITECHTHDG
     C                     WRITEDETHDG
      *
      * If indicator 10 is Off continue processing else output
      * No Records format.
     C           *IN10     IFEQ '0'
      *
      * Do While Not Last Record
     C           *IN10     DOWEQ'0'
      *
      * Only process retail and non-title records.
     C           A5TOIN    IFEQ 'N'
     C           A5ACTY    ANDEQ'R'
      *
      **Convert*Account*Title to lower case.                              063184
     C**********           EXSR SUB01                                     063184
     C                     MOVE A5ACCD    ACCODE
     C                     MOVEAACCN,1    TITLE1 25
     C                     MOVE TITLE1    TITLE
     C                     MOVE A5ACTY    TYPE
     C                     MOVE A5ACSC    ACSC
     C                     MOVE A5CIAD    RCIAD
     C                     MOVE A5CIAC    RCIAC
     C                     MOVE A5DIAD    RDIAD
     C                     MOVE A5DIAC    RDIAC
     C                     MOVE A5ICID    RICID
     C                     MOVE A5ICIC    RICIC
     C                     MOVE A5NARR    TITLEN                          CSD005
      *
      *** If Account Narrative is required then write details for         CSD005
      *** new field positions                                             CSD005
      *                                                                   CSD005
     C           *IN73     IFEQ *ON                                       CSD005
     C                     WRITEDETFM2                                    CSD005
     C                     MOVE 'Y'       RECORD                          CSD005
     C                     ELSE                                           CSD005
     C                     WRITEDETFMT
     C                     MOVE 'Y'       RECORD
     C                     ENDIF                                          CSD005
      *
      * Reset output fields to blank.
     C                     MOVE *BLANK    ACCODE
     C                     MOVE *BLANK    TITLE
     C                     MOVE *BLANK    TYPE
     C                     MOVE *BLANK    ACSC
     C                     MOVE *BLANKS   RCIAD
     C                     MOVE *BLANKS   RCIAC
     C                     MOVE *BLANKS   RDIAD
     C                     MOVE *BLANKS   RDIAC
     C                     MOVE *BLANKS   RICID
     C                     MOVE *BLANKS   RICIC
     C                     MOVE *BLANKS   TITLEN                          CSD005
     C                     END
      *
      * Read file SDACODL1.
     C                     READ @A5REA6                  10
      *
      * Check for Overflow. Check for last record being a Title Only
      * record. Print page headings and details.
     C           *IN09     IFEQ '1'
     C                     WRITEPAGHDG
     C                     WRITECHTHDG
     C                     WRITEDETHDG
      *
     C                     SETOF                     09
     C                     END
     C                     END
      *
      * If no records have been output then write 'no record' format.
     C           RECORD    IFEQ 'N'
     C                     WRITENORECFMT
     C                     ELSE
      *
      * End of Report
     C                     WRITEENDFMT
     C                     END
      *
     C                     ELSE
      *
      * No Records to Report.
     C                     WRITENORECFMT
     C                     END
      *
     C                     SETON                     LR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**
     C***SUB01*Subroutine to convert the account title to lower           063184
     C*********case when the title only indicator is not on. The          063184
     C*********first letter is left as upper case.                        063184
     C**
     C**  CALLED BY : Main Processing
     C**
     C**  CALLS     : None
     C*
     C********** SUB01     BEGSR                                          063184
     C********** A5TOIN    IFNE 'Y'                                       063184
     C**********           MOVE 02        X       20                      063184
     C********** X         DOWLE25                                        063184
     C********** ACCN,X    LOKUPALPHBT                   50               063184
     C********** *IN50     IFEQ '1'                                       063184
     C**********           BITOF'1'       ACCN,X                          063184
     C**********           END                                            063184
     C**********           ADD  1         X                               063184
     C**********           END                                            063184
     C**********           END                                            063184
     C********** SUB01E    ENDSR                                          063184
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
      *
      *  *PSSR Subroutine to handle program error
      *
      *    CALLED BY : Main Processing
      *
      *    CALLS     : PGM/ZSFILE
      *
     C           *PSSR     BEGSR
      *
      * Check that this is the first time through the routine
      * to prevent recursive calling which would loop the progam.
     C           RUN1      IFEQ 'Y'
     C                     MOVE 'N'       RUN1
      *
      * Check if any of the calls to ZSFILE ended in error.  ZSFILE
      * has its own error handling so most of this routine can be
      * skipped.
     C           *IN21     IFEQ '0'
     C                     DUMP
      *
      * Database error handling.
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVELERRPGM    DBPGM
     C                     MOVELERRKEY    DBKEY
     C                     MOVELERRFIL    DBFILE
     C                     Z-ADDERRBAS    DBASE
     C                     OUT  LDA
      *
      * Open audit file and perform ZSFILE processing.
     C                     OPEN GL0037AU
      *
      * Ensure totals spool file recorded by RCF
     C                     Z-ADDSFNUMU    ZSNUM
     C                     CALL 'ZSFILE'
     C                     PARM           SEQNO
     C                     PARM *BLANKS   ENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUM
     C                     PARM           ZSERR
      *
      * Write audit report.
     C                     WRITEGL0037AH
     C                     WRITEGL0037AE
     C                     END
     C                     END
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDSR
     C********************************************************************
     C/EJECT
      **
     C**  (Following table removed)                                       063184
     C**********ABCDEFGHIJKLMNOPQRSTUVWXYZ                                063184
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
