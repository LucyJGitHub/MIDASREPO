     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Account Keys Report Prompt')
     F********************************************************************
     F*                                                                  *
     F*  Midas General Ledger Module
     F*                                                                  *
     F*     GL0050V - Account Keys Report Prompt                         *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*     LAST AMEND NO. E38930             DATE 29APR92               *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
     F*     E38930 - DB error while amending periodic request as the     *
     F*              sequence number is not valid to chain to            *
     F*              PF/FCPARPPD. This fix removes all references to     *
     F*              PF/FCPARPPD as the parameters have already been     *
     F*              retrieved by the time this program is called and    *
     F*              is passed into it by the calling CLP.               *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
     F*  C R E A T I O N     P A R A M E T E R S                         *
     F*                                                                  *
     F*                                                                  *
     F********************************************************************
     F*CPARDL1IF**E***        K        DISK                               E38930
     FGL0050V#CF  E                    WORKSTN
     F*
     F********************************************************************
     F*
     F*  Function of Indicators
     F*
     F*  03 - Command Key 3 to Exit
     F*  10 - Command Key 10 to Delete
     F*  12 - Command Key 12 for Previous Screen
     F*  25 - FLD1 Validation Indicator ie. 'Y' or 'N'
     F*  26 - FLD2 Validation Indicator ie. 'Y' or 'N'
     F*  27 - FLD3 Validation Indicator ie. 'Y' or 'N'
     F*  35 - Chain fail to FCPARDL1
     F*  60 - Lending ON, Funds Transfer ON
     F*  61 - Lending ON, Funds Transfer OFF
     F*  62 - Lending OFF, Funds Transfer ON
     F*  63 - Lending OFF, Funds Transfer OFF
     F*  64 - Securities Trading ON
     F*  65 - Futures and Options ON
     F*  70 - Screen Valid
     F*  80 - Ensure parameter field is 'Output' only
     F*  90 - Display Footer with 'F10=Delete'
     F*
     F********************************************************************
     F/EJECT
     E*
     E                    CPY@    1   1 80
     E** Copyright Array
     E/EJECT
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I** Data Structure for Compilation  - Copyright Insertion
     I*
     I*
     IPSDS       SDS
     I                                      244 253 WSID
     I                                      254 263 USRID
     I** Data Structure to retrieve User and Workstation ID.
     I*
     I            DS
     I                                        1   3 SLCTNS
     I                                        1   1 FLD1
     I                                        1   1 FLD1@
     I                                        2   2 FLD2
     I                                        2   2 FLD2@
     I                                        3   3 FLD3
     I                                        3   3 FLD3@
     I** Selection Data Structure
     I*
     IRUNDAT      DS                             13
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 @MBIN
     I** Rundate Data Area
     I*
     ISDMMOD    E DSSDMMODPD
     I** Module Indicators
     I*
     IDSFDY     E DSDSFDY
     I** First DS for Access Programs, Short Data Structure
     I*
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      142 151 DBKEY1
     I                                      152 156 DBKEY2
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I** Local Data Area
     I*
     I******************************************************************
     I/EJECT
     I******************************************************************
     C*
     C* INDEX OF SUBROUTINES
     C*
     C* AMEND  - Amend Selection Options on Report Request.
     C* VALID  - Validation of Selection Options entered.
     C* PARMS  - Retrieve Selection Option from FCPARDL1.
     C* CMDKY  - Inform RCF if program ended via Command Key :
     C*            F3, F12 or F10.
     C* ZASNMS - Send message to program message queue.
     C*
     C********************************************************************
     C*
     C** Receive entry parameters
     C*
     C           *ENTRY    PLIST
     C                     PARM           NXSEQ   5        RPT SEQ NO.
     C                     PARM           LEVEL   1        LEVEL
     C                     PARM           ENTTY   3        ENTITY
     C                     PARM           PARAS   3        PARAMETERS
     C                     PARM           ACTIO   1        ACTION
     C                     PARM           COMMD   1        COMMAND
     C*
     C** KLIST for FCPARDL1
     C*
     C***********REPKEY    KLIST                                          E38930
     C***********          KFLD           REPNAM 10                       E38930
     C***********          KFLD           REPSEQ  5                       E38930
     C*
     C** Define Rundat Data Area
     C*
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C** Define Local Data Area
     C*
     C           *NAMVAR   DEFN           LDA
     C*
     C** Clear all fields to send messages to message queue.
     C*
     C                     MOVEL*BLANK    ZAPGM            Program queue
     C                     MOVEL*BLANK    ZAPGRL           Rel queue
     C                     MOVEL*BLANK    ZAMSID           Message Id.
     C                     MOVEL*BLANK    ZAMSGF           Message file.
     C                     MOVEL*BLANK    ZAMSDA           Message data.
     C                     MOVEL*BLANK    ZAMSTP           Message type.
     C*
     C                     MOVE *BLANKS   SPGMQ            Program queue
     C                     MOVEL'GL0050V' SPGMQ            Program queue
     C*
     C** Use access program AOMMODR0 to retrieve module
     C** indicator details
     C*
     C                     CALL 'AOMMODR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDMMOD    PARM SDMMOD    DSFDY
     C*
     C** If error in access program, exit via DBERR subroutine
     C*
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD0         DBASE
     C                     MOVE *BLANKS   DBPGM
     C                     MOVEL'GL0050V' DBPGM
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'SDMMODPD'DBFILE           ***************
     C                     Z-ADD002       DBASE            * DBERROR 002 *
     C                     OUT  LDA                        ***************
     C                     EXSR *PSSR
     C                     END
     C*
     C** Check which modules are on for different screen options
     C*
     C           BGCSLN    IFEQ 'Y'
     C           BGFDTR    ANDEQ'Y'
     C                     MOVE 1         *IN60
     C                     END
     C*
     C           BGCSLN    IFEQ 'Y'
     C           BGFDTR    ANDEQ'N'
     C                     MOVE 1         *IN61
     C                     END
     C*
     C           BGCSLN    IFEQ 'N'
     C           BGFDTR    ANDEQ'Y'
     C                     MOVE 1         *IN62
     C                     END
     C*
     C           BGCSLN    IFEQ 'N'
     C           BGFDTR    ANDEQ'N'
     C                     MOVE 1         *IN63
     C                     END
     C*
     C           BGSECS    IFEQ 'Y'
     C                     MOVE 1         *IN64
     C                     END
     C*
     C           BGFUOP    IFEQ 'Y'
     C                     MOVE 1         *IN65
     C                     END
     C*
     C                     WRITEHEADER
     C*
     C** Determine the program mode.
     C*
     C           ACTIO     CASEQ'A'       AMEND
     C           ACTIO     CASEQ'I'       INSRT
     C           ACTIO     CASEQ'D'       DELET
     C           ACTIO     CASEQ'E'       ENQRE
     C                     END
     C*
     C                     SETON                         LR
     C/EJECT
     C********************************************************************
     C*                                                                  *
     C* AMEND :     Amend Report Request                                 *
     C*                                                                  *
     C* CALLED BY:  Initial processing                                   *
     C*                                                                  *
     C* CALLS:      PARMS subroutine.                                    *
     C*             Y2CLMSC clears message queue.                        *
     C*             VALID subroutine.                                    *
     C*                                                                  *
     C********************************************************************
     C*
     C           AMEND     BEGSR
     C*
     C** Retrieve Parameters from FCPARDL1.
     C*
     C***********          EXSR PARMS                                     E38930
     C                     MOVE PARAS     SLCTNS
     C*
     C           *IN03     DOWEQ'0'
     C           *IN12     ANDEQ'0'
     C           *IN70     ANDEQ'0'
     C*
     C** Display Prompt screen.
     C*
     C                     WRITE#MSGCTL
     C                     EXFMTKEYFMT
     C*
     C** Clear messages from program message queue.
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM SPGMQ     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C*
     C                     EXSR VALID
     C*
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*                                                                  *
     C* INSRT :     Insert Report Request Subroutine                     *
     C*                                                                  *
     C* CALLED BY:  Initial processing                                   *
     C*                                                                  *
     C* CALLS:      Y2CLMSC clears message queue.                        *
     C*             VALID subroutine.                                    *
     C*                                                                  *
     C********************************************************************
     C*
     C           INSRT     BEGSR
     C*
     C                     MOVE 'NNN'     SLCTNS
     C*
     C           *IN03     DOWEQ'0'
     C           *IN12     ANDEQ'0'
     C           *IN70     ANDEQ'0'
     C*
     C** Display Prompt screen.
     C*
     C                     WRITE#MSGCTL
     C                     EXFMTKEYFMT
     C*
     C** Clear messages from program message queue.
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM SPGMQ     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C*
     C                     EXSR VALID
     C*
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*                                                                  *
     C* DELET :     Delete Report Request Subroutine                     *
     C*                                                                  *
     C* CALLED BY:  Initial processing                                   *
     C*                                                                  *
     C* CALLS:      PARMS clears message queue.                          *
     C*             CMDKY subroutine.                                    *
     C*                                                                  *
     C********************************************************************
     C*
     C           DELET     BEGSR
     C*
     C** Retrieve Parameters from FCPARDL1.
     C*
     C***********          EXSR PARMS                                     E38930
     C*
     C** Display Prompt with parameters (output only).
     C** Footer allows 'F10=Delete'.
     C*
     C                     MOVE '1'       *IN80
     C                     MOVE '1'       *IN90
     C                     MOVE PARAS     SLCTNS
     C*
     C** Display Prompt screen.
     C*
     C                     WRITE#MSGCTL
     C                     EXFMTKEYFMT
     C*
     C** Inform RCF how program has ended.
     C*
     C                     EXSR CMDKY
     C*
     C                     ENDSR
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*                                                                  *
     C* ENQRE :     Enquire on Report Request                            *
     C*                                                                  *
     C* CALLED BY:  Initial processing                                   *
     C*                                                                  *
     C* CALLS:      PARMS subroutine.                                    *
     C*             Y2CLMSC clears message queue.                        *
     C*                                                                  *
     C********************************************************************
     C*
     C           ENQRE     BEGSR
     C*
     C** Retrieve Parameters from FCPARDL1.
     C*
     C***********          EXSR PARMS                                     E38930
     C*
     C** Display Prompt with parameters (output only).
     C*
     C                     MOVE '1'       *IN80
     C                     MOVE PARAS     SLCTNS
     C*
     C           *IN03     DOWEQ'0'
     C           *IN12     ANDEQ'0'
     C           *IN70     ANDEQ'0'
     C*
     C** Display Prompt screen.
     C*
     C                     WRITE#MSGCTL
     C                     EXFMTKEYFMT
     C*
     C** Clear messages from program message queue.
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM SPGMQ     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     ANDEQ'0'
     C           *IN10     ANDEQ'0'
     C                     MOVE '1'       *IN70
     C*
     C                     ELSE
     C*
     C** Inform RCF how program has ended.
     C*
     C                     EXSR CMDKY
     C*
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
     C*EJECT*****                                                         E38930
     C********************************************************************E38930
     C*                                                                  *E38930
     C**PARMS*:**   Retrieve Report Parameters from FCPARDPD.            *E38930
     C***********                                                        *E38930
     C**CALLED*BY:  AMEND  subroutine                                    *E38930
     C***********   DELET  subroutine                                    *E38930
     C***********   ENQRE  subroutine                                    *E38930
     C**CALLS:***   *PSSR subroutine                                     *E38930
     C***********                                                        *E38930
     C********************************************************************E38930
     C***********                                                         E38930
     C***********PARMS     BEGSR                                          E38930
     C***********                                                         E38930
     C***Retrieve*fields*for*Report*Name*&*Sequeunce*Number,              E38930
     C***held*on*FCPARDPD.***                                             E38930
     C***********          MOVEL'GL0051'  REPNAM                          E38930
     C***********          MOVE NXSEQ     REPSEQ                          E38930
     C***********REPKEY    CHAINFCPARDL1             35                   E38930
     C************IN35     IFEQ '0'                                       E38930
     C***********          MOVELD7PARM    PARAS                           E38930
     C***********                                                         E38930
     C***If*record*not*found,*exit*with*DB*error.                         E38930
     C***********          ELSE                                           E38930
     C************LOCK     IN   LDA                                       E38930
     C***********          MOVE *BLANKS   DBFILE                          E38930
     C***********          MOVE *BLANKS   DBKEY                           E38930
     C***********          Z-ADD0         DBASE                           E38930
     C***********          MOVE *BLANKS   DBPGM                           E38930
     C***********          MOVEL'FCPARDL1'DBFILE                          E38930
     C***********          MOVELREPNAM    DBKEY1                          E38930
     C***********          MOVELREPSEQ    DBKEY2           ***************E38930
     C***********          Z-ADD001       DBASE            ** DBERR 001 **E38930
     C***********          MOVEL'GL0051'  DBPGM            ***************E38930
     C***********          OUT  LDA                                       E38930
     C***********          EXSR *PSSR                                     E38930
     C***********          END                                            E38930
     C***********          ENDSR                                          E38930
     C***********                                                         E38930
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*                                                                  *
     C* VALID :     Validate Parameters Subroutine                       *
     C*                                                                  *
     C* CALLED BY:  AMEND subroutine.                                    *
     C*             INSRT subroutine.                                    *
     C*                                                                  *
     C* CALLS:      ZASNMS to send error message to program queue.       *
     C*             CMDKY subroutine.                                    *
     C*                                                                  *
     C********************************************************************
     C*
     C           VALID     BEGSR
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     ANDEQ'0'
     C           *IN10     ANDEQ'0'
     C*
     C** Exit program when no reports required
     C*
     C           FLD1      IFEQ 'N'
     C           FLD2      ANDEQ'N'
     C           FLD3      ANDEQ'N'
     C                     MOVE 'USR4250' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
     C*
     C** Validate selection fields or Exit
     C*
     C           FLD1      IFEQ 'Y'
     C           FLD1      OREQ 'N'
     C                     MOVE '0'       *IN25
     C                     ELSE
     C                     MOVE '1'       *IN25
     C                     MOVE 'USR4237' ZAMSID
     C                     EXSR ZASNMS
     C                     END
     C*
     C           *IN64     IFEQ '0'
     C                     MOVE '0'       *IN26
     C                     ELSE
     C           FLD2      IFEQ 'Y'
     C           FLD2      OREQ 'N'
     C                     MOVE '0'       *IN26
     C                     ELSE
     C                     MOVE '1'       *IN26
     C                     MOVE 'USR4237' ZAMSID
     C                     EXSR ZASNMS
     C                     END
     C                     END
     C*
     C           *IN65     IFEQ '0'
     C                     MOVE '0'       *IN27
     C                     ELSE
     C           FLD3      IFEQ 'Y'
     C           FLD3      OREQ 'N'
     C                     MOVE '0'       *IN27
     C                     ELSE
     C                     MOVE '1'       *IN27
     C                     MOVE 'USR4237' ZAMSID
     C                     EXSR ZASNMS
     C                     END
     C                     END
     C*
     C           *IN25     IFEQ '0'
     C           *IN26     ANDEQ'0'
     C           *IN27     ANDEQ'0'
     C                     MOVELSLCTNS    PARAS
     C                     MOVE '1'       *IN70
     C                     END
     C*
     C                     END
     C*
     C** Inform RCF how program has ended.
     C*
     C                     ELSE
     C                     EXSR CMDKY
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*                                                                  *
     C* CMDKY :     Inform RCF if program ended via Command Key.         *
     C*                                                                  *
     C* CALLED BY:  DELET subroutine                                     *
     C*             ENQRE subroutine                                     *
     C*             VALID subroutine                                     *
     C*                                                                  *
     C* CALLS:      ZASNMS to send error message to program queue.       *
     C*                                                                  *
     C********************************************************************
     C*
     C           CMDKY     BEGSR
     C*
     C** Inform RCF how program has ended:
     C*
     C           *IN03     IFEQ '1'                        F3 pressed
     C                     MOVE 'E'       COMMD
     C                     END
     C*
     C           *IN12     IFEQ '1'                        F12 pressed
     C                     MOVE 'F'       COMMD
     C                     END
     C*
     C** CF10 only allowed in Delete Mode, otherwise error message sent
     C*
     C           *IN10     IFEQ '1'                        F10 pressed
     C*
     C           ACTIO     IFEQ 'D'
     C                     MOVE 'D'       COMMD
     C                     ELSE
     C                     MOVEL'USR4238' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     MOVE '0'       *IN10
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*                                                                  *
     C* ZASNMS:     Send message to program message queue                *
     C*                                                                  *
     C* CALLED BY:  VALID subroutine                                     *
     C*             CMDKY subroutine                                     *
     C*                                                                  *
     C* CALLS:      Y2SNMGC                                              *
     C*                                                                  *
     C********************************************************************
     C*
     C           ZASNMS    BEGSR
     C*
     C** Declare message file name
     C*
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C*
     C                     CALL 'Y2SNMGC'
     C                     PARM SPGMQ     ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
     C*
     C                     ENDSR
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*                                                                  *
     C* *PSSR:      Database error processing subroutine                 *
     C*                                                                  *
     C* CALLED BY:  PARMS subroutine                                     *
     C*                                                                  *
     C* CALLS:                                                           *
     C*                                                                  *
     C********************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C** Seton Database error inds., dump pgm and return control
     C*
     C                     SETON                     U7U8
     C*
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
     C                     SETON                     LR
     C                     RETRN
     C*
     C                     ENDSR
     C*
     C********************************************************************
     C/EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
