     H        1
      *================================================================
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Account Transfer Update REACRD')
      *================================================================
      *                                                               *
      *  Midas   -  General Ledger Module                             *
      *                                                               *
      *  GL005600 - Midas GL Account Transfer Update REACRD           *
      *                                                               *
      *  Function:  This program updates the Midas Retail History File*
      *             - Posting Rec,with the new account detail from the*
      *             Account Transfer Selected Accounts File, GLSLACPD.*
      *             It will also write an audit trail record for each *
      *             update of REACR.                                  *
      *                                                               *
      *  Called by: GL003600                                          *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. BUG27769           Date 09Jun10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG27769 - Incorrect error code                              *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *================================================================
      *  Function of Indicators                                       *
      *                                                               *
      *  01 - 19      Input Control.                                  *
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)      *
      *   20          Record Not Found                                *
      *   21          File Error Occurred                             *
      *   22          Start/End Of File Reached                       *
      *   25          Lookup/Scan.                                    *
      *  30 - 39      Program Work Indicators                         *
      *  40 - 49      Output Control.                                 *
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)    *
      *  90 - 99      Error Control.                                  *
      *                                                               *
      *****************************************************************
      *
     FREACR   UF  E           K        DISK         KINFSR *PSSR A
     F            REACRAF                           KIGNORE
     F            REACRZF                           KIGNORE
     F                                              KCOMIT
     FGLAUTFPDO   E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IDSFDY     E DSDSFDY
      *
      ** Definition: First DS for Access Programs, Short Data Structure
      *
     ISDBANK    E DSSDBANKPD
      *
      ** Definition: Bank Details Accessed Via Access Program.
      *
     IDSFRAC      DS
     I                                        1   6 DSFCNU
     I                                        7   9 DSFCCY
     I                                       10  19 DSFACO
     I                                       20  21 DSFACS
     I                                       22  24 DSFBRC
      *
     IDSTOAC      DS
     I                                        1   6 DSTCNU
     I                                        7   9 DSTCCY
     I                                       10  19 DSTACO
     I                                       20  21 DSTACS
     I                                       22  24 DSTBRC
      *
     IDSKITE      DS                             90
     I                                        1   6 KCNUM
     I                                        7   9 KCCY
     I                                       10  190KACOD
     I                                       20  210KACSQ
     I                                       22  24 KBRCA
      *
     I            DS
      * Datastructure: Zoned to Alpha (Last Trans No)
     I                                        1   60WKTNLZ
     I                                        1   6 WKTNLA
      *
     I            DS
      * Datastructure: Zoned to Alpha (Original Entry Date)
     I                                        1   60WKOREZ
     I                                        1   6 WKOREA
      *
     I            DS
      * Datastructure: Zoned to Alpha (Last Chg Date)
     I                                        1   60WKLCDZ
     I                                        1   6 WKLCDA
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           P@PRCD  4
     C                     PARM           P@RFNO 10
     C           DSFRAC    PARM           P@FRAC 24
     C           DSTOAC    PARM           P@TOAC 24
     C                     PARM           P@RTCD  1
      *
     C                     EXSR SRINIT
      *
     C           P@PRCD    IFEQ '*LR '
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     EXSR XSIV01
     C                     EXSR XMREAC
     C                     RETRN
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMREAC - Maintain Retail History                             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: XSAUDT                                                *
      *                                                               *
      *****************************************************************
     C           XMREAC    BEGSR
      *
     C           KREAC     SETLLREACRDF
     C           KREAC     READEREACRDF                2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'002'     DBASE
     C                     MOVEL'REACR   'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETREACRDF                21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'003'     DBASE
     C                     MOVEL'REACR   'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELDSTBRC    BRCA
     C                     MOVELDSTCNU    CUST
     C                     MOVELDSTCCY    ACCY
     C                     MOVELDSTACO    ACOD
     C                     MOVELDSTACS    ASEQ
     C                     WRITEREACRDF                21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'004'     DBASE
     C                     MOVEL'REACR   'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     EXSR XSAUDT
      *
     C           KREAC     READEREACRDF                2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'005'     DBASE
     C                     MOVEL'REACR   'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSAUDT -  Set Audit records.                                 *
      *                                                               *
      *  Called By: XMREAC                                            *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           XSAUDT    BEGSR
      *
      ** Set fields for audit file and write a audit record.
      *
     C                     MOVELP@RFNO    AURFNO
     C                     MOVEL'REACRD  'AUFILE
     C                     MOVEL'REACR   'AULFIL
     C                     MOVELDSFBRC    AUFBRC
     C                     MOVELDSFCNU    AUFCNU
     C                     MOVELDSFACO    AUFACO
     C                     MOVELDSFACS    AUFACS
     C                     MOVELDSTBRC    AUTBRC
     C                     MOVELDSTCNU    AUTCNU
     C                     MOVELDSTACO    AUTACO
     C                     MOVELDSTACS    AUTACS
     C                     Z-ADDTNLU      WKTNLZ
     C                     Z-ADDORED      WKOREZ
     C                     Z-ADDLCD       WKLCDZ
     C           WKTNLA    CAT  WKOREA:1  AUTEXT
     C                     CAT  WKLCDA:1  AUTEXT
     C                     CAT  CHTP:1    AUTEXT 50 P
     C                     MOVELWKDFMT    AUCRTD
     C                     WRITEGLAUTFD0               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'001'     DBASE
     C                     MOVEL'GLAUTFPD'DBFILE
     C                     MOVELAURFNO    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSIV01 -  Set Up Initial Values.                             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           XSIV01    BEGSR
      *
      * Set up key for REACR
      *
     C                     MOVELDSFCNU    KCNUM
     C                     MOVELDSFCCY    KCCY
     C                     MOVELDSFACO    KACOD
     C                     MOVELDSFACS    KACSQ
     C                     MOVELDSFBRC    KBRCA
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT -  Initialise and define fields                       *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      ** Initialise parameters, work fields, etc.
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Retrieve Run Date.
      *
     C                     CALL 'AOBANKR0'             9090
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If return with an error (LR seton in called program) then
      ** process for database error.
      *
     C           *IN90     IFEQ '1'
     C********** @RTCD     OREQ '*ERROR*'                                                   BUG27769
     C           @RTCD     ORNE *BLANKS                                                     BUG27769
     C           *LOCK     IN   LDA
     C                     MOVEL'AOBANKR0'DBFILE
     C                     MOVEL'*FIRST'  DBKEY
     C                     Z-ADD006       DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBJMRDT    WKDFMT  9
     C                     MOVE BJMRDT    WKYEAR  20
      *
     C           WKYEAR    IFLT 72
     C           WKYEAR    ADD  2000      WKCCYY  40
     C                     ELSE
     C           WKYEAR    ADD  1900      WKCCYY
     C                     ENDIF
      *
     C                     MOVE WKCCYY    WKDFMT
      *
     C           KREAC     KLIST
     C                     KFLD           KCNUM
     C                     KFLD           KCCY
     C                     KFLD           KACOD
     C                     KFLD           KACSQ
     C                     KFLD           KBRCA
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *  Called By: (**calling routines**)                            *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
      ** Check to see that *PSSR has not been called yet
      *
     C                     MOVE 'F'       P@RTCD
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
** CPY@
(c) Finastra International Limited 2010
