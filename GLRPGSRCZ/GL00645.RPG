     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Work Postings Extract')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL00645 - Work Postings Extract                              *
      *                                                               *
      *  Called By: GLC0064 - Statement of Account Ctl Pgm            *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD054856           Date 06Jan20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 BUG4844  *CREATE   Date 20Oct03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD054856 - Customer Account Balance Enquiry option 6 does not*
      *             show movements for Account                        *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG4844- Errors in on-line statements                        *
      *                                                               *
      *****************************************************************
     F*STMTAH* IP  E           K        DISK                                               MD054856
     FSTMTAH  IF  E           K        DISK                                                MD054856
     FCGSTMTL1IF  E           K        DISK                                                MD054856
     FAPOST   IF  E           K        DISK
     F            APOSTPDF                          KRENAMEAPOSTF
     FGLACPHL1IF  E           K        DISK
     F            APOSTPDF                          KRENAMEGLACPF
     FGLWKPSPDO   E                    DISK
      *
     C           ACKY      KLIST
     C                     KFLD           BRCA
     C                     KFLD           CNUM
     C                     KFLD           CCY
     C                     KFLD           ACOD
     C                     KFLD           ACSQ
      *
     C                     READ STMTAH                   92                                MD054856
     C           *IN92     IFEQ '1'                                                        MD054856
     C                     READ CGSTMTL1                 92                                MD054856
     C           *IN92     IFEQ '0'                                                        MD054856
     C                     MOVELSDBRCA    BRCA                                             MD054856
     C                     MOVELSDCNUM    CNUM                                             MD054856
     C                     MOVELSDCCY     CCY                                              MD054856
     C                     Z-ADDSDACOD    ACOD                                             MD054856
     C                     Z-ADDSDACSQ    ACSQ                                             MD054856
     C                     ENDIF                                                           MD054856
     C                     ENDIF                                                           MD054856
      *                                                                                    MD054856
     C           ACKY      SETLLAPOST
     C           ACKY      READEAPOST                    99
     C           *IN99     DOWEQ*OFF
     C                     WRITEAPOSTPDF
     C           ACKY      READEAPOST                    99
     C                     ENDDO
      *
     C           ACKY      SETLLGLACPHL1
     C           ACKY      READEGLACPHL1                 99
     C           *IN99     DOWEQ*OFF
     C                     WRITEAPOSTPDF
     C           ACKY      READEGLACPHL1                 99
     C                     ENDDO
     C                     SETON                     LR                                    MD054856
     C                     RETRN                                                           MD054856
