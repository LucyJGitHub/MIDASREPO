     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Account Transfer Update DEALSDG')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL006600 - Midas GL Account Transfer Update DEALSDG          *
      *                                                               *
      *  Function:  This program updates the Midas Fwd rate agreements*
      *             /int. rate swaps File, DEALSDG with the new       *
      *             account detail from the A/c Transfer Selected     *
      *             Accounts File, GLSLACPD.                          *
      *             It will also write an audit trail record for each *
      *             update of DEALSDG.                                *
      *                                                               *
      *  Called By: GL003600                                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. BUG27903           Date 13Jul10               *
      *  Prev Amend No. BUG27769           Date 09Jun10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG27903 - Unable to transfer account with setl 01 and 08    *
      *  BUG27769 - Incorrect error code                              *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
      *  Function of Indicators                                       *
      *                                                               *
      *  01 - 19      Input Control.                                  *
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)      *
      *   20          Record Not Found                                *
      *   21          File Error Occurred                             *
      *   22          Start/End Of File Reached                       *
      *   25          Lookup/Scan.                                    *
      *  30 - 39      Program Work Indicators                         *
      *  40 - 49      Output Control.                                 *
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)    *
      *  90 - 99      Error Control.                                  *
      *****************************************************************
      *
     FDEALSL28UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
     FDEALSL29UF  E           K        DISK         KINFSR *PSSR A
     F            DEALSDGF                          KRENAMEDEALSF20
     F                                              KCOMIT
     FDEALSL47UF  E           K        DISK         KINFSR *PSSR                            BUG27903
     F            DEALSDGF                          KRENAMEDEALSF47                         BUG27903
     F                                              KCOMIT                                  BUG27903
     FDEALSL48UF  E           K        DISK         KINFSR *PSSR                            BUG27903
     F            DEALSDGF                          KRENAMEDEALSF48                         BUG27903
     F                                              KCOMIT                                  BUG27903
     FGLAUTFPDO   E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
     FGLSLACL1IF  E           K        DISK         KINFSR *PSSR
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     I              '0000000000  '        C         WKZERO
     ILDA       E DSLDA                         256
      *                                                                                     BUG27903
     INOSTD     E DSNOSTD                         8                                         BUG27903
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     ISDBANK    E DSSDBANKPD
      *
     IDSFDY     E DSDSFDY
      *
     IDSFRAC      DS
     I                                        1   6 DSFCNU
     I                                        7   9 DSFCCY
     I                                       10  19 DSFACO
     I                                       20  21 DSFACS
     I                                       22  24 DSFBRC
      *
     IDSTOAC      DS
     I                                        1   6 DSTCNU
     I                                        7   9 DSTCCY
     I                                       10  19 DSTACO
     I                                       20  21 DSTACS
     I                                       22  24 DSTBRC
      *
     IDSKITE      DS                             90
     I                                        1  18 KDLIN
     I                                       19  21 KCCY
     I                                       22  24 KBRCA
      *
     IDSKIT2      DS                             90
     I                                        1  18 KDLIN2
     I                                       19  21 KCCY2
     I                                       22  24 KBRCA2
      *
     IDSKIT3      DS                             90
     I                                        1   6 KSFCNU
     I                                        7   9 KSFCCY
     I                                       10  19 KSFACO
     I                                       20  21 KSFACS
     I                                       22  24 KSFBRC
     I                                       25  34 KSRFNO
      *                                                                                     BUG27903
     IWNOST       DS                                                                        BUG27903
      * External Data Area for Nostro Details                                               BUG27903
     I                                        1   3 WWCCY                                   BUG27903
     I                                        4   5 WWNOSN                                  BUG27903
     I                                        6   8 WWBRCH                                  BUG27903
      *
     I            DS
     I                                        1   60HMDN38
     I                                        1   6 WKDN38
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           P@PRCD  4
     C                     PARM           P@RFNO 10
     C           DSFRAC    PARM           P@FRAC 24
     C           DSTOAC    PARM           P@TOAC 24
     C                     PARM           P@RTCD  1
      *
     C                     EXSR SRINIT
      *
     C           P@PRCD    IFEQ '*LR '
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     EXSR XSIV01
     C                     EXSR XMDEDD
      *
     C           DSTBRC    IFNE DSFBRC
     C           KDLIN2    ANDNE*BLANKS
     C           KDLIN2    ANDNEWKZERO
     C                     EXSR XMDED2
     C                     ENDIF
      *                                                                                     BUG27903
     C           WNOST     IFNE *BLANKS                                                     BUG27903
     C                     EXSR XMDED3                                                      BUG27903
     C                     ENDIF                                                            BUG27903
      *
     C                     RETRN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMDEDD - Maintain Midas Fwd rate agreements/int. rate swaps  *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:     XSAUDT                                            *
      *                                                               *
      *****************************************************************
      *
     C           XMDEDD    BEGSR
      *
     C           KDEAL     SETLLDEALSL28
     C           KDEAL     READEDEALSL28               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'001'     DBASE
     C                     MOVEL'DEALSL28'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Delete and Write Records until End of File for Selected Account
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETDEALSDGF               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'002'     DBASE
     C                     MOVEL'DEALSL28'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARROBR
     C                     CLEARUCUCY
     C                     CLEARURACC
     C                     MOVELDSTBRC    ROBR
     C                     MOVELDSTCCY    UCUCY
     C                     MOVELWKDLIN    URACC
      *
     C                     WRITEDEALSDGF               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'003'     DBASE
     C                     MOVEL'DEALSL28'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARGLAUTFD0
     C                     MOVEL'DEALSL28'AULFIL
     C                     EXSR XSAUDT
      *
     C           KDEAL     READEDEALSL28               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'004'     DBASE
     C                     MOVEL'DEALSL28'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDDO
      *
     C           KDEAL     SETLLDEALSL29
     C           KDEAL     READEDEALSL29               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'005'     DBASE
     C                     MOVEL'DEALSL29'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETDEALSF20               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'006'     DBASE
     C                     MOVEL'DEALSL29'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARPOBR
     C                     CLEARUCUCY
     C                     CLEARUPACC
     C                     MOVELDSTBRC    POBR
     C                     MOVELDSTCCY    UCUCY
     C                     MOVELWKDLIN    UPACC
      *
     C                     WRITEDEALSF20               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'007'     DBASE
     C                     MOVEL'DEALSL29'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARGLAUTFD0
     C                     MOVEL'DEALSL29'AULFIL
     C                     EXSR XSAUDT
      *
     C           KDEAL     READEDEALSL29               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'008'     DBASE
     C                     MOVEL'DEALSL29'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMDED2 - Maintain Midas Fwd rate agreements/int. rate swaps  *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *****************************************************************
      *
     C           XMDED2    BEGSR
      *
     C           KDEAL2    SETLLDEALSL28
     C           KDEAL2    READEDEALSL28               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'009'     DBASE
     C                     MOVEL'DEALSL28'DBFILE
     C                     MOVELDSKIT2    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Delete and Write Records until End of File for Selected Account
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETDEALSDGF               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'010'     DBASE
     C                     MOVEL'DEALSL28'DBFILE
     C                     MOVELDSKIT2    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARROBR
     C                     MOVELDSTBRC    ROBR
      *
     C                     WRITEDEALSDGF               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'011'     DBASE
     C                     MOVEL'DEALSL28'DBFILE
     C                     MOVELDSKIT2    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Set fields for audit file and write an audit record.
      *
     C                     CLEARGLAUTFD0
     C                     MOVEL'DEALSL28'AULFIL
     C                     EXSR XSAUDT
      *
      ** Read DEAL
      *
     C           KDEAL2    READEDEALSL28               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'012'     DBASE
     C                     MOVEL'DEALSL28'DBFILE
     C                     MOVELDSKIT2    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDDO
      *
     C           KDEAL2    SETLLDEALSL29
     C           KDEAL2    READEDEALSL29               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'013'     DBASE
     C                     MOVEL'DEALSL29'DBFILE
     C                     MOVELDSKIT2    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Delete and Write Records until End of File for Selected Acct
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETDEALSF20               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'014'     DBASE
     C                     MOVEL'DEALSL29'DBFILE
     C                     MOVELDSKIT2    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARPOBR
     C                     MOVELDSTBRC    POBR
      *
     C                     WRITEDEALSF20               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'015'     DBASE
     C                     MOVEL'DEALSL29'DBFILE
     C                     MOVELDSKIT2    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Set fields for audit file and write a audit record.
      *
     C                     CLEARGLAUTFD0
     C                     MOVEL'DEALSL29'AULFIL
     C                     EXSR XSAUDT
      *
      ** Read DEAL
      *
     C           KDEAL2    READEDEALSL29               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'016'     DBASE
     C                     MOVEL'DEALSL29'DBFILE
     C                     MOVELDSKIT2    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************                     BUG27903
      /EJECT                                                                                BUG27903
      *****************************************************************                     BUG27903
      *                                                               *                     BUG27903
      *  XMDED3 - Maintain Midas Fwd rate agreements/int. rate swaps  *                     BUG27903
      *                                                               *                     BUG27903
      *  Called By: Main Processing                                   *                     BUG27903
      *                                                               *                     BUG27903
      *  Calls:     None                                              *                     BUG27903
      *                                                               *                     BUG27903
      *****************************************************************                     BUG27903
      *                                                                                     BUG27903
     C           XMDED3    BEGSR                                                            BUG27903
      *                                                                                     BUG27903
     C                     MOVE *BLANKS   KDLIN3 18                                         BUG27903
     C                     MOVELWWNOSN    KDLIN3                                            BUG27903
     C                     MOVELWNOST     WNOST2  5                                         BUG27903
      *                                                                                     BUG27903
     C           KDEAL3    SETLLDEALSL47                                                    BUG27903
     C           KDEAL3    READEDEALSL47               2122                                 BUG27903
      *                                                                                     BUG27903
     C           *IN21     IFEQ *ON                                                         BUG27903
     C           *LOCK     IN   LDA                                                         BUG27903
     C                     MOVEL'020'     DBASE                                             BUG27903
     C                     MOVEL'DEALSL47'DBFILE                                            BUG27903
     C                     MOVELWNOST2    DBKEY                                             BUG27903
     C                     OUT  LDA                                                         BUG27903
     C                     EXSR *PSSR                                                       BUG27903
     C                     ENDIF                                                            BUG27903
      *                                                                                     BUG27903
      ** Update Records until End of File for Selected Account                              BUG27903
      *                                                                                     BUG27903
     C           *IN22     DOWEQ*OFF                                                        BUG27903
      *                                                                                     BUG27903
     C                     CLEARROBR                                                        BUG27903
     C                     MOVELWWBRCH    ROBR                                              BUG27903
      *                                                                                     BUG27903
     C                     UPDATDEALSF47               21                                   BUG27903
      *                                                                                     BUG27903
     C           *IN21     IFEQ *ON                                                         BUG27903
     C           *LOCK     IN   LDA                                                         BUG27903
     C                     MOVEL'021'     DBASE                                             BUG27903
     C                     MOVEL'DEALSL47'DBFILE                                            BUG27903
     C                     MOVELWNOST2    DBKEY                                             BUG27903
     C                     OUT  LDA                                                         BUG27903
     C                     EXSR *PSSR                                                       BUG27903
     C                     ENDIF                                                            BUG27903
      *                                                                                     BUG27903
      ** Set fields for audit file and write an audit record.                               BUG27903
      *                                                                                     BUG27903
     C                     CLEARGLAUTFD0                                                    BUG27903
     C                     MOVEL'DEALSL47'AULFIL                                            BUG27903
     C                     EXSR XSAUDT                                                      BUG27903
      *                                                                                     BUG27903
      ** Read DEAL                                                                          BUG27903
      *                                                                                     BUG27903
     C           KDEAL3    READEDEALSL47               2122                                 BUG27903
      *                                                                                     BUG27903
     C                     ENDDO                                                            BUG27903
      *                                                                                     BUG27903
     C           KDEAL3    SETLLDEALSL48                                                    BUG27903
     C           KDEAL3    READEDEALSL48               2122                                 BUG27903
      *                                                                                     BUG27903
     C           *IN21     IFEQ *ON                                                         BUG27903
     C           *LOCK     IN   LDA                                                         BUG27903
     C                     MOVEL'022'     DBASE                                             BUG27903
     C                     MOVEL'DEALSL48'DBFILE                                            BUG27903
     C                     MOVELWNOST2    DBKEY                                             BUG27903
     C                     OUT  LDA                                                         BUG27903
     C                     EXSR *PSSR                                                       BUG27903
     C                     ENDIF                                                            BUG27903
      *                                                                                     BUG27903
      ** Update Records until End of File for Selected Acct                                 BUG27903
      *                                                                                     BUG27903
     C           *IN22     DOWEQ*OFF                                                        BUG27903
      *                                                                                     BUG27903
     C                     CLEARPOBR                                                        BUG27903
     C                     MOVELWWBRCH    POBR                                              BUG27903
      *                                                                                     BUG27903
     C                     UPDATDEALSF48               21                                   BUG27903
      *                                                                                     BUG27903
     C           *IN21     IFEQ *ON                                                         BUG27903
     C           *LOCK     IN   LDA                                                         BUG27903
     C                     MOVEL'023'     DBASE                                             BUG27903
     C                     MOVEL'DEALSL48'DBFILE                                            BUG27903
     C                     MOVELWNOST2    DBKEY                                             BUG27903
     C                     OUT  LDA                                                         BUG27903
     C                     EXSR *PSSR                                                       BUG27903
     C                     ENDIF                                                            BUG27903
      *                                                                                     BUG27903
      ** Set fields for audit file and write a audit record.                                BUG27903
      *                                                                                     BUG27903
     C                     CLEARGLAUTFD0                                                    BUG27903
     C                     MOVEL'DEALSL48'AULFIL                                            BUG27903
     C                     EXSR XSAUDT                                                      BUG27903
      *                                                                                     BUG27903
      ** Read DEAL                                                                          BUG27903
      *                                                                                     BUG27903
     C           KDEAL3    READEDEALSL48               2122                                 BUG27903
      *                                                                                     BUG27903
     C                     ENDDO                                                            BUG27903
      *                                                                                     BUG27903
     C                     ENDSR                                                            BUG27903
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSAUDT - Set Audit records                                   *
      *                                                               *
      *  Called By: XMDEDD, XMDED2                                    *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *****************************************************************
      *
     C           XSAUDT    BEGSR
      *
     C                     MOVELP@RFNO    AURFNO
     C                     MOVEL'DEALSDG 'AUFILE
     C                     MOVELDSFBRC    AUFBRC
     C                     MOVELDSFCNU    AUFCNU
     C                     MOVELDSFACO    AUFACO
     C                     MOVELDSFACS    AUFACS
     C                     MOVELDSTBRC    AUTBRC
     C                     MOVELDSTCNU    AUTCNU
     C                     MOVELDSTACO    AUTACO
     C                     MOVELDSTACS    AUTACS
     C                     MOVELDLNO      WKDLNO  6
     C                     MOVELDDAT      WKDDAT  5
     C                     MOVELUPAMT     WKUPAM 13
     C                     MOVELTPAMT     WKTPAM 13
     C           WKDLNO    CAT  WKDDAT:1  AUTEXT
     C                     CAT  WKUPAM:1  AUTEXT
     C                     CAT  WKTPAM:1  AUTEXT 50 P
     C                     MOVELWKDFMT    AUCRTD
     C                     WRITEGLAUTFD0               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'017'     DBASE
     C                     MOVEL'GLAUTFPD'DBFILE
     C                     MOVELAURFNO    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSIV01 - Set up initial values                               *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *****************************************************************
      *
     C           XSIV01    BEGSR
      *
     C                     MOVELDSFCCY    KCCY
     C                     MOVELDSFBRC    KBRCA
     C           DSFCNU    CAT  DSFACO:0  KDLIN
     C                     CAT  DSFACS:0  KDLIN
     C                     MOVELDSFCNU    KSFCNU
     C                     MOVELDSFCCY    KSFCCY
     C                     MOVELDSFACO    KSFACO
     C                     MOVELDSFACS    KSFACS
     C                     MOVELDSFBRC    KSFBRC
     C                     MOVELP@RFNO    KSRFNO
      *
     C           KSLAC     CHAINGLSLACL1             3031
      *
     C           *IN30     IFEQ *ON
     C           *IN31     OREQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'018'     DBASE
     C                     MOVEL'GLSLACL1'DBFILE
     C                     MOVELDSKIT3    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELDSFCCY    KCCY2
     C                     MOVELDSFBRC    KBRCA2
     C                     MOVELSLACNO    KDLIN2
      *
     C           DSTCNU    CAT  DSTACO:0  WKDLIN 18
     C                     CAT  DSTACS:0  WKDLIN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Initialise and define fields                        *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           NOSTD                                             BUG27903
      *                                                                                     BUG27903
      ** Check nostro account changes                                                       BUG27903
      *                                                                                     BUG27903
     C                     MOVE *BLANKS   WNOST                                             BUG27903
     C           *LOCK     IN   NOSTD                                                       BUG27903
     C                     MOVE DANOST    WNOST                                             BUG27903
     C                     OUT  NOSTD                                                       BUG27903
      *
      ** Retrieve Run Date.
      *
     C                     CALL 'AOBANKR0'             9090
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If return with an error (LR seton in called program) then
      ** process for database error.
      *
     C           *IN90     IFEQ '1'
     C********** @RTCD     OREQ '*ERROR*'                                                   BUG27769
     C           @RTCD     ORNE *BLANKS                                                     BUG27769
     C           *LOCK     IN   LDA
     C                     MOVEL'AOBANKR0'DBFILE
     C                     MOVEL'*FIRST'  DBKEY
     C                     Z-ADD019       DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBJMRDT    WKDFMT  9
     C                     MOVE BJMRDT    WKYEAR  20
      *
     C           WKYEAR    IFLT 72
     C           WKYEAR    ADD  2000      WKCCYY  40
     C                     ELSE
     C           WKYEAR    ADD  1900      WKCCYY
     C                     ENDIF
      *
     C                     MOVE WKCCYY    WKDFMT  9
     C                     MOVE *BLANKS   KDLIN
     C                     MOVE *BLANKS   WKDLIN
      *
     C           KDEAL     KLIST
     C                     KFLD           KDLIN
     C                     KFLD           KCCY
     C                     KFLD           KBRCA
      *
     C           KDEAL2    KLIST
     C                     KFLD           KDLIN2
     C                     KFLD           KCCY2
     C                     KFLD           KBRCA2
      *                                                                                     BUG27903
     C           KDEAL3    KLIST                                                            BUG27903
     C                     KFLD           KDLIN3                                            BUG27903
     C                     KFLD           WWCCY                                             BUG27903
      *
     C           KSLAC     KLIST
     C                     KFLD           KSFBRC
     C                     KFLD           KSFCNU
     C                     KFLD           KSFCCY
     C                     KFLD           KSFACO
     C                     KFLD           KSFACS
     C                     KFLD           KSRFNO
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     MOVE 'F'       P@RTCD
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2010
