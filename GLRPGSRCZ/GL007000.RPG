     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Account Transfer Update FCLTYFN')             *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL007000 - Midas GL Account Transfer Update FCLTYFN          *
      *                                                               *
      *  Function:  This program updates the Midas LE Facilities Detl *
      *             file with the new account detail from the Account *
      *             Transfer Selected Accounts File, GLSLACPD.        *
      *             It will also write an audit trail record for each *
      *             update of FCLTY.                                  *
      *                                                               *
      *  Called By: GL003600                                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. CLE064             Date 06Dec10               *
      *  Prev Amend No. BUG27903           Date 13Jul10               *
      *                 BUG27769           Date 09Jun10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE064 - Enhancement of Lending Frequencies (Recompile)      *
      *  BUG27903 - Unable to transfer account with setl 01 and 08    *
      *  BUG27769 - Incorrect error code                              *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
      *  Function of Indicators                                       *
      *                                                               *
      *  01 - 19      Input Control.                                  *
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)      *
      *   20          Record Not Found                                *
      *   21          File Error Occurred                             *
      *   22          Start/End Of File Reached                       *
      *   25          Lookup/Scan.                                    *
      *  30 - 39      Program Work Indicators                         *
      *  40 - 49      Output Control.                                 *
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)    *
      *  90 - 99      Error Control.                                  *
      *****************************************************************
      *
     FFCLTYL10IF  E           K        DISK         KINFSR *PSSR
     FFCLTYL11IF  E           K        DISK         KINFSR *PSSR                            BUG27903
     F            FCLTYFJF                          KRENAMEFCLTYFJ2                         BUG27903
     FFCLTY   UF  E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
     FGLAUTFPDO   E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     I              '0000000000  '        C         WKZERO
     ILDA       E DSLDA                         256
      *                                                                                     BUG27903
     INOSTD     E DSNOSTD                         8                                         BUG27903
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     ISDBANK    E DSSDBANKPD
      *
     IDSFDY     E DSDSFDY
      *
     IDSFRAC      DS
     I                                        1   6 DSFCNU
     I                                        7   9 DSFCCY
     I                                       10  19 DSFACO
     I                                       20  21 DSFACS
     I                                       22  24 DSFBRC
      *
     IDSTOAC      DS
     I                                        1   6 DSTCNU
     I                                        7   9 DSTCCY
     I                                       10  19 DSTACO
     I                                       20  21 DSTACS
     I                                       22  24 DSTBRC
      *
     IDSFCLT      DS
     I                                        1   6 KCNUM
     I                                        7   90KFACT
     I                                       10  110KFCNO
     I                                       12  12 KRCTP
      *                                                                                     BUG27903
     IWNOST       DS                                                                        BUG27903
      * External Data Area for Nostro Details                                               BUG27903
     I                                        1   3 WWCCY                                   BUG27903
     I                                        4   5 WWNOSN                                  BUG27903
     I                                        6   8 WWBRCH                                  BUG27903
      *
     IDSKITE      DS                             90
     I                                        1  18 KDLIN
     I                                       19  21 KCCY
     I                                       22  24 KBRCA
      *
     I            DS
     I                                        1   30FACT
     I                                        1   2 WKFACT
      *
     I            DS
     I                                        1   20FCNO
     I                                        1   2 WKFCNO
      *
     I            DS
     I                                        1   50STDT
     I                                        1   5 WKSTDT
      *
     I            DS
     I                                        1   50EDDT
     I                                        1   5 WKEDDT
      *
     I            DS
     I                                        1   50DUDT
     I                                        1   5 WKDUDT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           P@PRCD  4
     C                     PARM           P@RFNO 10
     C           DSFRAC    PARM           P@FRAC 24
     C           DSTOAC    PARM           P@TOAC 24
     C                     PARM           P@RTCD  1
      *
     C                     EXSR SRINIT
      *
     C           P@PRCD    IFEQ '*LR '
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     EXSR XSIV01
     C                     EXSR XMFCJ1
      *                                                                                     BUG27903
     C           WNOST     IFNE *BLANKS                                                     BUG27903
     C                     EXSR XMFCJ2                                                      BUG27903
     C                     ENDIF                                                            BUG27903
      *
     C                     RETRN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMFCJ1 - Maintain Midas LE Facilities.                       *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:     XSAUDT                                            *
      *                                                               *
      *****************************************************************
      *
     C           XMFCJ1    BEGSR
      *
     C           KFCJ1     SETLLFCLTYFJF
     C           KFCJ1     READEFCLTYFJF               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'001'     DBASE
     C                     MOVEL'FCLTYL10'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Delete and Write Records until End of File for Selected Account
      *
     C           *IN22     DOWEQ*OFF
      *
     C           FCCY      IFEQ DSFCCY
     C                     MOVE *BLANKS   KCNUM                                             BUG27903
     C                     MOVE *ZERO     KFACT                                             BUG27903
     C                     MOVE *ZERO     KFCNO                                             BUG27903
     C                     MOVE *BLANKS   KRCTP                                             BUG27903
     C                     MOVE CNUM      KCNUM                                             BUG27903
     C                     MOVE FACT      KFACT                                             BUG27903
     C                     MOVE FCNO      KFCNO                                             BUG27903
     C                     MOVE 'B'       KRCTP                                             BUG27903
     C           KFCLT     CHAINFCLTYFNF             2221
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'002'     DBASE
     C                     MOVEL'FCLTY   'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARCFBR
     C                     CLEAROBAC
     C                     MOVELDSTBRC    CFBR
     C                     MOVELWKDLIN    OBAC
      *
     C                     UPDATFCLTYFNF               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'003'     DBASE
     C                     MOVEL'FCLTY   'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARGLAUTFD0
     C                     MOVEL'FCLTY   'AULFIL
     C                     EXSR XSAUDT
     C                     ENDIF
      *
     C           KFCJ1     READEFCLTYFJF               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'004'     DBASE
     C                     MOVEL'FCLTYL10'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************                     BUG27903
      /EJECT                                                                                BUG27903
      *****************************************************************                     BUG27903
      *                                                               *                     BUG27903
      *  XMFCJ2 - Maintain Midas LE Facilities.                       *                     BUG27903
      *                                                               *                     BUG27903
      *  Called By: Main Processing                                   *                     BUG27903
      *                                                               *                     BUG27903
      *  Calls:     XSAUDT                                            *                     BUG27903
      *                                                               *                     BUG27903
      *****************************************************************                     BUG27903
      *                                                                                     BUG27903
     C           XMFCJ2    BEGSR                                                            BUG27903
      *                                                                                     BUG27903
     C                     MOVE *BLANKS   KDLIN3 18                                         BUG27903
     C                     MOVELWWNOSN    KDLIN3                                            BUG27903
     C                     MOVELWNOST     WNOST2  5                                         BUG27903
      *                                                                                     BUG27903
     C           KFCJ2     SETLLFCLTYFJ2                                                    BUG27903
     C           KFCJ2     READEFCLTYFJ2               2122                                 BUG27903
      *                                                                                     BUG27903
     C           *IN21     IFEQ *ON                                                         BUG27903
     C           *LOCK     IN   LDA                                                         BUG27903
     C                     MOVEL'007'     DBASE                                             BUG27903
     C                     MOVEL'FCLTYL11'DBFILE                                            BUG27903
     C                     MOVELWNOST2    DBKEY                                             BUG27903
     C                     OUT  LDA                                                         BUG27903
     C                     EXSR *PSSR                                                       BUG27903
     C                     ENDIF                                                            BUG27903
      *                                                                                     BUG27903
      ** Update Records until End of File for Selected Account                              BUG27903
      *                                                                                     BUG27903
     C           *IN22     DOWEQ*OFF                                                        BUG27903
      *                                                                                     BUG27903
     C           FCCY      IFEQ DSFCCY                                                      BUG27903
     C                     MOVE *BLANKS   KCNUM                                             BUG27903
     C                     MOVE *ZERO     KFACT                                             BUG27903
     C                     MOVE *ZERO     KFCNO                                             BUG27903
     C                     MOVE *BLANKS   KRCTP                                             BUG27903
     C                     MOVE CNUM      KCNUM                                             BUG27903
     C                     MOVE FACT      KFACT                                             BUG27903
     C                     MOVE FCNO      KFCNO                                             BUG27903
     C                     MOVE 'B'       KRCTP                                             BUG27903
     C           KFCLT     CHAINFCLTYFNF             2221                                   BUG27903
      *                                                                                     BUG27903
     C           *IN21     IFEQ *ON                                                         BUG27903
     C           *LOCK     IN   LDA                                                         BUG27903
     C                     MOVEL'008'     DBASE                                             BUG27903
     C                     MOVEL'FCLTY   'DBFILE                                            BUG27903
     C                     MOVELWNOST2    DBKEY                                             BUG27903
     C                     OUT  LDA                                                         BUG27903
     C                     EXSR *PSSR                                                       BUG27903
     C                     ENDIF                                                            BUG27903
      *                                                                                     BUG27903
     C                     CLEARCFBR                                                        BUG27903
     C                     MOVELDSTBRC    CFBR                                              BUG27903
      *                                                                                     BUG27903
     C                     UPDATFCLTYFNF               21                                   BUG27903
      *                                                                                     BUG27903
     C           *IN21     IFEQ *ON                                                         BUG27903
     C           *LOCK     IN   LDA                                                         BUG27903
     C                     MOVEL'009'     DBASE                                             BUG27903
     C                     MOVEL'FCLTY   'DBFILE                                            BUG27903
     C                     MOVELWNOST2    DBKEY                                             BUG27903
     C                     OUT  LDA                                                         BUG27903
     C                     EXSR *PSSR                                                       BUG27903
     C                     ENDIF                                                            BUG27903
      *                                                                                     BUG27903
     C                     CLEARGLAUTFD0                                                    BUG27903
     C                     MOVEL'FCLTY   'AULFIL                                            BUG27903
     C                     EXSR XSAUDT                                                      BUG27903
     C                     ENDIF                                                            BUG27903
      *                                                                                     BUG27903
     C           KFCJ2     READEFCLTYFJ2               2122                                 BUG27903
      *                                                                                     BUG27903
     C                     ENDDO                                                            BUG27903
      *                                                                                     BUG27903
     C                     ENDSR                                                            BUG27903
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSAUDT - Set Audit records                                   *
      *                                                               *
      *  Called By: XMFCJ1                                            *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *****************************************************************
      *
     C           XSAUDT    BEGSR
      *
     C                     MOVELP@RFNO    AURFNO
     C                     MOVEL'FCLTYFN 'AUFILE
     C                     MOVELDSFBRC    AUFBRC
     C                     MOVELDSFCNU    AUFCNU
     C                     MOVELDSFACO    AUFACO
     C                     MOVELDSFACS    AUFACS
     C                     MOVELDSTBRC    AUTBRC
     C                     MOVELDSTCNU    AUTCNU
     C                     MOVELDSTACO    AUTACO
     C                     MOVELDSTACS    AUTACS
     C           WKFACT    CAT  WKFCNO:1  AUTEXT 50 P
     C                     CAT  WKSTDT:1  AUTEXT
     C                     CAT  WKEDDT:1  AUTEXT
     C                     CAT  WKDUDT:1  AUTEXT
     C                     MOVELWKDFMT    AUCRTD
     C                     WRITEGLAUTFD0               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'005'     DBASE
     C                     MOVEL'GLAUTFPD'DBFILE
     C                     MOVELAURFNO    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSIV01 - Set up initial values                               *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *****************************************************************
      *
     C           XSIV01    BEGSR
      *
     C                     MOVELDSFCCY    KCCY
     C                     MOVELDSFBRC    KBRCA
     C           DSFCNU    CAT  DSFACO:0  KDLIN
     C                     CAT  DSFACS:0  KDLIN
      *
     C           DSTCNU    CAT  DSTACO:0  WKDLIN 18
     C                     CAT  DSTACS:0  WKDLIN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Initialise and define fields                        *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           NOSTD                                             BUG27903
      *                                                                                     BUG27903
      ** Check nostro account changes                                                       BUG27903
      *                                                                                     BUG27903
     C                     MOVE *BLANKS   WNOST                                             BUG27903
     C           *LOCK     IN   NOSTD                                                       BUG27903
     C                     MOVE DANOST    WNOST                                             BUG27903
     C                     OUT  NOSTD                                                       BUG27903
      *
      ** Retrieve Run Date.
      *
     C                     CALL 'AOBANKR0'             9090
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If return with an error (LR seton in called program) then
      ** process for database error.
      *
     C           *IN90     IFEQ '1'
     C********** @RTCD     OREQ '*ERROR*'                                                   BUG27769
     C           @RTCD     ORNE *BLANKS                                                     BUG27769
     C           *LOCK     IN   LDA
     C                     MOVEL'AOBANKR0'DBFILE
     C                     MOVEL'*FIRST'  DBKEY
     C                     Z-ADD006       DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBJMRDT    WKDFMT  9
     C                     MOVE BJMRDT    WKYEAR  20
      *
     C           WKYEAR    IFLT 72
     C           WKYEAR    ADD  2000      WKCCYY  40
     C                     ELSE
     C           WKYEAR    ADD  1900      WKCCYY
     C                     ENDIF
      *
     C                     MOVE WKCCYY    WKDFMT  9
     C                     MOVE *BLANKS   KDLIN
     C                     MOVE *BLANKS   WKDLIN
      *
     C           KFCJ1     KLIST
     C                     KFLD           KDLIN
     C                     KFLD           KBRCA
      *                                                                                     BUG27903
     C           KFCJ2     KLIST                                                            BUG27903
     C                     KFLD           KDLIN3                                            BUG27903
     C                     KFLD           KBRCA                                             BUG27903
      *
     C           KFCLT     KLIST
     C                     KFLD           KCNUM
     C                     KFLD           KFACT
     C                     KFLD           KFCNO
     C                     KFLD           KRCTP
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     MOVE 'F'       P@RTCD
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2010
