     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Account Transfer Update STANDSB')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL007300 - Midas GL Account Transfer Update STANDSB          *
      *                                                               *
      *  Function:  This program updates the Midas RE Standing Orders *
      *             Details File with the new account detail from the *
      *             Account Transfer Selected Accounts File, GLSLACPD.*
      *             It also will write an audit trail record for each *
      *             update of STANDL6 and STANDL7                     *
      *                                                               *
      *  Called By: GL003600                                          *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. BUG27417B          Date 13Aug10               *
      *                 BUG27769           Date 09Jun10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG27417B - GLC30 00001 FOOB                                 *
      *  BUG27769 - Incorrect error code                              *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
      *  Function of Indicators                                       *
      *                                                               *
      *  01 - 19      Input Control.                                  *
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)      *
      *   20          Record Not Found                                *
      *   21          File Error Occurred                             *
      *   22          Start/End Of File Reached                       *
      *   25          Lookup/Scan.                                    *
      *  30 - 39      Program Work Indicators                         *
      *  40 - 49      Output Control.                                 *
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)    *
      *  90 - 99      Error Control.                                  *
      *****************************************************************
      *
     FSTANDL6 UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
     F            STANDSBF                          KRENAMESTANDSF6
     FSTANDL7 UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
     F            STANDSBF                          KRENAMESTANDSF7
     FGLAUTFPDO   E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     ISDBANK    E DSSDBANKPD
      *
     IDSFDY     E DSDSFDY
      *
     IDSFRAC      DS
     I                                        1   6 DSFCNU
     I                                        7   9 DSFCCY
     I                                       10  19 DSFACO
     I                                       20  21 DSFACS
     I                                       22  24 DSFBRC
      *
     IDSTOAC      DS
     I                                        1   6 DSTCNU
     I                                        7   9 DSTCCY
     I                                       10  19 DSTACO
     I                                       20  21 DSTACS
     I                                       22  24 DSTBRC
      *
     IDSKITE      DS                             90
     I                                        1   3 KBRCA
     I                                        4   9 KCNUM
     I                                       10  12 KCCY
     I                                       13  220KACOD
     I                                       23  240KACSQ
      *
     I            DS
     I                                        1   50STOR
     I                                        1   5 WKSTOR
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           P@PRCD  4
     C                     PARM           P@RFNO 10
     C           DSFRAC    PARM           P@FRAC 24
     C           DSTOAC    PARM           P@TOAC 24
     C                     PARM           P@RTCD  1
      *
     C                     EXSR SRINIT
      *
     C           P@PRCD    IFEQ '*LR '
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     EXSR XSIV01
     C                     EXSR XMSTOR
      *
     C                     RETRN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMSTOR - Maintain RE Standing Orders Detail                  *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:     XSAUDT                                            *
      *                                                               *
      *****************************************************************
      *
     C           XMSTOR    BEGSR
      *
     C           KSTOR     SETLLSTANDSF6
     C           KSTOR     READESTANDSF6               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'001'     DBASE
     C                     MOVEL'STANDL6 'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Delete and Write Records until End of File for Selected Account
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETSTANDSF6               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'002'     DBASE
     C                     MOVEL'STANDL6 'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELDSTBRC    BRCA
     C                     MOVELDSTBRC    DRBR
     C                     MOVELDSTCNU    DCUS
     C                     MOVELDSTCCY    CCY
     C                     MOVELDSTACO    DACD
     C                     MOVELDSTACS    DACS
      *
     C                     MOVE *BLANKS   MBSI                                             BUG27417B
     C           DRBR      IFEQ CBRC                                                       BUG27417B
     C                     MOVE 'N'       MBSI                                             BUG27417B
     C                     ELSE                                                            BUG27417B
     C                     MOVE 'Y'       MBSI                                             BUG27417B
     C                     ENDIF                                                           BUG27417B
      *                                                                                    BUG27417B
     C                     WRITESTANDSF6               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'003'     DBASE
     C                     MOVEL'STANDL6 'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARGLAUTFD0
     C                     MOVEL'STANDL6 'AULFIL
     C                     EXSR XSAUDT
      *
     C           KSTOR     READESTANDSF6               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'004'     DBASE
     C                     MOVEL'STANDL6 'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDDO
      *
     C           KSTOR     SETLLSTANDSF7
     C           KSTOR     READESTANDSF7               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'005'     DBASE
     C                     MOVEL'STANDL7 'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETSTANDSF7               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'006'     DBASE
     C                     MOVEL'STANDL7 'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELDSTBRC    CBRC
     C                     MOVELDSTCNU    CCUS
     C                     MOVELDSTCCY    CCY
     C                     MOVELDSTACO    CSCA
     C                     MOVELDSTACS    CACD
      *
     C                     MOVE *BLANKS   MBSI                                             BUG27417B
     C           DRBR      IFEQ CBRC                                                       BUG27417B
     C                     MOVE 'N'       MBSI                                             BUG27417B
     C                     ELSE                                                            BUG27417B
     C                     MOVE 'Y'       MBSI                                             BUG27417B
     C                     ENDIF                                                           BUG27417B
      *                                                                                    BUG27417B
     C                     WRITESTANDSF7               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'007'     DBASE
     C                     MOVEL'STANDL7 'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARGLAUTFD0
     C                     MOVEL'STANDL7 'AULFIL
     C                     EXSR XSAUDT
      *
     C           KSTOR     READESTANDSF7               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'008'     DBASE
     C                     MOVEL'STANDL7 'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSAUDT - Set Audit records                                   *
      *                                                               *
      *  Called By: XMSTOR                                            *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *****************************************************************
      *
     C           XSAUDT    BEGSR
      *
     C                     MOVELP@RFNO    AURFNO
     C                     MOVEL'STANDSB 'AUFILE
     C                     MOVELDSFBRC    AUFBRC
     C                     MOVELDSFCNU    AUFCNU
     C                     MOVELDSFACO    AUFACO
     C                     MOVELDSFACS    AUFACS
     C                     MOVELDSTBRC    AUTBRC
     C                     MOVELDSTCNU    AUTCNU
     C                     MOVELDSTACO    AUTACO
     C                     MOVELDSTACS    AUTACS
     C           WKSTOR    CAT  PNAR:1    AUTEXT 50 P
     C                     MOVELWKDFMT    AUCRTD
     C                     WRITEGLAUTFD0               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'009'     DBASE
     C                     MOVEL'GLAUTFPD'DBFILE
     C                     MOVELAURFNO    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSIV01 - Set up initial values                               *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *****************************************************************
      *
     C           XSIV01    BEGSR
      *
     C                     MOVELDSFBRC    KBRCA
     C                     MOVELDSFCNU    KCNUM
     C                     MOVELDSFCCY    KCCY
     C                     MOVELDSFACO    KACOD
     C                     MOVELDSFACS    KACSQ
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Initialise and define fields                        *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Retrieve Run Date.
      *
     C                     CALL 'AOBANKR0'             9090
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If return with an error (LR seton in called program) then
      ** process for database error.
      *
     C           *IN90     IFEQ '1'
     C********** @RTCD     OREQ '*ERROR*'                                                   BUG27769
     C           @RTCD     ORNE *BLANKS                                                     BUG27769
     C           *LOCK     IN   LDA
     C                     MOVEL'AOBANKR0'DBFILE
     C                     MOVEL'*FIRST'  DBKEY
     C                     Z-ADD010       DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBJMRDT    WKDFMT  9
     C                     MOVE BJMRDT    WKYEAR  20
      *
     C           WKYEAR    IFLT 72
     C           WKYEAR    ADD  2000      WKCCYY  40
     C                     ELSE
     C           WKYEAR    ADD  1900      WKCCYY
     C                     ENDIF
      *
     C                     MOVE WKCCYY    WKDFMT  9
      *
     C           KSTOR     KLIST
     C                     KFLD           KBRCA
     C                     KFLD           KCNUM
     C                     KFLD           KCCY
     C                     KFLD           KACOD
     C                     KFLD           KACSQ
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     MOVE 'F'       P@RTCD
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2010
