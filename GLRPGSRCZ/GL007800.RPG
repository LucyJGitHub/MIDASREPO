     H        1
      *================================================================
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Account Transfer Update INPAYDD')             *
      *================================================================
      *                                                               *
      *  Midas   -  General Ledger Module                             *
      *                                                               *
      *  GL007800 - Midas GL Account Transfer Update INPAYDD          *
      *                                                               *
      *  Function:  This program updates the Midas FT Incoming        *
      *             Payments Dtls File, INPAYDD with the new          *
      *             account detail from the A/c Transfer Selected     *
      *             Accounts File, GLSLACPD.                          *
      *             It will also write an audit trail record for each *
      *             update of INPAYDD.                                *
      *                                                               *
      *  Called by: GL003600                                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. CFT120             Date 28Sep12               *
      *  Prev Amend No. BUG27769           Date 09Jun10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFT120 - FT IN/OP - Charges to DR of Account Currency        *
      *           (Recompile)                                         *
      *  BUG27769 - Incorrect error code                              *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *================================================================
      *  Function of Indicators                                       *
      *                                                               *
      *  01 - 19      Input Control.                                  *
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)      *
      *   20          Record Not Found                                *
      *   21          File Error Occurred                             *
      *   22          Start/End Of File Reached                       *
      *   25          Lookup/Scan.                                    *
      *  30 - 39      Program Work Indicators                         *
      *  40 - 49      Output Control.                                 *
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)    *
      *  90 - 99      Error Control.                                  *
      *                                                               *
      *****************************************************************
      *
     FINPAYL6 UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
     FINPAYL9 UF  E           K        DISK         KINFSR *PSSR A
     F            INPAYDDF                          KRENAMEINPAYF9
     F                                              KCOMIT
     FINPAYL7 UF  E           K        DISK         KINFSR *PSSR A
     F            INPAYDDF                          KRENAMEINPAYF7
     F                                              KCOMIT
     FINPAYL8 UF  E           K        DISK         KINFSR *PSSR A
     F            INPAYDDF                          KRENAMEINPAYF8
     F                                              KCOMIT
     FGLAUTFPDO   E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IDSFDY     E DSDSFDY
      *
      ** Definition: First DS for Access Programs, Short Data Structure
      *
     ISDBANK    E DSSDBANKPD
      *
      ** Definition: Bank Details Accessed Via Access Program.
      *
     IDSFRAC      DS
     I                                        1   6 DSFCNU
     I                                        7   9 DSFCCY
     I                                       10  19 DSFACO
     I                                       20  21 DSFACS
     I                                       22  24 DSFBRC
     IDSTOAC      DS
     I                                        1   6 DSTCNU
     I                                        7   9 DSTCCY
     I                                       10  19 DSTACO
     I                                       20  21 DSTACS
     I                                       22  24 DSTBRC
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           P@PRCD  4
     C                     PARM           P@RFNO 10
     C           DSFRAC    PARM           P@FRAC 24
     C           DSTOAC    PARM           P@TOAC 24
     C                     PARM           P@BCCY  3
     C                     PARM           P@RTCD  1
      *
     C                     EXSR SRINIT
      *
     C           P@PRCD    IFEQ '*LR '
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     EXSR XSIV01
     C                     EXSR XMINP0
     C                     EXSR XMINP1
     C                     EXSR XMINP2
      *
     C           P@BCCY    IFEQ DSFCCY
     C                     EXSR XMINP3
     C                     ENDIF
      *
     C                     RETRN
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMINP0 - Maintain Midas FT Incoming Payments Dtls, L0        *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           XMINP0    BEGSR
      *
     C           KINP0     SETLLINPAYL6
     C           KINP0     READEINPAYL6                2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'002'     DBASE
     C                     MOVEL'INPAYL6 'DBFILE
     C                     MOVELKSND1     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETINPAYDDF               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'003'     DBASE
     C                     MOVEL'DEALPPL0'DBFILE
     C                     MOVELKSND1     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARSND1
     C                     MOVELWKSND1    SND1
     C                     WRITEINPAYDDF               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'004'     DBASE
     C                     MOVEL'INPAYL6 'DBFILE
     C                     MOVELKSND1     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARGLAUTFD0
     C                     MOVEL'INPAYL6 'AULFIL
     C                     EXSR XSAUDT
      *
     C           KINP0     READEINPAYL6                2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'005'     DBASE
     C                     MOVEL'INPAYL6 'DBFILE
     C                     MOVELKSND1     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMINP1 - XMINP1in Midas FT Incoming Payments Dtls, L1        *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: XSAUDT                                                *
      *                                                               *
      *****************************************************************
     C           XMINP1    BEGSR
      *
     C           KINP1     SETLLINPAYL9
     C           KINP1     READEINPAYL9                2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'006'     DBASE
     C                     MOVEL'INPAYL9 'DBFILE
     C                     MOVELKACBK     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETINPAYF9                21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'007'     DBASE
     C                     MOVEL'INPAYL9 'DBFILE
     C                     MOVELKACBK     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARACBK
     C                     CLEARPCCY
     C                     CLEARACBB
     C                     MOVELWKACBK    ACBK
     C                     MOVELDSTCCY    PCCY
     C                     MOVELDSTBRC    ACBB
     C                     WRITEINPAYF9                21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'008'     DBASE
     C                     MOVEL'INPAYL9 'DBFILE
     C                     MOVELKACBK     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARGLAUTFD0
     C                     MOVEL'INPAYL9 'AULFIL
     C                     EXSR XSAUDT
      *
     C           KINP1     READEINPAYL9                2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'009'     DBASE
     C                     MOVEL'INPAYL9 'DBFILE
     C                     MOVELKACBK     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMINP2 - Maintain Midas FT Incoming Payments Dtls, L2        *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: XSAUDT                                                *
      *                                                               *
      *****************************************************************
     C           XMINP2    BEGSR
      *
     C           KINP2     SETLLINPAYL7
     C           KINP2     READEINPAYL7                2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'010'     DBASE
     C                     MOVEL'INPAYL7 'DBFILE
     C                     MOVELKBNC1     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETINPAYF7                21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'011'     DBASE
     C                     MOVEL'INPAYL7 'DBFILE
     C                     MOVELKBNC1     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARBNC1
     C                     CLEARPCCY
     C                     MOVELWKBNC1    BNC1
     C                     MOVELDSTCCY    PCCY
     C                     WRITEINPAYF7                21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'012'     DBASE
     C                     MOVEL'INPAYL7 'DBFILE
     C                     MOVELKBNC1     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARGLAUTFD0
     C                     MOVEL'INPAYL7 'AULFIL
     C                     EXSR XSAUDT
      *
     C           KINP2     READEINPAYL7                2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'013'     DBASE
     C                     MOVEL'INPAYL7 'DBFILE
     C                     MOVELKBNC1     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMINP3- Maintain Midas FT Incoming Payments Dtls, L3         *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: XSAUDT                                                *
      *                                                               *
      *****************************************************************
     C           XMINP3    BEGSR
      *
     C           KINP3     SETLLINPAYL8
     C           KINP3     READEINPAYL8                2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'014'     DBASE
     C                     MOVEL'INPAYL8 'DBFILE
     C                     MOVELKCDRO     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETINPAYF8                21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'015'     DBASE
     C                     MOVEL'INPAYL8 'DBFILE
     C                     MOVELKCDRO     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARCDRO
     C                     CLEARCDBR
     C                     MOVELWKCDRO    CDRO
     C                     MOVELDSTBRC    CDBR
     C                     WRITEINPAYF8                21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'016'     DBASE
     C                     MOVEL'INPAYL8 'DBFILE
     C                     MOVELKCDRO     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARGLAUTFD0
     C                     MOVEL'INPAYL8 'AULFIL
     C                     EXSR XSAUDT
      *
     C           KINP3     READEINPAYL8                2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'017'     DBASE
     C                     MOVEL'INPAYL8 'DBFILE
     C                     MOVELKCDRO     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSAUDT -  Set Audit records.                                 *
      *                                                               *
      *  Called By: XMINP0, XMINP1, XMINP2, XMINP3                    *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           XSAUDT    BEGSR
      *
      ** Set fields for audit file and write a audit record.
      *
     C                     MOVELP@RFNO    AURFNO
     C                     MOVEL'INPAYDD 'AUFILE
     C                     MOVELDSFBRC    AUFBRC
     C                     MOVELDSFCNU    AUFCNU
     C                     MOVELDSFACO    AUFACO
     C                     MOVELDSFACS    AUFACS
     C                     MOVELDSTBRC    AUTBRC
     C                     MOVELDSTCNU    AUTCNU
     C                     MOVELDSTACO    AUTACO
     C                     MOVELDSTACS    AUTACS
     C                     MOVELPREF      WKPREF 15
     C                     MOVELSMAM      WKSMAM 13
     C                     MOVELPYAM      WKPYAM 13
     C                     MOVELPVDT      WKPVDT 13
     C           WKPREF    CAT  WKSMAM:1  AUTEXT
     C                     CAT  WKPYAM:1  AUTEXT
     C                     CAT  WKPVDT:1  AUTEXT 50 P
     C                     MOVELWKDFMT    AUCRTD
     C                     WRITEGLAUTFD0               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'001'     DBASE
     C                     MOVEL'GLAUTFPD'DBFILE
     C                     MOVELAURFNO    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSIV01 -  Set Up Initial Values.                             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           XSIV01    BEGSR
      *
      ** Set up key for INPAYL6
      *
     C                     MOVEL*BLANKS   KSND1  35
     C           DSFCNU    CAT  DSFCCY:0  KSND1
     C                     CAT  DSFACO:0  KSND1
     C                     CAT  DSFACS:0  KSND1
     C                     CAT  DSFBRC:0  KSND1
      *
      ** Set up key for INPAYL9
      *
     C           DSFCNU    CAT  DSFACO:0  KACBK  18
     C                     CAT  DSFACS:0  KACBK
     C                     MOVELDSFCCY    KPCCY   3
     C                     MOVELDSFBRC    KACBB   3
      *
      ** Set up key for INPAYL7
      *
     C                     MOVEL*BLANKS   KBNC1  35
     C           DSFCNU    CAT  DSFACO:0  KBNC1
     C                     CAT  DSFACS:0  KBNC1
     C                     CAT  DSFBRC:0  KBNC1
     C                     MOVELDSFCCY    KPCCY2  3
      *
      ** Set up key for INPAYL8
      *
     C           DSFCNU    CAT  DSFACO:0  WKKCDR 18
     C                     CAT  DSFACS:0  WKKCDR
     C                     MOVELWKKCDR    KCDRO  18
     C                     MOVELDSFBRC    KCDBR   3
      *
      ** Set up 'To' values (INPAYL6)
      *
     C                     MOVEL*BLANKS   WKSND1 35
     C           DSTCNU    CAT  DSTCCY:0  WKSND1
     C                     CAT  DSTACO:0  WKSND1
     C                     CAT  DSTACS:0  WKSND1
     C                     CAT  DSTBRC:0  WKSND1
      *
      ** Set up 'To' values (INPAYL9)
      *
     C           DSTCNU    CAT  DSTACO:0  WKACBK 18
     C                     CAT  DSTACS:0  WKACBK
     C                     MOVELDSTCCY    WKPCCY  3
     C                     MOVELDSTBRC    WKACBB  3
      *
      ** Set up 'To' values (INPAYL7)
      *
     C                     MOVEL*BLANKS   WKBNC1 35
     C           DSTCNU    CAT  DSTACO:0  WKBNC1 35
     C                     CAT  DSTACS:0  WKBNC1
     C                     CAT  DSTBRC:0  WKBNC1
     C                     MOVELDSFCCY    WKPCY2  3
      *
      ** Set up 'To' values (INPAYL8)
      *
     C           DSTCNU    CAT  DSTACO:0  WKCDRO 18
     C                     CAT  DSTACS:0  WKCDRO
     C                     MOVELDSTBRC    WKCDBR  3
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT -  Initialise and define fields                       *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      ** Initialise parameters, work fields, etc.
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Retrieve Run Date.
      *
     C                     CALL 'AOBANKR0'             9090
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If return with an error (LR seton in called program) then
      ** process for database error.
      *
     C           *IN90     IFEQ '1'
     C********** @RTCD     OREQ '*ERROR*'                                                   BUG27769
     C           @RTCD     ORNE *BLANKS                                                     BUG27769
     C           *LOCK     IN   LDA
     C                     MOVEL'AOBANKR0'DBFILE
     C                     MOVEL'*FIRST'  DBKEY
     C                     Z-ADD018       DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBJMRDT    WKDFMT  9
     C                     MOVE BJMRDT    WKYEAR  20
      *
     C           WKYEAR    IFLT 72
     C           WKYEAR    ADD  2000      WKCCYY  40
     C                     ELSE
     C           WKYEAR    ADD  1900      WKCCYY
     C                     ENDIF
      *
     C                     MOVE WKCCYY    WKDFMT
     C                     MOVE *BLANKS   KSND1
     C                     MOVE *BLANKS   KACBK
     C                     MOVE *BLANKS   KBNC1
     C                     MOVE *BLANKS   WKKCDR
     C                     MOVE *BLANKS   WKSND1
     C                     MOVE *BLANKS   WKACBK
     C                     MOVE *BLANKS   WKBNC1
     C                     MOVE *BLANKS   WKCDRO
      *
     C           KINP0     KLIST
     C                     KFLD           KSND1
      *
     C           KINP1     KLIST
     C                     KFLD           KACBK
     C                     KFLD           KPCCY
     C                     KFLD           KACBB
      *
     C           KINP2     KLIST
     C                     KFLD           KBNC1
     C                     KFLD           KPCCY2
      *
     C           KINP3     KLIST
     C                     KFLD           KCDRO
     C                     KFLD           KCDBR
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *  Called By: (**calling routines**)                            *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
      ** Check to see that *PSSR has not been called yet
      *
     C                     MOVE 'F'       P@RTCD
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDSR
      *****************************************************************
** CPY@
(c) Misys International Banking Systems Ltd. 2010
