     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Account Transfer Update REIACD')              *
      *****************************************************************
      *                                                               *
      *  Midas   -  General Ledger Module                             *
      *                                                               *
      *  GL008700 - Midas GL Account Transfer Update REIACD           *
      *                                                               *
      *  Function:  This program updates the Midas RE Retail Int. and *
      *             charges detail, REIACD with the new account       *
      *             detail from the Account Transfer Selected         *
      *             Accounts File, GLSLACPD.  It also will write an   *
      *             audit trail record for each update of REIACD.     *
      *                                                               *
      *  Called by: GL003600                                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. CER049             Date 06Dec10               *
      *  Prev Amend No. BUG27769           Date 09Jun10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER049 - Stamp Tax (Recompile)                               *
      *  BUG27769 - Incorrect error code                              *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
      *  Function of Indicators                                       *
      *                                                               *
      *  01 - 19      Input Control.                                  *
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)      *
      *   20          Record Not Found                                *
      *   21          File Error Occurred                             *
      *   22          Start/End Of File Reached                       *
      *   25          Lookup/Scan.                                    *
      *  30 - 39      Program Work Indicators                         *
      *  40 - 49      Output Control.                                 *
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)    *
      *  90 - 99      Error Control.                                  *
      *                                                               *
      *****************************************************************
      *
     FIACBR   UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
     FREIACDL0UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
     F            REIACDF                           KRENAMEREIACDF0
     FREIACDL1UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
     F            REIACDF                           KRENAMEREIACDF1
     FREIACDL2UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
     F            REIACDF                           KRENAMEREIACDF2
     FGLAUTFPDO   E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IDSFDY     E DSDSFDY
      *
      ** Definition: First DS for Access Programs, Short Data Structure
      *
     ISDBANK    E DSSDBANKPD
      *
      ** Definition: Bank Details Accessed Via Access Program.
      *
     IDSFRAC      DS
     I                                        1   6 DSFCNU
     I                                        7   9 DSFCCY
     I                                       10  19 DSFACO
     I                                       20  21 DSFACS
     I                                       22  24 DSFBRC
      *
     IDSTOAC      DS
     I                                        1   6 DSTCNU
     I                                        7   9 DSTCCY
     I                                       10  19 DSTACO
     I                                       20  21 DSTACS
     I                                       22  24 DSTBRC
      *
     IDSKITE      DS                             90
     I                                        1   3 KBRCA
     I                                        4   9 KCNUM
     I                                       10  12 KCCY
     I                                       13  220KACOD
     I                                       23  240KACSQ
     IDSKIT0      DS
     I                                        1  21 KDACP
     I                                       22  24 KDRBR
     IDSKIT1      DS
     I                                        1  21 KCACP
     I                                       22  24 KCRBR
     IDSKIT2      DS
     I                                        1  21 KCHAC
     I                                       22  24 KCHBR
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           P@PRCD  4
     C                     PARM           P@RFNO 10
     C           DSFRAC    PARM           P@FRAC 24
     C           DSTOAC    PARM           P@TOAC 24
     C                     PARM           P@RTCD  1
      *
     C                     EXSR SRINIT
      *
     C           P@PRCD    IFEQ '*LR '
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     EXSR XSIV01
     C                     EXSR XMIACD
     C                     RETRN
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMIACD - Maintain REIACD records                             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: XSAUDT                                                *
      *                                                               *
      *****************************************************************
     C           XMIACD    BEGSR
      *
     C           KIACB     SETLLIACBR
     C           KIACB     READEIACBR                  2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'001'     DBASE
     C                     MOVEL'IACBR   'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETREIACDF                21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'002'     DBASE
     C                     MOVEL'IACBR   'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARBRCA
     C                     CLEARCNUM
     C                     CLEARCCY
     C                     CLEARACOD
     C                     CLEARACSQ
      *
     C                     MOVELDSTBRC    BRCA
     C                     MOVELDSTCNU    CNUM
     C                     MOVELDSTCCY    CCY
     C                     MOVELDSTACO    ACOD
     C                     MOVELDSTACS    ACSQ
      *
     C                     WRITEREIACDF                21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'003'     DBASE
     C                     MOVEL'IACBR   'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Set fields for audit file and write an audit record.
      *
     C                     CLEARGLAUTFD0
     C                     MOVEL'IACBR   'AULFIL
     C                     EXSR XSAUDT
      *
      ** Retrieve Midas RE Retail Int. and charges detail (IACBR)
      *
     C           KIACB     READEIACBR                  2122
     C                     ENDDO
      *
      ** Position File (REHISPL0)
      *
     C           KIAC0     SETLLREIACDL0
     C           KIAC0     READEREIACDL0               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'004'     DBASE
     C                     MOVEL'REIACDL0'DBFILE
     C                     MOVELDSKIT0    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETREIACDF0               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'005'     DBASE
     C                     MOVEL'REIACDL0'DBFILE
     C                     MOVELDSKIT0    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARWKCNUM
     C                     CLEARWKCCY
     C                     CLEARWKACOD
     C                     CLEARWKACSQ
     C                     CLEARDACP
     C                     CLEARDRBR
      *
     C                     MOVELDSTCNU    WKCNUM
     C                     MOVELDSTCCY    WKCCY
     C                     MOVELDSTACO    WKACOD
     C                     MOVELDSTACS    WKACSQ
     C           WKCNUM    CAT  WKCCY:0   DACP
     C                     CAT  WKACOD:0  DACP
     C                     CAT  WKACSQ:0  DACP
     C                     MOVELDSTBRC    DRBR
      *
     C                     WRITEREIACDF0               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'006'     DBASE
     C                     MOVEL'REIACDL0'DBFILE
     C                     MOVELDSKIT0    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Set fields for audit file and write a audit record.
      *
     C                     CLEARGLAUTFD0
     C                     MOVEL'REIACDL0'AULFIL
     C                     EXSR XSAUDT
      *
      ** Retrieve Midas RE Retail Int. and charges detail (REIACDL0)
      *
     C           KIAC0     READEREIACDL0               2122
     C                     ENDDO
      *
      ** Position File (REHISPL1)
      *
     C           KIAC1     SETLLREIACDL1
     C           KIAC1     READEREIACDL1               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'007'     DBASE
     C                     MOVEL'REIACDL1'DBFILE
     C                     MOVELDSKIT1    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETREIACDF1               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'008'     DBASE
     C                     MOVEL'REIACDL1'DBFILE
     C                     MOVELDSKIT1    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARWKCNUM
     C                     CLEARWKCCY
     C                     CLEARWKACOD
     C                     CLEARWKACOD
     C                     CLEARCACP
     C                     CLEARCRBR
      *
     C                     MOVELDSTCNU    WKCNUM
     C                     MOVELDSTCCY    WKCCY
     C                     MOVELDSTACO    WKACOD
     C                     MOVELDSTACS    WKACSQ
     C           WKCNUM    CAT  WKCCY:0   CACP
     C                     CAT  WKACOD:0  CACP
     C                     CAT  WKACSQ:0  CACP
     C                     MOVELDSTBRC    CRBR
      *
     C                     WRITEREIACDF1               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'009'     DBASE
     C                     MOVEL'REIACDL1'DBFILE
     C                     MOVELDSKIT1    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Set fields for audit file and write a audit record.
      *
     C                     CLEARGLAUTFD0
     C                     MOVEL'REIACDL1'AULFIL
     C                     EXSR XSAUDT
      *
      ** Retrieve Midas RE Retail Int. and charges detail (REIACDL1)
      *
     C           KIAC1     READEREIACDL1               2122
     C                     ENDDO
      *
      ** Position File (REHISPL2)
      *
     C           KIAC2     SETLLREIACDL2
     C           KIAC2     READEREIACDL2               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'010'     DBASE
     C                     MOVEL'REIACDL2'DBFILE
     C                     MOVELDSKIT2    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETREIACDF2               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'011'     DBASE
     C                     MOVEL'REIACDL2'DBFILE
     C                     MOVELDSKIT2    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLEARWKCNUM
     C                     CLEARWKCCY
     C                     CLEARWKACOD
     C                     CLEARWKACSQ
     C                     CLEARCHAC
     C                     CLEARCHBR
      *
     C                     MOVELDSTCNU    WKCNUM
     C                     MOVELDSTCCY    WKCCY
     C                     MOVELDSTACO    WKACOD
     C                     MOVELDSTACS    WKACSQ
     C           WKCNUM    CAT  WKCCY:0   CHAC
     C                     CAT  WKACOD:0  CHAC
     C                     CAT  WKACSQ:0  CHAC
     C                     MOVELDSTBRC    CHBR
      *
     C                     WRITEREIACDF2               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'012'     DBASE
     C                     MOVEL'REIACDL2'DBFILE
     C                     MOVELDSKIT2    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Set fields for audit file and write a audit record.
      *
     C                     CLEARGLAUTFD0
     C                     MOVEL'REIACDL2'AULFIL
     C                     EXSR XSAUDT
      *
      ** Retrieve Midas RE Retail Int. and charges detail (REIACDL2)
      *
     C           KIAC2     READEREIACDL2               2122
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSAUDT -  Set Audit records.                                 *
      *                                                               *
      *  Called By: XMIACD                                            *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           XSAUDT    BEGSR
      *
      ** Set Fields.
      *
     C                     MOVELP@RFNO    AURFNO
     C                     MOVEL'REIACD  'AUFILE
     C                     MOVELDSFBRC    AUFBRC
     C                     MOVELDSFCNU    AUFCNU
     C                     MOVELDSFACO    AUFACO
     C                     MOVELDSFACS    AUFACS
     C                     MOVELDSTBRC    AUTBRC
     C                     MOVELDSTCNU    AUTCNU
     C                     MOVELDSTACO    AUTACO
     C                     MOVELDSTACS    AUTACS
     C                     MOVELCHGT      WKCHGT  2
     C                     MOVELNRCA      WKNRCA 13
     C                     MOVELRCA       WKRCA  13
     C                     MOVELTCHO      WKTCHO  7
     C           WKCHGT    CAT  WKNRCA:1  AUTEXT
     C                     CAT  WKRCA:1   AUTEXT
     C                     CAT  WKTCHO:1  AUTEXT 50 P
     C                     MOVELWKDFMT    AUCRTD
     C                     WRITEGLAUTFD0               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'013'     DBASE
     C                     MOVEL'GLAUTFPD'DBFILE
     C                     MOVELAURFNO    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSIV01 -  Set Up Initial Values.                             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           XSIV01    BEGSR
      *
      ** Set up key for IACBR
      *
     C                     MOVELDSFBRC    KBRCA
     C                     MOVELDSFCNU    KCNUM
     C                     MOVELDSFCCY    KCCY
     C                     MOVELDSFACO    KACOD
     C                     MOVELDSFACS    KACSQ
      *
      ** Set Work fields
      *
     C                     MOVELDSFCNU    WKCNUM  6
     C                     MOVELDSFCCY    WKCCY   3
     C                     MOVELDSFACO    WKACOD 10
     C                     MOVELDSFACS    WKACSQ  2
      *
      ** Set key for REIACDL0
      *
     C           WKCNUM    CAT  WKCCY:0   KDACP
     C                     CAT  WKACOD:0  KDACP
     C                     CAT  WKACSQ:0  KDACP
     C                     MOVELDSFBRC    KDRBR
      *
      ** Set key for REIACDL1
      *
     C           WKCNUM    CAT  WKCCY:0   KCACP
     C                     CAT  WKACOD:0  KCACP
     C                     CAT  WKACSQ:0  KCACP
     C                     MOVELDSFBRC    KCRBR
      *
      ** Set key for REIACDL2
      *
     C           WKCNUM    CAT  WKCCY:0   KCHAC
     C                     CAT  WKACOD:0  KCHAC
     C                     CAT  WKACSQ:0  KCHAC
     C                     MOVELDSFBRC    KCHBR
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT -  Initialise and define fields                       *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      ** Initialise parameters, work fields, etc.
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Retrieve Run Date.
      *
     C                     CALL 'AOBANKR0'             9090
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If return with an error (LR seton in called program) then
      ** process for database error.
      *
     C           *IN90     IFEQ '1'
     C********** @RTCD     OREQ '*ERROR*'                                                   BUG27769
     C           @RTCD     ORNE *BLANKS                                                     BUG27769
     C           *LOCK     IN   LDA
     C                     MOVEL'AOBANKR0'DBFILE
     C                     MOVEL'*FIRST'  DBKEY
     C                     Z-ADD014       DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBJMRDT    WKDFMT  9
     C                     MOVE BJMRDT    WKYEAR  20
      *
     C           WKYEAR    IFLT 72
     C           WKYEAR    ADD  2000      WKCCYY  40
     C                     ELSE
     C           WKYEAR    ADD  1900      WKCCYY
     C                     ENDIF
      *
     C                     MOVE WKCCYY    WKDFMT
     C                     MOVE *BLANKS   DACP
     C                     MOVE *BLANKS   CACP
     C                     MOVE *BLANKS   CHAC
     C                     MOVE *BLANKS   KDACP
     C                     MOVE *BLANKS   KCACP
     C                     MOVE *BLANKS   KCHAC
      *
     C           KIACB     KLIST
     C                     KFLD           KBRCA
     C                     KFLD           KCNUM
     C                     KFLD           KCCY
     C                     KFLD           KACOD
     C                     KFLD           KACSQ
      *
     C           KIAC0     KLIST
     C                     KFLD           KDACP
     C                     KFLD           KDRBR
      *
     C           KIAC1     KLIST
     C                     KFLD           KCACP
     C                     KFLD           KCRBR
      *
     C           KIAC2     KLIST
     C                     KFLD           KCHAC
     C                     KFLD           KCHBR
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *  Called By: (**calling routines**)                            *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
      ** Check to see that *PSSR has not been called yet
      *
     C                     MOVE 'F'       P@RTCD
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
** CPY@
(c) Misys International Banking Systems Ltd. 2010
