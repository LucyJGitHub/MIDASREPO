     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Account postings file initialisation')        *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL0091 - Account Postings Initialisation                     *
      *                                                               *
      *  Function:  This utility program will setup the new fields    *
      *  (I/C)      in PF/APOSTPD and PF/GLACPHPD.                    *
      *                                                               *
      *  Called By:                                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CGL020             Date 12Dec02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CGL007  *Create    Date 26Mar99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL020 - Midas Compliance Watch - Additional A/C Postings    *
      *           Information (Recompile)                             *
      *  CGL007 - Account Postings Enquiry                            *
      *                                                               *
      *****************************************************************
     FAPOSTPD UF  E                    DISK         KINFSR *PSSR
      *
     FGLACPHPDUF  E                    DISK         KINFSR *PSSR
     F            APOSTPDF                          KRENAMEGLACPHDF
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  30 - End of File for PF/APOSTPD                              *
      *  31 - End of File for PF/GLACPHPD                             *
      *  32 - Update file                                             *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRP002 - Program Initialisation                              *
      *  SRP003 - Process PF/APOSTPD and PF/GLACPHPD                  *
      *  SRP004 - Process to update fields                            *
      *                                                               *
      *  *PSSR  - Program exception error routine                     *
      *  ZALIGN - Validate input as numeric                           *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /COPY ZSRSRC,ZALIGNZ1
      /EJECT
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
     I/COPY WNCPYSRC,GLH00477
     IDSFDY     E DSDSFDY
      ** Short data structure for access programs (200 long)
      *
     I/COPY WNCPYSRC,GLH00478
      /EJECT
      *****************************************************************
      *                                                               *
      *  P001 - Main Processing                                       *
      *                                                               *
      *  Called by: None                                              *
      *                                                               *
      *  Calls:     SRP002 - Program Initialisation                   *
      *             SRP003 - Process PF/APOSTPD and PF/GLACPHPD       *
      *                                                               *
      *****************************************************************
     C                     MOVEACPY@      BIS@   80
      *
     C                     EXSR SRP002
      *
     C           PMODE     IFEQ '1'
     C           PMODE     OREQ '2'
     C                     EXSR SRP003
     C                     ENDIF
      *
     C                     MOVE *ON       *INLR
      ****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRP002 - Program Initialisation                              *
      *                                                               *
      *  Called by: Main Processing                                   *
      *                                                               *
      *  Calls:     AOBANKR0 - Bank ICD Access Object                 *
      *             *PSSR    - Handles Database Error                 *
      *                                                               *
      *****************************************************************
     C           SRP002    BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           PRCDE   7
     C                     PARM           PMODE   1
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Access Bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE           ************
     C                     Z-ADD1         DBASE            * DBERR 01 *
     C                     MOVELPOPTN     DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C/COPY WNCPYSRC,GLH00479
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRP003 - Process PF/APOSTPD and PF/GLACPHPD                  *
      *                                                               *
      *  Called by: Main Processing                                   *
      *                                                               *
      *  Calls:   SRP004 - Process to update fields                   *
      *                                                               *
      *****************************************************************
     C           SRP003    BEGSR
      *
     C                     READ APOSTPDF                 30
      *
     C           *IN30     DOWEQ*OFF
      *
     C                     EXSR SRP004
      *
     C           *IN32     IFEQ *ON
     C                     UPDATAPOSTPDF
     C                     ENDIF
      *
     C                     READ APOSTPDF                 30
      *
     C                     ENDDO
      *
     C                     READ GLACPHDF                 31
      *
     C           *IN31     DOWEQ*OFF
      *
     C                     EXSR SRP004
      *
     C           *IN32     IFEQ *ON
     C                     UPDATGLACPHDF
     C                     ENDIF
      *
     C                     READ GLACPHDF                 31
      *
     C                     ENDDO
      *
     C                     ENDSR
      ****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRP004 - Process to update fields                            *
      *                                                               *
      *  Called by: SRP003 - Process PF/APOSTPD and PF/GLACPHPD       *
      *                                                               *
      *  Calls:     ZALIGN - Validates input if numeric               *
      *                                                               *
      *****************************************************************
     C           SRP004    BEGSR
      *
     C/COPY WNCPYSRC,GLH00480
     C                     MOVE *OFF      *IN32
     C                     MOVEL*BLANKS   ZFIELD
     C/COPY WNCPYSRC,GLH00481
     C                     MOVELSPOS      WSPOS6  6
     C                     MOVE WSPOS6    ZFIELD
     C                     Z-ADD*ZEROS    ZADEC
     C                     Z-ADD6         ZADIG
     C                     EXSR ZALIGN
     C/COPY WNCPYSRC,GLH00482
      *
      ** Processing for batch posting
      *
     C           *IN99     IFEQ *OFF
     C                     MOVE *ON       *IN32
     C                     MOVELSPOS      WSPOS3  3
     C                     MOVELWSPOS3    OTRF
     C                     ELSE
      *
     C                     SELEC
      *
      ** Processing for Future and Option posting
      *
     C           SPOS      WHEQ '  GE-FF'
     C                     MOVELOTRF      OTST
     C                     MOVE *ON       *IN32
      *
      ** Processing for Securities Trading posting
      *
     C           SPOS      WHEQ '  GE-ST'
     C                     MOVE OTTP      WOTTP   2
      *
      ** Depot Movement
      *
     C           WOTTP     IFEQ 'RR'
     C           WOTTP     OREQ 'RP'
     C           WOTTP     OREQ 'PD'
     C           WOTTP     OREQ 'PL'
     C           WOTTP     OREQ 'WI'
     C           WOTTP     OREQ 'WO'
     C           WOTTP     OREQ 'DT'
     C           WOTTP     OREQ 'BB'
     C           WOTTP     OREQ 'BL'
     C                     MOVE *ON       *IN32
     C                     MOVEL'DM'      TRST
     C                     ENDIF
      *
      ** Position settlement
      *
     C           WOTTP     IFEQ 'CP'
     C           WOTTP     OREQ 'DV'
     C           WOTTP     OREQ 'PP'
     C           WOTTP     OREQ 'MP'
     C           WOTTP     OREQ 'MT'
     C           WOTTP     OREQ 'RC'
     C                     MOVE *ON       *IN32
     C                     MOVELWOTTP     OTRF
     C                     MOVEL'PS'      TRST
     C                     ENDIF
      *
      ** Trade
      *
     C           WOTTP     IFEQ 'TP'
     C           WOTTP     OREQ 'TS'
     C                     MOVE *ON       *IN32
     C                     MOVEL'SE'      TRST
     C                     ENDIF
      *
     C                     ENDSL
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Database error subroutine.                           *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     MOVEL'*ERROR*' PRCDE
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      /EJECT
      /COPY ZSRSRC,ZALIGNZ2
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
