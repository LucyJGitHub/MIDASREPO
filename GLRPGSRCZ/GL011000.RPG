     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Account Transfer Update POSETD')
      *****************************************************************
      *                                                               *
      *  Midas   -  General Ledger Module                             *
      *                                                               *
      *  GL011000 - Midas GL Account Transfer Update POSETD           *
      *                                                               *
      *  Function:  This program updates the Midas SE                 *
      *             Position Settlements                              *
      *             file with the new account detail from the Account *
      *             Transfer Selected Accounts File, GLSLACPD.        *
      *             It will also write an audit trail record for each *
      *             update of POSETDL2                                *
      *                                                               *
      *  Called by:                                                   *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. BUG27903           Date 13Jul10               *
      *                 BUG27769           Date 09Jun10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG27903 - Unable to transfer account with setl 01 and 08    *
      *  BUG27769 - Incorrect error code                              *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
      *  Function of Indicators                                       *
      *                                                               *
      *  01 - 19      Input Control.                                  *
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)      *
      *   20          Record Not Found                                *
      *   21          File Error Occurred                             *
      *   22          Start/End Of File Reached                       *
      *   25          Lookup/Scan.                                    *
      *  30 - 39      Program Work Indicators                         *
      *  40 - 49      Output Control.                                 *
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)    *
      *  90 - 99      Error Control.                                  *
      *                                                               *
      *****************************************************************
      *
     FPOSETDL2UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
     F            POSETDF                           KRENAMEPOSEF1
     FPOSETDL3UF  E           K        DISK         KINFSR *PSSR                            BUG27903
     F                                              KCOMIT                                  BUG27903
     F            POSETDF                           KRENAMEPOSEF3                           BUG27903
     FGLAUTFPDO   E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
     FGLSLACL1IF  E           K        DISK         KINFSR *PSSR
      /EJECT
     E                    CPY@    1   1 80
      /COPY ZSRSRC,ZDATE2Z1
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *                                                                                     BUG27903
     INOSTD     E DSNOSTD                         8                                         BUG27903
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     I              '0000000000  '        C         WKZERO
     IDSFRAC      DS
      * Datastructure: From Account Details
     I                                        1   6 DSFCNU
     I                                        7   9 DSFCCY
     I                                       10  19 DSFACO
     I                                       20  21 DSFACS
     I                                       22  24 DSFBRC
      *
     IDSTOAC      DS
     I                                        1   6 DSTCNU
     I                                        7   9 DSTCCY
     I                                       10  19 DSTACO
     I                                       20  21 DSTACS
     I                                       22  24 DSTBRC
      *
     IDSKITE      DS                             90
     I                                        1   3 KCCY
     I                                        4   6 KBRCA
     I                                        7  24 KACCT
      *
     IDSKIT2      DS                             90
     I                                        1   3 KCCY2
     I                                        4   6 KBRCA2
     I                                        7  24 KACCT2
      *
     IDSKIT3      DS                             90
     I                                        1   6 KSFCNU
     I                                        7   9 KSFCCY
     I                                       10  19 KSFACO
     I                                       20  21 KSFACS
     I                                       22  24 KSFBRC
     I                                       25  34 KSRFNO
      *                                                                                     BUG27903
     IWNOST       DS                                                                        BUG27903
      * External Data Area for Nostro Details                                               BUG27903
     I                                        1   3 WWCCY                                   BUG27903
     I                                        4   5 WWNOSN                                  BUG27903
     I                                        6   8 WWBRCH                                  BUG27903
      *
     ISDBANK    E DSSDBANKPD
      * Bank details accessed via access program
      *
     IDSFDY     E DSDSFDY
      * First DS for access programs - Short data structure
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           P@PRCD  4
     C                     PARM           P@RFNO 10
     C           DSFRAC    PARM           P@FRAC 24
     C           DSTOAC    PARM           P@TOAC 24
     C                     PARM           P@RTCD  1
      *
     C                     EXSR SRINIT
      *
     C           P@PRCD    IFEQ '*LR '
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     EXSR XSIV01
     C                     EXSR XMPSV0
      *
      ** Search files again, this time using retail a/c no
      *
     C           DSTBRC    IFNE DSFBRC
     C           KACCT2    ANDNE*BLANKS
     C           KACCT2    ANDNEWKZERO
     C                     EXSR XMPV01
     C                     ENDIF
      *                                                                                     BUG27903
     C           WNOST     IFNE *BLANKS                                                     BUG27903
     C                     EXSR XMPV02                                                      BUG27903
     C                     ENDIF                                                            BUG27903
      *
     C                     RETRN
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMPSV0 - Maintain Midas SE Position Settlements              *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: XSAUDT, XXERR                                         *
      *                                                               *
      *****************************************************************
     C           XMPSV0    BEGSR
      *
     C                     MOVEL'POSETDL2'WKFIL1    P
      *
     C           KSETD     CHAINPOSEF1               2221
     C                     MOVEL'002'     WKERRN  3
     C                     EXSR XXERR
      *
      ** Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETPOSEF1                 21
     C                     MOVEL'003'     WKERRN
     C                     EXSR XXERR
      *
     C                     CLEARPSEB
     C                     CLEARPSEA
     C                     MOVELDSTBRC    PSEB
     C                     MOVELWKACCT    PSEA
      *
     C                     WRITEPOSEF1                 21
     C                     MOVEL'004'     WKERRN
     C                     EXSR XXERR
      *
     C                     CLEARGLAUTFD0
     C                     MOVELWKFIL1    AULFIL
     C                     EXSR XSAUDT
      *
     C           KSETD     READEPOSEF1                 2122
     C                     MOVEL'005'     WKERRN
     C                     EXSR XXERR
      *
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMPV01 - Maintain Midas SE Position Settlements              *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: XSAUDT, XXERR                                         *
      *                                                               *
      *****************************************************************
     C           XMPV01    BEGSR
      *
     C                     MOVEL'POSETDL2'WKFIL1    P
      *
     C           KSETD2    CHAINPOSEF1               2221
     C                     MOVEL'010'     WKERRN
     C                     EXSR XXERR
      *
      ** Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETPOSEF1                 21
     C                     MOVEL'011'     WKERRN
     C                     EXSR XXERR
      *
     C                     CLEARPSEB
     C                     MOVELDSTBRC    PSEB
      *
     C                     WRITEPOSEF1                 21
     C                     MOVEL'012'     WKERRN
     C                     EXSR XXERR
      *
     C                     CLEARGLAUTFD0
     C                     MOVELWKFIL1    AULFIL
     C                     EXSR XSAUDT
      *
     C           KSETD2    READEPOSEF1                 2122
     C                     MOVEL'013'     WKERRN
     C                     EXSR XXERR
      *
     C                     ENDDO
     C                     ENDSR
      *****************************************************************                     BUG27903
      /EJECT                                                                                BUG27903
      *****************************************************************                     BUG27903
      *                                                               *                     BUG27903
      *  XMPV02 - Maintain Midas SE Position Settlements (Nostro)     *                     BUG27903
      *                                                               *                     BUG27903
      *  Called By: Main Processing                                   *                     BUG27903
      *                                                               *                     BUG27903
      *  Calls: XSAUDT, XXERR                                         *                     BUG27903
      *                                                               *                     BUG27903
      *****************************************************************                     BUG27903
     C           XMPV02    BEGSR                                                            BUG27903
      *                                                                                     BUG27903
     C                     MOVE *BLANKS   KDLIN3 18                                         BUG27903
     C                     MOVELWWNOSN    KDLIN3                                            BUG27903
     C                     MOVELWNOST     WNOST2  5                                         BUG27903
      *                                                                                     BUG27903
     C                     MOVEL'POSETDL3'WKFIL1    P                                       BUG27903
      *                                                                                     BUG27903
     C           KSETD3    CHAINPOSEF3               2221                                   BUG27903
     C                     MOVEL'014'     WKERRN                                            BUG27903
     C                     EXSR XXERR                                                       BUG27903
      *                                                                                     BUG27903
      ** Update Records until End of File for Selected Account.                             BUG27903
      *                                                                                     BUG27903
     C           *IN22     DOWEQ*OFF                                                        BUG27903
      *                                                                                     BUG27903
     C                     CLEARPSEB                                                        BUG27903
     C                     MOVELWWBRCH    PSEB                                              BUG27903
      *                                                                                     BUG27903
     C                     UPDATPOSEF3                 21                                   BUG27903
     C                     MOVEL'015'     WKERRN                                            BUG27903
     C                     EXSR XXERR                                                       BUG27903
      *                                                                                     BUG27903
     C                     CLEARGLAUTFD0                                                    BUG27903
     C                     MOVELWKFIL1    AULFIL                                            BUG27903
     C                     EXSR XSAUDT                                                      BUG27903
      *                                                                                     BUG27903
     C           KSETD3    READEPOSEF3                 2122                                 BUG27903
     C                     MOVEL'016'     WKERRN                                            BUG27903
     C                     EXSR XXERR                                                       BUG27903
      *                                                                                     BUG27903
     C                     ENDDO                                                            BUG27903
     C                     ENDSR                                                            BUG27903
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSAUDT -  Set Audit records.                                 *
      *                                                               *
      *  Called By: XMPSV0, XMPV01                                    *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           XSAUDT    BEGSR
      *
      ** Set fields for audit file and write a audit record.
      *
     C                     MOVELP@RFNO    AURFNO
     C                     MOVEL'POSETD  'AUFILE
     C                     MOVELDSFBRC    AUFBRC
     C                     MOVELDSFCNU    AUFCNU
     C                     MOVELDSFACO    AUFACO
     C                     MOVELDSFACS    AUFACS
     C                     MOVELDSTBRC    AUTBRC
     C                     MOVELDSTCNU    AUTCNU
     C                     MOVELDSTACO    AUTACO
     C                     MOVELDSTACS    AUTACS
     C                     MOVELPCPY      WKPCPY  6
     C                     Z-ADDPDTG      ZDAYNO
     C                     EXSR ZDATE2
     C           PSSH      CAT  WKPCPY:1  AUTEXT 50 P
     C                     MOVELZDATE     WKDATE  6
     C                     CAT  WKDATE:1  AUTEXT
     C                     MOVELWKDFMT    AUCRTD
     C                     WRITEGLAUTFD0               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'001'     DBASE
     C                     MOVEL'GLAUTFPD'DBFILE
     C                     MOVELAURFNO    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSIV01 -  Set Up Initial Values.                             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           XSIV01    BEGSR
      *
      ** Set up 'From' values
      *
     C                     MOVELDSFCCY    KCCY
     C                     MOVELDSFBRC    KBRCA
     C           DSFCNU    CAT  DSFACO:0  KACCT
     C                     CAT  DSFACS:0  KACCT
      *
      ** Set up key for GLSLACL1
      *
     C                     MOVELDSFCNU    KSFCNU
     C                     MOVELDSFCCY    KSFCCY
     C                     MOVELDSFACO    KSFACO
     C                     MOVELDSFACS    KSFACS
     C                     MOVELDSFBRC    KSFBRC
     C                     MOVELP@RFNO    KSRFNO
      *
      ** Set up retail a/c number key
      ** Access the Transfer file for the retail number
      *
     C           KSLAC     CHAINGLSLACL1             3031
      *
     C           *IN30     IFEQ *ON
     C           *IN31     OREQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'GLSLACL1'DBFILE
     C                     MOVELDSKIT3    DBKEY
     C                     MOVEL'034'     DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELDSFCCY    KCCY2
     C                     MOVELDSFBRC    KBRCA2
     C                     MOVELSLACNO    KACCT2
      *
      ** Set up 'To' values
      *
     C           DSTCNU    CAT  DSTACO:0  WKACCT 18
     C                     CAT  DSTACS:0  WKACCT
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XXERR -  Database Error processing                           *
      *                                                               *
      *  Called By:  XMPSV0, XMPV01                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           XXERR     BEGSR
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVELWKERRN    DBASE
     C                     MOVELWKFIL1    DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT -  Initialise and define fields                       *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      ** Initialise parameters, work fields, etc.
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           NOSTD                                             BUG27903
      *                                                                                     BUG27903
      ** Check nostro account changes                                                       BUG27903
      *                                                                                     BUG27903
     C                     MOVE *BLANKS   WNOST                                             BUG27903
     C           *LOCK     IN   NOSTD                                                       BUG27903
     C                     MOVE DANOST    WNOST                                             BUG27903
     C                     OUT  NOSTD                                                       BUG27903
      *
      ** Workfields...
      *
     C           *LIKE     DEFN DBFILE    WKFIL1
     C           *LIKE     DEFN DBFILE    WKFIL2
      *
     C           KSETD     KLIST
     C                     KFLD           KCCY
     C                     KFLD           KBRCA
     C                     KFLD           KACCT
      *
     C           KSETD2    KLIST
     C                     KFLD           KCCY2
     C                     KFLD           KBRCA2
     C                     KFLD           KACCT2
      *                                                                                     BUG27903
     C           KSETD3    KLIST                                                            BUG27903
     C                     KFLD           WWCCY                                             BUG27903
     C                     KFLD           KDLIN3                                            BUG27903
      *
     C           KSLAC     KLIST
     C                     KFLD           KSFBRC
     C                     KFLD           KSFCNU
     C                     KFLD           KSFCCY
     C                     KFLD           KSFACO
     C                     KFLD           KSFACS
     C                     KFLD           KSRFNO
      *
      **  Retrieve bank details using access program.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *                                                                                     BUG27769
      ** If return with an error (LR seton in called program) then                          BUG27769
      ** process for database error.                                                        BUG27769
      *                                                                                     BUG27769
     C           @RTCD     IFNE *BLANKS                                                     BUG27769
     C           *LOCK     IN   LDA                                                         BUG27769
     C                     MOVEL'GL011000'DBPGM                                             BUG27769
     C                     MOVEL'SDBANKPD'DBFILE                                            BUG27769
     C                     MOVEL'*FIRST'  DBKEY                                             BUG27769
     C                     Z-ADD002       DBASE                                             BUG27769
     C                     OUT  LDA                                                         BUG27769
     C                     EXSR *PSSR                                                       BUG27769
     C                     ENDIF                                                            BUG27769
      *
     C                     MOVELBJMRDT    WKDFMT  9
     C                     MOVE BJMRDT    WKYEAR  20
      *
     C           WKYEAR    IFLT 72
     C           WKYEAR    ADD  2000      WKCCYY  40
     C                     ELSE
     C           WKYEAR    ADD  1900      WKCCYY
     C                     ENDIF
      *
     C                     MOVE WKCCYY    WKDFMT
     C                     MOVE *BLANKS   KACCT
     C                     MOVE *BLANKS   WKACCT
      *
      ** Indicator 98 seton if date format indicator = M
      *
     C           BJDFIN    COMP 'M'                      98
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *  Called By: (**calling routines**)                            *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
      ** Check to see that *PSSR has not been called yet
      *
     C                     MOVE 'F'       P@RTCD
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      /COPY ZSRSRC,ZDATE2Z2
     C/EJECT
** CPY@
(c) Finastra International Limited 2010
     X/COPY ZSRSRC,ZDATE2Z3
