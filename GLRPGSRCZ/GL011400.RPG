     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Account Transfer Update STDSED')
      *****************************************************************
      *                                                               *
      *  Midas   -  General Ledger Module                             *
      *                                                               *
      *  GL011400 - Midas GL Account Transfer Update STDSED           *
      *                                                               *
      *  Function:  This program updates the Midas SE Standard        *
      *             Settlement Instructions file with the new account *
      *             detail from the Account Transfer Selected Accounts*
      *             File, GLSLACPD. It will also write an audit trail *
      *             record for each update of STDSEDL0 and STDSEDL1   *
      *                                                               *
      *  Called by:                                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. BUG27903           Date 13Jul10               *
      *  Prev Amend No. BUG27769           Date 09Jun10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG27903 - Unable to transfer account with setl 01 and 08    *
      *  BUG27769 - Incorrect error code                              *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *****************************************************************
      *  Function of Indicators                                       *
      *                                                               *
      *  01 - 19      Input Control.                                  *
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)      *
      *   20          Record Not Found                                *
      *   21          File Error Occurred                             *
      *   22          Start/End Of File Reached                       *
      *   25          Lookup/Scan.                                    *
      *  30 - 39      Program Work Indicators                         *
      *  40 - 49      Output Control.                                 *
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)    *
      *  90 - 99      Error Control.                                  *
      *                                                               *
      *****************************************************************
      *
     FSTDSEDL0UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
     F            STDSEDF                           KRENAMESTSEF0
     FSTDSEDL1UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
     F            STDSEDF                           KRENAMESTSEF1
     FSTDSEDL2UF  E           K        DISK         KINFSR *PSSR                            BUG27903
     F                                              KCOMIT                                  BUG27903
     F            STDSEDF                           KRENAMESTSEF2                           BUG27903
     FSTDSEDL3UF  E           K        DISK         KINFSR *PSSR                            BUG27903
     F                                              KCOMIT                                  BUG27903
     F            STDSEDF                           KRENAMESTSEF3                           BUG27903
     FGLAUTFPDO   E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
     FGLSLACL1IF  E           K        DISK         KINFSR *PSSR
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *                                                                                     BUG27903
     INOSTD     E DSNOSTD                         8                                         BUG27903
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     I              '0000000000  '        C         WKZERO
     IDSFDY     E DSDSFDY
      *
      ** Definition: First DS for Access Programs, Short Data Structure
      *
     ISDBANK    E DSSDBANKPD
      *
      ** Definition: Bank Details Accessed Via Access Program.
      *
     IDSFRAC      DS
     I                                        1   6 DSFCNU
     I                                        7   9 DSFCCY
     I                                       10  19 DSFACO
     I                                       20  21 DSFACS
     I                                       22  24 DSFBRC
      *
     IDSTOAC      DS
     I                                        1   6 DSTCNU
     I                                        7   9 DSTCCY
     I                                       10  19 DSTACO
     I                                       20  21 DSTACS
     I                                       22  24 DSTBRC
      *
     IDSKIT2      DS                             90
     I                                        1   3 KCCY2
     I                                        4   6 KBRCA2
     I                                        7  24 KACCT2
      *
     IDSKIT3      DS                             90
     I                                        1   6 KSFCNU
     I                                        7   9 KSFCCY
     I                                       10  19 KSFACO
     I                                       20  21 KSFACS
     I                                       22  24 KSFBRC
     I                                       25  34 KSRFNO
      *                                                                                     BUG27903
     IWNOST       DS                                                                        BUG27903
      * External Data Area for Nostro Details                                               BUG27903
     I                                        1   3 WWCCY                                   BUG27903
     I                                        4   5 WWNOSN                                  BUG27903
     I                                        6   8 WWBRCH                                  BUG27903
      *
     IDSKITE      DS                             90
     I                                        1   3 KCCY
     I                                        4   6 KBRCA
     I                                        7  24 KACCT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           P@PRCD  4
     C                     PARM           P@RFNO 10
     C           DSFRAC    PARM           P@FRAC 24
     C           DSTOAC    PARM           P@TOAC 24
     C                     PARM           P@RTCD  1
      *
     C                     EXSR SRINIT
      *
     C           P@PRCD    IFEQ '*LR '
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     EXSR XSIV01
     C                     EXSR XMSSV0
     C                     EXSR XMSSV1
      *
      ** Search files again, this time using retail a/c no
      *
     C           DSTBRC    IFNE DSFBRC
     C           KACCT2    ANDNE*BLANKS
     C           KACCT2    ANDNEWKZERO
     C                     EXSR XMSV01
     C                     EXSR XMSV11
     C                     ENDIF
      *                                                                                     BUG27903
     C           WNOST     IFNE *BLANKS                                                     BUG27903
     C                     EXSR XMSV03                                                      BUG27903
     C                     EXSR XMSV13                                                      BUG27903
     C                     ENDIF                                                            BUG27903
      *
     C                     RETRN
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMSSV0 - Maintain Midas SE Standard Settlement Instructions  *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: XSAUDT, XXERR                                         *
      *                                                               *
      *****************************************************************
     C           XMSSV0    BEGSR
      *
     C                     MOVEL'STDSEDL0'WKFIL1    P
      *
     C           KDSED     CHAINSTSEF0               2221
     C                     MOVEL'002'     WKERRN  3
     C                     EXSR XXERR
      *
      ** Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETSTSEF0                 21
     C                     MOVEL'003'     WKERRN
     C                     EXSR XXERR
      *
     C                     CLEARSRAB
     C                     CLEARSRAC
     C                     MOVELDSTBRC    SRAB
     C                     MOVELWKACCT    SRAC
      *
     C                     WRITESTSEF0                 21
     C                     MOVEL'004'     WKERRN
     C                     EXSR XXERR
      *
     C                     CLEARGLAUTFD0
     C                     MOVELWKFIL1    AULFIL
     C                     EXSR XSAUDT
      *
     C           KDSED     READESTSEF0                 2122
     C                     MOVEL'005'     WKERRN
     C                     EXSR XXERR
      *
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMSSV1 - Maintain Midas SE Standard Settlement Instructions  *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: XSAUDT, XXERR                                         *
      *                                                               *
      *****************************************************************
     C           XMSSV1    BEGSR
      *
     C                     MOVEL'STDSEDL1'WKFIL1    P
      *
     C           KDSED     CHAINSTSEF1               2221
     C                     MOVEL'006'     WKERRN
     C                     EXSR XXERR
      *
      ** Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETSTSEF1                 21
     C                     MOVEL'007'     WKERRN
     C                     EXSR XXERR
      *
     C                     CLEARSPBA
     C                     CLEARSPAT
     C                     MOVELDSTBRC    SPBA
     C                     MOVELWKACCT    SPAT
      *
     C                     WRITESTSEF1                 21
     C                     MOVEL'008'     WKERRN
     C                     EXSR XXERR
      *
     C                     CLEARGLAUTFD0
     C                     MOVELWKFIL1    AULFIL
     C                     EXSR XSAUDT
      *
     C           KDSED     READESTSEF1                 2122
     C                     MOVEL'009'     WKERRN
     C                     EXSR XXERR
      *
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMSV01 - Maintain Midas SE Standard Settlement Instructions  *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: XSAUDT, XXERR                                         *
      *                                                               *
      *****************************************************************
     C           XMSV01    BEGSR
      *
     C                     MOVEL'STDSEDL0'WKFIL1    P
      *
     C           KDSED2    CHAINSTSEF0               2221
     C                     MOVEL'010'     WKERRN
     C                     EXSR XXERR
      *
      ** Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETSTSEF0                 21
     C                     MOVEL'011'     WKERRN
     C                     EXSR XXERR
      *
     C                     CLEARSRAB
     C                     MOVELDSTBRC    SRAB
      *
     C                     WRITESTSEF0                 21
     C                     MOVEL'012'     WKERRN
     C                     EXSR XXERR
      *
     C                     CLEARGLAUTFD0
     C                     MOVELWKFIL1    AULFIL
     C                     EXSR XSAUDT
      *
     C           KDSED2    READESTSEF0                 2122
     C                     MOVEL'013'     WKERRN
     C                     EXSR XXERR
      *
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMSV11 - Maintain Midas SE Standard Settlement Instructions  *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: XSAUDT, XXERR                                         *
      *                                                               *
      *****************************************************************
     C           XMSV11    BEGSR
      *
     C                     MOVEL'STDSEDL1'WKFIL1    P
      *
     C           KDSED2    CHAINSTSEF1               2221
     C                     MOVEL'014'     WKERRN
     C                     EXSR XXERR
      *
      ** Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETSTSEF1                 21
     C                     MOVEL'015'     WKERRN
     C                     EXSR XXERR
      *
     C                     CLEARSPBA
     C                     MOVELDSTBRC    SPBA
      *
     C                     WRITESTSEF1                 21
     C                     MOVEL'016'     WKERRN
     C                     EXSR XXERR
      *
     C                     CLEARGLAUTFD0
     C                     MOVELWKFIL1    AULFIL
     C                     EXSR XSAUDT
      *
     C           KDSED2    READESTSEF1                 2122
     C                     MOVEL'017'     WKERRN
     C                     EXSR XXERR
      *
     C                     ENDDO
     C                     ENDSR
      *****************************************************************                     BUG27903
      /EJECT                                                                                BUG27903
      *****************************************************************                     BUG27903
      *                                                               *                     BUG27903
      *  XMSV03 - Maintain Midas SE Standard Settlement Instructions  *                     BUG27903
      *                                                               *                     BUG27903
      *  Called By: Main Processing                                   *                     BUG27903
      *                                                               *                     BUG27903
      *  Calls: XSAUDT, XXERR                                         *                     BUG27903
      *                                                               *                     BUG27903
      *****************************************************************                     BUG27903
     C           XMSV03    BEGSR                                                            BUG27903
      *                                                                                     BUG27903
     C                     MOVE *BLANKS   KDLIN3 18                                         BUG27903
     C                     MOVELWWNOSN    KDLIN3                                            BUG27903
     C                     MOVELWNOST     WNOST2  5                                         BUG27903
      *                                                                                     BUG27903
     C                     MOVEL'STDSEDL2'WKFIL1    P                                       BUG27903
      *                                                                                     BUG27903
     C           KDSED3    CHAINSTSEF2               2221                                   BUG27903
     C                     MOVEL'018'     WKERRN                                            BUG27903
     C                     EXSR XXERR                                                       BUG27903
      *                                                                                     BUG27903
      ** Update Records until End of File for Selected Account.                             BUG27903
      *                                                                                     BUG27903
     C           *IN22     DOWEQ*OFF                                                        BUG27903
      *                                                                                     BUG27903
     C                     CLEARSRAB                                                        BUG27903
     C                     MOVELWWBRCH    SRAB                                              BUG27903
      *                                                                                     BUG27903
     C                     UPDATSTSEF2                 21                                   BUG27903
     C                     MOVEL'019'     WKERRN                                            BUG27903
     C                     EXSR XXERR                                                       BUG27903
      *                                                                                     BUG27903
     C                     CLEARGLAUTFD0                                                    BUG27903
     C                     MOVELWKFIL1    AULFIL                                            BUG27903
     C                     EXSR XSAUDT                                                      BUG27903
      *                                                                                     BUG27903
     C           KDSED3    READESTSEF2                 2122                                 BUG27903
     C                     MOVEL'020'     WKERRN                                            BUG27903
     C                     EXSR XXERR                                                       BUG27903
      *                                                                                     BUG27903
     C                     ENDDO                                                            BUG27903
     C                     ENDSR                                                            BUG27903
      *****************************************************************                     BUG27903
      /EJECT                                                                                BUG27903
      *****************************************************************                     BUG27903
      *                                                               *                     BUG27903
      *  XMSV13 - Maintain Midas SE Standard Settlement Instructions  *                     BUG27903
      *                                                               *                     BUG27903
      *  Called By: Main Processing                                   *                     BUG27903
      *                                                               *                     BUG27903
      *  Calls: XSAUDT, XXERR                                         *                     BUG27903
      *                                                               *                     BUG27903
      *****************************************************************                     BUG27903
     C           XMSV13    BEGSR                                                            BUG27903
      *                                                                                     BUG27903
     C                     MOVEL'STDSEDL3'WKFIL1    P                                       BUG27903
      *                                                                                     BUG27903
     C           KDSED3    CHAINSTSEF3               2221                                   BUG27903
     C                     MOVEL'021'     WKERRN                                            BUG27903
     C                     EXSR XXERR                                                       BUG27903
      *                                                                                     BUG27903
      ** Update Records until End of File for Selected Account.                             BUG27903
      *                                                                                     BUG27903
     C           *IN22     DOWEQ*OFF                                                        BUG27903
      *                                                                                     BUG27903
     C                     CLEARSPBA                                                        BUG27903
     C                     MOVELWWBRCH    SPBA                                              BUG27903
      *                                                                                     BUG27903
     C                     UPDATSTSEF3                 21                                   BUG27903
     C                     MOVEL'022'     WKERRN                                            BUG27903
     C                     EXSR XXERR                                                       BUG27903
      *                                                                                     BUG27903
     C                     CLEARGLAUTFD0                                                    BUG27903
     C                     MOVELWKFIL1    AULFIL                                            BUG27903
     C                     EXSR XSAUDT                                                      BUG27903
      *                                                                                     BUG27903
     C           KDSED3    READESTSEF3                 2122                                 BUG27903
     C                     MOVEL'023'     WKERRN                                            BUG27903
     C                     EXSR XXERR                                                       BUG27903
      *                                                                                     BUG27903
     C                     ENDDO                                                            BUG27903
     C                     ENDSR                                                            BUG27903
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSAUDT -  Set Audit records.                                 *
      *                                                               *
      *  Called By: XMSSV0, XMSSV1, XMSV01, XMSV11                    *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           XSAUDT    BEGSR
      *
      ** Set fields for audit file and write a audit record.
      *
     C                     MOVELP@RFNO    AURFNO
     C                     MOVEL'STDSED  'AUFILE
     C                     MOVELDSFBRC    AUFBRC
     C                     MOVELDSFCNU    AUFCNU
     C                     MOVELDSFACO    AUFACO
     C                     MOVELDSFACS    AUFACS
     C                     MOVELDSTBRC    AUTBRC
     C                     MOVELDSTCNU    AUTCNU
     C                     MOVELDSTACO    AUTACO
     C                     MOVELDSTACS    AUTACS
     C                     MOVELCSTM      WKCSTM  6
     C                     MOVELWKCSTM    AUTEXT 50 P
     C                     MOVELWKDFMT    AUCRTD
     C                     WRITEGLAUTFD0               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'001'     DBASE
     C                     MOVELPGM       DBPGM
     C                     MOVEL'GLAUTFPD'DBFILE
     C                     MOVELAURFNO    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSIV01 -  Set Up Initial Values.                             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           XSIV01    BEGSR
      *
      ** Set up 'From' values
      *
     C                     MOVELDSFCCY    KCCY
     C                     MOVELDSFBRC    KBRCA
     C           DSFCNU    CAT  DSFACO:0  KACCT
     C                     CAT  DSFACS:0  KACCT
      *
      ** Set up key for GLSLACL1
      *
     C                     MOVELDSFCNU    KSFCNU
     C                     MOVELDSFCCY    KSFCCY
     C                     MOVELDSFACO    KSFACO
     C                     MOVELDSFACS    KSFACS
     C                     MOVELDSFBRC    KSFBRC
     C                     MOVELP@RFNO    KSRFNO
      *
      ** Set up retail a/c number key
      ** Access the Transfer file for the retail number
      *
     C           KSLAC     CHAINGLSLACL1             3031
      *
     C           *IN30     IFEQ *ON
     C           *IN31     OREQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'GLSLACL1'DBFILE
     C                     MOVELPGM       DBPGM
     C                     MOVELDSKIT3    DBKEY
     C                     MOVEL'034'     DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELDSFCCY    KCCY2
     C                     MOVELDSFBRC    KBRCA2
     C                     MOVELSLACNO    KACCT2
      *
      ** Set up 'To' values
      *
     C           DSTCNU    CAT  DSTACO:0  WKACCT 18
     C                     CAT  DSTACS:0  WKACCT
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XXERR -  Database Error processing                           *
      *                                                               *
      *  Called By:  XMSSV0, XMSSV1, XMSV01, XMSV11                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           XXERR     BEGSR
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVELWKERRN    DBASE
     C                     MOVELPGM       DBPGM
     C                     MOVELWKFIL1    DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT -  Initialise and define fields                       *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      ** Initialise parameters, work fields, etc.
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           NOSTD                                             BUG27903
      *                                                                                     BUG27903
      ** Check nostro account changes                                                       BUG27903
      *                                                                                     BUG27903
     C                     MOVE *BLANKS   WNOST                                             BUG27903
     C           *LOCK     IN   NOSTD                                                       BUG27903
     C                     MOVE DANOST    WNOST                                             BUG27903
     C                     OUT  NOSTD                                                       BUG27903
      *
      ** Workfields...
      *
     C           *LIKE     DEFN DBFILE    WKFIL1
     C           *LIKE     DEFN DBFILE    WKFIL2
      *
      ** Retrieve Run Date.
      *
     C                     CALL 'AOBANKR0'             9090
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If return with an error (LR seton in called program) then
      ** process for database error.
      *
     C           *IN90     IFEQ '1'
     C********** @RTCD     OREQ '*ERROR*'                                                   BUG27769
     C           @RTCD     ORNE *BLANKS                                                     BUG27769
     C           *LOCK     IN   LDA
     C                     MOVEL'AOBANKR0'DBFILE
     C                     MOVEL'*FIRST'  DBKEY
     C                     Z-ADD002       DBASE
     C                     MOVELPGM       DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBJMRDT    WKDFMT  9
     C                     MOVE BJMRDT    WKYEAR  20
      *
     C           WKYEAR    IFLT 72
     C           WKYEAR    ADD  2000      WKCCYY  40
     C                     ELSE
     C           WKYEAR    ADD  1900      WKCCYY
     C                     ENDIF
      *
     C                     MOVE WKCCYY    WKDFMT
     C                     MOVE *BLANKS   KACCT
     C                     MOVE *BLANKS   WKACCT
      *
     C           KDSED     KLIST
     C                     KFLD           KCCY
     C                     KFLD           KBRCA
     C                     KFLD           KACCT
      *
     C           KDSED2    KLIST
     C                     KFLD           KCCY2
     C                     KFLD           KBRCA2
     C                     KFLD           KACCT2
      *                                                                                     BUG27903
     C           KDSED3    KLIST                                                            BUG27903
     C                     KFLD           WWCCY                                             BUG27903
     C                     KFLD           KDLIN3                                            BUG27903
      *
     C           KSLAC     KLIST
     C                     KFLD           KSFBRC
     C                     KFLD           KSFCNU
     C                     KFLD           KSFCCY
     C                     KFLD           KSFACO
     C                     KFLD           KSFACS
     C                     KFLD           KSRFNO
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *  Called By: (**calling routines**)                            *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
      ** Check to see that *PSSR has not been called yet
      *
     C                     MOVE 'F'       P@RTCD
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
** CPY@
(c) Misys International Banking Systems Ltd. 2010
