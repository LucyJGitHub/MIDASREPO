     H        1
      *================================================================
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Account Transfer/Update DPTMVD')
      *================================================================
      *                                                               *
      *  Midas   -  General Ledger Module                             *
      *                                                               *
      *  GL011600 - Midas GL Account Transfer/Update DPTMVD           *
      *                                                               *
      *  Function:  This program updates the Midas SE Depot Movements *
      *             file with the new account detail from the Account *
      *             Transfer Selected Accounts File, GLSLACPD. It     *
      *             also write an audit trail record for each update  *
      *             of DPTMVDL0.                                      *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. BUG27903           Date 13Jul10               *
      *                 BUG27769           Date 09Jun10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *----------------------------------------------------------------
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG27903 - Unable to transfer account with setl 01 and 08    *
      *  BUG27769 - Incorrect error code                              *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *================================================================
      *  Indicator    Description                                     *
      *  ~~~~~~~~~    ~~~~~~~~~~~                                     *
      *  01 - 19      Input Control.                                  *
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)      *
      *   20          Record Not Found                                *
      *   21          File Error Occurred                             *
      *   22          Start/End Of File Reached                       *
      *   25          Lookup/Scan.                                    *
      *  30 - 39      Program Work Indicators                         *
      *  40 - 49      Output Control.                                 *
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)    *
      *  90 - 99      Error Control.                                  *
      *                                                               *
      *----------------------------------------------------------------
      *  Routine      Description                                     *
      *  ~~~~~~~      ~~~~~~~~~~~                                     *
      *  XSIV01       Set Initial Values                              *
      *  XMDMV0       Maintain Midas SE Depot Movements               *
      *  XMDV01       Maintain Midas SE Depot Movements               *
      *  XXERR        Process database error                          *
      *  XSAUDT       Set Audit Records                               *
      *  *INZSR       Initial Routine                                 *
      *  *PSSR        Program Error Routine                           *
      *  @DEFN        Definition Routine (Not Called)                 *
      *                                                               *
      *----------------------------------------------------------------
      *  File         Description                                     *
      *  ~~~~         ~~~~~~~~~~~                                     *
      *  DPTMVDL0     Midas SE Depot Movements LF1                    *
      *  GLAUTFPD     Batch Update Audit File - Generated Entries     *
      *                                                               *
      *================================================================
     F/EJECT
      *================================================================
     FDPTMVDL0UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
     F            DPTMVDF                           KRENAMEDPTMF0
     FDPTMVDL3UF  E           K        DISK         KINFSR *PSSR                            BUG27903
     F                                              KCOMIT                                  BUG27903
     F            DPTMVDF                           KRENAMEDPTMF3                           BUG27903
     FGLAUTFPDO   E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
     FGLSLACL1IF  E           K        DISK         KINFSR *PSSR
      *================================================================
     E/EJECT
      *================================================================
     E                    CPY@    1   1 80
      /COPY ZSRSRC,ZDATE2Z1
      *================================================================
     I/EJECT
      *================================================================
     I              '0000000000  '        C         WKZERO
      *
      ** Program Status Data Structure
      *
     IDSPGM     ESDSY2PGDSP
      *
      ** Local Data Area
      *
     ILDA       E DSLDA                         256
      *                                                                                     BUG27903
     INOSTD     E DSNOSTD                         8                                         BUG27903
      *
      ** Datastructure: From Account Details
      *
     IDSFRAC      DS
     I                                        1   6 DSFCNU
     I                                        7   9 DSFCCY
     I                                       10  19 DSFACO
     I                                       20  21 DSFACS
     I                                       22  24 DSFBRC
      *
      ** Datastructure: To Account Details
      *
     IDSTOAC      DS
     I                                        1   6 DSTCNU
     I                                        7   9 DSTCCY
     I                                       10  19 DSTACO
     I                                       20  21 DSTACS
     I                                       22  24 DSTBRC
      *
      ** Datastructure: Key
      *
     IDSKITE      DS                             90
     I                                        1   3 KCCY
     I                                        4   6 KBRCA
     I                                        7  24 KACCT
      *
      ** Datastructure: Key - DPTMVDL0
      *
     IDSKIT2      DS                             90
     I                                        1   3 KCCY2
     I                                        4   6 KBRCA2
     I                                        7  24 KACCT2
      *
      ** Datastructure: Key - GLSLACL1
      *
     IDSKIT3      DS                             90
     I                                        1   6 KSFCNU
     I                                        7   9 KSFCCY
     I                                       10  19 KSFACO
     I                                       20  21 KSFACS
     I                                       22  24 KSFBRC
     I                                       25  34 KSRFNO
      *                                                                                     BUG27903
     IWNOST       DS                                                                        BUG27903
      * External Data Area for Nostro Details                                               BUG27903
     I                                        1   3 WWCCY                                   BUG27903
     I                                        4   5 WWNOSN                                  BUG27903
     I                                        6   8 WWBRCH                                  BUG27903
      *
      ** Bank details accessed via access program
      *
     ISDBANK    E DSSDBANKPD
      *
      ** First DS for access programs - Short data structure
      *
     IDSFDY     E DSDSFDY
      *================================================================
     C/EJECT
      *================================================================
      * Main Processing Control.
      *
     C           P@PRCD    IFEQ '*LR '
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     EXSR XSIV01
     C                     EXSR XMDMV0
      *
      ** Search files again, this time using retail a/c no
      *
     C           DSTBRC    IFNE DSFBRC
     C           KACCT2    ANDNE*BLANKS
     C           KACCT2    ANDNEWKZERO
     C                     EXSR XMDV01
     C                     ENDIF
      *                                                                                     BUG27903
     C           WNOST     IFNE *BLANKS                                                     BUG27903
     C                     EXSR XMDV02                                                      BUG27903
     C                     ENDIF                                                            BUG27903
      *
     C                     RETRN
      *================================================================
     C/EJECT
     C           XMDMV0    BEGSR
      *================================================================
      * XMDMV0: Maintain Midas SE Depot Movements
      *
      *         Retrieve all records with key equal to that on the
      *         Selected Account File. For each record retrieved, delete
      *         it, write a new record with the new details from the
      *         Selected Accounts File, then write an Audit trail record.
      *
      *----------------------------------------------------------------
      *
     C                     MOVEL'DPTMVDL0'WKFIL1    P
      *
      * Position File.
      *
     C           KTMVD     CHAINDPTMF0               2221
     C                     MOVEL'002'     WKERRN  3
     C                     EXSR XXERR
      *----------------------------------------------------------------
      * Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETDPTMF0                 21
     C                     MOVEL'003'     WKERRN  3
     C                     EXSR XXERR
      *
     C                     CLEARDCAB
     C                     CLEARDCAT
     C                     MOVELDSTBRC    DCAB
     C                     MOVELWKACCT    DCAT
      *
     C                     WRITEDPTMF0                 21
     C                     MOVEL'004'     WKERRN  3
     C                     EXSR XXERR
      *
     C                     CLEARGLAUTFD0
     C                     MOVELWKFIL1    AULFIL
     C                     EXSR XSAUDT
      *
     C           KTMVD     READEDPTMF0                 2122
     C                     MOVEL'005'     WKERRN  3
     C                     EXSR XXERR
      *
     C                     ENDDO
      *================================================================
     C                     ENDSR
     C/EJECT
     C           XMDV01    BEGSR
      *================================================================
      * XMDV01: Maintain Midas SE Depot Movements
      *
      *         Retrieve all records with key equal to Retail A/c No of
      *         Selected Account File. For each record retrieved, delete
      *         it, write a new record with the new details from the
      *         Selected Accounts File, then write an Audit trail record.
      *
      *----------------------------------------------------------------
      *
     C                     MOVEL'DPTMVDL0'WKFIL1    P
      *
      * Position File.
      *
     C           KTMVD2    CHAINDPTMF0               2221
     C                     MOVEL'010'     WKERRN  3
     C                     EXSR XXERR
      *----------------------------------------------------------------
      * Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETDPTMF0                 21
     C                     MOVEL'011'     WKERRN  3
     C                     EXSR XXERR
      *
     C                     CLEARDCAB
     C                     MOVELDSTBRC    DCAB
      *
     C                     WRITEDPTMF0                 21
     C                     MOVEL'012'     WKERRN  3
     C                     EXSR XXERR
      *
     C                     CLEARGLAUTFD0
     C                     MOVELWKFIL1    AULFIL
     C                     EXSR XSAUDT
      *
     C           KTMVD2    READEDPTMF0                 2122
     C                     MOVEL'013'     WKERRN  3
     C                     EXSR XXERR
      *
     C                     ENDDO
      *================================================================
     C                     ENDSR
      *****************************************************************                     BUG27903
      /EJECT                                                                                BUG27903
      *****************************************************************                     BUG27903
      *                                                               *                     BUG27903
      *  XMDV02 - Maintain Midas SE Depot Movements                   *                     BUG27903
      *                                                               *                     BUG27903
      *  Called By: Main Processing                                   *                     BUG27903
      *                                                               *                     BUG27903
      *  Calls: XSAUDT, XXERR                                         *                     BUG27903
      *                                                               *                     BUG27903
      *****************************************************************                     BUG27903
     C           XMDV02    BEGSR                                                            BUG27903
      *                                                                                     BUG27903
     C                     MOVE *BLANKS   KDLIN3 18                                         BUG27903
     C                     MOVELWWNOSN    KDLIN3                                            BUG27903
     C                     MOVELWNOST     WNOST2  5                                         BUG27903
      *                                                                                     BUG27903
     C                     MOVEL'DPTMVDL3'WKFIL1    P                                       BUG27903
      *                                                                                     BUG27903
     C           KTMVD3    CHAINDPTMF3               2221                                   BUG27903
     C                     MOVEL'014'     WKERRN                                            BUG27903
     C                     EXSR XXERR                                                       BUG27903
      *                                                                                     BUG27903
      ** Update Records until End of File for Selected Account.                             BUG27903
      *                                                                                     BUG27903
     C           *IN22     DOWEQ*OFF                                                        BUG27903
      *                                                                                     BUG27903
     C                     CLEARDCAB                                                        BUG27903
     C                     MOVELWWBRCH    DCAB                                              BUG27903
      *                                                                                     BUG27903
     C                     UPDATDPTMF3                 21                                   BUG27903
     C                     MOVEL'015'     WKERRN                                            BUG27903
     C                     EXSR XXERR                                                       BUG27903
      *                                                                                     BUG27903
     C                     CLEARGLAUTFD0                                                    BUG27903
     C                     MOVELWKFIL1    AULFIL                                            BUG27903
     C                     EXSR XSAUDT                                                      BUG27903
      *                                                                                     BUG27903
     C           KTMVD3    READEDPTMF3                 2122                                 BUG27903
     C                     MOVEL'016'     WKERRN                                            BUG27903
     C                     EXSR XXERR                                                       BUG27903
      *                                                                                     BUG27903
     C                     ENDDO                                                            BUG27903
     C                     ENDSR                                                            BUG27903
     C/EJECT
     C           XSAUDT    BEGSR
      *================================================================
      * XSAUDT: Set Audit records.
      *----------------------------------------------------------------
      * Set fields for audit file and write a audit record.
      *
     C                     MOVELP@RFNO    AURFNO
     C                     MOVEL'DPTMVD  'AUFILE
     C                     MOVELDSFBRC    AUFBRC
     C                     MOVELDSFCNU    AUFCNU
     C                     MOVELDSFACO    AUFACO
     C                     MOVELDSFACS    AUFACS
     C                     MOVELDSTBRC    AUTBRC
     C                     MOVELDSTCNU    AUTCNU
     C                     MOVELDSTACO    AUTACO
     C                     MOVELDSTACS    AUTACS
      *
     C                     MOVELDPID      WKDPID  6
     C           DPSS      CAT  WKDPID:1  AUTEXT 50 P
      *
     C                     Z-ADDDPMD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZDATE     WKDPMD  6
     C                     CAT  WKDPMD:1  AUTEXT
      *
     C                     MOVELDPOD      WKDPOD  6
     C                     CAT  WKDPOD:1  AUTEXT
      *
     C                     Z-ADDDPDO      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZDATE     WKDPDO  6
     C                     CAT  WKDPDO:1  AUTEXT
      *
     C                     MOVELDPBN      WKDPBN  6
     C                     CAT  WKDPBN:1  AUTEXT
     C                     MOVELWKDFMT    AUCRTD
      *
     C                     WRITEGLAUTFD0               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL##PGM     DBPGM     P
     C                     MOVEL'GLAUTFPD'DBFILE    P
     C                     MOVELAURFNO    DBKEY     P
     C                     MOVEL'001'     DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *================================================================
     C                     ENDSR
     C/EJECT
     C           XSIV01    BEGSR
      *================================================================
      * XSIV01: Set Up Initial Values.
      *
      *----------------------------------------------------------------
      * Set up key for ATPRPD
      *
      * Set up 'From' values
     C                     MOVELDSFCCY    KCCY
     C                     MOVELDSFBRC    KBRCA
     C           DSFCNU    CAT  DSFACO:0  KACCT
     C                     CAT  DSFACS:0  KACCT
      * Set up key for GLSLACL1
      *
     C                     MOVELDSFCNU    KSFCNU
     C                     MOVELDSFCCY    KSFCCY
     C                     MOVELDSFACO    KSFACO
     C                     MOVELDSFACS    KSFACS
     C                     MOVELDSFBRC    KSFBRC
     C                     MOVELP@RFNO    KSRFNO
      * Set up retail a/c number key
      *
      * Access the Transfer file for the retail number
      *
     C           KSLAC     CHAINGLSLACL1             3031
      *
     C           *IN30     IFEQ *ON
     C           *IN31     OREQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'GLSLACL1'DBFILE    P
     C                     MOVELDSKIT3    DBKEY     P
     C                     MOVEL'034'     DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELDSFCCY    KCCY2
     C                     MOVELDSFBRC    KBRCA2
     C                     MOVELSLACNO    KACCT2
      *
      * Set up 'To' values
     C           DSTCNU    CAT  DSTACO:0  WKACCT 18
     C                     CAT  DSTACS:0  WKACCT
      *                                                                                     BUG27903
     C           *NAMVAR   DEFN           NOSTD                                             BUG27903
      *                                                                                     BUG27903
      ** Check nostro account changes                                                       BUG27903
      *                                                                                     BUG27903
     C                     MOVE *BLANKS   WNOST                                             BUG27903
     C           *LOCK     IN   NOSTD                                                       BUG27903
     C                     MOVE DANOST    WNOST                                             BUG27903
     C                     OUT  NOSTD                                                       BUG27903
      *================================================================
     C                     ENDSR
     C/EJECT
     C           XXERR     BEGSR
      *================================================================
      * XXERR : Database error Processing
      *
      *----------------------------------------------------------------
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL##PGM     DBPGM     P
     C                     MOVELWKFIL1    DBFILE    P
     C                     MOVELDSKITE    DBKEY     P
     C                     MOVELWKERRN    DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *================================================================
     C                     ENDSR
     C/EJECT
     C           *INZSR    BEGSR
      *================================================================
      * *INZSR: Initialization Routine.
      *
      *----------------------------------------------------------------
      * Initialise parameters, work fields, etc.
      *
     C                     MOVEACPY@      MKI@@  80
      *
      *  Retrieve bank details using access program.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *                                                                                     BUG27769
      ** If return with an error (LR seton in called program) then                          BUG27769
      ** process for database error.                                                        BUG27769
      *                                                                                     BUG27769
     C           @RTCD     IFNE *BLANKS                                                     BUG27769
     C           *LOCK     IN   LDA                                                         BUG27769
     C                     MOVEL'SDBANKPD'DBFILE                                            BUG27769
     C                     MOVEL'*FIRST'  DBKEY                                             BUG27769
     C                     Z-ADD002       DBASE                                             BUG27769
     C                     OUT  LDA                                                         BUG27769
     C                     EXSR *PSSR                                                       BUG27769
     C                     ENDIF                                                            BUG27769
      *
     C                     MOVELBJMRDT    WKDFMT  9
     C                     MOVE BJMRDT    WKYEAR  20
      *
     C           WKYEAR    IFLT 72
     C           WKYEAR    ADD  2000      WKCCYY  40
     C                     ELSE
     C           WKYEAR    ADD  1900      WKCCYY
     C                     ENDIF
      *
     C                     MOVE WKCCYY    WKDFMT
     C                     MOVE *BLANKS   KACCT
     C                     MOVE *BLANKS   WKACCT
      *
      *  Indicator 98 seton if date format indicator = M
      *
     C           BJDFIN    COMP 'M'                      98
      *================================================================
     C                     ENDSR
     C/EJECT
     C           *PSSR     BEGSR
      *================================================================
      * *PSSR: Error Handling.
      *
      *----------------------------------------------------------------
      * Check to see that *PSSR has not been called yet
      *
     C                     MOVE 'F'       P@RTCD
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
      *----------------------------------------------------------------
      * If *PSSR already run, then just end the program here
      *
     C                     MOVE *ON       *INLR          LR
     C                     RETRN
      *================================================================
     C                     ENDSR
     C/EJECT
     C           @DEFN     BEGSR
      *================================================================
      * @DEFN: Definition Routine  (Not Called)
      *
      *----------------------------------------------------------------
      * Entry List...
      *
     C           *ENTRY    PLIST
     C                     PARM           P@PRCD  4
     C                     PARM           P@RFNO 10
     C           DSFRAC    PARM           P@FRAC 24
     C           DSTOAC    PARM           P@TOAC 24
     C                     PARM           P@RTCD  1
      *----------------------------------------------------------------
      * Dataareas...
      *
     C           *NAMVAR   DEFN           LDA
      *----------------------------------------------------------------
      * Workfields...
      *
     C           *LIKE     DEFN DBFILE    WKFIL1
     C           *LIKE     DEFN DBFILE    WKFIL2
      *----------------------------------------------------------------
      * Lists...
      *
     C           KTMVD     KLIST
     C                     KFLD           KCCY
     C                     KFLD           KBRCA
     C                     KFLD           KACCT
      *
     C           KTMVD2    KLIST
     C                     KFLD           KCCY2
     C                     KFLD           KBRCA2
     C                     KFLD           KACCT2
      *                                                                                     BUG27903
     C           KTMVD3    KLIST                                                            BUG27903
     C                     KFLD           WWCCY                                             BUG27903
     C                     KFLD           KDLIN3                                            BUG27903
      *
     C           KSLAC     KLIST
     C                     KFLD           KSFBRC
     C                     KFLD           KSFCNU
     C                     KFLD           KSFCCY
     C                     KFLD           KSFACO
     C                     KFLD           KSFACS
     C                     KFLD           KSRFNO
      *
      *================================================================
     C                     ENDSR
      /EJECT
      /COPY ZSRSRC,ZDATE2Z2
     C/EJECT
** CPY@: Object Copyright
(c) Finastra International Limited 2010
     X/COPY ZSRSRC,ZDATE2Z3
