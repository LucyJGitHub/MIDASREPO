     H        1
      *================================================================
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Account Transfer/Update SEOCFSPD')
      *================================================================
      *                                                               *
      *  Midas   -  General Ledger Module                             *
      *                                                               *
      *  GL011800 - Midas GL Account Transfer/Update SEOCFSPD         *
      *                                                               *
      *  Function:  This program updates the Midas SE Outstanding SCF *
      *             Settlements file with the new account detail from *
      *             the Account Transfer Selected Accounts File,      *
      *             GLSLACPD. It will also write an audit trail       *
      *             record for each update of SEOCSFV0.               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. BUG27769           Date 09Jun10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CGL114  *CREATE    Date 04Jan10               *
      *                                                               *
      *----------------------------------------------------------------
      *                                                               *
      *  BUG27769 - Incorrect error code                              *
      *  CGL114 - Account Transfer Utility                            *
      *                                                               *
      *================================================================
      *  Indicator    Description
      *  ~~~~~~~~~    ~~~~~~~~~~~
      *  01 - 19      Input Control.
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)
      *   20          Record Not Found
      *   21          File Error Occurred
      *   22          Start/End Of File Reached
      *   25          Lookup/Scan.
      *  30 - 39      Program Work Indicators
      *  40 - 49      Output Control.
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)
      *  90 - 99      Error Control.
      *
      *----------------------------------------------------------------
      *  Routine      Description
      *  ~~~~~~~      ~~~~~~~~~~~
      *  XSIV01       Set Initial Values
      *  XMOSV0       Maintain Midas SE Outstanding SCF Settlements
      *  XMOV01       Maintain Midas SE Outstanding SCF Settlements
      *  XXERR        Process database error
      *  XSAUDT       Set Audit Records
      *  *INZSR       Initial Routine
      *  *PSSR        Program Error Routine
      *  @DEFN        Definition Routine (Not Called)
      *
      *----------------------------------------------------------------
      *  File         Description
      *  ~~~~         ~~~~~~~~~~~
      *  SEOCFSL0     Midas SE Outstanding SCF Settlements LF1
      *  GLAUTFPD     Batch Update Audit File - Generated Entries
      *
      *================================================================
     F/EJECT
      *================================================================
     FSEOCFSL0UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
     F            SEOCFSP0                          KRENAMEOSCFF0
     FGLAUTFPDO   E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
     FGLSLACL1IF  E           K        DISK         KINFSR *PSSR
      *================================================================
     E/EJECT
      *================================================================
     E                    CPY@    1   1 80
      *================================================================
     I/EJECT
      *================================================================
     I              '0000000000  '        C         WKZERO
      *
      ** Program Status Data Structure
      *
     IDSPGM     ESDSY2PGDSP
      *----------------------------------------------------------------
     ILDA       E DSLDA                         256
      * Dataarea: Local Data Area.
      *----------------------------------------------------------------
     IDSFRAC      DS
      * Datastructure: From Account Details
     I                                        1   6 DSFCNU
     I                                        7   9 DSFCCY
     I                                       10  19 DSFACO
     I                                       20  21 DSFACS
     I                                       22  24 DSFBRC
      *----------------------------------------------------------------
     IDSTOAC      DS
      * Datastructure: To Account Details
     I                                        1   6 DSTCNU
     I                                        7   9 DSTCCY
     I                                       10  19 DSTACO
     I                                       20  21 DSTACS
     I                                       22  24 DSTBRC
      *----------------------------------------------------------------
     IDSKITE      DS                             90
      * Datastructure: Key
     I                                        1   3 KCCY
     I                                        4   6 KBRCA
     I                                        7  24 KACCT
      *----------------------------------------------------------------
     IDSKIT2      DS                             90
      * Datastructure: Key - SEOCFSLx
     I                                        1   3 KCCY2
     I                                        4   6 KBRCA2
     I                                        7  24 KACCT2
      *----------------------------------------------------------------
     IDSKIT3      DS                             90
      * Datastructure: Key - GLSLACL1
     I                                        1   6 KSFCNU
     I                                        7   9 KSFCCY
     I                                       10  19 KSFACO
     I                                       20  21 KSFACS
     I                                       22  24 KSFBRC
     I                                       25  34 KSRFNO
      *
     ISDBANK    E DSSDBANKPD
      * Bank details accessed via access program
      *
     IDSFDY     E DSDSFDY
      * First DS for access programs - Short data structure
      *
      *----------------------------------------------------------------
     C/EJECT
      *================================================================
      * Main Processing Control.
      *
      *----------------------------------------------------------------
      *
     C           P@PRCD    IFEQ '*LR '
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     EXSR XSIV01
     C                     EXSR XMOSV0
      *
      * Search files again, this time using retail a/c no
     C           DSTBRC    IFNE DSFBRC
     C           KACCT2    ANDNE*BLANKS
     C           KACCT2    ANDNEWKZERO
     C                     EXSR XMOV01
     C                     ENDIF
      *
     C                     RETRN
      *================================================================
     C/EJECT
     C           XMOSV0    BEGSR
      *================================================================
      * XMOSV0: Maintain Midas SE Outstanding SCF Settlements
      *
      *         Retrieve all records with key equal to that on the
      *         Selected Account File. For each record retrieved, delete
      *         it, write a new record with the new details from the
      *         Selected Accounts File, then write an Audit trail record.
      *
      *----------------------------------------------------------------
      *
     C                     MOVEL'SEOCFSL0'WKFIL1    P
      *
      * Position File.
      *
     C           KOCFS     CHAINOSCFF0               2221
     C                     MOVEL'002'     WKERRN  3
     C                     EXSR XXERR
      *----------------------------------------------------------------
      * Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETOSCFF0                 21
     C                     MOVEL'003'     WKERRN  3
     C                     EXSR XXERR
      *
     C                     CLEARSECUSB
     C                     CLEARSECUSA
     C                     MOVELDSTBRC    SECUSB
     C                     MOVELWKACCT    SECUSA
      *
     C                     WRITEOSCFF0                 21
     C                     MOVEL'004'     WKERRN  3
     C                     EXSR XXERR
      *
     C                     CLEARGLAUTFD0
     C                     MOVELWKFIL1    AULFIL
     C                     EXSR XSAUDT
      *
     C           KOCFS     READEOSCFF0                 2122
     C                     MOVEL'005'     WKERRN  3
     C                     EXSR XXERR
      *
     C                     ENDDO
      *================================================================
     C                     ENDSR
     C/EJECT
     C           XMOV01    BEGSR
      *================================================================
      * XMOV01: Maintain Midas SE Outstanding SCF Settlements
      *
      *         Retrieve all records with key equal to Retail A/c No of
      *         Selected Account File. For each record retrieved, delete
      *         it, write a new record with the new details from the
      *         Selected Accounts File, then write an Audit trail record.
      *
      *----------------------------------------------------------------
      *
     C                     MOVEL'SEOCFSL0'WKFIL1    P
      *
      * Position File.
      *
     C           KOCFS2    CHAINOSCFF0               2221
     C                     MOVEL'010'     WKERRN  3
     C                     EXSR XXERR
      *----------------------------------------------------------------
      * Delete and Write Records until End of File for Selected Account.
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETOSCFF0                 21
     C                     MOVEL'011'     WKERRN  3
     C                     EXSR XXERR
      *
     C                     CLEARSECUSB
     C                     MOVELDSTBRC    SECUSB
      *
     C                     WRITEOSCFF0                 21
     C                     MOVEL'012'     WKERRN  3
     C                     EXSR XXERR
      *
     C                     CLEARGLAUTFD0
     C                     MOVELWKFIL1    AULFIL
     C                     EXSR XSAUDT
      *
     C           KOCFS2    READEOSCFF0                 2122
     C                     MOVEL'013'     WKERRN  3
     C                     EXSR XXERR
      *
     C                     ENDDO
      *================================================================
     C                     ENDSR
     C/EJECT
     C           XSAUDT    BEGSR
      *================================================================
      * XSAUDT: Set Audit records.
      *----------------------------------------------------------------
      * Set fields for audit file and write a audit record.
      *
     C                     MOVELP@RFNO    AURFNO
     C                     MOVEL'SEOCFSPD'AUFILE
     C                     MOVELDSFBRC    AUFBRC
     C                     MOVELDSFCNU    AUFCNU
     C                     MOVELDSFACO    AUFACO
     C                     MOVELDSFACS    AUFACS
     C                     MOVELDSTBRC    AUTBRC
     C                     MOVELDSTCNU    AUTCNU
     C                     MOVELDSTACO    AUTACO
     C                     MOVELDSTACS    AUTACS
      *
     C                     MOVELSECNUM    WKCNUM  6
     C                     CAT  WKCNUM:1  AUTEXT 50 P
     C                     MOVELWKDFMT    AUCRTD
      *
     C                     WRITEGLAUTFD0               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL##PGM     DBPGM     P
     C                     MOVEL'GLAUTFPD'DBFILE    P
     C                     MOVELAURFNO    DBKEY     P
     C                     MOVEL'001'     DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *================================================================
     C                     ENDSR
     C/EJECT
     C           XSIV01    BEGSR
      *================================================================
      * XSIV01: Set Up Initial Values.
      *
      *----------------------------------------------------------------
      * Set up key for ATPRPD
      *
     C                     MOVE *BLANKS   KACCT
     C                     MOVE *BLANKS   WKACCT
      * Set up 'From' values
     C                     MOVELDSFCCY    KCCY
     C                     MOVELDSFBRC    KBRCA
     C           DSFCNU    CAT  DSFACO:0  KACCT
     C                     CAT  DSFACS:0  KACCT
      * Set up key for GLSLACL1
      *
     C                     MOVELDSFCNU    KSFCNU
     C                     MOVELDSFCCY    KSFCCY
     C                     MOVELDSFACO    KSFACO
     C                     MOVELDSFACS    KSFACS
     C                     MOVELDSFBRC    KSFBRC
     C                     MOVELP@RFNO    KSRFNO
      * Set up retail a/c number key
      *
      * Access the Transfer file for the retail number
      *
     C           KSLAC     CHAINGLSLACL1             3031
      *
     C           *IN30     IFEQ *ON
     C           *IN31     OREQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'GLSLACL1'DBFILE    P
     C                     MOVELDSKIT3    DBKEY     P
     C                     MOVEL'034'     DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELDSFCCY    KCCY2
     C                     MOVELDSFBRC    KBRCA2
     C                     MOVELSLACNO    KACCT2
      *
      * Set up 'To' values
     C           DSTCNU    CAT  DSTACO:0  WKACCT 18
     C                     CAT  DSTACS:0  WKACCT
      *================================================================
     C                     ENDSR
     C/EJECT
     C           XXERR     BEGSR
      *================================================================
      * XXERR : Database error Processing
      *
      *----------------------------------------------------------------
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL##PGM     DBPGM     P
     C                     MOVELWKFIL1    DBFILE    P
     C                     MOVELDSKITE    DBKEY     P
     C                     MOVELWKERRN    DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *================================================================
     C                     ENDSR
     C/EJECT
     C           *INZSR    BEGSR
      *================================================================
      * *INZSR: Initialization Routine.
      *
      *----------------------------------------------------------------
      *
      **  Retrieve bank details using access program.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *                                                                                     BUG27769
      ** If return with an error (LR seton in called program) then                          BUG27769
      ** process for database error.                                                        BUG27769
      *                                                                                     BUG27769
     C           @RTCD     IFNE *BLANKS                                                     BUG27769
     C           *LOCK     IN   LDA                                                         BUG27769
     C                     MOVEL'SDBANKPD'DBFILE                                            BUG27769
     C                     MOVEL'*FIRST'  DBKEY                                             BUG27769
     C                     Z-ADD002       DBASE                                             BUG27769
     C                     OUT  LDA                                                         BUG27769
     C                     EXSR *PSSR                                                       BUG27769
     C                     ENDIF                                                            BUG27769
      *
     C                     MOVELBJMRDT    WKDFMT  9
     C                     MOVE BJMRDT    WKYEAR  20
      *
     C           WKYEAR    IFLT 72
     C           WKYEAR    ADD  2000      WKCCYY  40
     C                     ELSE
     C           WKYEAR    ADD  1900      WKCCYY
     C                     ENDIF
      *
     C                     MOVE WKCCYY    WKDFMT
      *
      * Initialise parameters, work fields, etc.
      *
     C                     MOVEACPY@      MKI@@  80
      *================================================================
     C                     ENDSR
     C/EJECT
     C           *PSSR     BEGSR
      *================================================================
      * *PSSR: Error Handling.
      *
      *----------------------------------------------------------------
      * Check to see that *PSSR has not been called yet
      *
     C                     MOVE 'F'       P@RTCD
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
      *----------------------------------------------------------------
      * If *PSSR already run, then just end the program here
      *
     C                     MOVE *ON       *INLR          LR
     C                     RETRN
      *================================================================
     C                     ENDSR
     C/EJECT
     C           @DEFN     BEGSR
      *================================================================
      * @DEFN: Definition Routine  (Not Called)
      *
      *----------------------------------------------------------------
      * Entry List...
      *
     C           *ENTRY    PLIST
     C                     PARM           P@PRCD  4
     C                     PARM           P@RFNO 10
     C           DSFRAC    PARM           P@FRAC 24
     C           DSTOAC    PARM           P@TOAC 24
     C                     PARM           P@RTCD  1
      *----------------------------------------------------------------
      * Dataareas...
      *
     C           *NAMVAR   DEFN           LDA
      *----------------------------------------------------------------
      * Workfields...
      *
     C           *LIKE     DEFN DBFILE    WKFIL1
     C           *LIKE     DEFN DBFILE    WKFIL2
      *----------------------------------------------------------------
      * Lists...
      *
     C           KOCFS     KLIST
     C                     KFLD           KCCY
     C                     KFLD           KBRCA
     C                     KFLD           KACCT
      *
     C           KOCFS2    KLIST
     C                     KFLD           KCCY2
     C                     KFLD           KBRCA2
     C                     KFLD           KACCT2
      *
     C           KSLAC     KLIST
     C                     KFLD           KSFBRC
     C                     KFLD           KSFCNU
     C                     KFLD           KSFCCY
     C                     KFLD           KSFACO
     C                     KFLD           KSFACS
     C                     KFLD           KSRFNO
      *
      *================================================================
     C                     ENDSR
     C/EJECT
** CPY@: Object Copyright
(c) Misys International Banking Systems Ltd. 2010
