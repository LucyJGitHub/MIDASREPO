      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Convert Day Number to Date')
     T* Utility Calls: Convert Day number to Date
      *
     H*  SYSTEM  : UTILITY OBJECTS
     H* ANALYST     : J L Blenkinsop
     H* DATE        : June 1988
     H* DESCRIPTION : Converts MIDAS day number to date in requested format
      *
     F********************************************************************
     F*                                                                  *
     F*  Midas  GENERAL LEDGER MODULE
     F*                                                                  *
     F*     GL0134U  -                                                   *S01251
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01199             Date 02May90               *
     F*                 S01251             Date 09FEB90               *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*     S01199 - RECOMPILED FOR CURSOR SENSITIVE HELP                *
     F*     S01251 - PROGRAM AND FIELD NAME CHANGES                      *
     F*                                                                  *
     F********************************************************************
     F*  P R O G R A M     C R E A T I O N     P A R A M E T E R S    *
     F*                                                               *
     F*                                                               *
     F********************************************************************
     M* MAINTENANCE : *NONE
      *
     P* PARAMETERS  : Call with the following code :
     P*
     P*                    CALL '@UCVDND0'             90  <CONVERT DATE>
     P*                    PARM (Day No.) P@DYNO  50       I:Day number
     P*                    PARM (Format)  P@FMAT  3        I:Format flag
     P*          (Alpha)   PARM           P@DTAL  7        O:DDMMMYY
     P*          (Date)    PARM           P@DTNO  60       O:DMY/MDY/YMD
     P*
     P*          *IN90     IFEQ '1'                        IF CALL ERROR
     P*          (Perform error reporting if required)
     P*                    END                             FI
     P*
     P*    NOTE: Date format must be either "DMY" (Day/Month/Year)
     P*                                  or "MDY" (Month/Day/Year)
     P*                                  or "YMD" (Year/Month/Day)
      *
      ***************************************************************
      /SPACE 3
     E*ESTART
      ***************************************************
     E                    @M1    12  12  5 0A            Days in norm.year
     E                    @M2    12  12  5 0A            Days in leap year
     E                    @M3    12  12  3               Month names
     E                    @YD     3   3  5 0A            Non-leap offsets
     E                    @LY    25  25  2 0             Leap years
     E                    @LD    12  25  5 0A            Days to leap year
     E                    CPY@    1   1 80               COPYRIGHT STATEMENT
      ***************************************************
      /SPACE 3
     I*ISTART
      *****************************************************************
     ID@DMY       DS
     I                                        1   20W@DD
     I                                        3   40W@MM
     I                                        5   60W@YY
      *
     ID@MDY       DS
     I                                        1   20W@M2
     I                                        3   40W@D2
     I                                        5   60W@Y2
      *
     IP@DTAL      DS
     I                                        1   2 W@D3
     I                                        3   5 W@M3
     I                                        6   7 W@Y3
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      *****************************************************************
      /EJECT
     C*CSTART
      *****************************************************
     C                     MOVEACPY@      BIS@   80
     C           *ENTRY    PLIST
     C           W@DYNO    PARM           P@DYNO  50       I:Day number
     C                     PARM           P@FMAT  3        I:Format flag
     C                     PARM           P@DTAL  7        O:DDMMMYY
     C                     PARM           P@DTNO  60       O:DMY/MDY/YMD
      *****************************************************
      * Initialise :
      *
     C           *LIKE     DEFN P@DYNO    W@DYNO           Day number
     C                     Z-ADD*ZERO     P@DTNO           Numer.format
     C                     MOVE *BLANK    P@DTAL           Alpha format
     C                     Z-ADD1         W@MM             Set month
      *
      * Check Day No. in Leap Year Days table :
      *
     C                     Z-ADD1         P1      30       Set pointer
     C           W@DYNO    LOKUP@LD,P1                 0102FCT1<=FCT2
     C                     SUB  @LD,P1    W@DYNO           Decr.Day No.
     C                     Z-ADD@LY,P1    W@YY             Year
      *
      * If not a Leap year, determine year offset :
      *
     C           W@DYNO    IFGT 366                        IF NOT LEAP
     C                     Z-ADD1         P1                 Set ptr.
     C           W@DYNO    LOKUP@YD,P1                 0102  FCT1<=FCT2
     C                     ADD  P1        W@YY               Year offset
     C                     SUB  @YD,P1    W@DYNO             Decr.Day No.
     C           W@DYNO    LOKUP@M1,W@MM               0102  Ptr=Month
     C           W@DYNO    SUB  @M1,W@MM  W@DD               Day
     C                     ELSE                            EL LEAP YEAR
     C           W@DYNO    LOKUP@M2,W@MM               0102  Ptr=Month
     C           W@DYNO    SUB  @M2,W@MM  W@DD               Day
     C                     END                             FI
      *
      * Format in DDMMMYY :
      *
     C                     MOVE @M3,W@MM  W@M3             Month
     C                     MOVE W@DD      W@D3             Day
     C                     MOVE W@YY      W@Y3             Year
      *
      * Numeric format as required :
      *
     C           P@FMAT    IFEQ 'MDY'                      IF 4 MMDDYY
     C                     Z-ADDW@MM      W@M2               Month
     C                     Z-ADDW@DD      W@D2               Day
     C                     Z-ADDW@YY      W@Y2               Year
     C                     MOVE D@MDY     P@DTNO             Output
     C                     END                             FI 4
      *
     C           P@FMAT    IFEQ 'YMD'                      IF 5 YYMMDD
     C                     Z-ADDW@YY      W@M2               Year
     C                     Z-ADDW@MM      W@D2               Day
     C                     Z-ADDW@DD      W@Y2               Month
     C                     MOVE D@MDY     P@DTNO             Output
     C                     END                             FI 5
      *
     C           P@FMAT    IFEQ 'DMY'                      IF 6 YYMMDD
     C                     MOVE D@DMY     P@DTNO             Output
     C                     END                             FI 6
      *
      * Return to caller; leave this program active in pool :
      *
     C                     RETRN
      *
      *****************************************************************
      /SPACE 3
** @M1 - Days in a normal year
000000003100059000900012000151001810021200243002730030400334
** @M2 - Days in a leap year
000000003100060000910012100152001820021300244002740030500335
** @M3 - Names of months
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
** @YD - Days offset for year /
003660073101096
** @LY - Leap years from 1972 to 2068, starting at 1972 /
72768084889296000408121620242832364044485256606468
** @LD - Days to Leap years from 1972 to 2068, starting at 1972 /
000000146102922043830584407305087661022711688131491461016071
175321899320454219152337624837262982775929220306813214233603
35064
**  CPY@
(c) Finastra International Limited 2001
