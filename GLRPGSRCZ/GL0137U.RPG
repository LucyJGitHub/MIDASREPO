      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Find Last Batch Item Number')                 *
     F*  Midas - General Ledger Module
     ** Title           : Journal Entries
     ** Function type   : Execute User Program
     ** Date written    : 22th. May  1989
      *  (c) Finastra International Limited 2001                      *
     ** Description     : Retrieve last item number for batch.
     **
     ** Maintenance     : *NONE
     F*                                                                  *
     F*  Midas  GENERAL LEDGER MODULE
     F*                                                                  *
     F*     GL0137U  -                                                   *S01251
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     **
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD047608           Date 06Oct17               *
      *                 MD027587           Date 19Jun14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01340             Date 21Feb92               *
      *                 S01199             Date 02May90               *
     **                    S01251             DATE 09FEB90               *
     **                    S01234             DATE 21DEC89               *
     **                    S01216             DATE 10/10/89              *
     **                                                                --*
     *********************************************************************
     **                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  MD047608 - Batch cannot be authorized because it has 1000    *
      *             items. Fix is to not pass back 999 if 999 is the  *
      *             latest item number used.                          *
      *  MD027587 - Unable to used the C for Copy and T for Template  *
      *             (Recompile)                                       *
     **   Program to retrieve the last batch item number from the JE     *
     **   postings file for a specific Batch.                            *
     **                                                                  *
     **   S01340 - RECOMPILED FOR MIS RELEASE 10                         *
     **   S01199 - RECOMPILED FOR CURSOR SENSITIVE HELP                  *
     **   S01234 - BATCH NUMBER IS NOW A CHARACTER FIELD                 *
     **                                                                  *
     **   S01251 - PROGRAM AND FIELD NAME CHANGES                        *
     **                                                                  *
     *********************************************************************
      *
     F**********GLBTREL1IF  E           K        DISK                     S01251
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F**********GLBTREL1IF  E           K        DISK                     S01251
     FGLJENPL1IF  E           K        DISK                               S01251
     E                    CPY@    1   1 80               COPYRIGHT STATEMENT
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
     C                     MOVEACPY@      BIS@   80
     C           *ENTRY    PLIST
     C******************** PARM           BCHNUM  30        BATCH NO      S01234
     C                     PARM           BCHNUM  3         BATCH NO      S01234
     C                     PARM           BCHIT   30        BATCH ITEM
      *
      * Set Batch number to next Batch in the sequence to find the 1st
      * record of the next batch.
      *
     C                     MOVE BCHNUM    LATBAT  3         BATCH NO      S01234
      *                                                                   S01234
     C********** BCHNUM    ADD  1         BCHNO   30                      S01234
     C***********LATBAT    ADD  1         BCHNO   30                      S01234
      *                                                                   S01234
     C*********************MOVE BCHNO     LATCHA  3         BATCH NO      S01234
      *                                                                   S01234
     C***********BCHNO     SETLL@BTREAL              82    *82=EOF        S01234
     C           LATBAT    SETGT@BTREAL              82    *82=EOF        S01234
      *
      * Read Previous record to extract the last journal entry for
      * the batch in which you are dealing with. If the record read
      * is EOF then the file is empty so Batch Item No = 0
      *
     C                     READP@BTREAL                9182*82=EOF
     C           *IN82     IFEQ '1'                        If 1
     C                     Z-ADD0         BCHIT
     C                     ELSE                            E 1
      *
      * If the batch number read in is equal to the batch number
      * read then the Batch Item No = Batch Item Number off File.
      * Otherwise set the Batch Item No = 0
      *
     C********** BCHNUM    IFEQ BTJDNB                     If 2           S01251
     C**********           Z-ADDBTJNNB    BCHIT                           S01251
     C           BCHNUM    IFEQ BTBTNB                     If 2
     C                     Z-ADDBTBINB    BCHIT
      * If latest batch item is 999, look for one that would be deleted                     MD047608
     C           BCHIT     IFEQ 999                                                         MD047608
     C                     Z-ADD1         PREV    30                                        MD047608
     C           LATBAT    SETLL@BTREAL              82                                     MD047608
     C           LATBAT    READE@BTREAL                9182                                 MD047608
     C           *IN82     DOWEQ'0'                                                         MD047608
     C           BTBINB    IFEQ PREV                                                        MD047608
     C                     ADD  1         PREV                                              MD047608
     C           LATBAT    READE@BTREAL                9182                                 MD047608
     C                     ELSE                                                             MD047608
     C           PREV      SUB  1         BCHIT                                             MD047608
     C           BCHIT     IFEQ 999                                                         MD047608
     C                     Z-ADD0         BCHIT                                             MD047608
     C                     ENDIF                                                            MD047608
     C                     LEAVE                                                            MD047608
     C                     ENDIF                                                            MD047608
     C                     ENDDO                                                            MD047608
      * If all items wre used, pass back 0                                                  MD047608
     C           BTBINB    IFEQ 999                                                         MD047608
     C                     Z-ADD0         BCHIT                                             MD047608
     C                     ENDIF                                                            MD047608
     C                     ENDIF                                                            MD047608
      *                                                                                     MD047608
     C                     ELSE                            E 2
     C                     Z-ADD0         BCHIT
     C                     END                             Endif 2
     C                     END                             Endif 1
      *
      * Close Down Program
      *
     C                     SETON                     LR
**  CPY@
(c) Finastra International Limited 2001
