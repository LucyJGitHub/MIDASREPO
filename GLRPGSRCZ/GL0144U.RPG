      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL SPF Branches in Interface Batch')             *
     F********************************************************************
     F*                                                                  *
     F*  Midas GENERAL LEDGER MODULE
     F*                                                                  *
     F*   GL0144U -   Validate Branch codes within a batch against       *
     F*               a specific action code .                           *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
      *  Last Amend No. CRE075             Date 06Dec10               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER047             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 110922             Date 24Sep97               *
     F*                 S01340             Date 21Feb92                  *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
      *  CRE075 - Effective Date for Retail Accounts (Recompile)      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *           (Recompile)                                         *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*  110922 - Get the branch code for Retail accounts, and write it  *
     F*           to GLJEIPPD if it was left blank. Used for validation. *
     F*      S01340 - RECOMPILED FOR MIS RELEASE 10                      *
     F*                                                                  *
     F********************************************************************
     F*  P R O G R A M     C R E A T I O N     P A R A M E T E R S    *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      *
      ********************************************************************
      /EJECT
     F*FSTART
      *****************************************************************
     F***GLJEIPL1IF**E           K        DISK                            110922
     FGLJEIPL1UF  E           K        DISK                               110922
      *       @POST2  Interface Batch (BTNB¦BINB)
      *****************************************************************
     E                    CPY@    1   1 80               Copyright     array
      /EJECT
      * Data structures:-
      /EJECT
     I*ISTART
      *                                                                   110922
     ID@ACCT    E DSACCNTAB                                               110922
      *                                                                   110922
      *  Account Details DS for assigning data from Access                110922
      *  Programs to work fields                                          110922
      *                                                                   110922
      *                                                                   110922
     ID@RETL    E DSSDRETLPD                                              110922
      *                                                                   110922
      *  Retail Details DS for assigning data from Access                 110922
      *  Programs to work fields                                          110922
      *                                                                   110922
      *                                                                   110922
     IUUSDY     E DSDSSDY                                                 110922
      *                                                                   110922
      * 800 Byte Data Structure for Incoming Data from Access Program     110922
      *                                                                   110922
     IUUFDY     E DSDSFDY                                                 110922
      *                                                                   110922
      * 200 Byte Data Structure for Incoming Data from Access Program     110922
      *                                                                   110922
     IA@CPY       DS
      * Copyright array
     I                                        1  80 CPY@
      *****************************************************************
      /EJECT
     C*CSTART
      *****************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P@RTCD  10       O:Return code
     C                     PARM           P@BTNO  3        B:Batch Number
     C                     PARM           P@ANCD  1        B:Action code
      *****************************************************
      *MAIN
      *                                                                   110922
      ** Get RE ICD details to know if Retail numbers are used.           110922
     C                     MOVEL*BLANK    WUAPRC  7                       110922
     C                     CALL 'AORETLR0'                                110922
     C           WUAPRC    PARM WUAPRC    UUAPRC  7        B:Return Cd    110922
     C                     PARM '*FIRST'  UUAPOP  7        I:Option       110922
     C           D@RETL    PARM *BLANK    UUFDY            O:Format       110922
      *                                                                   110922
      *
      * Read all Posting records and for each record check the action
      * code and the branch code by calling the SPF program ZVACTBU.   ecks
      *
     C           P@BTNO    SETLL@POST2                   03 Set Batch
      *
     C           *IN03     IFEQ '1'                        IF Data 1
     C           *IN01     DOUEQ'1'                        DO Read 1
     C                     READ @POST2                   01Read Posting
     C           BTBTNB    IFNE P@BTNO
     C                     SETON                     01
     C                     END
      *                                                                   110922
      ** If branch code was left blank in GLJEIPPD, and retail account    110922
      ** account number was used, and retail numbers are supported...     110922
      *                                                                   110922
     C           BTIBCA    IFEQ *BLANKS                                   110922
     C           BTRACN    ANDNE*ZEROS                                    110922
     C           BMRANR    ANDEQ'Y'                                       110922
      *                                                                   110922
     C                     MOVELBTRACN    UUACID                          110922
      *                                                                   110922
      *  Call to Access Program - AOACCTR0 Check Account Number           110922
      *  Access Account Details file to get branch code of account.       110922
      *                                                                   110922
     C                     CALL 'AOACCTR0'                                110922
     C           WUAPRC    PARM WUAPRC    UUAPRC  7        B:Return Cd    110922
     C                     PARM '*KEY'    UUAPOP  7        B:Option       110922
     C                     PARM           UUACID 10        I:Account Ident110922
     C                     PARM           UUCUST  6        I:Customer Numb110922
     C                     PARM           UUCYCD  3        I:Currency Code110922
     C**********           PARM           UUACNO  4        I:Account Code              110922 CGL029
     C                     PARM           UUACNO 10                                           CGL029
     C                     PARM           UUACSN  2        I:Account Seque110922
     C                     PARM           UUBRCD  3        I:Branch code  110922
     C           D@ACCT    PARM *BLANK    UUSDY            O:Format       110922
      *                                                                   110922
      ** move account's branch code to interface file branch field.       110922
     C           UUAPRC    IFEQ *BLANKS                                   110922
     C                     MOVELBRCA      BTIBCA                          110922
     C                     END                                            110922
     C                     END                                            110922
      *                                                                   110922
      * validate user action against branch code.                         110922
     C           *IN01     IFEQ '0'                        IF Data 2
     C                     CALL 'ZVACTBU'
     C                     PARM           P@ANCD           Action Code
     C                     PARM           BTIBCA           Branch Code
     C                     PARM           P@RTCD           Error Code
     C           P@RTCD    IFNE 0                          IF      3
     C                     SETON                     01      Not Auth
     C                     Z-ADD1         P@RTCD           SPF Error Condition
     C                     ELSE                                           110922
      * write branch code to file, if valid.                              110922
     C                     EXCPTJEIP                                      110922
     C                     END                             FI      3
     C                     END                             FI      2
     C                     END                             OD      1
     C                     END                             FI      1
      * Return to caller :
      *
     C                     SETON                     LR
     C                     RETRN
      *
      *****************************************************************
      *
     O@POST2  E                JEIP                                       110922
     O                         BTIBCA                                     110922
      *                                                                   110922
** CPY@
(c) Misys International Banking Systems Ltd. 2001
