     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Obtain Spread Journal Ccy Total')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  GL0171U - Obtain Spread Journal Currency Total               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CSD006             Date 11Oct00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CGL008             Date 03Mar99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD006 - Use correct parameters for Access Objects           *
      *  CGL008 - Security on Journal Entry. Recompile.               *
      *                                                               *
      *****************************************************************
      /EJECT
     FGLJENPL2IF  E           K        DISK                               S01251
      * IN  - @BTREBG Journal items    (JDNB¦AHCD)
      *
     FGLJENSL0UF  E           K        DISK                      A        S01251
      * I/O - @BSREAI Journal currency (JDNB¦AHCD)
      *****************************************************************
      /EJECT
      *****************************************************************
     IP@FMAT    E DSGLJENHPD
      * Batch header file format data structure
      *
     IP@CURF    E DSSDCURRPD
      * Currency file format data structure
      *
     IDSSDY     E DSDSSDY                                                                     CSD006
      ** External DS for long dummy data structure                                            CSD006
      *                                                                                       CSD006
     I            DS
      * #1 Hash normalisation data structure
     I                                        1  150W@FB15
     I                                        1  140W@FB14
     I                                        1  130W@FB13
     I                                        1  120W@FB12
     I                                       15  151W@FB01
     I                                       14  152W@FB02
     I                                       13  153W@FB03
     I            DS
      * #2 Hash normalisation data structure
     I                                        1   93W@##09
     I                                        1   60W@##06
     I                                        7   90W@##03
      *****************************************************************
      /EJECT
      *****************************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  1        O:Option
     C                     PARM           P@CLPG  1        I:Calling Pgm  S01212
     C                     PARM           P@FMAT           B:Format
     C*****************************************************************
     C           *LIKE     DEFN BTCYCD    W@CYCD           Currency code  S01251
     C           *LIKE     DEFN BTIBCA    W@IBCA           Branch Code    S01117
     C           *LIKE     DEFN P@RTCD    W@RTCD           Return code
      *
      * Delete existing Currency records for Batch :
      *
     C           BRBTNB    SETLL@BSREAI              010103 Set currency  S01251
      *
     C           *IN03     IFEQ '1'                        IF DATA
     C           *IN01     DOUEQ'1'                        DO READ GROUP
     C           BRBTNB    READE@BSREAI                0101  Read Ccy.    S01251
     C           *IN01     IFEQ '0'                        IF DATA
     C                     DELET@BSREAI                01    Remove
     C                     END                             FI
     C                     END                             OD
     C                     END                             FI
      *
      * Reset calculated Hash fields :
      *
     C                     Z-ADD*ZERO     BRHINC           Reset items    S01251
     C                     Z-ADD*ZERO     BRHICC           Reset integer  S01251
     C                     Z-ADD*ZERO     BRHDCC           Reset decimal  S01251
     C                     Z-ADD*ZERO     W@FB15           Set work field
     C                     Z-ADD*ZERO     W@##09           Set work field
      *
      * Set to start of group of item records for Batch :
      *
     C           BRBTNB    SETLL@BTREBG              010203 Set file      S01251
      *
      * Read group; at end of each currency, add currency record :
      *
     C           *IN01     IFEQ '0'                        IF   NOT EMPTY
     C           *IN02     ANDEQ'0'                         AND NO ERRORS
     C           *IN03     ANDEQ'1'                         AND DATA
      *                    ¦
     C           *IN04     DOUEQ'1'                        DO READ GROUP
     C           BRBTNB    READE@BTREBG                0204  Read items   S01251
      *                    ¦|
     C           *IN04     IFEQ '0'                        IF DATA
     C           *IN02     ANDEQ'0'                         AND NO ERRORS
      *
      * If Currency Code is Blank move '***' to it
      *
     C           BTCYCD    IFEQ '   '                                     S01251
     C                     MOVE '***'     BTCYCD                          S01251
     C                     END                             FI
      *
     C           BTRINE    IFEQ 'Y'                        IF RETAIL ERRORS01251
     C           BTRINO    ANDNE'Y'                         AND ¬OVRIDDEN S01251
     C                     MOVE 'E'       P@OPTN             Inform
     C                     END                             FI
      *                    ¦|¦
     C                     ADD  1         BRHINC             Count items  S01251
      *                    ¦|¦
     C           BTCYCD    IFNE W@CYCD                     IF NEW CURRENCYS01251
     C           BTIBCA    ORNE W@IBCA                      OR NEW BRANCH S01117
      *                    ¦|¦¦
     C           W@CYCD    IFNE *BLANK                     IF NOT FIRST   S01251
     C                     MOVE BTBTNB    BSBTNB             Batch number S01251
     C                     MOVE W@CYCD    BSCYCD             Currency codeS01251
     C                     MOVE W@IBCA    BSBRCD             Branch Code  S01117
     C                     WRITE@BSREAI                      Write  file
     C                     END                             FI
      *                    ¦|¦¦
     C                     Z-ADD*ZERO     BSDBTT             Debit total  S01251
     C                     Z-ADD*ZERO     BSCRTT             Credit total S01251
     C                     MOVE BTCYCD    W@CYCD             New currency S01251
     C                     MOVE BTIBCA    W@IBCA             New Branch   S01117
      *
      * If Currency code is '***' move 0 to no. of decimal places
      * and do not call Access Currency Codes
      *
     C           BTCYCD    IFEQ '***'                                     S01251
     C                     MOVE 2         A6NBDP             No. Dec PlaceS01251
      *
     C                     ELSE
      *                                                                   S01251
     C                     CALL 'AOCURRR0'             90    <CURRENCY>   S01251
     C                     PARM *BLANK    P@RTCD  7          B:Return codeS01251
     C                     PARM '*KEY   ' P@CUOP  7          I:Option     S01251
     C                     PARM BTCYCD    P@CYCD  3          I:Key field  S01251
     C***********          PARM           P@CURF             O:Format     S01251              CSD006
     C           P@CURF    PARM P@CURF    DSSDY              O:Format     S01251              CSD006
     C                     END                             FI
      *                    ¦|¦¦
     C                     END                             FI
      *                    ¦|¦
      * Add amount to Dr or Cr of Currency record :
      *                    ¦|¦
     C           BTDCIN    IFEQ 'D'                        IF DEBIT AMT.  S01251
     C                     ADD  BTPTAM    BSDBTT             Debit total  S01251
     C                     ELSE                            EL CREDIT AMT.
     C                     ADD  BTPTAM    BSCRTT             Credit total S01251
     C                     END                             FI
      *                    ¦|¦
      * Normalise amount and add to calculated hash total :
      *                    ¦|¦
     C                     Z-ADDBTPTAM    W@FB15             To work fieldS01251
     C                     MLLZO'0'       W@FB15             Make positive
      *                    ¦|¦
     C           A6NBDP    IFEQ *ZERO                      IF DECIMAL=0   S01251
     C                     ADD  W@FB15    BRHICC             Hash integer S01251
     C                     END                             FI
      *                    ¦|¦
     C           A6NBDP    IFEQ 1                          IF DECIMAL=1   S01251
     C                     ADD  W@FB14    BRHICC             Hash integer S01251
     C                     ADD  W@FB01    W@##09             Check decimal
     C                     END                             FI
      *                    ¦|¦
     C           A6NBDP    IFEQ 2                          IF DECIMAL=2   S01251
     C                     ADD  W@FB13    BRHICC             Hash integer S01251
     C                     ADD  W@FB02    W@##09             Check decimal
     C                     END                             FI
      *                    ¦|¦
     C           A6NBDP    IFEQ 3                          IF DECIMAL=3   S01251
     C                     ADD  W@FB12    BRHICC             Hash integer S01251
     C                     ADD  W@FB03    W@##09             Check decimal
     C                     END                             FI
      *                    ¦|¦
     C                     END                             FI
      *                    ¦|
     C                     END                             OD
      *                    ¦
      * End of Batch - add last currency record :
      *                    ¦
     C                     MOVE BTBTNB    BSBTNB           Batch number   S01251
     C                     MOVE W@CYCD    BSCYCD           Currency code  S01251
     C                     MOVE W@IBCA    BSBRCD           Branch Code    S01117
      *
     C                     WRITE@BSREAI                    Write  file
      *                    ¦
      * Add decimal amount to Hash and add carry to hash integer :
      *                    ¦
     C                     Z-ADDW@##03    BRHDCC           Hash decimal   S01251
     C                     ADD  W@##06    BRHICC           Carry integer  S01251
      *                    ¦
     C                     END                             FI
      *
      * Return to caller :
      *
     C                     SETON                     LR
      *
      *****************************************************************
