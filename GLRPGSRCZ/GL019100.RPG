     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Account Transfer Update GLACNTQD')            *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL019100 - Midas Account Transfer Update GLACNTQD            *
      *                                                               *
      *  Function:  This program updates the Midas GL Account Master  *
      *             Extension File, GLACNTQD with the new account     *
      *             details from Account Transfer Selected Accounts   *
      *             File, GLSLACPD.                                   *
      *             It will also write an audit trail record for each *
      *             update of GLACNTQD                                *
      *                                                               *
      *  Called By: GL003600                                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. CRE095             Date 25Apr14               *
      *  Prev Amend No. CRE090             Date 14Feb14               *
      *                 AR1095876          Date 30Sep13               *
      *                 AR899028           Date 25Jan12               *
      *                 AR847269           Date 09Nov11               *
      *                 CER042             Date 11May11               *
      *                 BUG27769           Date 09Jun10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 BUG27222 *CREATE   Date 15Mar10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE095 - Rate Fixing for RE Accounts (Recompile)             *
      *  CRE090 - Delay Capitalisation of Interest (Recompile)        *
      *  AR1095876- ABC - Forward Days 1 should = DNWD-1, not just    *
      *             RUND (Child: AR1095877) (Recompile)               *
      *  AR899028 - NOSTRO AMAD System errors occured; applied        *
      *             fix AR847269                                      *
      *  AR847269 - Column F1DACN not in specified tables (Recompile) *
      *  CER042 - Interest Scale Report Correspondence                *
      *           (Upgrade of FGE178/179) (Recompile)                 *
      *  BUG27769 - Incorrect error code                              *
      *  BUG27222 - ATU Additional updates programs                   *
      *                                                               *
      *****************************************************************
      *  Function of Indicators                                       *
      *                                                               *
      *  01 - 19      Input Control.                                  *
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)      *
      *   20          Record Not Found                                *
      *   21          File Error Occurred                             *
      *   22          Start/End Of File Reached                       *
      *   25          Lookup/Scan.                                    *
      *  30 - 39      Program Work Indicators                         *
      *  40 - 49      Output Control.                                 *
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)    *
      *  90 - 99      Error Control.                                  *
      *****************************************************************
      *
     FGLACNTL3UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
     FGLAUTFPDO   E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     ISDBANK    E DSSDBANKPD
      *
     IDSFDY     E DSDSFDY
      *
     IDSFRAC      DS
     I                                        1   6 DSFCNU
     I                                        7   9 DSFCCY
     I                                       10  19 DSFACO
     I                                       20  21 DSFACS
     I                                       22  24 DSFBRC
      *
     IDSTOAC      DS
     I                                        1   6 DSTCNU
     I                                        7   9 DSTCCY
     I                                       10  19 DSTACO
     I                                       20  21 DSTACS
     I                                       22  24 DSTBRC
      *
     IDSKITE      DS                             90
     I                                        1   6 KCNUM
     I                                        7   9 KCCY
     I                                       10  190KACOD
     I                                       20  210KACSQ
     I                                       22  24 KBRCA
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           P@PRCD  4
     C                     PARM           P@RFNO 10
     C           DSFRAC    PARM           P@FRAC 24
     C           DSTOAC    PARM           P@TOAC 24
     C                     PARM           P@RTCD  1
      *
     C                     EXSR SRINIT
      *
     C           P@PRCD    IFEQ '*LR '
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     EXSR XVACCN
      *
     C                     RETRN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XVACCN - Validate Midas GL Accounts Master Extension File    *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:     XSIV01, XMACCN                                    *
      *                                                               *
      *****************************************************************
      *
     C           XVACCN    BEGSR
      *
      ** Check if 'To' details already exist on GLACNTQD
      *
     C                     MOVELDSTCNU    KCNUM
     C                     MOVELDSTCCY    KCCY
     C                     MOVELDSTACO    KACOD
     C                     MOVELDSTACS    KACSQ
     C                     MOVELDSTBRC    KBRCA
     C           KACCN     SETLLGLACNTD0               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'001'     DBASE
     C                     MOVEL'GLACNTL3'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Process each Primary file record. The corresponding posting
      ** records are retrieved and updated.
      *
     C           *IN22     IFEQ *OFF
     C                     EXSR XSIV01
     C                     EXSR XMACCN
      *
     C                     ELSE
     C                     MOVE 'F'       P@RTCD
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XMACCN - Maintain Midas GL Accounts Master Extension File    *
      *                                                               *
      *  Called By: XVACCN                                            *
      *                                                               *
      *  Calls:     XSAUDT                                            *
      *                                                               *
      *****************************************************************
      *
     C           XMACCN    BEGSR
      *
     C           KACCN     SETLLGLACNTL3
     C           KACCN     READEGLACNTL3               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'002'     DBASE
     C                     MOVEL'GLACNTL3'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Delete and Write Records until End of File for Selected Account
      *
     C           *IN22     DOWEQ*OFF
     C                     DELETGLACNTD0               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'003'     DBASE
     C                     MOVEL'GLACNTL3'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELDSTCNU    F1CNUM
     C                     MOVELDSTCCY    F1CCY
     C                     MOVELDSTACO    F1ACOD
     C                     MOVELDSTACS    F1ACSQ
     C                     MOVELDSTBRC    F1BRCA
     C                     WRITEGLACNTD0               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'004'     DBASE
     C                     MOVEL'GLACNTL3'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     EXSR XSAUDT
      *
     C           KACCN     READEGLACNTL3               2122
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'005'     DBASE
     C                     MOVEL'GLACNTL3'DBFILE
     C                     MOVELDSKITE    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSAUDT - Set Audit records                                   *
      *                                                               *
      *  Called By: XMACCN                                            *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *****************************************************************
      *
     C           XSAUDT    BEGSR
      *
     C                     MOVELP@RFNO    AURFNO
     C                     MOVEL'GLACNTQD'AUFILE
     C                     MOVEL'GLACNTL3'AULFIL
     C                     MOVELDSFBRC    AUFBRC
     C                     MOVELDSFCNU    AUFCNU
     C                     MOVELDSFACO    AUFACO
     C                     MOVELDSFACS    AUFACS
     C                     MOVELDSTBRC    AUTBRC
     C                     MOVELDSTCNU    AUTCNU
     C                     MOVELDSTACO    AUTACO
     C                     MOVELDSTACS    AUTACS
     C                     MOVELF1FCHD    WKFCHD  5
     C                     MOVELF1ACBL    WKACBL 15
     C                     MOVELF1TEXO    WKTEXO 15
     C           WKFCHD    CAT  WKACBL:1  AUTEXT
     C                     CAT  WKTEXO:1  AUTEXT 50 P
     C                     MOVELWKDFMT    AUCRTD
     C                     WRITEGLAUTFD0               21
      *
     C           *IN21     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'006'     DBASE
     C                     MOVEL'GLAUTFPD'DBFILE
     C                     MOVELAURFNO    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  XSIV01 - Set up initial values                               *
      *                                                               *
      *  Called By: XVACCN                                            *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *****************************************************************
      *
     C           XSIV01    BEGSR
      *
     C                     MOVELDSFBRC    KBRCA
     C                     MOVELDSFCNU    KCNUM
     C                     MOVELDSFCCY    KCCY
     C                     MOVELDSFACO    KACOD
     C                     MOVELDSFACS    KACSQ
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Initialise and define fields                        *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Retrieve Run Date.
      *
     C                     CALL 'AOBANKR0'             9090
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If return with an error (LR seton in called program) then
      ** process for database error.
      *
     C           *IN90     IFEQ '1'
     C********** @RTCD     OREQ '*ERROR*'                                                   BUG27769
     C           @RTCD     ORNE *BLANKS                                                     BUG27769
     C           *LOCK     IN   LDA
     C                     MOVEL'AOBANKR0'DBFILE
     C                     MOVEL'*FIRST'  DBKEY
     C                     Z-ADD007       DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBJMRDT    WKDFMT  9
     C                     MOVE BJMRDT    WKYEAR  20
      *
     C           WKYEAR    IFLT 72
     C           WKYEAR    ADD  2000      WKCCYY  40
     C                     ELSE
     C           WKYEAR    ADD  1900      WKCCYY
     C                     ENDIF
      *
     C                     MOVE WKCCYY    WKDFMT  9
      *
     C           KACCN     KLIST
     C                     KFLD           KCNUM
     C                     KFLD           KCCY
     C                     KFLD           KACOD
     C                     KFLD           KACSQ
     C                     KFLD           KBRCA
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     MOVE 'F'       P@RTCD
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2010
