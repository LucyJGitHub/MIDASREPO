     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('WHT not in Base currency report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL0201 - Interest tax not in Base Currency                   *
      *                                                               *
      *  Function:  This report runs daily during COB and prints      *
      *  (COB)      details of tax deducted from interest today for   *
      *             MM deals & Retail accounts not in Base Currency.  *
      *                                                               *
      *  Called By: GLC0201  - Tax not in Base Currency control       *
      *                                                               *
      *  Calls    : ZSFILE   - Set up spool file numbers file         *
      *                                                               *
      *  (c) Finastra International Limited 2006                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL013  *CREATE    Date 08Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL013 - Print details of tax deducted from interest today   *
      *           for MM deals & Retail accounts not in Base Currency *
      *                                                               *
      *****************************************************************
      *
     FGLXTAXL0IF  E           K        DISK         KINFSR *PSSR
      ** Extracted Dealing/Retail Account Tax Details
      *
     FGL0201P1O   E             69     PRINTER      KINFDS SPOOL      UC
      ** Tax not in Base Currency report file
      *
     FGL0201AUO   E                    PRINTER      KINFDS SPOOLU     UC
      ** Tax not in Base Currency audit file
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Function of indicators                                       *
      *  ----------------------                                       *
      *                                                               *
      *     66 - Deal/Account Indicator is 'D'                        *
      *     67 - Change of branch                                     *
      *     68 - Change of customer                                   *
      *     69 - Overflow Indicator                                   *
      *     70 - Record not found                                     *
      *     98 - Date Format Indicator                                *
      *                                                               *
      *  U7+U8 - Database error occurs                                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Subroutines                                                  *
      *  -----------                                                  *
      *  ZFRPED - Edit amounts for output on report                   *
      *  ZDATE2 - Convert Midas day number to date                    *
      *  ZCONV  - Convert Currency routine                            *
      *  #EMAIN - Main processing                                     *
      *  #EINIT - Initial Processing                                  *
      *  #ECYCH - Change of Currency                                  *
      *  #ECUCH - Change of Customer                                  *
      *  #ELAST - Final Processing                                    *
      *  *PSSR  - Program error                                       *
      *                                                               *
      *****************************************************************
     E/COPY ZSRSRC,ZFRPEDZ1
      ** Run-time arrays for subroutine ZFRPED
      *
     E/COPY ZSRSRC,ZDATE2Z1
      ** Compile-time arrays for subroutine ZDATE2
      *
     E/COPY ZSRSRC,ZPOWERZ1
      ** Compile-time array for subroutine ZCONV
      *
     E                    CPY@    1   1 80
      ** Array for Object Copyright Statement
      *****************************************************************
      /EJECT
      *****************************************************************
     ILDA        UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
     ISPOOL       DS
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
      *
     ISPOOLU      DS
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     I/COPY ZSRSRC,ZFRPEDZ2
      ** Data structures for subroutine ZFRPED
      *
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
     ISDCURR    E DSSDCURRPD
      ** Currency Details
      *
     ISDCUST    E DSSDCUSTPD
      ** Customer Details
      *
     IDSFDY     E DSDSFDY
      ** Short Data Structure for access programs
      *
     IDSSDY     E DSDSSDY
      ** Long Data Structure for access programs
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *            #EMAIN - Main Control Processing                   *
      *                                                               *
      *  Calls     #EINIT - Initial Processing                        *
      *            #ECYCH - Change of Currency                        *
      *            #ECUCH - Change of Customer                        *
      *            #ELAST - Final Processing                          *
      *            ZFRPED - Edit amounts for output on report         *
      *            ZDATE2 - Convert Midas day number to date          *
      *            ZCONV  - Convert Currency routine                  *
      *                                                               *
      *****************************************************************
      ** Initial Processing
     C                     EXSR #EINIT
      *
      ** Main Processing
      *
      ** Set AUDIT  to 'N'    - Do not record audit spool file by RCF
      ** Set OLDCCY to blanks - old currency code
      ** Set OLDBCH to blanks - old branch code
      ** Set OLDCUS to blanks - old customer number
     C                     MOVE 'N'       AUDIT   1
     C                     MOVE *BLANKS   OLDCCY  3
     C                     MOVE *BLANKS   OLDBCH  3
     C                     MOVE *BLANKS   OLDCUS  6
      *
      ** Go to the first record
     C           *LOVAL    SETLLGLXTAXL0
      *
      ** Read the record from Tax Details file
     C                     READ GLXTAXL0                 70
      *
      ** If READ succesful, open print file and print the header
     C           *IN70     IFEQ *OFF
     C                     OPEN GL0201P1
     C                     WRITEHEADER
      ** Otherwise, set AUDIT to 'Y' to record audit spool file by RCF,
      ** open the audit file, print the message & terminate program
     C                     ELSE
     C                     MOVE 'Y'       AUDIT
     C                     OPEN GL0201AU
     C                     WRITEAUHEAD
     C                     WRITENODETL
     C                     CLOSEGL0201AU
     C                     ENDIF
      *
      ** Do while READ succesful
     C           *IN70     DOWEQ*OFF
      *
      ** If deal, set *IN66 ON and extract 6 digits of deal number
     C                     MOVE *OFF      *IN66
     C           EDAIN     IFEQ 'D'
     C                     MOVE *ON       *IN66
     C                     MOVE EDLAC     WDLNUM  60
     C                     ENDIF
      *
      ** If currency has changed, retrieve decimal places, print
      ** currency header & change the branch
     C           ECURR     IFNE OLDCCY
     C                     EXSR #ECYCH
     C                     ENDIF
      *
      ** If branch has changed, set *IN67 ON, update old branch code
      ** and clear old customer code
     C           EBRCA     IFNE OLDBCH
     C                     MOVE *ON       *IN67
     C                     MOVE EBRCA     OLDBCH
     C                     MOVE *BLANKS   OLDCUS
     C                     ENDIF
      *
      ** If customer has changed, set *IN68 ON and retrieve shortname
     C           ECUST     IFNE OLDCUS
     C                     MOVE *ON       *IN68
     C                     EXSR #ECUCH
     C                     ENDIF
      *
      ** Move customer number to printer fields
     C                     MOVE ECUST     WCUST1
     C                     MOVE ECUST     WCUST2
      *
      ** Convert Value Date
     C                     MOVE EVDAT     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    CVDAT
      *
      ** Convert tax amount (Ccy)
     C                     MOVE ETAXA     ZFLD15
     C                     MOVE A6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    CURTAX
      *
      ** Calculate & Convert tax amount (Base)
     C                     MOVE ETAXA     ZAMTCI
     C                     MOVE A6SPRT    ZEXCH
     C                     MOVE A6MDIN    ZMD
     C                     MOVE ECURR     ZCCYI
     C                     MOVE BJCYCD    ZCCYO
     C                     MOVE A6NBDP    ZCDPI
     C                     MOVE BASDP     ZCDPO
     C                     EXSR ZCONV
     C                     MOVE ZAMTCO    WTAXA  150
      *
     C                     MOVE WTAXA     ZFLD15
     C                     MOVE BASDP     ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    BASTAX
      *
      ** Print detail line
     C                     WRITEDETAIL
      *
      ** Set indicators OFF
     C                     MOVE *OFF      *IN67
     C                     MOVE *OFF      *IN68
      *
      ** If overflow, print header & currency detail line
     C           *IN69     IFEQ *ON
     C                     WRITEHEADER
     C                     WRITECURHDR
     C                     MOVE *OFF      *IN69
     C                     ENDIF
      *
      ** Read next record from tax details file
     C                     READ GLXTAXL0                 70
      *
     C                     ENDDO
      *
      ** Print the last line of the report if AUDIT is 'N'
     C           AUDIT     IFEQ 'N'
     C                     WRITEFOOTER
     C                     CLOSEGL0201P1
     C                     ENDIF
      *
      ** Terminate the program
     C                     EXSR #ELAST
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #EINIT - Initial processing                                  *
      *                                                               *
      *  Calls    AOBANKR0 - Retrieve run date                        *
      *           *PSSR    - Program error                            *
      *                                                               *
      *****************************************************************
     C           #EINIT    BEGSR
      *
      ** RCF parameters
     C           *ENTRY    PLIST
     C                     PARM           @RSEQ   5
     C                     PARM           @RLEV   1
     C                     PARM           @RENT   3
      *
      ** Define LDA
     C           *NAMVAR   DEFN           LDA
      *
      ** Retrieve Run Date, User Report Title & Base Ccy via AOBANKR0
     C                     CALL 'AOBANKR0'
     C                     PARM '*BLANK ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C** If error occured, terminate the program
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVEL@OPTN     DBKEY
     C                     Z-ADD01        DBASE
     C                     EXSR *PSSR
     C                     END
      *
      ** Set Date Format Indicator *IN98
     C                     MOVE *OFF      *IN98
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ** Retrieve Base Ccy decimal places via access program AOCURRR0
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C           BJCYCD    PARM BJCYCD    @CYCD   3
     C           SDCURR    PARM SDCURR    DSSDY
     C** If error occured, terminate the program
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCURRPD'DBFILE
     C                     MOVEL@OPTN     DBKEY
     C                     Z-ADD02        DBASE
     C                     EXSR *PSSR
     C                     END
      *
      ** Store Base Ccy decimal places
     C                     MOVE A6NBDP    BASDP   10
      *
      ** Copyright statement
     C                     MOVEACPY@      CPY2@  80
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #ECYCH - Currency change                                     *
      *                                                               *
      *  Calls    AOCURRR0 - Retrieve currency decimal places         *
      *           *PSSR    - Program error                            *
      *                                                               *
      *****************************************************************
     C           #ECYCH    BEGSR
      *
      ** If currency has changed, retrieve currency decimal places,
      ** print currency header and update old currency code
      *
      ** Retrieve currency decimal places via access program AOCURRR0
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C           ECURR     PARM ECURR     @CYCD   3
     C           SDCURR    PARM SDCURR    DSSDY
     C** If error occured, terminate the program
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCURRPD'DBFILE
     C                     MOVEL@OPTN     DBKEY
     C                     Z-ADD03        DBASE
     C                     EXSR *PSSR
     C                     END
      *
      ** Print currency detail line
     C                     WRITECURHDR
      *
      ** Update the old currency code
     C                     MOVE ECURR     OLDCCY
      *
      ** Clear old branch code & old customer code
     C                     MOVE *BLANKS   OLDBCH
     C                     MOVE *BLANKS   OLDCUS
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #ECUCH - Customer change                                     *
      *                                                               *
      *  Calls    AOCUSTR0 - Retrieve customer shortname              *
      *           *PSSR    - Program error                            *
      *                                                               *
      *****************************************************************
     C           #ECUCH    BEGSR
      *
      ** If customer number has changed, retrieve his shortname and
      ** update the old customer number
      *
      ** Retrieve customer shortname via access program AOCUSTR0
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY  '  @OPTN   7
     C                     PARM ECUST     @KEY1  10
     C                     PARM           @KYST   7
     C           SDCUST    PARM SDCUST    DSFDY
     C** If error occured, terminate the program
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCUSTPD'DBFILE
     C                     MOVEL@OPTN     DBKEY
     C                     Z-ADD04        DBASE
     C                     EXSR *PSSR
     C                     END
      *
      ** Update the old branch code
     C                     MOVE ECUST     OLDCUS
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #ELAST - Final processing                                    *
      *                                                               *
      *****************************************************************
     C           #ELAST    BEGSR
      *
      ** Update LDA before calling ZSFILE
     C                     OUT  LDA
      *
      ** If AUDIT is 'N', record report spool file
     C           AUDIT     IFEQ 'N'
     C                     Z-ADDSFNUM     SFNUM1  60
     C                     CALL 'ZSFILE'
     C                     PARM           @RSEQ
     C                     PARM *BLANKS   @PARM   3
     C                     PARM           SFILE
     C                     PARM           SFNUM1
     C                     PARM           ZSERR   1
      ** Otherwise, record audit spool file
     C                     ELSE
     C                     Z-ADDSFNUMU    SFNUM2  60
     C                     CALL 'ZSFILE'
     C                     PARM           @RSEQ
     C                     PARM *BLANKS   @PARM   3
     C                     PARM           SFILEU
     C                     PARM           SFNUM2
     C                     PARM           ZSERR
     C                     ENDIF
      *
      ** Retrieve LDA after calling ZSFILE
     C           *LOCK     IN   LDA
      *
      ** If error in ZSFILE
     C           ZSERR     IFEQ 'Y'
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Terminate the program
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program error                                       *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
      ** Update LDA
     C                     MOVEL'GL0201'  DBPGM     P
     C                     OUT  LDA
      *
      ** Open audit file & print standard DBERR printout
     C                     OPEN GL0201AU
     C                     WRITEAUHEAD
     C                     WRITEDBERROR
     C                     CLOSEGL0201AU
      *
      ** If *PSSR has not been called yet, dump the program
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     END
      *
      ** If *PSSR already run, then just end the program here
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     C/COPY ZSRSRC,ZCONVZ1
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     C/COPY ZSRSRC,ZDATE2Z2
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     C/COPY ZSRSRC,ZFRPEDZ3
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Compile-time arrays for subroutine ZDATE2
     O/COPY ZSRSRC,ZDATE2Z3
** POWER - Array of powers for currency conversion
0000001
0000010
0000100
0001000
0010000
0100000
1000000
** CPY@ - Object copyright
(c) Finastra International Limited 2006
