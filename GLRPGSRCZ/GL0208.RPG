     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Customer Details installation utility')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL0208 - Customer Detail Installation Utility                *
      *                                                               *
      *  Function - In modes 1 & 2 sets on Taxable indicator to       *
      *             '0' if 'N' and '1' if 'Y'. Also, in mode 1        *
      *             Customer Type is set to 'Retail'.                 *
      *             In mode 3 Taxable indicator set to 'N' if '0',    *
      *             otherwise it is set to 'Y'.                       *
      *             Report produced displaying details before & after *
      *             feature is switched ON/OFF.                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. MD038105           Date 26Apr16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL013  *CREATE    Date 08Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD038105 - Program corrupts SDCUSTPD BBTAIN on re-run.       *
      *             When mode is '1' or '2' and BBTAIN is 'Y' or '1'  *
      *             the converted value after switched on of CDL013,  *
      *             set value as '1'.                                 *
      *  CDL013 - Tiered Withholding Tax                              *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Function of Indicators                                       *
      *  ----------------------                                       *
      *                                                               *
      *     65 - PRINT REPORT OVERFLOW INDICATOR                      *
      *     66 - PRINT REPORT OVERFLOW INDICATOR                      *
      *                                                               *
      *     80 - NO RECORD FOUND ON SDCUSTL0                          *
      *                                                               *
      *     U7 - ERROR PROCEESING INDICATORS                          *
      *     U8 - ERROR PROCESSING INDICATORS                          *
      *     LR - LAST RECORD INDICATOR                                *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     FSDCUSTL0UF  E           K        DISK
      * Customer Details
      *
     FGL0208P1O   E             65     PRINTER                            S01117
      * Customer Details Amendments                                         "
      *                                                                     "
     FGL0208AUO   E             66     PRINTER                            S01117
      * Customer Details Amendments - Audit                                 "
      *                                                                     "
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80                             array
      ** Array containing Copyright statement
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      *
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
     I*                                                                   S01319
     IDSFDY     E DSDSFDY                                                 S01194
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01194
     I*                                                                   S01117
     I              'Account Number'      C         BANK
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  #EENIT - Perform initial processing.                         *
      *  #EEMAN - Perform main processing via mode 1, 2 or 3.         *
      *  #EEND  - Commit files and exit program.                      *
      *                                                               *
      *****************************************************************
      *          C O N T R O L      M O D U L E                       *
      *****************************************************************
      * Entry parameters
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
     C                     EXSR #EEINT
     C                     EXSR #EEMAN
     C                     EXSR #EEEND
      *
      *****************************************************************
      * #EEMAN - Main Subroutine                                      *
      * Called by: - Control.                                         *
      *****************************************************************
     C           #EEMAN    BEGSR
      *
      ** Access first record on LF/SDCUSTL0
     C                     READ SDCUSTL0                 80
      *
      ** If records on file print report heading.
     C           *IN80     IFEQ *OFF
     C                     WRITEHEADER
     C           MODE      IFEQ '1'
     C           MODE      OREQ '2'
     C                     WRITESUBHD1
     C                     ELSE
     C                     WRITESUBHD2
     C                     ENDIF
      *
      ** If no records on file print audit
     C                     ELSE
     C                     WRITEDBHEAD
     C                     WRITENOREC
     C                     ENDIF
      *
      ** Do while records on file.
     C           *IN80     DOWEQ*OFF
      *
      ** Save before values for Customer Type & Tax Indicator
     C                     MOVE BBCSTY    UBCSTY  1
     C                     MOVE BBTAIN    UBTAIN  1
      *
      ** If Tiered Withhilding Tax Feature is switched ON
      ** Mode is '1' or '2'.
     C           MODE      IFEQ '1'
     C           MODE      OREQ '2'
      *
     C           BBTAIN    IFEQ 'Y'
     C           BBTAIN    OREQ '1'                                                         MD038105
     C                     MOVE '1'       BBTAIN
     C                     ELSE
     C                     MOVE '0'       BBTAIN
     C                     ENDIF
      *
      ** If mode is '1' set customer type to 'R'(Retail)
     C           MODE      IFEQ '1'
     C                     MOVE 'R'       BBCSTY
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** If Tiered Withholding Tax Feature is switched OFF(mode is'3')
     C           MODE      IFEQ '3'
      *
      ** If Tax Indicator is '0' set to 'N' else set to 'Y'
     C           BBTAIN    IFEQ '0'
     C           BBTAIN    OREQ 'N'                                                         MD038105
     C                     MOVE 'N'       BBTAIN
     C                     ELSE
     C                     MOVE 'Y'       BBTAIN
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Write before &  after details to report and update PF/SDCUSTPD
     C                     WRITEDETAIL
     C                     UPDAT@BBREBG
     C                     READ SDCUSTL0                 80
      *
     C                     ADD  1         RECTOT
      *
      ** If overflow indicator on reprint header
     C           *IN65     IFEQ *ON
     C                     WRITEHEADER
     C           MODE      IFEQ '1'
     C           MODE      OREQ '2'
     C                     WRITESUBHD1
     C                     ELSE
     C                     WRITESUBHD2
     C                     ENDIF
     C                     MOVE *OFF      *IN65
     C                     ENDIF
     C                     ENDDO
      *
      ** Print Footer
     C           RECTOT    IFGT 0
     C                     WRITEFOOTER
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      * #EEINT - Initial processing                                   *
      * Called by: - Control Module.                                  *
      *****************************************************************
     C           #EEINT    BEGSR
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      ** Retrieve Run Date & User Report Title via AOBANKR0
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If error occured, terminate the program
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVELBANK      DBKEY
     C                     Z-ADD01        DBASE
     C                     MOVEL'GL0207'  DBPGM
     C                     WRITEDBHEAD
     C                     WRITEDBERR
     C                     EXSR #EEEND
     C                     END
      *
      ** Initialise record counter
     C                     Z-ADD0         RECTOT  40
     C                     ENDSR
      *
      *****************************************************************
      * #EEEND - Exit program                                         *
      * Called by: - Control.                                         *
      *****************************************************************
     C           #EEEND    BEGSR
      *
      ** Exit program
     C                     SETON                     LR
     C                     RETRN
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
** CPY@ - Object copyright
(c) Misys International Banking Systems Ltd. 2006
