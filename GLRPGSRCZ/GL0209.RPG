     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Set up Tax indicator on deals utility')                *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL0209 - Set Up Tax Indicator on Deals Utility               *
      *                                                               *
      *  Function - In modes 1 & 2 sets on Tax/Non Tax indicator to   *
      *             '0' if 'N' and '1' if 'Y'.                        *
      *             In mode 3 Tax/Non Tax indicator set to 'N' if '0' *
      *             otherwise it is set to 'Y'.                       *
      *             Report produced displaying details before & after *
      *             feature is switched ON/OFF.                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. MD038105           Date 26Apr16               *
      *  Prev Amend No. CGL165             Date 17Feb15               *
      *                 CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      *                 CRE073             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL013  *CREATE    Date 08Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD038105 - Program corrupts DEALSDC TAXI on re-run.          *
      *             When mode is '1' or '2' and TAXI is 'Y' or '1'    *
      *             the converted value after switched on of CDL013,  *
      *             set value as '1'.                                 *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CDL013 - Tiered Withholding Tax                              *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Function of Indicators                                       *
      *  ----------------------                                       *
      *                                                               *
      *     60 - PRINT REPORT OVERFLOW INDICATOR                      *
      *     61 - PRINT REPORT OVERFLOW INDICATOR                      *
      *                                                               *
      *     80 - NO RECORD FOUND ON DEALS                             *
      *                                                               *
      *     LR - LAST RECORD INDICATOR                                *
      *                                                               *
      *****************************************************************
      *
     FMMDELDL2UF  E           K        DISK
     FDEALS   UF  E           K        DISK
     F            DEALSDAF                          KIGNORE
     F            DEALSDBF                          KIGNORE
     F            DEALSDCF                          KRENAMEDEALSFMT
     F            DEALSDDF                          KIGNORE
     F            DEALSDEF                          KIGNORE
     F            DEALSDFF                          KIGNORE
     F            DEALSDGF                          KIGNORE
      * Customer Details
      *
     FGL0209P1O   E             65     PRINTER
      * Money market deals update report file
      *
     FGL0209AUO   E             66     PRINTER
      * Money market deal updates audit file
      *
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
      ** Copyright statement array
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
      *
     IDSFDY     E DSDSFDY
      ** Short DS for access programs
      *
     I              'Account Number'      C         BANK
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  #EENIT - Perform initial processing.                         *
      *  #EEMAN - Perform main processing via mode 1, 2 or 3 (DEALSDC)*
      *  #EEMA2 - Perform main processing via mode 1, 2 or 3 (MMDELDPP)
      *  #EEND  - Commit files and exit program.                      *
      *                                                               *
      *****************************************************************
      *          C O N T R O L      M O D U L E                       *
      *****************************************************************
      * Entry parameters
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
     C                     EXSR #EEINT
     C                     EXSR #EEMAN
     C                     EXSR #EEMA2
     C                     EXSR #EEEND
      *
      *****************************************************************
      * #EEMAN - Main Subroutine  (DEALSDC)                           *
      * Called by: - Control.                                         *
      *****************************************************************
     C           #EEMAN    BEGSR
      *
      ** Access first record on LF/DEALS
     C                     READ DEALS                    80
      *
      ** If records on file print report heading.
     C           MODE      IFEQ '3'
     C                     MOVE *ON       *IN61
     C                     ELSE
     C                     MOVE *ON       *IN60
     C                     ENDIF
     C           *IN80     IFEQ *OFF
     C                     WRITEHEADER
      *
      ** If no records on file print audit
     C                     ELSE
     C                     WRITEDBHEAD
     C                     WRITENOREC
     C                     ENDIF
      *
      ** Do while records on file.
     C           *IN80     DOWEQ*OFF
      *
      ** Save value for Tax Indicator
     C                     MOVE TAXI      #ETAXI  1
      *
      ** If Tiered Withhilding Tax Feature is switched ON
      ** Mode is '1' or '2'.
     C           MODE      IFEQ '1'
     C           MODE      OREQ '2'
      *
     C           #ETAXI    IFEQ 'Y'
     C           #ETAXI    OREQ '1'                                                         MD038105
     C                     MOVE '1'       TAXI
     C                     ELSE
     C                     MOVE '0'       TAXI
     C                     ENDIF
      *
     C                     ENDIF
      ** If Tiered Withholding Tax Feature is switched OFF(mode is'3')
     C           MODE      IFEQ '3'
      *
      ** If Tax Indicator is '0' set to 'N' else set to 'Y'
      *
     C           #ETAXI    IFEQ '0'
     C           #ETAXI    OREQ 'N'                                                         MD038105
     C                     MOVE 'N'       TAXI
     C                     ELSE
     C                     MOVE 'Y'       TAXI
     C                     ENDIF
     C                     ENDIF
      ** Write before &  after details to report and update PF/SDDEALPD
     C                     WRITEDETAIL
     C                     UPDATDEALSFMT
     C                     READ DEALS                    80
      *
     C                     ADD  1         RECTOT
      *
      ** If overflow indicator on reprint header
     C           *IN65     IFEQ *ON
     C                     WRITEHEADER
     C                     MOVE *OFF      *IN65
     C                     ENDIF
     C                     ENDDO
      *
      ** Print Footer
     C           RECTOT    IFGT 0
     C                     WRITEFOOTER
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      * #EEMA2 - Main Subroutine  (MMDELDPP)                          *
      * Called by: - Control.                                         *
      *****************************************************************
     C           #EEMA2    BEGSR
      *
      ** Access first record on LF/MMDELDL2
     C                     READ MMDELDL2                 80
      *
      ** Do while records on file.
     C           *IN80     DOWEQ*OFF
      *
      ** Save value for Tax Indicator
     C                     MOVE HKMTAX    #ETAXI  1
      *
      ** If Tiered Withhilding Tax Feature is switched ON
      ** Mode is '1' or '2'.
     C           MODE      IFEQ '1'
     C           MODE      OREQ '2'
      *
     C           #ETAXI    IFEQ 'Y'
     C           #ETAXI    OREQ '1'                                                         MD038105
     C                     MOVE '1'       HKMTAX
     C                     ELSE
     C                     MOVE '0'       HKMTAX
     C                     ENDIF
      *
     C                     ENDIF
      ** If Tiered Withholding Tax Feature is switched OFF(mode is'3')
     C           MODE      IFEQ '3'
      *
      ** If Tax Indicator is '0' set to 'N' else set to 'Y'
      *
     C           #ETAXI    IFEQ '0'
     C           #ETAXI    OREQ 'N'                                                         MD038105
     C                     MOVE 'N'       HKMTAX
     C                     ELSE
     C                     MOVE 'Y'       HKMTAX
     C                     ENDIF
     C                     ENDIF
      *
     C                     UPDATMMDELDD0
     C                     READ MMDELDL2                 80
      *
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************
      * #EEINT - Initial processing                                   *
      * Called by: - Control Module.                                  *
      *****************************************************************
     C           #EEINT    BEGSR
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      ** Retrieve Run Date & User Report Title via AOBANKR0
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If error occured, terminate the program
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVELBANK      DBKEY
     C                     Z-ADD01        DBASE
     C                     MOVEL'GL0209'  DBPGM
      *
     C           MODE      IFEQ '3'
     C                     MOVE *ON       *IN61
     C                     ELSE
     C                     MOVE *ON       *IN60
     C                     ENDIF
      *
     C                     WRITEDBHEAD
     C                     WRITEDBERR
     C                     EXSR #EEEND
     C                     END
      *
      ** Initialise record counter
     C                     Z-ADD0         RECTOT  40
     C                     ENDSR
      *
      *****************************************************************
      * #EEEND - Exit program                                         *
      * Called by: - Control.                                         *
      *****************************************************************
     C           #EEEND    BEGSR
      *
      ** Exit program
     C                     SETON                     LR
     C                     RETRN
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
** CPY@ - Object copyright
(c) Misys International Banking Systems Ltd. 2006
