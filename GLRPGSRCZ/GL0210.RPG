     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Set up Tax indicator on accounts utility')             *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL0210 - Set Up Tax Indicator on Accounts Utility            *
      *                                                               *
      *  Function - In modes 1 & 2 sets on Tax/Non Tax indicator to   *
      *             '0' FOR 'N' AND '1' FOR 'Y'.                      *
      *             In mode 3 Tax/Non Tax indicator set to 'N' if '0' *
      *             otherwise it is set to 'Y'.                       *
      *             Report produced displaying details before & after *
      *             feature is switched ON/OFF.                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. CGL165             Date 17Feb15               *
      *  Prev Amend No. CER055             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL013  *CREATE    Date 08Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CER055 - Penalty Interest on Exceeding Overdraft Limit       *
      *           (Recompile)                                         *
      *  CDL013 - Tiered Withholding Tax                              *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Function of Indicators                                       *
      *  ----------------------                                       *
      *                                                               *
      *     60 - PRINT REPORT OVERFLOW INDICATOR                      *
      *     61 - PRINT REPORT OVERFLOW INDICATOR                      *
      *                                                               *
      *     80 - NO RECORD FOUND ON DEALS                             *
      *                                                               *
      *     LR - LAST RECORD INDICATOR                                *
      *                                                               *
      *****************************************************************
      *
     FACCNTBY0UF  E           K        DISK
      * Account Details
      *
     FGL0210P1O   E             65     PRINTER
      * Money market deals update report file
      *
     FGL0210AUO   E             66     PRINTER
      * Money market deal updates audit file
      *
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
      ** Copyright statement array
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      *
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
      *
     IDSFDY     E DSDSFDY
      ** Short DS for access programs
      *
     I              'Account Number'      C         BANK
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  #EENIT - Perform initial processing.                         *
      *  #EEMAN - Perform main processing via mode 1, 2 or 3.         *
      *  #EEND  - Commit files and exit program.                      *
      *                                                               *
      *****************************************************************
      *          C O N T R O L      M O D U L E                       *
      *****************************************************************
      * Entry parameters
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
     C                     EXSR #EEINT
     C                     EXSR #EEMAN
     C                     EXSR #EEEND
      *
      *****************************************************************
      * #EEMAN - Main Subroutine                                      *
      * Called by: - Control.                                         *
      *****************************************************************
     C           #EEMAN    BEGSR
      *
      ** Access first record on LF/ACCNTBY0
     C                     READ ACCNTBY0                 80
      *
      ** If records on file print report heading.
     C           MODE      IFEQ '3'
     C                     MOVE *ON       *IN61
     C                     ELSE
     C                     MOVE *ON       *IN60
     C                     ENDIF
     C           *IN80     IFEQ *OFF
     C                     WRITEHEADER
      *
      ** If no records on file print audit
     C                     ELSE
     C                     WRITEDBHEAD
     C                     WRITENOREC
     C                     ENDIF
      *
      ** Do while records on file.
     C           *IN80     DOWEQ*OFF
      *
      ** Save value for Tax Indicator
     C                     MOVE ATITAX    #ETAXI  1
      *
      ** If Tiered Withhilding Tax Feature is switched ON
      ** Mode is '1' or '2'.
     C           MODE      IFEQ '1'
     C           MODE      OREQ '2'
      *
     C           #ETAXI    IFEQ 'Y'
     C                     MOVE '1'       ATITAX
     C                     ELSE
     C                     MOVE '0'       ATITAX
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** If Tiered Withholding Tax Feature is switched OFF(mode is'3')
     C           MODE      IFEQ '3'
      *
      ** If Tax Indicator is '0' set to 'N' else set to 'Y'
      *
     C           #ETAXI    IFEQ '0'
     C                     MOVE 'N'       ATITAX
     C                     ELSE
     C                     MOVE 'Y'       ATITAX
     C                     ENDIF
      *
     C                     ENDIF
      ** Write before & after details to report and update LF/ACCNTBY0
     C                     WRITEDETAIL
     C                     UPDATACCNTBD0
     C                     READ ACCNTBY0                 80
      *
     C                     ADD  1         RECTOT
      *
      ** If overflow indicator on reprint header
     C           *IN65     IFEQ *ON
     C                     WRITEHEADER
     C                     MOVE *OFF      *IN65
     C                     ENDIF
     C                     ENDDO
      *
      ** Print Footer
     C           RECTOT    IFGT 0
     C                     WRITEFOOTER
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      * #EEINT - Initial processing                                   *
      * Called by: - Control Module.                                  *
      *****************************************************************
     C           #EEINT    BEGSR
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      ** Retrieve Run Date & User Report Title via AOBANKR0
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If error occured, terminate the program
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVELBANK      DBKEY
     C                     Z-ADD01        DBASE
     C                     MOVEL'GL0210'  DBPGM
      *
     C           MODE      IFEQ '3'
     C                     MOVE *ON       *IN61
     C                     ELSE
     C                     MOVE *ON       *IN60
     C                     ENDIF
      *
     C                     WRITEDBHEAD
     C                     WRITEDBERR
     C                     EXSR #EEEND
     C                     END
      *
      ** Initialise record counter
     C                     Z-ADD0         RECTOT  40
     C                     ENDSR
      *
      *****************************************************************
      * #EEEND - Exit program                                         *
      * Called by: - Control.                                         *
      *****************************************************************
     C           #EEEND    BEGSR
      *
      ** Exit program
     C                     SETON                     LR
     C                     RETRN
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
** CPY@ - Object copyright
(c) Misys International Banking Systems Ltd. 2006
