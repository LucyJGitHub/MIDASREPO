     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Write history control records')
     F*****************************************************************
     F*                                                               *
     F*  Midas - General Ledger Module                                *
     F*                                                               *
     F*  GL0421 - Write History Control Records                       *
     F*                                                               *
     F*  Function:  This program writes records to CPOSTDD using the  *
     F*             parameters passed from GL0420                     *
     F*                                                               *
     F*  Called By: GL0420 - Account Statements                       *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 162946             Date 29Aug00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD005             Date 01MAR99               *
      *                 154040             Date 26APR99               *
      *                 CTI002             Date 23SEP98               *
     F*                 124083             DATE 20FEB98               *
     F*                 S01520  *CREATE    DATE 02NOV94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*  162946 - FIX TO PREVENT FILE CPOST OPENED AND CLOSED EACH    *
     F*           THIS PGM IS CALLED.                                 *
      *  CSD005 - Standing Data Account Narrative.  Add new field     *
      *           to SDACODPD.  Recompilation of programs.            *
     F*  154040 - Check also if the Account Posting Retention Period  *
     F*           APPLY FIX 131884 .                                  *
      *  CTI002 - Recompiled : File SDACODPD changed                  *
     F*  124083 - APPLY FIX 117318 (ONLY HEADER BOX UPDATED WHEN      *
     F*           FIX APPLIED PREVIOUSLY THEREFORE CODE ADDED NOW     *
     F*           UNDER THIS FIX NUMBER).                             *
     F*  117318 - CHECK ALSO IF THE ACCOUNT POSTING RETENTION PERIOD  *
     F*           FIELD IS ZERO BEFORE BYPASSING UPDATE OF CPOSTDD    *
     F*  S01520 - BLI Account Statements Changes.                     *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FCPOST   UF  E           K        DISK                      A
     F**  Historic Balances
     F*
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   20  - No record found in LF/CPOST                           *
     F*   21  - Update/Write error                                    *
     F* U7+U8 - Database error                                        *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  *PSSR  - Program Error Processing Subroutine                 *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
     E*
     E** Array containing Copyright statement
     E*
     I/EJECT
     I*
     I** Dummy Data Structures used by Access Programs.
     I*
     ISDACOD    E DSSDACODPD
     I** External data structures for Account Code Details
     I*                                                                   124083
     ISDGELR    E DSSDGELRPD                                              124083
     I              QQACCD                          ACCDQQ                                    CGL029
     I** External data structures for General Ledger Details              124083
     I*
     IDSFDY     E DSDSFDY
     I**  Short data structure for access objects
     I*
     IDSSDY     E DSDSSDY                                                                     CGL029
      **  Long data structure for access objects                                              CGL029
      *                                                                                       CGL029
     C/TITLE Main Processing
     C*================================================================
     C*  P R O G R A M   S T A R T                                    *
     C*================================================================
     C*
     C** Set up copyright parameter
     C*
     C                     MOVEACPY@      ACT@   80
     C*
     C** Define KLIST for CPOST
     C*
     C           KFLDA     KLIST
     C                     KFLD           PBRCA
     C                     KFLD           WCNUM
     C                     KFLD           PCCY
     C                     KFLD           WACOD
     C                     KFLD           WACSQ
     C                     KFLD           PRUND
     C*
     C** Receive parameter list
     C*
     C           *ENTRY    PLIST
     C                     PARM           PBRCA   3
     C                     PARM           PCNUM   6
     C                     PARM           PCCY    3
     C**********           PARM           PACOD   4                                           CGL029
     C                     PARM           PACOD  10                                           CGL029
     C                     PARM           PACSQ   2
     C                     PARM           PRUND   50
     C                     PARM           PPSTB  150
     C                     PARM           B#APRP  20                      154040
     C                     PARM           P@ERR   1
     C*
     C** Initialise error indicator
     C*
     C                     MOVE '0'       *IN21
     C*
     C** Get details of SDACODPD record using access object
     C*
     C                     CALL 'AOACODR0'
     C                     PARM *BLANKS   P@RTCD  7
     C                     PARM '*KEY   ' P@OPTN  7
     C**********           PARM PACOD     P@ACOD  4                                           CGL029
     C********** SDACOD    PARM SDACOD    DSFDY                                               CGL029
     C                     PARM PACOD     P@ACOD 10                                           CGL029
     C           SDACOD    PARM SDACOD    DSSDY                                               CGL029
     C*
     C** Check if an error occurred
     C*
     C           P@RTCD    IFNE *BLANKS
     C                     MOVE 'S'       P@ERR
     C                     EXSR *PSSR
     C                     END
     C*
     C** Process record if the Account code history retention period
     C***is*not*zero***                                                   124083
     C*  or the Account Posting Retention Period is not zero              124083
     C*
     C           A5ACPR    IFNE 0
     C           B#APRP    ORNE 0                                         154040
     C***********BKAPRP    ORNE *ZEROS                              124083154040
     C*
     C** Set up key fields
     C*
     C**********           MOVELPCNUM     WCNUM   60                                          CSD027
     C                     MOVELPCNUM     WCNUM   6                                           CSD027
     C**********           MOVELPACOD     WACOD   40                                          CGL029
     C                     MOVELPACOD     WACOD  100                                          CGL029
     C                     MOVELPACSQ     WACSQ   20
     C*
     C** Chain to CPOST using the details retrieved from the passed
     C** parameters
     C*
     C           KFLDA     CHAINCPOST                20
     C*
     C** If a record is found, update the Historic Carried Forward
     C** Balance by the balance retrieved from the passed parameters
     C*
     C           *IN20     IFEQ '0'
     C                     Z-ADDPPSTB     HCFB
     C                     UPDATCPOSTD0                21
     C*
     C** Else write a new record to CPOSTDD using the details passed
     C** to the program
     C*
     C                     ELSE
     C                     MOVEL'D'       RECI
     C                     MOVELPBRCA     BRCA
     C                     MOVELPCNUM     CNUM
     C                     MOVELPCCY      CCY
     C                     MOVELPACOD     ACOD
     C                     MOVELPACSQ     ACSQ
     C                     Z-ADDPRUND     PSTD
     C                     Z-ADDPPSTB     HCFB
     C                     WRITECPOSTD0                21
     C                     END
     C*
     C** If an update/write error has occured, update error field to
     C** indicate that an error has occured
     C*
     C           *IN21     IFEQ '1'
     C                     MOVE 'C'       P@ERR
     C                     EXSR *PSSR
     C                     END
     C*
     C                     END
     C*
     C*********            SETON                     LR                   162946
     C                     RETRN
     C*
     C*================================================================
     C*  P R O G R A M   E N D                                        *
     C*================================================================
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  *PSSR  - Error control subroutine                            *
     C*                                                               *
     C*  Called By: Main Processing                                   *
     C*                                                               *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR                           ** *PSSR **
     C*
     C** Check to see that *PSSR has not been called yet
     C*
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     END
     C*
     C** If *PSSR already run, then just end the program here
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C                     ENDSR
     C*
     C*****************************************************************
**  CPY@ - OBJECT COPYRIGHT
(c) Finastra International Limited 2001
