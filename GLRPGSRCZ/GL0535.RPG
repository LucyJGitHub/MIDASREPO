     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Nostro Events Extract')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL0535 - GL Nostro Events Extract                            *
      *                                                               *
      *  Function:  This program extracts all future valued manual    *
      *             postings for Nostro account types and outputs     *
      *             the records to the new GL Nostro events extract   *
      *             file PF/NOSEXGL.                                  *
      *                                                               *
      *  Called By: GLC0802C                                          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CSW037A            Date 02May05               *
      *                 BUG6198            Date 04Apr05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 196833             Date 16Aug01               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CGL010   *CREATE   Date 08Nov99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  BUG6198- Cannot save new Details for CUSD (Recompile)        *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  196833 - Additional SWIFT Address for CLS Nostros (Recompile)*
      *  CGL010 - Future Value Dated Journal Entries for Non-Retail   *
      *           Accounts.                                           *
      *                                                               *
      *****************************************************************
     FGLVALPL1IF  E           K        DISK         KINFSR *PSSR
      ** Future Postings
      *
     FSDNOSTL4IF  E           K        DISK         KINFSR *PSSR
     FSDCUSTL0IF  E           K        DISK         KINFSR *PSSR
      ** Nostro and Customer details
      *
     FNOSEXGL O   E                    DISK         KINFSR *PSSR
      ** Nostro Events Extract file
      *
     FNOSEXZZ UF  E                    DISK
      ** Nostro Events Extract trailer file
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   10  - Chain to SDNOSTL4                                     *
      *   11  - READE to GLVALPL1                                     *
      *   25  - Chain to SDCUSTL0                                     *
      *                                                               *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRINIT - Initialisation processing                           *
      *  SRDETL - Main processing                                     *
      *  SRTERM - Termination processing                              *
      *  ZHASH  - Hash totals                                         *
      *  ZZADD  - Add amounts to hash fields                          *
      *                                                               *
      *  *PSSR  - Error handling subroutine                           *
      *                                                               *
      *****************************************************************
      /EJECT
      *
      ** Array containing Copyright statement
      *
     E                    CPY@    1   1 80
      *
      ** Use in ZHASH operation
      *
     E                    INT         1 15 0             ZHASH integers
     E                    DEC         1  3 0             ZHASH decimals
      /SPACE 3
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     I/COPY ZSRSRC,ZHASHZ2
      *
     IOUTREC    E DSNOSEXGL
      ** Data extructure for clearing the output record NOSEXGLF
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ** Perform Initialisation.
      *
     C                     EXSR SRINIT
      *
      ** Perform Detail Processing.
      *
     C                     EXSR SRDETL
      *
      ** Termination processing
      *
     C                     EXSR SRTERM
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR                           *** SRINIT ***
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Set up keyfields for Nostro and Customer details
      *
     C           WRKKEY    KLIST
     C                     KFLD           A7CUST
     C                     KFLD           A7CYCD
     C                     KFLD           A7ACCD
     C                     KFLD           A7ACSN
     C                     KFLD           A7BRCD
      *
      ** Setup LDA
      *
     C           *NAMVAR   DEFN           LDA
     C                     MOVEL'GL0535  'DBPGM
      *
     C                     CLEAROUTREC
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRDETL
      *****************************************************************
      *                                                               *
      *  SRDETL - Detail processing (Generates event records)         *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRDETL    BEGSR                           *** SRDETL ***
      *
      ** Retrieve future valued manual postings from file.
      *
     C                     MOVE 'N'       WNOREC  1
      *
     C           'N'       SETLLGLVALPL1
     C           'N'       READE@VALPL1                  11
      *
      ** No Records on file
      *
     C           *IN11     IFEQ *ON
     C                     MOVE 'Y'       WNOREC
     C                     ENDIF
      *
      ** Generate events for all future valued postings
      *
     C           *IN11     DOWEQ*OFF
     C                     MOVE 'D'       RECI
     C                     MOVELV1SPOS    DLNO
     C                     MOVE 'ZZZZ'    ETYP
     C                     MOVE V1CNUM    CNUM
     C                     MOVE V1BRCA    BRCA
     C                     MOVE V1VALD    EDAT
     C                     MOVE V1VALD    FFVD
     C                     MOVE V1PSTA    EAMT
     C                     MOVE V1CCY     ECCY
      *
      ** Find the nostro number on SDNOSTL4 and get the customer name
      ** and town.
      *
     C                     MOVE V1CNUM    A7CUST
     C                     MOVE V1CCY     A7CYCD
     C                     MOVE V1ACOD    A7ACCD
     C                     MOVE V1ACSQ    A7ACSN
     C                     MOVE V1BRCA    A7BRCD
      *
     C           WRKKEY    CHAINSDNOSTL4             10
      *
     C           *IN10     IFEQ '0'
     C                     MOVE A7NONB    OSAC1
     C                     MOVELBBCRNM    CRNM
     C                     MOVELBBCRTN    CTWN
     C                     ELSE
     C                     MOVEL*BLANKS   JPNUM   6
     C                     MOVELV1CNUM    JPNUM
      *
     C           JPNUM     CHAINSDCUSTL0             25
      *
     C           *IN25     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCUSTL0'DBFILE           *************
     C                     MOVELJPNUM     DBKEY            * DBERR 001 *
     C                     Z-ADD001       DBASE            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVEL'00'      OSAC1
     C                     MOVELBBCRNM    CRNM
     C                     MOVELBBCRTN    CTWN
     C                     ENDIF
     C                     ENDIF
      *
      ** Inverse debit-credit indicator
      *
     C           V1DRCR    IFEQ 1
     C                     MOVE 'O'       INOI
     C                     ELSE
     C                     MOVE 'I'       INOI
     C                     ENDIF
      *
      ** Accumulate for hash totals
      *
     C                     ADD  1         WRCRIT  50
      *
     C                     Z-ADD1         S
     C                     Z-ADD1         Z
     C                     MOVE EAMT      ZZAMT
      *
     C                     EXSR ZHASH
     C                     Z-ADDZZTOTI    ZZAMT
     C                     MOVE ZZTOTD    ZZAMT
     C                     EXSR ZZADD
      *
     C                     WRITENOSEXGLF
      *
      ** Read next future posting
      *
     C                     READE@VALPL1                  11
      *
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRTERM
      *****************************************************************
      *                                                               *
      *  SRTERM - Termination processing                              *
      *                                                               *
      *  Called By: Main processing                                   *
      *                                                               *
      *  Calls:                                                       *
      *  ZHASH  - Hash Totals                                         *
      *                                                               *
      *****************************************************************
      *
     C           SRTERM    BEGSR                           *** SRTERM ***
      *
     C                     MOVE *ON       *INLR
      *
      ** File not empty, update trailer file
      *
     C           WNOREC    IFEQ 'N'
     C           1         CHAINNOSEXZZF             99
      *
     C                     Z-ADDINT,S     ZZTOTI
     C                     Z-ADDDEC,S     ZZTOTD
     C                     Z-ADDHRWN      ZZAMT
     C                     MOVE HRDC      ZZAMT
      *
     C                     EXSR ZZADD
      *
     C                     Z-ADDZZTOTI    HRWN
     C                     Z-ADDZZTOTD    HRDC
     C           NORE      ADD  WRCRIT    NORE
      *
     C                     UPDATNOSEXZZF
     C                     ENDIF
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ** Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     DUMP
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
     C/COPY ZSRSRC,ZHASHZ3
     C/COPY ZSRSRC,ZZADDZ1
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2001
