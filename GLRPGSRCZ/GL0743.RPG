     H
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Balances Sort 2  S/34,36 Only')               *
     F********************************************************************
     F*                                                                  *
     F*  Midas - General Ledger Module
     F*                                                                  *
     F*     GL0743 - SUMMARIZES LF/GLX13 TO PF/GLX14 FOR GL0750          *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      *  Last Amend No. 262907             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 LN0529             Date 9AUG90                *
     F*                 S01117             Date 28/02/89              *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
      *  262907 - FOOB GLC0730 and GLC0750 due to truncated balance   *
      *         - Increase the length of the LDBL from 15,0 to 18,0 to*
      *           make sure that balance will not be truncated.       *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*     LN0529  -  IF ONLY ONE COMPANY REQUESTED STOP AT END OF CO.  *
     F*     S01117  -  MULTIBRANCHING                                    *
     F*                                                                  *
     F********************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     FGLX13   IPE E           K        DISK
     FGLX14   O   E                    DISK
     F*
     F********************************************************************
     E                    CPY@    1   1 80
     I*              GLX13(SGLEY) INPUT FIELDS
     IGLEXGCF     01
     I**************CMPY                            CMPY  L1              S01117
     I              CMPYM                           CMPYM L1              S01117
     I              ASEC                            ASEC  L1
     I              TACD                            TACD  L1
     I              ACOD                            ACOD  L1
     I              CCY                             CCY   L1
     I              DCIN                            DCIN  L1
     I*
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I********************************************************************
     C           *ENTRY    PLIST                                          LN0529
     C                     PARM           ENTITY  3                       LN0529
     C                     MOVEACPY@      BIS@   80
     C*
     C           *IN10     IFEQ '0'
     C                     MOVEACPY@      BIS@   80
     C**********           Z-ADD0         TLDBL  150                                          262907
     C                     Z-ADD0         TLDBL  180                                          262907
     C*
     C                     SETON                     1011
     C                     END
     C*
     C           ENTITY    IFNE 'ALL'                                     LN0529
     C           ENTITY    IFNE CMPYM                                     LN0529
     C                     SETON                       LR                 LN0529
     C                     RETRN                                          LN0529
     C                     END                                            LN0529
     C                     END                                            LN0529
     C*
     C           TLDBL     ADD  LDBL      TLDBL
     C*
     CL1                   DO
     CL1                   Z-ADDTLDBL     LDBL
     C*
     CL1                   SETOF                     10
     CL1                   END
     C*
     C********************************************************************
     O*              GLX14(GLEX) OUTPUT FIELDS
     OGLX14F  T        L1 11
     O                         *ALL
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
