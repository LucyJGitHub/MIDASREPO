     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('GL Balances Sort for EMU - 2')                         *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL0746 - General Ledger Balances Sort for EMU - 2            *
      *                                                               *
      *  Function:  This program is used to sort records from         *
      *             LF/GLEXTRL5 to PF/GLX16 for GL0750.               *
      *                                                               *
      *  Called By: GLC76E- GL Balances in Cons. Euro Ccy- Br. GL lev.*
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. 262907             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU011  *CREATE    Date 11Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  262907 - FOOB GLC0730 and GLC0750 due to truncated balance   *
      *         - Increase the length of the LDBL from 15,0 to 18,0 to*
      *           make sure that balance will not be truncated.       *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CEU011 - EMU Position/Risk Reporting                         *
      *                                                               *
      *****************************************************************
      /EJECT
      *
     FGLEXTRL5IF  E           K        DISK
      ** GL Balances extract 05
      *
     FGLX16   O   E                    DISK
      ** GL Balances extract 16
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *                  FUNCTION OF INDICATORS                       *
      *                  ----------------------                       *
      *                                                               *
      *  70 - End of File on LF/GLEXTRL5                              *
      *  LR - End of Program                                          *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /EJECT
      *****************************************************************
      *
     C                     MOVEACPY@      MKI@   80
      ** Set up copyright parameter
      *
      /EJECT
      *
     C           *ENTRY    PLIST
     C                     PARM           PRENT   3
      *
     C           WKEY1     KLIST
     C                     KFLD           BRCA
     C                     KFLD           ASEC
     C                     KFLD           TACD
     C                     KFLD           ACOD
     C                     KFLD           ECSS
     C                     KFLD           CCY
     C                     KFLD           DCIN
      *
     C           PRENT     IFEQ 'ALL'
     C                     READ GLEXTRL5                 70
     C                     ELSE
      *
     C                     MOVE PRENT     BRCA
     C                     MOVE *LOVAL    ASEC
     C                     MOVE *LOVAL    TACD
     C                     MOVE *LOVAL    ACOD
     C                     MOVE *LOVAL    ECSS
     C                     MOVE *LOVAL    CCY
     C                     MOVE *LOVAL    DCIN
      *
     C           WKEY1     SETLLGLEXTRL5                 70
     C           BRCA      READEGLEXTRL5                 70
     C                     ENDIF
      *
     C           *IN70     IFEQ '0'
     C                     MOVELBRCA      WBRCA   3
     C                     MOVELASEC      WASEC   10
     C**********           MOVELTACD      WTACD   40                                          CGL029
     C**********           MOVELACOD      WACOD   40                                          CGL029
     C                     MOVELTACD      WTACD  100                                          CGL029
     C                     MOVELACOD      WACOD  100                                          CGL029
     C                     MOVELCCY       WCCY    3
     C                     MOVELDCIN      WDCIN   1
     C                     ENDIF
      *
     C           *IN70     DOWEQ'0'
     C           PRENT     ANDEQ'ALL'
     C           *IN70     OREQ '0'
     C           PRENT     ANDEQBRCA
      *
     C           BRCA      IFEQ WBRCA
     C           ASEC      ANDEQWASEC
     C           TACD      ANDEQWTACD
     C           ACOD      ANDEQWACOD
     C           CCY       ANDEQWCCY
     C           DCIN      ANDEQWDCIN
      *
     C**********           ADD  LDBL      TLDBL  150                                          262907
     C                     ADD  LDBL      TLDBL  180                                          262907
      *
     C                     ELSE
     C                     MOVELBRCA      WBRCA
     C                     MOVELASEC      WASEC
     C                     MOVELTACD      WTACD
     C                     MOVELACOD      WACOD
     C                     MOVELCCY       WCCY
     C                     MOVELDCIN      WDCIN
      *
     C                     READPGLEXTRL5                 70
      *
     C                     Z-ADDTLDBL     LDBL
     C                     WRITEGLX16F
     C**********           Z-ADD*ZEROS    TLDBL  150                                          262907
     C                     Z-ADD*ZEROS    TLDBL  180                                          262907
     C                     ENDIF
      *
     C                     READ GLEXTRL5                 70
      *
     C                     ENDDO
      *
     C           WBRCA     IFNE *BLANKS
     C                     READPGLEXTRL5                 70
     C                     Z-ADDTLDBL     LDBL
     C                     WRITEGLX16F
     C                     ENDIF
      *
     C                     MOVE '1'       *INLR
     C                     RETRN
      *****************************************************************
** CPY@   **      Object Copyright
(c) Finastra International Limited 2001
