     H
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Account details request (extract)')
      *****************************************************************
      *                                                               *
      *  Midas   GENERAL LEDGER MODULE                                *
      *                                                               *
      *   GL0781 - ACCOUNT MASTER DETAILS PROMPT                      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD031966           Date 29Jul15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      *                 CSF002             Date 11Aug03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 079767             Date 14JAN98               *
      *                   RT0027     DATE   9MAY91                    *
      *                   LN0493     DATE   6JUL90                    *
      *                   LN0281     DATE  29MAY90                    *
      *                   LN0280     DATE  29MAY90                    *
      *                   LN0117     DATE  03MAY90                    *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD031966 - BFM GL3572P1 issue                                *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *   CSF002 - Global layer.                                      *
      *            - Remove unused ZMITEM layout.                     *
      *   079767 - Account Master Details report in I/C cannot be run *
      *            using retail account numbers. Fix is to check for  *
      *            Retail installed in the system.                    *
      *   RT0027 - DO NOT ALLOW F6 UNLESS AT LEAST ONE RANGE ENTERED  *
      *   LN0493 - DO NOT ALLOW ANY FURTHER CHOICES ONCE AN 'ALL'     *
      *            ACCOUNTS REQUEST HAS BEEN MADE                     *
      *   LN0281 - DO NOT SHOW BRANCH IN SINGLE BRANCH SYSTEM         *
      *   LN0280 - SET UP BRCDFM IN SINGLE BRANCH CASE                *
      *   LN0117 - A/C CODE VALIDATION CHANGE                         *
      *                                                               *
      *****************************************************************
      *
     FGL0781DFCF  E                    WORKSTN
     F                                        RRN1  KSFILE GL0781S1
     F*
     F*CNUM***IF**E           K        DISK                           U1  079767
     FACNUM   IF  E           K        DISK                               079767
     F            ACCNTABF                          KRENAMEACNMB1
     FACCBR   IF  E           K        DISK
     F            ACCNTABF                          KRENAMEACBRB1
     FACCNT   IF  E           K        DISK
     F            ACCNTABF                          KRENAMEACNTB1
     FGL0781EXUF  E           K        DISK                      A
     F                                              KCOMIT
     F*
     F****************************************************************
     F*    FUNCTION OF INDICATORS.
     F*
     F* 03            CMD 3 TAKEN FROM DSPF-END PROGRAM
     F* 06            CMD 6 TAKEN FROM DSPF-END PROMPTING AND CONTINUE
     F* 12            CMD 12 TAKEN FROM DSPF-RETRN TO RCF INITIAL PROMP
     F*       21      ACCOUNT NO. FROM  FIELD  BLANK ON INPUT. (RANGA)
     F*       22      ACCOUNT NO. TO  FIELD  BLANK ON INPUT.   (RANGZ)
     F*       23      BRANCH CODE FIELD  BLANK ON INPUT.       (BRCDFM)
     F*       24      'ALL'  ENTERED  IN FIELD. (BLANK DEFAULTS TO ALL)
     F*   30          WORK INDICATOR FOR ARRAY LOKUP.
     F*   31          WORK INDICATOR FOR ARRAY LOKUP.
     F*          41   FULL KEY -- ALL 18 CHARS INPUT.
     F*          42   PARTIAL KEY -- 15 CHARS INPUT.
     F*          43   PARTIAL KEY -- 13 CHARS INPUT.
     F*          44   PARTIAL KEY --  9 CHARS INPUT.
     F*          45   PARTIAL KEY --  6 CHARS INPUT.
     F*          49   BRANCH IN A/C FILE IS VALID FOR USER.
     F*      50       SFLEND
     F*      51       SFLCLR
     F*      52       SFLDSPCTL
     F*      53       SFLDSP
     F*      55       ROLLUP
     F*         61    ERROR DETECTED.  DISPLAY MESSAGE IN RI.
     F*         62    ACCOUNT NO.  FROM  INVALID.
     F*         63    ACCOUNT NO. TO     INVALID.
     F*         64    BRANCH  CODE FIELD INVALID.
     F*      70       END  OF  OUTPUT  LOOP. (SR-ACNTSR,ACNMSR,ACBRSR)
     F*      72       CHAIN  TO  ACCOUNT MASTER FAILS. (ACCNT/ACCBR)
     F*      73       CHAIN  TO  ACCOUNT MASTER FAILS. (ACNUM/ACCBR)
     F*      74       VALID RECORD FOUND IN FILE FOR PARTIAL KEY.
     F*      79       END-OF-FILE REACHED (ACNUM/ACCBR/ACCNT)
     F*      80       MULTI BRANCH SYSTEM
     F*      81       USED FOR DELET SUBR AS INDICATOR ON READE
     F*      82       OPTION 6                                            LN0493
     F*      83       OPTION 5                                            LN0493
     F*       98      DATE FORMAT  -- ON  =  MMDDYY
     F*       99      ANY OF THE ABOVE ERRORS EXIST.
     F*          U1   ACNUM EXISTS   (RETAIL MODULE?)
     F*          U7   DATA BASE ERROR :: INSTL. CTL REC. NOT FOUND
     F*          U8   DATA BASE ERROR.
     F*          LR   END  PROGRAM.
     F*
     F****************************************************************
     E                    CPY@    1   1 80               COPYRIGHT STATEMENT
     E*********           MSG     2   6 20               ERROR MESSAGES   RT0027
     E                    MSG     2   7 30               ERROR MESSAGES   RT0027
     E                    ARR        15  1               WORK ARRAY
     E                    COD     5   5  2 0             PART KEY SIZE
     E                    C36    36  36  1               A-Z, 0-9                 MD031966
     E                    C26    26  26  1               A-Z                      MD031966
     E****************************************************************
     IACNTB1
     I              ZZ004                           ZZ004A
     IACBRB1
     I              ZZ004                           ZZ004B
     I*****************************************************
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*    DATA  AREA FOR  DBASE ERROR MESSAGE.
     I*    LOCAL DATA AREA
     I*
     ILDA        UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I**   RETRIEVE WSID FROM PROGRAM STATUS DATA STRUCTURE
     IPSDS       SDS
     I                                      244 246 WSID
     I                                      245 246 WSID2
     I                                      244 253 SWSID
     I                                      254 263 USRID
     I                                      276 2810DATE                          MD031966
     I                                      282 2870TIME                          MD031966
     I**   DATA STRUCTURE  FOR  THE SUBFILE  DETAILS.
     ISFDET       DS
     I                                        1  15 FR
     I                                       17  31 TU
     I                                       33  35 BR
     I**   DS FOR RANGE COMPARISION OF  ACCBR
     IACBRTO      DS
     I                                        1   3 BR1TO
     I                                        4   9 CU1TO
     I                                       10  12 CY1TO
     I**********                             13  16 AC1TO                                     CGL029
     I**********                             17  18 SE1TO                                     CGL029
     I                                       13  22 AC1TO                                     CGL029
     I                                       23  24 SE1TO                                     CGL029
     I**   DS FOR RANGE COMPARISION OF  ACCNT
     IACNTTO      DS
     I                                        1   6 CU2TO
     I                                        7   9 CY2TO
     I**********                             10  13 AC2TO                                     CGL029
     I**********                             14  15 SE2TO                                     CGL029
     I**********                             16  18 BR2TO                                     CGL029
     I                                       10  19 AC2TO                                     CGL029
     I                                       20  21 SE2TO                                     CGL029
     I                                       22  24 BR2TO                                     CGL029
     I**   DS FOR  ACCOUNT  DETAILS :: PART OF KEY.
     IPARTK       DS
     I**********                              1   60CUSN                                      CSD027
     I                                        1   6 CUSN                                      CSD027
     I                                        7   9 CURN
     I**********                             10  130ACCU                                      CGL029
     I**********                             14  150SEQN                                      CGL029
     I                                       10  190ACCU                                      CGL029
     I                                       20  210SEQN                                      CGL029
     I***EXTERNAL*DATA*AREA*OF*MENU*ITEM*REQUESTED******************************              CSF002
     I*ZMITEM***  UDS                                                                         CSF002
     I**********                              2   50ZMGRP                                     CSF002
     I**********                              6   9 ZICDE                                     CSF002
0276 I*
     I* DATA STRUCTURES FOR USE WITH ACCESS OBJECTS
     I*
     ISDBANK    E DSSDBANKPD
     I              BJRDNB                          RUND
     I              BJSUC                           DATF
     I              BJMRDT                          RUNA
     I*
     ISDMMOD    E DSSDMMODPD
     I              BGMBIN                          MBIN
     I*
     ISDBRCH    E DSSDBRCHPD
     I*
     I*
     IDSFDY     E DSDSFDY                                                 S01194
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01194
     I*
     IUUSEQ       DS                                                              MD031966
     I                                        1   1 SEQNO1                        MD031966
     I                                        2   2 SEQNO2                        MD031966
     I                                        3   3 SEQNO3                        MD031966
     I                                        4   4 SEQNO4                        MD031966
     I                                        5   5 SEQNO5                        MD031966
     I*                                                                           MD031966
     C           *ENTRY    PLIST
     C                     PARM           SEQNO   5
     C                     PARM           LEVEL   1
     C                     PARM           BRCDFM  3
     C                     PARM           SPARM 100
     C                     PARM           ACTION  1
     C                     PARM           ACTCMD  1
     C****   NOTE THAT AS THE UNDERLYING REPORT CAN BE RUN
     C****    BOTH DURING INPUT CYCLE AND CLOSE OF BUSINESS
     C****    ONLY INSERT ACTION CODE IS ALLOWED - REPORT MAY
     C****    ALREADY BE RUNNING IF AMENDMENT OF INPUT CYCLE
     C****    REQUEST WERE PERMITTED.
     C*
     C*****************************************************
     C**           INDEX  TO  SUBROUTINES
     C**
     C**    1.  CHECK  :::  VALIDATION OF INPUT DATA.
     C**    2.  NONRET :::  VALIDATION FOR NON-RETAIL A/C #.
     C**    3.  SETKEY :::  SETS KEY NUMBER FROM INPUT VALUES.
     C**    4.  FSEL   :::  VALIDATES INPUT AND SETS FILE NO.
     C**    5.  FSELNT :::  SETS FILE NO. FOR A PARTIAL KEY (ACCNT).
     C**    6.  FRVAL  :::  VALIDATES RANGA FIELD.
     C**    7.  FSELBR :::  SETS FILE NO. FOR A PARTIAL KEY (ACCBR).
     C**    8.  TOVAL  :::  VALIDATES RANGZ FIELD.
     C**    9.  INIT   :::  INITIALIZES ALL WORK/ KEY FIELDS.
     C**   10.  RETAIL :::  VALIDATION FOR A RETAIL A/C #.
     C**   11.  DOJOB  :::  CALLS APPROPRIATE SR. FOR EXTRACTION.
     C**   12.  FILSF  :::  FILLS SUBFILE WITH A/C #S REQUESTED.
     C**   13.  CLRSF  :::  CLEARS SUBFILE AND WRITES HEADER RECORD.
     C**   14.  DELET  :::  DELETES RECORDS FOR THIS REQUEST ON CMD3/12
     C**
     C*****************************************************
     C**
     C**   PARTIAL KEY SETUP FOR FILE ACCBR :::
     C**
     C**   |BRC|CUSTNO|CUR|ACOD|SE|   (ACBRF1/ACBRT1= #1)
     C**   |BRC|CUSTNO|CUR|ACOD|      (ACBRF2/ACBRT2= #2)
     C**   |BRC|CUSTNO|CUR|           (ACBRF3/ACBRT3= #3)
     C**   |BRC|CUSTNO|               (ACBRF4/ACBRT4= #4)
     C**   |BRC|                      (ACBRF5/ACBRT5= #5)
     C**
     C**   KFLD  DESCRIPTIONS FILE  ACCBR  *** FROM ***
     C           ACBRF1    KLIST
     C                     KFLD           BR1F    3
     C**********           KFLD           CU1F    60                                          CSD027
     C                     KFLD           CU1F    6                                           CSD027
     C                     KFLD           CY1F    3
     C**********           KFLD           AC1F    40                                          CGL029
     C                     KFLD           AC1F   100                                          CGL029
     C                     KFLD           SE1F    20
     C           ACBRF2    KLIST
     C                     KFLD           BR1F    3
     C**********           KFLD           CU1F    60                                          CSD027
     C                     KFLD           CU1F    6                                           CSD027
     C                     KFLD           CY1F    3
     C**********           KFLD           AC1F    40                                          CGL029
     C                     KFLD           AC1F                                                CGL029
     C           ACBRF3    KLIST
     C                     KFLD           BR1F    3
     C**********           KFLD           CU1F    60                                          CSD027
     C                     KFLD           CU1F    6                                           CSD027
     C                     KFLD           CY1F    3
     C           ACBRF4    KLIST
     C                     KFLD           BR1F    3
     C**********           KFLD           CU1F    60                                          CSD027
     C                     KFLD           CU1F    6                                           CSD027
     C           ACBRF5    KLIST
     C                     KFLD           BR1F    3
     C**
     C**   KFLD  DESCRIPTION  FILE  ACCBR  *** TO   ***
     C           ACBRT1    KLIST
     C                     KFLD           BR1T    3
     C**********           KFLD           CU1T    60                                          CSD027
     C                     KFLD           CU1T    6                                           CSD027
     C                     KFLD           CY1T    3
     C**********           KFLD           AC1T    40                                          CGL029
     C                     KFLD           AC1T   100                                          CGL029
     C                     KFLD           SE1T    20
     C           ACBRT2    KLIST
     C                     KFLD           BR1T    3
     C**********           KFLD           CU1T    60                                          CSD027
     C                     KFLD           CU1T    6                                           CSD027
     C                     KFLD           CY1T    3
     C**********           KFLD           AC1T    40                                          CGL029
     C                     KFLD           AC1T                                                CGL029
     C           ACBRT3    KLIST
     C                     KFLD           BR1T    3
     C**********           KFLD           CU1T    60                                          CSD027
     C                     KFLD           CU1T    6                                           CSD027
     C                     KFLD           CY1T    3
     C           ACBRT4    KLIST
     C                     KFLD           BR1T    3
     C**********           KFLD           CU1T    60                                          CSD027
     C                     KFLD           CU1T    6                                           CSD027
     C           ACBRT5    KLIST
     C                     KFLD           BR1T    3
     C*****************************************************
     C**
     C**   PARTIAL KEY SETUP FOR FILE ACCNT :::
     C**   |CUSTNO|CUR|ACOD|SE|BRC|   (ACNTF1/ACNTT1= #1)
     C**   |CUSTNO|CUR|ACOD|SE|       (ACNTF2/ACNTT2= #2)
     C**   |CUSTNO|CUR|ACOD|          (ACNTF3/ACNTT3= #3)
     C**   |CUSTNO|CUR|               (ACNTF4/ACNTT4= #4)
     C**   |CUSTNO|                   (ACNTF5/ACNTT5= #5)
     C**
     C**   KFLD  DESCRIPTIONS FILE  ACCNT  *** FROM ***
     C           ACNTF1    KLIST
     C**********           KFLD           CU1F    60                                          CSD027
     C                     KFLD           CU1F    6                                           CSD027
     C                     KFLD           CY1F    3
     C**********           KFLD           AC1F    40                                          CGL029
     C                     KFLD           AC1F                                                CGL029
     C                     KFLD           SE1F    20
     C                     KFLD           BR1F    3
     C           ACNTF2    KLIST
     C**********           KFLD           CU1F    60                                          CSD027
     C                     KFLD           CU1F    6                                           CSD027
     C                     KFLD           CY1F    3
     C**********           KFLD           AC1F    40                                          CGL029
     C                     KFLD           AC1F                                                CGL029
     C                     KFLD           SE1F    20
     C           ACNTF3    KLIST
     C**********           KFLD           CU1F    60                                          CSD027
     C                     KFLD           CU1F    6                                           CSD027
     C                     KFLD           CY1F    3
     C**********           KFLD           AC1F    40                                          CGL029
     C                     KFLD           AC1F                                                CGL029
     C           ACNTF4    KLIST
     C**********           KFLD           CU1F    60                                          CSD027
     C                     KFLD           CU1F    6                                           CSD027
     C                     KFLD           CY1F    3
     C           ACNTF5    KLIST
     C**********           KFLD           CU1F    60                                          CSD027
     C                     KFLD           CU1F    6                                           CSD027
     C**
     C**   KFLD  DESCRIPTION  FILE  ACCNT  *** TO   ***
     C           ACNTT1    KLIST
     C**********           KFLD           CU1T    60                                          CSD027
     C                     KFLD           CU1T    6                                           CSD027
     C                     KFLD           CY1T    3
     C**********           KFLD           AC1T    40                                          CGL029
     C                     KFLD           AC1T                                                CGL029
     C                     KFLD           SE1T    20
     C                     KFLD           BR1T    3
     C           ACNTT2    KLIST
     C**********           KFLD           CU1T    60                                          CSD027
     C                     KFLD           CU1T    6                                           CSD027
     C                     KFLD           CY1T    3
     C**********           KFLD           AC1T    40                                          CGL029
     C                     KFLD           AC1T                                                CGL029
     C                     KFLD           SE1T    20
     C           ACNTT3    KLIST
     C**********           KFLD           CU1T    60                                          CSD027
     C                     KFLD           CU1T    6                                           CSD027
     C                     KFLD           CY1T    3
     C**********           KFLD           AC1T    40                                          CGL029
     C                     KFLD           AC1T                                                CGL029
     C           ACNTT4    KLIST
     C**********           KFLD           CU1T    60                                          CSD027
     C                     KFLD           CU1T    6                                           CSD027
     C                     KFLD           CY1T    3
     C           ACNTT5    KLIST
     C**********           KFLD           CU1T    60                                          CSD027
     C                     KFLD           CU1T    6                                           CSD027
     C**
     C**   KFLD  FOR FILE  ACNUM  *** FROM / TO ***
     C           ACNMF     KLIST
     C                     KFLD           ACN2F  100
     C           ACNMT     KLIST
     C                     KFLD           ACN2T  100
     C*****************************************************
     C**
     C**    * PROGRAM (MAIN RTN.)  BEGINS :::
     C**
     C**      I.   GET INSTALLATION CTL REC. IF NOT FOUND,
     C**           DATA BASE ERR. SETON  U7 & U8. SETON LR.
     C**           ACCESS SDBANKPD/ICD TO OBTAIN RUNDATE.
     C**
     C*
     C                     MOVEACPY@      BIS@   80
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANK    @RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C           @RTCD     IFNE *BLANK
     C                     SETON                     99
     C                     ELSE
     C                     MOVE RUNA      SRUNA
     C                     END
     C*
     C           *IN99     IFEQ '1'
     C                     SETON                     U7U8
     C                     MOVE '01'      DBASE                         **
     C                     MOVE 'SDBANKPD'DBFILE
     C                     GOTO JOBEND
     C                     END
     C*
     C**           ACCESS SDMMODPD/ICD TO OBTAIN MBIN
     C*
     C                     CALL 'AOMMODR0'
     C                     PARM *BLANK    @RTCD
     C                     PARM '*FIRST  '@OPTN
     C           SDMMOD    PARM SDMMOD    DSFDY
     C*
     C           @RTCD     IFNE *BLANK
     C                     SETON                     99
     C                     END
     C*
     C           *IN99     IFEQ '1'
     C                     SETON                     U7U8
     C                     MOVE '01'      DBASE                         **
     C                     MOVE 'SDMMODPD'DBFILE
     C                     GOTO JOBEND
     C                     END
     C           BGRTBN    IFEQ 'Y'                                       079767
     C           BGIOAC    OREQ 'Y'                                       079767
     C                     MOVE '1'       *INU1                           079767
     C                     END                                            079767
     C           MBIN      IFEQ 'Y'
     C                     MOVE '1'       *IN80
     C                     ELSE                                           LN0280
     C***   IN SINGLE BRANCH CASE SET BRCDFM (PASSED AS BLANK FROM RCF)   LN0280
     C***   TO SINGLE BRANCH CODE FROM  SDBANKPD                          LN0280
     C*********TO*'ALL'*                                                  LN0280
     C**********           MOVE 'ALL'     BRCDFM                          LN0280
     C                     MOVE BJSBRC    BRCDFM                          LN0280
     C                     END
     C**
     C**   SYSTEM DATE FORMAT : *IN98 = 1 IF MMDDYY.
     C           DATF      COMP 'M'                      98
     C**
     C**
     C**     II.   INITIALIZE SUBFILE & WORK/KEY FLDS
     C                     EXSR CLRSF
     C                     EXSR INIT
     C**
     C**    III.   DO LOOP TILL CMD 03,12 OR 06 ARE PRESSED:::
     C**
     C                     WRITEGL0781F1
     C           *IN06     DOUEQ'1'
     C           *IN03     OREQ '1'
     C           *IN12     OREQ '1'
     C**
     C**      1.  DISPLAY/ACCEPT INPUT FIELDS.
     C                     EXFMTGL0781F2
     C                     SETOF                     616263
     C                     SETOF                     64
     C**
     C**      2.  VALIDATE  INPUT.
     C           *IN03     IFNE '1'
     C********** *IN06     ANDNE'1'                                       RT0027
     C           *IN12     ANDNE'1'
     C           *IN82     ANDNE'1'                                       LN0493
     C           *IN83     ANDNE'1'                                       LN0493
     C                     EXSR CHECK
     C**
     C**      3.  WRITE OUT A/C RECORDS REQUESTED
     C**          IF NO ERROR AND DISPLAY SUBFILE
     C**          DETAILS.
     C           *IN06     IFNE '1'                                       RT0027
     C           ERROR     IFEQ 0
     C**
     C**      IF ALL ACCOUNTS FOR ALL BRANCHES HAVE BEEN REQUESTED
     C**
     C           OPTION    IFEQ '6'
     C                     SETON                     82
     C                     SETON                     84
     C                     MOVEL'ALL'     SPARM
     C                     ELSE
     C                     MOVEL'XXX'     SPARM
     C                     END
     C*
     C                     EXSR DOJOB
     C                     WRITEGL0781C1
     C                     ELSE
     C                     EXSR INIT
     C                     END
     C*
     C                     END                                            RT0027
     C                     END
     C                     END
     C**
     C**    IV.   END PROGRAM.
     C**
     C           JOBEND    TAG                             *** JOBEND ***
     C           *IN03     IFEQ '1'
     C                     MOVE 'E'       ACTCMD
     C                     EXSR DELET
     C                     END
     C           *IN12     IFEQ '1'
     C                     SETOF                     12
     C                     MOVE 'F'       ACTCMD
     C                     EXSR DELET
     C                     END
     C                     SETON                     LR
     C                     RETRN
     C**
     C**    *****   PROGRAM (MAIN RTN.)  ENDS.
     C**
     C*****************************************************
     C**    SUBROUTINE (01) :::  CHECK
     C**                    SCREEN  INPUT  VALIDATION
     C**
     C**---------------------------------------------------
     C           CHECK     BEGSR
     C**    INITIALIZE  WORK  FIELDS.
     C**
     C                     Z-ADD0         ERROR   10
     C                     Z-ADD0         X       10
     C                     MOVE ' '       OPTION  1
     C                     MOVE *BLANKS   ERR
     C                     SETOF                     24
     C*                                                                   RT0027
     C**    IF F6 TAKEN CHECK IF A RANGE HAS BEEN ENTERED                 RT0027
     C*                                                                   RT0027
     C           *IN06     IFEQ '1'                                       RT0027
     C*                                                                   RT0027
     C           RRN1      IFEQ 1                                         RT0027
     C                     SETOF                     06                   RT0027
     C                     Z-ADD7         X                               RT0027
     C                     GOTO VALERR                                    RT0027
     C                     ELSE                                           RT0027
     C                     GOTO VALEND                                    RT0027
     C                     END                                            RT0027
     C*                                                                   RT0027
     C                     END                                            RT0027
     C**
     C**    1.  SET INDICATORS 21 TO 24 ACCORDING TO INPUT.
     C**        IND.   ON WHEN:            OFF WHEN:
     C**
     C**        21     RANGA == *BLANKS    RANGA  NOT *BLKS
     C**        22     RANGZ == *BLANKS    RANGZ  NOT *BLKS
     C**        23     BRCDFM== *BLANKS    BRCDFM NOT *BLKS
     C**        24     'ALL' OR BLANK IN-  BRCDFM IS A SPECIFIC
     C**               BRCDFM FIELD        BRANCH CODE
     C**
     C           RANGA     COMP *BLANKS                  21
     C           RANGZ     COMP *BLANKS                  22
     C           BRCDFM    COMP *BLANKS                  23
     C  N23      BRCDFM    COMP 'ALL'                    24
     C   23                SETON                     24
     C**
     C**
     C**    2.  SET OPTION ACCORDING TO VALUES INPUT :::
     C**    OPT.   DESCRIPTION                  21 22 23 24
     C**
     C**      1.   ONE A/C # FOR ALL BRANCHES  A:0  1  1  1
     C**      2.   ONE A/C # FOR ONE BRANCH.     0  1  0  0
     C**      3.   A RANGE  FOR ALL BRANCHES   A:0  0  1  1
     C**      4.   A RANGE  FOR ONE BRANCH       0  0  0  0
     C**      5.   ONE BRANCH,  ALL A/C #S       1  1  0  0
     C**      6    ALL A/C #S,  ALL BRANCHES     1  1  0  1
     C**
     C  N21 22 24          MOVE '1'       OPTION
     C**
     C  N21N23N24
     CAN 22                MOVE '2'       OPTION
     C**
     C  N21N22 24          MOVE '3'       OPTION
     C**
     C  N24
     CANN21N22N23          MOVE '4'       OPTION
     C**
     C   21 22
     CANN23N24             MOVE '5'       OPTION
     C**
     C   21 22
     CANN23 24             DO
     C                     MOVE '6'       OPTION
     C                     GOTO VALEND
     C                     END
     C**
     C**    3.  IF OPTION CANNOT BE DETERMINED, ERROR.
     C**        SET CONDITIONS TO REFLECT ERROR & EXIT.
     C**
     C           OPTION    IFEQ ' '
     C                     Z-ADD1         X
     C                     GOTO VALERR
     C                     END
     C**
     C**    4.  MOVE INPUT FIELDS TO WORK FIELDS.
     C**
     C                     MOVELRANGA     BLK15F
     C                     MOVELRANGZ     BLK15T
     C                     MOVEARANGA     ARR
     C**
     C**    5.  DETERMINE IF A/C # ENTERED IS RETAIL.
     C**        IF A RETAIL A/C NO., AND MODULE PRESENT (U1 ON),
     C**        VALIDATE FOR RETAIL A/C;  ELSE VALIDATE
     C**        FOR A NON-RETAIL A/C.
     C**
     C                     Z-ADD1         Z       20
     C           ' '       LOKUPARR,Z                    30
     C           Z         IFEQ 11
     C           *INU1     IFEQ '1'
     C                     EXSR RETAIL
     C                     ELSE
     C                     Z-ADD1         X
     C                     GOTO VALERR
     C                     END
     C                     ELSE
     C                     EXSR NONRET
     C                     END
     C**
     C           VALERR    TAG                             *** VALERR ***
     C           X         IFNE 0
     C                     SETON                     61
     C                     MOVEAMSG,X     ERR
     C                     ADD  1         ERROR
     C                     END
     C           VALEND    ENDSR
     C**
     C*****************************************************
     C**    SUBROUTINE (03) :::  NONRET
     C**                    NON-RETAIL ACCOUNT VALIDATION.
     C**        CHECK FOR A VALID PARTIAL KEY AND SET KEY
     C**        NUMBER. MOVE INPUT TO PROPER KEY FIELDS.
     C**
     C**---------------------------------------------------
     C           NONRET    BEGSR
     C**
     C**        1.   VALIDATE RANGA FIELD;
     C**             DETERMINE KEY NUMBER & IF KEYNUM IS
     C**             ZERO, ERROR IN INPUT.
     C**
     C                     Z-ADD0         X
     C                     MOVELRANGA     BLK15F
     C                     EXSR SETKEY
     C********** KEYNUM    IFEQ 0                                         LN0117
     C           KEYNUM    IFLE 0                                         LN0117
     C                     Z-ADD1         X
     C                     SETON                     62
     C                     ELSE
     C**
     C**        2. MOVE RANGA FIELD TO KEY FIELDS.
     C**
     C                     MOVELBLK15F    PARTK
     C                     MOVE BRCDFM    BR1F
     C                     MOVE CUSN      CU1F
     C                     MOVE CURN      CY1F
     C                     MOVE ACCU      AC1F
     C                     MOVE SEQN      SE1F
     C**
     C**        3. EXECUTE FRVAL TO CHECK IF THE A/C
     C**           EXISTS IN FILE. IF FIELD X IS NOT
     C**           ZERO, ERROR.
     C**
     C                     EXSR FRVAL
     C                     MOVE BRCA      ABRCA   3                       079767
     C           X         IFEQ 0
     C**
     C**        4.  VALIDATE RANGZ FIELD IF INPUT
     C**            AND RANGA FIELD HAS NO ERROR.
     C**
     C           *IN22     IFNE '1'
     C**
     C**        5.  DETERMINE KEY NUMBER & IF KEYNUM IS
     C**            ZERO OR GT KEYNUM OF RANGA FLD, ERROR.
     C**            FIELD,  ERROR.
     C**
     C                     Z-ADDKEYNUM    PREKEY  10
     C                     MOVEARANGZ     ARR
     C                     EXSR SETKEY
     C********** KEYNUM    IFEQ 0                                         LN0117
     C           KEYNUM    IFLE 0                                         LN0117
     C           KEYNUM    ORGT PREKEY
     C                     Z-ADD1         X
     C                     SETON                     63
     C                     GOTO NREND
     C                     END
     C**
     C**        6. MOVE RANGZ FIELD TO KEY FIELDS.
     C**
     C                     MOVELBLK15T    PARTK
     C                     END
     C                     MOVE BRCDFM    BR1T
     C                     MOVE CUSN      CU1T
     C                     MOVE CURN      CY1T
     C                     MOVE ACCU      AC1T
     C                     MOVE SEQN      SE1T
     C                     MOVE BRCDFM    BR1TO
     C                     MOVE BRCDFM    BR2TO
     C                     MOVE CUSN      CU1TO
     C                     MOVE CUSN      CU2TO
     C                     MOVE CURN      CY1TO
     C                     MOVE CURN      CY2TO
     C                     MOVE ACCU      AC1TO
     C                     MOVE ACCU      AC2TO
     C                     MOVE SEQN      SE1TO
     C                     MOVE SEQN      SE2TO
     C**
     C**        7. EXECUTE TOVAL TO CHECK IF THE A/C
     C**           EXISTS IN FILE. IF FIELD X IS NOT
     C**           ZERO, ERROR.
     C**
     C           *IN22     IFNE '1'
     C                     EXSR TOVAL
     C                     END
     C                     END
     C                     END
     C           NREND     ENDSR
     C**
     C*****************************************************
     C**      SUBROUTINE (04) ::: SETKEY
     C**          VALIDATES INPUT FIELDS FOR PARTIAL KEY
     C**          ENTRY, AND SETS KEY NUMBER & FILE NUMBER:
     C**          1= ACCNT,  2= ACCBR.
     C**
     C**   INPUT  VALUE TAKEN  FROM ARRAY ARR.
     C**
     C**  FLD ID.        KEY NUMBER WILL BE :::
     C**   1. CUST #     1 - ALL FLDS, INCL BRCD      *IN41
     C**   2. CURNCY     2 - ALL 4 FIELDS ENTERED.    *IN42
     C**   3. A/C #      3 - FIRST 3 FIELDS ENTERED.  *IN43
     C**   4. A/C SQ     4 - FIRST 2 FIELDS ENTERED.  *IN44
     C**                 5 - 1ST FIELD ONLY ENTERED.  *IN45
     C**
     C**   IF BRANCH CODE HAS ALSO BEEN ENTERED, KEY NUMBER
     C**   WILL BE DECREMENTED BY ONE. (REFER KLIST SETUP)
     C**
     C**---------------------------------------------------
     C           SETKEY    BEGSR
     C**   INITIALISE WORK FIELDS & INDICATORS.
     C                     SETOF                     414243
     C                     SETOF                     4445
     C                     Z-ADD1         FILNUM  10
     C                     Z-ADD0         KEYNUM  10
     C**
     C**   IF OPTION IS 5, SET KEYNUM TO 5 & FILNUM TO 2
     C           OPTION    IFEQ '5'
     C                     Z-ADD5         KEYNUM
     C                     Z-ADD2         FILNUM
     C                     SETON                     45
     C                     SETON                     8384                 LN0493
     C                     ELSE
     C**
     C**   ELSE, IF FULL KEY IS  INPUT, SET  KEYNUM TO 2
     C           ARR,15    IFNE ' '
     C                     Z-ADD2         KEYNUM  10
     C                     ELSE
     C**
     C**   IF INPUT IS A PARTIAL KEY, DETERMINE POSITION
     C**   OF FIRST BLANK CHARACTER.
     C                     Z-ADD1         Z       20
     C           ' '       LOKUPARR,Z                    30
     C**
     C**   CHECK IF THE POSITION OF THE BLANK CHARACTER
     C**   IS VALID FROM COD TABLE. IF NOT, KEYNUM == 0.
     C                     Z-ADD1         ZZ      10
     C           Z         LOKUPCOD,ZZ                   31
     C           *IN31     IFEQ '0'
     C                     Z-ADD0         KEYNUM
     C                     ELSE
     C**
     C**   IF PARTIAL KEY INPUT IS VALID, KEYNUM WILL BE
     C**   THE INDEX OF THE ARRAY COD.
     C                     Z-ADDZZ        KEYNUM
     C                     END
     C                     END
     C**
     C**   IF BRANCH CODE IS INPUT, RESET KEYNUM FOR ACCBR
     C**   FILE.
     C**
     C           *IN24     IFEQ '0'
     C                     SUB  1         KEYNUM
     C                     Z-ADD2         FILNUM
     C                     END
     C**
     C**   SETON INDICATOR (41 TO 45) ACCORDING TO THE
     C**   VALUE OF KEYNUM.
     C**
     C           KEYNUM    ADD  40        Z       20
     C                     MOVE '1'       *IN,Z
     C**
     C                     END
     C                     ENDSR
     C**
     C*****************************************************
     C**    SUBROUTINE (05) :::  FSEL
     C**        SELECT FILE WHICH HAS VALID KEY.
     C**        INPUT::: FIELD :: FILSEL  1 A
     C**                 FILSEL :: 1 = CHECK FROM KEYS
     C**                           2 = CHECK TO KEYS
     C**                           0 = ERROR.
     C**                 FILNUM :: 2 = ACCBR FILE
     C**                           1 = ACCNT FILE
     C**
     C**    RETURNS A VALUE  OF 1/2  (FIELD=FILE 1A )
     C**             IF  KEY EXISTS IN FILE: ACCNT/ACCBR
     C**             AND 0, IF THE KEY SPECIFIED IS INVALID
     C**
     C**    REQUIRES SUBROUTINES FSELBR(ACCBR) AND FSELNT
     C**      (ACCNT) IF PARTIAL KEYS ARE INPUT.  THESE 2
     C**      SRTNS. SET *IN75. IF OFF, KEY DOES NOT EXIST.
     C**
     C**---------------------------------------------------
     C           FSEL      BEGSR
     C**      1.  INITIALISE WORK FIELDS/INDICATORS.
     C**
     C                     SETOF                     727375
     C                     MOVE '0'       FILE    1
     C**
     C**      2.  IF ACCBR FILE IS SET (FILNUM==2)
     C**          IF  *IN41 IS 1       (KEYNUM==1)
     C**          CHAIN INTO FILE, ELSE CALL FSELBR.
     C**
     C           FILNUM    IFEQ 2
     C**
     C           *IN41     IFEQ '1'
     C           FILSEL    IFEQ '1'
     C   41      ACBRF1    CHAINACBRB1               72
     C                     ELSE
     C   41      ACBRT1    CHAINACBRB1               72
     C                     END
     C                     ELSE
     C                     EXSR FSELBR
     C   75                MOVE '2'       FILE
     C  N75                SETON                     72
     C                     END
     C**
     C**      3.  IF RECORD FOUND AND ACTIVE, SET FILE
     C**          FIELD TO 1.
     C**
     C           *IN72     IFEQ '0'
     C           RECI      ANDEQ'D'
     C                     MOVE '2'       FILE
     C                     END
     C                     ELSE
     C**
     C**      4.  IF ACCNT FILE IS SET (FILNUM==1)
     C**          IF  *IN41 IS 1       (KEYNUM==1)
     C**          CHAIN INTO FILE, ELSE CALL FSELNT.
     C**
     C           *IN41     IFEQ '1'
     C           FILSEL    IFEQ '1'
     C   41      ACNTF1    CHAINACNTB1               73
     C                     ELSE
     C   41      ACNTT1    CHAINACNTB1               73
     C                     END
     C                     ELSE
     C                     EXSR FSELNT
     C  N75                SETON                     73
     C   75                MOVE '1'       FILE
     C                     END
     C**
     C**      5.  IF RECORD FOUND AND ACTIVE, SET FILE
     C**          FIELD TO 2.
     C**
     C           *IN73     IFEQ '0'
     C           RECI      ANDEQ'D'
     C                     MOVE '1'       FILE
     C                     END
     C                     END
     C                     ENDSR
     C**
     C*****************************************************
     C**    SUBROUTINE (06) :::  FSELNT
     C**
     C**    TO FIND IF PARTIAL KEY EXISTS IN FILE - ACCNT.
     C**    INPUT :::    FIELD :: FILSEL  1 A
     C**                 FILSEL :: 1 = CHECK FROM KEYS
     C**                           2 = CHECK TO KEYS
     C**                           0 = ERROR.
     C           FSELNT    BEGSR
     C                     SETOF                     7475
     C**
     C**      1.  SETLL WITH THE KEY ACCORDING TO KEYNUM
     C**          (*IN42 TO *IN45)  (FROM/TO FIELD)
     C**
     C           FILSEL    IFEQ '1'
     C   42      ACNTF2    SETLLACNTB1
     C   43      ACNTF3    SETLLACNTB1
     C   44      ACNTF4    SETLLACNTB1
     C   45      ACNTF5    SETLLACNTB1
     C                     ELSE
     C   42      ACNTT2    SETLLACNTB1
     C   43      ACNTT3    SETLLACNTB1
     C   44      ACNTT4    SETLLACNTB1
     C   45      ACNTT5    SETLLACNTB1
     C                     END
     C**
     C**      2.  READ FILE TILL NO RECORDS WITH SAME KEY
     C**          FOUND OR A VALID RECORD IS FOUND.
     C**
     C           *IN74     DOUEQ'1'
     C           FILSEL    IFEQ '1'
     C   42      ACNTF2    READEACNTB1                   74
     C   43      ACNTF3    READEACNTB1                   74
     C   44      ACNTF4    READEACNTB1                   74
     C   45      ACNTF5    READEACNTB1                   74
     C                     ELSE
     C   42      ACNTT2    READEACNTB1                   74
     C   43      ACNTT3    READEACNTB1                   74
     C   44      ACNTT4    READEACNTB1                   74
     C   45      ACNTT5    READEACNTB1                   74
     C                     END
     C**
     C**      3.  IF A RECORD HAS BEEN READ, SHOULD BE
     C**          AN ACTIVE RECORD
     C**
     C           *IN74     IFEQ '0'
     C           RECI      ANDEQ'D'
     C           BRCDFM    IFEQ 'ALL'
     C           BRCA      OREQ BRCDFM
     C                     SETON                     7475
     C                     END
     C                     END
     C**
     C                     END
     C                     ENDSR
     C**
     C*****************************************************
     C**    SUBROUTINE (07) :::  FRVAL
     C**        VALIDATE  THE  RANGA FIELD.
     C**        FOR A  NON RETAIL A/C #.
     C**
     C**---------------------------------------------------
     C           FRVAL     BEGSR
     C**
     C**    SET WORK FIELDS -- X,FILSEL & RFROM
     C**
     C                     Z-ADD0         X
     C                     MOVE ' '       RFROM   1
     C                     MOVE '1'       FILSEL  1
     C**
     C**    EXECUTE FSEL.  IF FIELD FILE IS ZERO, ERROR.
     C**
     C                     EXSR FSEL
     C           FILE      IFEQ '0'
     C                     Z-ADD2         X
     C                     SETON                     62
     C           FILNUM    COMP 2                        64
     C                     ELSE
     C                     MOVE FILE      RFROM   1
     C                     END
     C                     ENDSR
     C**
     C*****************************************************
     C**    SUBROUTINE (08) :::  FSELBR
     C**        TO FIND IF PARTIAL KEY EXISTS IN ACCBR
     C**        INPUT :::    FIELD :: FILSEL  1 A
     C**                    FILSEL :: 1 = CHECK FROM KEYS
     C**                              2 = CHECK TO KEYS
     C**
     C**---------------------------------------------------
     C           FSELBR    BEGSR
     C                     SETOF                     7475
     C**
     C**      1.  SETLL WITH THE KEY ACCORDING TO KEYNUM
     C**          (*IN42 TO *IN45)  (FROM/TO FIELD)
     C**
     C           FILSEL    IFEQ '1'
     C   42      ACBRF2    SETLLACBRB1
     C   43      ACBRF3    SETLLACBRB1
     C   44      ACBRF4    SETLLACBRB1
     C   45      ACBRF5    SETLLACBRB1
     C                     ELSE
     C   42      ACBRT2    SETLLACBRB1
     C   43      ACBRT3    SETLLACBRB1
     C   44      ACBRT4    SETLLACBRB1
     C   45      ACBRT5    SETLLACBRB1
     C                     END
     C**
     C**      2.  READ FILE TILL NO RECORDS WITH SAME KEY
     C**          FOUND OR A VALID RECORD IS FOUND.
     C**
     C           *IN74     DOUEQ'1'
     C           FILSEL    IFEQ '1'
     C   42      ACBRF2    READEACBRB1                   74
     C   43      ACBRF3    READEACBRB1                   74
     C   44      ACBRF4    READEACBRB1                   74
     C   45      ACBRF5    READEACBRB1                   74
     C                     ELSE
     C   42      ACBRT2    READEACBRB1                   74
     C   43      ACBRT3    READEACBRB1                   74
     C   44      ACBRT4    READEACBRB1                   74
     C   45      ACBRT5    READEACBRB1                   74
     C                     END
     C**
     C**      3.  IF A RECORD HAS BEEN READ, A) SHOULD BE
     C**          AN ACTIVE RECORD, B) SHOULD BELONG TO
     C**          THE USER.
     C**
     C           *IN74     IFEQ '0'
     C           RECI      ANDEQ'D'
     C           BRCDFM    IFEQ 'ALL'
     C           BRCA      OREQ BRCDFM
     C                     SETON                     7475
     C                     END
     C                     END
     C                     END
     C                     ENDSR
     C**
     C*****************************************************
     C**    SUBROUTINE (09) :::  TOVAL
     C**        VALIDATE THE RANGZ FIELD:(NON RETAIL A/C)
     C**         A.  CHECK IF PRESENT IN THE ACCOUNTS FILE
     C**         B.  CHECK IF THE FILE CODE IS THE SAME AS
     C**             RANGA.
     C**         C.  CHECK IF THE RANGZ FIELD HAS GOT A
     C**             HIGHER VALUE THAN THE RANGZ FIELD.
     C**
     C**---------------------------------------------------
     C           TOVAL     BEGSR
     C**
     C**    SET WORK FIELDS -- X,FILSEL & RFROM
     C**
     C                     Z-ADD0         X
     C                     MOVE '2'       FILSEL  1
     C**
     C**    EXECUTE FSEL.  IF FIELD FILE IS ZERO, ERROR.
     C**
     C                     EXSR FSEL
     C           FILE      IFEQ '0'
     C                     Z-ADD2         X
     C           FILNUM    COMP 2                        64
     C                     SETON                     63
     C                     ELSE
     C**
     C**    IF RANGE FROM IS GREATER THAN RANGE TO, OR
     C**    IF FILE SET IS NOT THE SAME AS RFROM, ERROR
     C**
     C           BRCDFM    IFEQ 'ALL'                                     079767
     C                     MOVE *BLANKS   F18ACA 18                       079767
     C                     MOVE *BLANKS   F18ACZ 18                       079767
     C                     MOVE RANGA     F18ACA                          079767
     C                     MOVELABRCA     F18ACA                          079767
     C                     MOVE RANGZ     F18ACZ                          079767
     C                     MOVE BRCA      ZBRCA   3                       079767
     C                     MOVELZBRCA     F18ACZ                          079767
     C           FILE      IFNE RFROM
     C***********RANGA     ORGT RANGZ                                     079767
     C           F18ACA    ORGT F18ACZ                                    079767
     C                     Z-ADD3         X
     C                     SETON                     6362
     C                     END
     C                     ELSE                                           079767
     C           FILE      IFNE RFROM                                     079767
     C           RANGA     ORGT RANGZ                                     079767
     C                     Z-ADD3         X                               079767
     C                     SETON                     6362                 079767
     C                     END                                            079767
     C                     END                                            079767
     C                     END
     C                     ENDSR
     C**
     C*****************************************************
     C**    SUBROUTINE (10) :::  INIT
     C**        TO  INIT ALL KEY/  WORK  FIELDS.
     C**
     C**---------------------------------------------------
     C           INIT      BEGSR
     C**         COMP FLD FOR ACCNT **  *IN42 / 43 / 44
     C                     MOVE *BLANKS   ACNTC2 15
     C                     MOVE *BLANKS   ACNTC3 13
     C                     MOVE *BLANKS   ACNTC4  9
     C**         COMP FLD FOR  ACCBR **  *IN42 / 43 / 44
     C                     MOVE *BLANKS   ACBRC2 16
     C                     MOVE *BLANKS   ACBRC3 12
     C                     MOVE *BLANKS   ACBRC4  9
     C**
     C**********           MOVEL'00000000'BLK15F 15                                           CGL029
     C**********           MOVE '00000000'BLK15F                                              CGL029
     C**********           MOVEL'00000000'BLK15T 15                                           CGL029
     C**********           MOVE '00000000'BLK15T                                              CGL029
     C                     MOVE *ZEROS    BLK15F 21                                           CGL029
     C                     MOVE *ZEROS    BLK15T 21                                           CGL029
     C                     MOVE *BLANKS   SFDET
     C                     MOVE *BLANKS   ACBRTO
     C                     MOVE *BLANKS   ACNTTO
     C                     MOVE *BLANKS   CURN
     C                     MOVE *BLANKS   CY1F
     C                     MOVE *BLANKS   CY1T
     C                     MOVE *BLANKS   FILE
     C                     MOVE *BLANKS   TOFLD
     C**
     C**********           MOVE *ZEROS    CUSN                                                CSD027
     C                     MOVE *BLANKS   CUSN                                                CSD027
     C                     MOVE *ZEROS    ACCU
     C                     MOVE *ZEROS    SEQN
     C                     MOVE *BLANKS   BR1F
     C**********           MOVE *ZEROS    CU1F                                                CSD027
     C                     MOVE *BLANKS   CU1F                                                CSD027
     C                     MOVE *ZEROS    AC1F
     C                     MOVE *ZEROS    SE1F
     C                     MOVE *ZEROS    ACN2F
     C                     MOVE *ZEROS    ACN2T
     C                     MOVE *ZEROS    BR1T
     C**********           MOVE *ZEROS    CU1T                                                CSD027
     C                     MOVE *BLANKS   CU1T                                                CSD027
     C                     MOVE *ZEROS    AC1T
     C                     MOVE *ZEROS    SE1T
     C                     MOVE *BLANKS   ABRCA                           079767
     C                     MOVE *BLANKS   ZBRCA                           079767
     C                     ENDSR
     C**
     C*****************************************************
     C**      SUBROUTINE (11) :::   RETAIL
     C**          VALIDATES INPUT FIELDS IF RETAIL A/C
     C**          CHAIN INTO  ACNUM FILE.
     C**          VALIDATE INPUT FIELDS AGAINST ACNUM.
     C**
     C**---------------------------------------------------
     C           RETAIL    BEGSR
     C**
     C                     SETOF                     7372
     C                     Z-ADD0         X
     C                     MOVELBLK15F    ACN2F
     C**      1. IF A/C # IN FIELD RANGA NOT FOUND IN ACNUM
     C**         ERROR.
     C           ACNMF     CHAINACNUM                72
     C           *IN72     IFEQ '1'
     C           RECI      ORNE 'D'
     C                     Z-ADD2         X
     C                     SETON                     62
     C                     GOTO RETERR
     C                     END
     C**
     C**      2. IF A RANGE IS INPUT, CHECK IF A/C # IN THE
     C**         RANGZ FIELD EXISTS.
     C**         IF NOT MOVE RANGA VALUE TO 'TO' KEY FIELDS
     C**         AND EXIT.
     C           *IN22     IFEQ '1'
     C                     MOVELBLK15F    ACN2T
     C                     GOTO RETEND
     C                     END
     C**
     C                     MOVELBLK15T    ACN2T
     C           ACNMT     CHAINACNUM                73
     C           *IN73     IFEQ '1'
     C           RECI      ORNE 'D'
     C                     Z-ADD2         X
     C                     SETON                     63
     C                     GOTO RETERR
     C                     END
     C**      3. CHECK IF RANGZ IS GREATER THAN THE
     C**         RANGA FIELD.
     C           ACN2T     IFLT ACN2F
     C                     Z-ADD3         X
     C                     SETON                     6263
     C                     GOTO RETERR
     C                     END
     C**
     C           RETEND    TAG                             *** RETEND ***
     C           *IN72     IFEQ '0'
     C           *IN73     ANDEQ'0'
     C                     Z-ADD3         FILNUM  10
     C                     END
     C           RETERR    TAG                             *** RETERR ***
     C                     ENDSR
     C**
     C*****************************************************
     C**    SUBROUTINE (12) :::  DOJOB
     C**
     C**        TO EXTRACT RECORDS ACCORDING TO OPTION
     C**      FILE  TO  USE :::
     C**      FILNUM == 3  ACNUM    RETAIL A/C NO. ENTERED.
     C**      FILNUM == 2  ACCBR    PART KEY & BRCD OR ALL.
     C**      FILNUM == 1  ACCNT    FULL KEY & BRCD OR
     C**                            A/C NUMBER ONLY.
     C**
     C**---------------------------------------------------
     C           DOJOB     BEGSR
     C                     MOVE *BLANKS   TOFLD  18
     C                     MOVE *BLANKS   FRFLD  18
     C**
     C**       UPDATE SUBFILE WITH
     C**      I/O DETAILS & INITIALIZE ALL WORK/OUTPUT/
     C**      INPUT KEY FIELDS.
     C                     EXSR FILLSF
     C                     EXSR INIT
     C                     MOVE *BLANKS   RANGA
     C                     MOVE *BLANKS   RANGZ
     C                     ENDSR
     C**
     C*****************************************************
     C**    SUBROUTINE (13) :::  FILLSF
     C**        TO  ADD  ACCOUNT  NUMBERS  TO SUBFILE
     C**        AND WRITE TO EXTRACT FILE
     C**---------------------------------------------------
     C           FILLSF    BEGSR
     C**
     C                     Z-ADDRRN1      PGREC1
     C           *IN21     IFEQ '0'
     C                     MOVELRANGA     FR
     C                     ELSE
     C                     MOVEL'START'   FR
     C                     END
     C           *IN22     IFEQ '0'
     C                     MOVELRANGZ     TU
     C                     ELSE
     C                     MOVEL'END'     TU
     C                     END
     C***    ONLY SET UP BRANCH CODE FIELD IN MULTI BRANCH SYSTEM         LN0281
     C*********************MOVELBRCDFM    BR                              LN0281
     C   80                MOVELBRCDFM    BR                              LN0281
     C                     MOVE SFDET     SFOUT
     C                     WRITEGL0781S1
     C**
     C**     Use dummy sequence number                                            MD031966
     C*                                                                           MD031966
     C           SEQNO     IFEQ *BLANKS                                           MD031966
     C           ACTION    ANDEQ'I'                                               MD031966
     C                     EXSR SEQNOG                                            MD031966
     C                     MOVE UUSEQ     SEQNO   5                               MD031966
     C                     ENDIF                                                  MD031966
     C*                                                                           MD031966
     C                     WRITEGL0781E1
     C                     COMIT
     C                     ADD  1         RRN1
     C                     ENDSR
     C**
     C*****************************************************
     C**    SUBROUTINE (14) :::  CLRSF
     C**        CLEAR SUBFILE AND WRITE HEADER RECORD.
     C**
     C**---------------------------------------------------
     C           CLRSF     BEGSR
     C**
     C                     SETOF                     525355
     C                     SETON                     51
     C                     WRITEGL0781C1
     C                     SETOF                     51
     C                     SETON                     525350
     C                     Z-ADD1         PGREC1
     C                     Z-ADD1         RRN1    50
     C                     ENDSR
     C**
     C*****************************************************
     C**    SUBROUTINE (16) :::  DELET
     C**        DELET RECORDS FOR THIS REQUEST IF CMD 3
     C**        OR CMD 12 ARE TAKEN
     C**
     C**---------------------------------------------------
     C           DELET     BEGSR
     C**
     C           SEQNO     SETLLGL0781E1
     C           *IN81     DOWEQ'0'
     C           SEQNO     READEGL0781E1                 81
     C           *IN81     IFEQ '0'
     C                     DELETGL0781E1
     C                     COMIT
     C                     END
     C                     END
     C                     ENDSR
     C**
     C*****************************************************                       MD031966
     C**    SUBROUTINE (15) :::  SEQNOG                                           MD031966
     C**        GENERATE A DUMMY SEQUENCE NUMBER FOR THE                          MD031966
     C**        GL0781EX FILE                                                     MD031966
     C**---------------------------------------------------                       MD031966
     C           SEQNOG    BEGSR                                                  MD031966
     C**                                                                          MD031966
     C           BJDFIN    IFEQ 'M'                                               MD031966
     C                     MOVELDATE      DAY     20                              MD031966
     C                     ELSE                                                   MD031966
     C                     MOVELDATE      WMMDD   4                               MD031966
     C                     MOVE WMMDD     DAY     20                              MD031966
     C                     ENDIF                                                  MD031966
     C                     MOVELTIME      HOUR    20                              MD031966
     C                     MOVE TIME      MINSEC  40                              MD031966
     C*                                                                           MD031966
     C** Determine day of the month and hour of the day                           MD031966
     C**                                                                          MD031966
     C                     MOVEAC36,DAY   SEQNO1                                  MD031966
     C           HOUR      IFEQ 0                                                 MD031966     C
     C                     MOVE '0'       SEQNO2                                  MD031966
     C                     ELSE                                                   MD031966
     C                     MOVEAC26,HOUR  SEQNO2                                  MD031966
     C                     ENDIF                                                  MD031966
     C*                                                                           MD031966
     C** Determine the minute/second of the day in 0-[A-Z]                        MD031966
     C*                                                                           MD031966
     C* Position __X                                                              MD031966
     C           MINSEC    DIV  26        WNUM01  40                              MD031966
     C           WNUM01    DIV  26        W       40                              MD031966
     C           Y         IFEQ 0                                                 MD031966
     C                     MOVE '0'       SEQN33  1                               MD031966     C
     C                     ELSE                                                   MD031966
     C                     MOVEAC26,W     SEQN33                                  MD031966
     C                     ENDIF                                                  MD031966
     C* Position _X_                                                              MD031966
     C           WNUM01    DIV  26        WNUM02 128                              MD031966
     C           WNUM02    SUB  W         WNUM03  98                              MD031966
     C           WNUM03    MULT 26        Y       40                              MD031966
     C           Y         IFEQ 0                                                 MD031966
     C                     MOVE '0'       SEQN32  1                               MD031966
     C                     ELSE                                                   MD031966
     C                     MOVEAC26,Y     SEQN32                                  MD031966
     C                     ENDIF                                                  MD031966
     C* Position X__                                                              MD031966
     C           MINSEC    DIV  26        WNUM04 128                              MD031966
     C           WNUM04    SUB  WNUM01    WNUM05  98                              MD031966
     C           WNUM05    MULT 26        Q       40                              MD031966
     C           Q         IFEQ 0                                                 MD031966
     C                     MOVE '0'       SEQN31  1                               MD031966
     C                     ELSE                                                   MD031966
     C                     MOVEAC26,Q     SEQN31                                  MD031966
     C                     ENDIF                                                  MD031966
     C* Merge X__ + _X_ + __X                                                     MD031966
     C                     MOVE SEQN33    SEQNO3                                  MD031966
     C                     MOVE SEQN32    SEQNO4                                  MD031966
     C                     MOVE SEQN31    SEQNO5                                  MD031966
     C*                                                                           MD031966
     C                     ENDSR                                                  MD031966
     C**                                                                          MD031966
     C*****************************************************
**  CPY@
(c) Finastra International Limited 2001
** MSG
INVALID INPUT                 INVALID A/C NUMBER
INVALID RANGE                 BRANCH NOT ON TABLE
INVALID BRANCH                USER/BRANCH INVALID
F6 INVALID UNTIL ENTER PRESSED                                            RT0027
** COD
1916141007
**   C36 - COUNTING 36
ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789
**   C26 - COUNTING 26
ABCDEFGHIJKLMNOPQRSTUVWXYZ
