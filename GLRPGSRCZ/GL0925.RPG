     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Set Up File Controls')
      ********************************************************************
      *                                                                  *
      *  Midas - General Ledger Module                                *
      *                                                                  *
      *     GL0925 - SET UP GL FILE CONTROLS
      *                                                                  *
      *  (c) Finastra International Limited 2001                      *
      *                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 088176             Date 15Jun95               *
      *                 052250             Date 15Apr93               *
      *                    S10736             DATE 26OCT92               *
      *                                                                  *
      *------------------------------------------------------------------*
      *                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *      088176 - AOMMODR0 should be called with P@OPTN =  '*FREE'   *
      *               to prevent the use of old values from before Mgmt  *
      *               Accts was set up. The subsequent call will refresh *
      *               the file with the current value.                   *
      *      052250 - U7 & U8 set, on sucessful write and not on error   *
      *      S10736 - MANAGEMENT ACCOUNTS                                *
      *                                                                  *
      ********************************************************************
      *                                                                  *
      *  C R E A T I O N     P A R A M E T E R S                         *
      *                                                                  *
      *                                                                  *
      ********************************************************************
      *                                                               *
     FGLFCTLL0IF  E           K        DISK                      A
      *
      ********************************************************************
      *
     F*  INDICATOR USAGE                                              *
      *                                                               *
     F*  90   RECORD ALREADY EXISTS ON GLFCTLL0                       *
     F*  91   WRITE TO GLFCTLL0 FAILS                                 *
      *                                                               *
     E********************************************************************
      *                                                               *
     E                    CPY@    1   1 80
      /EJECT
     IA@CPY       DS
     I* Copyright array
     I                                        1  80 CPY@
     ILDA       E DSLDA                                                   S10736
      * Local data structure used for error handling                      S10736
     IDSMMOD    E DSSDMMODPD                                              S10736
      * Data structures for SDMMODPD access program                       S10736
     IDSFDY     E DSDSFDY                                                 S10736
      /EJECT
      *****************************************************************
      *                                                               *
      * SET UP WORK AND OUTPUT FIELDS
     C                     MOVEL*BLANK    WUFLNM 10
     C                     Z-ADD*ZERO     GFNORC
     C                     Z-ADD*ZERO     GFNOIN
     C                     Z-ADD*ZERO     GFNOAM
     C                     Z-ADD*ZERO     GFNODL
      *                                                                   088176
      * CALL AOMMODR0 '*FREE' TO CLOSE THE FILE READY TO REACQUIRE DATA   088176
     C                     CALL 'AOMMODR0'                                088176
     C                     PARM *BLANKS   P@RTCD  7                       088176
     C                     PARM '*FREE  ' P@OPTN  7                       088176
     C           DSMMOD    PARM DSMMOD    DSFDY                           088176
      *                                                                   S10736
      * CALL AOMMODR0 TO CHECK IF MANAGEMENT ACCOUNTS ARE ON THE SYSTEM   S10736
     C                     CALL 'AOMMODR0'                                S10736
     C                     PARM *BLANKS   P@RTCD  7                       S10736
     C                     PARM '*FIRST ' P@OPTN  7                       S10736
     C           DSMMOD    PARM DSMMOD    DSFDY                           S10736
      *                                                                   S10736
      * DB ERROR IF NO RECORD FOUND                                       S10736
     C           P@RTCD    IFNE *BLANKS                                   S10736
     C           *NAMVAR   DEFN           LDA                             S10736
     C           *LOCK     IN   LDA                                       S10736
     C                     MOVEL'AOMMODR0'DBFILE  8        ************   S10736
     C                     Z-ADD1         DBASE   30       * Error 01 *   S10736
     C                     MOVEL*BLANKS   DBKEY  29        ************   S10736
     C                     MOVEL'GL0925'  DBPGM  10                       S10736
     C                     OUT  LDA                                       S10736
     C                     SETON                     U7U8LR               S10736
     C                     RETRN                                          S10736
     C                     END                                            S10736
      *                                                                   S10736
      **CREATE*GENERAL*LEDGER*FILE*CONTROLS*RECORD*********************   S10736
      * CREATE GENERAL LEDGER FILE CONTROLS RECORDS                       S10736
     C                     MOVEL'GLSPHDPD'WUFLNM
     C                     EXSR CREATE
      *                                                                   S10736
     C                     MOVEL'GLPERSPD'WUFLNM                          S10736
     C                     EXSR CREATE                                    S10736
      *                                                                   S10736
     C           BGNVST    IFEQ 'Y'                                       S10736
     C                     MOVEL'GLHBCGPD'WUFLNM                          S10736
     C                     EXSR CREATE                                    S10736
     C                     END                                            S10736
      *
      * EXIT PROGRAM
     C                     SETON                     LR
     C                     RETRN
      *
      *****************************************************************
      /EJECT
     C           CREATE    BEGSR
      *
      * MOVE FILE NAME TO OUTPUT FIELD
     C                     MOVELWUFLNM    GFFLNM
      *
     C           KLCRNA    KLIST
     C                     KFLD           GFFLNM
      *
      * CHECK IF RECORD ALREADY EXISTS
     C           KLCRNA    SETLL@AHREAU                  90
      *
     C           *IN90     IFEQ '0'
     C                     WRITE@AHREAU                91
      *
     C************IN91     IFEQ '0'                                       ssssss
     C           *IN91     IFEQ '1'                                       ssssss
     C                     SETON                       U7U8
     C                     END
      *
     C                     END
      *
     CSR                   ENDSR
      *================================================================
      /EJECT
** CPY@     : Copyright notice for inclusion in all programs
(c) Finastra International Limited 2001
