     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Control audit list programs')
      ********************************************************************
      *                                                                  *
      *  Midas - General Ledger Module                                *
      *                                                                  *
      *     GL0930 - CONTROL AUDIT REPORTS LIST
      *                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *     LAST AMEND NO. XXXXXX             DATE XXXXXXX               *
      *                                                                  *
      *                                                                  *
      *------------------------------------------------------------------*
      *                                                                  *
      *      XXXXXX - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX              *
      *                                                                  *
      ********************************************************************
      *                                                                  *
      *  C R E A T I O N     P A R A M E T E R S                         *
      *                                                                  *
      *                                                                  *
      ********************************************************************
     F*  INDICATOR USAGE                                              *
     F*                                                               *
     F*  30    LOOK UP ON TABLE FAILED                                *
     F*  40    CALL TO AUDIT REPORT PROGRAM FAILED                    *
     F*  41    CALL TO SDC0310 FAILED                                 *
     F*  50    CHAIN TO GLFCTLL0 FAILED                               *
     F*  51    CHAIN TO GLFCTLL1 FAILED                               *
     F*  52    CHAIN TO SDBANKL1 FAILED                               *
     F*  60    OVERFLOW FOR REPORT                                    *
     F*****************************************************************
     FGL0930AUO   E             60     PRINTER
     FGLFCTLL1IF  E           K        DISK
     FGLFCTLL0UF  E           K        DISK
     FSDBANKL1IF  E           K        DISK
      /EJECT
     E                    TABFIL  1   1 10   TABPGM 10
     E                    MSG     1   3 23
     E                    CPY@    1   1 80
      ************************************************************
      /EJECT
     IA@CPY       DS
     I* Copyright array
     I                                        1  80 CPY@
     IMESSAG      DS
     I                                        1   5 TEXT
     I                                        6  23 FILE
     I*
     I** LDA FOR ERRORS
     I*
     ILDA        UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      ************************************************************
      /EJECT
     C*****************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7
     C                     PARM           SEQNO   5
     C                     PARM           LEVEL   1
     C                     PARM           ENTTY   3
     C*****************************************************************
     C* MAIN PROCESSING
     C*****************************************************************
     C*
     C** DEFINE THE LDA
     C*
     C           *NAMVAR   DEFN           LDA
     C                     IN   LDA
     C                     OUT  LDA
     C*
     C                     MOVEL'FILE'    TEXT
     C*
     C** OBTAIN THE BANK'S TITLE AND THE DATE
     C*
     C                     MOVEL'BANK'    BNKEY  10
     C           BNKEY     CHAIN@BJREEF              52
     C           *IN52     IFEQ '1'
     C                     MOVE *BLANKS   FILE
     C                     MOVEL'SDBANKL1'FILE
     C                     MOVE *BLANKS   DBCAUS
     C                     MOVE MESSAG    DBCAUS
     C                     MOVEL'SDBANKL1'SAFILE  8
     C                     Z-ADD1         SAASE   30
     C                     MOVELBNKEY     SAKEY  29
     C                     EXSR DBERR
     C                     ELSE
     C                     MOVE BJURPT    BANKT
     C                     MOVE BJMRDT    DATEA
     C                     END
     C                     WRITETITLE
     C*
     C           *LOVAL    SETLLGLFCTLL1               51
     C           *IN51     IFEQ '1'
     C                     MOVE MSG,1     DBCAUS
     C                     MOVEL'GLFCTLL1'SAFILE
     C                     Z-ADD1         SAASE
     C                     MOVEL'*LOVAL'  SAKEY
     C                     EXSR DBERR
     C                     ELSE
     C                     READ GLFCTLL1                 51
     C           *IN51     DOWEQ'0'
     C*
     C** FOR EACH RECORD ON THE FILE CHECK TO SEE IF AN AUDIT
     C** REPORT NEEDS TO BE PRODUCED
     C*
     C                     EXSR PROC
     C                     READ GLFCTLL1                 51
     C                     END
     C                     END
     C                     EXSR END
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE PROC                                              *
     C*  CHECKS THE FILE CONTROLS TO DETERMINE WHETHER AN AUDIT REPORT*
     C*  NEEDS TO BE PRODUCED                                         *
     C*                                                               *
     C*****************************************************************
     C           PROC      BEGSR
     C*
     C** IF THERE HAVE BEEN ANY CHANGES TO THE FILE THEN AN AUDIT
     C** REPORT MUST BE PRODUCED
     C*
     C           GFNOIN    IFNE 0
     C           GFNOAM    ORNE 0
     C           GFNODL    ORNE 0
     C                     EXSR FREPT
     C                     END
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*
     C*  SUBROUTINE FREPT
     C*  FIND THE REPORT THAT NEEDS TO BE CALLED
     C*
     C*****************************************************************
     C           FREPT     BEGSR
     C           GFFLNM    LOKUPTABFIL    TABPGM         30
     C           *IN30     IFEQ '0'
     C*
     C** ERROR OCCURRED WHILST LOOKING FOR AUDIT REPORT PROGRAM NAME
     C*
     C                     ELSE
     C                     SETOF                     U7U8
     C                     MOVE *BLANKS   PGM
     C                     MOVELTABPGM    PGM
     C                     CALL PGM                    40
     C                     PARM *BLANKS   P0RTN
     C                     PARM 'A'       AUREQ   1
     C                     PARM           SEQNO
     C                     PARM           LEVEL
     C                     PARM           ENTTY
     C           *IN40     IFEQ '1'
     C*
     C** ERROR OCCURRED ON CALL TO AUDIT REPORT (DATABASE)
     C*
     C                     MOVE *BLANKS   SAFILE
     C                     Z-ADD3         SAASE
     C                     MOVE *BLANKS   SAKEY
     C                     EXSR CALERR
     C                     ELSE
     C*
     C** CHECK THE JOB SWITCHES
     C*
     C                     EXSR CHECK
     C                     END
     C                     END
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE CHECK                                             *
     C*  CHECK THE JOB SWITCHES TO FIND OUT IF A DATABASE OR A FILE   *
     C*  CONTROL ERROR HAS OCCURRED IN THE AUDIT REPORT               *
     C*                                                               *
     C*****************************************************************
     C           CHECK     BEGSR
     C                     MOVE *BLANKS   PGM
     C                     MOVEL'SDC0310' PGM    10
     C                     CALL PGM                    41
     C                     PARM           U7STS   1
     C                     PARM           U8STS   1
     C           *IN41     IFEQ '1'
     C*
     C** CALL TO PROGRAM FAILED (DATABASE ERROR)
     C*
     C                     MOVE *BLANKS   SAFILE
     C                     Z-ADD4         SAASE
     C                     MOVE *BLANKS   SAKEY
     C                     EXSR CALERR
     C                     ELSE
     C           U8STS     IFEQ '1'
     C           U7STS     IFEQ '1'
     C*
     C** DATABASE ERROR OCCURRED
     C*
     C                     MOVE *BLANKS   FILE
     C                     MOVELGFFLNM    FILE
     C                     MOVE *BLANKS   DBCAUS
     C                     MOVE MESSAG    DBCAUS
     C                     MOVELGFFLNM    SAFILE
     C                     Z-ADD5         SAASE
     C                     MOVE *BLANKS   SAKEY
     C                     EXSR DBERR
     C                     ELSE
     C*
     C** FILE CONTROL ERROR OCCURRED
     C*
     C                     MOVE *BLANKS   SAFILE
     C                     Z-ADD6         SAASE
     C                     MOVE *BLANKS   SAKEY
     C                     EXSR FCERR
     C                     END
     C                     ELSE
     C                     EXSR RESET
     C                     END
     C                     END
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE RESET                                             *
     C*  RESET INSERT, AMEND AND DELETE FIELDS FOR THE FILE CONTROLS  *
     C*                                                               *
     C*****************************************************************
     C           RESET     BEGSR
     C           GFFLNM    CHAINGLFCTLL0             50
     C           *IN50     IFEQ '1'
     C*
     C** DATABASE ERROR
     C*
     C                     MOVE MSG,2     DBCAUS
     C                     MOVEL'GLFCTLL0'SAFILE
     C                     Z-ADD7         SAASE
     C                     MOVELGFFLNM    SAKEY
     C                     EXSR DBERR
     C                     ELSE
     C                     Z-ADD0         GFNOIN
     C                     Z-ADD0         GFNOAM
     C                     Z-ADD0         GFNODL
     C                     UPDAT@AHREAU                50
     C           *IN50     IFEQ '1'
     C*
     C** DATABASE ERROR
     C*
     C                     MOVE MSG,3     DBCAUS
     C                     MOVEL'GLFCTLL0'SAFILE
     C                     Z-ADD8         SAASE
     C                     MOVELGFFLNM    SAKEY
     C                     EXSR DBERR
     C                     END
     C                     END
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE DBERR                                             *
     C*  PROCESSING FOR A DATABASE ERROR                              *
     C*                                                               *
     C*****************************************************************
     C           DBERR     BEGSR
     C                     MOVE 'Y'       DBSTS   1
     C                     WRITEDBERRF
     C           *IN60     IFEQ '1'
     C                     WRITETITLE
     C                     SETOF                     60
     C                     END
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE CALERR                                            *
     C*  PROCESSING FOR AN ERROR ON CALLING A PROGRAM                 *
     C*                                                               *
     C*****************************************************************
     C           CALERR    BEGSR
     C                     MOVE 'Y'       CALSTS  1
     C                     WRITECALERRF
     C           *IN60     IFEQ '1'
     C                     WRITETITLE
     C                     SETOF                     60
     C                     END
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE FCERR                                             *
     C*  PROCESSING FOR A DATABASE ERROR                              *
     C*                                                               *
     C*****************************************************************
     C           FCERR     BEGSR
     C                     MOVE 'Y'       FCSTS   1
     C                     MOVE *BLANKS   FCCAUS
     C                     MOVELGFFLNM    FCCAUS
     C                     WRITEFCERRF
     C           *IN60     IFEQ '1'
     C                     WRITETITLE
     C                     SETOF                     60
     C                     END
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE END                                               *
     C*  TERMINATE THE PROGRAM                                        *
     C*                                                               *
     C*****************************************************************
     C           END       BEGSR
     C           DBSTS     IFEQ 'Y'
     C           CALSTS    OREQ 'Y'
     C           FCSTS     OREQ 'Y'
     C                     EXSR ERROR
     C                     ELSE
     C                     WRITEFOOTER
     C                     MOVE *BLANKS   P0RTN
     C                     END
     C                     WRITEENDF
     C                     SETON                     LR
     C                     RETRN
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE ERROR                                             *
     C*  SET UP LDA                                                   *
     C*                                                               *
     C*****************************************************************
     C           ERROR     BEGSR
     C           DBSTS     IFEQ 'Y'
     C           CALSTS    OREQ 'Y'
     C*
     C** DATABASE ERROR
     C*
     C                     SETON                     U7U8
     C                     MOVEL'*DBERR ' P0RTN
     C                     ELSE
     C*
     C** FILE CONTROL ERROR
     C*
     C                     SETON                     U8
     C                     MOVEL'*FCOOB ' P0RTN
     C                     END
     C*
     C** SET UP LDA
     C*
     C           *LOCK     IN   LDA
     C                     MOVE SAFILE    DBFILE
     C                     MOVE SAKEY     DBKEY
     C                     MOVE SAASE     DBASE
     C                     MOVE 'GL0930'  DBPGM
     C                     OUT  LDA
     C                     ENDSR
     C*****************************************************************
     C/EJECT
**   TABLE
GLSPHDPD  GL5500P          SPREAD PROFILES
** MSG
CHAIN TO FILE GLFCTLL1
CHAIN TO FILE GLFCTLL0
UPDATE TO FILE GLFCTLL0
** CPY@     : Copyright notice for inclusion in all programs
(c) Misys International Banking Systems Ltd. 2001
