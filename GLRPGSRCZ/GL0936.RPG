0001 H
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Increase File Size - STRAN')
0002 F********************************************************************
0010 F*                                                                  *
     F*  Midas - General Ledger Module
0004 F*                                                                  *
0005 F*       GL0936 - STRAN FILE SIZE INCREASE                          *
0006 F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
0008 F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
0009 F*       LAST AMEND.DT0072               DATE 02MAY91               *
0010 F*                                                                  *
     F*----------------------------------------------------------------
     F*   DT0072   Add processing for STRANH
0011 F********************************************************************
0010 F*                                                                  *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FSTRAN   UF  F     200            DISK
     FSTRANH  UF  E                    DISK                               DT0072
     FMFLSZFM CF  E                    WORKSTN
     F*
     F**  FUNCTION OF INDICATORS
     F* 01       STRAN FILE
     F* 03       INCREMENT FILE SIZE BY 100 RECORDS
     F* 11       ERROR IN SCREEN INPUT - TWO INPUTS,ONLY ONE ALLOWED
     F* 13       ERROR IN SCREEN INPUT - NON NUMERIC FILE SIZE
     F* 14       ERROR IN SCREEN INPUT - NO INPUT TO SCREEN
     F* 21       EXCEPTION OUTPUT TO STRAN
     F* 38       SCREEN INPUT ERROR
     F* 39       FAILED CHAIN TO STRAN
     F*
     F*    U7    SET ON BY CMD/3 NO INCREASE
     F*    U8    DATABASE ERROR - STRAN HEADER NOT FOUND
     F/SPACE 5
     E                    CPY@    1   1 80
0188 E                    ZA1        16  1               ZALIGN SR. ARRAY
0189 E                    ZA2        16  1               ZALIGN SR. ARRAY
     F/SPACE 5
     I**
     I**  STRAN FILE
     I**
     ISTRAN   NS  01
     I                                        1   1 RECI
     I                                        2   60NAVT
     I                                        7  110MNT
     I                                       12  160NORC
     I                                       17  210NOMA
     I**
     I** STATUS DATA STRUCTURE
     I**
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     IGLSTAT     UDS                            256
     I                                       16  200STAN90
     I                                       21  250STAN95
     I                                       26  300NSTRAN
     I/EJECT
     C                     MOVEACPY@      BIS@   80
     C           *ENTRY    PLIST
     C                     PARM           INCR    1
     C                     PARM           MNT     50
     C*
     C                     MOVE 'STRAN'   FLNAM   5
     C*
     C           1         CHAINSTRAN                39
     C*
     C           *IN39     IFEQ '1'
     C                     SETON                     LRU8
     C                     RETRN
     C                     END
     C*
     C           1         CHAINSTRANHF              39                   DT0072
     C*                                                                   DT0072
     C           *IN39     IFEQ '1'                                       DT0072
     C                     MOVEL'STRANF  'DBFILE 10         ************* DT0072
     C                     MOVEL'01'      DBASE   3         *DBERROR 001* DT0072
     C                     SETON                     LRU8   ************* DT0072
     C                     EXSR *PSSR                                     DT0072
     C                     RETRN                                          DT0072
     C                     END                                            DT0072
     C*
     C                     Z-ADDMNT       FLSIZ   50
     C*
     C           *IN38     DOUEQ'0'
     C                     EXFMTMFLSZF1
     C                     SETOF                     031138
     C                     SETOF                     13
     C*
     C   KC                SETON                     LRU7
     C   KC                RETRN
     C*
     C** NUMERIC CHECK FOR FILE SIZE ENTRY
     C                     MOVEL*BLANK    ZFIELD 16
     C                     MOVELNEWSIZ    ZFIELD
     C                     Z-ADD0         ZADEC   10
     C                     Z-ADD5         ZADIG   20
     C                     EXSR ZALIGN
     C   99                SETON                     3813
     C                     MOVE ZFIELD    WORKN   50
     C*
     C* CHECK FOR SCREEN INPUT
     C           INCMT     COMP 'Y'                      03
     C           *IN03     IFEQ '0'
     C           NEWSIZ    IFEQ '     '
     C                     SETON                     1438
     C                     END
     C                     ELSE
     C*
     C* CHECK ONLY ONE SCREEN INPUT
     C           WORKN     IFGT 0
     C                     SETON                     3811
     C                     END
     C                     END
     C                     END
     C*
     C** INCREMENT FILE SIZE BY 100 RECORDS
     C           *IN03     IFEQ '1'
     C           MNT       ADD  100       MNT     50
     C                     MOVE 'Y'       INCR    1
     C                     ELSE
     C*
     C** UPDATE GLSTAT & HEADER WITH NEW SIZE
     C           WORKN     IFGT NSTRAN
     C                     MOVE 'Y'       INCR    1
     C                     END
     C*
     C                     Z-ADDWORKN     NSTRAN
     C           NSTRAN    MULT .95       STAN95
     C           NSTRAN    MULT .90       STAN90
     C*
     C                     Z-ADDWORKN     MNT
     C                     END
     C*
     C                     SETON                     21
     C                     EXCPT
     C                     SETOF                     21
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
     C********************************************************************
     C**
     C**   ZALIGN SR. TO VALIDATE AND RIGHT-ALIGN NUMERIC FIELDS.
     C**
     C           ZALIGN    BEGSR                           *** ZALIGN ***
     C**
     C                     SETOF                     929399
     C**
     C**   SAVE INPUT FIELD IN ARRAY, ZA1.
     C                     MOVEAZFIELD    ZA1
     C**
     C**   CALCULATION TO DEFINE NUMBER STRUCTURE CONTROL FIELDS.
     C                     Z-ADDZADIG     ZADIG   20
     C                     Z-ADDZADEC     ZADEC   10
     C**
     C**   CALCULATIONS TO DEFINE/CLEAR FIELDS.
     C                     MOVE ' '       ZA2
     C                     MOVEAZA2       ZFIELD 16
     C                     Z-ADD0         ZX      20
     C                     Z-ADD0         ZY      20
     C**
     C**   ENSURE REQUIRED STRUCTURE OF FIELD IS VALID.
     C           ZADIG     ADD  ZADEC     ZZ      20
     C           ZZ        COMP 15                   99
     C   99                GOTO ZAEND
     C**
     C**   LOOP TO FIND DECIMAL POINT, BLANKS AND CHARACTERS.
     C           ZATAG1    TAG                             ** ZATAG1 TAG *
     C           ZX        ADD  1         ZX
     C**
     C**   CHECK FOR DECIMAL POINT. ERROR IF SECOND DECIMAL POINT.
     C           ZA1,ZX    COMP '.'                      90
     C   90 93             SETON                     99
     C   99                GOTO ZAEND
     C**
     C**
     C**   CHECK FOR 'M' CONSTANT. ERROR IF NOT LAST CHARACTER ENTERED
     C           ZA1,ZX    COMP 'M'                      96
     C   96                DO
     C           ZX        ADD  1         ZX
     C           ZA1,ZX    COMP ' '                  9999
     C   99                GOTO ZAEND
     C           ZX        SUB  1         ZX
     C           ZX        COMP 9                    99
     C   99                GOTO ZAEND
     C                     END
     C**
     C**   CHECK FOR 'T' CONSTANT. ERROR IF NOT LAST CHARACTER ENTERED
     C           ZA1,ZX    COMP 'T'                      97
     C   97                DO
     C           ZX        ADD  1         ZX
     C           ZA1,ZX    COMP ' '                  9999
     C   99                GOTO ZAEND
     C           ZX        SUB  1         ZX
     C           ZX        COMP 12                   99
     C   99                GOTO ZAEND
     C                     END
     C**
     C**   CLEAR ALPHAMERIC CONSTANT FROM ZA1
     C   96
     COR 97                MOVE ' '       ZA1,ZX
     C**
     C**   CHECK FOR BLANKS. BYPASS FOR FIRST BLANK AFTER A DIGIT.
     C           ZA1,ZX    COMP ' '                      91
     C   91 92             GOTO ZATAG2
     C**
     C**   CHECK FOR NUMERIC, IF NOT '.' OR ' '.
     C  N90N91   ZA1,ZX    COMP '0'                    99
     C  N90N91N99ZA1,ZX    COMP '9'                  99
     C   99                GOTO ZAEND                      NOT NUMERIC
     C**
     C**   STORE DIGITS IN ARRAY AND HOW MANY.
     C**   ZY, TOTAL OF DIGITS IN THE INPUT FIELD.
     C**   ZZ, TOTAL OF DIGITS TO THE LEFT OF DECIMAL POINT.
     C  N90N91   ZY        ADD  1         ZY         92
     C  N90N91             MOVE ZA1,ZX    ZA2,ZY
     C   90                Z-ADDZY        ZZ         93  93
     C**
     C           ZX        COMP 16                     94  CHECK IF ALL
     C   94                GOTO ZATAG1
     C**
     C           ZATAG2    TAG                             ** ZATAG2 TAG *
     C**
     C**   IF 'M' SPECIFIED, MULTIPLY ZA1 BY 1 MILLION
     C   96      ZZ        ADD  6         ZZ
     C**
     C**   IF 'T' SPECIFIED, MULTIPLY ZA1 BY 1 THOUSAND
     C   97      ZZ        ADD  3         ZZ
     C**
     C**   FILL IN ZEROS IN ANY BLANKS LEFT OF DECIMAL POINT
     C   96 93
     COR 97 93   ZZ        DOWGTZY
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C                     END
     C**
     C**   IF CONSTANT SPECIFIED WITH NO DECIMAL POINT ZEROISE BLANKS
     C   96N93             DO   6
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C                     END
     C**
     C   97N93             DO   3
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C                     END
     C**
     C**   IF NO DECIMAL POINT FOUND, SET TOTAL FIELD ZZ.
     C  N93                Z-ADDZY        ZZ
     C**
     C**   CHECK FOR EMBEDDED BLANKS.
     C   91 92             MOVEAZA1,ZX    ZFIELD
     C   91 92   ZFIELD    COMP ' '                  9999
     C   99                GOTO ZAEND                      EMBEDDED BLANK
     C**
     C**   ENSURE THAT NUMBER OF DIGITS ENTERED EITHER SIDE
     C**   OF DECIMAL POINT ARE NOT MORE THAN ALLOWED.
     C           ZZ        COMP ZADIG                99
     C           ZY        SUB  ZZ        ZX
     C  N99      ZADEC     SUB  ZX        ZX           9995
     C   99                GOTO ZAEND
     C   95                GOTO ZATAG4                     NO TRAILING
     C**                                                   BLANKS.
     C**   FILL THE TRAILING BLANKS WITH ZEROS.
     C           ZY        ADD  ZX        ZZ
     C           ZATAG3    TAG                             ** ZATAG3 TAG *
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C           ZY        COMP ZZ                     94
     C   94                GOTO ZATAG3
     C**
     C**   RIGHT-ALIGN THE VALUE BY MOVING BACK TO ARRAY, ZA1.
     C           ZATAG4    TAG                             ** ZATAG4 TAG *
     C                     MOVE '0'       ZA1
     C                     Z-ADD16        ZX
     C           ZATAG5    TAG                             ** ZATAG5 TAG *
     C           ZY        COMP 0                        94
     C   94                GOTO ZATAG6
     C                     MOVE ZA2,ZY    ZA1,ZX
     C           ZY        SUB  1         ZY
     C           ZX        SUB  1         ZX
     C                     GOTO ZATAG5
     C           ZATAG6    TAG                             ** ZATAG6 TAG *
     C**
     C**   MOVE VALIDATED AND RIGHT-ALIGNED NUMBER BACK INTO ZFIELD.
     C                     MOVEAZA1       ZFIELD
     C**
     C                     SETOF                     9697
     C**
     C           ZAEND     ENDSR                           * ZAEND ENDSR *
     C**
     C**
     C********************************************************************
     C*                                                                   DT0072
     C           *PSSR     BEGSR                                          DT0072
     C*                                                                   DT0072
     C           @RUN      IFEQ *BLANK                                    DT0072
     C                     MOVE 'Y'       @RUN    1                       DT0072
     C                     DUMP                                           DT0072
     C                     END                                            DT0072
     C*                                                                   DT0072
     C                     ENDSR                                          DT0072
     C********************************************************************
     O/SPACE 10
     O**
     O** STRAN FILE
     O**
     OSTRAN   E        21
     O                         MNT       11
     OSTRANHF E        21                                                 DT0072
     O                         MNT                                        DT0072
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
