     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Setup Existing Amount Accruals for CGL003')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL1111 - Setup Existing Amount Accruals for CGL003           *
      *           Switchable Feature                                  *
      *                                                               *
      *  Function:  This program sets up the accrual frequency fields *
      *  (I/C)      for existing Amount Accruals                      *
      *                                                               *
      *  Called By: GLC1111                                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL064             Date 10Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CGL003  *CREATE    Date 27Feb97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL064 - Associated Customer / Cost Centre Validation        *
      *           Changes (Recompile)                                 *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL003 - Accrual Frequency on Amount Accruals                *
      *                                                               *
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *                                                               *
      *****************************************************************
     F*
     FAMACCD  UP  E                    DISK
     F*
     F*****************************************************************
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*****************************************************************
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*  S U B R O U T I N E   I N D E X                              *
     F*****************************************************************
     F*                                                               *
     F*  SRINIT - Routine to handle initial processing                *
     F*  *PSSR  - Routine to handle database errors                   *
     F*                                                               *
     F*****************************************************************
      *
     E                    CPY@    1   1 80
      *
      *
     IPSDS       SDS
      * Program Status Data Structure
     I                                     *PROGRAM ##PGM
      *
     ISDGELR    E DSSDGELRPD
     I* GL ICD Details accessed via access program
      *
     IDSSDY     E DSDSSDY
     I* Access programs data structure
     I*
     I*    STANDARD LOCAL DATA AREA FIELDS
     I*
     ILDA       E DSLDA
      *
      *****************************************************************
      *  P R O G R A M     S T A R T                                  *
      *****************************************************************
      *
      *
      ** Initialisation routine
     C           WFIRST    IFEQ *BLANK
     C                     EXSR SRINIT
     C                     MOVE 'N'       WFIRST  1
     C                     ENDIF
      *
      ** Update existing accruals with accrual data from GL ICD.
     C           AMAPFQ    IFEQ *BLANK
     C                     MOVE BKAPFQ    AMAPFQ
     C                     MOVE BKAPFD    AMFDAY
      ** Use start date if accrual/profit date on GL ICD is before
      ** start date
     C           BKAPDT    IFLT STDT
     C                     MOVE STDT      AMAPDA
     C                     ELSE
     C                     MOVE BKAPDT    AMAPDA
     C                     ENDIF
      *
     C                     UPDATAMACCDF
     C                     ENDIF
      *
     C           LREND     TAG
      *
      *****************************************************************
      *                                                               *
      *  SRINIT - Routine to handle initial processing.               *
      *                                                               *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
     C*
     C** OBTAIN GENERAL LEDGER DETAILS
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDGELR    PARM SDGELR    DSSDY
     C           @RTCD     IFNE *BLANKS
     C                     MOVEL'SDGELRPD'DBFILE             *************
     C                     MOVEL'001'     DBASE              * DBERR 001 *
     C                     MOVEL@OPTN     DBOPTN  7          *************
     C                     MOVEL@OPTN     DBKEY
     C                     MOVE ##PGM     DBPGM
     C                     EXSR *PSSR
     C                     SETON                     U7U8LR
     C                     GOTO LREND
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           ** *PSSR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      *
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2001
