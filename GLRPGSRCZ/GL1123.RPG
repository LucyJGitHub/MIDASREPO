0001 H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Amount accrual input - amend screen')         *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL1123 - Amount Accrual Input - S/AMACC                      *
      *           Amend screen validation                             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD029265           Date 15Aug14               *
      *                 CGL141             Date 12Dec12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 06May06               *
      *                 CGL014             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 197200  *CREATE    Date 20Aug01               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD029265 - Cannot complete accrual input insert due to       *
      *             missing narrative upon 'Enter' (Recompile)        *
      *  CGL141 - Amount Accruals Extended Narrative.                 *
      *           Added Hooks: CGL141_017 to CGL141_020               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL014 - Collaterals Processing                              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  197200 - Add amend action to amount accruals maintenance     *
      *                                                               *
      *                                                               *
      *****************************************************************
     FGL1120FMCF  E                    WORKSTN
      ** Display file
      *
     FACCNT   IF  E           K        DISK                                                   CGL014
     F            ACCNTAAF                          KIGNORE                                   CGL014
     F            ACCNTACF                          KIGNORE                                   CGL014
      ** GL Accounts master file                                                              CGL014
      *                                                                                       CGL014
     E                    CPY@    1   1 80
      ** Array for Copyright statement
      *
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZALIGNZ1
     E                    ERCD       19  4
      *
     IGL1120F3    78
      ** Workstation file - format for Amend screen
      *
     IACCNTABF                                                                                CGL014
     I              LISP                            ACLISP                                    CGL014
      *                                                                                       CGL014
     IWSTRN      UDS                            860
      ** Workstation transaction data area
      *
     I                                        1   1 MRECI
     I                                        2   70MREFN
     I                                        2   7 SCREFN
     I                                        8   8 MREFT
     I                                    P  31  330MTNLU
     I                                    P  34  360MORED
     I                                    P  37  390MLCD
     I                                       40  40 MCHTP
     I                                    P  41  470MHASH
     I                                    P  41  470MAMNT
     I                                       48  50 MCCY
     I                                    P  51  530MSDAT
     I                                    P  54  560MMDAT
     I**********                             57  600MDRAC                                     CGL029
     I                                       57  600QQDRAC                                    CGL029
     I                                       61  660MDRCN
     I**********                             67  700MCRAC                                     CGL029
     I                                       67  700QQCRAC                                    CGL029
     I                                       71  760MCRCN
     I                                       77 106 MNARR
     I                                    P 107 1130MAMPD
     I                                      114 1150MDRSQ
     I                                      116 1170MCRSQ
     I                                      126 128 BRCA
     I                                      129 129 MAPFQ
     I                                      130 1310MFDAY
     I                                    P 132 1340MAPDA
     I                                      135 1440MDRAC                                     CGL029
     I                                      145 1540MCRAC                                     CGL029
     I**********                            135 200 WST200                                    CGL029
     I                                      155 200 WST200                                    CGL029
     I                                      201 400 WST400
     I                                      401 600 WST600
     I                                      601 800 WST800
     I                                      801 860 WST860
     I                                      401 401 WST401
     I                                    P 402 4040WST404
     I                                    P 405 4070WST407
     I                                      408 409 WST409
     I                                      410 418 WST418
     I                                      419 429 WST429
     I                                      430 430 WST430
     I                                      431 431 WST431
     I                                      461 467 WST467
     I                                      468 469 WST469
     I                                      470 475 WST475
     I                                      476 478 WST478
     I                                      479 484 WST484
     I                                      485 485 WST485
     I                                      486 488 WST488
     I                                      489 491 WST491
     I                                      492 505 WST505
     I                                      506 509 WST509
     I                                      510 515 WST515
     I                                      516 521 WST521
     I**********                            522 525 WST525                                    CGL029
     I**********                            526 527 WST527                                    CGL029
     I**********                            528 533 WST533                                    CGL029
     I**********                            534 537 WST537                                    CGL029
     I**********                            538 539 WST539                                    CGL029
     I**********                            540 545 WST545                                    CGL029
     I**********                            546 575 WST575                                    CGL029
     I**********                            576 576 WST576                                    CGL029
     I**********                            577 578 WST578                                    CGL029
     I**********                            579 584 WST584                                    CGL029
     I                                      522 531 WST525                                    CGL029
     I                                      532 533 WST527                                    CGL029
     I                                      534 539 WST533                                    CGL029
     I                                      540 549 WST537                                    CGL029
     I                                      550 551 WST539                                    CGL029
     I                                      552 557 WST545                                    CGL029
     I                                      558 587 WST575                                    CGL029
     I                                      588 588 WST576                                    CGL029
     I                                      589 590 WST578                                    CGL029
     I                                      591 596 WST584                                    CGL029
      *
     IRUNDT       DS
      ** External DTAARA for run-date
      *
     I                                       13  13 MBIN
      *
     ICPYR@#      DS
      ** Data structure for compilation - copyright insertion
      *
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     ILDA        UDS                            256
      ** Local data area
      *
     I                                        1   8 LPROC
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I                                      184 1880LRUND
     I                                      189 195 LRUNA
     I                                      196 196 LDATF
     I                                      197 197 LSFLG
     I                                      198 1990LKEYL
     I                                      200 205 LISP
     I                                      206 206 LTWOL
     I                                      207 2070LLDLP
     I                                      208 2120LTNLU
     I                                      213 213 LDELM
     I                                      214 214 LDUD
      *
     ISDCURR    E DSSDCURRPD
      ** Currency codes detail
      *
     ISDGELR    E DSSDGELRPD
      ** General Ledger details
      *
     ISCSARD    E DSSCSARDPD                                                                  CGL014
      ** Switchable features                                                                  CGL014
      *                                                                                       CGL014
     IDSFDY     E DSDSFDY
      ** First DS for access programs, short data structure
      *
     IDSSDY     E DSDSSDY
      ** Second DS for access programs, long data structure
      *
     IPSDS       SDS
     I                                      244 246 SWID
     I/COPY WNCPYSRC,GLH00542
     I/COPY OFCPYSRCZ,CGL141_017                                                              CGL141
      *
      ** Lock in data areas
      *
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           WSTRN
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C           *LOCK     IN   LDA
     C           *LOCK     IN   WSTRN
      *
     C           *IN75     IFEQ *OFF
      *
      ** First Cycle processing
      *
     C                     MOVEACPY@      BIS@   80
      *
     C                     MOVEL'GL1123'  DBPGM
      *
      ** Check the multibranching indicator in the data area RUNDAT
      ** is On or Off
      *
     C                     IN   RUNDT
      *
     C           MBIN      IFEQ 'Y'
     C                     MOVE *OFF      *IN40
     C                     ELSE
     C                     MOVE *ON       *IN40
     C                     ENDIF
      *
      ** Check if profit centre processing is required
      *
     C                     EXSR SRPRFC
      *
      ** Check if accrual frequency switchable feature is switched on
      *
     C                     MOVE 'N'       CGL003  1
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   #RTCD   7
     C                     PARM '*VERIFY' #OPTN   7
     C                     PARM 'CGL003'  WSARD   6
      *
     C           #RTCD     IFNE *BLANKS
     C           #RTCD     ANDNE'*NRF    '
     C                     MOVEL'AOSARDR0'DBFILE
     C                     Z-ADD2         DBASE
     C                     MOVELWSARD     DBKEY
     C                     MOVE #OPTN     DBOPTN  7
     C                     EXSR *PSSR
     C                     ELSE
      *
     C           #RTCD     IFEQ *BLANK
     C                     MOVE 'Y'       CGL003  1
     C                     MOVE *ON       *IN46
     C                     ENDIF
      *
     C                     ENDIF
      *                                                                                       CGL014
      ** Check if enhancement CGL014 (Collaterals Processing) is on                           CGL014
      *                                                                                       CGL014
     C                     CALL 'AOSARDR0'                                                    CGL014
     C                     PARM *BLANKS   PRTCD   7                                           CGL014
     C                     PARM '*VERIFY' POPTN   7                                           CGL014
     C                     PARM 'CGL014'  PSARD   6                                           CGL014
     C           SCSARD    PARM SCSARD    DSFDY                                               CGL014
      *                                                                                       CGL014
     C           PRTCD     IFNE *BLANKS                                                       CGL014
     C           PRTCD     ANDNE'*NRF   '                                                     CGL014
     C                     MOVEL'SCSARDPD'DBFILE                                              CGL014
     C                     MOVEL'CGL014  'DBKEY                                               CGL014
     C                     Z-ADD3         DBASE                                               CGL014
     C                     EXSR *PSSR                                                         CGL014
     C                     ENDIF                                                              CGL014
      *                                                                                       CGL014
     C           PRTCD     IFEQ *BLANKS                                                       CGL014
     C                     MOVE 'Y'       CGL014  1                                           CGL014
     C                     MOVE '0'       WWARN   1                                           CGL014
     C                     Z-ADD0         WCOPY  130                                          CGL014
     C                     ELSE                                                               CGL014
     C                     MOVE 'N'       CGL014                                              CGL014
     C                     ENDIF                                                              CGL014
      *                                                                                       CGL014
      ** Key list for LF/ACCNT                                                                CGL014
      *                                                                                       CGL014
     C           KACCNT    KLIST                                                              CGL014
     C**********           KFLD           KCNUM   60                                   CGL014 CSD027
     C                     KFLD           KCNUM   6                                           CSD027
     C                     KFLD           KCCY    3                                           CGL014
     C                     KFLD           KACOD  100                                          CGL014
     C                     KFLD           KACSQ   20                                          CGL014
     C                     KFLD           KBRCA   3                                           CGL014
      *
     C/COPY WNCPYSRC,GLH00543
     C/COPY OFCPYSRCZ,CGL141_018                                                              CGL141
     C                     ENDIF
      *
     C           *IN45     DOUEQ*OFF
     C                     READ GL1120FM                 45
     C                     ENDDO
      *
     C           *IN75     IFEQ *OFF
      *
      ** First cycle processing
      *
     C                     MOVE *ON       *IN75
      *
      ** Set up standard input program fields  -  WPROG is this program
      *
     C                     MOVELLISP      WPROG   8
     C                     MOVE '3  '     WPROG
     C                     MOVELWPROG     SPID    6
      *
      ** Set up standard LDA fields
      *
     C                     MOVE WPROG     DBPGM
     C                     Z-ADD4         LLDLP
      *
      ** Set up standard blank fields
      *
     C           LDATF     IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
     C           LSFLG     IFEQ 'Y'
     C                     MOVE *ON       *IN79
     C                     ENDIF
      *
     C                     MOVE ' '       BLK200200
      *
     C                     Z-ADD0         ZERO05  50
      *
      ** Log program name
      *
     C                     MOVE LISP      WLOGP   6
     C                     MOVE '9'       WLOGP
      *
      ** Initialise field to blank out key/action code
      *
     C                     MOVE ' '       BLK7    7
      *
     C                     ENDIF
      *
      ** Initialise standard indicators
      *
     C                     MOVEA'111'     *IN,86
     C                     MOVE *OFF      *IN89
     C                     MOVEA'000000'  *IN,80
     C                     MOVE *OFF      *IN76
      *
      ** Initialise non-standard indicators
      *
     C                     MOVE *OFF      *IN04
     C                     MOVE *OFF      *IN06
      *
      ** Check for correct screen read in
      *
     C           *IN78     IFEQ *OFF
      *
     C                     MOVEL'SCREN'   DBFILE
     C                     MOVE BLK200    DBKEY
     C                     MOVEL'SA'      DBKEY
     C                     MOVE 'FOUND   'DBKEY
     C                     MOVE SID       DBKEY
     C                     MOVE '92'      DBASE
     C                     EXSR *PSSR
      *
     C                     ENDIF
      *
      ** Clear error code array, reset index
      *
     C                     MOVE BLK200    ERMSG  79
     C                     MOVE '    '    ERCD
     C                     Z-ADD0         S
      *
     C           *INKC     DOWEQ*OFF
     C           *INKL     ANDEQ*OFF
      *
      ** Do while F3 and F12 is not pressed
      *
     C                     MOVE '    '    ERCD
     C                     Z-ADD0         S       20
     C                     MOVE *OFF      *IN04
     C                     MOVE *OFF      *IN06
      *
      ** Validate amended fields
      *
     C                     EXSR SRVALS
      *
      ** Format amounts for output
      *
     C                     EXSR SRFRMT
      *
      ** If error is encountered, redisplay amend screen
      *
     C           S         IFNE 0
     C                     EXSR SUBSA
     C                     MOVEAERCD      ERCOD
     C                     MOVE *OFF      *IN88
     C                     MOVE *ON       *IN67
     C                     WRITEGL1120F1
     C                     EXFMTGL1120F3
      *
     C           *INKE     IFEQ *ON
      *
      ** If F5 is pressed, refresh amended fields
      *
     C                     MOVE '    '    ERCD
     C                     Z-ADD0         S       20
     C                     MOVE *OFF      *IN04
     C                     MOVE *OFF      *IN06
     C                     MOVE *ON       *IN88
      *
      ** Format Amount for output
      *
     C                     MOVE MAMNT     ZFIELD
     C           13        SUB  A6NBDP    ZADIG
     C                     Z-ADDA6NBDP    ZADEC
     C                     EXSR ZEDIT
      *
      ** Format Maturity date for output
      *
     C                     MOVE *BLANKS   SMDAT
     C                     MOVE ZFIELD    SAMNT
     C                     MOVE MMDAT     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE *BLANKS   SMDAT
     C                     MOVE ZDATE     SMDAT
      *
     C                     EXFMTGL1120F3
     C                     ENDIF
      *
     C                     ELSE
     C/COPY WNCPYSRC,GLH00544
     C/COPY OFCPYSRCZ,CGL141_019                                                              CGL141
      *
      ** If no error, move values to data area for update
      *
     C                     EXSR SRWSTR
     C                     MOVE '9  '     LPROC
      *
      ** Set up output
      *
     C                     Z-ADDZDAYNO    MMDAT
     C                     Z-ADDWAMNT     MAMNT
      *
     C                     Z-ADDZERO05    MTNLU
     C                     Z-ADDLRUND     MLCD
     C                     MOVE 'A'       MCHTP
     C                     OUT  LDA
     C                     OUT  WSTRN
     C                     MOVE *ON       *IN76
     C                     MOVE *ON       *INLR
     C                     LEAVE
      *
     C/COPY WNCPYSRC,GLH00545
     C/COPY OFCPYSRCZ,CGL141_020                                                              CGL141
     C                     ENDIF
      *
     C                     ENDDO
      *
      ** If F3 is pressed, end the program
      *
     C           *INKC     IFEQ *ON
     C                     MOVEL'**      'LPROC
     C                     MOVE *ON       *INLR
     C                     ENDIF
      *
      ** If F12 is pressed, output initial blank screen
      *
     C           *INKL     IFEQ *ON
     C                     EXSR SUBSF
     C                     MOVE *ON       *IN88
     C                     WRITEGL1120F1
     C                     OUT  LDA
     C                     OUT  WSTRN
     C                     ENDIF
      *
     C           *INLR     IFEQ *ON
     C                     RETRN
     C                     ENDIF
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SRFRMT - Subroutine to format amounts                         *
      *                                                               *
      * Called by: Main                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRFRMT    BEGSR
      *
      ** Calculate and format posted amount and remainder
      *
     C           MAMNT     SUB  MAMPD     MAREM  130
     C                     MOVE MAMPD     ZFIELD
     C           13        SUB  A6NBDP    ZADIG
     C                     Z-ADDA6NBDP    ZADEC
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    SAMPD
      *
     C                     MOVE MAREM     ZFIELD
     C           13        SUB  A6NBDP    ZADIG
     C                     Z-ADDA6NBDP    ZADEC
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    SAREM
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SUBSF  - Subroutine to Setup Initial Screen Output            *
      *                                                               *
      * Called by: Main                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SUBSF     BEGSR
     C                     MOVE *ON       *IN81
     C                     MOVE *ON       *INLR
     C                     MOVE *BLANKS   FAREF
     C                     MOVE *BLANKS   ACTN
     C                     MOVE '1  '     LPROC
     C                     MOVE 'SF'      SID
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SRWSTR - Subroutine to Setup WSTRN data                       *
      *                                                               *
      * Called by: Main                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRWSTR    BEGSR
      *
     C                     MOVE *BLANKS   WST200
     C                     MOVE *BLANKS   WST400
     C                     MOVE *BLANKS   WST600
     C                     MOVE *BLANKS   WST800
     C                     MOVE *BLANKS   WST860
      *
     C                     MOVE 'D'       WST401
     C                     Z-ADDZERO05    WST404
     C                     Z-ADDZERO05    WST407
     C                     MOVE SWID      WST409
     C                     MOVE DBFILE    WST418
     C                     MOVE WLOGP     WST429
     C                     MOVE '1'       WST430
     C                     MOVE 'A'       WST431
      *
      ** Set up screen in data area
      *
     C                     MOVE RUNA      WST467
     C                     MOVE SID       WST469
     C                     MOVE SPID      WST475
     C                     MOVE SWID      WST478
     C                     MOVE FAREF     WST484
     C                     MOVE ACTN      WST485
     C                     MOVE SBRCA     WST488
     C                     MOVE SCCY      WST491
     C                     MOVE SAMNT     WST505
     C                     MOVE SPRFC     WST509
     C                     MOVE SSTDT     WST515
     C                     MOVE SMDAT     WST521
     C                     MOVE SDRCO     WST525
     C                     MOVE SDRSQ     WST527
     C                     MOVE SDRCN     WST533
     C                     MOVE SCRCO     WST537
     C                     MOVE SCRSQ     WST539
     C                     MOVE SCRCN     WST545
     C                     MOVE SPNAR     WST575
     C                     MOVE SAPFQ     WST576
     C                     MOVE SFDAY     WST578
     C                     MOVE SAPDA     WST584
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SRVALS - Subroutine to validate amended fields                *
      *                                                               *
      * Called by: Main                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRVALS    BEGSR
      *
      ** Validate Amount
      *
     C           SAMNT     IFEQ *BLANKS
      *
      ** Check if blank
      *
     C           S         ADD  1         S
     C                     MOVE ' 004'    ERCD,S
     C                     MOVE *ON       *IN04
     C                     ENDIF
      *
      ** Check if numeric
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM SCCY      @AJCD   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C                     Z-ADDA6NBDP    ZADEC
     C           13        SUB  A6NBDP    ZADIG
     C                     MOVE SAMNT     ZFIELD
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ *ON
     C           S         ADD  1         S
     C                     MOVE ' 005'    ERCD,S
     C                     MOVE *ON       *IN04
     C                     ENDIF
      *
      ** Must be greater than zero
      *
     C           *IN99     IFEQ *OFF
     C           SAMNT     ANDNE*BLANKS
     C                     MOVE ZFIELD    WAMNT  130
     C           WAMNT     IFEQ 0
     C           S         ADD  1         S
     C                     MOVE ' 006'    ERCD,S
     C                     MOVE *ON       *IN04
     C                     ELSE
      *
      ** Must not be greater than the original amount, nor be less than
      ** amount posted to date
      *
     C           WAMNT     IFGT MAMNT
     C           WAMNT     ORLT MAMPD
     C           S         ADD  1         S
     C                     MOVE ' 070'    ERCD,S
     C                     MOVE *ON       *IN04
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *                                                                                       CGL014
      ** If CGL014 is on and amount is valid, check for sufficient funds                      CGL014
      *                                                                                       CGL014
     C           CGL014    IFEQ 'Y'                                                           CGL014
     C           *IN04     ANDEQ*OFF                                                          CGL014
      *                                                                                       CGL014
     C                     MOVE SDRCN     KCNUM                                               CGL014
     C                     MOVE SCCY      KCCY                                                CGL014
     C                     MOVE SDRCO     KACOD                                               CGL014
     C                     MOVE SDRSQ     KACSQ                                               CGL014
     C                     MOVE SBRCA     KBRCA                                               CGL014
      *                                                                                       CGL014
     C           KACCNT    CHAINACCNT                26                                       CGL014
      *                                                                                       CGL014
     C           *IN26     IFEQ *ON                                                           CGL014
     C                     MOVEL'ACCNTAB 'DBFILE                                              CGL014
     C                     MOVELKCNUM     DBKEY                                               CGL014
     C                     Z-ADD004       DBASE                                               CGL014
     C                     EXSR *PSSR                                                         CGL014
      *                                                                                       CGL014
     C                     ELSE                                                               CGL014
      *                                                                                       CGL014
      ** If debit account is retail and is held for collateral,                               CGL014
      ** check if remaining balance is sufficient to cover the blocked amount                 CGL014
      *                                                                                       CGL014
     C           ATYP      IFEQ 'R'                                                           CGL014
     C           COLF      ANDEQ'Y'                                                           CGL014
     C                     Z-ADDWAMNT     PTAMNT                                              CGL014
      *                                                                                       CGL014
     C                     CALL 'GL006000'                                                    CGL014
     C                     PARM           PTAMNT 150                                          CGL014
     C                     PARM SCCY      PTCCY   3                                           CGL014
     C                     PARM SBRCA     PABRCA  3                                           CGL014
     C                     PARM SDRCN     PACNUM  6                                           CGL014
     C                     PARM SCCY      PACCY   3                                           CGL014
     C                     PARM SDRCO     PAACOD 10                                           CGL014
     C                     PARM SDRSQ     PAACSQ  2                                           CGL014
     C                     PARM           PAACNO 100                                          CGL014
     C                     PARM *BLANKS   PRTCD   7                                           CGL014
      *                                                                                       CGL014
      ** Error if available balance is not sufficient to cover blocked amount                 CGL014
      *                                                                                       CGL014
     C                     SELEC                                                              CGL014
     C           PRTCD     WHEQ '*BLKERR'                                                     CGL014
     C           S         ADD  1         S                                                   CGL014
     C                     MOVE ' 012'    ERCD,S                                              CGL014
     C                     MOVE *ON       *IN04                                               CGL014
      *                                                                                       CGL014
      ** Warning:  Balance is greater than blocked amount but less                            CGL014
      ** than minimum balance                                                                 CGL014
      *                                                                                       CGL014
     C           PRTCD     WHEQ '*WARN  '                                                     CGL014
     C           WAMNT     IFNE WCOPY                                                         CGL014
     C                     MOVE '0'       WWARN                                               CGL014
     C                     ENDIF                                                              CGL014
     C           WWARN     IFEQ '0'                                                           CGL014
     C                     ADD  1         S                                                   CGL014
     C                     MOVE ' W01'    ERCD,S                                              CGL014
     C                     MOVE *ON       *IN04                                               CGL014
     C                     Z-ADDWAMNT     WCOPY                                               CGL014
     C           S         IFEQ 1                                                             CGL014
     C                     MOVE '1'       WWARN                                               CGL014
     C                     ENDIF                                                              CGL014
     C                     ENDIF                                                              CGL014
     C                     ENDSL                                                              CGL014
      *                                                                                       CGL014
     C                     ENDIF                                                              CGL014
     C                     ENDIF                                                              CGL014
     C                     ENDIF                                                              CGL014
      *
      ** If valid, format amount for output screen
      *
     C           *IN04     IFEQ *OFF
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    SAMNT
     C                     ENDIF
      *
      ** Validate Maturity Date
      *
     C                     MOVE SMDAT     ZDATE
     C                     EXSR ZDATE1
      *
     C           *IN99     IFEQ *ON
     C           S         ADD  1         S
     C                     MOVE ' 010'    ERCD,S
     C                     MOVE *ON       *IN06
      *
     C                     ELSE
      *
     C           ZDAYNO    IFLT LRUND
     C           S         ADD  1         S
     C                     MOVE ' 011'    ERCD,S
     C                     MOVE *ON       *IN06
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SUBSA  - Subroutine to Setup Amend Screen Output              *
      *                                                               *
      * Called by: Main                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SUBSA     BEGSR
     C                     MOVE *ON       *IN83
     C                     MOVE '3  '     LPROC
     C                     MOVE 'SA'      SID
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SRPRFC - Subroutine for Profit Centre Processing              *
      *                                                               *
      * Called by: 1st cycle                                          *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRPRFC    BEGSR
      *
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
     C           @RTCD     IFNE *BLANKS
     C                     MOVEL'SDGELRPD'DBFILE
     C                     MOVEL@OPTN     DBOPTN  7
     C                     MOVEL@OPTN     DBKEY
     C                     Z-ADD1         DBASE
     C                     EXSR *PSSR
     C                     ELSE
      *
      ** Check if 'profit centre used' in ICD is off and set on 50
      *
     C           BKPRCU    IFEQ 'Y'
     C                     MOVE *ON       *IN50
     C                     END
      *
      ** Check if 'profit centre amend' in ICD is off and Seton 51
      *
     C           BKPRCA    IFEQ 'Y'
     C                     MOVE *ON       *IN51
     C                     ENDIF
      *
      ** Check if 'profit centre default' in ICD is off and set on 53
      *
     C           BKPCDU    IFEQ 'Y'
     C                     MOVE *ON       *IN53
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
     C*****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Subroutine to handle abnormal error conditions       *
      *                                                               *
      * Called by: Main, SR/SRPRFC                                    *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     ENDIF
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
     C/COPY ZSRSRC,ZDATE1Z2
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZALIGNZ2
     C/COPY ZSRSRC,ZEDITZ2
** CPY@   **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
