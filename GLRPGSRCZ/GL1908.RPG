     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Cust. Serv. Fees - Charge Maint.')            *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL1908 - GL Customer Service Fees Charge Maintenance         *
      *                                                               *
      *  Function:  This program allows users to to insert, amend,    *
      *  delete, enquire upon, authorise, stop or release customer    *
      *  service fee charges.                                         *
      *                                                               *
      *  Called By: GLC1908 - Customer Service Fees Charge Control    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CRE075             Date 06Dec10               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER047             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 BUG9525            Date 20Feb06               *
      *                 CSW037A            Date 02May05               *
      *                 204838             Date 30Apr02               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 202033             Date 07Mar02               *
      *                 192177             Date 07Mar02               *
      *                 CSD011             Date 04Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE075 - Effective Date for Retail Accounts (Recompile)      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *           (Recompile)                                         *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG9525 - Customer service fees maintenance error            *
      *            (Recompile)                                        *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  204838 - Need to access the fees master record again after   *
      *           the program has chained in the Fee/charge record    *
      *           from SDCFTPPD. Otherwise error message CSF9837 is   *
      *           sent because F1FECP is blank.                       *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  202033 - Status Narrative not showing first 3 characters.    *
      *           Recompile for change to #1TEXT in GL1908DF.         *
      *           Also message CSF9812 always shown on AMEND.         *
      *  192177 - Charge amount should default for Hold Mail fees.    *
      *           Charge currency should default to fee type currency.*
      *  CSD011 - Performance Incentive Fees                          *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
      /EJECT
     FGLCHRGL0UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
      ** CH - GL Customer Fees Charge - Upd Idx
      *
     FGLCHRNPDUF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
      ** CX - GL Customer Fees Charge - Reference Number
      *
     FGLCHRGL1IF  E           K        DISK         KINFSR *PSSR
     F            GLCHRGD0                          KRENAMEGLCHRGD1
      ** CH - GL Customer Fees Charge - Rtv Idx
      *
     FGLCHRGL6IF  E           K        DISK         KINFSR *PSSR
     F            GLCHRGD0                          KRENAMEGLCHRGD6
      ** CH - GL Customer Fees Charge - Rtv Idx
      *
     FSDCFCDL1IF  E           K        DISK         KINFSR *PSSR
      ** BB - SD Customer Extension Details
      *
     FSDCFTPL0IF  E           K        DISK         KINFSR *PSSR
      ** F1 - GL Fees Types
      *
      *                                                                                       192177
      **   F3 - SE Rate Codes                                                                 192177
     FSDRTCDL0IF  E           K        DISK                                                   192177
     F                                              KINFSR *PSSR                              192177
      *                                                                                       192177
      **   F4 - SE Charge Group/Fee Type details                                              192177
     FSDCGFRL0IF  E           K        DISK                                                   192177
     F                                              KINFSR *PSSR                              192177
     FPMPORTLLIF  E           K        DISK         KINFSR *PSSR
      ** PN - PM Portfolio Definition Details
      *
     FGL1908DFCF  E                    WORKSTN      KINFDS SCRDS
     F                                              KINFSR *PSSR
     F                                        #0SRRNKSFILE GL1908S0
      ** Display file
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   KC : CMD  3 - End of program                                *
      *   KE : CMD  5 - Reset Screen                                  *
      *   KI : CMD  9 - Go to 'Add' or 'Change' Mode                  *
      *   KL : CMD 12 - Previous screen                               *
      *                                                               *
      *   LR :    Last record                                         *
      *   U7/U8 : Database error                                      *
      *                                                               *
      *   10 : Profit Centre Accounting 'ON'                          *
      *   11 : Reverse Image on Profit Centre                         *
      *   15 : Reverse Image on Charge Currency                       *
      *   16 : Reverse Image on Amount                                *
      *   17 : Reverse Image on Branch                                *
      *   18 : Protect/Non-display fields                             *
      *   19 : Reverse Image on Settlement Account                    *
      *   20 : Reverse Image on Fee Start/Last Settlement Date        *
      *   21 : Reverse Image on Automatic Posting                     *
      *   22 : Reverse Image on Settlement Frequency                  *
      *   23 : Reverse Image on Settlement Generation Days            *
      *   24 : Reverse Image on Settlement Day in Month               *
      *   25 : Reverse Image on First/Next Settlement Date            *
      *   26 : Reverse Image on Final Settlement Date                 *
      *   27 : Rollup subfile                                         *
      *   28 : Reverse Image on Charge Group                          *
      *   29 : Protect Branch, Charge Currency and Accrue Fees        *
      *   30 : End of Subfile                                         *
      *   31 : Reverse Image on Accrue Fees                           *
      *   32 : Reverse Image on Charge Reference                      *
      *   35 : Reverse Image on Portfolio Reference                   *
      *   36 : Reverse Image on Charge Group                          *
      *   37 : Reverse Image on other Subfile Fields                  *
      *   38 : Reverse Image on Fee Narrative                         *
      *   39 : Reverse Image on Fee Type                              *
      *   40 : Confirm RESTART or STOP ACCRUALS                       *
      *   41 : Confirm AUTHORISATION                                  *
      *   43 : 'AMEND' Mode if *ON                                    *
      *        'ADD  ' Mode if *OFF                                   *
      *   44 : Error indicator                                        *
      *   45 : "First" Time Processing                                *
      *   46 : Command Key pressed                                    *
      *   47 : Confirmation Screen or 'Full Details' Screen           *
      *   48 : Reverse Image on Calculation Frequency                 *                       CSD011
      *   50 : Error indicator (Position Field)                       *
      *   51 : Error indicator (Position Field)                       *
      *   52 : Error indicator (Position Field)                       *
      *   53 : Error indicator (Position Field)                       *
      *   54 : Protect Dates                                          *
      *   57 : Protect and non-display 'Action'                       *
      *   58 : Protect 'Charge Reference', 'Customer', 'Portfolio     *
      *        Reference'                                             *
      *   59 : Reverse Image on the Calculation Day in Month          *                       CSD011
      *   60 : Reverse Image on High Water Mark Fee                   *                       CSD011
      *   61 : Enquiry Mode                                           *
      *   62 : Error indicator (Action)                               *
      *   65 : Portfolio Reference should or not be displayed         *
      *   69 : Error indicator (Selection Field)                      *
      *   70 : Action Code 'D'                                        *
      *   71 : Action Code 'X'                                        *
      *   72 : Action Code 'S'                                        *
      *   73 : Action Code 'R'                                        *
      *   75 : CSD011 switched ON                                     *                       CSD011
      *   76 : Error indicator (Position Field)                       *
      *   77 : Error indicator (Position Field)                       *
      *   78 : Error indicator (Position Field)                       *
      *   79 : Reverse Image on Next Calculation Date                 *                       CSD011
      *   80 : Display Subfile control                                *
      *   81 : Display Subfile                                        *
      *   82 : Error indicator (Position Field)                       *
      *   83 : CHAIN to SDCGFRL0                                                              192177
      *   84 : Subfile Next Change                                    *
      *   85 : CHAIN to SDRTCDL0                                                              192177
      *   86 : Error indicator (Position Field)                       *
      *   87 : Read Change indicator                                  *
      *   88 : Working indicator                                      *
      *   89 : Working indicator                                      *
      *   90-99 : Used by Standard Subroutines                        *
      *   98 : Indicator used by subroutine ZDATE1                    *
      *   99 : Indicator used by subroutine ZDATE1                    *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *       SRMNPR  - Main processing                               *
      *       SRTERM - Termination processing                         *
      *       SRSFCL - Clear Subfile                                  *
      *       SRSFAD  - Build the subfile for Add Mode                *
      *       SRSFAM - Build the subfile for Amend/Enquiry Mode       *
      *       SRVAL1 - Validate Position/Selection fields             *
      *       SRVAL2 - Validate subfile records                       *
      *       SRVAL3 - Validate 'Action' field                        *
      *       SRACTN - Process 'Action'                               *
      *       SRMOVE - Move Subfile Fields into Screen Fields         *
      *       SRCONF - Process 'Confirmation' screen                  *
      *       SRINDI - Initialise error indicators                    *
      *       SRCLRF - Clear Subfile Fields                           *
      *       SRLOAD - Load Subfile Fields                            *
      *       SRPSEL - Reinitialise Position/selection fields         *
      *       SRDTLS - Process 'Full details' screen                  *
      *       SRVDTL - Validate 'Full details' screen and update file *
      *       SRRTRV - Retrieve Next Available Reference Number       *
      *       SRPREF - Position into the File                         *
      *       SRREAD - Read next record in File                       *
      *                                                               *
      *       *INZSR - Initial processing                             *
      *       *PSSR - "Standard" Database error processing            *
      *                                                               *
      *       ZDATE1 - Dates Validation and Conversion                *
      *       ZDATE2 - Dates Validation and Conversion                *
      *       SRSNMS - Send Message to Program's message queue        *
      *       ZSEDIT - Insert a decimal point and sign into a numeric *
      *                field and blank out leading zeros              *
      *       ZALIGN - To validate and right-align numeric fields     *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
      ** For compilation - Copyright Insertion
      *
     E                    ZYDY    4   4  4 0
     E                    ZMDY   13  13  3 0
     E                    ZMNM   12  12  3
      ** Arrays used by ZDATE1
      *
     E/COPY ZSRSRC,ZALIGNZ1
     E/COPY ZSRSRC,ZASIGNZ1
     E/COPY ZSRSRC,ZSEDITZ1
      *
     E**********          NARR    1   7 12                                                    CSD011
     E                    NARR    1   8 15                                                    CSD011
     E/COPY ZSRSRC,ZHOLE
      *****************************************************************
      /EJECT
      *****************************************************************
      ** Program Data Structure
      *
     IPGMDS     ESDSY2PGDSP
      ** File Inforamtion Data Structure
      *
     IINFDS1    E DSY2I1DSP
      ** File Record Data Structure
      *
     IFIELD     E DSGLCHRGPD
      *
     ISCRDS       DS
     I                                      197 205 DSPNUM
     I                                    B 378 3790CPFPOS
      ** Data Structure for database error
      *
     ILDA         DS                            256
     I                                      132 183 WWLOT
     I                                      132 141 WWFILE
     I                                      142 170 WWKEY
     I                                      171 180 WWPGM
     I                                      181 1830WWASE
     I                                      184 189 PGMNAM
      *
     I              '0123456789'          C         DIGITS
      *
     ILOCKDS      DS
     I                                        1  10 WUSER
     I                                       11  20 WJOBN
      *
     I****PDATA*      DS                             18                                       CGL029
     IPDATA       DS                             24                                           CGL029
     I                                        1   3 PSTBR
     I                                        4   9 PSTCN
     I                                       10  12 PSTCU
     I**********                             13  16 PSTAC                                     CGL029
     I**********                             17  18 PSTSQ                                     CGL029
     I                                       13  22 PSTAC                                     CGL029
     I                                       23  24 PSTSQ                                     CGL029
     I                                        1  10 WWRETA
     I**********                             11  18 WWNRET                                    CGL029
     I                                       11  24 WWNRET                                    CGL029
     I**********                              1  18 #1SEAC                                    CGL029
     I                                        1  24 #1SEAC                                    CGL029
      *
     I***E6DATA*     DS                             18                                        CGL029
     IE6DATA      DS                             24                                           CGL029
     I                                        1   3 E6SBCH
     I                                        4   9 E6SCNM
     I                                       10  12 E6SCCY
     I**********                             13  16 E6SACD                                    CGL029
     I**********                             17  18 E6SASQ                                    CGL029
     I                                       13  22 E6SACD                                    CGL029
     I                                       23  24 E6SASQ                                    CGL029
      *
     I***WWSMAC*     DS                             12                                        CGL029
     IWWSMAC      DS                             18                                           CGL029
     I**********                              1  12 W6SMAC                                    CGL029
     I                                        1  18 W6SMAC                                    CGL029
     I                                        1   6 WWSCNM
     I**********                              7  10 WWSACD                                    CGL029
     I**********                             11  12 WWSASQ                                    CGL029
     I                                        7  16 WWSACD                                    CGL029
     I                                       17  18 WWSASQ                                    CGL029
     I**********                              1  10 WWRETN                                    CGL029
     I                                        1  16 WWRETN                                    CGL029
      *
     I***W1DATA*     DS                             18                                        CGL029
     IW1DATA      DS                             24                                           CGL029
     I                                        1   3 W1STBR
     I                                        4   9 W1STCN
     I                                       10  12 W1STCU
     I**********                             13  16 W1STAC                                    CGL029
     I**********                             17  18 W1STSQ                                    CGL029
     I                                       13  22 W1STAC                                    CGL029
     I                                       23  24 W1STSQ                                    CGL029
      *
     IREVWDS      DS                             36
     I                                        1   8 #0RREF
     I                                        9  10 #0RFTY
     I                                       11  12 #0RGRO
     I                                       13  18 #0RCNU
     I                                       19  22 #0RPOR
     I                                       23  23 #0RFRE
     I                                       24  29 #0RFNS
     I                                       30  35 #0RFIN
     I                                       36  36 #0RSTA
      *
     IZMUSER      DS                             17
      ** Data area ZMUSER
      *
     I                                       11  13 BVBRCD
     ISCSARD      DS                                                                          CSD011
     I                                        1   6 SARN                                      CSD011
     I                                        7  56 SARD                                      CSD011
      **                                                                                      CSD011
     IRUNDAT    E DS
      ** Standard Data Area Layout for System flags and Run Date
      *
     ISDBANK    E DSSDBANKPD
      ** Data structure for bank details
      *
     ISDMMOD    E DSSDMMODPD
      ** Data structure for Module details
      *
     ISDPRFC    E DSSDPRFCPD
      ** Data Structure for Access Object Retrieval
      *
     ISDCURR    E DSSDCURRPD
      ** Data structure for Currency Details
      *
     ISDBRCH    E DSSDBRCHPD
      ** Data structure for branch details
      *
     IACCNT     E DSACCNTAB
      ** Data Structure for Account details
      *
     ISDSTRD    E DSSDSTRDPD
      ** Data structure for securities trading details
      *
     ISDCUST    E DSSDCUSTPD
      ** External data structures for Customer Details
     I              QQDFAC                          QQDFAX                                    CGL029
      *
     I/COPY ZSRSRC,ZSEDITZ2
     I/COPY ZSRSRC,ZHOLI
     I/COPY ZSRSRC,ZHOLDS
      *
     *****************************************************************
      /EJECT
      ****************************************************************
      **    P R O G R A M   S T A R T
      ****************************************************************
      *
      ** Main Processing
      *
     C                     EXSR SRMNPR
      *
      ** Termination Processing
      *
     C                     EXSR SRTERM
      *
      *****************************************************************
      *     P R O G R A M     E N D                                   *
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRMNPR : Main processing                                      *
      *                                                               *
      * Called by:  Main program                                      *
      * Calls:  ZVACTU  ZVACTBU  SRSFCL                               *
      *****************************************************************
      *
     C           SRMNPR    BEGSR
      *
      ** While F3 not pressed
      *
     C           *INKC     DOWEQ*OFF
      *
      ** If first time processing or F5 is pressed
      *
     C           *IN45     IFEQ *OFF
     C           *INKE     OREQ *ON
      *
      ** If not Enquiry mode
      *
     C           *IN61     IFEQ *OFF
      *
      ** If Amend mode, build the first subfile page
      *
     C           *IN43     IFEQ *ON
     C                     MOVE *OFF      *IN45
      *
      ** Clear subfile
      *
     C                     EXSR SRSFCL
      *
      ** Protect on 'POSITION/SELECTION' fields *OFF
      *
     C                     MOVE *OFF      *IN55
      *
     C                     MOVE *OFF      *IN56
     C                     MOVE *OFF      *IN57
     C                     MOVE *ON       *IN58
      *
      ** Initialise error indicators
      *
     C                     EXSR SRINDI
      *
      ** Reinitialise POSITION/SELECTION Fields
      *
     C                     EXSR SRPSEL
      *
      ** Set key fields with blanks or zeros
      *
     C                     Z-ADD*ZEROS    KREFE
     C                     Z-ADD*ZEROS    KFNSD
     C                     Z-ADD*ZEROS    KFIND
      *
      ** Build the first subfile page
      *
     C                     EXSR SRSFAM
     C                     ELSE
      *
      ** If Add Mode, build a blank subfile
      *
     C                     EXSR SRSFCL
      *
      ** Protect on 'POSITION/SELECTION' fields
      *
     C                     MOVE *ON       *IN56
     C                     MOVE *ON       *IN57
     C                     MOVE *OFF      *IN58
      *
      ** Initialise error indicators
      *
     C                     EXSR SRINDI
      *
      ** Build a blank subfile
      *
     C                     EXSR SRSFAD
     C                     Z-ADD1         #0SRRN
     C                     ENDIF
      *
      ** If Enquiry Mode
      *
     C                     ELSE
     C                     MOVE *OFF      *IN45
      *
      ** Clear Subfile
      *
     C                     EXSR SRSFCL
      *
      ** Reinitialise POSITION/SELECTION fields
      *
     C                     EXSR SRPSEL
      *
      ** Initialise error indicators
      *
     C                     EXSR SRINDI
      *
      ** Set key fields with blanks or zeros
      *
     C                     Z-ADD*ZEROS    KREFE
     C                     Z-ADD*ZEROS    KFNSD
     C                     Z-ADD*ZEROS    KFIND
      *
      ** Build the first subfile page
      *
     C                     EXSR SRSFAM
     C                     ENDIF
      *
     C                     MOVE *ON       *IN45
     C                     ENDIF
      *
      ** If F9 is pressed
      *
     C           *INKI     IFEQ *ON
      *
      ** Check if user is authorised to action 'Insert'
      *
     C           AGMBIN    IFNE 'Y'
      *
      ** If single branch environment
      *
     C                     CALL 'ZVACTU'
     C                     PARM 'I'       PLACTN  1
     C                     PARM *ZERO     PLERR   10
      *
     C           PLERR     IFNE *ZERO
     C                     MOVEL'CSF9006' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
      *
     C                     ELSE
      *
      ** If multibranching environment
      *
     C                     CALL 'ZVACTBU'
     C                     PARM 'I'       PLACTN
     C                     PARM BVBRCD    PLBRCH  3
     C                     PARM *ZERO     PLERR
      *
     C           PLERR     IFEQ 1
     C                     MOVEL'CSF9007' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           PLERR     IFEQ 2
     C                     MOVEL'CSF9008' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           PLERR     IFEQ *ZERO
      *
      ** If Amend Mode, build a blank subfile
      *
     C           *IN43     IFEQ *ON
      *
      ** Initialise error indicators
      *
     C                     EXSR SRINDI
      *
      ** Clear Subfile
      *
     C                     EXSR SRSFCL
     C                     MOVE *ON       *IN67
     C                     MOVE *OFF      *IN66
     C                     MOVE *OFF      *IN68
      *
      ** Protect 'POSITION/SELECTION' fields
      *
     C                     MOVE *ON       *IN56
     C                     MOVE *ON       *IN57
     C                     MOVE *OFF      *IN58
      *
     C                     MOVE *OFF      *IN30
      *
      ** Build a blank subfile
      *
     C                     EXSR SRSFAD
     C                     Z-ADD1         #0SRRN
     C                     MOVE *OFF      *IN43
     C                     ELSE
      *
      ** Clear subfile
      *
     C                     EXSR SRSFCL
      *
      ** Initialise error indicators
      *
     C                     EXSR SRINDI
      *
      ** If Add Mode, build a first subfile page
      *
     C                     MOVE *ON       *IN68
     C                     MOVE *OFF      *IN66
     C                     MOVE *OFF      *IN67
      *
      ** Unprotect  'POSITION/SELECTION' Fields
      *
     C                     MOVE *OFF      *IN56
     C                     MOVE *OFF      *IN57
     C                     MOVE *ON       *IN58
      *
     C                     MOVE *OFF      *IN45
      *
      ** Build the first subfile page
      *
     C                     EXSR SRSFAM
     C                     MOVE *ON       *IN45
     C                     MOVE *ON       *IN43
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** If rollup is pressed
      *
     C           *IN27     IFEQ *ON
     C                     Z-ADDWWRRN     #0SRRN
      *
      ** Reinitialise error indicators
      *
     C                     EXSR SRINDI
      *
      ** Clear messages from program message queue.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     PAPGM
     C                     PARM '*SAME'   PAPGRL
      *
      ** If Add Mode only
      *
     C           *IN43     IFEQ *OFF
     C           *IN61     ANDEQ*OFF
      *
     C                     MOVE *BLANKS   #0HGRO
      *
      ** Build a blank subfile page
      *
     C                     EXSR SRSFAD
      *
      ** If Amend Mode or Enquiry Mode
      *
     C                     ELSE
      *
      ** Set key fields with blanks or zeros
      *
     C                     Z-ADD*ZEROS    KREFE
     C                     Z-ADD*ZEROS    KFNSD
     C                     Z-ADD*ZEROS    KFIND
      *
      ** Build the next subfile page
      *
     C                     EXSR SRSFAM
     C                     ENDIF
     C                     ENDIF
      *
     C           WVALID    IFEQ 'Y'
     C           WWREVW    ORNE REVWDS
     C           ERMSG     OREQ 'Y'
      *
     C                     MOVE *OFF      *IN50
     C                     MOVE *OFF      *IN51
     C                     MOVE *OFF      *IN52
     C                     MOVE *OFF      *IN53
     C                     MOVE *OFF      *IN69
     C                     MOVE *OFF      *IN86
     C                     MOVE *OFF      *IN76
     C                     MOVE *OFF      *IN77
     C                     MOVE *OFF      *IN78
     C                     MOVE *OFF      *IN82
      *
      ** If one of the POSITION/SELECTION Fields is changed
      *
     C           #0RREF    IFNE #0HRRE
     C           #0RFTY    ORNE #0HRFT
     C           #0RGRO    ORNE #0HRGR
     C           #0RCNU    ORNE #0HRCN
     C           #0RPOR    ORNE #0HRPO
     C           #0RFRE    ORNE #0HRFR
     C           #0RFNS    ORNE #0HRFN
     C           #0RFIN    ORNE #0HRFI
     C           #0RSTA    ORNE #0HRST
      *
      ** Validate POSITION/SELECTION fields
      *
     C                     EXSR SRVAL1
      *
      ** If no error is found, build the first subfile page
      *
     C           *IN50     IFEQ *OFF
     C           *IN51     ANDEQ*OFF
     C           *IN52     ANDEQ*OFF
     C           *IN53     ANDEQ*OFF
     C           *IN69     ANDEQ*OFF
     C           *IN86     ANDEQ*OFF
     C           *IN76     ANDEQ*OFF
     C           *IN77     ANDEQ*OFF
     C           *IN78     ANDEQ*OFF
     C           *IN82     ANDEQ*OFF
     C                     MOVE 'N'       ERMSG
      *
      ** Clear subfile
      *
     C                     EXSR SRSFCL
      *
      ** Set key fields with blanks or zeros
      *
     C                     Z-ADD*ZEROS    KREFE
     C                     Z-ADD*ZEROS    KFNSD
     C                     Z-ADD*ZEROS    KFIND
      *
      ** Build the first subfile page
      *
     C                     EXSR SRSFAM
      *
     C                     MOVE *BLANKS   #0RREF
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     MOVE REVWDS    WWREVW 36
      *
      ** Execute the subfile control record
      *
     C                     WRITE#MSGCTL
     C                     WRITEGL1908F0
     C                     EXFMTGL1908C0
      *
     C           *INKL     IFEQ *ON
     C           CHRREF    ANDNE*BLANKS
     C                     MOVE *ON       *INKC
     C                     ENDIF
      *
      ** Clear messages from program message queue.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     PAPGM
     C                     PARM '*SAME'   PAPGRL
      *
      ** If no function is pressed, validate all subfile records
      *
     C           *IN46     IFEQ *OFF
     C           #0RREF    ANDEQ#0HRRE
     C           #0RFTY    ANDEQ#0HRFT
     C           #0RGRO    ANDEQ#0HRGR
     C           #0RCNU    ANDEQ#0HRCN
     C           #0RPOR    ANDEQ#0HRPO
     C           #0RFRE    ANDEQ#0HRFR
     C           #0RFNS    ANDEQ#0HRFN
     C           #0RFIN    ANDEQ#0HRFI
     C           #0RSTA    ANDEQ#0HRST
     C           WWCPT     ANDNE*ZEROS
     C           WWREVW    OREQ REVWDS
     C           ERMSG     ANDNE'Y'
     C           WWCPT     ANDNE*ZEROS
     C           *IN46     ANDEQ*OFF
      *
      ** Reinitialise error indicators
      *
     C                     EXSR SRINDI
      *
     C                     EXSR SRVAL2
     C                     ENDIF
      *
     C           *IN43     IFEQ *ON
     C           #0SELE    ANDEQ*BLANKS
     C           #0RREF    ANDEQ*BLANKS
     C           #0RFTY    ANDEQ*BLANKS
     C           #0RGRO    ANDEQ*BLANKS
     C           #0RCNU    ANDEQ*BLANKS
     C           #0RPOR    ANDEQ*BLANKS
     C           #0RFRE    ANDEQ*BLANKS
     C           #0RFNS    ANDEQ*BLANKS
     C           #0RFIN    ANDEQ*BLANKS
     C           #0RSTA    ANDEQ*BLANKS
     C           *IN27     ANDEQ*OFF
     C           *IN46     ANDEQ*OFF
     C                     MOVE *OFF      *IN45
     C                     ELSE
     C                     MOVE *ON       *IN45
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           *INKL     IFEQ *ON
     C           *INKC     ANDEQ*ON
     C           CHRREF    ANDNE*BLANKS
     C                     MOVE *OFF      *INKC
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRTERM : Termination processing                               *
      *                                                               *
      * Called by:  Main program                                      *
      * Calls:  None                                                  *
      *****************************************************************
      *
     C           SRTERM    BEGSR
      *
     C           *INKC     IFEQ *ON
     C                     MOVE 'Q'       CHQUIT
     C                     ELSE
     C           *INKL     IFEQ *ON
     C                     MOVE 'U'       CHQUIT
     C                     ENDIF
     C                     ENDIF
      *
      ** Rollback uncommited changes
      *
     C                     ROLBK
      *
      ** End of program
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRSFCL - Subroutine to clear the subfile                      *
      *                                                               *
      * Called by:  SRMNPR  SRVAL2                                    *
      * Calls:  None                                                  *
      *****************************************************************
     C           SRSFCL    BEGSR
      *
      ** Clear subfile
      *
     C                     MOVE *ON       *IN80
     C                     WRITEGL1908C0
     C                     MOVE *OFF      *IN80
     C                     MOVE *OFF      *IN81
     C                     Z-ADD0         #0SRRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRSFAD : Build the subfile for Add Mode                       *
      *                                                               *
      * Called by:  SRMNPR                                            *
      * Calls:  SRPSEL  Y2CLMSC  SRCLRF                               *
      *****************************************************************
      *
     C           SRSFAD    BEGSR
      *
      ** Reinitialise POSITION/SELECTION fields
      *
     C                     EXSR SRPSEL
      *
      ** Clear messages from program message queue.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     PAPGM
     C                     PARM '*SAME'   PAPGRL
      *
      ** Load a blank subfile page
      *
     C                     MOVE *OFF      *IN84
      *
      ** Init screen line number
      *
     C                     Z-ADD0         WWCPT   20
      *
      ** Load subfile
      *
     C           WWCPT     DOWLT15
      *
     C                     ADD  1         #0SRRN     81
     C                     ADD  1         WWCPT
      *
      ** Clear subfile fields
      *
     C                     EXSR SRCLRF
      *
      ** Add record in subfile
      *
     C                     WRITEGL1908S0
     C                     ENDDO
      *
      ** Save position
      *
     C                     Z-ADD#0SRRN    WWRRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRSFAM : Build the subfile for amend/enquiry mode             *
      *                                                               *
      * Called by:  SRMNPR  SRVAL2                                    *
      * Calls:  ZDATE1  SRPREF  SRREAD  SRCLRF  SRLOAD                *
      *****************************************************************
      *
     C           SRSFAM    BEGSR
      *
      ** Load a blank subfile page
      *
     C                     MOVE *OFF      *IN84
      *
      ** If rollup (Amend/Enquiry), set key by last record of
      ** previous screen
      *
     C           *IN27     IFEQ *ON
     C           *IN43     ANDEQ*ON
     C           *IN27     OREQ *ON
     C           *IN61     ANDEQ*ON
      *
     C           #0SRRN    CHAINGL1908S0             88
     C                     MOVE #0REFE    KREFE
      *
     C                     MOVE #0FNSD    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    KFNSD
      *
     C                     MOVE #0FIND    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    KFIND
      *
     C                     ELSE
      *
     C           #0RREF    IFNE *BLANKS
     C           #0RFTY    ORNE *BLANKS
     C           #0RGRO    ORNE *BLANKS
     C           #0RCNU    ORNE *BLANKS
     C           #0RPOR    ORNE *BLANKS
     C           #0RFRE    ORNE *BLANKS
     C           #0RFNS    ORNE *BLANKS
     C           #0RFIN    ORNE *BLANKS
     C           #0RSTA    ORNE *BLANKS
     C                     MOVE #0RREF    KREFE
     C           KREFE     IFEQ *ZEROS
     C                     MOVE *OFF      *IN45
     C                     ENDIF
     C                     MOVE #0RFNS    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    KFNSD
     C                     MOVE #0RFIN    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    KFIND
     C                     ENDIF
     C                     ENDIF
      *
      ** Position into referencence file
      *
     C                     EXSR SRPREF
      *
      ** Read next record of the reference file
      *
     C                     EXSR SRREAD
      *
      ** Init screen line number
      *
     C                     Z-ADD0         WWCPT   20
      *
      ** Load subfile
      *
     C           *IN30     DOWEQ*OFF
     C           WWCPT     ANDLT15
      *
      ** If one of the 'SELECTION' Field is not blank
      **  Check if the record must be taken or not in the subfile
      *
     C                     MOVE 'N'       WREADN  1
     C           #0RFTY    IFNE *BLANKS
     C           #0RFTY    ANDNECHFTYP
     C           #0RGRO    ORNE *BLANKS
     C           #0RGRO    ANDNECHGROU
     C           #0RCNU    ORNE *BLANKS
     C           #0RCNU    ANDNECHCNUM
     C           #0RPOR    ORNE *BLANKS
     C           #0RPOR    ANDNECHPORT
     C           #0RFRE    ORNE *BLANKS
     C           #0RFRE    ANDNECHFREQ
     C           #0RFNS    ORNE *BLANKS
     C           KFNSD     ANDNECHFNSD
     C           #0RFIN    ORNE *BLANKS
     C           KFIND     ANDNECHFIND
     C           #0RSTA    ORNE *BLANKS
     C           #0RSTA    ANDNECHSTAT
     C                     MOVE 'Y'       WREADN
     C                     ENDIF
      *
     C           WREADN    IFEQ 'N'
      *
      ** If 'LOCKED BY USER IDENTIFIER' is not blank
      ** protect action field
      *
     C           CHUSER    IFNE *BLANKS
     C                     MOVE *ON       *IN55
     C                     ELSE
     C                     MOVE *OFF      *IN55
     C                     ENDIF
      *
     C                     ADD  1         #0SRRN     81
     C                     ADD  1         WWCPT
      *
      ** Clear subfile fields
      *
     C                     EXSR SRCLRF
      *
      ** Load subfile fields
      *
     C                     EXSR SRLOAD
      *
      ** Add record in subfile
      *
     C                     WRITEGL1908S0
      *
     C                     ENDIF
      *
      ** Read next record of the Reference File
      *
     C                     EXSR SRREAD
      *
      ** End of do while
      *
     C                     ENDDO
      *
      ** Save position
      *
     C                     Z-ADD#0SRRN    WWRRN
      *
      ** If not rollup
      *
     C           *IN27     IFEQ *OFF
     C                     Z-ADD1         #0SRRN
     C                     ENDIF
      *
      ** If no records found, display error message
      ** Send message 'NO DATA TO DISPLAY'
      *
     C           #0RFTY    IFNE *BLANKS
     C           #0RGRO    ORNE *BLANKS
     C           #0RCNU    ORNE *BLANKS
     C           #0RPOR    ORNE *BLANKS
     C           #0RFRE    ORNE *BLANKS
     C           #0RFNS    ORNE *BLANKS
     C           #0RFIN    ORNE *BLANKS
     C           #0RSTA    ORNE *BLANKS
      *
     C           WWCPT     IFLT 15
      *
      ** Reinitialise POSITION/SELECTION fields
      *
     C                     MOVE *BLANKS   #0RFTY
     C                     MOVE *BLANKS   #0RGRO
     C                     MOVE *BLANKS   #0RCNU
     C                     MOVE *BLANKS   #0RCNU
     C                     MOVE *BLANKS   #0RPOR
     C                     MOVE *BLANKS   #0RFRE
     C                     MOVE *BLANKS   #0RFNS
     C                     MOVE *BLANKS   #0RFIN
     C                     MOVE *BLANKS   #0RSTA
     C                     MOVE 'Y'       WVALID  1
     C                     ELSE
     C                     MOVE 'N'       WVALID
     C                     ENDIF
     C                     ELSE
     C                     MOVE 'Y'       WVALID
     C                     ENDIF
      *
     C           WWCPT     IFEQ *ZEROS
     C                     MOVEL'CSF9803' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRVAL1 : Validate Position/Selection fields                   *
      *                                                               *
      * Called by:  SRMNPR                                            *
      * Calls: AOCUSTR0                                               *
      *****************************************************************
      *
     C           SRVAL1    BEGSR
      *
     C                     MOVE *OFF      *IN50
     C                     MOVE *OFF      *IN51
     C                     MOVE *OFF      *IN52
     C                     MOVE *OFF      *IN53
     C                     MOVE *OFF      *IN86
     C                     MOVE *OFF      *IN76
     C                     MOVE *OFF      *IN77
     C                     MOVE *OFF      *IN78
     C                     MOVE *OFF      *IN82
      *
     C           #0RREF    IFNE *BLANKS
      *
      ** Check if valid numeric field
      *
     C           DIGITS    CHECK#0RREF:1  N       10     89
      *
      ** If not a valid numeric field
      *
     C           *IN89     IFEQ *ON
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN50
     C                     MOVEL'CSF9802' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
     C           #0RFTY    IFNE *BLANKS
      *
      ** Access fee types details
      *
     C           #0RFTY    CHAINSDCFTPL0             89
      *
      ** If record is not found
      *
     C           *IN89     IFEQ *ON
     C           F1RECI    OREQ '*'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN51
     C                     MOVEL'CSF9010' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
     C           #0RCNU    IFNE *BLANKS
      *
      ** Access customer details
      *
     C                     MOVEL#0RCNU    PCNUM     P
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM           PCNUM
     C                     PARM *BLANKS   PKSTS   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           PCNUM     IFNE *BLANKS
     C                     MOVELPCNUM     #0RCNU
     C                     ENDIF
      *
     C           PRTCD     IFNE *BLANKS
     C           BBDTDL    ORNE *ZEROS
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN52
     C                     MOVEL'CSF9807' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
     C           #0RFRE    IFNE *BLANKS
     C           #0RFRE    ANDNE'M'
     C           #0RFRE    ANDNE'Q'
     C           #0RFRE    ANDNE'X'
     C           #0RFRE    ANDNE'Y'
     C           #0RFRE    ANDNE'1'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN76
     C                     MOVEL'CSF9819' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
      *
     C           #0RFNS    IFNE *BLANKS
     C                     MOVEL#0RFNS    WNUM7   70
     C                     MOVELWNUM7     WALF6   6
     C                     MOVE #0RFNS    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    W1FNSD
      *
      ** If it is not a valid date format
      *
     C           *IN99     IFEQ *ON
     C           #0RFNS    ORNE WALF6
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN77
     C                     MOVEL'CSF9824' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
     C           #0RFIN    IFNE *BLANKS
     C                     MOVEL#0RFIN    WNUM7
     C                     MOVELWNUM7     WALF6
     C                     MOVE #0RFIN    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    W1FIND  50
      *
      ** If it is not a valid date format
      *
     C           *IN99     IFEQ *ON
     C           #0RFIN    ORNE WALF6
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN78
     C                     MOVEL'CSF9825' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
     C           #0RSTA    IFNE *BLANKS
     C           #0RSTA    ANDNE'U'
     C           #0RSTA    ANDNE'D'
     C           #0RSTA    ANDNE'A'
     C           #0RSTA    ANDNE'G'
     C           #0RSTA    ANDNE'E'
     C           #0RSTA    ANDNE'O'
     C           #0RSTA    ANDNE'S'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN82
     C                     MOVEL'CSF9831' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRVAL2 : Validate subfile records                             *
      *                                                               *
      * Called by:  SRMNPR                                            *
      * Calls: AOCUSTR0  SRVAL3  SRACTN  SRDTLS                       *
      *****************************************************************
      *
     C           SRVAL2    BEGSR
      *
     C                     MOVE 'N'       ERMSG   1
      *
      ** Clear messages from program message queue.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     PAPGM
     C                     PARM '*SAME'   PAPGRL
      *
      ** Read changed subfile record
      *
     C                     READCGL1908S0                 87
      *
      ** Setup the RRN with the current CPF'S position within subfile
      *
     C           *IN87     IFEQ *ON
     C                     Z-ADDCPFPOS    #0SRRN
     C                     ENDIF
      *
      ** Do while end of file is not reached and F12 or F3 not
      ** pressed
      *
     C           *IN87     DOWEQ*OFF
     C           *INKC     ANDEQ*OFF
     C           *INKL     ANDEQ*OFF
      *
     C           #0GROU    IFNE #0HGRO
     C                     MOVE 'N'       #0HWAR  1
     C                     MOVE #0GROU    #0HGRO
     C                     ELSE
     C                     MOVE 'Y'       #0HWAR
     C                     ENDIF
      *
      ** Reinitialise error indicators
      *
     C                     EXSR SRINDI
      *
     C                     MOVE *ON       *IN84
      *
      ** If 'Add Mode'
      *
     C           *IN43     IFEQ *OFF
     C           *IN61     ANDEQ*OFF
      *
      ** If at least one field is different than blanks
      *
     C           #0REFE    IFNE *BLANKS
     C           #0FTYP    ORNE *BLANKS
     C           #0GROU    ORNE *BLANKS
     C           #0CNUM    ORNE *BLANKS
     C           #0PORT    ORNE *BLANKS
      *
      ** If 'Charge Reference' not blank
      *
     C           #0REFE    IFNE *BLANKS
      *
     C                     MOVE #0REFE    W1REFE  80
     C           W1REFE    CHAINGLCHRGL1             89
      *
      ** If record is found
      *
     C           *IN89     IFEQ *OFF
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN32
     C                     MOVEL'CSF9801' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
      *
      ** Check if a valid numberic field
      *
     C           DIGITS    CHECK#0REFE:1  N       10     89
      *
      ** If not a valid numeric field
      *
     C           *IN89     IFEQ *ON
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN32
     C                     MOVEL'CSF9802' PAMSID
     C                     EXSR SRSNMS
      *
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** If 'Fee Type' left blank
      *
     C           #0FTYP    IFEQ *BLANKS
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN39
     C                     MOVEL'CSF9804' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
      *
      ** Access fee types details
      *
     C           #0FTYP    CHAINSDCFTPL0             89
      *
      ** If record is not found
      *
     C           *IN89     IFEQ *ON
     C           F1RECI    OREQ '*'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN39
     C                     MOVEL'CSF9010' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
     C           #0GROU    IFNE *BLANKS
      *
      ** Check if the fee type override charge group exists
      ** else check with the default charge group
      *
     C                     MOVE #0FTYP    KWFETP
     C                     MOVE #0GROU    KWCHGR
     C           KWFEE     CHAINSDCFTPL0             89
      *
     C           *IN89     IFEQ *ON
     C           F1RECI    OREQ '*'
      *
     C           #0HWAR    IFEQ 'N'
     C           #0HWAR    OREQ ' '
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN36
     C                     MOVEL'CSF9812' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
      *
     C                     MOVE 'Y'       #0HWAR
      *
      ** If *IN89 is *off then we have read in a different record                             204838
      ** from SDCFTPPD. We need to re access the 'master record' for                          204838
      ** this fee type since details of it are used later.                                    204838
      *                                                                                       204838
     C                     ELSE                                                               204838
     C           #0FTYP    CHAINSDCFTPL0             89                                       204838
      *                                                                                       204838
      ** If record is not found                                                               204838
      *                                                                                       204838
     C           *IN89     IFEQ *ON                                                           204838
     C           F1RECI    OREQ '*'                                                           204838
     C                     MOVE 'Y'       ERMSG                                               204838
     C                     MOVE *ON       *IN39                                               204838
     C                     MOVEL'CSF9010' PAMSID                                              204838
     C                     EXSR SRSNMS                                                        204838
     C                     ENDIF                                                              204838
     C                     ENDIF
     C                     ENDIF
      *
      ** If 'Customer' left blank
      *
     C           #0CNUM    IFEQ *BLANKS
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN34
     C                     MOVEL'CSF9806' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
      *
      ** Access customer details
      *
     C                     MOVEL#0CNUM    PCNUM     P
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM           PCNUM
     C                     PARM *BLANKS   PKSTS
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           PCNUM     IFNE *BLANKS
     C                     MOVELPCNUM     #0CNUM
     C                     ENDIF
      *
     C           PRTCD     IFNE *BLANKS
     C           BBDTDL    ORNE *ZEROS
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN34
     C                     MOVEL'CSF9807' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
      ** If 'Portfolio' entered
      *
     C           #0PORT    IFNE *BLANKS
     C           *IN65     ANDEQ*OFF
      *
     C           F1FECP    IFNE 'P'
     C           *IN36     ANDEQ*OFF
     C           *IN39     ANDEQ*OFF
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN35
     C                     MOVEL'CSF9837' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
      *
     C           *IN34     IFEQ *ON
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN35
     C                     MOVEL'CSF9139' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
      *
     C                     MOVE #0CNUM    KCUST
     C                     MOVE #0PORT    KPTFR
      *
      ** Access portfolio details
      *
     C           KEY1      CHAINPMPORTLL             89
      *
     C           *IN89     IFEQ *ON
     C           PNRECI    ORNE 'D'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN35
     C                     MOVEL'CSF9139' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C                     ELSE
      *
     C           F1FECP    IFEQ 'P'
     C           *IN36     ANDEQ*OFF
     C           *IN39     ANDEQ*OFF
     C           *IN65     ANDEQ*OFF
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN35
     C                     MOVEL'CSF9838' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
      ** Record must be unique for customer/portfolio/fee type
      *
     C           *IN34     IFEQ *OFF
     C           *IN35     ANDEQ*OFF
     C           *IN39     ANDEQ*OFF
     C                     MOVE #0CNUM    KCNUM
     C                     MOVE #0PORT    KPTFR
     C                     MOVE #0FTYP    KFETP
     C           KEY2      CHAINGLCHRGL6             89
      *
     C           *IN89     IFEQ *OFF
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN35
     C                     MOVE *ON       *IN39
     C                     MOVEL'CSF9840' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** If 'Amend Mode' or 'Enquiry' Mode
      *
     C           *IN43     IFEQ *ON
     C           *IN61     OREQ *ON
      *
      ** If action indicator is changed
      *
     C           #0SELE    IFNE *BLANKS
     C           ERMSG     ANDEQ'N'
     C                     EXSR SRVAL3
     C                     ENDIF
      *
      ** Process action (only Enquiry Mode)
      *
     C           ERMSG     IFEQ 'N'
     C           *IN61     ANDEQ*ON
     C                     EXSR SRACTN
     C                     MOVE *OFF      *IN84
     C                     ENDIF
     C                     ENDIF
      *
      ** If an error message is sent
      ** or if 'add mode'
      *
     C           ERMSG     IFEQ 'Y'
     C           *IN43     OREQ *OFF
      *
     C                     MOVE #0REFE    KREFE
     C           KREFE     CHAINGLCHRGL0             89
      *
      ** If 'locked by user identifier' is not blank
      ** protect action field
      *
     C           CHUSER    IFNE *BLANKS
     C                     MOVE *ON       *IN55
     C                     ELSE
     C                     MOVE *OFF      *IN55
     C                     ENDIF
      *
      ** Update subfile record (for reversed error fields)
      *
     C                     UPDATGL1908S0
      *
      ** Goto next read of the subfile changing
      *
     C                     GOTO TAG4
     C                     ENDIF
      *
      ** If 'Amend Mode'
      *
     C           *IN43     IFEQ *ON
      *
     C                     MOVE #0REFE    KREFE
     C           KREFE     CHAINGLCHRGL0             89
      *
      ** If record is different than read at charging of subfile
      *
     C           FIELD     IFNE #0HFLD
     C           #0SELE    IFNE *BLANKS
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN33
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN35
     C                     MOVE *ON       *IN36
     C                     MOVE *ON       *IN37
     C                     MOVE *ON       *IN39
     C                     MOVEL'CSF9809' PAMSID
     C                     EXSR SRSNMS
      *
      ** Update subfile record (for reversed error fields)
      *
     C                     UPDATGL1908S0
      *
      ** Release the record
      *
     C                     MOVE #0REFE    KREFE      89
     C           KREFE     SETLLGLCHRGL0
      *
      ** Goto next read of the subfile changing
      *
     C                     GOTO TAG4
     C                     ENDIF
     C                     ELSE
     C                     MOVE CHJOBN    WAJOBN 10
     C                     MOVE CHUSER    WAUSER 10
     C                     Z-ADDCHJNBR    WAJNBR  60
     C                     MOVE *BLANKS   CHJOBN
     C                     MOVE *BLANKS   CHUSER
     C                     Z-ADD*ZEROS    CHJNBR
     C                     MOVE FIELD     #0HFLD
     C                     MOVE WAJOBN    CHJOBN
     C                     MOVE WAUSER    CHUSER
     C                     Z-ADDWAJNBR    CHJNBR
     C                     ENDIF
      *
      ** Execute commitment control
      *
     C                     COMIT
      *
      ** If action is changed
      *
     C           #0SELE    IFNE #0HSEL
      *
      ** Access charge reference with current reference
      *
     C                     MOVE #0REFE    KREFE
     C           KREFE     CHAINGLCHRGL0             89
      *
      ** If record is different than read at charging of subfile
      *
     C           FIELD     IFNE #0HFLD
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN33
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN35
     C                     MOVE *ON       *IN36
     C                     MOVE *ON       *IN37
     C                     MOVE *ON       *IN39
     C                     MOVEL'CSF9809' PAMSID
     C                     EXSR SRSNMS
      *
      ** Update subfile record (for reversed error fields)
      *
     C                     UPDATGL1908S0
      *
      ** Release the record
      *
     C                     MOVE #0REFE    KREFE
     C           KREFE     SETLLGLCHRGL0
      *
      ** Goto next read of the subfile changing
      *
     C                     GOTO TAG4
     C                     ELSE
      *
     C                     MOVE #0WSID    CHJOBN
     C                     MOVE #0USER    CHUSER
     C                     Z-ADD##JNO     CHJNBR
      *
      ** Update the record in charge reference file
      *
     C                     UPDATGLCHRGD0
     C                     MOVE CHJOBN    WAJOBN
     C                     MOVE CHUSER    WAUSER
     C                     Z-ADDCHJNBR    WAJNBR
     C                     MOVE *BLANKS   CHJOBN
     C                     MOVE *BLANKS   CHUSER
     C                     Z-ADD*ZEROS    CHJNBR
     C                     MOVE FIELD     #0HFLD
     C                     MOVE WAJOBN    CHJOBN
     C                     MOVE WAUSER    CHUSER
     C                     Z-ADDWAJNBR    CHJNBR
      *
      ** Execute the commitment instruction
      *
     C                     COMIT
      *
      ** Process action
      *
     C                     EXSR SRACTN
     C                     ENDIF
     C                     ENDIF
      *
      ** If 'LOCKED BY USER IDENTIFIER' is not blank
      **  protect action field
      *
     C           CHUSER    IFNE *BLANKS
     C                     MOVE *ON       *IN55
     C                     ELSE
     C                     MOVE *OFF      *IN55
     C                     ENDIF
      *
      ** If no error message sent
      *
     C           ERMSG     IFEQ 'N'
     C                     MOVE *OFF      *IN84
     C                     UPDATGL1908S0
     C                     ELSE
     C                     MOVE *ON       *IN84
     C                     UPDATGL1908S0
     C                     ENDIF
     C                     ENDIF
      *
     C           TAG4      TAG
      *
     C                     WRITE#MSGCTL
      *
      ** Read changed subfile record, if F12 not pressed
      *
     C           *INKL     IFEQ '0'
     C                     READCGL1908S0                 87
     C                     ENDIF
      *
     C                     ENDDO
      *
      ** If 'ADD MODE'
      ** and if no error message is sent
      *
     C           *IN43     IFEQ *OFF
     C           *IN61     ANDEQ*OFF
     C           ERMSG     ANDEQ'N'
      *
     C                     Z-ADD1         #0SRRN
     C                     MOVE 'Y'       FINSR   1
      *
      ** Read first record of subfile
      *
     C                     READCGL1908S0                 87
      *
      ** Do while end of subfile is not reached
      *
     C           *IN87     DOWEQ*OFF
      *
      ** If charge reference is left blank
      *
     C           #0CNUM    IFNE *BLANKS
      *
     C                     MOVE #0REFE    KREFE
     C           KREFE     CHAINGLCHRGL0             88
      *
      ** If a record is found
      *
     C           *IN88     IFEQ *OFF
     C                     MOVEL'CSF9833' PAMSID
     C                     EXSR SRSNMS
      *
      ** Update subfile record
      *
     C                     UPDATGL1908S0
      *
      ** Rollback instruction to reverse all inserts
      *
     C                     ROLBK
      *
      ** Terminate current processing
      *
     C                     GOTO TAG5
     C                     ELSE
      *
      ** Write a record in charge fee file
      *
     C                     MOVE 'D'       CHRECI
      *
     C           #0REFE    IFEQ *BLANKS
      *
      ** Retrieve next available reference number
      *
     C                     EXSR SRRTRV
     C                     ENDIF
      *
     C                     MOVE #0REFE    CHREFE
     C                     MOVE #0FTYP    CHFTYP
     C                     MOVE #0CNUM    CHCNUM
     C                     MOVE #0PORT    CHPORT
     C                     MOVE #0GROU    CHGROU
     C                     MOVE *BLANKS   CHCCY
     C                     Z-ADD*ZEROS    CHAMT
     C                     MOVE *BLANKS   CHBRCA
      *
     C           #0CNUM    CHAINSDCFCDL1             89
     C           *IN89     IFEQ *OFF
     C           E6SMAC    ANDNE*BLANKS
      *
     C                     MOVE E6SMAC    WWSMAC
     C                     MOVE WWSCNM    E6SCNM
     C                     MOVE WWSACD    E6SACD
     C                     MOVE WWSASQ    E6SASQ
      *
     C           E6SASQ    IFNE *BLANKS
     C                     MOVELE6DATA    CHSEAC    P
     C                     ELSE
     C                     CALL 'AOACCTR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM WWRETN    PKRET  10                                           CGL029
     C                     PARM *BLANKS   PCNUM  10
     C                     PARM *BLANKS   PCCY    3
     C**********           PARM *BLANKS   PACOD   4                                           CGL029
     C                     PARM *BLANKS   PACOD  10                                           CGL029
     C                     PARM *BLANKS   PACSQ   2
     C                     PARM *BLANKS   PBRCA   3
     C           ACCNT     PARM ACCNT     DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE
     C                     MOVEL'SDACCTPD'DBFILE
     C                     MOVELPOPTN     DBKEY
     C                     MOVEL'GL1908'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVELBRCA      PSTBR
     C                     MOVELCNUM      PSTCN
     C                     MOVELCCY       PSTCU
     C                     MOVELACOD      PSTAC
     C                     MOVELACSQ      PSTSQ
     C                     MOVELPDATA     CHSEAC
     C                     ENDIF
     C                     ENDIF
      *
     C                     ELSE
     C                     MOVE *BLANKS   CHSEAC
     C                     ENDIF
     C                     MOVELCHSEAC    #1SEAC
      *
     C                     Z-ADD*ZEROS    CHSLSD
     C                     MOVE *BLANKS   CHAPOS
     C                     MOVE *BLANKS   CHPFTC
     C                     Z-ADD*ZEROS    CHSEDA
     C                     MOVE *BLANKS   CHFREQ
     C                     Z-ADD*ZEROS    CHDAMO
     C                     Z-ADD*ZEROS    CHFNSD
     C                     Z-ADD*ZEROS    CHFIND
     C                     MOVE *BLANKS   CHACCR
     C                     MOVE *BLANKS   CHNAR1
     C                     MOVE *BLANKS   CHNAR2
     C                     MOVE *BLANKS   CHNAR3
     C                     MOVE *BLANKS   CHNAR4
     C                     MOVE 'U'       CHSTAT
     C                     MOVE #0WSID    CHJOBN
     C                     MOVE #0USER    CHUSER
     C                     Z-ADD##JNO     CHJNBR
     C                     MOVE 'I'       CHCHTP
     C                     Z-ADDBJRDNB    CHLCD
      *
     C                     WRITEGLCHRGD0
      *
      ** Update subfile record
      *
     C                     UPDATGL1908S0
      *
      ** Initialise error indicators
      *
     C                     MOVE *OFF      *IN15
     C                     MOVE *OFF      *IN16
     C                     MOVE *OFF      *IN17
      *
     C                     MOVE *OFF      *IN11
     C                     MOVE *OFF      *IN19
     C                     MOVE *OFF      *IN20
     C                     MOVE *OFF      *IN21
     C                     MOVE *OFF      *IN22
     C                     MOVE *OFF      *IN23
     C                     MOVE *OFF      *IN24
     C                     MOVE *OFF      *IN25
     C                     MOVE *OFF      *IN26
     C                     MOVE *OFF      *IN28
      *
     C                     MOVE *OFF      *IN31
     C                     MOVE *OFF      *IN38
      *
     C                     MOVE *OFF      *IN37
      *
     C                     MOVE *OFF      *IN60            High Water M F                     CSD011
     C                     MOVE *OFF      *IN48            Calculation Frq                    CSD011
     C                     MOVE *OFF      *IN59            Calc Day in Mnt                    CSD011
     C                     MOVE *OFF      *IN79            Next Calc Date                     CSD011
      *
     C                     EXSR SRDTLS
     C                     MOVE *OFF      *IN43
     C                     MOVE ' '       #0SELE
      *
     C           *INKC     IFEQ *ON
     C           *INKL     OREQ *ON
     C                     MOVE *ON       *IN89
     C                     ROLBK
     C                     ELSE
      *
     C                     MOVE #0REFE    KREFE
     C           KREFE     CHAINGLCHRGL0             89
      *
     C                     MOVE *BLANKS   CHJOBN
     C                     MOVE *BLANKS   CHUSER
     C                     Z-ADD*ZEROS    CHJNBR
     C                     ENDIF
      *
     C           *IN89     IFEQ *OFF
     C                     UPDATGLCHRGD0
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** Read first record of subfile
      *
     C                     READCGL1908S0                 87
     C                     ENDDO
      *
      ** If at least one record created
      *
     C           WWREFE    IFNE WKREFE
     C           *LOVAL    SETLLGLCHRNPD
     C                     READ GLCHRNPD                 89
      *
     C           *IN89     IFEQ *OFF
     C                     MOVE WWREFE    CXREFE
     C                     UPDATGLCHRND0
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Execute commitment control to fix all inserted records
      *
     C                     COMIT
      *
      ** Set key fields from KLIST to first inserted record
      *
     C                     Z-ADD0         WWRRN   40
      *
      ** Clear Subfile
      *
     C                     EXSR SRSFCL
      *
      ** Set indicator to return to 'Amend Mode'
      *
     C                     MOVE *ON       *IN43
     C                     MOVE *ON       *IN68
     C                     MOVE *OFF      *IN66
     C                     MOVE *OFF      *IN67
      *
      ** Unprotect 'POSITION/SELECTION' Fields
      *
     C                     MOVE *OFF      *IN56
     C                     MOVE *OFF      *IN57
     C                     MOVE *ON       *IN58
      *
     C                     MOVE *OFF      *IN45
     C                     MOVE *OFF      *IN30
      *
     C                     EXSR SRSFAM
      *
     C                     MOVE *ON       *IN45
      *
     C                     ENDIF
      *
     C           TAG5      ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRVAL3 : Validate 'Action' Field                              *
      *                                                               *
      * Called by:  SRVAL2                                            *
      * Calls:  SRSNMS                                                *
      *****************************************************************
      *
     C           SRVAL3    BEGSR
      *
      ** If Enquiry Mode, 'Action' must be equal to 'E'
      *
     C           *IN61     IFEQ *ON
      *
     C           #0SELE    IFNE 'E'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN62
     C                     MOVEL'CSF9805' PAMSID
     C                     EXSR SRSNMS
     C                     GOTO TAG2
     C                     ENDIF
      *
     C                     ELSE
      *
      **-----------------------------------------------------*
      **           *             STATUS                      *
      ** Action    *  U  *  D  *  A  *  G  *  E  *  O  *  S  *
      **-----------------------------------------------------*
      ** Amend     *  Y  *  N  *  Y  *  Y  *  Y  *  Y  *  Y  *
      ** Delete    *  Y  *  N  *  Y  *  Y  *  Y  *  Y  *  Y  *
      ** Enquire   *  Y  *  Y  *  Y  *  Y  *  Y  *  Y  *  Y  *
      ** Release   *  N  *  N  *  N  *  N  *  N  *  N  *  Y  *
      ** Stop      *  Y  *  N  *  Y  *  N  *  N  *  N  *  N  *
      ** Authorise *  Y  *  N  *  N  *  N  *  N  *  N  *  N  *
      **-----------------------------------------------------*
      *
      ** Action Code can be 'A','D','E','R','S','X'
      *
     C           #0SELE    IFNE 'A'
     C           #0SELE    ANDNE'D'
     C           #0SELE    ANDNE'E'
     C           #0SELE    ANDNE'R'
     C           #0SELE    ANDNE'S'
     C           #0SELE    ANDNE'X'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN62
     C                     MOVEL'CSF9808' PAMSID
     C                     EXSR SRSNMS
     C                     GOTO TAG2
     C                     ENDIF
      *
     C           #0STAT    IFEQ 'D'
     C           #0SELE    ANDNE'E'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN62
     C                     MOVEL'CSF9834' PAMSID
     C                     EXSR SRSNMS
     C                     GOTO TAG2
     C                     ENDIF
      *
     C           #0SELE    IFEQ 'A'
     C           #0STAT    ANDNE'U'
     C           #0STAT    ANDNE'A'
     C           #0STAT    ANDNE'S'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN62
     C                     MOVEL'CSF9828' PAMSID
     C                     EXSR SRSNMS
     C                     GOTO TAG2
     C                     ENDIF
      *
     C           #0SELE    IFEQ 'S'
     C           #0STAT    ANDNE'U'
     C           #0STAT    ANDNE'A'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN62
     C                     MOVEL'CSF9829' PAMSID
     C                     EXSR SRSNMS
     C                     GOTO TAG2
     C                     ENDIF
      *
     C           #0SELE    IFEQ 'R'
     C           #0STAT    ANDNE'S'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN62
     C                     MOVEL'CSF9830' PAMSID
     C                     EXSR SRSNMS
     C                     GOTO TAG2
     C                     ENDIF
      *
     C           #0SELE    IFEQ 'X'
     C           #0STAT    ANDNE'U'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN62
     C                     MOVEL'CSF9835' PAMSID
     C                     EXSR SRSNMS
     C                     GOTO TAG2
     C                     ENDIF
     C                     ENDIF
      *
      ** Access reference file
      *
     C                     MOVE #0REFE    KREFE
     C           KREFE     CHAINGLCHRGL1             89
      *
      ** If locked by user identifier is not blank
      *
     C           CHUSER    IFNE *BLANKS
     C                     MOVE CHUSER    WUSER
     C                     MOVE CHJOBN    WJOBN
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN33
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN35
     C                     MOVE *ON       *IN36
     C                     MOVE *ON       *IN37
     C                     MOVE *ON       *IN39
     C                     MOVELLOCKDS    PAMSDA
     C                     MOVEL'CSF9811' PAMSID
     C                     EXSR SRSNMS
     C                     GOTO TAG2
     C                     ENDIF
      *
      ** Check if user is authorised to the Action Code
      *
     C                     MOVE #0SELE    PLACTN
      *
      ** If single branch environment
      *
     C           AGMBIN    IFNE 'Y'
     C                     CALL 'ZVACTU'
     C                     PARM           PLACTN
     C                     PARM           PLERR
      *
     C           PLERR     IFNE *ZERO
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN62
     C                     MOVEL'CSF9006' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
      *
     C                     ELSE
      *
      *   If multibranching environment
      *
     C                     CALL 'ZVACTBU'
     C                     PARM           PLACTN
     C                     PARM BVBRCD    PLBRCH
     C                     PARM           PLERR
      *
     C           PLERR     IFEQ 1
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN62
     C                     MOVEL'CSF9007' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           PLERR     IFEQ 2
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN62
     C                     MOVEL'CSF9008' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C           TAG2      ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRACTN : Process 'Action'                                     *
      *                                                               *
      * Called by:  SRVAL2                                            *
      * Calls:  SRDTLS  SRVDTL  SRSNMS  SRCONF  ZDATE1  ZDATE2        *
      *****************************************************************
      *
     C           SRACTN    BEGSR
      *
      ** Initialise error indicators
      *
     C                     MOVE *OFF      *IN15
     C                     MOVE *OFF      *IN16
     C                     MOVE *OFF      *IN17
      *
     C                     MOVE *OFF      *IN11
     C                     MOVE *OFF      *IN19
     C                     MOVE *OFF      *IN20
     C                     MOVE *OFF      *IN21
     C                     MOVE *OFF      *IN22
     C                     MOVE *OFF      *IN23
     C                     MOVE *OFF      *IN24
     C                     MOVE *OFF      *IN25
     C                     MOVE *OFF      *IN26
     C                     MOVE *OFF      *IN28
      *
     C                     MOVE *OFF      *IN31
      *
     C                     MOVE *OFF      *IN37
      *
     C                     MOVE *OFF      *IN60            High Water M F                     CSD011
     C                     MOVE *OFF      *IN48            Calculation Frq                    CSD011
     C                     MOVE *OFF      *IN59            Calc Day in Mnt                    CSD011
     C                     MOVE *OFF      *IN79            Next Calc Date                     CSD011
      *
      ** Produce 'FULL DETAIL SCREEN'
      *
     C           #0SELE    IFEQ 'A'
     C           #0SELE    OREQ 'E'
     C                     EXSR SRDTLS
      *
      ** Access customer fees charge
      *
     C                     MOVE #0REFE    KREFE
     C           KREFE     CHAINGLCHRGL0             88
      *
     C           #0SELE    IFEQ 'A'
     C           *INKC     ANDEQ*OFF
     C           *INKL     ANDEQ*OFF
     C                     MOVE 'D'       CHRECI
     C                     Z-ADDBJRDNB    CHLCD
     C                     MOVE 'A'       CHCHTP
     C                     MOVE 'U'       CHSTAT
      *
     C                     MOVE 'U'       #0STAT
     C                     ENDIF
      *
     C                     MOVE *BLANKS   CHJOBN
     C                     MOVE *BLANKS   CHUSER
     C                     Z-ADD*ZEROS    CHJNBR
      *
     C                     UPDATGLCHRGD0
      *
      ** Update subfile fields
      *
     C                     MOVE FIELD     #0HFLD
      *
      ** Commitment instruction
      *
     C                     COMIT
      *
     C                     MOVE *BLANKS   #0SELE
      *
     C                     ENDIF
      *
      ** If 'ACTION' is release, delete, authorisation or stop
      *
     C           #0SELE    IFEQ 'D'
     C           #0SELE    OREQ 'R'
     C           #0SELE    OREQ 'S'
     C           #0SELE    OREQ 'X'
      *
      ** Move Subfile Fields onto Screen Fields
      *
     C                     EXSR SRMOVE
      *
     C                     MOVE *ON       *IN18
     C                     MOVE 'N'       ERROR
      *
     C           #0SELE    IFNE 'D'
     C                     MOVE 'Y'       TSTVAL
     C                     MOVE 'Y'       WARN    1
      *
      ** Verify all details entered and validate
      *
     C                     EXSR SRVDTL
     C                     MOVE 'N'       WARN
     C                     ENDIF
      *
      ** Clear messages from program message queue.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     PAPGM
     C                     PARM '*SAME'   PAPGRL
      *
     C           ERROR     IFEQ 'Y'
      *
     C                     MOVE 'Y'       ERMSG
     C                     MOVE *ON       *IN62
     C                     MOVEL'CSF9832' PAMSID
     C                     EXSR SRSNMS
      *
     C                     MOVE #0REFE    KREFE
     C           KREFE     CHAINGLCHRGL0             88
      *
     C                     MOVE *BLANKS   CHJOBN
     C                     MOVE *BLANKS   CHUSER
     C                     Z-ADD*ZEROS    CHJNBR
      *
     C                     UPDATGLCHRGD0
      *
      ** Update subfile fields
      *
     C                     MOVE FIELD     #0HFLD
      *
     C                     COMIT
      *
      ** Terminate current processing
      *
     C                     GOTO TAG3
     C                     ELSE
      *
      ** Produce confirmation screen
      *
     C                     EXSR SRCONF
      *
      ** Clear messages from program message queue.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     PAPGM
     C                     PARM '*SAME'   PAPGRL
      *
     C           WWCONF    IFNE 'Y'
     C           ERROR     OREQ 'Y'
      *
      ** Access to charge file with current reference
      *
     C                     MOVE #0REFE    KREFE
     C           KREFE     CHAINGLCHRGL0             88
      *
     C                     MOVE *BLANKS   CHJOBN
     C                     MOVE *BLANKS   CHUSER
     C                     Z-ADD*ZEROS    CHJNBR
      *
     C                     UPDATGLCHRGD0
     C                     MOVE FIELD     #0HFLD
      *
     C                     COMIT
      *
     C                     MOVE *BLANKS   #0SELE
      *
      ** Terminate current processing
      *
     C                     GOTO TAG3
      *
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** If 'ACTION' is release
      *
     C           #0SELE    IFEQ 'R'
      *
     C                     MOVE #0REFE    KREFE
     C           KREFE     CHAINGLCHRGL0             88
      *
     C                     MOVE *BLANKS   CHJOBN
     C                     MOVE *BLANKS   CHUSER
     C                     Z-ADD*ZEROS    CHJNBR
      *
     C                     MOVE 'D'       CHRECI
     C                     Z-ADDBJRDNB    CHLCD
     C                     MOVE 'R'       CHCHTP
     C                     MOVE 'U'       CHSTAT
      *
     C           #0FNSD    IFNE *ZEROS
     C                     MOVE #0FNSD    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    CHFNSD
     C                     ENDIF
      *
     C           #0HSLS    IFNE *BLANKS
     C                     MOVE #0HSLS    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    CHSLSD
     C                     ENDIF
      *
     C           #0FIND    IFNE *ZEROS
     C                     MOVE #0FIND    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    CHFIND
     C                     ENDIF
      *
     C                     UPDATGLCHRGD0
      *
     C                     MOVE 'U'       #0STAT
      *
     C                     MOVE FIELD     #0HFLD
      *
     C                     COMIT
      *
     C                     MOVE *BLANKS   #0SELE
     C                     ENDIF
      *
     C           #0SELE    IFEQ 'S'
      *
     C                     MOVE #0REFE    KREFE
     C           KREFE     CHAINGLCHRGL0             88
      *
     C                     MOVE *BLANKS   CHJOBN
     C                     MOVE *BLANKS   CHUSER
     C                     Z-ADD*ZEROS    CHJNBR
      *
     C                     MOVE 'D'       CHRECI
     C                     Z-ADDBJRDNB    CHLCD
     C                     MOVE 'S'       CHCHTP
     C                     MOVE 'S'       CHSTAT
     C                     UPDATGLCHRGD0
      *
     C                     MOVE 'S'       #0STAT
      *
     C                     MOVE FIELD     #0HFLD
      *
     C                     COMIT
      *
     C                     MOVE *BLANKS   #0SELE
     C                     ENDIF
      *
      ** If 'ACTION' is delete
      *
     C           #0SELE    IFEQ 'D'
      *
     C                     MOVE #0REFE    KREFE
     C           KREFE     CHAINGLCHRGL0             88
      *
     C           *IN88     IFEQ *OFF
      *
     C                     MOVE *BLANKS   CHJOBN
     C                     MOVE *BLANKS   CHUSER
     C                     Z-ADD*ZEROS    CHJNBR
      *
     C                     MOVE '*'       CHRECI
     C                     Z-ADDBJRDNB    CHLCD
     C                     MOVE 'D'       CHCHTP
     C                     MOVE 'D'       CHSTAT
     C                     UPDATGLCHRGD0
      *
     C                     MOVE 'D'       #0STAT
     C                     MOVE FIELD     #0HFLD
     C                     ENDIF
      *
      ** Commitment instruction
      *
     C                     COMIT
      *
     C                     MOVE *BLANKS   #0SELE
     C                     MOVE *OFF      *IN45
     C                     ENDIF
      *
      ** If 'ACTION' is authorisation
      *
     C           #0SELE    IFEQ 'X'
      *
     C                     MOVE #0REFE    KREFE
     C           KREFE     CHAINGLCHRGL0             88
      *
     C                     MOVE *BLANKS   CHJOBN
     C                     MOVE *BLANKS   CHUSER
     C                     Z-ADD*ZEROS    CHJNBR
      *
     C                     MOVE 'D'       CHRECI
     C                     Z-ADDBJRDNB    CHLCD
     C                     MOVE 'X'       CHCHTP
     C                     MOVE 'A'       CHSTAT
     C                     UPDATGLCHRGD0
      *
     C                     MOVE 'A'       #0STAT
      *
      ** Update subfile fields
      *
     C                     MOVE FIELD     #0HFLD
      *
     C                     COMIT
      *
     C                     MOVE *BLANKS   #0SELE
     C                     ENDIF
      *
     C           TAG3      ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRMOVE : Move Subfile fields onto Screen Fields               *
      *                                                               *
      * Called by:  SRACTN                                            *
      * Calls:  None                                                  *
      *****************************************************************
      *
     C           SRMOVE    BEGSR
      *
      ** Setup the display fields
      *
     C                     MOVE #0SELE    #1SELE
      *
     C                     MOVE CHREFE    #1REFE
     C                     MOVE CHFTYP    #1FTYP
     C                     MOVE CHGROU    #1GROU
     C                     MOVE CHCNUM    #0CNUM
     C           #0CNUM    IFNE *BLANKS
     C                     MOVEL#0CNUM    PCNUM
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM           PCNUM
     C                     PARM *BLANKS   PKSTS
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE
     C                     MOVE 'SDCUSTPD'DBFILE
     C                     MOVELPCNUM     DBKEY
     C                     MOVEL'GL1908'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
     C           #0SELE    IFEQ *BLANKS
     C                     MOVE 'I'       #1SELE
     C                     ENDIF
      *
     C                     MOVE BBCSSN    #1CSSN
     C                     MOVE BBCRNM    #0CRNM
     C                     MOVE CHFTYP    #1FTYP
     C                     MOVE CHGROU    #1GROU
     C                     MOVE CHPORT    #1PORT
     C                     MOVE CHCCY     #1CCY
      *
     C           CHAMT     IFNE *ZEROS
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM #1CCY     PAJCD   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE CHAMT     ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   #1AMT
     C                     MOVE ZOUT21    WFLD17 17
     C                     MOVELWFLD17    #1AMT
     C                     ELSE
     C                     MOVE *BLANKS   #1AMT
     C                     ENDIF
      *
     C           CHBRCA    IFNE *BLANKS
     C                     MOVE CHBRCA    #1BRCA
     C                     ELSE
     C                     MOVE BBBRCD    #1BRCA
     C                     ENDIF
      *
     C                     MOVE CHSEAC    #1SEAC
      *
     C           CHSLSD    IFNE *ZEROS
     C                     Z-ADDCHSLSD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #1SLSD
     C                     ELSE
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #1SLSD
     C                     ENDIF
      *
     C                     MOVE CHAPOS    #1APOS
      *
     C           CHSEDA    IFNE *ZEROS
     C                     MOVE CHSEDA    #1SEDA
     C                     ELSE
     C                     MOVE *BLANKS   #1SEDA
     C                     ENDIF
      *
     C                     MOVE CHFREQ    #1FREQ
      *
     C           CHDAMO    IFNE *ZEROS
     C                     MOVE CHDAMO    #1DAMO
     C                     ELSE
     C                     MOVE *BLANKS   #1DAMO
     C                     ENDIF
      *
     C           CHACCR    IFNE *BLANKS
     C                     MOVE CHACCR    #1ACCR
     C                     ELSE
     C           CHFREQ    IFNE '1'
     C                     MOVE 'Y'       #1ACCR
     C                     ELSE
     C                     MOVE 'N'       #1ACCR
     C                     ENDIF
     C                     ENDIF
      *
     C           CHFNSD    IFNE *ZEROS
     C                     Z-ADDCHFNSD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #1FNSD
     C                     ELSE
     C                     MOVE *BLANKS   #1FNSD
     C                     ENDIF
      *
     C           CHFIND    IFNE *ZEROS
     C                     Z-ADDCHFIND    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #1FIND
     C                     ELSE
     C                     MOVE *BLANKS   #1FIND
     C                     ENDIF
      *
     C                     MOVE CHPFTC    #1PFTC
     C                     MOVE CHNAR1    #1NAR1
     C                     MOVE CHNAR2    #1NAR2
     C                     MOVE CHNAR3    #1NAR3
     C                     MOVE CHNAR4    #1NAR4
      *                                                                                       CSD011
      *                                                                                       CSD011
     C           CSD011    IFEQ 'Y'                                                           CSD011
      *                                                                                       CSD011
     C                     MOVE *BLANKS   #1HWMF                                              CSD011
     C                     MOVE *BLANKS   #1CFRE                                              CSD011
     C                     MOVE *BLANKS   #1CDMO                                              CSD011
     C                     MOVE *BLANKS   #1NCDA                                              CSD011
     C                     MOVE *BLANKS   ULNCDA                                              CSD011
      *                                                                                       CSD011
     C           #1SELE    IFNE 'I'                                                           CSD011
      *                                                                                       CSD011
     C                     MOVE CHHWMF    #1HWMF           High Water M Fe                    CSD011
     C                     MOVE CHCFRE    #1CFRE           Calc Frequency                     CSD011
      *                                                                                       CSD011
     C           CHCDMO    IFNE *ZEROS                                                        CSD011
     C                     MOVE CHCDMO    #1CDMO           Calc Day in Mnt                    CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C           CHNCDA    IFNE *ZEROS                                                        CSD011
     C                     Z-ADDCHNCDA    ZDAYNO                                              CSD011
     C                     EXSR ZDATE2                                                        CSD011
     C                     MOVE ZDATE     #1NCDA                                              CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C           F1PRTP    IFEQ 'PP'                                                          CSD011
     C           #1SELE    ANDEQ'I'                                                           CSD011
      *                                                                                       CSD011
      ** Next Calculation Date:  Defaulted to the Next Accrual Date                           CSD011
      *  from the fee type.                                                                   CSD011
      *                                                                                       CSD011
     C           F1ACDT    IFNE *ZEROS                                                        CSD011
     C                     Z-ADDF1ACDT    ZDAYNO                                              CSD011
     C                     EXSR ZDATE2                                                        CSD011
     C                     MOVE ZDATE     #1NCDA                                              CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
      ** Calculation Day in Month:  Defaulted to the Accrual Day in                           CSD011
      *  Month from the fee type.                                                             CSD011
      *                                                                                       CSD011
     C           F1ACDM    IFNE *ZEROS                                                        CSD011
     C                     MOVE F1ACDM    #1CDMO                                              CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
      ** Calculation Frequency:  Defaulted from fee type if it is one                         CSD011
      *  of of this value 'M', 'Q', 'X', or 'Y'.                                              CSD011
      *                                                                                       CSD011
     C           F1ACFR    IFEQ 'M'                                    cy                     CSD011
     C           F1ACFR    OREQ 'Q'                                    cy                     CSD011
     C           F1ACFR    OREQ 'X'                                    cy                     CSD011
     C           F1ACFR    OREQ 'Y'                                    cy                     CSD011
     C                     MOVE F1ACFR    #1CFRE                                              CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
      *                                                                                       CSD011
     C           CHSTAT    IFEQ 'U'
     C                     MOVE NARR,1    #1TEXT
     C                     ELSE
     C           CHSTAT    IFEQ 'D'
     C                     MOVE NARR,2    #1TEXT
     C                     ELSE
     C           CHSTAT    IFEQ 'A'
     C                     MOVE NARR,3    #1TEXT
     C                     ELSE
     C           CHSTAT    IFEQ 'G'
     C                     MOVE NARR,4    #1TEXT
     C                     ELSE
     C           CHSTAT    IFEQ 'E'
     C                     MOVE NARR,5    #1TEXT
     C                     ELSE
     C           CHSTAT    IFEQ 'O'
     C                     MOVE NARR,6    #1TEXT
     C                     ELSE
     C           CHSTAT    IFEQ 'S'
     C                     MOVE NARR,7    #1TEXT
     C                     ELSE                                                               CSD011
     C           CHSTAT    IFEQ 'C'                                                           CSD011
     C                     MOVE NARR,8    #1TEXT                                              CSD011
     C                     ENDIF                                                              CSD011
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRCONF : Process 'CONFIRMATION' screen                        *
      *                                                               *
      * Called by:  SRACTN                                            *
      * Calls:  ZDATE1  ZDATE2  SRSNMS                                *
      *****************************************************************
      *
     C           SRCONF    BEGSR
      *
     C                     MOVE 'N'       WWCONF  1
     C                     MOVE 'Y'       WFIRST  1
      *
     C                     MOVE *OFF      *IN20
     C                     MOVE *OFF      *IN25
     C                     MOVE *OFF      *IN26
      *
     C                     MOVE *ON       *IN54
      *
     C                     MOVE *OFF      *IN40
     C                     MOVE *OFF      *IN41
      *
     C                     MOVE *OFF      *IN44
     C                     MOVE *ON       *IN47
      *
     C                     MOVE 'Y'       ERROR
      *
      ** Set on Indicator to display confirmation key
      *
     C           #0SELE    IFEQ 'D'
     C                     MOVE *ON       *IN70
     C                     MOVE *OFF      *IN71
     C                     MOVE *OFF      *IN72
     C                     MOVE *OFF      *IN73
     C                     ENDIF
     C           #0SELE    IFEQ 'X'
     C                     MOVE *ON       *IN71
     C                     MOVE *OFF      *IN70
     C                     MOVE *OFF      *IN72
     C                     MOVE *OFF      *IN73
     C                     ENDIF
     C           #0SELE    IFEQ 'S'
     C                     MOVE *ON       *IN72
     C                     MOVE *OFF      *IN70
     C                     MOVE *OFF      *IN71
     C                     MOVE *OFF      *IN73
     C                     ENDIF
     C           #0SELE    IFEQ 'R'
     C                     MOVE *ON       *IN73
     C                     MOVE *OFF      *IN70
     C                     MOVE *OFF      *IN71
     C                     MOVE *OFF      *IN72
     C                     ENDIF
      *
      ** Do while function keys not pressed or error occurs
      *
     C           *INKC     DOWEQ*OFF
     C           *INKJ     ANDEQ*OFF
     C           *INKK     ANDEQ*OFF
     C           *INKL     ANDEQ*OFF
     C           *INKW     ANDEQ*OFF
     C           WWCONF    ANDEQ'N'
     C           ERROR     ANDEQ'Y'
      *
      ** Clear messages from program message queue.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     PAPGM
     C                     PARM '*SAME'   PAPGRL
      *
     C                     MOVE 'N'       ERROR
      *
     C                     MOVE *OFF      *IN20
     C                     MOVE *OFF      *IN25
     C                     MOVE *OFF      *IN26
      *
      ** Send message: "Press F10 to delete"
      *
     C           #0SELE    IFEQ 'D'
     C           WFIRST    ANDEQ'N'
     C                     MOVEL'CSF9101' PAMSID
     C                     EXSR SRSNMS
     C                     MOVE 'Y'       ERROR
     C                     ENDIF
      *
      ** Send message: "Press F11 to Authorise"
      *
     C           #0SELE    IFEQ 'X'
     C           WFIRST    ANDEQ'N'
     C                     MOVEL'CSF9112' PAMSID
     C                     EXSR SRSNMS
     C                     MOVE 'Y'       ERROR
     C                     ENDIF
      *
      ** Send message: "Press F22 to stop"
      *
     C           #0SELE    IFEQ 'S'
     C           WFIRST    ANDEQ'N'
     C                     MOVEL'CSF9853' PAMSID
     C                     EXSR SRSNMS
     C                     MOVE 'Y'       ERROR
     C                     ENDIF
      *
      ** Send message: "Press F22 to release"
      *
     C           #0SELE    IFEQ 'R'
     C           WFIRST    ANDEQ'N'
     C                     MOVEL'CSF9854' PAMSID
     C                     EXSR SRSNMS
     C                     MOVE 'Y'       ERROR
     C                     ENDIF
      *
     C                     MOVE 'N'       WFIRST
      *
      ** Execute screen format
      *
     C                     WRITE#MSGCTL
     C                     WRITEGL1908F2
     C                     EXFMTGL1908F1
      *
      ** If Action is delete and user press enter
      *
     C           #0SELE    IFEQ 'D'
     C           *INKJ     ANDEQ*OFF
     C           #0SELE    OREQ 'X'
     C           *INKK     ANDEQ*OFF
     C           #0SELE    OREQ 'S'
     C           *INKW     ANDEQ*OFF
     C           #0SELE    OREQ 'R'
     C           *INKW     ANDEQ*OFF
     C                     MOVE 'Y'       ERROR
     C                     ENDIF
      *
      ** If Action Code is 'R'
      *
     C           #1SELE    IFEQ 'R'
     C           #1SLSD    IFNE *BLANKS
     C                     MOVEL#1SLSD    WNUM7
     C                     MOVELWNUM7     WALF6
     C                     MOVE #1SLSD    ZDATE
     C                     EXSR ZDATE1
      *
      ** If it is not a valid date format
      *
     C           *IN99     IFEQ *ON
     C           #1SLSD    ORNE WALF6
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN20
     C                     MOVEL'CSF9818' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           ZDAYNO    IFLT BJRDNB
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN20
     C                     MOVEL'CSF9818' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
     C                     ELSE
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #1SLSD
     C                     ENDIF
      *
     C           #1FNSD    IFEQ *BLANKS
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN25
     C                     MOVEL'CSF9824' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C                     MOVEL#1FNSD    WNUM7
     C                     MOVELWNUM7     WALF6
     C                     MOVE #1FNSD    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    W1FNSD  50
      *
      ** If it is not a valid date format
      *
     C           *IN99     IFEQ *ON
     C           #1FNSD    ORNE WALF6
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN25
     C                     MOVEL'CSF9824' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           ZDAYNO    IFLT BJRDNB
     C           #1FREQ    ANDNE'1'
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN25
     C                     MOVEL'CSF9026' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           #1FREQ    IFEQ '1'
     C           #1FIND    ANDEQ*BLANKS
     C                     MOVE #1FNSD    #1FIND
     C                     ENDIF
      *
     C           #1FREQ    IFEQ '1'
      *
     C                     Z-ADDZDAYNO    WWFNSD  50
      *
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     MOVE #1CCY     ZCCY
     C                     MOVE *BLANKS   ZLOC
     C                     MOVE #1SEDA    ZNRDYS
      *
     C           #1CCY     IFEQ *BLANKS
     C           #1SEDA    OREQ *BLANKS
      *
     C           #1FTYP    CHAINSDCFTPL0             89
     C           *IN89     IFEQ *OFF
     C           #1CCY     ANDEQ*BLANKS
     C           F1CCCY    ANDNE*BLANKS
     C                     MOVE F1CCCY    ZCCY
     C                     ENDIF
      *
     C           *IN89     IFEQ *OFF
     C           #1SEDA    ANDEQ*BLANKS
     C           F1STGE    ANDNE*ZEROS
     C                     Z-ADDF1STGE    ZNRDYS
     C                     ENDIF
      *
     C                     MOVE #1FTYP    KWFETP
     C                     MOVE #1GROU    KWCHGR
     C           KWFEE     CHAINSDCFTPL0             89
     C           *IN89     IFEQ *OFF
     C           #1CCY     ANDEQ*BLANKS
     C           F1CCCY    ANDNE*BLANKS
     C                     MOVE F1CCCY    ZCCY
     C                     ENDIF
      *
     C           *IN89     IFEQ *OFF
     C           #1SEDA    ANDEQ*BLANKS
     C           F1STGE    ANDNE*ZEROS
     C                     Z-ADDF1STGE    ZNRDYS
     C                     ENDIF
     C                     ENDIF
      *
     C                     EXSR ZBKDT
     C           WWFNSD    IFLT ZDYNBR
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN25
     C                     MOVEL'CSF9851' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C           #1FIND    IFNE *BLANKS
     C                     MOVEL#1FIND    WNUM7
     C                     MOVELWNUM7     WALF6
     C                     MOVE #1FIND    ZDATE
     C                     EXSR ZDATE1
      *
      ** If not a valid date format
      *
     C           *IN99     IFEQ *ON
     C           #1FIND    ORNE WALF6
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN26
     C                     MOVEL'CSF9825' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           #1FREQ    IFEQ *ON
     C           #1FIND    ANDNE#1FNSD
     C           *IN25     ANDEQ*OFF
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN26
     C                     MOVEL'CSF9826' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           #1FREQ    IFNE '1'
     C           ZDAYNO    ANDLEW1FNSD
     C           *IN25     ANDEQ*OFF
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN26
     C                     MOVEL'CSF9827' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           *INKJ     IFEQ *ON
     C           *INKK     OREQ *ON
     C           *INKW     OREQ *ON
     C                     MOVE 'Y'       WWCONF
     C                     MOVE 'N'       ERROR
     C                     MOVE *OFF      *INKJ
     C                     MOVE *OFF      *INKK
     C                     MOVE *OFF      *INKW
     C                     ENDIF
      *
     C                     MOVE *OFF      *IN47
     C                     MOVE *OFF      *IN54
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRINDI : Reinitialise indicators                              *
      *                                                               *
      * Called by:  SRMNPR  SRVAL2                                    *
      * Calls:  None                                                  *
      *****************************************************************
      *
     C           SRINDI    BEGSR
      *
      ** Reinitialise indicators
      *
     C                     MOVE *OFF      *IN32
     C                     MOVE *OFF      *IN33
     C                     MOVE *OFF      *IN34
     C                     MOVE *OFF      *IN35
     C                     MOVE *OFF      *IN36
     C                     MOVE *OFF      *IN37
     C                     MOVE *OFF      *IN39
      *
     C                     MOVE *OFF      *IN62
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRCLRF : Clear subfile fields                                 *
      *                                                               *
      * Called by:  SRSFAD  SRSFAM                                    *
      * Calls:  None                                                  *
      *****************************************************************
      *
     C           SRCLRF    BEGSR
      *
      ** Initialise subfile record
      *
     C                     MOVE *BLANKS   #0SELE
     C                     MOVE *BLANKS   #0REFE
     C                     MOVE *BLANKS   #0FTYP
     C                     MOVE *BLANKS   #0GROU
     C                     MOVE *BLANKS   #0CNUM
     C                     MOVE *BLANKS   #0CRNM
     C                     MOVE *BLANKS   #0PORT
     C                     MOVE *BLANKS   #0FREQ
     C                     MOVE *BLANKS   #0FNSD
     C                     MOVE *BLANKS   #0FIND
     C                     MOVE *BLANKS   #0STAT
      *
      ** Initialise hidden subfile fields
      *
     C                     MOVE *BLANKS   #0HFLD
     C                     MOVE *BLANKS   #0HCCY
     C                     MOVE *BLANKS   #0HAMT
     C                     MOVE *BLANKS   #0HBRC
     C                     MOVE *BLANKS   #0HSEA
     C                     MOVE *BLANKS   #0HSLS
     C                     MOVE *BLANKS   #0HAPO
     C                     MOVE *BLANKS   #0HPFT
     C                     MOVE *BLANKS   #0HSED
     C                     MOVE *BLANKS   #0HDAM
     C                     MOVE *BLANKS   #0HACC
     C                     MOVE *BLANKS   #0HNA1
     C                     MOVE *BLANKS   #0HNA2
     C                     MOVE *BLANKS   #0HNA3
     C                     MOVE *BLANKS   #0HNA4
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRLOAD: Load subfile fields                                   *
      *                                                               *
      * Called by:  SRSFAM                                            *
      * Calls:  None                                                  *
      *****************************************************************
      *
     C           SRLOAD    BEGSR
      *
      ** Initialise subfile record
      *
     C                     MOVE *BLANKS   #0SELE
     C                     MOVE CHREFE    #0REFE
     C                     MOVE CHFTYP    #0FTYP
     C                     MOVE CHGROU    #0GROU
     C                     MOVE CHCNUM    #0CNUM
     C                     MOVEL#0CNUM    PCNUM
     C           #0CNUM    IFNE *BLANKS
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM           PCNUM
     C                     PARM *BLANKS   PKSTS
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE
     C                     MOVE 'SDCUSTPD'DBFILE
     C                     MOVELPCNUM     DBKEY
     C                     MOVEL'GL1908'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
     C                     MOVE BBCSSN    WCSSN
     C                     MOVE BBCSSN    #0HCSS
     C                     MOVE BBCRNM    #0CRNM
     C                     MOVE CHFTYP    #0FTYP
     C                     MOVE CHGROU    #0GROU
     C                     MOVE CHPORT    #0PORT
     C                     MOVE CHGROU    #0GROU
     C                     MOVE CHCCY     #0HCCY
      *
     C           CHAMT     IFNE *ZEROS
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM #0HCCY    PAJCD
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE CHAMT     ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   #0HAMT
     C                     MOVE ZOUT21    WFLD17 17
     C                     MOVELWFLD17    #0HAMT
     C                     ELSE
     C                     MOVE *BLANKS   #0HAMT
     C                     ENDIF
      *
     C           CHBRCA    IFNE *BLANKS
     C                     MOVE CHBRCA    #0HBRC
     C                     ELSE
     C                     MOVE BBBRCD    #0HBRC
     C                     ENDIF
      *
     C                     MOVE CHSEAC    #0HSEA
      *
     C           CHSLSD    IFNE *ZEROS
     C                     Z-ADDCHSLSD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #0HSLS
     C                     ELSE
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #0HSLS
     C                     ENDIF
      *
     C                     MOVE CHAPOS    #0HAPO
      *
     C           CHSEDA    IFNE *ZEROS
     C                     MOVE CHSEDA    #0HSED
     C                     ELSE
     C                     MOVE *BLANKS   #0HSED
     C                     ENDIF
      *
     C                     MOVE CHFREQ    #0FREQ
      *
     C           CHDAMO    IFNE *ZEROS
     C                     MOVE CHDAMO    #0HDAM
     C                     ELSE
     C                     MOVE *BLANKS   #0HDAM
     C                     ENDIF
      *
     C           CHACCR    IFNE *BLANKS
     C                     MOVE CHACCR    #0HACC
     C                     ELSE
     C           CHFREQ    IFNE '1'
     C                     MOVE 'Y'       #0HACC
     C                     ELSE
     C                     MOVE 'N'       #0HACC
     C                     ENDIF
     C                     ENDIF
      *
     C           CHFNSD    IFNE *ZEROS
     C                     Z-ADDCHFNSD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #0FNSD
     C                     ELSE
     C                     MOVE *BLANKS   #0FNSD
     C                     ENDIF
      *
     C           CHFIND    IFNE *ZEROS
     C                     Z-ADDCHFIND    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #0FIND
     C                     ELSE
     C                     MOVE *BLANKS   #0FIND
     C                     ENDIF
      *
     C                     MOVE CHPFTC    #0HPFT
     C                     MOVE CHNAR1    #0HNA1
     C                     MOVE CHNAR2    #0HNA2
     C                     MOVE CHNAR3    #0HNA3
     C                     MOVE CHNAR4    #0HNA4
     C                     MOVE CHSTAT    #0STAT
      *
      ** Initialise hidden subfile fields
      *
     C                     MOVE FIELD     #0HFLD
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRPSEL: Reinitialise POSITION/SELECTION fields                *
      *                                                               *
      * Called by:  SRSFAD  SRMNPR                                    *
      * Calls:  None                                                  *
      *****************************************************************
      *
     C           SRPSEL    BEGSR
      *
     C                     MOVE *BLANKS   #0RREF
     C                     MOVE *BLANKS   #0RFTY
     C                     MOVE *BLANKS   #0RGRO
     C                     MOVE *BLANKS   #0RCNU
     C                     MOVE *BLANKS   #0RPOR
     C                     MOVE *BLANKS   #0RFRE
     C                     MOVE *BLANKS   #0RFNS
     C                     MOVE *BLANKS   #0RFIN
     C                     MOVE *BLANKS   #0RSTA
     C                     MOVE *BLANKS   #0HRSE
     C                     MOVE *BLANKS   #0HRRE
     C                     MOVE *BLANKS   #0HRCN
     C                     MOVE *BLANKS   #0HRPO
     C                     MOVE *BLANKS   #0HRFR
     C                     MOVE *BLANKS   #0HRFN
     C                     MOVE *BLANKS   #0HRFI
     C                     MOVE *BLANKS   #0HRST
      *
     C                     MOVE *OFF      *IN50
     C                     MOVE *OFF      *IN51
     C                     MOVE *OFF      *IN52
     C                     MOVE *OFF      *IN53
     C                     MOVE *OFF      *IN69
     C                     MOVE *OFF      *IN86
     C                     MOVE *OFF      *IN76
     C                     MOVE *OFF      *IN77
     C                     MOVE *OFF      *IN78
     C                     MOVE *OFF      *IN82
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRDTLS: Process 'FULL DETAILS' screen                         *
      *                                                               *
      * Called by:  SRVAL2  SRACTN                                    *
      * Calls:  SRVDTL                                                *
      *****************************************************************
      *
     C           SRDTLS    BEGSR
      *
     C                     EXSR SRMOVE
      *
     C           #0SELE    IFEQ 'E'
     C                     MOVE *ON       *IN18
     C                     ELSE
     C                     MOVE *OFF      *IN18
     C                     ENDIF
      *
     C                     MOVE *ON       *IN47
      *
     C                     MOVE *ON       *IN66
     C                     MOVE *OFF      *IN67
     C                     MOVE *OFF      *IN68
      *
     C                     MOVE #1GROU    WSGROU  2
     C                     MOVE *BLANKS   W1GROU
     C**********           MOVE *BLANKS   W1SEAC 18                                           CGL029
     C                     MOVE *BLANKS   W1SEAC 24                                           CGL029
     C                     MOVE 'Y'       ERROR   1
     C                     MOVE 'Y'       WARNI1  1
     C                     MOVE 'Y'       WARNI2  1
     C                     MOVE 'N'       TSTVAL  1
     C                     MOVE *ON       *IN84
      *
     C           #0SELE    IFEQ 'A'
     C                     MOVE *ON       *IN29
     C                     ELSE
     C                     MOVE *OFF      *IN29
     C                     ENDIF
      *
     C           CHSLSD    IFLT BJRDNB
     C           CHSLSD    ANDNE*ZEROS
     C                     MOVE *ON       *IN42
     C                     ELSE
     C                     MOVE *OFF      *IN42
     C                     ENDIF
      *
      ** Do while F12 or F3 is not pressed and no error
      *
     C           *INKC     DOWEQ*OFF
     C           *INKL     ANDEQ*OFF
     C           ERROR     ANDEQ'Y'
      *
      ** Execute screen format
      *
     C                     MOVE *OFF      *IN70
     C                     MOVE *OFF      *IN71
     C                     MOVE *OFF      *IN72
     C                     MOVE *OFF      *IN73
     C                     WRITE#MSGCTL
     C                     WRITEGL1908F2
     C                     EXFMTGL1908F1
      *
      ** Clear messages from program message queue.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     PAPGM
     C                     PARM '*SAME'   PAPGRL
      *
     C                     MOVE 'N'       ERROR
     C                     MOVE 'N'       TSTVAL
      *
      ** Validate details and update file if no error
      *
     C           #1SELE    IFEQ 'A'
     C           #1SELE    OREQ 'I'
      *
     C           *INKC     IFEQ *OFF
     C           *INKL     ANDEQ*OFF
     C                     EXSR SRVDTL
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
      ** Setup correctly the command keys available
      *
      *
     C           CHRREF    IFNE *BLANKS
     C                     MOVE *ON       *IN66
     C                     MOVE *OFF      *IN67
     C                     MOVE *OFF      *IN68
     C                     ELSE
     C                     MOVE *ON       *IN68
     C                     MOVE *OFF      *IN66
     C                     MOVE *OFF      *IN67
     C                     ENDIF
      *
     C                     MOVE *OFF      *IN47
      *
      ** Clear messages from program message queue.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     PAPGM
     C                     PARM '*SAME'   PAPGRL
      *
     C                     ENDSR
      ****************************************************************
      /EJECT
      *****************************************************************
      * SRVDTL: Validate 'FULL DETAILS' screen and update file        *
      *                                                               *
      * Called by:  SRVAL2  SRACTN                                    *
      * Calls:  SRACTN  SRDTLS                                        *
      *****************************************************************
      *
     C           SRVDTL    BEGSR
      *
      ** Initialise error indicators
      *
     C                     MOVE *OFF      *IN15
     C                     MOVE *OFF      *IN16
     C                     MOVE *OFF      *IN17
      *
     C                     MOVE *OFF      *IN11
     C                     MOVE *OFF      *IN19
     C                     MOVE *OFF      *IN20
     C                     MOVE *OFF      *IN21
     C                     MOVE *OFF      *IN22
     C                     MOVE *OFF      *IN23
     C                     MOVE *OFF      *IN24
     C                     MOVE *OFF      *IN25
     C                     MOVE *OFF      *IN26
     C                     MOVE *OFF      *IN28
      *
     C                     MOVE *OFF      *IN31
     C                     MOVE *OFF      *IN38
     C                     MOVE *OFF      *IN60            High Water M F                     CSD011
     C                     MOVE *OFF      *IN48            Calculation Frq                    CSD011
     C                     MOVE *OFF      *IN59            Calc Day in Mnt                    CSD011
     C                     MOVE *OFF      *IN79            Next Calc Date                     CSD011
      *
     C           #1GROU    IFNE W1GROU
     C           #1GROU    ANDNE*BLANKS
     C                     MOVE 'N'       WARNI2  1
     C                     MOVE #1GROU    W1GROU  2
     C                     ELSE
     C                     MOVE 'Y'       WARNI2
     C                     ENDIF
      *
     C           WARNI2    IFEQ 'N'
     C           WARNI2    OREQ ' '
      *
     C********** #1SELE    IFNE 'I'                                                           202033
     C********** #1SELE    OREQ 'I'                                                           202033
     C           #1SELE    IFNE ' '                                                           202033
     C           #1GROU    ANDNEWSGROU
     C           WARN      IFNE 'Y'
     C                     MOVE 'Y'       ERROR
     C                     ENDIF
     C                     MOVE *ON       *IN28
     C                     MOVEL'CSF9812' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           #1CCY     IFNE *BLANKS
     C           #1AMT     ANDEQ*BLANKS
     C           #1CCY     OREQ *BLANKS
     C           #1AMT     ANDNE*BLANKS
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN15
     C                     MOVE *ON       *IN16
     C                     MOVEL'CSF9813' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
      *
     C           #1CCY     IFNE *BLANKS
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM #1CCY     PAJCD
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN15
     C                     MOVEL'CSF9038' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C                     MOVE A6CYCD    #1CCY
     C                     ENDIF
     C                     ENDIF
      *
     C           #1FTYP    CHAINSDCFTPL0             89
      *                                                                                       192177
      ** The charging currency must be the same as on the fee type                            192177
      *                                                                                       192177
     C           #1CCY     IFNE F1CCCY                                                        192177
     C           #1CCY     ANDNE*BLANKS                                                       192177
     C                     MOVE 'Y'       ERROR                                               192177
     C                     MOVE '1'       *IN15            Reverse image                      192177
     C                     MOVEL'CSF9860' PAMSID                                              192177
     C                     EXSR SRSNMS                                                        192177
     C                     ENDIF                                                              192177
      *                                                                                       192177
      ** For Hold Mail the charging currency and amount should default                        192177
      ** to the Fee Type charge currency and the rate code amount.                            192177
      *                                                                                       192177
     C           #1CCY     IFEQ *BLANKS                                                       192177
     C           #1AMT     ANDEQ*BLANKS                                                       192177
     C           F1PRTP    ANDEQ'HM'                                                          192177
     C                     MOVE F1CCCY    #1CCY            Fee Type CCY                       192177
     C           KCGFR     KLIST                                                              192177
     C                     KFLD           #1FTYP                                              192177
     C                     KFLD           #1GROU                                              192177
     C           KCGFR     CHAINSDCGFRL0             83                                       192177
      *                                                                                       192177
     C           *IN83     IFEQ *ON                                                           192177
     C           F4RECI    OREQ '*'                                                           192177
     C                     MOVE 'Y'       ERROR                                               192177
     C                     MOVE '1'       *IN28            Reverse image                      192177
     C                     MOVEL'CSF9861' PAMSID                                              192177
     C                     EXSR SRSNMS                                                        192177
      *                                                                                       192177
     C                     ELSE                            *IN83 OFF                          192177
     C           KRTCD     KLIST                                                              192177
     C                     KFLD           F4RTCD                                              192177
     C                     KFLD           #1CCY                                               192177
     C           KRTCD     CHAINSDRTCDL0             85                                       192177
      *                                                                                       192177
     C           *IN85     IFEQ *ON                                                           192177
     C           F3RECI    OREQ '*'                                                           192177
     C                     MOVE 'Y'       ERROR                                               192177
     C                     MOVE '1'       *IN28            Reverse image                      192177
     C                     MOVEL'CSF9862' PAMSID                                              192177
     C                     EXSR SRSNMS                                                        192177
     C                     ELSE                            *IN85 OFF                          192177
      *                                                                                       192177
      ** For Hold Mail the Rate Code must have a single Flat Amount.                          192177
      *                                                                                       192177
     C           F3FL01    IFEQ *ZERO                                                         192177
     C           F3FL02    ORNE *ZERO                                                         192177
     C                     MOVE 'Y'       ERROR                                               192177
     C                     MOVE '1'       *IN28            Reverse image                      192177
     C                     MOVEL'CSF9043' PAMSID                                              192177
     C                     EXSR SRSNMS                                                        192177
      *                                                                                       192177
     C                     ELSE                            F3FL01=Zero                        192177
     C                     MOVE *BLANKS   ZFLD15                                              192177
     C                     MOVE F3FL01    ZFLD15                                              192177
     C                     Z-ADDA6NBDP    ZDECS                                               192177
     C                     MOVE 'L'       ZECODE                                              192177
     C                     EXSR ZSEDIT                                                        192177
     C                     MOVEL*BLANKS   #1AMT                                               192177
     C                     MOVE ZOUT21    WFLD17                                              192177
     C                     MOVELWFLD17    #1AMT                                               192177
      *                                                                                       192177
     C                     ENDIF                           F3FL01=Zero                        192177
      *                                                                                       192177
     C                     ENDIF                           *IN85 *ON                          192177
      *                                                                                       192177
     C                     ENDIF                           *IN83 *ON                          192177
      *                                                                                       192177
     C                     ENDIF                           #1CCY *BLANK                       192177
      *
     C           F1PRTP    IFNE 'FI'
     C           F1PRTP    ANDNE'HM'
     C           F1PRTP    ANDNE'NA'
     C           #1AMT     ANDNE*BLANKS
     C                     MOVE 'Y'       ERROR
      *
     C           #1CCY     IFNE *BLANKS
     C                     MOVE *ON       *IN15
     C                     ENDIF
      *
     C                     MOVE *ON       *IN16
     C                     MOVEL'CSF9814' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
      *
     C           #1AMT     IFNE *BLANKS
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE #1AMT     ZFIELD    P
     C                     Z-ADDA6NBDP    ZADEC
     C           15        SUB  A6NBDP    ZADIG
     C                     MOVE *OFF      *IN99
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ *ON
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN16
     C                     MOVEL'CSF9815' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
     C           #1BRCA    IFEQ *BLANKS
     C                     MOVE BBBRCD    #1BRCA
     C                     ELSE
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM #1BRCA    PBRCH   3
     C           SDBRCH    PARM SDBRCH    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN17
     C                     MOVEL'CSF9038' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C                     MOVE A8BRCD    #1BRCA
     C                     ENDIF
     C                     ENDIF
      *
      *                                                                                       CSD011
     C           CSD011    IFEQ 'Y'                                                           CSD011
      *                                                                                       CSD011
      ** Validate High Water Mark Fee.                                                        CSD011
      *                                                                                       CSD011
     C           F1PRTP    IFNE 'PP'                                                          CSD011
      *                                                                                       CSD011
     C           #1HWMF    IFNE *BLANKS                                                       CSD011
     C                     MOVE 'Y'       ERROR                                               CSD011
     C                     MOVE *ON       *IN60                                               CSD011
     C                     MOVEL'CSF8906' PAMSID                                              CSD011
     C                     EXSR SRSNMS                                                        CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ELSE                                                               CSD011
      *                                                                                       CSD011
     C           #1HWMF    IFNE 'Y'                                                           CSD011
     C           #1HWMF    ANDNE'N'                                                           CSD011
     C                     MOVE 'Y'       ERROR                                               CSD011
     C                     MOVE *ON       *IN60                                               CSD011
     C                     MOVEL'CSF8907' PAMSID                                              CSD011
     C                     EXSR SRSNMS                                                        CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
      *                                                                                       CSD011
     C           #1SEAC    IFNE *BLANKS
     C                     MOVELPDATA     W1DATA
      *
      ** If the settlement account is not retail
      *
     C           WWNRET    IFNE *BLANKS
      *
     C                     CALL 'AOACCTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY'    POPTN
     C                     PARM *BLANKS   PKRET
     C                     PARM           PSTCN   6
     C                     PARM           PSTCU   3
     C**********           PARM           PSTAC   4                                           CGL029
     C                     PARM           PSTAC  10                                           CGL029
     C                     PARM           PSTSQ   2
     C                     PARM           PSTBR   3
     C           ACCNT     PARM ACCNT     DSSDY
     C                     ELSE
      *
      ** The settlement account is retail
      *
     C                     MOVE WWRETA    CHK100 100
     C                     MOVE CHK100    CHK10A 10
     C           WWRETA    IFEQ CHK10A
     C                     CALL 'AOACCTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY'    POPTN
     C                     PARM WWRETA    PKRET
     C                     PARM *BLANKS   PSTCN
     C                     PARM *BLANKS   PSTCU
     C                     PARM *BLANKS   PSTAC
     C                     PARM *BLANKS   PSTSQ
     C                     PARM *BLANKS   PSTBR
     C           ACCNT     PARM ACCNT     DSSDY
     C                     ELSE
     C                     MOVEL'*ERROR'  PRTCD
     C                     ENDIF
     C                     ENDIF
      *
     C           PRTCD     IFEQ *BLANKS
     C                     MOVELBRCA      PSTBR
     C                     MOVELCNUM      PSTCN
     C                     MOVELCCY       PSTCU
     C                     MOVELACOD      PSTAC
     C                     MOVELACSQ      PSTSQ
     C                     ELSE
     C                     MOVELW1DATA    PDATA
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN19
     C                     MOVEL'CSF9816' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
      *
     C           #1SEAC    IFNE W1SEAC
     C           #1SEAC    ANDNE*BLANKS
     C                     MOVE 'N'       WARNI1  1
     C**********           MOVE #1SEAC    W1SEAC 18                                           CGL029
     C                     MOVE #1SEAC    W1SEAC 24                                           CGL029
     C                     ELSE
     C                     MOVE 'Y'       WARNI1
     C                     ENDIF
      *
     C           PTFR      IFNE #1PORT
     C           #1PORT    ANDNE*BLANKS
     C           *IN19     ANDEQ*OFF
      *
     C           WARNI1    IFEQ 'N'
     C           WARNI1    OREQ ' '
     C           WARN      IFNE 'Y'
     C                     MOVE 'Y'       ERROR
     C                     ENDIF
     C                     MOVE *ON       *IN19
     C                     MOVEL'CSF9839' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
      *
     C                     MOVE 'Y'       WARNI1
     C                     ENDIF
      *
     C                     ELSE
      *
     C           #0CNUM    CHAINSDCFCDL1             89
     C           *IN89     IFEQ *OFF
     C           E6SMAC    ANDNE*BLANKS
      *
     C                     MOVE E6SMAC    WWSMAC
     C                     MOVE WWSCNM    E6SCNM
     C                     MOVE WWSACD    E6SACD
     C                     MOVE WWSASQ    E6SASQ
      *
     C           E6SASQ    IFNE *BLANKS
     C                     MOVELE6DATA    CHSEAC    P
     C                     ELSE
     C                     CALL 'AOACCTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM WWRETN    PKRET
     C                     PARM *BLANKS   PCNUM
     C                     PARM *BLANKS   PCCY
     C                     PARM *BLANKS   PACOD
     C                     PARM *BLANKS   PACSQ
     C                     PARM *BLANKS   PBRCA
     C           ACCNT     PARM ACCNT     DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD7         DBASE
     C                     MOVEL'SDACCTPD'DBFILE
     C                     MOVELPOPTN     DBKEY
     C                     MOVEL'GL1908'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVELBRCA      PSTBR
     C                     MOVELCNUM      PSTCN
     C                     MOVELCCY       PSTCU
     C                     MOVELACOD      PSTAC
     C                     MOVELACSQ      PSTSQ
     C                     MOVELPDATA     CHSEAC
     C                     ENDIF
     C                     ENDIF
     C                     MOVELCHSEAC    #1SEAC
      *
     C                     ELSE
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN19
     C                     MOVEL'CSF9836' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
      * Fee Start/Last Settlement date must be entered if Fee Type is
      * 'PP'.                                                                                 CSD011
     C           CSD011    IFEQ 'Y'                                                           CSD011
     C           #1SLSD    ANDEQ*BLANKS                                                       CSD011
     C           F1PRTP    ANDEQ'PP'                                                          CSD011
     C                     MOVE 'Y'       ERROR                                               CSD011
     C                     MOVE *ON       *IN20                                               CSD011
     C                     MOVEL'CSF8905' PAMSID                                              CSD011
     C                     EXSR SRSNMS                                                        CSD011
     C                     ELSE                                                               CSD011
      *                                                                                       CSD011
     C           #1SLSD    IFNE *BLANKS
     C                     MOVEL#1SLSD    WNUM7
     C                     MOVELWNUM7     WALF6
     C                     MOVE #1SLSD    ZDATE
     C                     EXSR ZDATE1
      *
      ** If not a valid date format
      *
     C           *IN99     IFEQ *ON
     C           #1SLSD    ORNE WALF6
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN20
     C                     MOVEL'CSF9818' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           ZDAYNO    IFLT BJRDNB
     C           CHSLSD    ANDGEBJRDNB
     C           #1SELE    ANDNE'R'
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN20
     C                     MOVEL'CSF9818' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
     C                     ELSE
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #1SLSD
     C                     ENDIF
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *
     C                     Z-ADDZDAYNO    W1SLSD  50
      *
     C           #1APOS    IFNE *BLANKS
      *
     C           #1APOS    IFNE 'Y'
     C           #1APOS    ANDNE'N'
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN21
     C                     MOVEL'CSF9016' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
     C           #1FREQ    IFNE *BLANKS
      *                                                                                       CSD011
      * Entry not allowed if Fee Type is PP.                                                  CSD011
     C           CSD011    IFEQ 'Y'                                                           CSD011
     C           F1PRTP    ANDEQ'PP'                                                          CSD011
     C                     MOVE 'Y'       ERROR                                               CSD011
     C                     MOVE *ON       *IN22                                               CSD011
     C                     MOVEL'CSF8900' PAMSID                                              CSD011
     C                     EXSR SRSNMS                                                        CSD011
     C                     ELSE                                                               CSD011
      *
     C           #1FREQ    IFNE 'M'
     C           #1FREQ    ANDNE'Q'
     C           #1FREQ    ANDNE'X'
     C           #1FREQ    ANDNE'Y'
     C           #1FREQ    ANDNE'1'
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN22
     C                     MOVEL'CSF9819' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF                                                              CSD011
      *
     C                     ENDIF
      *
      *
      **  Validation of Profit Centre
      *
     C           *IN10     IFEQ *ON
      *
      **  Validate Entry for Profit Centre
      **  Call Access Object for Profit Centre
      *
     C           #1PFTC    IFNE *BLANKS
     C           #1PFTC    OREQ '?'
     C                     CALL 'AOPRFCR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*VERIFY' POPTN
     C                     PARM #1PFTC    PPTFC   4
     C           SDPRFC    PARM SDPRFC    DSSDY
      *
     C           PPTFC     IFNE *BLANKS
     C                     MOVE PPTFC     #1PFTC
     C                     ENDIF
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVEL'CSF9136' PAMSID
     C                     MOVE *ON       *IN11
     C                     MOVE 'Y'       ERROR
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
      ** Get Default Value if #1PFTC is equal to blank
      *
     C           #1PFTC    IFEQ *BLANKS
      *
     C                     CALL 'AOPRFMR0'
     C                     PARM *BLANKS   P1RTCD  1
     C                     PARM *BLANKS   PBOOK   2
     C                     PARM #1FTYP    PTRTP   5
     C                     PARM *BLANKS   PSUTP   2
     C                     PARM #1BRCA    PBRCA
     C                     PARM *BLANKS   PDEPT   3
     C                     PARM *BLANK    PUSER  10
     C                     PARM BBACOC    PACOF   2
     C                     PARM CHCNUM    PCUST   6
     C                     PARM *BLANKS   PPFTC   4
      *
     C                     MOVE PPFTC     #1PFTC
      *
     C           P1RTCD    IFEQ '2'
     C                     MOVEL'CSF9136' PAMSID
     C                     MOVE *ON       *IN11
     C                     MOVE 'Y'       ERROR
     C                     EXSR SRSNMS
     C                     ENDIF
     C           #1PFTC    IFEQ *BLANKS
     C                     MOVEL'CSF9144' PAMSID
     C                     MOVE *ON       *IN11
     C                     MOVE 'Y'       ERROR
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           #1SEDA    IFNE *BLANKS
      *                                                                                       CSD011
      * Entry not allowed if Fee Type is PP.                                                  CSD011
     C           CSD011    IFEQ 'Y'                                                           CSD011
     C           F1PRTP    ANDEQ'PP'                                                          CSD011
     C                     MOVE 'Y'       ERROR                                               CSD011
     C                     MOVE *ON       *IN23                                               CSD011
     C                     MOVEL'CSF8901' PAMSID                                              CSD011
     C                     EXSR SRSNMS                                                        CSD011
     C                     ELSE                                                               CSD011
      *                                                                                       CSD011
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE #1SEDA    ZFIELD    P
     C                     Z-ADD0         ZADEC
     C                     Z-ADD2         ZADIG
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ *ON
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN23
     C                     MOVEL'CSF9024' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           #1SEDA    IFLT '01'
     C           #1SEDA    ORGT '14'
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN23
     C                     MOVEL'CSF9024' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF                                                              CSD011
      *
     C                     ENDIF
      *
     C           #1DAMO    IFNE *BLANKS
      *                                                                                       CSD011
      * Entry not allowed if Fee Type is PP.                                                  CSD011
     C           CSD011    IFEQ 'Y'                                                           CSD011
     C           F1PRTP    ANDEQ'PP'                                                          CSD011
     C                     MOVE 'Y'       ERROR                                               CSD011
     C                     MOVE *ON       *IN24                                               CSD011
     C                     MOVEL'CSF8902' PAMSID                                              CSD011
     C                     EXSR SRSNMS                                                        CSD011
     C                     ELSE                                                               CSD011
      *                                                                                       CSD011
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE #1DAMO    ZFIELD    P
     C                     Z-ADD0         ZADEC
     C                     Z-ADD2         ZADIG
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ *ON
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN24
     C                     MOVEL'CSF9030' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           #1DAMO    IFLT '01'
     C           #1DAMO    ORGT '31'
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN24
     C                     MOVEL'CSF9030' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF                                                              CSD011
      *
     C                     ENDIF
      *
      *                                                                                       CSD011
     C           CSD011    IFEQ 'Y'                                                           CSD011
      *                                                                                       CSD011
      ** Validate Calculation Frequency                                                       CSD011
      *                                                                                       CSD011
      *  If Fee Processing Type is 'PP' and calc freq is blank then                           CSD011
      *  default it from Accrual Frequency set up for Fee type. But                           CSD011
      *  only do this if the Fee Type value is M,Q,X or Y.                                    CSD011
      *                                                                                       CSD011
     C           F1PRTP    IFEQ 'PP'                                                          CSD011
     C           #1CFRE    ANDEQ*BLANKS                                                       CSD011
      *                                                                                       CSD011
     C           F1ACFR    IFEQ 'M'                                                           CSD011
     C           F1ACFR    OREQ 'Q'                                                           CSD011
     C           F1ACFR    OREQ 'X'                                                           CSD011
     C           F1ACFR    OREQ 'Y'                                                           CSD011
     C                     MOVE F1ACFR    #1CFRE                                              CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
      ** Mandatory for fee with processing type 'PP', forbidden for                           CSD011
      *  other type processing.                                                               CSD011
      *                                                                                       CSD011
     C           F1PRTP    IFNE 'PP'                                                          CSD011
      *                                                                                       CSD011
     C           #1CFRE    IFNE *BLANKS                                                       CSD011
     C                     MOVE 'Y'       ERROR                                               CSD011
     C                     MOVE *ON       *IN48                                               CSD011
     C                     MOVEL'CSF8908' PAMSID                                              CSD011
     C                     EXSR SRSNMS                                                        CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ELSE                                                               CSD011
      *                                                                                       CSD011
     C           #1CFRE    IFNE 'M'                                                           CSD011
     C           #1CFRE    ANDNE'Q'                                                           CSD011
     C           #1CFRE    ANDNE'X'                                                           CSD011
     C           #1CFRE    ANDNE'Y'                                                           CSD011
     C                     MOVE 'Y'       ERROR                                               CSD011
     C                     MOVE *ON       *IN48                                               CSD011
     C                     MOVEL'CSF8909' PAMSID                                              CSD011
     C                     EXSR SRSNMS                                                        CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
      *                                                                                       CSD011
      ** Validate the First/Next Settlement Date.                                             CSD011
      *                                                                                       CSD011
      ** Entry not allowed if Fee Type is PP.                                                 CSD011
      *                                                                                       CSD011
     C           CSD011    IFEQ 'Y'                                                           CSD011
     C           F1PRTP    ANDEQ'PP'                                                          CSD011
      *                                                                                       CSD011
     C           #1FNSD    IFNE *BLANKS                                                       CSD011
     C                     MOVE 'Y'       ERROR                                               CSD011
     C                     MOVE *ON       *IN25                                               CSD011
     C                     MOVEL'CSF8903' PAMSID                                              CSD011
     C                     EXSR SRSNMS                                                        CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ELSE                                                               CSD011
      *                                                                                       CSD011
     C           #1FNSD    IFEQ *BLANKS
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN25
     C                     MOVEL'CSF9824' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C                     MOVEL#1FNSD    WNUM7
     C                     MOVELWNUM7     WALF6
     C                     MOVE #1FNSD    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    W1FNSD  50
      *
      ** If not a valid date format
      *
     C           *IN99     IFEQ *ON
     C           #1FNSD    ORNE WALF6
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN25
     C                     MOVEL'CSF9824' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           ZDAYNO    IFLT BJRDNB
     C           #1FREQ    ANDNE'1'
     C           #1SELE    ANDNE'R'
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN25
     C                     MOVEL'CSF9026' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           #1FREQ    IFEQ *ON
     C           #1FIND    ANDEQ*BLANKS
     C                     MOVE #1FNSD    #1FIND
     C                     ENDIF
      *
     C           *IN20     IFEQ *OFF
     C           *IN25     ANDEQ*OFF
     C           W1FNSD    IFLE W1SLSD
     C           #1FREQ    ANDNE'1'
     C           W1FNSD    ORLT W1SLSD
     C           #1FREQ    ANDEQ'1'
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN25
     C                     MOVEL'CSF9852' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
      *
     C           #1FREQ    IFEQ '1'
      *
     C                     Z-ADDZDAYNO    WWFNSD  50
      *
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     MOVE #1CCY     ZCCY
     C                     MOVE *BLANKS   ZLOC
     C                     MOVE #1SEDA    ZNRDYS
      *
     C           #1CCY     IFEQ *BLANKS
     C           #1SEDA    OREQ *BLANKS
      *
     C           #1FTYP    CHAINSDCFTPL0             89
     C           *IN89     IFEQ *OFF
     C           #1CCY     ANDEQ*BLANKS
     C           F1CCCY    ANDNE*BLANKS
     C                     MOVE F1CCCY    ZCCY
     C                     ENDIF
      *
     C           *IN89     IFEQ *OFF
     C           #1SEDA    ANDEQ*BLANKS
     C           F1STGE    ANDNE*ZEROS
     C                     Z-ADDF1STGE    ZNRDYS
     C                     ENDIF
      *
     C                     MOVE #1FTYP    KWFETP
     C                     MOVE #1GROU    KWCHGR
     C           KWFEE     CHAINSDCFTPL0             89
     C           *IN89     IFEQ *OFF
     C           #1CCY     ANDEQ*BLANKS
     C           F1CCCY    ANDNE*BLANKS
     C                     MOVE F1CCCY    ZCCY
     C                     ENDIF
      *
     C           *IN89     IFEQ *OFF
     C           #1SEDA    ANDEQ*BLANKS
     C           F1STGE    ANDNE*ZEROS
     C                     Z-ADDF1STGE    ZNRDYS
     C                     ENDIF
     C                     ENDIF
      *
     C                     EXSR ZBKDT
     C           WWFNSD    IFLT ZDYNBR
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN25
     C                     MOVEL'CSF9851' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *
      *                                                                                       CSD011
     C           CSD011    IFEQ 'Y'                                                           CSD011
      *                                                                                       CSD011
      ** Validate Calculation Day in Month                                                    CSD011
      *                                                                                       CSD011
      ** If Fee Processing Type is 'PP' and calc day in month is blank                        CSD011
      *  then default it from Accrual day in month set up for Fee type.                       CSD011
      *                                                                                       CSD011
     C           F1PRTP    IFEQ 'PP'                                                          CSD011
     C           #1CDMO    ANDEQ*BLANKS                                                       CSD011
      *                                                                                       CSD011
     C           F1ACDM    IFNE *ZEROS                                                        CSD011
     C                     MOVE F1ACDM    #1CDMO                                              CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
      ** Mandatory for fee with processing type 'PP', forbidden for                           CSD011
      *  other type processing.                                                               CSD011
      *                                                                                       CSD011
     C           F1PRTP    IFNE 'PP'                                                          CSD011
      *                                                                                       CSD011
     C           #1CDMO    IFNE *BLANKS                                                       CSD011
     C                     MOVE 'Y'       ERROR                                               CSD011
     C                     MOVE *ON       *IN59                                               CSD011
     C                     MOVEL'CSF8912' PAMSID                                              CSD011
     C                     EXSR SRSNMS                                                        CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ELSE                                                               CSD011
      *                                                                                       CSD011
     C                     MOVE *BLANKS   ZFIELD                                              CSD011
     C                     MOVE #1CDMO    ZFIELD                                              CSD011
     C                     Z-ADD0         ZADEC                                               CSD011
     C                     Z-ADD2         ZADIG                                               CSD011
     C                     EXSR ZALIGN                                                        CSD011
      *                                                                                       CSD011
     C           *IN99     IFEQ *OFF                                                          CSD011
     C                     MOVE #1CDMO    ULCDMO  20                                          CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C           *IN99     IFEQ *ON                                                           CSD011
     C           ULCDMO    OREQ *ZEROS                                                        CSD011
     C           ULCDMO    ORGT 31                                                            CSD011
     C                     MOVE 'Y'       ERROR                                               CSD011
     C                     MOVE *ON       *IN59                                               CSD011
     C                     MOVEL'CSF8913' PAMSID                                              CSD011
     C                     EXSR SRSNMS                                                        CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C           #1FIND    IFNE *BLANKS
      *                                                                                       CSD011
      * Entry not allowed if Fee Type is PP.                                                  CSD011
     C           CSD011    IFEQ 'Y'                                                           CSD011
     C           F1PRTP    ANDEQ'PP'                                                          CSD011
     C                     MOVE 'Y'       ERROR                                               CSD011
     C                     MOVE *ON       *IN26                                               CSD011
     C                     MOVEL'CSF8904' PAMSID                                              CSD011
     C                     EXSR SRSNMS                                                        CSD011
     C                     ELSE                                                               CSD011
      *                                                                                       CSD011
     C                     MOVEL#1FIND    WNUM7
     C                     MOVELWNUM7     WALF6
     C                     MOVE #1FIND    ZDATE
     C                     EXSR ZDATE1
      *
      ** If not a valid date format
      *
     C           *IN99     IFEQ *ON
     C           #1FIND    ORNE WALF6
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN26
     C                     MOVEL'CSF9825' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           #1FREQ    IFEQ '1'
     C           #1FIND    ANDNE#1FNSD
     C           *IN25     ANDEQ*OFF
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN26
     C                     MOVEL'CSF9826' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           #1FREQ    IFNE '1'
     C           ZDAYNO    ANDLEW1FNSD
     C           *IN25     ANDEQ*OFF
     C           #1SELE    ANDNE'R'
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN26
     C                     MOVEL'CSF9827' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF
      *
      *                                                                                       CSD011
     C           CSD011    IFEQ 'Y'                                                           CSD011
      *                                                                                       CSD011
      ** Validate Next Calculation Date                                                       CSD011
      *                                                                                       CSD011
      ** If Fee Processing Type is 'PP' and next calc date is blank then                      CSD011
      *  default it from Next Accrual Date set up for Fee type, but                           CSD011
      *  only if the value is not 0.                                                          CSD011
      *                                                                                       CSD011
     C           F1PRTP    IFEQ 'PP'                                                          CSD011
     C           #1NCDA    ANDEQ*BLANKS                                                       CSD011
     C           F1ACDT    ANDNE*ZEROS                                                        CSD011
     C                     Z-ADDF1ACDT    ZDAYNO                                              CSD011
     C                     EXSR ZDATE2                                                        CSD011
     C                     MOVE ZDATE     #1NCDA                                              CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
      ** Mandatory for fee with processing type 'PP', forbidden for                           CSD011
      *  other type processing.                                                               CSD011
      *                                                                                       CSD011
     C           F1PRTP    IFNE 'PP'                                                          CSD011
      *                                                                                       CSD011
     C           #1NCDA    IFNE *BLANKS                                                       CSD011
     C                     MOVE 'Y'       ERROR                                               CSD011
     C                     MOVE *ON       *IN79                                               CSD011
     C                     MOVEL'CSF8910' PAMSID                                              CSD011
     C                     EXSR SRSNMS                                                        CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ELSE                                                               CSD011
      *                                                                                       CSD011
      * Validate entered date                                                                 CSD011
      *                                                                                       CSD011
     C                     MOVE *BLANKS   ULNCDA  50                                          CSD011
     C                     MOVE #1NCDA    ZDATE                                               CSD011
     C                     EXSR ZDATE1                                                        CSD011
      *                                                                                       CSD011
     C           #1NCDA    IFEQ *BLANKS                                                       CSD011
     C           *IN99     OREQ *ON                                                           CSD011
     C                     MOVE 'Y'       ERROR                                               CSD011
     C                     MOVE *ON       *IN79                                               CSD011
     C                     MOVEL'CSF8911' PAMSID                                              CSD011
     C                     EXSR SRSNMS                                                        CSD011
     C                     ELSE                                                               CSD011
     C                     MOVE ZDAYNO    ULNCDA                                              CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
      *                                                                                       CSD011
     C           #1ACCR    IFNE *BLANKS
      *
     C           #1ACCR    IFNE 'Y'
     C           #1ACCR    ANDNE'N'
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN31
     C                     MOVEL'CSF9821' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           #1ACCR    IFEQ 'N'
     C           F1PRTP    ANDNE'FI'
     C           F1PRTP    ANDNE'HM'
     C           F1PRTP    ANDNE'NA'
     C           #1FREQ    ANDNE'1'
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN31
     C                     MOVEL'CSF9822' PAMSID
     C                     EXSR SRSNMS
     C                     ELSE
     C           #1ACCR    IFNE 'N'
     C           #1FREQ    ANDEQ'1'
     C                     MOVE 'Y'       ERROR
     C                     MOVE *ON       *IN31
     C                     MOVEL'CSF9823' PAMSID
     C                     EXSR SRSNMS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ELSE
      *
     C           #1FREQ    IFNE '1'
     C                     MOVE 'Y'       #1ACCR
     C                     ELSE
     C                     MOVE 'N'       #1ACCR
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** If no error detected
      *
     C           ERROR     IFEQ 'N'
     C           TSTVAL    ANDNE'Y'
      *
      ** Access to charge reference file with current reference
      *
     C                     MOVE #1REFE    KREFE
     C           KREFE     CHAINGLCHRGD0             89
      *
     C                     MOVE 'D'       CHRECI
     C                     Z-ADDBJRDNB    CHLCD
     C                     MOVE 'A'       CHCHTP
      *
     C                     MOVE *BLANKS   CHJOBN
     C                     MOVE *BLANKS   CHUSER
     C                     Z-ADD*ZEROS    CHJNBR
      *
     C                     MOVE #1FTYP    CHFTYP
     C                     MOVE #1GROU    CHGROU
     C                     MOVE #1CCY     CHCCY
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM #1CCY     PAJCD
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVEL#1AMT     ZFIELD
     C           15        SUB  A6NBDP    ZADIG
     C                     Z-ADDA6NBDP    ZADEC
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    CHAMT
      *
     C           #1BRCA    IFNE *BLANKS
     C                     MOVE #1BRCA    CHBRCA
     C                     ELSE
     C                     MOVE BBBRCD    CHBRCA
     C                     ENDIF
      *
     C                     MOVE #1SEAC    CHSEAC
      *
     C           #1SLSD    IFNE *BLANKS
     C                     MOVE #1SLSD    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    CHSLSD
     C                     ENDIF
      *
     C                     MOVE #1APOS    CHAPOS
     C                     MOVE #1SEDA    CHSEDA
     C                     MOVE #1FREQ    CHFREQ
     C                     MOVE #1DAMO    CHDAMO
      *
     C           #1FNSD    IFNE *ZEROS
     C                     MOVE #1FNSD    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    CHFNSD
     C                     ENDIF
      *
     C           #1FIND    IFNE *ZEROS
     C                     MOVE #1FIND    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    CHFIND
     C                     ENDIF
      *
     C                     MOVE #1ACCR    CHACCR
      *
     C                     MOVE #1PFTC    CHPFTC
     C                     MOVE #1NAR1    CHNAR1
     C                     MOVE #1NAR2    CHNAR2
     C                     MOVE #1NAR3    CHNAR3
     C                     MOVE #1NAR4    CHNAR4
     C                     MOVE #1HWMF    CHHWMF                                              CSD011
     C                     MOVE #1CFRE    CHCFRE                                              CSD011
     C                     MOVE #1CDMO    CHCDMO                                              CSD011
     C                     MOVE ULNCDA    CHNCDA                                              CSD011
      *
     C                     UPDATGLCHRGD0
     C                     MOVE FIELD     #0HFLD
      *
      ** Add commitment instruction
      *
     C                     COMIT
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRRTRV: Retrieve next available reference number              *
      *                                                               *
      * Called by:  SRVAL2                                            *
      * Calls:  None                                                  *
      *****************************************************************
      *
     C           SRRTRV    BEGSR
      *
      ** Setup "REFERENCE" field with next available number
      *
     C                     MOVE WWREFE    #0REFE
     C                     MOVE #0REFE    KREFE
     C                     ADD  1         WWREFE
      *
     C           KREFE     CHAINGLCHRGL1             88
      *
      ** If a record is found
      *
     C           *IN88     DOWEQ*OFF
      *
     C                     MOVE WWREFE    #0REFE
     C                     MOVE #0REFE    KREFE
     C                     ADD  1         WWREFE
      *
     C           KREFE     CHAINGLCHRGL1             88
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRPREF: Position into Reference File                          *
      *                                                               *
      * Called by:  SRSFAM                                            *
      * Calls:  None                                                  *
      *****************************************************************
      *
     C           SRPREF    BEGSR
      *
     C           CHRREF    IFNE *BLANKS
     C                     MOVE CHRREF    KREFE
     C           KREFE     SETLLGLCHRGL1
     C                     ELSE
      *
      ** First time processing
      *
     C           *IN45     IFEQ *OFF
     C           *HIVAL    SETLLGLCHRGL1
     C                     ELSE
     C           KREFE     IFNE *ZEROS
     C           KREFE     SETLLGLCHRGL1
     C                     ENDIF
     C                     ENDIF
      *
      ** If rollup in Amend/Enquiry mode
      *
     C           *IN27     IFEQ *ON
      *
     C           *IN43     IFEQ *ON
     C           *IN61     OREQ *ON
     C           KREFE     SETGTGLCHRGL1
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * SRREAD: Read next record of the Reference file                *
      *                                                               *
      * Called by:  SRSFAM                                            *
      * Calls:  None                                                  *
      *****************************************************************
      *
     C           SRREAD    BEGSR
      *
     C           CHRREF    IFNE *BLANKS
     C                     MOVE CHRREF    KREFE
     C           KREFE     READEGLCHRGL1                 30
     C                     ELSE
      *
      ** Access must be done by 'REFERENCE'
      *
     C           *IN45     IFEQ *OFF
     C           KREFE     ORNE *ZEROS
     C                     READ GLCHRGL1                 30
     C                     ELSE
      *
      ** If rollup in Amend/Enquiry mode
      *
     C           *IN27     IFEQ *ON
      *
     C           *IN43     IFEQ *ON
     C           *IN61     OREQ *ON
     C                     READ GLCHRGL1                 30
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * *INZSR : Initial processing                                   *
      *   ------                                                      *
      *   Called by: Main processing                                  *
      *   Calls: AOBANKR0                                             *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
      ** Copyright statement
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in DTAARA/RUNDAT
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
      ** Read in DTAARA/ZMUSER
      *
     C           *NAMVAR   DEFN           ZMUSER
     C                     IN   ZMUSER
      *
      **  Key fields
      *
     C           *LIKE     DEFN CHFIND    KFIND
     C           *LIKE     DEFN CHFNSD    KFNSD
     C           *LIKE     DEFN CHREFE    KREFE
     C           *LIKE     DEFN BBCSSN    WCSSN
      *
     C           KEY1      KLIST
     C**********           KFLD           KCUST   60                                          CSD027
     C                     KFLD           KCUST   6                                           CSD027
     C                     KFLD           KPTFR   4
      *
     C           KEY2      KLIST
     C                     KFLD           KCNUM   6
     C                     KFLD           KPTFR
     C                     KFLD           KFETP   2
      *
     C           KWFEE     KLIST
     C                     KFLD           KWFETP  2
     C                     KFLD           KWCHGR  2
      *
      ** Entry paramaters
      ** Recieve 'Charge Reference' to determine the processing mode
      *
     C           *ENTRY    PLIST
     C                     PARM           CHRREF  8
     C                     PARM           CHQUIT  1
      *
      ** Initialise key and working fields
      *
     C                     MOVE *BLANKS   WWLOT
     C                     MOVEL'GL1908'  DBPGM  10
      *
     C                     Z-ADD0         WWRRN   40
      *
      ** Access to bank details to retrieve the Midas run date
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*FIRST'  POPTN
     C           SDBANK    PARM SDBANK    DSSDY
      *
      ** If record not found
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE   30
     C                     MOVE 'SDBANKPD'DBFILE 10
     C                     MOVEL*BLANKS   DBKEY  29
     C                     MOVEL'GL1908'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVEL##USR     #0USER
     C                     MOVEL##USR     #1USER
     C                     MOVE BJMRDT    #0RUNA
     C                     MOVE BJMRDT    #1RUNA
     C                     MOVEL##JOB     #0WSID
     C                     MOVEL##JOB     #1WSID
      *
      ** Determine date format
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ** Check if Private Banking is installed
      *
     C                     CALL 'AOMMODR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*FIRST ' POPTN
     C           SDMMOD    PARM SDMMOD    DSSDY
      *
      ** If error in access program, exit via DBERR subroutine
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDMMODPD'DBFILE
     C                     Z-ADD5         DBASE
     C                     MOVEL'GL1908'  DBPGM
     C                     MOVELPOPTN     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           BGN0ST    IFEQ 'Y'
     C                     MOVE *ON       *IN10
     C                     ENDIF
      *
     C           BGPFMG    IFEQ 'Y'
     C           BGN4ST    OREQ 'Y'
     C                     MOVE *OFF      *IN65
     C                     ELSE
     C                     MOVE *ON       *IN65
     C                     ENDIF
      *
      ** Access SDBRCHPD using the default user branch
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM BVBRCD    PBRCH   3
     C           SDBRCH    PARM SDBRCH    DSSDY
      *
      ** Check if error occurred
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD6         DBASE
     C                     MOVE 'SDBRCHPD'DBFILE
     C                     MOVELBVBRCD    DBKEY
     C                     MOVEL'GL1908'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * If CSD011 is switched on, seton indicator to print Calculation                        CSD011
      * fields on report.                                                                     CSD011
     C                     CALL 'AOSARDR0'                                                    CSD011
     C                     PARM *BLANKS   PRTCD   7                                           CSD011
     C                     PARM '*KEY   ' POPTN   7                                           CSD011
     C                     PARM 'CSD011'  PSARD   6                                           CSD011
     C           SCSARD    PARM SCSARD    DSSDY                                               CSD011
     C           PRTCD     IFEQ *BLANKS                                                       CSD011
     C                     MOVE *ON       *IN75                                               CSD011
     C                     MOVE 'Y'       CSD011  1                                           CSD011
     C                     ELSE                                                               CSD011
     C                     MOVE *OFF      *IN75                                               CSD011
     C                     ENDIF                                                              CSD011
      *
      ** Calculate the GL Backvalue Date
      *
     C           BJRDNB    SUB  BVBVP     WBACKD  50
      *
      ** If the 'Charge Reference' parameter is not blanks
      ** seton indicator for enquiry mode
      *
     C           CHRREF    IFNE *BLANKS
     C                     MOVE *ON       *IN61
     C                     MOVE *ON       *IN66
     C                     MOVE *OFF      *IN67
     C                     MOVE *OFF      *IN68
     C                     ELSE
      *
      ** Access the charge reference file to see if there is record
      ** in the file
      *
     C           *HIVAL    SETGTGLCHRGL1
     C                     READ GLCHRGL1                 89
      *
      ** If record is not found, seton the indicator of 'ADD MODE'
      *
     C           *IN89     IFEQ *ON
     C                     MOVE *OFF      *IN43
     C                     MOVE *ON       *IN67
     C                     MOVE *OFF      *IN66
     C                     MOVE *OFF      *IN68
      *
      ** If record is not found, seton on indicator of  'AMEND MODE'
      *
     C                     ELSE
     C                     MOVE *ON       *IN43
     C                     MOVE *ON       *IN68
     C                     MOVE *OFF      *IN66
     C                     MOVE *OFF      *IN67
     C                     ENDIF
     C                     ENDIF
      *
      ** Retrieve next reference number
      *
     C           *LOVAL    SETLLGLCHRNPD
     C                     READ GLCHRNPD            N    89
      *
      ** If record not found
      *
     C           *IN89     IFEQ *ON
     C                     MOVE 'D'       CXRECI
     C                     Z-ADDBJRDNB    CXLCD
     C                     MOVE 'I'       CXCHTP
     C                     MOVE #0USER    CXLUID
     C                     MOVE '00000001'CXREFE
     C                     WRITEGLCHRND0
     C                     ENDIF
      *
     C                     MOVE CXREFE    WWREFE  80
     C                     MOVE CXREFE    WKREFE  80
      *
     C                     MOVE 'N'       #0HWAR
      *
      ** Select the program MSGQ for error messages
      *
     C                     MOVEL'*'       TOPQ   10
     C                     MOVEL'*PRV'    TOPRQ   5
     C                     MOVEL'GLUSRMSG'PMSGF  10
      *
      ** Fill in fields for subroutine SRSNMS
      *
     C                     MOVEL'GL1908'  PAPGM  10
     C                     MOVEL'GLUSRMSG'PAMSGF 10
     C                     MOVEL*BLANK    PAPGRL  5
     C                     MOVEL*BLANK    PAMSID  7
     C                     MOVEL*BLANK    PAMSDA132
     C                     MOVEL*BLANK    PAMSTP  7
      *
      ** Clear messages from program message queue.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     PAPGM
     C                     PARM '*SAME'   PAPGRL
      *
     C                     WRITE#MSGCTL
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * *PSSR - Error control subroutine                              *
      *  ------                                                       *
      *  Calls: DBERRCTL                                              *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
      ** Update LDA with error information
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVELDBFILE    WWFILE
     C                     MOVELDBKEY     WWKEY
     C                     MOVELDBPGM     WWPGM
     C                     MOVELDBASE     WWASE
     C                     OUT  LDA
      *
     C                     MOVE 'E'       CHQUIT
      *
      ** Rollback changes, print dump and cancel program
      *
     C                     ROLBK
     C                     DUMP
      *
      ** Try to release charge reference by access to charge file
      ** If current charge reference is not blank
      *
     C           #0REFE    IFNE *BLANKS
     C                     MOVE #0REFE    KREFE
     C           KREFE     SETLLGLCHRGL0
     C                     READ GLCHRGL0                 89
      *
      ** If the record exists
      *
     C           *IN89     IFEQ *OFF
      *
     C                     MOVE *BLANKS   CHJOBN
     C                     MOVE *BLANKS   CHUSER
     C                     Z-ADD*ZEROS    CHJNBR
      *
      ** Update the record
      *
     C                     UPDATGLCHRGD0
     C                     MOVE FIELD     #0HFLD
      *
      ** Commitment Instruction
      *
     C                     COMIT
     C                     ENDIF
     C                     ENDIF
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     CALL 'DBERRCTL'
     C                     RETRN
      *
     C                     ENDSR
      ****************************************************************
      /EJECT
      ****************************************************************
      *  SRSNMS : SEND MESSAGE TO PROGRAM'S MESSAGE QUEUE            *
      ****************************************************************
      *
     C           SRSNMS    BEGSR
      *
      ** message file specified use PMSGF
      *
     C                     MOVELPMSGF     PAMSGF
     C                     CALL 'Y2SNMGC'
     C                     PARM           PAPGM  10
     C                     PARM           PAPGRL  5
     C                     PARM           PAMSID  7
     C                     PARM           PAMSGF 10
     C                     PARM           PAMSDA132
     C                     PARM           PAMSTP  7
      *
      ** Clear all fields for default mechanism next time.
      *
     C                     MOVEL*BLANK    PAPGM
     C                     MOVEL*BLANK    PAPGRL
     C                     MOVEL*BLANK    PAMSDA
     C                     MOVEL*BLANK    PAMSTP
     C                     MOVEL*BLANK    PAMSGF
     C                     MOVEL*BLANK    PAMSID
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
     C/COPY ZSRSRC,ZDATE1Z2
      /EJECT
     C/COPY ZSRSRC,ZDATE2Z2
      /EJECT
     C/COPY ZSRSRC,ZALIGNZ2
      /EJECT
     C/COPY ZSRSRC,ZASIGNZ2
      /EJECT
     C/COPY ZSRSRC,ZSEDITZ3
     C/EJECT
     C/COPY ZSRSRC,ZBKDT
     C/EJECT
     C/COPY ZSRSRC,ZACCH
     C/EJECT
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  NARR
UNAUTHORISED
DELETED
AUTHORISED
GENERATED
EXPIRED
OVERDUE
STOPPED
CALCULATION REQ                                                                               CSD011
