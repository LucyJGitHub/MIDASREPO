     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Cust. Serv. Fees - Cust/Portf. to process')   *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL1932 - Customer Service Fees - Customers/portfolio to      *
      *           process - Audit                                     *
      *                                                               *
      * Function:  As some default values are available, we have      *
      * (COB)      to build a temporary file for the COB process      *
      *            GLCUFEPD will contains details of Fees to process  *
      *            with Customer/portfolio using them.                *
      *                                                               *
      *  Called By: GLC1932  Cust. Serv. Fees - Cust/Portf to process *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CLE064             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 192177             Date 07Mar02               *
      *                 CSD011             Date 04Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE064 - Enhancement of Lending Frequencies (Recompile)      *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  192177 - Allow Settlement Generation Days to be zero.        *
      *  CSD011 - Performance Incentive Fees.                         *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     FSDCFTPL0IF  E           K        DISK         KINFSR *PSSR
      ** Fee type details
      *
     FGLCHRGL2UF  E           K        DISK         KINFSR *PSSR
      ** Customer Fees Charges (Only Authorised, Generated or Overdue)
      *
     FGLCUFEPDO   E                    DISK         KINFSR *PSSR
      ** Customer Service Fees to process
      *
     FGL1932AUO   E                    PRINTER      KINFSR *PSSR      UC
     F                                              KINFDS SPOOLU
      ** Customer Service Fees to process - Audit
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   88  - Chain Failed Indicator                                *
      *   89  - End of File Indicator                                 *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRAUDT - Subroutine to Output Audit report and End Program.  *
      *  SRCHKD - Check other details                                 *
      *  SRRTVD - Retrieve fee details                                *
      *  SRSETP - Setup Customer Service to process (GLCUFEPD)        *
      *                                                               *
      *  *INSZR - Initialisation routine                              *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *                                                               *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZFRQAZ1
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZHOLE
      /SPACE 3
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
     ISDCUST    E DSSDCUSTPD
      ** Customer Details
      *
     ISDGELR    E DSSDGELRPD
      ** General Ledger Details
      *
     ISCSARD    E DSSCSARDPD                                                                  CSD011
      ** External data structures for Switchable features                                     CSD011
      *                                                                                       CSD011
     IDSFDY     E DSDSFDY
      ** First DS For Access Programs, Long Data Structure
      *
     ISPOOLU      DS
      ** File Information Data Structure for GL1932AU.
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
     I                                    B 188 1890OFLLNU
     I                                    B 367 3680PRTLNU
      *
     IPSDS       SDS
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     I/COPY ZSRSRC,ZHOLI
     I/COPY ZSRSRC,ZHOLDS
      *
      /EJECT
      ****************************************************************
      *                M A I N   P R O C E S S I N G                 *
      ****************************************************************
      *
      ** Read all customer/portfolio of the portfolio extension file
      *
     C                     READ GLCHRGL2                 89
      *
     C           *IN89     DOWEQ*OFF
      *
      ** Retrieve only Start Settlement Date lower
      ** or equal to Midas Run Date
      ** And Final date not passed
      *
     C           CHSLSD    IFLE BJRDNB
     C           CHFIND    ANDEQ0
     C           CHSLSD    ORLE BJRDNB
     C           CHFIND    ANDNE0
     C           CHFIND    ANDGEBJRDNB
      *
      ** Retrieve Default of different fields
      ** 1. Field is setuped in Customer Fees File
      ** 2. Field is setuped in Fee Type (Key Fee Type/Charge Group)
      ** 3. Field is setuped in Fee Type (Key Fee Type)
      *
      ** 1. Field is setuped in Customer Fees File
      *
     C                     MOVE CHCCY     DFCCCY  3
     C                     MOVE CHAPOS    DFAPOS  1
     C                     Z-ADDCHSEDA    DFSEDA  20
     C                     MOVE CHFREQ    DFFREQ  1
     C                     Z-ADDCHDAMO    DFDAMO  20
      *
      ** 2. Field is setuped in Fee Type (Key Fee Type/Charge Group)
      *
     C                     MOVE CHFTYP    KWFETP
     C                     MOVE CHGROU    KWCHGR
     C           KWFEE     CHAINSDCFTPL0             88
     C           *IN88     IFEQ *OFF
     C           F1RECI    ANDNE'*'
      *                                                                                       CSD011
      ** Any customer fees charge with fee processing type equal to                           CSD011
      *  'PP'is bypassed in this program as it will be processed in                           CSD011
      *  a new seperate RPG program called GL1993.                                            CSD011
      *                                                                                       CSD011
     C           CSD011    IFEQ 'Y'                                                           CSD011
     C           F1PRTP    ANDNE'PP'                                                          CSD011
      *                                                                                       CSD011
     C           DFCCCY    IFEQ *BLANKS
     C                     MOVE F1CCCY    DFCCCY
     C                     ENDIF
     C           DFAPOS    IFEQ *BLANKS
     C                     MOVE F1AUTO    DFAPOS
     C                     ENDIF
     C***********DFSEDA    IFEQ *ZEROS                                                        192177
     C***********          Z-ADDF1STGE    DFSEDA                                              192177
     C***********          ENDIF                                                              192177
     C           DFFREQ    IFEQ *BLANKS
     C                     MOVE F1STFR    DFFREQ
     C                     ENDIF
     C           DFDAMO    IFEQ *ZEROS
     C                     Z-ADDF1STDM    DFDAMO
     C                     ENDIF
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF
      *
      ** 3. Field is setuped in Fee Type (Key Fee Type)
      *
     C                     MOVE CHFTYP    KWFETP
     C                     MOVE *BLANKS   KWCHGR
     C           KWFEE     CHAINSDCFTPL0             88
     C           *IN88     IFEQ *OFF
     C           F1RECI    ANDNE'*'
      *                                                                                       CSD011
      ** Any customer fees charge with fee processing type equal to                           CSD011
      *  'PP'is bypassed in this program as it will be processed in                           CSD011
      *  a new seperate RPG program called GL1993.                                            CSD011
      *                                                                                       CSD011
     C           CSD011    IFEQ 'Y'                                                           CSD011
     C           F1PRTP    ANDNE'PP'                                                          CSD011
      *                                                                                       CSD011
     C           DFCCCY    IFEQ *BLANKS
     C                     MOVE F1CCCY    DFCCCY
     C                     ENDIF
     C           DFAPOS    IFEQ *BLANKS
     C                     MOVE F1AUTO    DFAPOS
     C                     ENDIF
     C***********DFSEDA    IFEQ *ZEROS                                                        192177
     C***********          Z-ADDF1STGE    DFSEDA                                              192177
     C***********          ENDIF                                                              192177
     C           DFFREQ    IFEQ *BLANKS
     C                     MOVE F1STFR    DFFREQ
     C                     ENDIF
     C           DFDAMO    IFEQ *ZEROS
     C                     Z-ADDF1STDM    DFDAMO
     C                     ENDIF
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDIF
      *
     C                     MOVE 'N'       WWCFFG  1
      *                                                                                       CSD011
     C           CSD011    IFEQ 'Y'                                                           CSD011
     C           F1PRTP    ANDNE'PP'                                                          CSD011
      *                                                                                       CSD011
      ** Check to see if next settl. date needs to be updated or not.
      ** Calculate the nth working day back from settlement date
      *
     C           CHFNSD    IFNE *ZERO
     C                     MOVE DFCCCY    ZCCY
     C                     Z-ADDCHFNSD    ZDAYNO
     C                     MOVE *BLANKS   ZLOC
     C                     Z-ADDDFSEDA    ZNRDYS
     C                     EXSR ZBKDT
     C                     Z-ADDZDYNBR    WWSTDT  50
      *
     C           WWSTDT    IFLE WWECD
     C                     Z-ADDCHFNSD    CHSLSD
      *
     C                     MOVE DFFREQ    ZFREQ
     C                     Z-ADDCHFNSD    ZDAYNO
     C                     Z-ADDDFDAMO    ZMDAY
     C                     MOVE DFCCCY    ZCCY
      *
      ** Now call the sub-routine with set up parameters
      ** work out "Next Settlement Date in the Month"
      *
     C                     EXSR ZFRQA
      *
     C           ZDAYNO    IFGT CHFIND
     C           CHFIND    ANDNE*ZEROS
     C                     Z-ADDCHFIND    CHFNSD
     C                     ELSE
     C                     Z-ADDZDAYNO    CHFNSD
     C                     ENDIF
      *
     C                     MOVE 'Y'       WWCFFG
     C                     ENDIF
     C                     ENDIF
      *
      ** Status Becomes 'O' if Status was 'G' and Run Date *GT Settle Dat
      *
     C           CHSTAT    IFEQ 'G'
     C           CHFNSD    ANDGEBJRDNB
     C                     MOVE 'O'       CHSTAT
     C                     ENDIF
      *
      ** Retrieve the fee type/charge group details
      *
     C                     MOVE 'N'       WWSTOP  1
     C                     EXSR SRRTVD
      *
      ** If the customer/portfolio/fee type processing is not stopped
      ** Add the new record in the tempory file
      *
     C           WWSTOP    IFEQ 'N'
      *
      ** Verify other Details
      *
     C                     EXSR SRCHKD
      *
     C                     WRITEGLCUFED0
     C                     UPDATGLCHRGD0
     C                     ENDIF
     C                     ENDIF
      *                                                                                       CSD011
     C                     ENDIF                                                              CSD011
      *
      ** Read next record.
      *
     C                     READ GLCHRGL2                 89
     C                     ENDDO
      *
      ** Normal End of Program
      *
     C           WWOPEN    IFEQ 'Y'
     C                     WRITETRAILAU
     C                     ENDIF
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      *         E N D   O F   M A I N   P R O C E S S I N G           *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRRTVD - Retrieve fee details                                *
      *                                                               *
      *  Called By : Main processing                                  *
      *                                                               *
      *  Calls     : SRAUDT                                           *
      *                                                               *
      *****************************************************************
      *
     C           SRRTVD    BEGSR
      *
     C                     CLEARGLCUFED0
      *
      ** Access the fee type default (fee type/charge group=blank)
      *
     C                     MOVE CHFTYP    KWFETP
     C                     MOVE *BLANKS   KWCHGR
     C           KWFEE     CHAINSDCFTPL0             88
      *
     C           *IN88     IFEQ *ON
     C           F1RECI    OREQ '*'
      *
      ** Add the details in the audit report, but no database error
      ** Stop this customer/portfolio/fee type processing
      *
     C                     EXSR SRAUDT
     C                     MOVE 'Y'       WWSTOP
     C                     ELSE
      *
      ** Load Fee details
      *
     C                     EXSR SRSETP
      *
     C                     ENDIF
      *
      ** Access the fee type/Charge group
      *
     C           WWSTOP    IFEQ 'N'
     C                     MOVE CHFTYP    KWFETP
     C                     MOVE CHGROU    KWCHGR
     C           KWFEE     CHAINSDCFTPL0             88
      *
     C           *IN88     IFEQ *ON
     C           F1RECI    OREQ '*'
      *
      ** Record not exist
      ** Keep the default, but load the charge group in the new record.
      *
     C                     MOVE CHGROU    F0CHGR
     C                     ELSE
      *
      ** Override the details of the previous access (to retrieve default)
      ** with the value not blank or not zero
      *
     C                     EXSR SRSETP
     C                     ENDIF
     C                     ENDIF
      *
      ** Stop this customer/portfolio/fee type processing
      *
     C           F0CFAC    IFNE 'Y'
     C           F0CFFG    ANDNE'Y'
     C                     MOVE 'Y'       WWSTOP
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRSETP - Setup Customer Service to process (GLCUFEPD)        *
      *                                                               *
      *  Called By : SRRTVD                                           *
      *                                                               *
      *  Calls     : None                                             *
      *                                                               *
      *****************************************************************
      *
     C           SRSETP    BEGSR
      *
     C                     MOVE CHCNUM    F0CNUM
     C                     MOVE CHPORT    F0PTFR
     C                     MOVE CHFTYP    F0FETP
     C                     MOVE CHGROU    F0CHGR
     C           F1PRTP    IFNE *BLANKS
     C                     MOVE F1PRTP    F0PRTP
     C                     ENDIF
     C           F1NARR    IFNE *BLANKS
     C                     MOVELF1NARR    F0NARR
     C                     ENDIF
     C           F1FECP    IFNE *BLANKS
     C                     MOVELF1FECP    F0FECP
     C                     ENDIF
     C           F1RETR    IFNE *ZEROS
     C                     Z-ADDF1RETR    F0RETR
     C                     ENDIF
     C           F1TAXE    IFNE *BLANKS
     C                     MOVE F1TAXE    F0TAXE
     C                     Z-ADDF1IRAC    F0IRAC
     C                     MOVE F1VATC    F0VATC
     C                     Z-ADDF1VATA    F0VATA
     C                     Z-ADDF1VATS    F0VATS
     C                     ENDIF
     C                     MOVE DFAPOS    F0AUTO
     C                     MOVE DFCCCY    F0CCCY
      *
      ** Minimum and maximum overriden together or not
      *
     C           F1MINA    IFNE *ZEROS
     C                     Z-ADDF1MINA    F0MINA
     C                     ENDIF
     C           F1MAXA    IFNE *ZEROS
     C                     Z-ADDF1MAXA    F0MAXA
     C                     ENDIF
     C           F1INAC    IFNE *ZEROS
     C                     Z-ADDF1INAC    F0INAC
     C                     ENDIF
     C           F1ACAC    IFNE *ZEROS
     C                     Z-ADDF1ACAC    F0ACAC
     C                     ENDIF
     C           F1ACFR    IFNE *BLANKS
     C                     MOVE F1ACFR    F0ACFR
     C                     Z-ADDF1ACDM    F0ACDM
     C                     Z-ADDF1ACDT    F0ACDT
     C                     ENDIF
     C           F1LADT    IFNE *ZEROS
     C                     Z-ADDF1LADT    F0LADT
     C                     ENDIF
     C           F1CADT    IFNE *ZEROS
     C                     Z-ADDF1CADT    F0CADT
     C                     ENDIF
     C                     Z-ADDDFSEDA    F0STGE
     C                     MOVE DFFREQ    F0STFR
     C                     Z-ADDDFDAMO    F0STDM
     C                     Z-ADDCHFNSD    F0STDT
     C                     Z-ADDCHSLSD    F0LSTD
     C                     Z-ADDCHSLSD    F0CSTD
     C           F1ALNB    IFNE *BLANKS
     C                     MOVE F1ALNB    F0ALNB
     C                     ENDIF
     C           F1IACC    IFNE *BLANKS
     C                     MOVE F1IACC    F0IACC
     C                     ENDIF
     C           F1INFF    IFNE *BLANKS
     C                     MOVE F1INFF    F0INFF
     C                     ENDIF
     C           F1CFAC    IFNE *BLANKS
     C           CHACCR    IFEQ 'Y'
     C                     MOVE F1CFAC    F0CFAC
     C                     ELSE
     C                     MOVE 'N'       F0CFAC
     C                     ENDIF
     C                     ENDIF
     C                     MOVE WWCFFG    F0CFFG
     C           F1VAIA    IFNE *ZEROS
     C                     Z-ADDF1VAIA    F0VAIA
     C                     ENDIF
     C                     MOVE *BLANKS   F0MRKT
     C                     MOVE CHSEAC    F0SEAC
     C                     MOVE CHPFTC    F0PFTC
     C                     Z-ADDCHAMT     F0FAMT
     C                     MOVELCHREFE    F0REFE
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRCHKD - Check other details                                 *
      *                                                               *
      *  Called By : Main processing                                  *
      *                                                               *
      *  Calls     : AOCUSTR0                                         *
      *                                                               *
      *****************************************************************
      *
     C           SRCHKD    BEGSR
      *
      ** Retrieve Customer branch
      *
     C                     MOVELF0CNUM    PKEYC  10 P
     C                     CALL 'AOCUSTR0'
     C                     PARM           PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM           PKEYC
     C                     PARM           PKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           PRTCD     IFEQ *BLANKS
     C                     MOVELBBBRCD    F0BRCA
     C                     ELSE
     C           *LOCK     IN   LDA
     C                     Z-ADD003       DBASE
     C                     MOVEL'SDCUSTPD'DBFILE    P
     C                     MOVE F0CNUM    DBKEY     P
     C                     MOVEL'GL1932'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Check if the fee charge next settlement date
      ** is less or equal to the rundate
      *
     C           CHFNSD    IFLE BJRDNB
      *
      ** Setup next current accrual date
      ** and current settlement date
      *
     C                     Z-ADDCHFNSD    F0ACDT
     C                     Z-ADDCHFNSD    F0CSTD
      *
      ** Set control flag accruals processing and control flag
      ** accruals generation
      *
     C                     MOVE 'Y'       F0CFAC
     C                     MOVE 'Y'       F0CFFG
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRAUDT - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By : Main Processing, *PSSR                           *
      *                                                               *
      *  Calls     : None.                                            *
      *                                                               *
      *****************************************************************
      *
     C           SRAUDT    BEGSR
      *
      ** Output Report Header and File Controls.
      *
     C           WWOPEN    IFEQ 'N'
     C                     OPEN GL1932AU
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
      ** Ensure Audit Spool File recorded by RCF.
      *
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   PSEQ    5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR   1
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     WRITEHEADAU
     C                     ENDIF
      *
      ** If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C           WWOPEN    IFEQ 'Y'
     C                     WRITEHEADAU
     C                     ENDIF
     C                     WRITEDBERROR
     C                     ELSE
      *
      ** Or, if no records read, Output "No Details" message.
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
      *
     C                     Z-ADD3         RQDLNU
     C           OFLLNU    SUB  PRTLNU    DIFLNU
     C           DIFLNU    IFLE RQDLNU
     C                     WRITEHEADAU
     C                     ENDIF
      *
      ** Normal audit
      *
     C                     MOVE 'Y'       WWOPEN
     C                     MOVE CHREFE    RREFE
     C                     MOVE CHCNUM    RCUST
     C                     MOVE CHPORT    RPTFR
     C                     MOVE CHFTYP    RFETP
     C                     MOVE CHGROU    RCHGR
      *
     C                     WRITEDETAIL
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *   *INZSR - Initial Processing                                 *
      *                                                               *
      *   Called by : Main processing                                 *
      *                                                               *
      *   Calls     : None.                                           *
      *                                                               *
      *****************************************************************
     C           *INZSR    BEGSR
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           KWFEE     KLIST
     C                     KFLD           KWFETP  2
     C                     KFLD           KWCHGR  2
      *
     C           KWCUFE    KLIST
     C                     KFLD           KWCNUM  6
     C                     KFLD           KWPTFR  4
     C                     KFLD           KWFETP  2
      *
     C           KWPORT    KLIST
     C                     KFLD           WNCNUM  60
      *
     C           *NAMVAR   DEFN           LDA
      *
     C                     MOVE 'N'       WWOPEN  1
      *
      ** Access Bank details file
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE
     C                     MOVEL'SDBANKPD'DBFILE    P
     C                     MOVEL'*FIRST'  DBKEY     P
     C                     MOVEL'GL1932'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Set *IN98 according to date format
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ** Access General Ledger details
      *
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*FIRST ' POPTN
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD002       DBASE
     C                     MOVEL'SDGELRPD'DBFILE    P
     C                     MOVEL'*FIRST'  DBKEY     P
     C                     MOVEL'GL1932'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Set event control date equal to the lesser of accrual profit
      ** date  and (Date of next working day - 1).
      *
     C           *LIKE     DEFN BJDNWD    WWECD
     C           BJDNWD    SUB  1         WWECD
     C           BKANWD    IFEQ '2'
     C           BKAPDT    IFLT WWECD
     C                     Z-ADDBKAPDT    WWECD
     C                     ELSE
     C                     Z-ADDBJRDNB    WWECD
     C                     ENDIF
     C                     ENDIF
      *
      ** Report Work fields.
      *
     C                     Z-ADD0         RQDLNU  30
     C                     Z-ADD0         DIFLNU  30
      *
      ** Determine if switchable feature CSD011 is switched ON.                               CSD011
      *                                                                                       CSD011
     C                     CALL 'AOSARDR0'                                                    CSD011
     C                     PARM *BLANKS   PRTCD   7                                           CSD011
     C                     PARM '*KEY   ' POPTN   7                                           CSD011
     C                     PARM 'CSD011'  PSARD   6                                           CSD011
     C           SCSARD    PARM SCSARD    DSFDY                                               CSD011
     C           PRTCD     IFEQ *BLANKS                                                       CSD011
     C                     MOVE 'Y'       CSD011  1                                           CSD011
     C                     ELSE                                                               CSD011
     C                     MOVE 'N'       CSD011                                              CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
      *
      ** Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C                     EXSR SRAUDT
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      *
      ***  Now copy in the routine to calculate next Accrual Date,
      ***  plus the two subordinate routines it uses - ZDATE1Z2,2Z2.
      *
     C/COPY ZSRSRC,ZFRQAZ2
     C/COPY ZSRSRC,ZDATE1Z2
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZCHKH
     C/COPY ZSRSRC,ZACCH
     C/COPY ZSRSRC,ZBKDT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZFMD  -  NUMBER OF DAYS IN THE MONTH
312831303130313130313031
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
