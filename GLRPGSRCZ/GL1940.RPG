     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Cust. serv. fees - SE holdings extract')      *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL1940 - Customer Service Fees -                             *
      *             - Securities Trading Holdings Extract             *
      *                                                               *
      *  Function:  This program Evaluate Sec. Trading Holdings to be *
      *  (phase(s)) able to calculate Safe Custody fees and/or        *
      *             Management Fees to Accrue.                        *
      *             Called during COB after Call of                   *
      *                                                               *
      *  Called By: GLC1940 - Cust. Serv. Fees - SE Holdings Extract  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. 248698             Date 01Jun20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 226449             Date 24Jun15               *
      *                 MD034776           Date 08Jul15               *
      *                 AR1067108          Date 11Dec12               *
      *                 AR1001183A         Date 20Nov12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 CSW037A            Date 02May05               *
      *                 229857             Date 07Oct04               *
      *                 226570             Date 27Jun04               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 194370             Date 12Jul01               *
      *                 CSE035             Date 22Apr02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CPK015             Date 17Jun01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  248698 - Rounding discrepancies on some SE reports. Applied  *
      *           fix 228682.                                         *
      *  228682 - Recompiled due to ZCNSDZ1 changes. Applied 176594.  *
      *  MD046248 - Finastra Rebranding                               *
      *  226449 - Recompiled over changes made in /COPY ZDYYRZ3.      *
      *         - Applied for MD034387.                               *
      *  MD034776 - Reverse AR1001183A to allow negative interest     *
      *             rate. (Recompile)                                 *
      *  AR1067108 - PMC1140 Component failed during Clean COB        *
      *              Applied fix AR1001183A                           *
      *  AR1001183A- Coupon rate was still used in computation of     *
      *              interest instead of using the details in FRN tab *
      *              (Reopen) (Recompile)                             *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  229857 - Recompiled due to change in ZYLDZ2.                 *
      *  226570 - Recompiled due to change in ZYLDZ2.                 *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  194370 - Recompile after change on copy sources ZYLDZ2,      *
      *           For securities with divisor basis = '5' and first   *
      *           coupon date > valuation date (rundate + 1), set     *
      *           event control date to first coupon date before      *
      *           processing SR/ZDYYR. This is to ensure that the     *
      *           correct hypothetical last and next coupon dates are *
      *           used in deriving the price/yield rate.              *
      *  CSE035 - NX/NNX Coupon Payments Processing.                  *
      *           Recompile over changed ZYLDZ2.                      *
      *  CPK015 - Conversion of ZYIELD from PLI to RPG.               *
      *           Recompile over changed ZYLDZ2.                      *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     FGLCUFEL7IF  E           K        DISK         KINFSR *PSSR
      ** Customers With SC or MG fees to accrued.
      *
     FCDEPC   IF  E           K        DISK         KINFSR *PSSR
      ** Client Holdings
      *
     FSECTY   IF  E           K        DISK         KINFSR *PSSR
      ** Security Type Details
      *
     FINVTP   IF  E           K        DISK         KINFSR *PSSR
      ** Investment Type
      *
     FSEDEV   IF  E           K        DISK         KINFSR *PSSR
      ** Security Diary Events file
      *
     FSECEO   IF  E           K        DISK         KINFSR *PSSR
      ** Securities Events file
     F            SNDEVDF                           KRENAMESNDEVDO
     F            SEDEVDF                           KRENAMESEDEVDO
      *
     FGLHOLDPDO   E                    DISK         KINFSR *PSSR
      ** Extract Holding Details
      *
     FGL1940AUO   E                    PRINTER
      ** SE Holdings extract - audit
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   86  - Chain Failed Indicator for INVTPD                     *
      *   87  - Chain Failed Indicator for SECTYD                     *
      *   88  - Chain Failed Indicator for CDEPC                      *
      *   89  - End of File Indicator                                 *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  *INSZR - Initialisation routine                              *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *                                                               *
      *****************************************************************
      /EJECT
      *
      ** Arrays for Se Standard routines
      *
      /COPY ZSRSRC,ZNCDZ1
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /COPY ZSRSRC,ZDATE2Z1
      /COPY ZSRSRC,ZDAYSZ1
      /COPY ZSRSRC,ZPOWER8Z1
      /SPACE 3
     ISECTYDF
      ** Rename Fields in format SECTYDF
     I              RECI                            SRECI
     IINVTPDF
      ** Rename Fields in format INVTPDF
     I              RECI                            IRECI
      *
      ** DS for Standard subroutines
      *
      /COPY ZSRSRC,ZNCDZ2
      /COPY ZSRSRC,ZYLDZ1
      *
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
     IPSDS       SDS
      ** Program Status Data Structure
     I                                     *PROGRAM @@PGM
      *
     ISDBANK    E DSSDBANKPD
      ** Access Object DS for Bank details file
      *
     ISDGELR    E DSSDGELRPD
      ** Access Object DS For General Ledger Module I.C.D. Details
      *
     ISDCURR    E DSSDCURRPD
      ** Access Object DS For Currency Details
      *
     ISDCUST    E DSSDCUSTPD
      ** Access Object DS For Customer Details (Depot Reference)
      *
     IDSSDY     E DSDSSDY
      ** Access Object DS Retrieve file details
      *
      /EJECT
      ***************************************************************
      *                      M A I N L I N E S                      *
      ***************************************************************
      *
      ** Read GLCUFEL7 until EOF
      *
     C                     READ GLCUFED0                 89
      *
     C           *IN89     DOWEQ*OFF
      *
      ** Read CDEPC until EOF
      *
     C**********           MOVE F0CNUM    KCNUM   60                                          CSD027
     C                     MOVE F0CNUM    KCNUM   6                                           CSD027
      *
     C           KCNUM     CHAINCDEPC                88
     C           *IN88     DOWEQ*OFF
      *
      ** Process only live trade at event control date
      *
     C           CDNV      IFNE 0
      *
      ** Fill up GLHOLDD0 with loans Details
      *
     C                     CLEARGLHOLDD0
      *
     C                     MOVE CDCN      F8CNUM
     C                     MOVE PTFR      F8PTFR
     C                     MOVEL'SE'      F8SRHO
      *
     C                     MOVELCDPT      PKEY   10
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM           PKEY
     C                     PARM           PKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCUSTPD'DBFILE
     C                     Z-ADD3         DBASE
     C                     MOVELCDPT      DBKEY     P
     C                     MOVEL@@PGM     DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBBCSSN    F8DPRF
      *
      ** Access SECTY for investment type, Currency code
      *
     C           CDSN      CHAINSECTY                87
     C           *IN87     IFEQ *ON
     C           SRECI     ORNE 'D'
     C           *LOCK     IN   LDA
     C                     MOVEL'SECTYD  'DBFILE
     C                     Z-ADD4         DBASE
     C                     MOVELCDSN      DBKEY     P
     C                     MOVEL@@PGM     DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELCDSN      F8SESN
     C                     MOVELNMCY      F8CCY
     C                     MOVELSITP      F8INVT
     C                     MOVELSMCC      F8MRKT
      *
      ** Access INVTP to retrieve instrument
      *
     C                     MOVELNMCY      KNMCY   3
     C                     MOVE SITP      KSITP   3
      *
     C           KINVT     CHAININVTP                86
     C           *IN86     IFEQ *ON
     C           IRECI     ORNE 'D'
     C           *LOCK     IN   LDA
     C                     MOVEL'INVTP   'DBFILE
     C                     Z-ADD5         DBASE
     C           NMCY      CAT  SITP      DBKEY     P
     C                     MOVEL@@PGM     DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELINNR      F8INST
      *
      ** Fill Nominal :
      *
     C           CDNV      IFGT 0
     C                     Z-ADDCDNV      F8ASNV
     C                     ELSE
     C                     Z-SUBCDNV      F8LINV
     C                     ENDIF
      *
      ** Fill Market  value
      *
     C                     Z-ADDCDNV      ZNOML
      *
      ** Convert Discount/Yield into Percentage Price
      *
     C           SPBS      IFEQ 'D'
     C           SPBS      OREQ 'Y'
     C                     Z-ADDBJRDNB    ZFDATE
     C                     Z-ADDMKPR      ZPRCIN
     C                     EXSR ZPRCI
     C                     Z-ADDZPRCOT    ZAVPR
     C                     ELSE
     C                     Z-ADDMKPR      ZAVPR
     C                     ENDIF
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM NMCY      PCURR   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCURRPD'DBFILE
     C                     Z-ADD6         DBASE
     C                     MOVELNMCY      DBKEY     P
     C                     MOVEL@@PGM     DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     Z-ADDNMDP      ZNMDP
     C                     Z-ADDA6NBDP    ZNMCD
     C                     MOVE SPBS      ZPRCB
     C                     EXSR ZAPNUM
     C                     Z-ADDZVALU     WWMVNC 150
      *
     C           WWMVNC    IFGT 0
     C                     Z-ADDWWMVNC    F8ASMV
     C                     ELSE
     C                     Z-SUBWWMVNC    F8LIMV
     C                     ENDIF
      *
      ** Accrued on interest
      *
     C           PROT      IFNE '2'
     C           PROT      ANDNE'4'
     C           PROT      ANDNE'7'
     C           CD01      ANDNE0
      *
     C           LCPN      IFNE 0
     C                     Z-ADDLCPN      PPLCDN
     C                     ELSE
     C                     Z-ADDISSD      PPLCDN
     C                     ENDIF
      *
     C                     CALL 'ZACRCD'
     C                     PARM           PPLCDN  50
     C                     PARM ECD       PPENDP  50
     C                     PARM CDPA      PPBRCA  3
     C**********           PARM CDCN      PPCUST  60                                          CSD027
     C                     PARM CDCN      PPCUST  6                                           CSD027
     C                     PARM CDSN      PPSESN 10
     C**********           PARM CDPT      PPDEPT  60                                          CSD027
     C                     PARM CDPT      PPDEPT  6                                           CSD027
     C                     PARM CDMK      PPMKTC  1
     C                     PARM ZNMCD     PPCDP   10
     C                     PARM           PPINTR 150
     C                     PARM           PPPOSI 150
     C                     PARM F8PTFR    PPPTFR  4
      *
     C           PPINTR    IFGT 0
     C                     Z-ADDPPINTR    F8ASAC
     C                     ELSE
     C                     Z-SUBPPINTR    F8LIMV
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** All other fees stay blanks or Zero
      *
     C                     WRITEGLHOLDD0
      *
     C                     ENDIF
      *
      ** next record
      *
     C           KCNUM     READECDEPC                    88
     C                     ENDDO
      *
      ** read next customer
      *
     C           F0CNUM    SETGTGLCUFEL7
     C                     READ GLCUFEL7                 89
     C                     ENDDO
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      ***************************************************************
      *               E N D    O F    M A I N L I N E S             *
      ***************************************************************
      /EJECT
      ***************************************************************
      *  *INZSR - Standard initial subroutine                       *
      *  ------                                                     *
      *  Called by : automatically                                  *
      *  Calls     :                                                *
      ***************************************************************
      *
     C           *INZSR    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Read SDBANKPD to retrieve Bank details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*FIRST ' POPTN
     C           SDBANK    PARM SDBANK    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE
     C                     Z-ADD1         DBASE
     C                     MOVEL'*FIRST ' DBKEY     P
     C                     MOVEL@@PGM     DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Read SDGELRPD to retrieve  Accrual Control date BKAPDT
      *
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*FIRST ' POPTN
     C           SDGELR    PARM SDGELR    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDGELRPD'DBFILE
     C                     Z-ADD002       DBASE
     C                     MOVEL'*FIRST ' DBKEY     P
     C                     MOVEL@@PGM     DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Event control date
      *
     C           BJDNWD    SUB  1         WWCTRL  50
     C           BKANWD    IFEQ '2'
     C           BKAPDT    IFLE WWCTRL
     C                     Z-ADDBKAPDT    WWCTRL  50
     C                     ELSE
     C                     Z-ADDBJRDNB    WWCTRL
     C                     ENDIF
     C                     ENDIF
      *
      ** Fill up 'Event Control Date' for ZDYYR subroutine
      *
     C                     Z-ADDWWCTRL    ECD     50
      *
      ** Define Key
      *
     C           KINVT     KLIST
     C                     KFLD           KNMCY   3
     C                     KFLD           KSITP   3
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *  *PSSR  - Program exception error routine                     *
      *  ------                                                       *
      * Called by : Automatically or EXSR                             *
      * Calls     :                                                   *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     DUMP
     C                     END
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /COPY ZSRSRC,ZDATE1Z2
      /COPY ZSRSRC,ZDATE2Z2
      /COPY ZSRSRC,ZAPNUM
      /COPY ZSRSRC,ZPRCIZ1
      /COPY ZSRSRC,ZDAYSZ2
      /COPY ZSRSRC,ZDYYRZ3
      /COPY ZSRSRC,ZYLDIZ1
      /COPY ZSRSRC,ZYLDZ2
      /COPY ZSRSRC,ZLCDZ1
      /COPY ZSRSRC,ZNCDZ3
      /COPY ZSRSRC,ZACCZZ1
      /COPY ZSRSRC,ZACCRZ1
      /COPY ZSRSRC,ZRATEZ1
      /COPY ZSRSRC,ZCALCD
      /COPY ZSRSRC,ZCNSDZ1
      *
      ** Standard Arrays : ZSRSRC : ZDATE2Z3, ZDAYSZ1, ZPOWER8Z2
      *
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**   ZF29 DAY NOS OF FEB 29 TO 2096 (INCLUDING 2000)
0006001521029820444305904073650882610287117481320914670161311759219053
2051421975234362489726358278192928030741322023366335124365853804639507
40968424294389045351
**  POWER8 - ARRAY OF POWERS FOR CURRENCY CONVERSION
00000001
00000010
00000100
00001000
00010000
00100000
01000000
10000000
