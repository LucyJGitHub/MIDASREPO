     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Cust. Serv. Fees -Accruals To Date Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL1984 - Customer Service Fees -                             *
      *           Fees Accrued to Date Report Prompt                  *
      *                                                               *
      *  Function:  This program allow users to select the fees       *
      *             accrued to date report by account officer,        *
      *             customer number, portfolio reference or fee type. *
      *                                                               *
      *  Called By: FCC0203                                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSD008   *CREATE   Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     FGLCHRGL5IF  E           K        DISK         KINFSR *PSSR
      **  Customer Fees Charges
      *
     FGLCHRGL6IF  E           K        DISK         KINFSR *PSSR
     F            GLCHRGD0                          KRENAMEGLCHRGD6
      **  Customer Fees Charges
      *
     FSDCFTPL0IF  E           K        DISK         KINFSR *PSSR
      **  Fee type Details
      *
     FGL1984DFCF  E                    WORKSTN      KINFSR *PSSR
      **  Fees Accrued to Date prompt
      *
     F                                              KINFDS INFDS#
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   21  - End program indicator                                 *
      *   34  - Subfile clear indicator for error message             *
      *   60  - Error Indicator for Account Officer field             *
      *   61  - Error Indicator for Customer Number field             *
      *   62  - Error Indicator for Portfolio Reference field         *
      *   63  - Error Indicator for Fee type                          *
      *   89  - Not found indicator for chain command                 *
      *                                                               *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRENTK - Enter key processing                                *
      *  SRSMSG - Send message in Subfile message                     *
      *                                                               *
      *  *INZSR - Initialisation routine                              *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *                                                               *
      *****************************************************************
     E                    CPY@    1   1 80
      **  Array containing Copyright statement
      *
      /EJECT
      *
     ILDA       E DSLDA                         256
      **  Local data area for database error details
      *
     IPSDS       SDS
      **  Program status data structure
      *
     I                                     *PROGRAM ##PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      **
      *
     ISDACOF    E DSSDACOFPD
      **  Account officer file
      *
     ISDCUST    E DSSDCUSTPD
      **  Data structure for customer details
      *
     ISDBANK    E DSSDBANKPD
      **  Bank Details
      *
     IDSSDY     E DSDSSDY
      **  Second DS For Access Programs, Long Data Structure
      *
     IINFDS#      DS
      **  I/O Feedback Area: Workstation.
      *
     I                                      261 270 SWFMT
      *
     IPPARM       DS                            100
      **  Parameter Data
      *
     I                                        1   4 WWACOC
     I                                        5  10 WWCUST
     I                                       11  14 WWPTFR
     I                                       15  18 WWFETP
      *
     C/EJECT
      *****************************************************************
      *                 M A I N   P R O C E S S I N G                 *
      *****************************************************************
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *IN21     DOWEQ*OFF
      *
      **  Display Screen.
      *
     C                     WRITE#MSGCTL
     C                     EXFMTGL1984F0
      *
      **  Clear screen message file
      *
     C                     CALL 'CLRERMSG'
     C                     PARM ##PGM     ZAPGM
     C                     PARM '*SAME'   ZAPGRL
     C                     MOVE *ON       *IN34
     C                     WRITE#MSGCTL
     C                     MOVE *OFF      *IN34
      *
     C           *INKC     IFEQ *ON
     C                     MOVE *BLANKS   WWACOC
     C                     MOVE *BLANKS   WWCUST
     C                     MOVE *BLANKS   WWPTFR
     C                     MOVE *BLANKS   WWFETP
     C                     MOVE 'E'       PCMD
     C                     MOVE *ON       *IN21
     C                     ELSE
      *
     C                     EXSR SRENTK
     C                     ENDIF
     C                     ENDDO
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      *          E N D   O F   M A I N   P R O C E S S I N G          *
      *****************************************************************
      /EJECT
      *****************************************************************
      *  SRENTK - Enter key processing                                *
      *                                                               *
      *  Called By : Main Processing                                  *
      *  Calls     : *PSSR Error processing                           *
      *****************************************************************
      *
     C           SRENTK    BEGSR
      *
      **  Reset Data Items.
      *
     C                     MOVE 'N'       WWERR   1
     C                     MOVE *OFF      *IN60
     C                     MOVE *OFF      *IN61
     C                     MOVE *OFF      *IN62
     C                     MOVE *OFF      *IN63
      *
     C           #0ACOC    IFNE '*ALL'
      *
      **  Access to Account officer file to retrieve the account
      **  officer name
      *
     C           #0ACOC    IFEQ '?'
     C                     MOVE 'Y'       WWERR
     C                     ENDIF
     C                     CALL 'AOACOFR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM #0ACOC    PCKEY2  2
     C           SDACOF    PARM SDACOF    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVE *ON       *IN60
     C                     MOVE 'Y'       WWERR
     C                     MOVEL'CSF9115' ZAMSID
     C                     EXSR SRSMSG
     C                     ELSE
     C                     MOVELAZACOC    #0ACOC    P
     C                     ENDIF
     C                     ENDIF
      *
      **  Access to customer details
      *
     C           #0CUST    IFNE '*ALL'
      *
     C           #0CUST    IFEQ '?'
     C                     MOVE 'Y'       WWERR
     C                     ENDIF
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM #0CUST    PCKEY  10
     C                     PARM           PKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVE *ON       *IN61
     C                     MOVE 'Y'       WWERR
     C                     MOVEL'CSF9080' ZAMSID
     C                     EXSR SRSMSG
     C                     ELSE
     C                     MOVELBBCUST    #0CUST
     C                     ENDIF
     C                     ENDIF
      *
      **  Access to Fee details
      *
     C           #0FETP    IFNE '*ALL'
     C                     MOVEL#0FETP    WKFETP  2
     C           WKFETP    CHAINSDCFTPL0             89
     C           *IN89     IFEQ *ON
     C           F1RECI    OREQ '*'
     C                     MOVE *ON       *IN63
     C                     MOVE 'Y'       WWERR
     C                     MOVEL'CSF9010' ZAMSID
     C                     EXSR SRSMSG
     C                     ENDIF
     C                     ENDIF
      *
     C           WWERR     IFEQ 'N'
     C           #0CUST    ANDNE'*ALL'
     C           #0PTFR    ANDNE'*ALL'
     C           #0FETP    ANDNE'*ALL'
      *
     C                     MOVE #0CUST    KCUST
     C                     MOVE #0PTFR    KPTFR
     C                     MOVEL#0FETP    KFETP
      *
      **  Check with portfolio details if portfolio reference is
      **  entered or check with customer details if portfolio
      **  reference is not entered
      *
     C           #0PTFR    IFEQ *BLANKS
     C           KWCUFE    CHAINGLCHRGL5             89
     C                     ELSE
     C           KWPOFE    CHAINGLCHRGL6             89
      *
     C           *IN89     IFEQ *ON
     C           F1FECP    ANDEQ'P'
     C           KWCUFE    CHAINGLCHRGL5             89
     C                     ENDIF
     C                     ENDIF
      *
     C           *IN89     IFEQ *ON
     C                     MOVE *ON       *IN61
     C                     MOVE *ON       *IN62
     C                     MOVE *ON       *IN63
     C                     MOVE 'Y'       WWERR
     C                     MOVEL'CSF9095' ZAMSID
     C                     EXSR SRSMSG
     C                     ENDIF
     C                     ENDIF
      *
     C           WWERR     IFEQ 'N'
     C                     MOVE *ON       *IN21
     C                     MOVE #0ACOC    WWACOC
     C                     MOVE #0CUST    WWCUST
     C                     MOVE #0PTFR    WWPTFR
     C                     MOVE #0FETP    WWFETP
     C                     MOVE ' '       PCMD
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *   SRSMSG - Send message in Subfile message                    *
      *   ------                                                      *
      * Called by : VALID                                             *
      * Calls     : SNDERMSG                                          *
      *****************************************************************
      *
     C           SRSMSG    BEGSR
      *
     C                     CALL 'SNDERMSG'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZAMSID  7
     C                     PARM           ZAMSGF 10
     C                     PARM           ZAMSDA132
     C                     PARM           ZAMSTP  7
      *
      * Clear all fields for default mechanism next time.
      *
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZAMSID
     C                     MOVEL*BLANK    ZAMSDA
     C                     MOVEL*BLANK    ZAMSTP
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *   *INZSR - Initial Processing                                 *
      *   -------                                                     *
      *   Called by : Main processing                                 *
      *   Calls     : None.                                           *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
      **  Obtain default message file.
      *
     C           *NAMVAR   DEFN Y2MGFLA   ZADFMF 10
     C                     IN   ZADFMF
      *
      **  Clear screen message file
      *
     C                     CALL 'CLRERMSG'
     C                     PARM ##PGM     ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
      **  Set Initial values.
      *
     C           *NAMVAR   DEFN           LDA
      *
      **  Access Bank details file
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ************
     C                     MOVE 'SDBANKPD'DBFILE    P      * DBERR 01 *
     C                     MOVEL'*FIRST ' DBKEY     P      ************
     C                     MOVEL'GL1984'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELUSER      #0USER
     C                     MOVE BJMRDT    #0RUNA
     C                     MOVELWSID      #0WSID
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PLVL    1
     C                     PARM           PENT    3
     C                     PARM           PPARM
     C                     PARM           PACT    1
     C                     PARM           PCMD    1
      *
     C           KWCUFE    KLIST
     C                     KFLD           KCUST   6
     C                     KFLD           KFETP   2
      *
     C           KWPOFE    KLIST
     C                     KFLD           KCUST
     C                     KFLD           KPTFR   4
     C                     KFLD           KFETP
      *
      **  Cleanup values from previous call.
      *
     C                     MOVE *BLANK    WWACOC
     C                     MOVE *BLANK    WWCUST
     C                     MOVE *BLANK    WWPTFR
     C                     MOVE *BLANK    WWFETP
     C                     MOVE *OFF      *IN21
     C                     MOVE *OFF      *IN60
     C                     MOVE *OFF      *IN61
     C                     MOVE *OFF      *IN62
     C                     MOVE *OFF      *IN63
     C                     MOVE *OFF      *INKC
      *
     C                     MOVEL'GLUSRMSG'ZAMSGF
     C                     MOVEL'*ALL'    #0ACOC
     C                     MOVEL'*ALL'    #0CUST
     C                     MOVEL'*ALL'    #0PTFR
     C                     MOVEL'*ALL'    #0FETP
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *  *PSSR  - Error control subroutine - automatically Called or  *
      *  ------                                                       *
      *  Called By : *INZSR                                           *
      *  Calls     : DBERRCTL                                         *
      *****************************************************************
     C           *PSSR     BEGSR
      *
      **  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANKS
     C                     MOVE 'Y'       WRUN    1
     C                     ROLBK
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     ENDIF
      *
      **  If *PSSR already run, then just end the program here.
      *
     C                     MOVE 'E'       PCMD
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
**  CPY@
(c) Finastra International Limited 2001
