     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL CS Fees Outs. Settlements Report Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL1986 - Customer Service Fees Outstanding Settlements Report*
      *           Prompt                                              *
      *                                                               *
      *  Function:  This program will allow users to select the       *
      *             the outstanding fee settlements report by account *
      *             officer, customer number, portfolio reference,fee *
      *             type and authorisation status.                    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSD008   *CREATE   Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     FGLCHRGL5IF  E           K        DISK         KINFSR *PSSR
      ** Customer Fees Charges
      *
     FGLCHRGL6IF  E           K        DISK         KINFSR *PSSR
     F            GLCHRGD0                          KRENAMEGLCHRGD6
      ** Customer Fees Charges
      *
     FSDCFTPL0IF  E           K        DISK         KINFSR *PSSR
      ** Fee type Details
      *
     FGL1986DFCF  E                    WORKSTN      KINFSR *PSSR
      ** Fees Accrued to Date prompt
      *
     F                                              KINFDS INFDS#
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *   21  - Exit indicator                                        *
      *   60  - Error Indicator for Account Officer                   *
      *   61  - Error Indicator for Customer Number                   *
      *   62  - Error Indicator for Portfolio Reference               *
      *   63  - Error Indicator for Fee Type                          *
      *   64  - Error Indicator for Authorisation                     *
      *   89  - Record not found                                      *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRENTR - Enter key processing                                *
      *  SRSMSG - Send message in Subfile message                     *
      *  *INZSR - Initial Processing                                  *
      *  *PSSR  - Error control subroutine - automatically Called     *
      *                                                               *
      *****************************************************************
      *
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /EJECT
      *
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
     ISDACOF    E DSSDACOFPD
      ** Account officer file
      *
     ISDCUST    E DSSDCUSTPD
      ** Data structure for customer details
      *
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
     IDSFDY     E DSDSFDY
      ** First DS For Access Programs, Short Data Structure
      *
     IDSSDY     E DSDSSDY
      ** Second DS For Access Programs, Long Data Structure
      *
     IPSDS       SDS
      ** Program Status Data Structure
      *
     I                                     *PROGRAM ##PGM
     I                                      244 253 #0WSID
     I                                      254 263 #0USER
      *
     IINFDS#      DS
      ** I/O Feedback Area: Workstation.
      *
     I                                      261 270 SWFMT
      *
     IPWPARM      DS                            100
      ** Parameter Data
      *
     I                                        1   4 WWACOC
     I                                        5  10 WWCUST
     I                                       11  14 WWPTFR
     I                                       15  18 WWFETP
     I                                       19  19 WWAUID
      *
      /EJECT
      *****************************************************************
      *                 M A I N   P R O C E S S I N G                 *
      *****************************************************************
      *
     C           *IN21     DOWEQ*OFF
      *
      ** Display Screen.
      *
     C                     WRITEGL1986F0
      *
     C           WWERR     IFEQ 'Y'
     C                     WRITE#MSGCTL
     C                     ENDIF
      *
     C                     READ GL1986F0                 89
      *
      ** Clear screen message file
      *
     C                     CALL 'CLRERMSG'
     C                     PARM ##PGM     ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
     C           *INKC     IFEQ *ON
     C                     MOVE *BLANKS   WWACOC
     C                     MOVE *BLANKS   WWCUST
     C                     MOVE *BLANKS   WWPTFR
     C                     MOVE *BLANKS   WWFETP
     C                     MOVE *BLANKS   WWAUID
     C                     MOVE 'E'       PCMD
     C                     MOVE *ON       *IN21
     C                     ELSE
      *
     C                     EXSR SRENTR
     C                     ENDIF
     C                     ENDDO
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      *          E N D   O F   M A I N   P R O C E S S I N G          *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRENTR - Enter key processing                                *
      *                                                               *
      *  Called By : Main Processing                                  *
      *  Calls     :                                                  *
      *   SRSMSG - Send message in Subfile message                    *
      *   AOACOFR0                                                    *
      *   AOCUSTR0                                                    *
      *****************************************************************
      *
     C           SRENTR    BEGSR
      *
      ** Reset Data Items.
      *
     C                     MOVE 'N'       WWERR   1
     C                     MOVEA'00000'   *IN,60
      *
     C           #0ACOC    IFNE '*ALL'
     C           #0ACOC    IFEQ '?'
     C                     MOVE 'Y'       WWERR
     C                     ENDIF
      *
      ** Access to Account officer file to retrieve the account officer name
      *
     C                     CALL 'AOACOFR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM #0ACOC    PCKEY2  2
     C           SDACOF    PARM SDACOF    DSFDY
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVE '1'       *IN60
     C                     MOVE 'Y'       WWERR
     C                     MOVEL'CSF9115' ZMSID
     C                     EXSR SRSMSG
     C                     ELSE
     C                     MOVELAZACOC    #0ACOC    P
     C                     ENDIF
     C                     ENDIF
      *
      ** Access to customer details
      *
     C           #0CUST    IFNE '*ALL'
      *
     C           #0CUST    IFEQ '?'
     C                     MOVE 'Y'       WWERR
     C                     ENDIF
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM #0CUST    PCKEY  10
     C                     PARM           PKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVE '1'       *IN61
     C                     MOVE 'Y'       WWERR
     C                     MOVEL'CSF9080' ZMSID
     C                     EXSR SRSMSG
     C                     ELSE
     C                     MOVELBBCUST    #0CUST
     C                     ENDIF
     C                     ENDIF
      *
      ** Access to Fee details
      *
     C           #0FETP    IFNE '*ALL'
     C                     MOVEL#0FETP    WKFETP  2
     C           WKFETP    CHAINSDCFTPL0             89
     C           *IN89     IFEQ *ON
     C           F1RECI    OREQ '*'
     C                     MOVE '1'       *IN63
     C                     MOVE 'Y'       WWERR
     C                     MOVEL'CSF9010' ZMSID
     C                     EXSR SRSMSG
     C                     ENDIF
     C                     ENDIF
      *
     C           WWERR     IFEQ 'N'
     C           #0CUST    ANDNE'*ALL'
     C           #0PTFR    ANDNE'*ALL'
     C           #0FETP    ANDNE'*ALL'
      *
     C                     MOVE #0CUST    KCUST
     C                     MOVE #0PTFR    KPTFR
     C                     MOVEL#0FETP    KFETP
      *
      ** Check with portfolio details if portfolio reference is entered
      ** or with customer details if portfolio reference is not entered
      *
     C           #0PTFR    IFEQ *BLANKS
     C           KWCUFE    CHAINGLCHRGL5             89
     C                     ELSE
     C           KWPOFE    CHAINGLCHRGL6             89
     C                     ENDIF
      *
     C           *IN89     IFEQ *ON
     C                     MOVEA'111'     *IN,61
     C                     MOVE 'Y'       WWERR
     C                     MOVEL'CSF9095' ZMSID
     C                     EXSR SRSMSG
     C                     ENDIF
     C                     ENDIF
      *
      ** Check Settlement Authorisation
      ** Must be (Y=Authorised, N=Not Authorised, X=All Settlement)
      *
     C           #0AUID    IFNE 'Y'
     C           #0AUID    ANDNE'N'
     C           #0AUID    ANDNE'X'
     C                     MOVE *ON       *IN64
     C                     MOVE 'Y'       WWERR
     C                     MOVEL'CSF9116' ZMSID
     C                     EXSR SRSMSG
     C                     ENDIF
      *
     C           WWERR     IFEQ 'N'
     C                     MOVE *ON       *IN21
     C                     MOVE #0ACOC    WWACOC
     C                     MOVE #0CUST    WWCUST
     C                     MOVE #0PTFR    WWPTFR
     C                     MOVE #0FETP    WWFETP
     C                     MOVE #0AUID    WWAUID
     C                     MOVE ' '       PCMD
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRSMSG - Send message in Subfile message                     *
      *                                                               *
      *  Called by : SRENTR                                           *
      *  Calls     : SNDERMSG                                         *
      *                                                               *
      *****************************************************************
      *
     C           SRSMSG    BEGSR
      *
     C                     CALL 'SNDERMSG'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZMSID   7
     C                     PARM           ZMSGF  10
     C                     PARM           ZMSDA 132
     C                     PARM           ZMSTP   7
      *
      ** Clear all fields for default mechanism next time.
      *
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZMSID
     C                     MOVEL*BLANK    ZMSDA
     C                     MOVEL*BLANK    ZMSTP
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *   *INZSR - Initial Processing                                 *
      *                                                               *
      *   Called by : Main processing                                 *
      *   Calls     : CLRERMSG, AOBANKR0                              *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PLVL    1
     C                     PARM           PENT    3
     C                     PARM           PWPARM
     C                     PARM           PACT    1
     C                     PARM           PCMD    1
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Clear screen message file
      *
     C                     CALL 'CLRERMSG'
     C                     PARM ##PGM     ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
      *
      ** Set Initial values.
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Access Bank details file
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      *
      ** Data base error in file (file)
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVEL'*FIRST'  DBKEY
     C                     MOVEL'GL1986'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE BJMRDT    #0MRDT
      *
     C           KWCUFE    KLIST
     C                     KFLD           KCUST   6
     C                     KFLD           KFETP   2
      *
     C           KWPOFE    KLIST
     C                     KFLD           KCUST   6
     C                     KFLD           KPTFR   4
     C                     KFLD           KFETP   2
      *
      ** Cleanup values from previous call.
      *
     C                     MOVE *BLANK    WWACOC
     C                     MOVE *BLANK    WWCUST
     C                     MOVE *BLANK    WWPTFR
     C                     MOVE *BLANK    WWFETP
     C                     MOVE *BLANK    WWAUID
     C                     MOVE *OFF      *IN21
     C                     MOVEA'0000'    *IN,60
     C                     MOVE *OFF      *INKC
      *
     C                     MOVEL'GLUSRMSG'ZMSGF
     C                     MOVEL'*ALL'    #0ACOC
     C                     MOVEL'*ALL'    #0CUST
     C                     MOVEL'*ALL'    #0PTFR
     C                     MOVEL'*ALL'    #0FETP
     C                     MOVEL'X'       #0AUID
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *  *PSSR  - Error control subroutine - automatically Called     *
      *                                                               *
      *  Calls     : DBERRCTL                                         *
      *****************************************************************
     C           *PSSR     BEGSR
      *
      ** Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     MOVE 'E'       PCMD
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      /EJECT
**  CPY@
(c) Finastra International Limited 2001
