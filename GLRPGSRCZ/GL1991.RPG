     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Cust. Serv. Fees -Drop Out Expired Charge')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL1991 - Customer Service Fees - Drop Out Expired Charges    *
      *                                                               *
      *  Function:  This program drops from the system all expired    *
      *  (COB)      fee charges record which are beyond the retention *
      *             period of the fee type.                           *
      *                                                               *
      *  Called by: GLC1991 - Drop of Expired Charges                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD008 - Customer Service Fees Enhancements                  *
      *                                                               *
      *****************************************************************
      /EJECT
     FGLCHRGL0UF  E           K        DISK
      ** FE Customer Fees Charges - By Charge Ref.
      *
     F                                              KINFSR *PSSR
      *
     FSDCFTPL0IF  E           K        DISK
      ** FE Fee Type Details - By Fee Type/Charge Group
      *
     F                                              KINFSR *PSSR
      *
     FGL1991P1O   E                    PRINTER      KINFDS SPOOL1     UC
      ** Detail Printer File
      *
     F                                              KINFSR *PSSR
      *
     FGL1991AUO   E                    PRINTER      KINFDS SPOOLU
      ** Audit Printer File
      *
     F                                              KINFSR *PSSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   88 - End of File                                            *
      *   89 - Record no Found                                        *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRAUDT - Output Audit Report                                 *
      *  SRODPF - Output Details to Printer File                      *
      *  SRRCF1 - Subroutine to register P1 report via RCF            *
      *  SRRCFA - Subroutine to register AU report via RCF            *
      *                                                               *
      *  *INSZR - Initialisation routine                              *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
      **   For Compilation - Copyright Insertion
      *
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZASIGNZ1
     E/COPY ZSRSRC,ZSEDITZ1
      *
      ******************************************************************
      /EJECT
      ******************************************************************
     ISPOOLU      DS
      ** File Information Data Structure (Audit Report)
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     ISPOOL1      DS
      ** File Information Data Structure (Detail Report)
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ILDA       E DSLDA                         256
      **   Data Structure for Database Error
      *
     IRUNDAT    E DS
      ** Standard data area layout for system flags and run date
      *
     ISDBANK    E DSSDBANKPD
      ** Dummy record format for access to Bank Details
      *
     ISDCURR    E DSSDCURRPD
      ** Dummy record format for access to Currency Details
      *
     ISDGELR    E DSSDGELRPD
      ***  General Ledger Details
      *
     ISDCUST    E DSSDCUSTPD
      ** Dummy record format for access to Customer Details
      *
     IDSFDY     E DSDSFDY
      ** First DS for access programs, short data structure
      *
     IDSSDY     E DSDSSDY
      ** Second DS for access programs, long data structure
      *
     I/COPY ZSRSRC,ZSEDITZ2
      *****************************************************************
      /EJECT
      *****************************************************************
      *                     M A I N L I N E S                         *
      *****************************************************************
      *
      ** Read first record in LF/GLCHRGL0
      *
     C           *LOVAL    SETLLGLCHRGL0
     C                     READ GLCHRGL0                 88
      *
      ** Do while end of file is not reached
      *
     C           *IN88     DOWEQ'0'
      *
      ** Retrive Retention Period of the Settled fees
      *
     C                     Z-ADD*ZEROS    WWNTDT  20
      *
      ** Field is setuped in Fee Type (Key Fee Type/Charge Group)
      *
     C                     MOVE CHFTYP    KWFETP
     C                     MOVE CHGROU    KWCHGR
     C           KWFEE     CHAINSDCFTPL0             89
     C           *IN89     IFEQ *OFF
     C           F1RECI    ANDNE'*'
     C           WWNTDT    IFEQ *ZEROS
     C                     Z-ADDF1RETP    WWNTDT
     C                     ENDIF
     C                     ENDIF
      *
     C           WWNTDT    IFEQ *ZEROS
      *
      ** Field is setuped in Fee Type (Key Fee Type)
      *
     C                     MOVE CHFTYP    KWFETP
     C                     MOVE *BLANKS   KWCHGR
     C           KWFEE     CHAINSDCFTPL0             89
     C           *IN89     IFEQ *OFF
     C           F1RECI    ANDNE'*'
     C           WWNTDT    IFEQ *ZEROS
     C                     Z-ADDF1RETP    WWNTDT
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** Calculate Retention Date
      *
     C           31        MULT WWNTDT    WWFLD1  50
     C           BJRDNB    SUB  WWFLD1    RTNTDT  50
      *
      ** If Allocation Effective Date is lower than or equal to
      ** Retention Date  And Status is Expired
      **                 or  Status is Deleted
      *
     C           CHFIND    IFLE RTNTDT
     C           CHFIND    ANDNE*ZEROS
     C           CHSTAT    ANDEQ'E'
     C           CHSTAT    OREQ 'D'
      *
      ** Open GL1991P1
      *
     C           WWOP1     IFEQ 'N'
     C                     OPEN GL1991P1
     C                     EXSR SRRCF1
     C                     WRITEHEADP1
     C                     MOVE 'Y'       WWOP1
     C                     ENDIF
      *
     C                     Z-ADD3         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ** Output a detail line
      *
     C                     EXSR SRODPF
      *
      ** Delete the record
      *
     C                     DELETGLCHRGD0
      *
     C                     ADD  1         WWNCAD
      *
     C                     ENDIF
      *
      ** Read next record
      *
     C                     READ GLCHRGL0                 88
      *
     C                     ENDDO
      *
      ** End Processing/Produce Audit List
      *
     C                     EXSR SRAUDT
      *
      ** Write end of report
      *
     C           WWOP1     IFEQ 'Y'
     C                     WRITETRAILP1
     C                     CLOSEGL1991P1
     C                     ENDIF
      *
      ** End of program
      *
     C                     MOVE '1'       *INLR
     C                     RETRN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRODPF - Output Details to Printer File                      *
      *                                                               *
      *  Called by : Main Processing                                  *
      *                                                               *
      *  Calls     : ZDATE2                                           *
      *                                                               *
      *****************************************************************
     C           SRODPF    BEGSR
      *
     C                     MOVELCHREFE    PPREFE
     C                     MOVELCHFTYP    PPFTYP
     C                     MOVELCHGROU    PPGROU
     C                     MOVELCHCNUM    PPCNUM
     C*                    MOVEL'SHORT'   PPCSSN
     C                     MOVELCHPORT    PPPORT
     C                     MOVE CHFIND    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    PPFIND
     C                     MOVELCHSEAC    PPSEAC
     C                     MOVE CHCCY     PPCCY
      *
      ** Get Customer Shortname
      *
     C                     MOVE *BLANKS   PKEY
     C                     MOVELCHCNUM    PKEY
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY'    POPTN
     C                     PARM           PKEY   10
     C                     PARM           PKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE
     C                     MOVEL*BLANKS   DBFILE
     C                     MOVE 'SDCUSTPD'DBFILE
     C                     MOVELPKEY      DBKEY     P
     C                     MOVEL'GL1991'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVE BBCSSN    PPCSSN
     C                     ENDIF
      *
      ** Format Amount
      *
     C           CHAMT     IFNE *ZEROS
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM CHCCY     PAJCD   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE
     C                     MOVEL*BLANKS   DBFILE
     C                     MOVE 'SDCURRPD'DBFILE
     C                     MOVELCHCCY     DBKEY     P
     C                     MOVEL'GL1991'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE CHAMT     ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE *BLANKS   PPAMT
     C                     MOVE ZOUT21    WFLD17 17
     C                     MOVELWFLD17    PPAMT
     C                     ELSE
     C                     MOVE *BLANKS   PPAMT
     C                     ENDIF
      *
      ** Write a record into the printer file
      *
     C                     WRITEDETAIL
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRAUDT - Subroutine to Output Audit Report                   *
      *                                                               *
      *  Called by: Main processing                                   *
      *                                                               *
      *  CALLS    :                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRAUDT    BEGSR
      *
      ** Output Report Header
      *
     C                     WRITEHEADAU
      *
      ** If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ** Or, if no records read, Output "No Details" message.
      *
     C           WWNCAD    IFEQ *ZERO
     C                     WRITENODTLS
     C                     ELSE
      *
      ** Normal audit  exit
      *
     C                     Z-ADDWWNCAD    PPNCAD
     C                     WRITECONTROL
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *INZSR - Initial Processing                                  *
      *                                                               *
      *  Called by: Start of pgm                                      *
      *                                                               *
      *  Calls    : SRRCFA                                            *
      *                                                               *
      *****************************************************************
     C           *INZSR    BEGSR
      *
      **  Copyright statement
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Check multibranching indicator.
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     Z-ADD0         DBASE
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'GL1991'  DBPGM     P
     C                     OUT  LDA
      *
     C           KWFEE     KLIST
     C                     KFLD           KWFETP  2
     C                     KFLD           KWCHGR  2
      *
     C           AGMBIN    IFEQ 'Y'
     C                     MOVE '1'       *IN39
     C                     END
      *
      ** Initialise key and work fields
      *
     C                     Z-ADD0         WWNCAD  50
      *
     C                     EXSR SRRCFA
      *
      ** Access to SDBANKPD to retrieve the MIDAS
      ** Run Date and User Report Title
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '        'PRTCD   7
     C                     PARM '*FIRST  'POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If record not found
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE
     C                     MOVEL*BLANKS   DBFILE
     C                     MOVE 'SDBANKPD'DBFILE
     C                     MOVEL'*FIRST'  DBKEY     P
     C                     MOVEL'GL1991'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Determine Date Format
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     END
      *
      ** Access to SDGELRPD to retrieve Retention Period.
      *
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '        'PRTCD
     C                     PARM '*FIRST  'POPTN
     C           SDGELR    PARM SDGELR    DSSDY
      *
      ** If record not found
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE
     C                     MOVEL*BLANKS   DBFILE
     C                     MOVE 'SDGELRPD'DBFILE
     C                     MOVEL'*FIRST'  DBKEY     P
     C                     MOVEL'GL1991'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
     C                     MOVE 'N'       WWOP1   1
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Standard Data Base Error Processing                 *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
      *
      ** Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFNE 'Y'
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     EXSR SRAUDT
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRRCF1 - Subroutine to register P1 report via RCF            *
      *                                                               *
      *  Called by: Main Processing                                   *
      *                                                               *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
     C           SRRCF1    BEGSR
      *
      ** Ensure report spool file is recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ     5
     C                     PARM *BLANKS   PPARM   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ** Error during ZSFILE processing, return to calling program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRRCFA - Subroutine to register AU report via RCF            *
      *                                                               *
      *  Called by: *INZSR                                            *
      *                                                               *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
     C           SRRCFA    BEGSR
      *
      ** Ensure audit spool file is recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ
     C                     PARM *BLANKS   PPARM
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM           ZSERR
      *
      ** Error during ZSFILE processing, return to calling program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZASIGNZ2
     C/COPY ZSRSRC,ZSEDITZ3
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
