     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL CS fees charges report prompt')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL1992 - Customer Service Fees Charges Report Prompt         *
      *                                                               *
      *  Function:  This program will allow the users to select       *
      *             the fee charges report by status.                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CSD011             Date 04Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD011 - Performance Incentive Fees                          *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
      *
     FGL1992DFCF  E                    WORKSTN      KINFSR *PSSR
      ** Fees Charges report prompt
      *
     F                                              KINFDS INFDS#
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   21  - F3 was pressed                                        *
      *   57  - CSD011 is switched ON                                 *                       CSD011
      *   60  - Error Indicator                                       *
      *   89  - Record not found                                      *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRENTR - Enter key processing                                *
      *  SRSMSG - Send message in Subfile message                     *
      *  *INZSR - Initial Processing                                  *
      *  *PSSR  - Error control subroutine - automatically Called     *
      *                                                               *
      *****************************************************************
      *
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /EJECT
      *
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
     ISCSARD    E DSSCSARDPD                                                                  CSD011
      ** External data structures for Switchable features                                     CSD011
      *                                                                                       CSD011
     IDSFDY     E DSDSFDY
      ** First DS For Access Programs, Short Data Structure
      *
     IPSDS       SDS
      ** Program Status Data Structure
      *
     I                                     *PROGRAM ##PGM
     I                                      244 253 #0WSID
     I                                      254 263 #0USER
      *
     IINFDS#      DS
      ** I/O Feedback Area: Workstation.
      *
     I                                      261 270 SWFMT
      *
      /EJECT
      *****************************************************************
      *                 M A I N   P R O C E S S I N G                 *
      *****************************************************************
      *
     C           *IN21     DOWEQ*OFF
      *
      ** Display Screen.
      *
     C                     WRITEGL1992F0
      *
     C           WWERR     IFEQ 'Y'
     C                     WRITE#MSGCTL
     C                     ENDIF
      *
     C                     READ GL1992F0                 89
      *
      ** Clear screen message file
      *
     C                     CALL 'CLRERMSG'
     C                     PARM ##PGM     ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
     C           *INKC     IFEQ *ON
     C                     MOVE *BLANKS   PSTAT
     C                     MOVE 'E'       PCMD
     C                     MOVE *ON       *IN21
     C                     ELSE
      *
     C                     EXSR SRENTR
     C                     ENDIF
     C                     ENDDO
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      *          E N D   O F   M A I N   P R O C E S S I N G          *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRENTR - Enter Key Processing                                *
      *                                                               *
      *  Called By : Main Processing                                  *
      *  Calls     :                                                  *
      *  SRSMSG - Send message in Subfile message                     *
      *                                                               *
      *****************************************************************
      *
     C           SRENTR    BEGSR
      *
      ** Reset Data Items.
      *
     C                     MOVE 'N'       WWERR   1
     C                     MOVE *OFF      *IN60
      *
      ** Can be 'C' if CSD011 is switched on.                                                 CSD011
      *                                                                                       CSD011
     C           *IN57     IFEQ *OFF                                                          CSD011
     C           #0STAT    IFEQ '*ALL'
     C           #0STAT    OREQ 'U   '
     C           #0STAT    OREQ 'D   '
     C           #0STAT    OREQ 'A   '
     C           #0STAT    OREQ 'G   '
     C           #0STAT    OREQ 'E   '
     C           #0STAT    OREQ 'O   '
     C           #0STAT    OREQ 'S   '
      *
      *
     C                     ELSE
     C                     MOVE '1'       *IN60
     C                     MOVE 'Y'       WWERR
     C                     MOVEL'CSF9992' ZMSID
     C                     EXSR SRSMSG
     C                     ENDIF
      *
     C                     ELSE                                                               CSD011
      *                                                                                       CSD011
     C           #0STAT    IFEQ '*ALL'                                                        CSD011
     C           #0STAT    OREQ 'U   '                                                        CSD011
     C           #0STAT    OREQ 'D   '                                                        CSD011
     C           #0STAT    OREQ 'A   '                                                        CSD011
     C           #0STAT    OREQ 'G   '                                                        CSD011
     C           #0STAT    OREQ 'E   '                                                        CSD011
     C           #0STAT    OREQ 'O   '                                                        CSD011
     C           #0STAT    OREQ 'S   '                                                        CSD011
     C           #0STAT    OREQ 'C   '                                                        CSD011
      *                                                                                       CSD011
      *                                                                                       CSD011
     C                     ELSE                                                               CSD011
     C                     MOVE '1'       *IN60                                               CSD011
     C                     MOVE 'Y'       WWERR                                               CSD011
     C                     MOVEL'CSF9993' ZMSID                                               CSD011
     C                     EXSR SRSMSG                                                        CSD011
     C                     ENDIF                                                              CSD011
     C                     ENDIF                                                              CSD011
      *
     C           WWERR     IFEQ 'N'
     C                     MOVE *ON       *IN21
     C                     MOVE #0STAT    PSTAT
     C                     MOVE ' '       PCMD
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *   SRSMSG - Send message in Subfile message                    *
      *                                                               *
      *   Called by : SRENTR                                          *
      *   Calls     : SNDERMSG                                        *
      *                                                               *
      *****************************************************************
      *
     C           SRSMSG    BEGSR
      *
     C                     CALL 'SNDERMSG'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZMSID   7
     C                     PARM           ZMSGF  10
     C                     PARM           ZMSDA 132
     C                     PARM           ZMSTP   7
      *
      ** Clear all fields for default mechanism next time.
      *
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZMSID
     C                     MOVEL*BLANK    ZMSDA
     C                     MOVEL*BLANK    ZMSTP
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  *INZSR - Initial Processing                                  *
      *                                                               *
      *  Called by : Main processing                                  *
      *  Calls     :                                                  *
      *  *PSSR  - Error control subroutine - automatically Called     *
      *  AOBANKR0                                                     *
      *                                                               *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PLVL    1
     C                     PARM           PENT    3
     C                     PARM           PSTAT   4
     C                     PARM           PACT    1
     C                     PARM           PCMD    1
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Clear screen message file
      *
     C                     CALL 'CLRERMSG'
     C                     PARM ##PGM     ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
      *
      ** Set Initial values.
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Access Bank details file
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Data base error in file (file)
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVEL'*FIRST'  DBKEY
     C                     MOVEL'GL1992'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE BJMRDT    #0MRDT
      *
      ** Cleanup values from previous call.
      *
     C                     MOVE *BLANK    PSTAT
     C                     MOVE *OFF      *IN21
     C                     MOVE *OFF      *IN60
     C                     MOVE *OFF      *INKC
      *
      * If CSD011 is switched on, seton indicator to print Calculation                        CSD011
      * fields on report.                                                                     CSD011
     C                     CALL 'AOSARDR0'                                                    CSD011
     C                     PARM *BLANKS   PRTCD   7                                           CSD011
     C                     PARM '*KEY   ' POPTN   7                                           CSD011
     C                     PARM 'CSD011'  PSARD   6                                           CSD011
     C           SCSARD    PARM SCSARD    DSFDY                                               CSD011
     C           PRTCD     IFEQ *BLANKS                                                       CSD011
     C                     MOVE *ON       *IN57                                               CSD011
     C                     ELSE                                                               CSD011
     C                     MOVE *OFF      *IN57                                               CSD011
     C                     ENDIF                                                              CSD011
     C                     MOVEL'GLUSRMSG'ZMSGF
     C                     MOVEL'*ALL'    #0STAT
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine - automatically Called or  *
      *                                                               *
      *  Called By : *INZSR                                           *
      *  Calls     : DBERRCTL                                         *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
      ** Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     MOVE 'E'       PCMD
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
