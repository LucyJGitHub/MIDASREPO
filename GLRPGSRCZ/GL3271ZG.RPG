     H
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Generate Extension File')                        *
     F*****************************************************************
     F*                                                               *
     F*  Midas   General Ledger Module
     F*                                                               *
     F*  GL3271ZG -  A/C DETAILS - GENERATE EXTENSION FILE            *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. CER050             Date 16Jun19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CGL165             Date 17Feb15               *
      *                 CER055             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 239720             Date 11May06               *
      *                 238639             Date 27Feb06               *
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CPK015             Date 28Jun02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CDE002             Date 21Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD005             Date 29Mar99               *
      *                 CTI002             Date 23Sep98               *
     F*                 CRE001             Date 18Feb97               *
     F*                 S01413 *CREATE     Date 13Apr93               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  CER050 - Annualised Percentage Rate (Recompile)              *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
     F*  CER055 - Penalty Interest on Exceeding Overdraft Limit       *
     F*           (Recompile)                                         *
     F*  239720 - No extension is needed if account is closed as      *
     F*           customer may also be closed (DBE AT 003)            *
     F*         - the program should be used to create missing        *
     F*           extensions ==> CHAIN to see if record already exists*
      *         - applied 238639.                                     *
     F*  238639 - No extension is needed if account is closed as      *
     F*           customer may also be closed (DBE AT 003)            *
     F*         - the program should be used to create missing        *
     F*           extensions ==> CHAIN to see if record already exists*
      *         - applied 231128                                      *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CPK015 - Release 4.01 Packaging:                             *
      *           - A default timestamp string is required for WRITE. *
     F*  CDE002 - Revenue Analysis - Recompiled due to changes in     *
     F*           PF/ACCNTBXC.                                        *
     F*  CSD005 - Standing Data Account Narrative.  Add new field     *
     F*           to SDACODPD.  Recompilation of programs.            *
     F*  CTI002 - Recompiled : File SDACODPD changed                  *
     F*  CRE001 - Overdraft Interest Calculations:                    *
     F*           Recompiled only.                                    *
     F*  S01413 - Retail 3 Incorporation                              *
     F*                                                               *
     F*****************************************************************
     F*  Use of indicators:                                           *
     F*                                                               *
     F*  *IN01  -  First Cycle                                        *
     F*  *IN68  -  Output error indicator.                            *
     F*  *INLR  -  End processing                                     *
     F*                                                               *
     F*---------------------------------------------------------------*
     FACCNTAB IPE E                    DISK
     F** Prefix : --
     F                                              KINFSR *PSSR
     F***ACCNTBXCO***E                    DISK                                                238639
     FACCNTBXCUF  E           K        DISK                      A                            238639
     F** Prefix : AT
     F                                              KINFSR *PSSR
     FTABTBRE IF  E                    DISK                                                   238639
     FGL3271ZPO   E                    PRINTER
     F** Prefix : RR
     E/EJECT
     E*
     E** Copyright Array
     E*
     E                    STAMP   1   1 26                                                    CPK015
      ** Array containing dummy timestamp                                                     CPK015
     E                    CPY@    1   1 80               COPYRIGHT
     I/EJECT
     I*-----------------------------------------------------------------
     ILDA         DS                            256
     I*
     I** Data structure for data-base error processing.
     I*
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     ISDBANK    E DSSDBANKPD
     I*
     I** Bank Details
     I*
     ISDCUST    E DSSDCUSTPD
     I*
     I** Customer Details
     I*
     ISDACOD    E DSSDACODPD
     I*
     I** Account Code Details
     I*
     IDSFDY     E DSDSFDY
     I*
     I** First DS for access programs, short data structure
     I*
     IDSSDY     E DSDSSDY
     I*
     I** Second DS for access programs, long data structure
     I*
     C/EJECT
     C*-----------------------------------------------------------------
     C* Main processing
     C*-----------------------------------------------------------------
     C*
     C** Copyright Statement
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C**   If it's first cycle
     C*
     C           *IN01     IFEQ '0'                        B001
     C                     MOVE '1'       *IN01
     C*
     C**   Prepare LDA
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'GL3271ZG'DBPGM
     C                     OUT  LDA
     C*
     C** Get Bank Details
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 01 *
     C                     MOVEL1         DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE                            X002
     C                     MOVE BJMRDT    RRMRDT
     C                     MOVE BJURPT    RRURPT
     C                     END                             E002
     C*
     C           1         SETLLTABTBRE                                                       238639
     C                     READ TABTBRE                  70                                   238639
     C                     END                             E001
     C*
     C           RECI      IFEQ 'D'
     C***********RECI      OREQ 'C'                                                           238639
     C*
     C** Get Account Code Details
     C*
     C                     MOVELACOD      @ACOD
     C*
     C                     CALL 'AOACODR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C**********           PARM           @ACOD   4                                           CGL029
     C                     PARM           @ACOD  10                                           CGL029
     C           SDACOD    PARM SDACOD    DSSDY
     C*
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE            ***************
     C                     MOVEL'SDACODPD'DBFILE           * DB ERROR 04 *
     C                     MOVELACOD      DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C           A5ACTY    IFEQ 'R'
     C*
     C** Get Customer Details
     C*
     C                     MOVELCNUM      @CUST
     C*
     C                     CALL 'AOCUSTR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @CUST  10
     C                     PARM           @CUSTS  7
     C           SDCUST    PARM SDCUST    DSSDY
     C*
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE            ***************
     C                     MOVEL'SDCUSTPD'DBFILE           * DB ERROR 03 *
     C                     MOVELCNUM      DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C**   Initialise output fields of Extended file
     C*
     C******               ADD  1         RRWRIT  70                                          238639
     C**********           Z-ADDCNUM      ATCNUM                                              CSD027
     C                     MOVE CNUM      ATCNUM                                              CSD027
     C                     MOVELCCY       ATCCY
     C                     Z-ADDACOD      ATACOD
     C                     Z-ADDACSQ      ATACSQ
     C                     MOVELBRCA      ATBRCA
     C                     Z-ADD*ZEROS    ATCRST
     C                     MOVE *BLANKS   ATSUBO
     C***********          MOVEL'Y'       ATSTCI                                              238639
     C                     MOVELTSTMC     ATSTCI                                              238639
     C                     MOVE *BLANKS   ATCHIN
     C                     Z-ADD*ZERO     ATCHAM
     C           BBTAIN    IFEQ *BLANK
     C                     MOVE 'N'       ATITAX
     C                     ELSE
     C                     MOVE BBTAIN    ATITAX
     C                     END
     C*
     C**   Write into Output file
     C*
     C                     MOVEASTAMP,1   ATTMST           Default Timestamp                  CPK015
     C           EXCHN     KLIST                                                              238639
     C                     KFLD           ATCNUM                                              238639
     C                     KFLD           ATCCY                                               238639
     C                     KFLD           ATACOD                                              238639
     C                     KFLD           ATACSQ                                              238639
     C                     KFLD           ATBRCA                                              238639
      *                                                                                       238639
     C           EXCHN     CHAINACCNTBD0             90                                       238639
     C           *IN90     IFEQ '1'                                                           238639
     C                     WRITEACCNTBD0               68
     C                     ADD  1         RRWRIT  70                                          238639
     C                     END                                                                238639
     C*
     C**   If the Write fails, then it is a data base error
     C*
     C           *IN68     IFEQ '1'                        B001
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVEL'ACCNTBXC'DBFILE           * DB ERROR 02 *
     C                     MOVELCNUM      DBKEY            ***************
     C                     MOVE CCY       DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END                             E001
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C**   If End Cycle
     C*
     CLR                   WRITEHEADAU
     CLR                   WRITECONTROL
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* *PSSR    - Data base Error Processing                         *
     C*                                                               *
     C* Calls:                                                        *
     C*                                                               *
     C* Called from:  MAIN                                            *
     C*                                                               *
     C* Routine uses variables:                                       *
     C*                                                               *
     C*****************************************************************
     C           *PSSR     BEGSR
     C*
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
     C*
     C                     SETON                     LRU7U8
     C                     WRITEHEADAU
     C                     WRITEERRORAU
      *
     C                     ENDSR
** STAMP - Dummy timestamp                                                                    CPK015
0001-01-01-00.00.00.000000                                                                    CPK015
** CPY@ OBJECT COPYRIGHT
(c) Finastra International Limited 2001
