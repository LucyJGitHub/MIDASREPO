     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CCB023
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  GL3310 - Unauthorised Collateral Items Processing and Report *
      *                                                               *
      *  Function:  This program will process all collateral          *
      *  (ICT)      transactions for the day. If any are found which  *
      *             have not been authorised, a report will be        *
      *             produced and the user will be informed of the     *
      *             error condition and propted to re-open the I/C.   *
      *                                                               *
      *  Called By: GLC3310 - Unauthorised Collateral Details Report  *
      *                             Control                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CCB023 *REDUNDANT  Date 06Aug12               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 BUG16354           Date 29Feb08               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CGL018             Date 30Sep02               *
      * Midas Release 4 ---------- Base ------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CGL012  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCB023 - COB Restructure - Input Cycle Termination           *
      *           Restructuring                                       *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  BUG16354 - Printer overflow                                  *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL018 - Risk Weighted Assets                                *
      *  CGL012 - Collateral Authorisation                            *
      *                                                               *
      *****************************************************************
     FGLCOLLLAIF  E           K        DISK         KINFSR *PSSR
     FGL3310P1O   E                    PRINTER
     F                                              KINFDS SPOOL1
     F*                                                                   CGL018
     FGLORWAL0IF  E           K        DISK                               CGL018
      * RTW: Collateral Risk Weigted Assets Code/Set                      CGL018
      *                                                                   CGL018
     F*                                                                   CGL018
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *                   Function of Indicators                      *
      *                  ------------------------                     *
      *                                                               *
      *  89   Read Indicator                                          *
      *  98   Date Format Indicator                                   *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
     E                    XRWA       90  5               Work Array       CGL018
     E/COPY ZSRSRC,ZFRPEDZ1
     E/COPY ZSRSRC,ZDATE2Z1
      *****************************************************************
      /SPACE 3
      *****************************************************************
     ISPOOL1      DS
      ** File Information Data Structure for GL3310P1.
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
     ISDBANK    E DSSDBANKPD
      ** External data structure for Bank Details
      *
     ISDCURR    E DSSDCURRPD
      ** External data structure for Currency Details
      *
     IDSFDY     E DSDSFDY
      ** Short data structures for Access Objects
      *
     IDSSDY     E DSDSSDY
      ** Long data structure for Access Objects
      *
     I*                                                                   CGL018
     ISCSARD    E DSSCSARDPD                                              CGL018
     I** MIDAS Switchable Features Accessed Via Access Program            CGL018
     I*                                                                   CGL018
      * Datastructure to output RWA details                               CGL018
      *                                                                   CGL018
     IRWSECO      DS                          1   5                       CGL018
     I                                        1   2 #RCODE                CGL018
     I I            ' '                       3   3 #BLANK                CGL018
     I                                        4   4 #RSET                 CGL018
     I I            ','                       5   5 #COMMA                CGL018
      *                                                                   CGL018
     I/COPY ZSRSRC,ZFRPEDZ2
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRINIT - Initial Program Processing                          *
      *  SRPROC - Main Processing                                     *
      *  SRCURR - Access Currency Details                             *
      *  SRTERM - End Processing                                      *
      *  *PSSR  - Standard DB Error Processing                        *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      * Main Program                                                  *
      *****************************************************************
      *
     C                     EXSR SRINIT
      *
     C                     EXSR SRPROC
      *
     C                     EXSR SRTERM
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SRINIT - Initial Program Processing                           *
      *                                                               *
      * Called by: Main Program                                       *
      *                                                               *
      * Calls: *PSSR, ZSFILE                                          *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Receive parameter list
      *
     C           *ENTRY    PLIST
     C                     PARM           PERR    1
     C                     PARM           PSEQ    5
      *
      ** Initialise LDA field
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'GL3310'  DBPGM
     C                     OUT  LDA
      *
      ** Access bank ICD details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE            ************
     C                     MOVEL'SDBANKPD'DBFILE           * DBERR 01 *
     C                     OUT  LDA                        ************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ** Ensure report spool file recorded by RCF
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ** If error occurs during ZSFILE processing, then return to the
      ** calling program
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *                                                                   CGL018
      ***  Determine if switchable feature CGL018 is activated            CGL018
      *                                                                   CGL018
     C                     CALL 'AOSARDR0'                                CGL018
     C                     PARM *BLANKS   PRTCD   7                       CGL018
     C                     PARM '*VERIFY' POPTN   7                       CGL018
     C                     PARM 'CGL018'  PSARD   6                       CGL018
     C           SCSARD    PARM SCSARD    DSFDY                           CGL018
      *                                                                   CGL018
     C           PRTCD     IFEQ *BLANK                                    CGL018
     C                     MOVE 'Y'       CGL018  1                       CGL018
     C                     SETON                     81                   CGL018
     C                     ELSE                                           CGL018
     C                     MOVE 'N'       CGL018                          CGL018
     C                     SETOF                     81                   CGL018
     C                     ENDIF                                          CGL018
      *                                                                   CGL018
      *
      ** Report Work fields
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SRPROC - Main Processing                                      *
      *                                                               *
      * Called by: Main Program                                       *
      *                                                               *
      * Calls: ZDATE2, ZFRPED, SRCURR                                 *
      *                                                               *
      *****************************************************************
      *
     C           SRPROC    BEGSR
      *
     C                     MOVE *OFF      *IN89
      *
     C                     READ GLCOLLLA                 89
      *
      ** Do While Not EOF
      *
     C           *IN89     DOWEQ*OFF
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
      *
     C**********           Z-ADD1         RQDLN1                          BUG16354
     C                     Z-ADD3         RQDLN1                          BUG16354
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
     C                     MOVE CDCREF    RCREF
     C                     MOVE CDENTY    RENTY
     C                     MOVE CDCMPY    RCMPY
     C                     MOVE CDBRCA    RBRCA
     C                     MOVE CDCNUM    RCNUM
     C                     MOVE CDCOLT    RCOLT
     C                     MOVE CDCCY     RCCY
     C                     MOVE CDIUSR    RIUSR
     C                     MOVE CDAUSR    RAUSR
      *
      ** Set up signing date
      *
     C                     MOVE CDSDAT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     RSDAT
      *
      ** Set up value date
      *
     C                     MOVE CDVDAT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     RVDAT
      *
      ** Set up end date
      *
     C           CDEDAT    IFNE 0
     C                     MOVE CDEDAT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     REDAT
     C                     ELSE
     C                     MOVE *BLANKS   REDAT
     C                     ENDIF
      *
     C                     EXSR SRCURR
      *
     C                     MOVE CDLAMT    ZFLD15
     C                     MOVE A6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    RLAMT
      *                                                                   CGL018
      * Output RWA details for each collateral                            CGL018
      *                                                                   CGL018
     C           CGL018    IFEQ 'Y'                                       CGL018
     C                     EXSR RWADTL                                    CGL018
     C                     ENDIF                                          CGL018
      *                                                                   CGL018
      *
     C                     WRITEDETAIL
     C                     MOVE *ON       WWORK   1
      *
     C                     READ GLCOLLLA                 89
      *
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SRCURR - Access Currency Details                              *
      *                                                               *
      * Called by: SRMAIN                                             *
      *                                                               *
      * Calls: *PSSR                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRCURR    BEGSR
      *
      ** Access currency details file
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM CDCCY     @CYCD   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD002       DBASE            ************
     C                     MOVEL'SDCURRPD'DBFILE           * DBERR 02 *
     C                     OUT  LDA                        ************
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SRTERM - End Processing                                       *
      *                                                               *
      * Called by: SRMAIN                                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRTERM    BEGSR
      *
     C           WWORK     IFEQ *ON
     C                     MOVE 'E'       PERR
     C                     WRITETRAILP1
     C                     ENDIF
      *
     C           WWORK     IFEQ *OFF
     C                     MOVE *BLANK    PERR
     C                     ENDIF
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: SRINIT, SRCURR                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      * RWA       : Output RWA information from GLORWAPD                * CGL018
      * ---                                                             * CGL018
      * Called By : CAPRDT                                              * CGL018
      * Calls     : None                                                * CGL018
      ******************************************************************* CGL018
     C           RWADTL    BEGSR                                          CGL018
      *                                                                   CGL018
     C           RCREF     SETLLGLORWAD0                                  CGL018
     C           RCREF     READEGLORWAD0                 97               CGL018
     C                     MOVE *BLANKS   XRWA                            CGL018
     C                     MOVEL*BLANKS   RWASE                           CGL018
     C                     MOVEL*BLANKS   RWASEC                          CGL018
     C                     MOVEL*BLANKS   #CONTI                          CGL018
     C                     Z-ADD0         U       20                      CGL018
     C           *IN97     DOWEQ'0'                                       CGL018
      *                                                                   CGL018
     C                     ADD  1         U                               CGL018
     C                     MOVE RWCODE    #RCODE                          CGL018
     C                     MOVE RWSETN    #RSET                           CGL018
      *                                                                   CGL018
     C           U         IFGT 18                                        CGL018
     C           #CONTI    ANDNE'C'                                       CGL018
     C                     Z-ADD1         U                               CGL018
     C                     MOVEL'C'       #CONTI  1                       CGL018
     C                     MOVEL*BLANKS   RWASE                           CGL018
     C                     MOVEAXRWA      RWASE                           CGL018
     C                     MOVE *BLANKS   XRWA                            CGL018
     C                     ENDIF                                          CGL018
      *                                                                   CGL018
     C                     MOVEARWSECO    XRWA,U                          CGL018
      *                                                                   CGL018
     C           RCREF     READEGLORWAD0                 97               CGL018
     C                     ENDDO                                          CGL018
      *                                                                   CGL018
     C           U         IFEQ 0                                         CGL018
     C                     MOVEL'None'    RWASE                           CGL018
     C                     ELSE                                           CGL018
      *                                                                   CGL018
      * Last one must be without comma, so clear and output again         CGL018
      *                                                                   CGL018
     C                     MOVEL*BLANKS   #COMMA                          CGL018
     C                     MOVEARWSECO    XRWA,U                          CGL018
      *                                                                   CGL018
     C           #CONTI    IFEQ 'C'                                       CGL018
     C                     MOVEAXRWA      RWASEC                          CGL018
     C                     ELSE                                           CGL018
     C                     MOVEAXRWA      RWASE                           CGL018
     C                     ENDIF                                          CGL018
      *                                                                   CGL018
     C                     MOVEL','       #COMMA                          CGL018
     C                     ENDIF                                          CGL018
      *                                                                   CGL018
     C                     ENDSR                                          CGL018
      ******************************************************************* CGL018
     C/COPY ZSRSRC,ZFRPEDZ3
     C/COPY ZSRSRC,ZDATE2Z2
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
