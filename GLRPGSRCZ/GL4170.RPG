      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Daily Balance Proof Extract File')
     F********************************************************************
     F*                                                                  *
     F*  Midas GENERAL LEDGER MODULE
     F*                                                                  *
     F*    GL4170 - DAILY BALANCE PROOF EXTRACT FILE                     *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CGL007             Date 26Mar99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01359             Date 03Mar92               *  *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL007 - Account Postings Enquiry                            *
     F*     S01359 - DAILY BALANCE PROOF REPORT                          *
     F*                                                                  *
     F********************************************************************
     F***OACCNT* IP  F     400 15AI     2 DISK                                                CGL029
     FOACCNT  IP  F     573 21AI     2 DISK                                                   CGL029
     F***EODPO***IS  F     160 15AI     2 DISK                            CGL007
     F***EODPO** IS  F     331 15AI     2 DISK                                         CGL007 CGL029
     FEODPO   IS  F     620 21AI     2 DISK                                                   CGL029
     FTABLE   IF  F      96 12AI     2 DISK
     FGLAUDTAPO   F      36            DISK
     F***GLAUDTDPO   F      36            DISK                                                CGL029
     FGLAUDTDPO   F      43            DISK                                                   CGL029
     FGLAUDTTPO   F      36            DISK
     FGL4170AUO   F     132            PRINTER
     F/SPACE 2
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F* 01            OACCNT - DETAIL RECORD - OPEN ACCOUNT
     F* 02                                  - CLOSED ACCOUNT
     F* 03                                  - DELETED ACCOUNT
     F* 04                  - HEADER
     F* 05                  - TRAILER
     F* 06                  - CATCHALL
     F* 07            EODPO - DETAIL RECORD - ACCOUNT POSTING
     F* 08                                  - RATE CHANGE
     F* 09                  - HEADER
     F* 10                  - TRAILER
     F* 11                  - CATCHALL
     F*       20      FIRST CYCLE PERFORMED
     F*       21      LEDGER BALANCE IS A CREDIT
     F*       30      OACCNT RECORD
     F*       40      EODPO RECORD
     F*       60      OUTPUT HEADER & TRAILER RECORDS
     F*       51      DIFFERENCE - NUMBER OF ACCOUNTS
     F*       52                 - NUMBER OF POSTINGS
     F*       53                 - ACCOUNT DEBITS VALUE
     F*       54                 - ACCOUNT CREDITS VALUE
     F*       55                 - ACCOUNT POSTINGS VALUE
     E*
     E                    CPY@    1   1 80
     I*
     I*    ACCOUNTS MASTER FILE - HEADER RECORD.
     I*
     IOACCNT  NS  04   1 CH
     I                                        1   1 RECI
     I                                       23  23 ORDI
     I                                    P  24  260FLSZ
     I*
     I*
     I*    ACCOUNTS MASTER FILE - DETAIL RECORD.
     I*
     I        NS  01   1 CD
     I       OR   02   1 CC
     I       OR   03   1 C*
     I                                        1   1 RECI
     I**********                              2   70CNUM                                      CSD027
     I                                        2   7 CNUM                                      CSD027
     I                                        8  10 CCY
     I**********                             11  140ACOD                                      CGL029
     I                                       11  140ACODQQ                                    CGL029
     I                                       15  160ACSQ
     I                                       23  25 BRCA
     I                                       26  26 ATYP
     I                                       27  27 STYP
     I                                       29  29 ACST
     I                                    P  30  320DACO
     I                                    P  33  350DACC
     I                                       36  55 ANAM
     I                                    P  56  630LDBL
     I                                    P  64  710CLBL
     I                                       72  72 STFQ
     I                                       73  740STDY
     I                                    P  75  770LSTD
     I                                    P  78  800NSTD
     I                                    P  81  820LSTP
     I                                    P  83  900CFSB
     I                                    P  91  930LMVD
     I                                    P  94 1010EPLB
     I                                    P 102 1090EPCB
     I                                    P 110 1170YTDB
     I                                    P 110 1170CFCB
     I                                      118 118 GLBT
     I                                    P 138 1400DLIM
     I                                    P 141 1430LRCR
     I                                    P 144 1510RTCB
     I                                    P 152 1590RTLB
     I                                    P 160 1660DITM
     I                                    P 167 1730CITM
     I                                    P 178 1837CDIR
     I                                    P 184 1897CCIR
     I                                    P 178 1837CDIR
     I                                    P 184 1897CCIR
     I                                      190 1990ACNO
     I                                      200 200 RETB
     I                                      201 201 ODSC
     I                                    P 202 2070ODLN
     I                                    P 208 2100ODED
     I                                    P 211 2120DIOD
     I                                      213 2150FACT
     I                                      216 2170FCNO
     I                                      218 219 EFAC
     I                                    P 220 2270HELD
     I                                    P 228 2330MINB
     I                                    P 234 2360LACD
     I                                    P 237 2380LISP
     I                                    P 239 2410RRNM
     I**********                            242 2430DRIB                                      CSD103
     I                                      242 243 DRIB                                      CSD103
     I                                    P 244 2497DRIS
     I                                    P 250 2520DRCD
     I                                      253 2530DRIC
     I                                      254 254 DRIF
     I                                      255 2560DRDY
     I                                    P 257 2590NDID
     I                                    P 260 2620LDID
     I                                    P 263 2690LDIA
     I                                      270 270 SDAP
     I                                    P 271 2730LPAR
     I                                    P 274 2800DIIE
     I                                    P 281 2880DAIC
     I                                    P 289 2950MADI
     I                                    P 296 3020GADI
     I                                      303 303 DDIS
     I                                    P 304 3060DDIA
     I                                    P 307 3090OLDR
     I**********                            310 3110OLDB                                      CSD103
     I                                      310 311 OLDB                                      CSD103
     I                                    P 312 3177OLDS
     I**********                            318 3190CRIB                                      CSD103
     I                                      318 319 CRIB                                      CSD103
     I                                    P 320 3257CRIS
     I                                    P 326 3280CRCD
     I                                      329 3290CRIC
     I                                      330 330 CRIF
     I                                      331 3320CRDY
     I                                    P 333 3350NCID
     I                                    P 336 3380LCID
     I                                    P 339 3450LCIA
     I                                      346 346 SCAP
     I                                    P 347 3490LPAP
     I                                    P 350 3560CIIE
     I                                    P 357 3640CAIC
     I                                    P 365 3710MACI
     I                                    P 372 3780GACI
     I                                      379 379 CDIS
     I                                    P 380 3820DCIA
     I                                    P 383 3850OLCR
     I**********                            386 3870OLCB                                      CSD103
     I                                      386 387 OLCB                                      CSD103
     I                                    P 388 3937OLCS
     I                                    P 394 3960LCD
     I                                      397 397 CHTP
     I                                    P 398 4000TNLU
     I                                      560 5690ACOD                                      CGL029
     I*
     I*
     I*    ACCOUNTS MASTER FILE - TRAILER RECORD.
     I*
     I        NS  05   1 CT
     I                                        1   1 RECI
     I                                    P  23  250NORE
     I                                    P  29  310NOOA
     I                                    P  32  340NINU
     I                                    P  35  370NOAM
     I                                    P  38  400NOCL
     I                                    P  41  430NOAU
     I                                    P  44  460NINP
     I                                    P  47  490NORD
     I                                    P  50  520NOCF
     I                                    P  53  550NAMP
     I                                    P  56  580NAUP
     I                                    P  59  660HWND
     I                                    P  67  680HDCD
     I                                    P  69  760HWNC
     I                                    P  77  780HDCC
     I                                    P 395 3970LCD
     I                                    P 398 4000TNLU
     I**
     I**   OACCNT - CATCHALL
     I**
     I        NS  06
     I*
     I*    ACCOUNTS POSTING FILE - HEADER RECORD.
     I*
     IEODPO   NS  09   1 CH
     I                                        1   1 RECI
     I                                    P   2   40FLSZ
     I*
     I*    ACCOUNTS POSTING FILE - ACCOUNT POSTING RECORD.
     I*
     I        NS  07   1 CP
     I                                        1   1 RECI
     I**********                              2   70CNUM                                      CSD027
     I                                        2   7 CNUM                                      CSD027
     I                                        8  10 CCY
     I**********                             11  140ACOD                                      CGL029
     I                                       11  140ACODQQ                                    CGL029
     I                                       15  160ACSQ
     I                                       23  320ACNO
     I                                    P  33  350PSTD
     I                                    P  36  380VALD
     I                                       39  43 TRAT
     I                                       44  73 PNAR
     I                                    P  74  800PSTA
     I                                       81  810DRCR
     I**********                             82  870ASOC                                      CSD027
     I                                       82  87 ASOC                                      CSD027
     I                                       88  950CHQN
     I                                       96 102 SPOS
     I                                      103 105 BRCA
     I                                      106 1060RIND
     I                                    P 107 1080REJC
     I                                      109 111 DPMT
     I                                    P 112 1140RRNM
     I                                      124 124 ATYP
     I                                      128 128 PRIN
     I                                      129 129 GETP
     I                                      130 1300ACUM
     I                                      161 1700ACOD                                      CGL029
     I**********                              1 160 LOT                                       CGL029
     I                                        1 170 LOT                                       CGL029
     I*
     I*
     I*    ACCOUNTS POSTING FILE - RATE CHANGE POSTING RECORD.
     I*
     I        NS  08   1 CR
     I                                        1   1 RECI
     I**********                              2   70CNUM                                      CSD027
     I                                        2   7 CNUM                                      CSD027
     I                                        8  10 CCY
     I**********                             11  140ACOD                                      CGL029
     I                                       11  140ACODQQ                                    CGL029
     I                                       15  160ACSQ
     I                                       23  320ACNO
     I                                    P  33  350PSTD
     I                                    P  36  380VALD
     I                                       39  43 TRAT
     I                                       44  73 PNAR
     I                                       96 102 SPOS
     I                                      103 105 BRCA
     I                                      124 124 ATYP
     I                                      128 128 PRIN
     I                                      153 1540BRT
     I                                       81  810DRCR
     I                                      161 1700ACOD                                      CGL029
     I**********                              1 160 LOT                                       CGL029
     I                                        1 170 LOT                                       CGL029
     I*
     I*
     I*    ACCOUNTS POSTING FILE - TRAILER RECORD.
     I*
     I        NS  10   1 CT
     I                                        1   1 RECI
     I                                        2   60NOREPO
     I                                    P   7  140HRWN
     I                                    P  15  160HRDC
     I**
     I**   EODPO - CATCHALL
     I**
     I        NS  11
     I*
     I*    TABLE FILE - INSTALLATION CONTROL DATA RECORD 1.
     I*
     ITABLE   NS       2 C0   3 C1  12 C1
     I       AND      13 C0
     I                                        1   1 RECI
     I                                        2   30RECT
     I                                       12  130RCDE
     I                                       14  66 TITL
     I                                       67  67 DATF
     I                                       68  700DFBR
     I                                       74  74 MODI1
     I                                       75  75 MODI2
     I                                       76  76 MODI3
     I                                    P  77  790RUND
     I                                       80  86 RUNA
     C   20                GOTO NOTFC
     C********************************************************************
     C**   FIRST CYCLE PROCESSING
     C**   ----------------------
     C*
     C** SETUP COPYRIGHT STATEMENT
     C*
     C                     MOVEACPY@      BIS@   80
     C                     SETON                     20
     C**
     C**   OBTAIN ICDR 1
     C                     EXSR ZSYSTM
     C   99                SETON                     U7U8LR
     C   99                GOTO END
     C**
     C**   SET UP WORK FIELDS
     C                     Z-ADD36        FLSZ    50
     C**
     C********************************************************************
     C**   DETAIL PROCESSING
     C*    -----------------
     C**
     C           NOTFC     TAG                             ** NOTFC TAG **
     C   06
     COR 11                GOTO END
     C**
     C*    IF ACCOUNTS MASTER RECORD, PERFORM BALANCE PROCESSING
     C   01
     COR 02
     COR 04
     COR 05                EXSR BAL
     C**
     C**   IF POSTING RECORD, PERFORM POSTING PROCESSING
     C   07
     COR 08
     COR 09
     COR 10                EXSR POST
     C**
     C           END       TAG                             *** END TAG ***
     C**
     C********************************************************************
     C**   LAST RECORD PROCESSING
     C**   ----------------------
     C**
     CLR U7                GOTO ENDLR
     CLR         NORE      ADD  NINU      WORK    50
     CLR                   ADD  NINP      WORK
     CLR         WORK      COMP NACCS                5151
     CLR         NOREPO    COMP NPOST                5252
     CLR         HWND      COMP INTDR                5353
     CLRN53      HDCD      COMP DECDR                5353
     CLR         HWNC      COMP INTCR                5454
     CLRN54      HDCC      COMP DECCR                5454
     CLR         HRWN      COMP INTP                 5555
     CLRN55      HRDC      COMP DECP                 5555
     CLR 51
     COR 52
     COR 53
     COR 54
     COR 55                SETON                     U8
     C**
     C**   OUTPUT THE HEADER AND TRAILER RECORDS
     CLR         RCOUT     ADD  2         RCOUT
     CLR                   SETON                     60
     CLR                   EXCPT
     CLR         ENDLR     TAG                             ** ENDLR TAG **
     C**
     C********************************************************************
     C/SPACE 2
     C********************************************************************
     C**   SUBROUTINE TO PROCESS THE ACCOUNTS MASTER RECORDS
     C**
     C           BAL       BEGSR
     C           NACCS     ADD  1         NACCS   50
     C  N01                GOTO ENDB
     C**
     C           LDBL      DIV  1000      ZZAMT        31
     C   31                Z-SUBZZAMT     ZZAMT
     C                     SETON                     30
     C                     EXSR HASH
     C                     SETOF                     3031
     C**
     C           ENDB      ENDSR                           ** ENDB TAG **
     C**
     C********************************************************************
     C**   SUBROUTINE TO PROCESS THE ACCOUNTS POSTING RECORDS
     C**
     C           POST      BEGSR
     C           NPOST     ADD  1         NPOST   50
     C  N07                GOTO ENDP
     C**
     C                     MOVE PSTA      ZZAMT
     C                     SETON                     40
     C                     EXSR HASH
     C                     SETOF                     40
     C**
     C           ENDP      ENDSR                           ** ENDP TAG **
     C**
     C********************************************************************
     C**   SUBROUTINE TO MAINTAIN THE HASH TOTALS
     C**
     C           HASH      BEGSR
     C**
     C   30N31             Z-ADDINTDR     ZZTOTI
     C   30N31             Z-ADDDECDR     ZZTOTD
     C   30 31             Z-ADDINTCR     ZZTOTI
     C   30 31             Z-ADDDECCR     ZZTOTD
     C   40                Z-ADDINTP      ZZTOTI
     C   40                Z-ADDDECP      ZZTOTD
     C**
     C                     EXSR GLZADD
     C**
     C   30N31             Z-ADDZZTOTI    INTDR  150
     C   30N31             Z-ADDZZTOTD    DECDR   30
     C   30 31             Z-ADDZZTOTI    INTCR  150
     C   30 31             Z-ADDZZTOTD    DECCR   30
     C   40                Z-ADDZZTOTI    INTP   150
     C   40                Z-ADDZZTOTD    DECP    30
     C   40                Z-ADDPSTA      PSTAX  150
     C**
     C**   OUTPUT THE RECORD TO THE EXTRACT FILE
     C                     EXCPT
     C           RCOUT     ADD  1         RCOUT   50
     C                     ENDSR
     C**
     C********************************************************************
     C**
     C**   ZSYSTM SR. TO CHAIN TO THE INSTALLATION CONTROL DATA RECORD.
     C**
     C           ZSYSTM    BEGSR                           *** ZSYSTM ***
     C**
     C**   SET UP KEY FOR INSTALLATION CONTROL RECORD.
     C                     MOVEL'01      'ZTABKY 12
     C                     MOVE '  10'    ZTABKY
     C**
     C           ZTABKY    CHAINTABLE                99    ON NOT THERE
     C  N99      RECI      COMP 'D'                  9999  ON, IS DELETED
     C**
     C**   CHECK SYSTEM DATE FORMAT, DDMMYY OR MMDDYY.
     C           DATF      COMP 'M'                      98MMDDYY, 98 ON
     C**
     C                     ENDSR
     C**
     C**
     C********************************************************************
     C* SUBROUTINE TO ADD AN AMOUNT (ZZAMT) TO THE TOTAL (ZZTOTI,ZZTOTD)
     C*   IND '99' IS SET BY AN OVERFLOW ERROR
     C*
     CSR         GLZADD    BEGSR                           *** GLZADD ****
     C*
     CSR                   Z-ADDZZAMT     ZZAMT  153     91-DEFINE ZZAMT
     CSR 91                GOTO ZZAEND                     AMT = 0.BYPASS
     C*
     C* SPLIT ZZAMT INTO INTEGER AND DECIMAL FIELDS
     CSR                   Z-ADDZZAMT     ZZAMTI 150       INTEGER FIELD
     CSR                   MOVE ZZAMT     ZZAMTD  30       DECIMAL FIELD
     C* BOTH ZZAMTI AND ZZAMTD CONTAIN A 'SIGN' ZONE NOW
     C*
     CSR                   EXSR GLZSUM
     CSR         ZZAEND    ENDSR                           ** ZZAEND TAG *
     C*
     C********************************************************************
     C* SUBROUTINE TO CARRY OUT THE ADDITION FOR SUBROUTINES -
     C*                        GLZADD, GLZSUB, GLZCMP.
     C*          PARAMETERS PASSED -
     C*                     I/P ZZAMTI ZZAMTD
     C*                     O/P ZZTOTI ZZTOTD ZZNEGD IND 96 IND 99.
     C*
     CSR         GLZSUM    BEGSR                           *** GLZSUM ****
     C*
     CSR                   Z-ADDZZTOTI    ZZTOTI 150       DEFINE ZZTOTI
     CSR                   Z-ADDZZTOTD    ZZTOTD  30       DEFINE ZZTOTD
     CSR                   SETOF                     919293
     CSR                   SETOF                     949599
     C*
     C*    DETERMINE SIGN OF ZZAMTI & D,   92 IF NEG
     CSR         ZZAMTI    COMP 0                      9293
     CSR 93      ZZAMTD    COMP 0                      9293
     CSR 93                GOTO ZZSEND                     ZERO BYPASS
     C*
     C*    DETERMINE SIGN OF ZZTOTI & D, 91 IF NEG.
     CSR         ZZTOTI    COMP 0                      9193
     CSR 93      ZZTOTD    COMP 0                      9193
     C*
     C*    IF ZZTOTAL IS ZERO, RETURN ZZAMOUNT.
     CSR 93                Z-ADDZZAMTI    ZZTOTI
     CSR 93                Z-ADDZZAMTD    ZZTOTD
     CSR 93                GOTO ZZSEND                     ZERO BYPASS
     C*    IF SIGNS DIFFER BYPASS OVERFLOW CHECKS.
     CSR 91N92
     CORN91 92             GOTO ZZOFPS
     C*
     CSR         ZZAMTD    ADD  ZZTOTD    ZZWK2   40
     CSR         ZZWK2     COMP 999                  93    '93' = CARRY
     CSRN93      ZZWK2     COMP -999                   93    INTO INTEGER.
     C*
     CSR 93N92   ZZAMTI    ADD  1         ZZWK3  150       ADD 'CARRY' +VE
     CSR    93 92ZZAMTI    SUB  1         ZZWK3            SUB 'CARRY' -VE
     CSR 93      ZZTOTI    ADD  ZZWK3     ZZWK3
     CSRN93      ZZTOTI    ADD  ZZAMTI    ZZWK3
     C*
     C* IF THE MODULUS OF ZZWK3 IS LT MOD. ZZTOTI THEN O/F HAS OCCURED
     CSRN92      ZZWK3     COMP ZZTOTI                 99  -92 MEANS NOS.
     CSR 92      ZZWK3     COMP ZZTOTI               99     NEGATIVE
     CSRN99                Z-ADDZZWK2     ZZTOTD
     CSRN99                Z-ADDZZWK3     ZZTOTI
     C*
     C* IF O/F ZEROIZE ZZAMT , IND '99' SET AND ZZTOT FIELDS LEFT INTACT.
     CSR 99                Z-ADD0         ZZAMT  153
     CSR                   GOTO ZZSEND
     C*
     C* THE 'SIGNS' ARE DIFFERENT
     CSR         ZZOFPS    TAG                             ** ZZOFPS TAG**
     C* IF ZZAMT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZTOT
     C*
     CSR 92                Z-SUBZZAMTI    ZZAMTI 150
     CSR 92                Z-SUBZZAMTD    ZZAMTD  30
     C*
     C* IF ZZTOT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZAMT.
     C*
     CSR 91                Z-SUBZZTOTI    ZZTOTI
     CSR 91                Z-SUBZZTOTD    ZZTOTD
     C* BOTH ZZAMT AND ZZTOT ARE NOW POSITIVE.
     C*
     CSR         ZZTOTI    COMP ZZAMTI               93  95
     CSR 95      ZZTOTD    COMP ZZAMTD               93  95
     C*
     C* IF ZZTOT EQ. ZZAMT RETURN ZERO.
     CSR 95                Z-ADD0         ZZTOTI
     CSR 95                Z-ADD0         ZZTOTD
     CSR 95                GOTO ZZSEND
     C*
     C* IF ZZTOT GT. ZZAMT.
     CSR 93      ZZAMTD    COMP ZZTOTD               94
     CSR 93 94   ZZTOTI    SUB  1         ZZTOTI
     CSR 93 94   ZZTOTD    ADD  1000      ZZWK2
     CSR 93      ZZTOTI    SUB  ZZAMTI    ZZTOTI
     CSR 93 94   ZZWK2     SUB  ZZAMTD    ZZTOTD
     CSR 93N94   ZZTOTD    SUB  ZZAMTD    ZZTOTD
     C*
     C*
     C* IF ZZAMT GT. ZZTOT.
     CSRN93      ZZTOTD    COMP ZZAMTD               94
     CSRN93 94   ZZAMTI    SUB  1         ZZWK3  150
     CSRN93 94   ZZAMTD    ADD  1000      ZZWK2
     CSRN93 94   ZZWK3     SUB  ZZTOTI    ZZTOTI
     CSRN93N94   ZZAMTI    SUB  ZZTOTI    ZZTOTI
     CSRN93 94   ZZWK2     SUB  ZZTOTD    ZZTOTD
     CSRN93N94   ZZAMTD    SUB  ZZTOTD    ZZTOTD
     C*
     C* REVERSE SIGN OF ZZTOT IF LARGER I/P FIELDS WERE NEGATIVE
     CSR                   SETOF                     94
     CSR 93 91
     CORN93 92             SETON                     94
     CSR 94                Z-SUBZZTOTI    ZZTOTI
     CSR 94                Z-SUBZZTOTD    ZZTOTD
     C**
     C**   RESTORE SIGN OF ZZAMTI & ZZAMTD IF IT WAS REVERSED.
     CSR 92                Z-SUBZZAMTI    ZZAMTI
     CSR 92                Z-SUBZZAMTD    ZZAMTD
     CSR         ZZSEND    TAG                             ** ZZSEND TAG *
     C**
     C**   IF ZZTOTD IS ZERO, AND ZZTOTI IS NEG. SET UP ZZNEGD.
     CSR                   SETOF                     96
     CSR         ZZTOTD    COMP 0                        91
     CSR 91      ZZTOTI    COMP 0                      96
     CSR 96                MOVE '.000-'   ZZNEGD  5
     CSR                   ENDSR                             ** ENDSR **
     C********************************************************************
     O********************************************************************
     O**
     O**   AUDIT - DETAIL RECORD
     O**
     OGLAUDTDPE        30
     O       OR        40
     O                                    1 'D'
     O                         CNUM       7
     O                         CCY       10
     O**********               ACOD      14                                                   CGL029
     O                         ACODQQ    14                                                   CGL029
     O                         ACSQ      16
     O                 30      LDBL      24P
     O                 40      PSTAX     24P
     O                 40      DRCR      25
     O                         BRCA      28
     O                 30                29 'B'
     O                 40                29 'M'
     O                         ACOD      43                                                   CGL029
     O**
     O**   AUDIT - HEADER RECORD
     O**
     OGLAUDTAPE        60
     O                                    1 'H'
     O                         FLSZ       4P
     O**
     O**   AUDIT - TRAILER RECORD
     O**
     OGLAUDTTPE        60
     O                                    1 'T'
     O                         RCOUT      4P
     O                         INTP      12P
     O                         DECP      14P
     O                         INTDR     22P
     O                         DECDR     24P
     O                         INTCR     32P
     O                         DECCR     34P
     OGL4170AUT  203   LR
     O                                   22 'REFERENCE GL4170AU'
     O                         TITL      89
     O                                  104 'DATE'
     O                         RUNA     113
     O                                  122 'PAGE'
     O                         PAGE  4  127
     O        T  1     LR
     O                                   66 'DAILY BALANCE PROOF MERG'
     O                                   84 'E - CONTROL REPORT'
     O        T  1     LR
     O                                   66 '------------------------'
     O                                   84 '------------------'
     O        T  112   LR
     O                                   14 'INPUT'
     O                                   65 'ON FILE'
     O                                   90 'IN PROGRAM'
     O        T  2     LR
     O                                   14 '-----'
     O        T  1     LR
     O                                   16 'S/OACCNT'
     O        T  2     LR
     O                                   38 'NUMBER OF ACCOUNTS'
     O                         WORK  3   65
     O                         NACCS 3   90
     O                 51               110 '*** DIFFERENCE ***'
     O        T  2     LR
     O                                   40 'HASH TOTAL OF DEBITS'
     O                         HWND  4   62
     O                         HDCD      65
     O                         INTDR 4   87
     O                         DECDR     90
     O                 53               110 '*** DIFFERENCE ***'
     O        T  2     LR
     O                                   41 'HASH TOTAL OF CREDITS'
     O                         HWNC  4   62
     O                         HDCC      65
     O                         INTCR 4   87
     O                         DECCR     90
     O                 54               110 '*** DIFFERENCE ***'
     O        T  1     LR
     O                                   16 'S/EODPO'
     O        T  2     LR
     O                                   38 'NUMBER OF POSTINGS'
     O                         NOREPO3   65
     O                         NPOST 3   90
     O                 52               110 '*** DIFFERENCE ***'
     O        T  3     LR
     O                                   42 'HASH TOTAL OF POSTINGS'
     O                         HRWN  4   62
     O                         HRDC      65
     O                         INTP  4   87
     O                         DECP      90
     O                 55               110 '*** DIFFERENCE ***'
     O        T  1     LR
     O                                   15 'OUTPUT'
     O        T  2     LR
     O                                   15 '------'
     O        T  1     LR
     O                                   16 'S/AUDIT'
     O        T  2     LR
     O                                   37 'NUMBER OF RECORDS'
     O                         RCOUT 3   90
     O        T  2     LR
     O                                   42 'HASH TOTAL OF POSTINGS'
     O                         INTP  4   87
     O                         DECP      90
     O        T  2     LR
     O                                   40 'HASH TOTAL OF DEBITS'
     O                         INTDR 4   87
     O                         DECDR     90
     O        T  3     LR
     O                                   41 'HASH TOTAL OF CREDITS'
     O                         INTCR 4   87
     O                         DECCR     90
     O        T  3     LR
     O                 U7                63 '*** PRINT TERMINATED - D'
     O                 U7                83 'ATABASE IN ERROR ***'
     O        T  1     LR
     O                                   72 '*** END OF REPORT ***'
**  CPY@
(c) Finastra International Limited 2001
