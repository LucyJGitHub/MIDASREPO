      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Daily Balance Proof Rept - Consld')
     F********************************************************************
     F*                                                                  *
     F*  Midas GENERAL LEDGER MODULE
     F*                                                                  *
     F*    GL4180 - DAILY BALANCE PROOF REPORT                           *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01359             Date 03Mar9292             *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*     S01359 - DAILY BALANCE PROOF REPORT                          *
     F*                                                                  *
     F********************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                         *
     F*                                                                  *
     F*                                                                  *
     F********************************************************************
     FGLAUDITLIP  E           K        DISK
     F***TABLE** IF  F      96 12AI     2 DISK                                                CGL029
     FTABLE   IF  F     230 12AI     2 DISK                                                   CGL029
     FGL4180P1O   F     132            PRINTER      KINFDS SPOOL1     UC
     FGL4180AUO   F     132            PRINTER      KINFDS SPOOLU
     F/SPACE 2
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F* 01            AUDIT - HEADER RECORD
     F* 02                  - DETAIL - BALANCE RECORD
     F* 03                           - MOVEMENT RECORD
     F* 04                  - TRAILER RECORD
     F* 05                  - CATCHALL
     F*       10      FIRST CYCLE COMPLETE
     F*    20         LEDGER BALANCE - CREDIT
     F*    21                        - DEBIT
     F*    30         ACCOUNT POSTING - CREDIT
     F*    31                         - DEBIT
     F*       37      DECIMAL PLACES - ZERO
     F*       38                     - ONE
     F*       39                     - TWO
     F*       40                     - THREE
     F*       41      DIFFERENCE - NUMBER OF RECORDS
     F*       42                 - HASH OF POSTINGS
     F*       43                 - HASH OF BALANCE DEBITS
     F*       44                 - HASH OF BALANCE CREDITS
     F*       50      OPENING BALANCE - DEBIT
     F*       51                      - CREDIT
     F*       53      CLOSING BALANCE - DEBIT
     F*       54                      - CREDIT
     F*       56      CLOSING BALANCE IS GREATER THAN THE OPENING BALANCE
     F*       57      CLOSING BALANCE IS LESS THAN THE OPENING BALANCE
     F*       75      INPUT FILE HAS AT LEAST ONE RECORD
     F*       U7 U8   DATA BASE ERROR - PROGRAM HALTS
     E*
     E                    CPY@    1   1 80
     I**
     I**   AUDIT - HEADER RECORD
     I**
     IGLAUDTAF    01
     I**
     I**   AUDIT - DETAIL BALANCE & MOVEMENT RECORD
     I**
     IGLAUDTDF    02
     I                                              CNUM  L1
     I                                              CCY   L1
     I                                              ACOD  L1
     I                                              ACSQ  L1
     I                                              CCY   L2
     I                                              LDBL        2120
     I                                              DRCR        30  31
     I                                              BRCA  L1
     I**
     I**   AUDIT - TRAILER RECORD
     I**
     IGLAUDTTF    04
     I              NORE                            RCOUT
     I              VSWN                            INTP
     I              VSDC                            DECP
     I              HWND                            INTDR
     I              HDCD                            DECDR
     I              HWNC                            INTCR
     I              HDCC                            DECCR
     I**
     I*    TABLE FILE - INSTALLATION CONTROL DATA RECORD 1.
     I*
     ITABLE   NS       2 C0   3 C1  12 C1
     I       AND      13 C0
     I                                        1   1 RECI
     I                                        2   30RECT
     I                                       12  130RCDE
     I                                       14  66 TITL
     I                                       67  67 DATF
     I                                       68  700DFBR
     I                                       74  74 MODI1
     I                                       75  75 MODI2
     I                                       76  76 MODI3
     I                                    P  77  790RUND
     I                                       80  86 RUNA
     I                                       87  870SFLG
     I                                    P  90  920LCD
     I                                       93  93 CHTP
     I                                    P  94  960TNLU
     I*
     I*    TABLE FILE - CURRENCIES RECORD 1.
     I*
     ITABLE   NS       1 CD   2 C2   3 C0
     I       AND      13 C1
     I                                        1   1 RECI
     I                                        2   30RECT
     I                                       10  12 CCY
     I                                       13  130SREC
     I                                       14  27 CCNM
     I                                    P  28  348SPOT
     I                                       35  37 CCYS
     I                                       38  380CDP
     I                                       39  390TNOT
     I                                       40  40 DCCY
     I                                       41  44 SRC1
     I                                       45  48 SRC2
     I**********                             49  520TACC                                      CGL029
     I**********                             53  560TEQV                                      CGL029
     I**********                             57  600SWQV                                      CGL029
     I**********                             61  640TAC4                                      CGL029
     I**********                             65  680TAC5                                      CGL029
     I**********                             69  69 HIND                                      CGL029
     I**********                          P  70  768PART                                      CGL029
     I**********                             77  780SSNO                                      CGL029
     I**********                          P  79  858ERLC                                      CGL029
     I**********                             86  860MDIN                                      CGL029
     I**********                             87  870MDIA                                      CGL029
     I**********                             88  880MDIB                                      CGL029
     I**********                          P  90  920LCD                                       CGL029
     I**********                             93  93 CHTP                                      CGL029
     I**********                          P  94  960TNLU                                      CGL029
     I                                       49  580TACC                                      CGL029
     I                                       59  680TEQV                                      CGL029
     I                                       69  780SWQV                                      CGL029
     I                                       79  880TAC4                                      CGL029
     I                                       89  980TAC5                                      CGL029
     I                                       99  99 HIND                                      CGL029
     I                                    P 100 1068PART                                      CGL029
     I                                      107 1080SSNO                                      CGL029
     I                                    P 109 1158ERLC                                      CGL029
     I                                      116 1160MDIN                                      CGL029
     I                                      117 1170MDIA                                      CGL029
     I                                      118 1180MDIB                                      CGL029
     I                                    P 120 1220LCD                                       CGL029
     I                                      123 123 CHTP                                      CGL029
     I                                    P 124 1260TNLU                                      CGL029
      *
      *     file information data structure
      *
     ISPOOL1      DS
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     ISPOOLU      DS
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      *
     IRUNDT       DS
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
     C           *ENTRY    PLIST
     C                     PARM           SEQ     5
     C   10                GOTO NOTFC
     C********************************************************************
     C**   FIRST CYCLE
     C**   -----------
     C**
     C*
     C** SETUP COPYRIGHT STATEMENT
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C                     SETON                     10
      *
      *  in the data area RUNDAT
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
      *
     C**
     C**   OBTAIN ICDR 1
     C                     EXSR ZSYSTM
     C   99                SETON                     U7U8LR
     C   99                GOTO END
     C**
     C**   SET-UP WORK FIELDS
     C                     MOVEL'20'      TABKEY 12
     C                     MOVE '1'       CWORK   4
     C**
     C********************************************************************
     C**   DETAIL PROCESSING
     C**   -----------------
     C**
     C           NOTFC     TAG                             ** NOTFC TAG **
      *
      *  if NOT OPEN, open GL4180P1
      *
     C           *IN75     IFNE '1'                        <---------\
     C                     SETON                     75              |
     C                     OPEN GL4180P1                             |
     C                     Z-ADD0         PAGE                       |
     C                     Z-ADD0         PAGE1                      |
      *                                                              |
      *  ensure pool file recorded by RCF                            |
      *                                                              |
     C                     Z-ADDSFNUM1    ZSNUM1  60                 |
     C                     CALL 'ZSFILE'                             |
     C                     PARM           SEQ     5                  |
     C                     PARM BRCA      SENTY   3                  |
     C                     PARM           SFILE1                     |
     C                     PARM           ZSNUM1                     |
     C                     PARM           ZSERR   1                  |
      *                                                              |
      *  if an error in ZSFILE processing, return to calling program |
      *                                                              |
     C           ZSERR     IFEQ 'Y'                        <-------\ |
     C                     SETON                     U7U8LR        | |
     C                     RETRN                                   | |
     C                     END                             <-------/ |
     C                     END                             <---------/
      *
     C**
     C**   CLEAR THE WORK FIELDS
     C   L1                Z-ADD0         OBAL   150
     C   L1                Z-ADD0         CBAL   150
     C**
     C  NL2                GOTO NOTL2
     C**
     C**   CLEAR THE ACCUMULATORS
     C                     Z-ADD0         DROB   150
     C                     Z-ADD0         DRPL   150
     C                     Z-ADD0         DRMI   150
     C                     Z-ADD0         DRTM   150
     C                     Z-ADD0         DRCB   150
     C**
     C                     Z-ADD0         CROB   150
     C                     Z-ADD0         CRPL   150
     C                     Z-ADD0         CRMI   150
     C                     Z-ADD0         CRTM   150
     C                     Z-ADD0         CRCB   150
     C**
     C**   DETERMINE THE CURRENCY NAME
     C                     MOVELCCY       CWORK
     C                     MOVE CWORK     TABKEY
     C           TABKEY    CHAINTABLE                99
     C   99                SETON                     U7U8LR
     C   99                GOTO END
     C**
     C**   DETERMINE THE NUMBER OF DECIMAL PLACES FOR THE CURRENCY
     C           CDP       COMP 1                      3738
     C           CDP       COMP 2                    40  39
     C**
     C           NOTL2     TAG                             ** NOTL2 TAG **
     C**
     C**   PERFORM HASH TOTALING
     C           NORE      ADD  1         NORE    50
     C           *IN02     CABNE'1'       END
     C**
     C**   BALANCE RECORD PROCESSING
     C           SORT      IFEQ 'B'
     C                     EXSR HASHB
     C   20      CROB      SUB  LDBL      CROB             CLOSING CREDITS
     C   21      DROB      ADD  LDBL      DROB             CLOSING DEBITS
     C           OBAL      ADD  LDBL      OBAL             OPENING BAL
     C           CBAL      ADD  LDBL      CBAL             CLOSING BAL
     C                     END
     C**
     C**   POSTING RECORD PROCESSING
     C           SORT      IFEQ 'M'
     C                     EXSR HASHP
     C   30      CBAL      SUB  LDBL      CBAL             CLOSING BAL
     C   31      CBAL      ADD  LDBL      CBAL             CLOSING BAL
     C   30      CRTM      ADD  LDBL      CRTM             TOTAL CREDITS
     C   31      DRTM      ADD  LDBL      DRTM             TOTAL DEBITS
     C                     END
     C**
     C           END       TAG                             *** END TAG ***
     C**
     C********************************************************************
     C**   TOTAL PROCESSING
     C**   ________________
     C**
     C**   AT CHANGE OF ACCOUNT, DETERMINE WHETHER OR NOT THE BALANCES
     C**   ARE DEBITS OR CREDITS AND IF THERE IS ANY BALANCE CHANGE
     CL1         CBAL      COMP OBAL                 5657
     CL1N56N57             GOTO ENDL1
     CL1         OBAL      COMP 0                    5051
     CL1         CBAL      COMP 0                    5354
     CL1         CBAL      SUB  OBAL      POST   150
     C**
     C**   AT CHANGE OF ACCOUNT, ACCUMULATE THE BALANCE CHANGE DUE TO
     C**   CROSSING OVER FROM A DEBIT BALANCE TO A CREDIT AND VICE VERSA.
     C**   ( EITHER OF THE BALANCES MAY BE ZERO )
     CL1N50 53             ADD  CBAL      DRPL
     CL1 50N53             ADD  OBAL      DRMI
     CL1N51 54             SUB  CBAL      CRPL
     CL1 51N54             SUB  OBAL      CRMI
     C**
     C**   AT CHANGE OF ACCOUNT, ACCUMULATE THE BALANCE CHANGE WHEN THE
     C**   BALANCE DOES NOT CROSSOVER
     CL1 50 53 56          ADD  POST      DRPL
     CL1 50 53 57          SUB  POST      DRMI
     CL1 51 54 56          ADD  POST      CRMI
     CL1 51 54 57          SUB  POST      CRPL
     C**
     CL1         ENDL1     TAG                             ** ENDL1 TAG **
     C**
     C**   AT CHANGE OF CURRENCY, DETERMINE OPENING BALANCES
     CL2         CROB      ADD  CRPL      CRCB
     CL2         CRCB      SUB  CRMI      CRCB             CLOSING CREDITS
     CL2         DROB      ADD  DRPL      DRCB
     CL2         DRCB      SUB  DRMI      DRCB             CLOSING DEBITS
     C**
     CLR U7                GOTO ENDLR
     C**
     C**   DETERMINE THE FILE INTEGRITY
     CLR         NORE      COMP RCOUT                4141
     CLR         PINTP     COMP INTP                 4242
     CLRN42      PDECP     COMP DECP                 4242
     CLR         PINTDR    COMP INTDR                4343
     CLRN43      PDECDR    COMP DECDR                4343
     CLR         PINTCR    COMP INTCR                4444
     CLRN44      PDECCR    COMP DECCR                4444
     CLR 41
     COR 42
     COR 43
     COR 44                SETON                     U8
      *
      *  ensure totals spool file recorded by RCF
      *
     CLR                   Z-ADDSFNUMU    ZSNUMU  60
     CLR                   CALL 'ZSFILE'
     CLR                   PARM           SEQ     5
     CLR                   PARM *BLANKS   SENTY
     CLR                   PARM           SFILEU
     CLR                   PARM           ZSNUMU
     CLR                   PARM           ZSERR   1
      *
      *  if an error in ZSFILE processing, return to calling program
      *
     CLR         ZSERR     IFEQ 'Y'                        <-------\
     CLR                   SETON                     U7U8LR        |
     CLR                   RETRN                                   |
     CLR                   END                             <-------/
     C**
     CLR         ENDLR     TAG                             ** ENDLR TAG **
     C**
     C********************************************************************
     C/SPACE 2
     C********************************************************************
     C**   SUBROUTINE TO MAINTAIN THE BALANCE HASH TOTALS
     C**
     C           HASHB     BEGSR
     C**
     C           LDBL      DIV  1000      ZZAMT
     C   20                Z-SUBZZAMT     ZZAMT
     C   20                Z-ADDPINTCR    ZZTOTI
     C   20                Z-ADDPDECCR    ZZTOTD
     C  N20                Z-ADDPINTDR    ZZTOTI
     C  N20                Z-ADDPDECDR    ZZTOTD
     C**
     C                     EXSR GLZADD
     C**
     C   20                Z-ADDZZTOTI    PINTCR 150
     C   20                Z-ADDZZTOTD    PDECCR  30
     C  N20                Z-ADDZZTOTI    PINTDR 150
     C  N20                Z-ADDZZTOTD    PDECDR  30
     C**
     C                     ENDSR
     C**
     C********************************************************************
     C**   SUBROUTINE TO MAINTAIN THE POSTING HASH TOTALS
     C**
     C           HASHP     BEGSR
     C**
     C                     Z-ADD0         ZZAMT
     C                     MOVE LDBL      ZZAMT
     C                     Z-ADDPINTP     ZZTOTI
     C                     Z-ADDPDECP     ZZTOTD
     C**
     C                     EXSR GLZADD
     C**
     C                     Z-ADDZZTOTI    PINTP  150
     C                     Z-ADDZZTOTD    PDECP   30
     C**
     C                     ENDSR
     C********************************************************************
     C**
     C**   ZSYSTM SR. TO CHAIN TO THE INSTALLATION CONTROL DATA RECORD.
     C**
     C           ZSYSTM    BEGSR                           *** ZSYSTM ***
     C**
     C**   SET UP KEY FOR INSTALLATION CONTROL RECORD.
     C                     MOVEL'01      'ZTABKY 12
     C                     MOVE '  10'    ZTABKY
     C**
     C           ZTABKY    CHAINTABLE                99    ON NOT THERE
     C  N99      RECI      COMP 'D'                  9999  ON, IS DELETED
     C**
     C**   CHECK SYSTEM DATE FORMAT, DDMMYY OR MMDDYY.
     C           DATF      COMP 'M'                      98MMDDYY, 98 ON
     C**
     C                     ENDSR
     C**
     C**
     C********************************************************************
     C* SUBROUTINE TO ADD AN AMOUNT (ZZAMT) TO THE TOTAL (ZZTOTI,ZZTOTD)
     C*   IND '99' IS SET BY AN OVERFLOW ERROR
     C*
     CSR         GLZADD    BEGSR                           *** GLZADD ****
     C*
     CSR                   Z-ADDZZAMT     ZZAMT  153     91-DEFINE ZZAMT
     CSR 91                GOTO ZZAEND                     AMT = 0.BYPASS
     C*
     C* SPLIT ZZAMT INTO INTEGER AND DECIMAL FIELDS
     CSR                   Z-ADDZZAMT     ZZAMTI 150       INTEGER FIELD
     CSR                   MOVE ZZAMT     ZZAMTD  30       DECIMAL FIELD
     C* BOTH ZZAMTI AND ZZAMTD CONTAIN A 'SIGN' ZONE NOW
     C*
     CSR                   EXSR GLZSUM
     CSR         ZZAEND    ENDSR                           ** ZZAEND TAG *
     C*
     C********************************************************************
     C* SUBROUTINE TO CARRY OUT THE ADDITION FOR SUBROUTINES -
     C*                        GLZADD, GLZSUB, GLZCMP.
     C*          PARAMETERS PASSED -
     C*                     I/P ZZAMTI ZZAMTD
     C*                     O/P ZZTOTI ZZTOTD ZZNEGD IND 96 IND 99.
     C*
     CSR         GLZSUM    BEGSR                           *** GLZSUM ****
     C*
     CSR                   Z-ADDZZTOTI    ZZTOTI 150       DEFINE ZZTOTI
     CSR                   Z-ADDZZTOTD    ZZTOTD  30       DEFINE ZZTOTD
     CSR                   SETOF                     919293
     CSR                   SETOF                     949599
     C*
     C*    DETERMINE SIGN OF ZZAMTI & D,   92 IF NEG
     CSR         ZZAMTI    COMP 0                      9293
     CSR 93      ZZAMTD    COMP 0                      9293
     CSR 93                GOTO ZZSEND                     ZERO BYPASS
     C*
     C*    DETERMINE SIGN OF ZZTOTI & D, 91 IF NEG.
     CSR         ZZTOTI    COMP 0                      9193
     CSR 93      ZZTOTD    COMP 0                      9193
     C*
     C*    IF ZZTOTAL IS ZERO, RETURN ZZAMOUNT.
     CSR 93                Z-ADDZZAMTI    ZZTOTI
     CSR 93                Z-ADDZZAMTD    ZZTOTD
     CSR 93                GOTO ZZSEND                     ZERO BYPASS
     C*    IF SIGNS DIFFER BYPASS OVERFLOW CHECKS.
     CSR 91N92
     CORN91 92             GOTO ZZOFPS
     C*
     CSR         ZZAMTD    ADD  ZZTOTD    ZZWK2   40
     CSR         ZZWK2     COMP 999                  93    '93' = CARRY
     CSRN93      ZZWK2     COMP -999                   93    INTO INTEGER.
     C*
     CSR 93N92   ZZAMTI    ADD  1         ZZWK3  150       ADD 'CARRY' +VE
     CSR    93 92ZZAMTI    SUB  1         ZZWK3            SUB 'CARRY' -VE
     CSR 93      ZZTOTI    ADD  ZZWK3     ZZWK3
     CSRN93      ZZTOTI    ADD  ZZAMTI    ZZWK3
     C*
     C* IF THE MODULUS OF ZZWK3 IS LT MOD. ZZTOTI THEN O/F HAS OCCURED
     CSRN92      ZZWK3     COMP ZZTOTI                 99  -92 MEANS NOS.
     CSR 92      ZZWK3     COMP ZZTOTI               99     NEGATIVE
     CSRN99                Z-ADDZZWK2     ZZTOTD
     CSRN99                Z-ADDZZWK3     ZZTOTI
     C*
     C* IF O/F ZEROIZE ZZAMT , IND '99' SET AND ZZTOT FIELDS LEFT INTACT.
     CSR 99                Z-ADD0         ZZAMT  153
     CSR                   GOTO ZZSEND
     C*
     C* THE 'SIGNS' ARE DIFFERENT
     CSR         ZZOFPS    TAG                             ** ZZOFPS TAG**
     C* IF ZZAMT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZTOT
     C*
     CSR 92                Z-SUBZZAMTI    ZZAMTI 150
     CSR 92                Z-SUBZZAMTD    ZZAMTD  30
     C*
     C* IF ZZTOT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZAMT.
     C*
     CSR 91                Z-SUBZZTOTI    ZZTOTI
     CSR 91                Z-SUBZZTOTD    ZZTOTD
     C* BOTH ZZAMT AND ZZTOT ARE NOW POSITIVE.
     C*
     CSR         ZZTOTI    COMP ZZAMTI               93  95
     CSR 95      ZZTOTD    COMP ZZAMTD               93  95
     C*
     C* IF ZZTOT EQ. ZZAMT RETURN ZERO.
     CSR 95                Z-ADD0         ZZTOTI
     CSR 95                Z-ADD0         ZZTOTD
     CSR 95                GOTO ZZSEND
     C*
     C* IF ZZTOT GT. ZZAMT.
     CSR 93      ZZAMTD    COMP ZZTOTD               94
     CSR 93 94   ZZTOTI    SUB  1         ZZTOTI
     CSR 93 94   ZZTOTD    ADD  1000      ZZWK2
     CSR 93      ZZTOTI    SUB  ZZAMTI    ZZTOTI
     CSR 93 94   ZZWK2     SUB  ZZAMTD    ZZTOTD
     CSR 93N94   ZZTOTD    SUB  ZZAMTD    ZZTOTD
     C*
     C*
     C* IF ZZAMT GT. ZZTOT.
     CSRN93      ZZTOTD    COMP ZZAMTD               94
     CSRN93 94   ZZAMTI    SUB  1         ZZWK3  150
     CSRN93 94   ZZAMTD    ADD  1000      ZZWK2
     CSRN93 94   ZZWK3     SUB  ZZTOTI    ZZTOTI
     CSRN93N94   ZZAMTI    SUB  ZZTOTI    ZZTOTI
     CSRN93 94   ZZWK2     SUB  ZZTOTD    ZZTOTD
     CSRN93N94   ZZAMTD    SUB  ZZTOTD    ZZTOTD
     C*
     C* REVERSE SIGN OF ZZTOT IF LARGER I/P FIELDS WERE NEGATIVE
     CSR                   SETOF                     94
     CSR 93 91
     CORN93 92             SETON                     94
     CSR 94                Z-SUBZZTOTI    ZZTOTI
     CSR 94                Z-SUBZZTOTD    ZZTOTD
     C**
     C**   RESTORE SIGN OF ZZAMTI & ZZAMTD IF IT WAS REVERSED.
     CSR 92                Z-SUBZZAMTI    ZZAMTI
     CSR 92                Z-SUBZZAMTD    ZZAMTD
     CSR         ZZSEND    TAG                             ** ZZSEND TAG *
     C**
     C**   IF ZZTOTD IS ZERO, AND ZZTOTI IS NEG. SET UP ZZNEGD.
     CSR                   SETOF                     96
     CSR         ZZTOTD    COMP 0                        91
     CSR 91      ZZTOTI    COMP 0                      96
     CSR 96                MOVE '.000-'   ZZNEGD  5
     CSR                   ENDSR                             ** ENDSR **
     C********************************************************************
     O********************************************************************
     O**
     O**   GL4180 - DAILY BALANCE PROOF
     O**
     OGL4180P1T  203   L2NU7
     O                                   24 'REFERENCE GL4180P1'
     O                         TITL      89
     O                                  104 'DATE'
     O                         RUNA     113
     O                                  122 'PAGE'
     O                         PAGE1 4  127
     O        T  1     L2NU7
     O                                   72 'DAILY BALANCE PROOF'
     O        T  2     L2NU7
     O                                   72 '-------------------'
     O        T  3     L2NU7
     O                                   60 'CURRENCY -'
     O                         CCNM      75
     O        T 33     L2NU7
     O                                   40 'OPENING BALANCE - DEBITS'
     O                       37DROB      62 '   ,   ,   ,   , 0 '
     O                       40DROB      62 '   ,   ,   , 0 .   '
     O                       39DROB      62 ' ,   ,   ,   , 0 .  '
     O                       38DROB      62 '  ,   ,   ,   , 0 . '
     O                                   90 'OPENING BALANCE - CREDIT'
     O                                   91 'S'
     O                       37CROB     113 '   ,   ,   ,   , 0 '
     O                       40CROB     113 '   ,   ,   , 0 .   '
     O                       39CROB     113 ' ,   ,   ,   , 0 .  '
     O                       38CROB     113 '  ,   ,   ,   , 0 . '
     O        T  3     L2NU7
     O                                   27 'PLUS DEBITS'
     O                       37DRPL      62 '   ,   ,   ,   , 0 '
     O                       40DRPL      62 '   ,   ,   , 0 .   '
     O                       39DRPL      62 ' ,   ,   ,   , 0 .  '
     O                       38DRPL      62 '  ,   ,   ,   , 0 . '
     O                                   78 'PLUS CREDITS'
     O                       37CRPL     113 '   ,   ,   ,   , 0 '
     O                       40CRPL     113 '   ,   ,   , 0 .   '
     O                       39CRPL     113 ' ,   ,   ,   , 0 .  '
     O                       38CRPL     113 '  ,   ,   ,   , 0 . '
     O        T  3     L2NU7
     O                                   28 'MINUS DEBITS'
     O                       37DRMI      62 '   ,   ,   ,   , 0 '
     O                       40DRMI      62 '   ,   ,   , 0 .   '
     O                       39DRMI      62 ' ,   ,   ,   , 0 .  '
     O                       38DRMI      62 '  ,   ,   ,   , 0 . '
     O                                   79 'MINUS CREDITS'
     O                       37CRMI     113 '   ,   ,   ,   , 0 '
     O                       40CRMI     113 '   ,   ,   , 0 .   '
     O                       39CRMI     113 ' ,   ,   ,   , 0 .  '
     O                       38CRMI     113 '  ,   ,   ,   , 0 . '
     O        T  3     L2NU7
     O                                   40 'CLOSING BALANCE - DEBITS'
     O                       37DRCB      62 '   ,   ,   ,   , 0 '
     O                       40DRCB      62 '   ,   ,   , 0 .   '
     O                       39DRCB      62 ' ,   ,   ,   , 0 .  '
     O                       38DRCB      62 '  ,   ,   ,   , 0 . '
     O                                   90 'CLOSING BALANCE - CREDIT'
     O                                   91 'S'
     O                       37CRCB     113 '   ,   ,   ,   , 0 '
     O                       40CRCB     113 '   ,   ,   , 0 .   '
     O                       39CRCB     113 ' ,   ,   ,   , 0 .  '
     O                       38CRCB     113 '  ,   ,   ,   , 0 . '
     O        T  3     L2NU7
     O                                   37 'TOTAL DEBIT MOVEMENTS'
     O                       37DRTM      62 '   ,   ,   ,   , 0 '
     O                       40DRTM      62 '   ,   ,   , 0 .   '
     O                       39DRTM      62 ' ,   ,   ,   , 0 .  '
     O                       38DRTM      62 '  ,   ,   ,   , 0 . '
     O                                   88 'TOTAL CREDIT MOVEMENTS'
     O                       37CRTM     113 '   ,   ,   ,   , 0 '
     O                       40CRTM     113 '   ,   ,   , 0 .   '
     O                       39CRTM     113 ' ,   ,   ,   , 0 .  '
     O                       38CRTM     113 '  ,   ,   ,   , 0 . '
     O        T  133   L2NU7
     O                                   88 'APPROVED BY'
     O        T  1     L2NU7
     O                                  113 '-----------------------'
     O        T 3      LR
     O                                   61 '*** END OF'
     O                                   72 ' REPORT ***'
     O**
     O**   GL4180 - RUN CONTROLS
     O**
     OGL4180AUT  203   LR
     O                                   24 'REFERENCE GL4180AU'
     O                         TITL      89
     O                                  104 'DATE'
     O                         RUNA     113
     O                                  122 'PAGE'
     O                         PAGE  4  127
     O        T  1     LR
     O                                   72 'DAILY BALANCE PROOF'
     O        T  1     LR
     O                                   72 '-------------------'
     O        T  115   LR
     O                                   21 'CONTROL TOTALS'
     O                                   68 'ON FILE'
     O                                   90 'IN PROGRAM'
     O        T  2     LR
     O                                   21 '--------------'
     O        T  2     LR
     O                                   39 'NUMBER OF RECORDS'
     O                         RCOUT 3   68
     O                         NORE  3   90
     O                       41         110 '*** DIFFERENCE ***'
     O        T  2     LR
     O                                   44 'HASH TOTAL OF POSTINGS'
     O                         INTP  4   65
     O                         DECP      68
     O                         PINTP 4   87
     O                         PDECP     90
     O                       42         110 '*** DIFFERENCE ***'
     O        T  2     LR
     O                                   46 'HASH TOTAL OF BALANCE DE'
     O                                   50 'BITS'
     O                         INTDR 4   65
     O                         DECDR     68
     O                         PINTDR4   87
     O                         PDECDR    90
     O                       43         110 '*** DIFFERENCE ***'
     O        T  3     LR
     O                                   46 'HASH TOTAL OF BALANCE CR'
     O                                   51 'EDITS'
     O                         INTCR 4   65
     O                         DECCR     68
     O                         PINTCR4   87
     O                         PDECCR    90
     O                       44         110 '*** DIFFERENCE ***'
     O        T  3     LR
     O                       U7          64 '*** PRINT TERMINATED -'
     O                       U7          86 'DATABASE IN ERROR ***'
     O        T 31     LR
     O                                   72 '*** END OF REPORT ***'
**  CPY@
(c) Finastra International Limited 2001
