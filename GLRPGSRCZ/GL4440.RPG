     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Transfer between Group Jobs-AME Enq')
      ****************************************************************
      *                                                              *
     F*  Midas - General Ledger Module
      *                                                              *
      *     GL4440 - ENQUIRY UTILITY PROGRAM TO TRANSFER             *
      *              BETWEEN DIFFERENT ENQUIRIES                     *
      *                                                              *
      *  (c) Finastra International Limited 2001                      *
      *                                                              *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. 185817             Date 32Jan01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 E20839             Date 07Feb00               *
      *                                                              *
      *--------------------------------------------------------------*
      *                                                              *
      *  MD046248 - Finastra Rebranding                               *
      *  185817 - Ensure that the program produces a dump whenever an *
      *           error is encountered.                              *
      *  E20839 - CHANGE QCAEXEC TO QCMDEXC                          *
      *                                                              *
      *--------------------------------------------------------------*
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
      ****************************************************************
      *                  FUNCTION OF INDICATORS
      *
      *       U1         PROGRAM ERROR - *PSSR EXECUTED
      ****************************************************************
      *                                                              *
      *  XCMD  is a table holding CL commands to be processed by     *
      *********QCAEXEC.***********************************************    E20839
      *        QCMDEXC.                                              *    E20839
      *                                                              *
      ****************************************************************
      *
     E                    XCMD    1   5 80
     E                    CPY@    1   1 80               COPYRIGHT STATEMENT
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      /EJECT
      ****************************************************************
      *                                                              *
      * MAINLINE Check the parameter passed to this program is a     *
      *          valid enquiry program name,  if not send a message  *
      *          to the job log.                                     *
      *                                                              *
      *          Call GLC440A to transfer *LDA to *GDA, if an error  *
      *          occurs during the call end this program.            *
      *                                                              *
      *          Transfer to a group job, on return to this job call *
      *          GLC440A to transfer *GDA to *LDA.  Return to the    *
      *          enquiry leaving this transfer program active.       *
      *                                                              *
      ****************************************************************
     C                     MOVEACPY@      BIS@   80
     C           *ENTRY    PLIST
     C                     PARM           WPARM1  6
     C*
     C*
     C           WPARM1    IFEQ 'GL4441'
     C                     Z-ADD1         X       30
     C                     ELSE
     C           WPARM1    IFEQ 'GL4442'
     C                     Z-ADD2         X
     C                     ELSE
     C           WPARM1    IFEQ 'GL4443'
     C                     Z-ADD3         X
     C                     ELSE
     C           WPARM1    IFEQ 'GL4444'
     C                     Z-ADD4         X
     C                     ELSE
     C                     Z-ADD5         X
     C                     END
     C                     END
     C                     END
     C                     END
     C*
      /EJECT
     C*
     C                     MOVE 'O'       WPARM2  1
     C                     CALL 'GLC440A'
     C                     PARM           WPARM2
     C*
     C           *INU1     IFEQ '1'
     C                     EXSR *PSSR
     C                     END
     C*
     C                     Z-ADD80        XLEN   155
     C***********          CALL 'QCAEXEC'                                 E20839
     C                     CALL 'QCMDEXC'                                 E20839
     C                     PARM           XCMD,X
     C                     PARM           XLEN
     C*
     C                     MOVE 'I'       WPARM2
     C                     CALL 'GLC440A'
     C                     PARM           WPARM2
     C*
     C           *INU1     IFEQ '1'
     C                     EXSR *PSSR
     C                     END
     C*
     C                     RETRN
     C*
      /EJECT
      ****************************************************************
      *                                                              *
      *  *PSSR  Program error trap subroutine.                       *
      *         *INU1 will be seton if a loop occurs within the SR   *
      *         or was seton after a call to GLC440A ended in error. *
      *                                                              *
      ****************************************************************
     C*
     C           *PSSR     BEGSR
     C                     DUMP                                           185817
     C           *INU1     IFEQ '1'
     C                     GOTO ENDIT
     C                     END
     C*
     C                     SETON                     U1
     C**********           DUMP                                           185817
     C           ENDIT     ENDSR'*CANCL'
      /SPACE 3
**  XCMD
TFRGRPJOB GRPJOB(GLCJOB1) INLGRPPGM(GLC441) TEXT('Acc & Stmt Details')
TFRGRPJOB GRPJOB(GLCJOB2) INLGRPPGM(GLC442) TEXT('Ldgr Mvmts TO SOB')
TFRGRPJOB GRPJOB(GLCJOB3) INLGRPPGM(GLC443) TEXT('Projected Mvmts')
TFRGRPJOB GRPJOB(GLCJOB4) INLGRPPGM(GLC444) TEXT('Items Clearing')
SNDPGMMSG *NONE CPF9898 QCPFMSG 'Invalid Parameter Passed' *PRV MSGTYPE(*STATUS)
**  CPY@
(c) Finastra International Limited 2001
