     H
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL Purge Historic Control Records')
     F*****************************************************************
     F*                                                               *
     F*  Midas GENERAL LEDGER MODULE                              *
     F*                                                               *
     F*  GL4455  -  Purge Historic Statements Control File            *
     F*                                                               *
     F*  Function: This program purges historic statements control    *
     F*  (COB)     file.  Records will be purged according to the     *
     F*            account code's retention period.  If the account   *
     F*            code's retention period is not set up, then the    *
     F*            purge will be conditioned on the A/C posting       *
     F*            history retention field in SDGELRPD.               *
     F*                                                               *
     F*  Called By: GLC4455                                           *
     F*                                                               *
     F*  Frequency: Run at the beginning of the month                 *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD005             Date 01MAR99               *
      *                 CTI002 EM / TI Interface 23/09/98             *
     F*                 S01520 *CREATE     DATE 02NOV94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD005 - Standing Data Account Narrative.  Add new field     *
      *           to SDACODPD.  Recompilation of programs.            *
      *  CTI002 - Recompiled : File SDACODPD changed                  *
     F*  S01520 - BLI Account Statements Changes.                     *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     FCPOSTDD UP  E           K        DISK
     F*
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   10  - First Cycle                                           *
     F*   20  - DELETE operation not successful                       *
     F* U7+U8 - Database error                                        *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  *PSSR  - Program Error Processing Subroutine                 *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     I/EJECT
     ILDA        UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      ** Local Data Area
      *
     IPSDS       SDS
      ** Program Status Data Structure
     I                                      244 253 JOB
     I                                      254 263 USRID
     IRUNDT       DS
      ** Data Area RUNDAT
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
      *
      ** Dummy Data Structures used by Access Programs.
      *
     ISDACOD    E DSSDACODPD
      ** External data structures for Account Codes Details
      *
     ISDGELR    E DSSDGELRPD
     I              QQACCD                          QQACD1                                    CGL029
      ** External data structures for General Ledger Details
      *
     IDSFDY     E DSDSFDY
      **  Short data structure for access objects
      *
     IDSSDY     E DSDSSDY                                                                     CGL029
      **  Long data structure for access objects                                              CGL029
      *                                                                                       CGL029
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      **  F I R S T   C Y C L E
      *
     C           *IN10     IFEQ '1'
     C                     GOTO ENDFC
     C                     ENDIF
      *
     C                     MOVE '1'       *IN10
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      ACT@   80
      *
      ** Initialise LDA fields
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'GL4455'  DBPGM
     C                     MOVEL*BLANKS   DBKEY
     C                     MOVEL*BLANKS   DBFILE
     C                     Z-ADD0         DBASE
     C                     OUT  LDA
      *
      ** Initialise RUNDT fields
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
      *
      ** Initialise Drop Date and Account Code work fields
      *
     C                     Z-ADD0         WDPDT   50
     C**********           Z-ADD0         WACDE   40                                          CGL029
     C**********           MOVE *BLANKS   WACOD   4                                           CGL029
     C                     Z-ADD0         WACDE  100                                          CGL029
     C                     MOVE *BLANKS   WACOD  10                                           CGL029
      *
      ** Get details of SDGELRPD record using access object
      *
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*MSG    '@RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
      ** Check if an error occurred
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'SDGELRPD'DBFILE           * DBERROR 001 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
     C           ENDFC     TAG
      *
      **  Calculate for the drop date using the a/c posting retention
      **  period from SDGELRPD.
      *
     C           BKAPRP    MULT 31        WDROP1  40
     C           RUND      SUB  WDROP1    WDPDT1  50
      *
      ** Get details of SDACODPD record using access object only
      ** if the current account code is not equal to the previous
      ** account code read.
      *
     C           ACOD      IFNE WACDE
     C                     Z-ADDACOD      WACDE
     C                     MOVE ACOD      WACOD
      *
     C                     CALL 'AOACODR0'
     C                     PARM '*MSG    '@RTCD   7
     C                     PARM '*KEY    '@OPTN   7
     C**********           PARM WACOD     @ACCD   4                                           CGL029
     C********** SDACOD    PARM SDACOD    DSFDY                                               CGL029
     C                     PARM WACOD     @ACCD  10                                           CGL029
     C           SDACOD    PARM SDACOD    DSSDY                                               CGL029
      *
      ** Check if an error occurred
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVEL'SDACODPD'DBFILE           * DBERROR 002 *
     C                     MOVEL@ACCD     DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
     C                     ENDIF
      *
      **  If A/C posting retention period from SDACODPD (A5ACPR) is not
      **  zero, compute for the drop date by multiplying A5ACPR by 31
      **  and subtracting the figure from the rundate and use this in
      **  comparing against the posting date of the record in CPOSTDD.
      **  Otherwise use the drop date calculated using the A/C posting
      **  retention period from SDGELRPD.
      *
     C           A5ACPR    IFNE 0
     C           A5ACPR    MULT 31        WDROP2  40
     C           RUND      SUB  WDROP2    WDPDT2  50
     C                     Z-ADDWDPDT2    WDPDT
     C                     ELSE
     C                     Z-ADDWDPDT1    WDPDT
     C                     ENDIF
      *
      **  If Posting Date from CPOSTDD is less than the calculated drop
      **  date, delete the record from CPOSTDD. Otherwise, the record
      **  stays.
      *
     C           PSTD      IFLT WDPDT
     C                     DELETCPOSTD0                20
     C                     END
      *
      **  If Delete operation is not successful then execute *PSSR
      **  subroutine.
      *
     C           *IN20     IFNE '0'
     C                     EXSR *PSSR
     C                     END
      *
     CLR                   RETRN
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ** Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     END
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
**  CPY@ - OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
