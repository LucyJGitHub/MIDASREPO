     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL History Postings Enquiry')                    *
     F*****************************************************************
     F*                                                               *
     F*  Midas - General Ledger Module                                *
     F*                                                               *
     F*  GL8000 - History Postings Enquiry                            *
     F*                                                               *
     F*  Function: This program shows the history postings historic   *
     F*   (I/C)    carried forward statement balances and the         *
     F*            associated historic carried forward statement      *
     F*            balances.                                          *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      *  Last Amend No. MD031011           Date 25Nov14               *
      *  Prev Amend No. AR322849           Date 19Aug14               *
      *                 247378             Date 27May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CAAA03             Date 19Apr04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CGL007             Date 26Mar99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD005             Date 01Mar99               *
      *                 154040             Date 26Apr99               *
      *                 CTI002             Date 23SEP99               *
     F*                 S01520  *CREATE    DATE 02NOV94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD031011 - GL Historic Movements enquiry shows wrong Brought *
      *             Forward Balance.                                  *
      *           - Enhance AR322849 fix to correctly calculate the   *
      *             carried forward balance due to incorrect brought  *
      *             forward balance when the brought forward balance  *
      *             has posting on the same date.                     *
      *  AR322849 - No historic movements are displayed. Display all  *
      *             movements starting from the earliest posting date *
      *             No Historic enquiry possible before first day of  *
      *             current month (supress Setup of PUDT).            *
      *           - Applied for MD027366.                             *
      *  247378 - Fix program to display the error message when       *
      *           an invalid or non-existing record is keyed.         *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CAAA03 - Webfacing Changes (recompile)                       *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL007 - Account Postings Enquiry (Recompile)                *
      *  CSD005 - Standing Data Account Narrative.  Add new field     *
      *           to SDACODPD.  Recompilation of programs.            *
      *  154040 - Display all records greater than or equal to the    *
      *           start date but less than purge date, take into      *
      *           account the retention period .Apply 131884 .        *
      *  CTI002 - Recompiled : File SDACODPD changed                  *
     F*  S01520 - BLI Account Statements Changes.                     *
     F*                                                               *
     F*****************************************************************
     FACCNT   IF  E           K        DISK         KINFSR *PSSR
     F** Accounts Master File
     F*
     F            ACCNTAAF                          KIGNORE
     F            ACCNTACF                          KIGNORE
     FACNUM   IF  E           K        DISK         KINFSR *PSSR
     F** Account Numbers File
     F*
     F            ACCNTABF                          KRENAMEACCNUMF
     FAPOSTL1 IF  E           K        DISK         KINFSR *PSSR
     F** Accounts Postings File
     F*
     FCPOST   IF  E           K        DISK         KINFSR *PSSR
     F** Historic Balances File
     F*
     FCLINT   IF  E           K        DISK         KINFSR *PSSR
     F** Customer Master File
     F*
     F            CLINTCAF                          KIGNORE
     F            CLINTCCF                          KIGNORE
     F            CLINTC1F                          KIGNORE
     F            CLINTSEF                          KIGNORE
     FGL8000FMCF  E                    WORKSTN      KINFDS DSPFDS
     F** Historic Postings Enquiry Display
     F*
     F                                        RRN   KSFILE GL8000S1
     F/EJECT
     F*****************************************************************
     F*                                                               *
     F*     F U N C T I O N   O F   I N D I C A T O R S               *
     F*                                                               *
     F*    08            Roll up key pressed                          *
     F*    20            Multibranching indicator                     *
     F*    21            Retail module indicator                      *
     F*    22            Display subfile control                      *
     F*    23            Display subfile                              *
     F*    24            Display error codes                          *
     F*    25            Account status indicator                     *
     F*    26            SFLEND - Message subfile                     *
     F*    27            SFLEND - Record subfile                      *
     F*    30            Error on account number                      *
     F*    31            General error indicator                      *
     F*    32            CHAIN fail indicator - LF/ACNUM              *
     F*    33            Error on retail a/c number                   *
     F*    34            CHAIN fail indicator - LF/ACCNT              *
     F*    35            Error on start date                          *
     F*    36            CHAIN fail indicator - LF/CLINT              *
     F*    37            Equal record - LF/CPOST                      *
     F*    38            End of file indicator - LF/CPOST             *
     F*    39            End of file indicator - LF/CPOST             *
     F*    40            End of file indicator - LF/APOSTL1           *
     F*    41            B/FWD balance line displayed                 *
     F*    42            Invalid record                               *
     F*    43            Input field changed                          *
     F*    44            First pass in program                        *
     F*    60            Change in A/c number field                   *
     F*    61            Change in Retail A/c field                   *
     F*    62            Change in Start date field                   *
     F*    63            SFLMSGID - Subfile error message             *
     F*    64            Indicator to control subfile heading         *
     F*    65            Error on both A/c number and Retail A/c      *
     F*    66            Display complete narrative                   *
     F*    67            Indicator for action performed               *
     F*    68            SPF error                                    *
     F*    98            Date format is 'M'                           *
     F*    99            Standard indicator                           *
     F*    KC            F3 pressed                                   *
     F*    LR            End of program                               *
     F*    U7-U8         Databaser error                              *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F*                                                               *
     F*     S U B R O U T I N E S                                     *
     F*                                                               *
     F*    MAIN Routine                                               *
     F*    #INIT   - Initial processing                               *
     F*    #PROC   - Controlling routine                              *
     F*    #VALD   - Validate input                                   *
     F*    #ROLUP  - Roll-Up key pressed                              *
     F*    #SETFP  - Set file pointer                                 *
     F*    #LOAD   - Load records to subfile                          *
     F*    #BFWD   - B/FWD balance processing                         *
     F*    #CFWD   - C/FWD balance processing                         *
     F*    #STOR   - Store entered values into data structure         *
     F*    #RSTOR  - Restore saved fields                             *
     F*    #FLDC   - Check if input fields changed                    *
     F*    #PUDT   - Compute last purge date of the historic records  *
     F*    #ZASNMS - Send message to program's message queue          *
     F*    *PSSR   - Error control subroutine                         *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E*
     E                    CPY@    1   1 80               COPYRIGHT STATEMENT
     E                    ERCD       19  4               ERROR CODES
     E/COPY ZSRSRC,ZALIGNZ1
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZFRPEDZ1
     E*
     I/EJECT
     I*
     IAPOSTPDF
     I              RECI                            RECIA
     I*
     IGLACPHPF
     I              RECI                            RECIA
     I*
     ICPOSTD0
     I              RECI                            RECIC
     I              BRCA                            BRCAC
     I              CNUM                            CNUMC
     I              CCY                             CCYC
     I              ACOD                            ACODC
     I              ACSQ                            ACSQC
     I              PSTD                            PSTDC
     I              HCFB                            HCFBC
     I*
     IACNODS      DS
     I                                        1   6 CNUMR
     I                                        7   9 CURRY
     I**********                             10  13 ACCOD                                     CGL029
     I**********                             14  15 ACSEQ                                     CGL029
     I**********                             16  18 BRNCH                                     CGL029
     I**********                              1  18 WACCN                                     CGL029
     I**********                              1  15 WACN                                      CGL029
     I**********                              1  18 MBACNT                                    CGL029
     I**********                              1  15 ACNT                                      CGL029
     I                                       10  19 ACCOD                                     CGL029
     I                                       20  21 ACSEQ                                     CGL029
     I                                       22  24 BRNCH                                     CGL029
     I                                        1  24 WACCN                                     CGL029
     I                                        1  21 WACN                                      CGL029
     I                                        1  24 MBACNT                                    CGL029
     I                                        1  21 ACNT                                      CGL029
     I            DS
     I** Data structure for error codes
     I*
     I                                        1  76 ERCD
     I                                        1  76 ERRMSG
     I            DS
     I** Data structure for last purge date
     I*
     I                                        1   60WRUN6
     I                                        1   20WDD
     I                                        3   40WMM
     I                                        5   60WYY
     I            DS
     I** Data structure for comparison
     I*
     I                                        1   3 BRCAC
     I**********                              4   90CNUMC                                     CSD027
     I                                        4   9 CNUMC                                     CSD027
     I                                       10  12 CCYC
     I**********                             13  160ACODC                                     CGL029
     I**********                             17  180ACSQC                                     CGL029
     I**********                              1  18 WACCC                                     CGL029
     I                                       13  220ACODC                                     CGL029
     I                                       23  240ACSQC                                     CGL029
     I                                        1  24 WACCC                                     CGL029
     I            DS
     I** Data structure for comparison
     I*
     I                                        1   3 BRCA
     I**********                              4   90CNUM                                      CSD027
     I                                        4   9 CNUM                                      CSD027
     I                                       10  12 CCY
     I**********                             13  160ACOD                                      CGL029
     I**********                             17  180ACSQ                                      CGL029
     I**********                              1  18 WACCP                                     CGL029
     I                                       13  220ACOD                                      CGL029
     I                                       23  240ACSQ                                      CGL029
     I                                        1  24 WACCP                                     CGL029
     I            DS
     I** Data structure for Account Number display
     I*
     I                                        1   3 WBRCA
     I                                        4   4 WDAS1
     I                                        5  10 WCNOM
     I                                       11  11 WDAS2
     I                                       12  14 WCURR
     I                                       15  15 WDAS3
     I**********                             16  19 WCODE                                     CGL029
     I**********                             20  20 WDAS4                                     CGL029
     I**********                             21  22 WSEQ                                      CGL029
     I**********                              5  22 ACNOS                                     CGL029
     I**********                              1  22 ACNOM                                     CGL029
     I                                       16  25 WCODE                                     CGL029
     I                                       26  26 WDAS4                                     CGL029
     I                                       27  28 WSEQ                                      CGL029
     I                                        5  28 ACNOS                                     CGL029
     I                                        1  28 ACNOM                                     CGL029
     IRUNDAT    E DS
     I** Data area for multibranching indicator
     I*
     IZMUSER    E DS
     I** Data area for default branch
     I*
     IPSDS       SDS
     I** Program status data structure
     I*
     I                                      244 253 SWSID
     I                                      254 263 SUSER
     I*
     ILDA         DS                            256
     I** Local data area for database error details
     I*
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I** Text for literals used on report
     I*
     I              'B/FWD BALANCE'       C         BNARR
     I              'C/FWD BALANCE'       C         CNARR
     I              ' * COMPLETE *'       C         SNARR
     I*
     ISDBANK    E DSSDBANKPD
     I** External data structure for Bank Details
     I*
     ISDACOD    E DSSDACODPD
     I** External data structures for Account Code Details
     I*
     ISDGELR    E DSSDGELRPD
     I              QQACCD                          QQACD1                                    CGL029
     I** External data structure for General Ledger Details
     I*
     ISDMMOD    E DSSDMMODPD
     I** External data structure for Midas Module Details
     I*
     ISDCURR    E DSSDCURRPD
     I** External data structure for Currency Details
     I*
     IDSFDY     E DSDSFDY
     I**  Short data structure for access objects
     I*
     IDSSDY     E DSDSSDY
     I**  Long data structure for access programs
     I*
     I/COPY WNCPYSRC,GL8000I001
     IDSPFDS      DS
     I**  DSPF DS  to retrieve CPF'S position within the subfile
     I*
     I                                    B 378 3790CPFPOS
     I** Data structure for ZFRPED
     I/COPY ZSRSRC,ZFRPEDZ2
     I/EJECT
     C/TITLE Main Processing
     C*================================================================
     C*  P R O G R A M   S T A R T                                    *
     C*================================================================
     C*
     C** Perform initial processing
     C*
     C                     EXSR #INIT
     C*
     C** Clear subfile
     C*
     C                     SETOF                     2223
     C                     WRITEGL8000C1
     C*
     C** Set ON indicator for subfile control record
     C*
     C                     SETON                     22
     C*
     C** DO while F3 not pressed
     C*
     C           *INKC     DOWEQ'0'
     C*
     C** Write screen heading
     C*
     C                     WRITEGL8000H1
     C*
     C** Write message subfile for any SPF error
     C*
     C                     WRITEGL8000C2
     C*
     C** Accept input
     C*
     C                     EXFMTGL8000C1
     C*
     C** Set OFF indicator for action performed
     C*
     C                     SETOF                     67
     C*
     C** Clear screen message queue
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM           WWTPGQ 10
     C                     PARM           WWPGQR  5
     C*
     C** Check if Roll-up key pressed
     C*
     C           *IN08     IFEQ '1'
     C                     SETON                     67
     C                     EXSR #ROLUP
     C                     END
     C*
     C** Check if input fields changed
     C*
     C           *IN67     IFEQ '0'
     C                     EXSR #FLDC
     C                     END
     C*
     C** If there is a change in input fields, process details
     C*
     C           *IN67     IFEQ '0'
     C           *IN43     IFEQ '1'
     C                     EXSR #PROC
     C*
     C** If no change, ensure that the subfile is redisplayed at the
     C** position
     C*
     C                     ELSE
     C                     Z-ADDCPFPOS    DSPREC
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C** End program
     C*
     C                     SETON                     LR
     C                     RETRN
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #INIT  -  Initial processing                                  *
     C*                                                               *
     C* Calls:  ZVBBU, ZASNMS                                         *
     C*                                                               *
     C* Called from: Main processing                                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           #INIT     BEGSR                           *** #INIT ***
     C*
     C** Copyright statement
     C*
     C                     MOVEACPY@      ACT@   80
     C*
     C** Key list used to chain to LF/ACCNT
     C*
     C           KFLDA     KLIST
     C                     KFLD           CNUM
     C                     KFLD           CCY
     C                     KFLD           ACOD
     C                     KFLD           ACSQ
     C                     KFLD           BRCA
     C*
     C** Key list used to chain to LF/ACCNT
     C*
     C           KFLDB     KLIST
     C                     KFLD           WCNUM
     C                     KFLD           CURRY
     C                     KFLD           WACOD
     C                     KFLD           WACSQ
     C                     KFLD           BRNCH
     C*
     C** Key list used to chain to LF/CLINT
     C*
     C           KFLDC     KLIST
     C                     KFLD           WCNO
     C                     KFLD           WRCDT
     C*
     C** Key list used to chain to LF/CPOST
     C*
     C           KFLDD     KLIST
     C                     KFLD           BRCA
     C                     KFLD           CNUM
     C                     KFLD           CCY
     C                     KFLD           ACOD
     C                     KFLD           ACSQ
     C                     KFLD           KDATE
     C*
     C** Key list used to set record pointer to LF/APOSTL1
     C*
     C           KFLDE     KLIST
     C                     KFLD           BRCA
     C                     KFLD           CNUM
     C                     KFLD           CCY
     C                     KFLD           ACOD
     C                     KFLD           ACSQ
     C                     KFLD           SDATE
     C*
     C** Key list used to read record from LF/APOSTL1
     C*
     C           KFLDF     KLIST
     C                     KFLD           BRCA
     C                     KFLD           CNUM
     C                     KFLD           CCY
     C                     KFLD           ACOD
     C                     KFLD           ACSQ
     C*
     C** Initialise indicators
     C*
     C                     SETOF                     202198
     C                     SETOF                     68
     C*
     C** Initialise LDA
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'GL8000'  DBPGM
     C                     Z-ADD0         DBASE
     C                     OUT  LDA
     C*
     C** Initialise screen message queue
     C*
     C                     MOVEL'*'       DDPGMQ
     C                     MOVE *BLANKS   WWTPGQ 10
     C                     MOVEL'*'       WWTPGQ
     C                     MOVE *BLANKS   WWPGQR  5
     C                     MOVEL'*PRV'    WWPGQR
     C                     MOVE '1'       *IN26
     C*
     C                     MOVE *BLANKS   ZAMSGF
     C                     MOVEL'GLUSRMSG'ZAMSGF
     C*
     C** Read in ZMUSER
     C*
     C           *NAMVAR   DEFN           ZMUSER
     C                     IN   ZMUSER
     C                     UNLCKZMUSER
     C*
     C** Read in RUNDAT for multibranching indicator
     C*
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C                     UNLCKRUNDAT
     C                     Z-ADDAGRDNB    WRNDT   50
     C*
     C/COPY WNCPYSRC,GL8000C001
     C** Check if the system is on multi-branch
     C*
     C           AGMBIN    IFEQ 'Y'
     C                     MOVE '1'       *IN20
     C                     END
     C*
     C** If system is on multi-branch, check if user is authorised to
     C** the booking branch
     C*
     C           *IN20     IFEQ '1'
     C                     CALL 'ZVBBU'
     C                     PARM DBRN      WDBRN
     C                     PARM *ZERO     WERCO
     C*
     C** Display error message if user not authorised
     C*
     C           WERCO     IFNE 0
     C                     SETON                     68
     C                     MOVEL'GLM0012' ZAMSID
     C                     EXSR ZASNMS
     C                     END
     C                     END
     C*
     C** Get bank details using access object
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   P@RTCD  7
     C                     PARM '*FIRST ' P@OPTN  7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C** Check if an error occurred
     C*
     C           P@RTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE           **************
     C                     Z-ADD01        DBASE            * DBERROR 01 *
     C                     OUT  LDA                        **************
     C                     EXSR *PSSR
     C                     END
     C*
     C** Rundate in DDMMMYY format
     C*
     C                     MOVE BJMRDT    SRUNA
     C*
     C** Get Midas module details using access object
     C*
     C                     CALL 'AOMMODR0'
     C                     PARM *BLANKS   P@RTCD
     C                     PARM '*FIRST ' P@OPTN
     C           SDMMOD    PARM SDMMOD    DSFDY
     C*
     C** Check if an error occurred
     C*
     C           P@RTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDMMODPD'DBFILE           **************
     C                     Z-ADD02        DBASE            * DBERROR 02 *
     C                     OUT  LDA                        **************
     C                     EXSR *PSSR
     C                     END
     C*
     C** Check if Retail Module is ON
     C*
     C           BGRTBN    IFEQ 'Y'
     C                     SETON                     21
     C                     END
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #PROC  -  Controlling routine                                 *
     C*                                                               *
     C* Calls:  #VALD, #STOR, #BFWD, #SETFP                           *
     C*                                                               *
     C* Called from: Main processing                                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           #PROC     BEGSR                           *** #PROC ***
     C*
     C** Clear subfile
     C*
     C                     SETOF                     2223
     C                     MOVE '0'       *IN27                                             AR322849
     C                     WRITEGL8000C1
     C                     SETON                     22
     C*
     C** Validate entry
     C*
     C                     EXSR #VALD
     C*
     C** Write subfile control record to display error codes, if any
     C*
     C                     WRITEGL8000C1
     C*
     C** If no error occurred, display statement balances
     C*
     C           *IN31     IFEQ '0'
     C           *IN68     ANDEQ'0'
     C                     EXSR #STOR
     C                     EXSR #BFWD
     C                     EXSR #SETFP
     C                     END
     C*
     C** Display only customer and account details if no error on input
     C*
     C           *IN31     IFEQ '1'
     C           *IN68     OREQ '1'
     C                     SETOF                     64
     C                     ELSE
     C*
     C** Move fields for display
     C*
     C                     MOVE BRCA      WBRCA
     C                     MOVE CNUM      WCNOM
     C                     MOVE CCY       WCURR
     C                     MOVE ACOD      WCODE
     C                     MOVE ACSQ      WSEQ
     C                     MOVE '-'       WDAS1
     C                     MOVE '-'       WDAS2
     C                     MOVE '-'       WDAS3
     C                     MOVE '-'       WDAS4
     C                     SETON                     64
     C                     END
     C*
     C                     WRITEGL8000F1
     C*
     C** Write subfile heading record
     C*
     C                     WRITEGL8000F2
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #VALD  -  Validate input                                      *
     C*                                                               *
     C* Calls:  #PUDT                                                 *
     C*                                                               *
     C* Called from:  #PROC                                           *
     C*                                                               *
     C*****************************************************************
     C*
     C           #VALD     BEGSR                           *** #VALD ***
     C*
     C** Clear error code array and initialise indicators
     C*
     C                     MOVE *BLANKS   ERCD
     C                     SETOF                     303133
     C                     SETOF                     356568
     C                     SETOF                     66
     C                     Z-ADD1         I       20
     C*
     C** Only one of the A/c number and the Retail A/c number should be
     C** entered.  If both are entered, flag as error.  Check this only
     C** if retail module is ON.  If the system is multibranching be
     C** sure to include the branch code in the validation.
     C*
     C           *IN21     IFEQ '1'
     C           *IN20     IFEQ '1'
     C           WACCN     IFNE *BLANKS
     C           RTAC      ANDNE*BLANKS
     C                     SETON                     6531
     C                     MOVE ' 001'    ERCD,I
     C                     ADD  1         I
     C                     END
     C                     ELSE
     C           WACN      IFNE *BLANKS
     C           RTAC      ANDNE*BLANKS
     C                     SETON                     6531
     C                     MOVE ' 001'    ERCD,I
     C                     ADD  1         I
     C                     END
     C                     END
     C                     END
     C*
     C** If no A/c number or Retail A/c number is entered, flag as
     C** error
     C*
     C           WACCN     IFEQ *BLANKS
     C           RTAC      ANDEQ*BLANKS
     C                     SETON                     6531
     C                     MOVE ' 007'    ERCD,I
     C                     ADD  1         I
     C                     END
     C*
     C** VALIDATE RETAIL A/C NUMBER
     C*
     C** Validate retail A/c if retail module is ON and no previous
     C** error occurred
     C*
     C           *IN21     IFEQ '1'
     C           *IN31     ANDEQ'0'
     C           RTAC      ANDNE*BLANKS
     C                     MOVELRTAC      WREAC  100
     C*
     C** If Retail A/c number is not on account numbers file, flag as
     C** error
     C*
     C           WREAC     CHAINACCNUMF              32
     C           *IN32     IFEQ '1'
     C                     SETON                     3331
     C                     MOVE ' 002'    ERCD,I
     C                     ADD  1         I
     C                     ELSE
     C*
     C** If Retail A/c number exists on account numbers file, get full
     C** account's detail from accounts master file
     C*
     C           KFLDA     CHAINACCNT                34
     C*
     C** Flag as error if not found
     C*
     C           *IN34     IFEQ '1'
     C                     SETON                     3331
     C                     MOVE ' 003'    ERCD,I
     C                     ADD  1         I
     C                     ELSE
     C*
     C** If account status is 'C', flag as closed account
     C*
     C           ACST      IFEQ 'C'
     C                     SETON                     25
     C                     ELSE
     C                     SETOF                     25
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C** VALIDATE ACCOUNT NUMBER
     C*
     C** Validate A/c number if no previous error occurred
     C*
     C           *IN31     IFEQ '0'
     C           WACCN     ANDNE*BLANKS
     C*
     C** If no entry on customer number field, flag as error
     C*
     C           CNUMR     IFEQ *BLANKS
     C                     SETON                     3031
     C                     MOVE ' 004'    ERCD,I
     C                     ADD  1         I
     C*
     C** If no entry on branch field, use default branch from ZMUSER
     C*
     C                     ELSE
     C           BRNCH     IFEQ *BLANKS
     C           CNUMR     ANDNE*BLANKS
     C           CURRY     ANDNE*BLANKS
     C           ACCOD     ANDNE*BLANKS
     C           ACSEQ     ANDNE*BLANKS
     C                     MOVELDBRN      BRNCH
     C                     END
     C**********           MOVELCNUMR     WCNUM   60                                          CSD027
     C                     MOVELCNUMR     WCNUM   6                                           CSD027
     C**********           MOVELACCOD     WACOD   40                                          CGL029
     C                     MOVELACCOD     WACOD  100                                          CGL029
     C                     MOVELACSEQ     WACSQ   20
     C*
     C** Entry must be on accounts master file, otherwise, error
     C*
     C           KFLDB     CHAINACCNT                34
     C/COPY WNCPYSRC,GL8000C003
     C           *IN34     IFEQ '1'
     C                     SETON                     3031
     C                     MOVE ' 005'    ERCD,I
     C                     ADD  1         I
     C*
     C/COPY WNCPYSRC,GL8000C002
     C** If system is on multi-branch, check if user is authorised to
     C** the booking branch
     C*
     C                     ELSE
     C           *IN20     IFEQ '1'
     C                     MOVE BRNCH     WDBRN   3
     C                     CALL 'ZVBBU'
     C                     PARM           BRNCH
     C                     PARM *ZERO     WERCO   10
     C*
     C** Display error message if user not authorised
     C*
     C           WERCO     IFNE 0
     C                     SETON                     3068
     C           ATYP      IFEQ 'R'
     C                     MOVEL'GLM0013' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
     C                     MOVEL'GLM0012' ZAMSID
     C                     EXSR ZASNMS
     C                     END
     C                     END
     C                     END
     C*
     C** If no previous error check account status
     C*
     C           *IN31     IFEQ '0'
     C           *IN68     ANDEQ'0'
     C           ACST      IFEQ 'C'
     C                     SETON                     25
     C                     ELSE
     C                     SETOF                     25
     C                     END
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C** VALIDATE START DATE
     C*
     C** Compute for the last purge date of the historic records
     C*
     C**********           EXSR #PUDT                                                       AR322849
     C*
     C** If Start Date is entered
     C*
     C           STDT      IFNE *BLANKS
     C*
     C** Check if value entered is numeric using Midas standard
     C** subroutine
     C*
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE STDT      ZFIELD
     C                     Z-ADD0         ZADEC
     C                     Z-ADD6         ZADIG
     C                     EXSR ZALIGN
     C*
     C** Set up error code if error
     C*
     C           *IN99     IFEQ '1'
     C                     MOVE ' 006'    ERCD,I
     C                     ADD  1         I
     C                     SETON                     3135
     C*
     C** Else, convert valid numeric to Midas day number
     C*
     C                     ELSE
     C*
     C** Check system date format
     C*
     C           AGDFF     IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     END
     C                     MOVE ZFIELD    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    SDATE   50
     C*
     C** Set up error code if error
     C*
     C           *IN99     IFEQ '1'
     C                     MOVE ' 006'    ERCD,I
     C                     ADD  1         I
     C                     SETON                     3135
     C*
     C** Else, further validate,  must be before the run date
     C*
     C                     ELSE
     C           SDATE     IFGE WRNDT
     C                     MOVE ' 006'    ERCD,I
     C                     ADD  1         I
     C                     SETON                     3135
     C*
     C** If start date entered is less than the last purge date,
     C** change the start date to last purge date
     C*
     C                     ELSE
     C           SDATE     IFLT PUDT
     C                     Z-ADDPUDT      SDATE
     C                     END
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C** If no value entered on Start Date, default the date to the
     C** last purge date of the historic records
     C*
     C                     ELSE
     C                     Z-ADDPUDT      SDATE
     C                     END
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #ROLUP  -  Processing when Roll-Up key is pressed             *
     C*                                                               *
     C* Calls:  #LOAD, #FLDC, #RSTOR                                  *
     C*                                                               *
     C* Called from:  Main processing                                 *
     C*                                                               *
     C*****************************************************************
     C*
     C           #ROLUP    BEGSR                           *** #ROLUP ***
     C*
     C** Check if input fields changed
     C*
     C                     EXSR #FLDC
     C*
     C** If there is a change in input fields, restore original values
     C*
     C           *IN43     IFEQ '1'
     C                     EXSR #RSTOR
     C                     END
     C*
     C** Set subfile record number
     C*
     C                     Z-ADDRRN       DSPREC
     C*
     C** Load subfile records
     C*
     C                     EXSR #LOAD
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #SETFP  -  Set file pointer                                   *
     C*                                                               *
     C* Calls:  #LOAD                                                 *
     C*                                                               *
     C* Called from:  #PROC                                           *
     C*                                                               *
     C*****************************************************************
     C*
     C           #SETFP    BEGSR                           *** #SETFP ***
     C*
     C** Set record pointer
     C*
     C           KFLDE     SETLLAPOSTL1
     C*
     C** Read an equal record
     C*
     C           KFLDF     READEAPOSTL1                  40
     C*
     C***Display only records with posting date less than or equal to     154040
     C***the*last statement date                                          154040
     C**********                                                          154040
     C********** *IN40     IFEQ '0'                                       154040
     C********** PSTD      ANDGTLSTD                                      154040
     C**********           SETON                     40                   154040
     C**********           END                                            154040
     C*
     C** Load subfile records
     C*
     C                     Z-ADD1         RRN     40                                        AR322849
      *                                                                                     MD031011
     C           DRCR      IFEQ 1                                                           MD031011
     C           PSTA      MULT -1        PSTAMT 130                                        MD031011
     C                     END                                                              MD031011
      *                                                                                     MD031011
     C                     EXSR #LOAD
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #LOAD  -  Load records to subfile                             *
     C*                                                               *
     C* Calls:  ZFRPED, ZDATE2, #CFWD                                 *
     C*                                                               *
     C* Called from:  #ROLUP, #SETFP                                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           #LOAD     BEGSR                           *** #LOAD ***
     C*
     C** If first line has been written to subfile do not reset counter
     C*
     C********** *IN41     IFEQ '0'                                                         AR322849
     C                     Z-ADDRRN       DSPREC
     C                     Z-ADD0         COUNT   30
     C**********           ELSE                                                             AR322849
     C**********           SETOF                     41                                     AR322849
     C**********           END                                                              AR322849
     C*
     C** DO while subfile page not full
     C*
     C           COUNT     DOWLT12
     C                     SETOF                     42
     C*
     C** If an equal record read, continue to process record
     C** Process only records with posting date greater than or equal to  154040
     C** the start date                                                   154040
     C*
     C           *IN40     IFEQ '0'
     C           PSTD      ANDGESDATE                                     154040
      *                                                                                     AR322849
      ** Check if posting date equal to the opening balance date.                           AR322849
      *                                                                                     AR322849
     C           PSTD      IFGE SDATEX                                                      AR322849
     C           *IN41     ANDEQ'0'                                                         AR322849
      *                                                                                     MD031011
      ** If posting date same as start date, use 1 day before posting                       MD031011
      ** date                                                                               MD031011
      *                                                                                     MD031011
     C           PSTD      IFGT SDATEX                                                      MD031011
     C                     MOVE DATE1X    DATE1                                             AR322849
     C                     ELSE                                                             MD031011
     C           PSTAMT    IFEQ TOTAL                                                       MD031011
     C                     SUB  PSTAMT    TOTAL                                             MD031011
     C                     ENDIF                                                            MD031011
     C                     MOVE DATE0X    DATE1                                             MD031011
      *                                                                                     MD031011
      ** Format amount for display                                                          MD031011
      *                                                                                     MD031011
     C                     Z-ADDA6NBDP    ZDECS                                             MD031011
     C                     Z-ADDTOTAL     ZFLD15                                            MD031011
     C                     MOVE 'A'       ZECODE                                            MD031011
     C                     EXSR ZFRPED                                                      MD031011
     C                     MOVE ZOUT22    PSTAMX                                            MD031011
     C                     ENDIF                                                            MD031011
      *                                                                                     MD031011
     C                     MOVE PSTAMX    PSTAM                                             AR322849
     C                     MOVELNARRX     NARR                                              AR322849
     C                     MOVE *BLANKS   VALDAT                                            AR322849
     C                     WRITEGL8000S1                                                    AR322849
     C                     ADD  1         RRN                                               AR322849
     C                     ADD  1         COUNT                                             AR322849
     C                     MOVE '1'       *IN23                                             AR322849
     C                     MOVE '1'       *IN41                                             AR322849
     C                     ENDIF                                                            AR322849
     C*
     C** Select only posting records
     C*
     C           RECIA     IFNE 'P'
     C                     SETON                     42
     C                     ELSE
     C*
     C** Set up proper posting amount (i.e, DEBIT/CREDIT)
     C*
     C           DRCR      IFEQ 1
     C           PSTA      MULT -1        PSTA
     C                     END
     C*
     C** Accumulate posting amount
     C*
     C           PSTD      IFGE SDATEX                                                      AR322849
     C           TOTAL     ADD  PSTA      TOTAL
     C                     ENDIF                                                            AR322849
     C                     Z-ADDPSTA      WAM1
     C*
     C** Format amount for display
     C*
     C                     Z-ADDA6NBDP    ZDECS
     C                     Z-ADDWAM1      ZFLD15
     C                     MOVE 'A'       ZECODE
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    PSTAM
     C*
     C** Format line narrative
     C*
     C                     MOVELPNAR      NARR
     C*
     C** Check system date format
     C*
     C           AGDFF     IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     END
     C*
     C** Convert date to date format
     C*
     C                     Z-ADDPSTD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    DATE1
     C*
     C** Value date should only be shown on screen if it is not equal
     C** to posting date
     C*
     C           PSTD      IFNE VALD
     C                     Z-ADDVALD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    VALDAT
     C                     ELSE
     C                     MOVE *BLANKS   VALDAT
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C** If valid record, write to subfile
     C** Write only records with posting date greater than or equal to    154040
     C** the start date                                                   154040
     C*
     C           *IN42     IFEQ '0'
     C           *IN40     ANDEQ'0'
     C           PSTD      ANDGESDATE                                     154040
     C                     WRITEGL8000S1
     C                     ADD  1         RRN
     C                     ADD  1         COUNT
     C                     SETON                     23
     C                     END
     C*
     C** Read an equal record
     C*
     C           KFLDF     READEAPOSTL1                  40
     C*
     C***Display*only*records*with*posting date less than or equal to     154040
     C***the*last*statement*date                                          154040
     C**********                                                          154040
     C********** *IN40     IFEQ '0'                                       154040
     C********** PSTD      ANDGTLSTD                                      154040
     C**********           SETON                     40                   154040
     C**********           END                                            154040
     C*
     C** If end of file
     C*
     C           *IN40     IFEQ '1'
     C           *IN41     IFEQ '0'                                                         AR322849
     C                     MOVE DATE1X    DATE1                                             AR322849
     C                     MOVE PSTAMX    PSTAM                                             AR322849
     C                     MOVELNARRX     NARR                                              AR322849
     C                     MOVE *BLANKS   VALDAT                                            AR322849
     C                     WRITEGL8000S1                                                    AR322849
     C                     ADD  1         RRN                                               AR322849
     C                     ADD  1         COUNT                                             AR322849
     C                     SETON                     23                                     AR322849
     C                     MOVE '1'       *IN41                                             AR322849
     C                     ENDIF                                                            AR322849
     C                     SETON                     27
     C*
     C** Display last subfile line (C/FWD BALANCE)
     C*
     C           COUNT     IFLT 12
     C*
     C** Do not display C/FWD balance line if it falls on last sufile
     C** line
     C*
     C           COUNT     IFEQ 11
     C                     MOVE *BLANKS   DATE1
     C                     MOVE *BLANKS   VALDAT
     C                     MOVE *BLANKS   NARR
     C                     MOVE *BLANKS   PSTAM
     C                     WRITEGL8000S1
     C                     ADD  1         RRN
     C                     SETOF                     27
     C                     ELSE
     C           *IN66     IFEQ '0'
     C                     EXSR #CFWD
     C                     END
     C*
     C** Display 'COMPLETE' narrative
     C*
     C           COUNT     IFLT 12
     C                     MOVE *BLANKS   NARR
     C                     MOVELSNARR     NARR
     C                     MOVE *BLANKS   VALDAT
     C                     MOVE *BLANKS   DATE1
     C                     MOVE *BLANKS   PSTAM
     C                     WRITEGL8000S1
     C                     ADD  1         RRN
     C                     ELSE
     C                     SETOF                     27
     C                     END
     C                     END
     C                     ELSE
     C                     SETOF                     27
     C                     END
     C                     Z-ADD13        COUNT
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #BFWD  -  B/FWD balance processing                            *
     C*                                                               *
     C* Calls:  *PSSR, ZDATE2, ZFRPED                                 *
     C*                                                               *
     C* Called from:  #PROC                                           *
     C*                                                               *
     C*****************************************************************
     C*
     C           #BFWD     BEGSR                           *** #BFWD ***
     C*
     C** Using customer number, chain to CLINTCB file to get the
     C** customer name and town
     C*
     C**********           MOVELCNUM      WCNO    60                                          CSD027
     C                     MOVELCNUM      WCNO    6                                           CSD027
     C                     MOVE 'A'       WRCDT   1
     C           KFLDC     CHAINCLINT                36
     C*
     C** Check if an error occurred
     C*
     C           *IN36     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVEL'CLINTCB 'DBFILE           **************
     C                     Z-ADD03        DBASE            * DBERROR 03 *
     C                     OUT  LDA                        **************
     C                     EXSR *PSSR
     C                     END
     C*
     C** Initialise totol amount
     C*
     C                     Z-ADD0         TOTAL  150
     C*
     C***If*the*start*date*is*equal*to*the*last*purge*date,*use*the       154040
     C***Account's*Historic*C/fwd*balance*from*the*ACCNT*file*(HCFB)      154040
     C***as*the*opening*balance*1i.e,*B/FWD balance*on*screen)*****       154040
     C***********                                                         154040
     C***********SDATE     IFEQ PUDT                                      154040
     C***********          Z-ADDSDATE     SDATEX  50                      154040
     C***********          Z-ADDHCFB      WAM1   150                      154040
     C***********          Z-ADDHCFB      TOTAL                           154040
     C***********SDATE     ADD  1         SDATE                           154040
     C***********                                                         154040
     C***If*the*start*date*is*not*equal*to*the*last*purge*date,*chain     154040
     C***to*CPOST*file*using*the*start*date*entered                       154040
     C***********                                                         154040
     C***********          ELSE                                           154040
     C***********          Z-ADDSDATE     KDATE   50                      154040
     C***********KFLDD     SETLLCPOST                    37               154040
     C***********                                                         154040
     C***If*and*qual*key*found,*use*the*balance*on*this*record*as*the     154040
     C***opening*balance                                                  154040
     C***********                                                         154040
     C************IN37     IFEQ '1'                                       154040
     C***********          READ CPOST                    39               154040
     C***********          Z-ADDHCFBC     WAM1                            154040
     C***********          Z-ADDHCFBC     TOTAL                           154040
     C***********          Z-ADDPSTDC     SDATE                           154040
     C***********          Z-ADDSDATE     SDATEX                          154040
     C***********PSTDC     ADD  1         SDATE                           154040
     C***********                                                         154040
     C***If*no*equal*key*found,*read*previous*record                      154040
     C***********                                                         154040
     C***********          ELSE                                           154040
     C***********          READPCPOST                    38               154040
     C***********                                                         154040
     C***If*another*account*found,*use*HCFB*on*ACCNT                      154040
     C***********                                                         154040
     C***********WACCC     IFNE WACCP                                     154040
     C***********          Z-ADDHCFB      WAM1                            154040
     C***********          Z-ADDHCFB      TOTAL                           154040
     C***********          Z-ADDPUDT      SDATE                           154040
     C***********          Z-ADDSDATE     SDATEX                          154040
     C***********PUDT      ADD  1         SDATE                           154040
     C***********                                                         154040
     C***If*same*A/c,*use*that*record                                     154040
     C***********                                                         154040
     C***********          ELSE                                           154040
     C***********          Z-ADDHCFBC     WAM1                            154040
     C***********          Z-ADDHCFBC     TOTAL                           154040
     C***********          Z-ADDPSTDC     SDATE                           154040
     C***********          Z-ADDSDATE     SDATEX                          154040
     C***********PSTDC     ADD  1         SDATE                           154040
     C***********          END                                            154040
     C***********          END                                            154040
     C***********          END                                            154040
     C*                                                                   154040
     C** If the start date is equal to the last statement date,           154040
     C** Use the Account's historic C/fwd balance from the ACCNT file     154040
     C** (HCFB) as the opening balance (i.e, B/FWD balance on screen)     154040
     C*                                                                   154040
     C           SDATE     IFEQ LSTD                       IF1            154040
     C                     Z-ADDSDATE     SDATEX  50                      154040
     C                     Z-ADDHCFB      WAM1   150                      154040
     C                     Z-ADDHCFB      TOTAL                           154040
     C********** SDATE     ADD  1         SDATE                                      154040 AR322849
     C*                                                                   154040
     C** If the start date is not equal to the last statement date, chain 154040
     C** to CPOST file using the start date entered                       154040
     C*                                                                   154040
     C                     ELSE                            ELSE1          154040
     C                     Z-ADDSDATE     KDATE   50                      154040
     C           KFLDD     SETLLCPOST                    37               154040
     C*                                                                   154040
     C** If an equal key found, use the balance on this record as the     154040
     C** opening balance                                                  154040
     C*                                                                   154040
     C           *IN37     IFEQ *ON                        IF2            154040
     C                     READ CPOST                    39               154040
     C                     Z-ADDHCFBC     WAM1                            154040
     C                     Z-ADDHCFBC     TOTAL                           154040
     C**********           Z-ADDPSTDC     SDATE                                      154040 AR322849
     C**********           Z-ADDSDATE     SDATEX                                     154040 AR322849
     C                     Z-ADDPSTDC     SDATEX                                            AR322849
     C********** PSTDC     ADD  1         SDATE                                      154040 AR322849
     C*                                                                   154040
     C** If no equal key found, read previous record                      154040
     C*                                                                   154040
     C                     ELSE                            ELSE2          154040
     C                     READPCPOST                    38               154040
     C*                                                                   154040
     C** If same account found, use that record                           154040
     C*                                                                   154040
     C           WACCC     IFEQ WACCP                      IF3            154040
     C                     Z-ADDHCFBC     WAM1                            154040
     C                     Z-ADDHCFBC     TOTAL                           154040
     C**********           Z-ADDPSTDC     SDATE                                      154040 AR322849
     C**********           Z-ADDSDATE     SDATEX                                     154040 AR322849
     C********** PSTDC     ADD  1         SDATE                                      154040 AR322849
     C                     Z-ADDPSTDC     SDATEX                                            AR322849
     C*                                                                   154040
     C** If another account found, read the next changed record           154040
     C*                                                                   154040
     C                     ELSE                            ELSE3          154040
     C           KFLDF     SETLLCPOST                    37               154040
     C*                                                                   154040
     C** If an equal key found, use the balance on this record as the     154040
     C** opening balance                                                  154040
     C*                                                                   154040
     C           *IN37     IFEQ *ON                        IF4            154040
     C           KFLDF     READECPOST                    37               154040
     C                     Z-ADDHCFBC     WAM1                            154040
     C                     Z-ADDHCFBC     TOTAL                           154040
     C**********           Z-ADDPSTDC     SDATE                                      154040 AR322849
     C**********           Z-ADDSDATE     SDATEX                                     154040 AR322849
     C********** PSTDC     ADD  1         SDATE                                      154040 AR322849
     C                     Z-ADDPSTDC     SDATEX                                            AR322849
     C*                                                                   154040
     C** If no equal key found, use the record in the account file        154040
     C** Use the Account's historic C/fwd balance from the ACCNT file     154040
     C** (HCFB) as the opening balance (i.e, B/FWD balance on screen)     154040
     C                     ELSE                            ELSE4          154040
     C                     Z-ADDSDATE     SDATEX  50                      154040
     C                     Z-ADDHCFB      WAM1   150                      154040
     C                     Z-ADDHCFB      TOTAL                           154040
     C********** SDATE     ADD  1         SDATE                                      154040 AR322849
     C                     END                             END4           154040
     C*                                                                   154040
     C                     END                             END3           154040
     C*                                                                   154040
     C                     END                             END2           154040
     C*                                                                   154040
     C                     END                             END1           154040
     C*
     C** Check system date format
     C*
     C           AGDFF     IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     END
     C                     Z-ADDSDATEX    ZDAYNO
     C                     EXSR ZDATE2
     C**********           MOVE ZADATE    DATE1                                             AR322849
     C                     MOVE ZADATE    DATE1X  7                                         AR322849
      *                                                                                     MD031011
     C                     SUB  1         ZDAYNO                                            MD031011
     C                     EXSR ZDATE2                                                      MD031011
     C                     MOVE ZADATE    DATE0X  7                                         MD031011
     C*
     C** Get currency decimal places
     C*
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   P@RTCD
     C                     PARM '*KEY   ' P@OPTN
     C                     PARM CCY       P@CCY   3
     C           SDCURR    PARM SDCURR    DSSDY
     C*
     C** Check if an error occurred
     C*
     C           P@RTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCURRPD'DBFILE           **************
     C                     Z-ADD04        DBASE            * DBERROR 04 *
     C                     OUT  LDA                        **************
     C                     EXSR *PSSR
     C                     END
     C*
     C** Format amount for display
     C*
     C                     Z-ADDA6NBDP    ZDECS
     C                     Z-ADDWAM1      ZFLD15
     C                     MOVE 'A'       ZECODE
     C                     EXSR ZFRPED
     C**********           MOVE ZOUT22    PSTAM                                             AR322849
     C                     MOVE ZOUT22    PSTAMX 22                                         AR322849
     C*
     C** Format first line narrative (B/FWD BALANCE)
     C*
     C                     MOVE *BLANKS   NARR
     C**********           MOVELBNARR     NARR                                              AR322849
     C                     MOVELBNARR     NARRX  30                                         AR322849
     C*****************************************************************                     AR322849
     C***Value*date*should*be*blank************************************                     AR322849
     C*****************************************************************                     AR322849
     C**********           MOVE *BLANKS   VALDAT                                            AR322849
     C*****************************************************************                     AR322849
     C***Reset*subfile*and*record*pointer******************************                     AR322849
     C*****************************************************************                     AR322849
     C**********           Z-ADD1         RRN     40                                        AR322849
     C**********           Z-ADD0         COUNT   30                                        AR322849
     C**********           SETOF                     27                                     AR322849
     C**********           Z-ADDRRN       DSPREC                                            AR322849
     C**********           WRITEGL8000S1                                                    AR322849
     C**********           ADD  1         RRN                                               AR322849
     C**********           ADD  1         COUNT                                             AR322849
     C**********           SETON                     23                                     AR322849
     C*****************************************************************                     AR322849
     C***Set*ON*indicator*to*indicate*first*record*has*been*displayed**                     AR322849
     C*****************************************************************                     AR322849
     C**********           SETON                     41                                     AR322849
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #CFWD  -  C/FWD balance processing                            *
     C*                                                               *
     C* Calls:  ZFRPED, ZDATE2                                        *
     C*                                                               *
     C* Called from:  #LOAD                                           *
     C*                                                               *
     C*****************************************************************
     C*
     C           #CFWD     BEGSR                           *** #CFWD ***
     C*
     C** Format amount for display
     C*
     C                     Z-ADDA6NBDP    ZDECS
     C                     Z-ADDTOTAL     ZFLD15
     C                     MOVE 'A'       ZECODE
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    PSTAM
     C*
     C** Format first line narrative (C/FWD BALANCE)
     C*
     C                     MOVE *BLANKS   NARR
     C                     MOVELCNARR     NARR
     C*
     C** Value date should be blank
     C*
     C                     MOVE *BLANKS   VALDAT
     C*
     C** Check system date format
     C*
     C           AGDFF     IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     END
     C*
     C** Convert date to date format
     C*
     C                     Z-ADDWRNDT     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    DATE1
     C*
     C** Write record to sufile
     C*
     C                     WRITEGL8000S1
     C                     ADD  1         RRN
     C                     ADD  1         COUNT
     C*
     C** Set ON indicator to display complete narrative
     C*
     C                     SETON                     66
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  #STOR  -  Store entered values into data structure           *
     C*                                                               *
     C*  Calles:  None                                                *
     C*                                                               *
     C*  Called from:  #PROC                                          *
     C*                                                               *
     C*****************************************************************
     C           #STOR     BEGSR                           *** #STOR ***
     C*
     C** If multibranching, include branch entered to the values
     C** being saved
     C*
     C           *IN20     IFEQ '1'
     C**********           MOVE MBACNT    MBACSV 18                                           CGL029
     C                     MOVE MBACNT    MBACSV 24                                           CGL029
     C                     ELSE
     C**********           MOVE ACNT      ACSV   15                                           CGL029
     C                     MOVE ACNT      ACSV   21                                           CGL029
     C                     END
     C                     MOVE RTAC      RTSV   10
     C                     MOVE STDT      STSV    6
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  #RSTOR  -  Restore saved fields                              *
     C*                                                               *
     C*  Calles:  None                                                *
     C*                                                               *
     C*  Called from:  #ROLUP                                         *
     C*                                                               *
     C*****************************************************************
     C*
     C           #RSTOR    BEGSR                           *** #RSTOR ***
     C*
     C** If multibranching, include branch entered to the values
     C** being restored
     C*
     C           *IN20     IFEQ '1'
     C                     MOVE MBACSV    MBACNT
     C                     ELSE
     C                     MOVE ACSV      ACNT
     C                     END
     C                     MOVE RTSV      RTAC
     C                     MOVE STSV      STDT
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  #FLDC  -  Check if input fields changed                      *
     C*                                                               *
     C*  Calls:  None                                                 *
     C*                                                               *
     C*  Called from:  Main processing, #ROLUP                        *
     C*                                                               *
     C*****************************************************************
     C*
     C           #FLDC     BEGSR                           *** #FLDC ***
     C*
     C** If multibranching, include branch entered to the values
     C** being compared
     C*
     C           *IN20     IFEQ '1'
     C           MBACNT    COMP MBACSV               6060
     C                     ELSE
     C           ACNT      COMP ACSV                 6060
     C                     END
     C*
     C** Check a change in Retail A/c field only if retail module is ON
     C*
     C           *IN21     IFEQ '1'
     C           RTAC      COMP RTSV                 6161
     C                     END
     C*
     C           STDT      COMP STSV                 6262
     C*
     C** If first entry into the program or invalid input
     C*
     C           *IN44     IFEQ '0'
     C           *IN31     OREQ '1'
     C           *IN68     OREQ '1'
     C                     SETON                     446061
     C                     SETON                     62
     C                     SETOF                     68
     C                     END
     C*
     C** If fields changed
     C*
     C           *IN60     IFEQ '1'
     C           *IN61     OREQ '1'
     C           *IN62     OREQ '1'
     C                     SETON                     43
     C                     ELSE
     C                     SETOF                     43
     C                     END
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  #PUDT  -  Compute last purge date of the historic record     *
     C*                                                               *
     C*  Calls:  ZDATE2, ZDATE1                                       *
     C*                                                               *
     C*  Called from:  #VALD                                          *
     C*                                                               *
     C*****************************************************************
     C*
     C           #PUDT     BEGSR                           *** #PUDT ***
     C*
     C** Reset last purge date
     C*
     C                     Z-ADD0         PUDT    50
     C                     SETOF                     98
     C** Get SDGELRPD details using the access object                     154040
     C*                                                                   154040
     C**********           CALL 'AOGELRR0'                                154040              CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM *BLANKS   P@RTCD                          154040
     C                     PARM '*FIRST ' P@OPTN                          154040
     C********** SDGELR    PARM SDGELR    DSFDY                           154040              CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
     C*                                                                   154040
     C** Check if an error occurred                                       154040
     C*                                                                   154040
     C           P@RTCD    IFNE *BLANKS                                   154040
     C           *LOCK     IN   LDA                                       154040
     C                     MOVEL'SDGELRPD'DBFILE           ************** 154040
     C                     Z-ADD05        DBASE            * DBERROR 05 * 154040
     C                     OUT  LDA                        ************** 154040
     C                     EXSR *PSSR                                     154040
     C                     END                                            154040
     C*                                                                   154040
     C           *IN31     IFEQ '0'                                                           247378
     C**********           MOVE ACOD      WWACOD  4                                    154040 CGL029
     C                     MOVE ACOD      WWACOD 10                                           CGL029
     C*                                                                   154040
     C** Get details of SDACODPD record using access object               154040
     C*                                                                   154040
     C                     CALL 'AOACODR0'                                154040
     C                     PARM *BLANKS   P@RTCD                          154040
     C                     PARM '*KEY   ' P@OPTN                          154040
     C**********           PARM WWACOD    P@ACOD  4                                    154040 CGL029
     C********** SDACOD    PARM SDACOD    DSFDY                                        154040 CGL029
     C                     PARM WWACOD    P@ACOD 10                                           CGL029
     C           SDACOD    PARM SDACOD    DSSDY                                               CGL029
     C*                                                                   154040
     C** Check if an error occurred                                       154040
     C*                                                                   154040
     C           P@RTCD    IFNE *BLANKS                                   154040
     C           *LOCK     IN   LDA                                       154040
     C                     MOVELWWACOD    DBKEY                           154040
     C                     MOVEL'SDACODPD'DBFILE           ************** 154040
     C                     Z-ADD06        DBASE            * DBERROR 06 * 154040
     C                     OUT  LDA                        ************** 154040
     C                     EXSR *PSSR                                     154040
     C                     END                                            154040
     C                     ENDIF                                                              247378
     C*
     C** Change run date to date format (DDMMYY) using Midas standard
     C** subroutine
     C*
     C                     Z-ADDWRNDT     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    WRUN7   7
     C                     MOVE ZDATE     WRUN6
     C*                                                                   154040
     C** No retention period present in both SDACODPD and SDGELRPD,       154040
     C** Use the last purge date (Based on APOSTPD) as the start date     154040
     C           A5ACPR    IFEQ 0                                         154040
     C           BKAPRP    ANDEQ0                                         154040
     C*
     C           WDD       IFGT 1
     C                     Z-ADD1         WDD
     C                     ELSE
     C           WMM       IFGT 1
     C                     SUB  1         WMM
     C                     ELSE
     C                     Z-ADD12        WMM
     C                     SUB  1         WYY
     C                     END
     C                     END
     C*                                                                   154040
     C                     GOTO PUDAT                                     154040
     C*                                                                   154040
     C** ACC Post retention present in SDACODPD or SDGELRPD or both       154040
     C                     ELSE                                           154040
     C*                                                                   154040
     C** Acct post retention period present in SDACODPD                   154040
     C           A5ACPR    IFNE 0                                         154040
     C                     MOVELA5ACPR    WRET    20                      154040
     C*                                                                   154040
     C** Acct post retention period not present in SDACODPD               154040
     C                     ELSE                                           154040
     C*                                                                   154040
     C** A/c posting retention period present in SDGELRPD                 154040
     C           BKAPRP    IFNE 0                                         154040
     C                     MOVELBKAPRP    WRET                            154040
     C                     END                                            154040
     C*                                                                   154040
     C                     END                                            154040
     C*                                                                   154040
     C** Get the number of years/months for the retention period          154040
     C*                                                                   154040
     C           WRET      DIV  12        WYY1    20                      154040
     C                     MVR            WMM1    20                      154040
     C*                                                                   154040
     C** Get the last purge date                                          154040
     C*                                                                   154040
     C           WYY       SUB  WYY1      WYY                             154040
     C           WMM       SUB  WMM1      WMM                             154040
     C*                                                                   154040
     C                     END                                            154040
     C*
     C** Change purge date to day number
     C*
     C           PUDAT     TAG                                            154040
     C                     MOVE WRUN6     ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    PUDT
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C/COPY ZSRSRC,ZALIGNZ2
     C/COPY ZSRSRC,ZDATE1Z2
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZFRPEDZ3
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  ZASNMS  -  Send message to program's message queue           *
     C*                                                               *
     C*  Calls:  Y2SNMGC                                              *
     C*                                                               *
     C*  Called from:  #INIT, #VALD                                   *
     C*                                                               *
     C*****************************************************************
     C*
     C           ZASNMS    BEGSR                           *** ZASNMS **
     C*
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZAMSID  7
     C                     PARM           ZAMSGF 10
     C                     PARM           ZAMSDA132
     C                     PARM           ZAMSTP  7
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  *PSSR  -  Error control subroutine                           *
     C*                                                               *
     C*  Calls:  None                                                 *
     C*                                                               *
     C*  Called from:  #INIT, #BFWD                                   *
     C*                                                               *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR                           *** *PSSR ***
     C*
     C** Check to see that *PSSR has not been called yet
     C*
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     END
     C*
     C** If *PSSR already run, then just end the program here
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C                     ENDSR
     C*****************************************************************
**  CPY@ - OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
