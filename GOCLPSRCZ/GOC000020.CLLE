/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas GO Initialise zone')                            */
/*********************************************************************/
/*                                                                   */
/*       Midas - Global Processing                                   */
/*                                                                   */
/*       GOC000020 - Initialise zone                                 */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*       Last Amend No. CUP033             Date 30Jan07              */
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CPK025             Date 04Aug06              */
/*                      CPK020             Date 28Jul04              */
/*                      CUP025  *CREATE    Date 27Nov03              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP033 - Update correct System Values.                      */
/*       CPK025 - MidasPlus 1.3 packaging.  Pass System Value        */
/*                correctly.                                         */
/*       CPK020 - Initialise system value.                           */
/*       CUP025 - MidasPlus Bridge changes.                          */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&ZONE &ZONECODE &ZONEDESC &ACMVRCVDAY)
 
             DCL        VAR(&ZONE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ZONECODE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&ZONEDESC) TYPE(*CHAR) LEN(50)
             DCL        VAR(&ACMVRCVDAY) TYPE(*DEC) LEN(3 0)
 
             DCL        VAR(&DVLIB) TYPE(*CHAR) LEN(10)                                   /*CUP033*/
             DCL        VAR(&RTNLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OWNER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&AUTL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MAINPFX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&SUPPPFX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&GPSBSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DTAQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTAQGEN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COUNTN) TYPE(*DEC) LEN(1 0) VALUE(0)
             DCL        VAR(&COUNTA) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SUPPUPD) TYPE(*CHAR) LEN(20) VALUE(' ')                      /*CPK020*/
             DCL        VAR(&SYSVAL) TYPE(*CHAR) LEN(200)                                 /*CPK025*/
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2003')
 
             MONMSG     MSGID(CPF0000 MCH0000) EXEC(GOTO CMDLBL(ABNOR))
 
             CHGJOB     SWS(00000000)
 
/* Retrieve Main System ID from SDSTAT. */
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&MAINPFX)
             CHGVAR     VAR(&DVLIB) VALUE(&MAINPFX *TCAT 'DVLIB')                         /*CUP033*/
 
/* Retrieve Support System ID from SDSTAT. */
             RTVDTAARA  DTAARA(SC24x7 (3 2)) RTNVAR(&SUPPPFX)
 
/* Call program to update GPZONEPD and GOICDTPD. */
             CALL       PGM(GO000020) PARM(&ZONE &ZONECODE &ZONEDESC +
                          &ACMVRCVDAY &MAINPFX &SUPPPFX)
 
/* Retrieve Global layer library for creation of dataqueues. */
             RTVOBJD    OBJ(GPUTUXXXXN) OBJTYPE(*DTAQ) RTNLIB(&RTNLIB)
 CRTDTAQ:
             CHGVAR     VAR(&COUNTN) VALUE(&COUNTN + 1)
             CHGVAR     VAR(&COUNTA) VALUE(&COUNTN)
             CHGVAR     VAR(&DTAQ) VALUE('GPUTU' *TCAT &ZONECODE *TCAT +
                          &COUNTA)
             CPYDTAQ    FMDTAQ(&RTNLIB/GPUTUXXXXN) +
                          TODTAQ(&RTNLIB/&DTAQ) CRTDTAQ(*YES)
             IF         COND(&COUNTN *LT 9) THEN(DO)
                GOTO       CMDLBL(CRTDTAQ)
             ENDDO
 
/* Change ownership and authorities. */
             CHGVAR     VAR(&GPSBSID) VALUE(%SST(&RTNLIB 1 2))
             CHGVAR     VAR(&OWNER) VALUE(&GPSBSID *TCAT 'OWNER')
             CHGVAR     VAR(&AUTL) VALUE(&GPSBSID *TCAT 'DATABASE')
             CHGVAR     VAR(&DTAQGEN) VALUE('GPUTU' *TCAT &ZONECODE +
                          *TCAT '*')
 
             CHGOWNAUT  OBJ(&RTNLIB/&DTAQGEN) OBJTYPE(*DTAQ) +
                          NEWOWN(&OWNER) PUBAUT(*AUTL) AUTL(&AUTL) +
                          CHGLIB(N)
 
 SYSVAL:                                                                                  /*CPK020*/
/**********  CALL       PGM(SD009510) PARM('ZoneSystemName' &ZONE +                    */ /*CPK025*/
/**********               &SUPPUPD)                                         */ /*CPK020*/ /*CPK025*/
             CHGVAR     VAR(&SYSVAL) VALUE(&ZONE)                                         /*CPK025*/
             OVRDBF     FILE(SDSVALL0) TOFILE(&DVLIB/SDSVALL0) +
                          OVRSCOPE(*JOB)                                                  /*CUP033*/
             CALL       PGM(SD009510) PARM('ZoneSystemName' &SYSVAL +
                          &SUPPUPD)                                                       /*CPK025*/
             DLTOVR     FILE(SDSVALL0) LVL(*JOB)                                          /*CUP033*/
 
             GOTO       CMDLBL(END)
 
/* Abnormal end processing. */
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
 
 END:
             ENDPGM
