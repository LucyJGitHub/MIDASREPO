/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas GO Midas system status monitor')                */
/*********************************************************************/
/*                                                                   */
/*       Midas - Global Processing                                   */
/*                                                                   */
/*       GOC000102 - Midas System Status Monitor (MIDASSTS)          */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. BUG9512            Date 05Dec05              */
/*       Prev Amend No. CGP002  *CREATE    Date 01Aug03              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       BUG9512 - Pgm doesn't handle errors in called programs      */
/*       CGP002 - Global Processing data download                    */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&DTQDLEN)  TYPE(*DEC) LEN(5) VALUE(3)
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(3)
             DCL        VAR(&WAIT)  TYPE(*DEC) LEN(5) VALUE(60)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2003')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(XXXXXX00)
 
/* Lock DTAARA/GOMIDASSTS to indicate that this job is active */
 
             ALCOBJ     OBJ((GOMIDASSTS *DTAARA *EXCL)) WAIT(1)
             MONMSG     MSGID(CPF1002) EXEC(DO)
                CHGJOB     LOG(*SAME *SAME *NOLIST)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
/* Clear DTAQ/MIDASSTSDQ of all old messages */
 
             CALL       PGM(QCLRDTAQ) PARM('MIDASSTSDQ' '*LIBL ')
 
UPDATE:
/* Run the global dashboard update job */
 
             CALL       PGM(GOC000100) PARM('*PHASE')
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))
 
/* Check for instruction to end the job */
 
             CALL       PGM(QRCVDTAQ) PARM('MIDASSTSDQ' '*LIBL     ' +
                          &DTQDLEN &MESSAGE &WAIT)
 
             IF         COND(&MESSAGE *EQ 'END') THEN(DO)
                CHGJOB     LOG(*SAME *SAME *NOLIST)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
             GOTO       CMDLBL(UPDATE)
 
ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(*LIBL/QCPFMSG) +
                          MSGDTA('Error in pgm GOC000102') +
                          MSGTYPE(*ESCAPE)                                               /*BUG9512*/
             MONMSG     MSGID(CPF0000)                                                   /*BUG9512*/
 
ENDPGM:
 
             ENDPGM
