/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI    TEXT('Midas Global Historic Spot Rates')                    */
/*********************************************************************/
/*                                                                   */
/*     Midas Standing Data Module                                    */
/*                                                                   */
/*     GOC002438 - Midas Global Historic Spot Rates                  */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2005           */
/*                                                                   */
/*       Last Amend No. CLE134             Date 01Aug12              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CAS014  *CREATE    Date 10Aug05              */
/*                                                                   */
/*********************************************************************/
/*       CLE134 - Past Due Call Loan Processing (Recompile)          */
/*       CAS014 - IAS21 The Effects of Changes in Foreign Exchange   */
/*                Rates                                              */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ)
 
/* Parameters declaration */
 
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5 0)
 
/* Variables Declaration */
 
             DCL        VAR(&BUSY) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&TOLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2005')
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2005')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('GOC002438 - Midas Global Historic Spot +
                          Rates Component') TOMSGQ(MRUNQ MOPERQ) +
                          MSGTYPE(*INFO)
 
             RTVJOBA    TYPE(&JOBTYPE)
             CHGJOB     SWS(XXXXXX00)
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&PRE)
             CHGVAR     VAR(&TOLIB) VALUE(&PRE *TCAT 'DPLIB')
 
             CALL       PGM(CB0160) PARM(&CNAM &CSEQ &BUSY)
 
/*   If Busy is 'Y', which is equal to the value of the restart  */
/*   indicator.                                                  */
 
             IF         COND(&BUSY *EQ 'Y') THEN(DO)
 
/*   Clear Global Spot History files  GPCUHSPD and GPCUHSPA   */
/*   Copy XGPCUHSPD and XGPCUHSPA (security copies) to        */
/*   GPCUHSPD and GPCUHSPA respectively                       */
 
             CPYF       FROMFILE(XGPCUHSPD) TOFILE(GPCUHSPD) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817)
 
             CPYF       FROMFILE(XGPCUHSPA) TOFILE(GPCUHSPA) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817)
 
             ENDDO
 
             ELSE       CMD(DO)
 
/*        Create Security copies of Updated files */
 
             CPYF       FROMFILE(GPCUHSPD) TOFILE(&TOLIB/XGPCUHSPD) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF2817)
 
             CPYF       FROMFILE(GPCUHSPA) TOFILE(&TOLIB/XGPCUHSPA) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF2817)
 
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ 'Y')
 
             ENDDO
 
             CALL       PGM(GO002438)
 
/*   If Switches 7 and 8 are on,                    */
/*   Retrieve Error Narrative from LDA and send a   */
/*   MEM0001 message from the message File MIDAS to */
/*   Message Queues MOPERQ and MRUNQ.               */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
 
             RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
 
             SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                          TOMSGQ(MOPERQ MRUNQ) MSGTYPE(*INFO)
             MONMSG     MSGID(CPF2469)
 
             GOTO ABNOR
 
             ENDDO
 
/*   If Switches 7 and 8 are off */
/*   Call PGM CB0150 with parameters 'Component Name',   */
/*   'Component Seq. No.' and Busy = 'N'.                */
/*   Clear Security copies XGPCUHSPD and XGPCUHSPA       */
 
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ 'N')
 
/* Delete security copies */
 
                DLTF       FILE(XGPCUHSPD)
                MONMSG     MSGID(CPF2105)
 
                DLTF       FILE(XGPCUHSPA)
                MONMSG     MSGID(CPF2105)
 
             GOTO       CMDLBL(END)
ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
 
/* Abnormal termination - batch job */
 
             IF         COND(&JOBTYPE = '0') THEN(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                             GOC002438 ended abnormally - see job log') +
                             TOMSGQ(MOPERQ)
                MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO
 
END:
 
             ENDPGM
