/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI    TEXT('Midas Drop Off Expired Global Historic Spot Rates')   */
/*********************************************************************/
/*                                                                   */
/*       Midas - IAS21 Module                                        */
/*                                                                   */
/*       GOC002439  - Midas Drop Off Expired Global Historic Spot    */
/*                   Rates                                           */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2005           */
/*                                                                   */
/*       Last Amend No. CLE134             Date 01Aug12              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CAS014  *Create    Date 10Aug05              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CLE134 - Past Due Call Loan Processing (Recompile)          */
/*       CAS014 - IAS21 The Effects of Changes in Foreign Exchange   */
/*                Rates                                              */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ)
 
/* Declaration of variables */
 
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2005')
 
             DCL        VAR(&MEM)    TYPE(*CHAR) LEN(50)
             DCL        VAR(&CNAM)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&BUSY)   TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&TOLIB)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRE)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&SDSTAT) TYPE(*CHAR) LEN(256)
             DCL        VAR(&CSEQ)   TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&RTCD)   TYPE(*CHAR) LEN(7)
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(00000000)
 
/* Create data area LDA */
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
 
             STRCMTCTL  LCKLVL(*CHG) CMTSCOPE(*JOB)
             MONMSG     MSGID(CPF8351)
 
/* Notify that component is running */
 
             CHGVAR     VAR(&MEM) VALUE('GO002439 - Midas Drop Off +
                          Global Historic Spot Rates')
             SNDPGMMSG  MSG(&MEM) TOMSGQ(MRUNQ MOPERQ) MSGTYPE(*INFO)
 
/* Checking of CLose of Business status */
 
             CALL       PGM(CB0160) PARM(&CNAM &CSEQ &BUSY)
 
/* Program requires restart */
 
             IF         COND(&BUSY *EQ 'Y') THEN(DO)
             CPYF       FROMFILE(XGPCUHSPD) TOFILE(GPCUHSPD) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(GPCUHSPD))
             CPYF       FROMFILE(XGPCUHSPA) TOFILE(GPCUHSPA) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(GPCUHSPA))
 
/* Initialise data to 'Y' */
 
             CHGVAR     VAR(&BUSY) VALUE('N')
 
/* Call CB0150 to update restart close of business indicator */
 
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &BUSY)
 
             ENDDO
 
/* Program do not need restart or component not yet run */
 
             IF         COND(&BUSY *NE 'Y') THEN(DO)
 
/* Getting the DP Library for back-up*/
 
             RTVDTAARA  DTAARA(SDSTAT) RTNVAR(&SDSTAT)
             CHGVAR     VAR(&PRE) VALUE(%SST(&SDSTAT 6 2))
             CHGVAR     VAR(&TOLIB) VALUE(&PRE *TCAT 'DPLIB')
 
/* Security copy of masterfiles */
 
             CPYF       FROMFILE(GPCUHSPD) TOFILE(&TOLIB/XGPCUHSPD) +
                          MBROPT(*REPLACE) CRTFILE(*YES) FMTOPT(*NONE)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(XGPCUHSPD))
 
             CPYF       FROMFILE(GPCUHSPA) TOFILE(&TOLIB/XGPCUHSPA) +
                          MBROPT(*REPLACE) CRTFILE(*YES) FMTOPT(*NONE)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(XGPCUHSPA))
             ENDDO
 
/* Call program that would drop daily historic spot rates */
 
             CALL       GO002439
 
/* Midas Standard Error Processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
 
/* Copy back original data to files */
 
             CPYF       FROMFILE(&TOLIB/XGPCUHSPA) TOFILE(GPCUHSPA) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(GPCUHSPA))
             CPYF       FROMFILE(&TOLIB/XGPCUHSPD) TOFILE(GPCUHSPD) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(GPCUHSPD))
 
/* Abnormal program termination */
             GOTO       CMDLBL(ABNOR)
 
             ENDDO
 
/* Removing records with drop status while copying to temporary file */
 
             CPYF       FROMFILE(GPCUHSPD) TOFILE(&TOLIB/XGPCUHSPD) +
                          MBROPT(*REPLACE) INCREL((*IF GHCHTP *NE +
                          '*')) CRTFILE(*YES) FMTOPT(*NONE)
 
/* Copy live records to original files */
 
             CPYF       FROMFILE(XGPCUHSPD) TOFILE(GPCUHSPD) +
                          MBROPT(*REPLACE) FMTOPT(*NONE)
 
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(GPCUHSPD))
 
/* Normal program termination */
 
             GOTO       CMDLBL(END)
 
/* Abnormal termination */
 
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
 
             CHGVAR     VAR(&RTCD) VALUE('*ERROR*')
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          GO002439 ended abnormally - see joblog +
                          ') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
/* Program temination */
 
 END:
 
/* Delete security files created */
 
                DLTF       FILE(XGPCUHSPD)
                MONMSG     MSGID(CPF2105)
 
                DLTF       FILE(XGPCUHSPA)
                MONMSG     MSGID(CPF2105)
 
/* End of program */
 
             CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2005.')
 
             ENDPGM
