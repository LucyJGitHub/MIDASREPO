/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    DFTACTGRP(*NO) ACTGRP(GOC011201)                            */
/*EXI    TEXT('Midas GO Bulk Sync Global Layer GZRECOMD - Subtask')  */
/*********************************************************************/
/*                                                                   */
/*       Midas - Global Processing Module                            */
/*                                                                   */
/*       GOC011201 - Midas GO Bulk Sync Global Layer GZRECOMD -      */
/*                   Subtask                                         */
/*                                                                   */
/*       (c) Finastra International Limited 2012                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. MD002850           Date 08Apr13              */
/*                      CGP017  *CREATE    Date 06Aug12              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       MD002850 - Correct the processing for restart of subtasks   */
/*                  after failure.                                   */
/*       CGP017 - Global Layer - GOC0100 Task Split                  */
/*                                                                   */
/*********************************************************************/
 
             PGM        PARM(&CNAME &CSEQ)
 
/* Additional variables */
 
             DCL        VAR(&CNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&CSEQC) TYPE(*CHAR) LEN(5)
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTAQLIBL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGLENGTH) TYPE(*DEC) LEN(5 0) VALUE(50)
             DCL        VAR(&MSGDATA) TYPE(*CHAR) LEN(50)
             DCL        VAR(&SNDDTAQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RCVDTAQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RCVWAIT) TYPE(*DEC) LEN(5 0) VALUE(600)
             DCL        VAR(&RESTART) TYPE(*CHAR) LEN(1)
             DCL        VAR(&STAT) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SNDMSG) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&INDEXFL) TYPE(*CHAR) LEN(10) +
                        VALUE('RECOMIPD')
             DCL        VAR(&DRIVER) TYPE(*CHAR) LEN(10) +
                        VALUE('RECOMDPD')
             DCL        VAR(&LDA) TYPE(*CHAR) LEN(256)
 
             DCL        VAR(&RTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ERMS)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&FULLCHECK) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&ZOZONE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ZOSHTC) TYPE(*CHAR) LEN(4)
             DCL        VAR(&ZORDNB) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&ZODNWD) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&ZOBCCY) TYPE(*CHAR) LEN(3)
             DCL        VAR(&ZONJOB) TYPE(*DEC) LEN(1 0)
             DCL        VAR(&GPF) TYPE(*CHAR) LEN(10) VALUE(GZRECOMD)
             DCL        VAR(&GLFU) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ZLFOVR) TYPE(*CHAR) LEN(60)
             DCL        VAR(&GLFOVR) TYPE(*CHAR) LEN(60)
             DCL        VAR(&GLFCLO) TYPE(*CHAR) LEN(60)
             DCL        VAR(&ZLFDLO) TYPE(*CHAR) LEN(60)
             DCL        VAR(&GLFDLO) TYPE(*CHAR) LEN(60)
             DCL        VAR(&OPNQRY) TYPE(*CHAR) LEN(32702)
             DCL        VAR(&ZLF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BLFD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&BLSY) TYPE(*CHAR) LEN(1)
 
             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(1)
 
             COPYRIGHT  TEXT('(c) Finastra International Limited +
                          2012')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
 
             SNDPGMMSG  MSG(&CNAME *TCAT ' - Midas GO Bulk Sync +
                          Global Layer GZRECOMD - Subtask') +
                          TOMSGQ(MRUNQ)
 
             RTVJOBA    TYPE(&JOBTYPE)
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                          VALUE(' ')
             MONMSG     MSGID(CPF0000)
 
             CHGJOB     SWS(00000000)
 
/* Read COB components file to check the restart status */
 
             CALL       PGM(CB0160) PARM(&CNAME &CSEQ &STAT)
 
/* Check if any problems with CB0160 call */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             CHGVAR     VAR(&RESTART) VALUE(&STAT)
 
/* Set restart status to 'Y' */
 
             CHGVAR     VAR(&STAT) VALUE('Y')
             CALL       PGM(CB0150) PARM(&CNAME &CSEQ &STAT)
 
/* Set up call to QSNDDTAQ and override files to their members */
 
             CHGVAR     VAR(&CSEQC) VALUE(&CSEQ)
             CHGVAR     VAR(&MEMBER) VALUE('RECOM' *CAT &CSEQC)
             CHGVAR     VAR(&DTAQLIBL) VALUE('*LIBL')
             CHGVAR     VAR(&SNDDTAQ) VALUE('RERCOMSVDQ')
             CHGVAR     VAR(&RCVDTAQ) VALUE(&MEMBER)
 
             OVRDBF     FILE(&DRIVER) TOFILE(&DRIVER) MBR(&MEMBER)
 
             STRCMTCTL  LCKLVL(*CHG) CMTSCOPE(*JOB)
 
/* If restart from component failed because of no respond          */
/* from Server. So resend message to Server and wait.              */
 
             IF         COND(&RESTART = 'F') THEN(DO)
                GOTO       CMDLBL(LOOP)
             ENDDO
 
/* If restart, not caused by server do not responds, */
/* do not wait for the data queue entry */
/* Clear any old data queue message from server but ensure that an */
/* 'END' message, sent from the end proc job is not lost */
 
             IF         COND(&RESTART = 'Y') THEN(DO)
                CHGVAR     VAR(&RCVWAIT) VALUE(5)
                CALL       PGM(QRCVDTAQ) PARM(&MEMBER &DTAQLIBL +
                             &MSGLENGTH &MSGDATA &RCVWAIT)
                CHGVAR     VAR(&RCVWAIT) VALUE(600)
                CHGVAR     VAR(&MSGLENGTH) VALUE(50)
                IF         COND(%SST(&MSGDATA 1 3) = 'END') THEN(DO)
                  CALL       PGM(QSNDDTAQ) PARM(&MEMBER &DTAQLIBL +
                               &MSGLENGTH &MSGDATA)
                ENDDO
                GOTO       CMDLBL(RESTART)
             ENDDO
 
LOOP:
 
/* Send message to main data queue */
 
             CHGVAR     VAR(&MSGDATA) VALUE(&MEMBER)
             CALL       PGM(QSNDDTAQ) PARM(&SNDDTAQ &DTAQLIBL +
                          &MSGLENGTH &MSGDATA)
/**********  CHGVAR     VAR(&STAT) VALUE('F')                      */                   /*MD002850*/
/**********  CALL       PGM(CB0150) PARM(&CNAME &CSEQ &STAT)       */                   /*MD002850*/
 
/* Wait on reply for 5 minutes */
 
             CALL       PGM(QRCVDTAQ) PARM(&RCVDTAQ &DTAQLIBL +
                          &MSGLENGTH &MSGDATA &RCVWAIT)
 
/* If no reply, check if the server is still active.        */
/* If Server is not active then error message and Terminate */
/* If Server is active, then resend a message to server     */
/* If Server still do not respond after message send, then  */
/* error message and Terminate.                             */
 
             IF         COND(&MSGLENGTH = 0) THEN(DO)
                ALCOBJ     OBJ((&INDEXFL *FILE *EXCL)) WAIT(0)
                MONMSG     MSGID(CPF1002) EXEC(DO)
                   IF         COND(&SNDMSG *EQ 'Y') THEN(DO)
                      CHGVAR     VAR(&STAT) VALUE('F')                                  /*MD002850*/
                      CALL       PGM(CB0150) PARM(&CNAME &CSEQ &STAT)                   /*MD002850*/
                      SNDPGMMSG  MSG('No response from server for ' +
                                   *BCAT &CNAME) TOMSGQ(MOPERQ MRUNQ)
                      GOTO       CMDLBL(ABNOR)
                   ENDDO
 
                   IF         COND(&SNDMSG *NE 'Y') THEN(DO)
                      CHGVAR     VAR(&SNDMSG) VALUE('Y')
                      GOTO       CMDLBL(LOOP)
                   ENDDO
                ENDDO
                                                                                        /*MD002850*/
                CHGVAR     VAR(&STAT) VALUE('F')                                        /*MD002850*/
                CALL       PGM(CB0150) PARM(&CNAME &CSEQ &STAT)                         /*MD002850*/
                SNDPGMMSG  MSG('Server for ' *BCAT &CNAME *TCAT +
                             ' not active') TOMSGQ(MOPERQ MRUNQ)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
/* If reply = END, set restart status to 'N' and terminate */
 
             IF         COND(%SST(&MSGDATA 1 3) = 'END') THEN(DO)
                CHGVAR     VAR(&STAT) VALUE('N')
                CALL       PGM(CB0150) PARM(&CNAME &CSEQ &STAT)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
RESTART:
 
/* Get zone */
 
             CALL       PGM(GOGETZONE) PARM(&RTCD &ERMS &FULLCHECK +
                          &ZOZONE &ZOSHTC &ZORDNB &ZODNWD &ZOBCCY +
                          &ZONJOB)
 
             IF         COND(&RTCD *NE '       ') THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
/* Get synchronisation executables*/
 
             CALL       PGM(GOGETSYNX) PARM(&GPF &ZOZONE &GLFU +
                          &ZLFOVR &GLFOVR &GLFCLO &ZLFDLO &GLFDLO +
                          &OPNQRY &ZLF &BLFD &BLSY)
 
/* End if bulk synchronisation not done for this file*/
 
             IF         COND(&BLSY *NE 'Y') THEN(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Bulk +
                             synchronisation was not done for file ' +
                             *CAT &GPF) TOPGMQ(*EXT)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
/* Do bulk synchronisation */
 
             CALL       PGM(QCMDEXC) PARM(&ZLFOVR 60)
             CALL       PGM(QCMDEXC) PARM(&GLFOVR 60)
             OVRDBF     FILE(&GPF) SEQONLY(*YES 4000)
             OVRDBF     FILE(GPGDRPPD) SEQONLY(*YES 5000)
 
/* Filter duplicates  */
 
             IF         COND(&BLFD = 'Y') THEN(DO)
                OPNQRYF    FILE((&ZLF)) OPTION(*INP) KEYFLD(*FILE) +
                             UNIQUEKEY(*ALL) SEQONLY(*YES 5000)
             ENDDO
 
             CALL       PGM(GO011201) PARM(&ZOZONE)
             CALL       PGM(QCMDEXC) PARM(&ZLFDLO 60)
             CALL       PGM(QCMDEXC) PARM(&GLFDLO 60)
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA *ALL) RTNVAR(&LDA)
                CHGVAR     VAR(&ERMS) VALUE(%SST(&LDA 134 50))
 
                SNDPGMMSG  MSGID(MEM0001) MSGF(REUSRMSG) MSGDTA(&ERMS) +
                             TOMSGQ(MOPERQ MRUNQ)
                ROLLBACK
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             IF         COND(*NOT %SWITCH(XXXXXX11)) THEN(DO)
                COMMIT
                CHGVAR     VAR(&RESTART) VALUE('N')
                CLRPFM     FILE(RECOMDPD) MBR(&MEMBER)
                GOTO       CMDLBL(LOOP)
             ENDDO
 
/* Successful completion */
 
 
ABNOR:
             IF         COND(&JOBTYPE = '0') THEN(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) +
                             MSGDTA('Program ' *BCAT &CNAME *TCAT +
                             ' ended abnormally') TOMSGQ(MOPERQ MRUNQ)
                MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO
 
             IF         COND(&JOBTYPE = '1') THEN(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) +
                             MSGDTA('Program ' *BCAT &CNAME *TCAT +
                             ' ended abnormally') TOPGMQ(*EXT)
                MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO
 
             CHGJOB     SWS(XXXXXX11)
 
ENDPGM:
             RCLRSC
             MONMSG     MSGID(CPF0000 MCH0000)
             RCLRSC     LVL(*CALLER)
             MONMSG     MSGID(CPF0000 MCH0000)
             RCLACTGRP  ACTGRP(*ELIGIBLE)
             MONMSG     MSGID(CPF0000 MCH0000)
             ENDCMTCTL
             MONMSG     MSGID(CPF0000)
             ENDPGM
