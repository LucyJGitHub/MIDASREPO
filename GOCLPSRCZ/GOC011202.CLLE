/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    DFTACTGRP(*NO) ACTGRP(GOC011202)                            */
/*EXI    TEXT('Midas GO Bulk Sync GZRECOMD - Task Split dltns')      */
/*********************************************************************/
/*                                                                   */
/*       Midas - Global Processing Module                            */
/*                                                                   */
/*       GOC011202 - Midas GO Bulk Sync Global Layer GZRECOMD        */
/*                   Deletions                                       */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2012           */
/*                                                                   */
/*       Last Amend No. CGP017   *CREATE   Date 06Aug12              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CGP017 - Global Layer - GOC0100 Task Split                  */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&CNAME) TYPE(*CHAR) LEN(10) +
                          VALUE('GOC011202')
             DCL        VAR(&RTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ERMS) TYPE(*CHAR) LEN(50)
             DCL        VAR(&FULLCHECK) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&ZOZONE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ZOSHTC) TYPE(*CHAR) LEN(4)
             DCL        VAR(&ZORDNB) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&ZODNWD) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&ZOBCCY) TYPE(*CHAR) LEN(3)
             DCL        VAR(&ZONJOB) TYPE(*DEC) LEN(1 0)
 
             DCL        VAR(&GPF) TYPE(*CHAR) LEN(10) VALUE('GZRECOMD')
             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(1)
 
             COPYRIGHT  TEXT('(c) Misys International Banking +
                          Systems Ltd. 2012')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG(&CNAME *TCAT ' - Bulk Synchronisation of +
                          Global Layer, task-split, deletions:' +
                          *CAT &GPF) TOMSGQ(MRUNQ)
 
             RTVJOBA    TYPE(&JOBTYPE)
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                          VALUE(' ')
             MONMSG     MSGID(CPF0000)
 
             CHGJOB     SWS(00000000)
 
 
/* Get zone */
 
             CALL       PGM(GOGETZONE) PARM(&RTCD &ERMS &FULLCHECK +
                          &ZOZONE &ZOSHTC &ZORDNB &ZODNWD &ZOBCCY +
                          &ZONJOB)
 
             IF         COND(&RTCD *NE '    ') THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
/* Bulk synchronisation program */
 
             STRCMTCTL  LCKLVL(*CHG) CMTSCOPE(*JOB)
             CALL       PGM(GO011202) PARM(&ZOZONE)
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                ROLLBACK
                GOTO       CMDLBL(ABNOR)
             ENDDO
             ELSE       CMD(DO)
                COMMIT
             ENDDO
 
/* Clear file of deleted accounts, written to by GL3320 */
 
             GOTO       CMDLBL(END)
 
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
 
             DMPCLPGM
             MONMSG     MSGID(CPF0000 MCH0000)
 
/* Abnormal termination - batch job */
 
             IF         COND(&JOBTYPE = '0') THEN(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) +
                             MSGDTA('Program' *BCAT &CNAME *TCAT ' +
                             ended abnormally - see job log') +
                             TOMSGQ(MOPERQ MRUNQ)
                MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO
 
/* Abnormal termination - interactive job */
 
             IF         COND(&JOBTYPE = '1') THEN(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) +
                             MSGDTA('Program' *BCAT &CNAME *TCAT ' +
                             ended abnormally - see job log') TOPGMQ(*EXT)
                MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO
 
END:
             RCLRSC
             MONMSG     MSGID(CPF0000 MCH0000)
             RCLRSC     LVL(*CALLER)
             MONMSG     MSGID(CPF0000 MCH0000)
             RCLACTGRP  ACTGRP(*ELIGIBLE)
             MONMSG     MSGID(CPF0000 MCH0000)
             ENDCMTCTL
             MONMSG     MSGID(CPF0000 MCH0000)
             ENDPGM
