/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas GO Bulk Sync of GZSDACUSPD - Task Split')       */
/*********************************************************************/
/*                                                                   */
/*       Midas - Global Processing Module                            */
/*                                                                   */
/*       GOC011501 - Midas GO Bulk Sync of GZSDACUSPD - Task Split   */
/*                                                                   */
/*       WARNING - THIS PROGRAM IS RUNNING IN TASK SPLIT MODE.       */
/*             DO NOT ADD ANY NEW PROGRAMS TO THIS CL IF IT IS NOT   */
/*             ABLE TO RUN IN TASK SPLIT.                            */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2012           */
/*                                                                   */
/*       Last Amend No. MD021155           Date 10Jul13              */
/*       Prev Amend No. MD020205 *CREATE   Date 24Apr13              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD021155 - COB Restructure Phase 1 remnants                 */
/*                - GOC0115 TS wrong approach                        */
/*       MD020205 - Continuous CoB run time improvement              */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ)
 
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&CSEQC) TYPE(*CHAR) LEN(5)
             DCL        VAR(&RESTART) TYPE(*CHAR) LEN(1)
             DCL        VAR(&STAT) TYPE(*CHAR) LEN(1)
             DCL        VAR(&RTCD)  TYPE(*CHAR) LEN( 7)
             DCL        VAR(&ERMS)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&FULLCHECK) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&ZOZONE)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&ZOSHTC)  TYPE(*CHAR) LEN(4)
             DCL        VAR(&ZORDNB)  TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&ZODNWD)  TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&ZOBCCY)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&ZONJOB)  TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&GPF) TYPE(*CHAR) LEN(10) VALUE(GZSDACUSPD)
             DCL        VAR(&GLFU)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&ZLFOVR)  TYPE(*CHAR) LEN(60)
             DCL        VAR(&GLFOVR)  TYPE(*CHAR) LEN(60)
             DCL        VAR(&GLFCLO)  TYPE(*CHAR) LEN(60)
             DCL        VAR(&ZLFDLO)  TYPE(*CHAR) LEN(60)
             DCL        VAR(&GLFDLO)  TYPE(*CHAR) LEN(60)
             DCL        VAR(&OPNQRY)  TYPE(*CHAR) LEN(32702)
             DCL        VAR(&ZLF   )  TYPE(*CHAR) LEN(10)
             DCL        VAR(&BLFD  )  TYPE(*CHAR) LEN(1)
             DCL        VAR(&BLSY  )  TYPE(*CHAR) LEN(1)
             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&STRRRN) TYPE(*DEC) LEN(10 0)
             DCL        VAR(&ENDRRN) TYPE(*DEC) LEN(10 0)
             DCL        VAR(&CSEQ2) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&CSEQ2C) TYPE(*CHAR) LEN(2)
             DCLF       ACUSIXPD
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('GOC011501 - Midas GO Bulk Sync of +
                          GZSDACUSPD - Task Split') TOMSGQ(MRUNQ)
 
             RTVJOBA    TYPE(&JOBTYPE)
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                          VALUE(' ')
             MONMSG     MSGID(CPF0000)
 
             CHGJOB SWS(00000000)
 
/* Check if normal run or restart */
 
             CALL       PGM(CB0160) PARM(&CNAM &CSEQ &STAT)
 
/* Check if any problems with COB components file */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
             GOTO       CMDLBL(ABNOR)
             ENDDO
 
             CHGVAR     VAR(&RESTART) VALUE(&STAT)
 
/* Set restart status to 'Y' */
 
             CHGVAR     VAR(&STAT) VALUE('Y')
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)
 
             RCVF
             IF       COND(&CSEQ *LT 1000) THEN(DO)
               CHGVAR     VAR(&STRRRN) VALUE(1)
               CHGVAR     VAR(&ENDRRN) VALUE(&NORE)
             ENDDO
             ELSE     DO
               CHGVAR     VAR(&CSEQC) VALUE(&CSEQ)
               CHGVAR     VAR(&CSEQ2C) VALUE(%SST(&CSEQC 4 2))
               CHGVAR     VAR(&CSEQ2) VALUE(&CSEQ2C)
               CHGVAR     VAR(&STRRRN) VALUE((&CSEQ2 * &NORE) +1 )
               CHGVAR     VAR(&ENDRRN) VALUE((&CSEQ2 + 1) * &NORE)
             ENDDO
 
/* Get zone */
 
             CALL       PGM(GOGETZONE) PARM(&RTCD &ERMS &FULLCHECK +
                          &ZOZONE &ZOSHTC &ZORDNB &ZODNWD &ZOBCCY +
                          &ZONJOB)
 
/* error */
/**********  IF         COND(&RTCD ¬= '       ') THEN(DO)                            */ /*MD021155*/
             IF         COND(&RTCD *NE '       ') THEN(DO)                              /*MD021155*/
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
 
/* Get synchronisation executables*/
 
             CALL       PGM(GOGETSYNX) PARM(&GPF &ZOZONE &GLFU +
                          &ZLFOVR &GLFOVR &GLFCLO &ZLFDLO &GLFDLO +
                          &OPNQRY &ZLF &BLFD &BLSY)
 
/* End if bulk synchronisation not done for this file*/
 
/**********  IF         COND(&BLSY ¬= 'Y') THEN(DO)                                  */ /*MD021155*/
             IF         COND(&BLSY *NE 'Y') THEN(DO)                                    /*MD021155*/
              SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Bulk +
                            synchronisation was not done for file ' +
                            *CAT &GPF) TOPGMQ(*EXT)
             GOTO       CMDLBL(ENDCL)
             ENDDO
 
 
/* Do bulk synchronisation */
 
             CALL       PGM(QCMDEXC) PARM(&ZLFOVR 60)
             CALL       PGM(QCMDEXC) PARM(&GLFOVR 60)
             CALL       PGM(QCMDEXC) PARM(&OPNQRY 32702)
             OVRDBF     FILE(&GPF) SEQONLY(*YES 4000)
             OVRDBF     FILE(GPGDRPPD) SEQONLY(*YES 5000)
 
/* Filter duplicates */
 
             IF (&BLFD = 'Y') THEN(DO)
             OPNQRYF    FILE((&ZLF)) OPTION(*INP) KEYFLD(*FILE) +
                          UNIQUEKEY(*ALL) SEQONLY(*YES 5000)
             ENDDO
             CALL       PGM(GOSDACUSPD) PARM(&ZOZONE &STRRRN &ENDRRN)
             CALL       PGM(QCMDEXC) PARM(&GLFCLO 60)
             CALL       PGM(QCMDEXC) PARM(&ZLFDLO 60)
             CALL       PGM(QCMDEXC) PARM(&GLFDLO 60)
 
/* Error */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))
 
/* Successful completion */
 
             CHGVAR     VAR(&RESTART) VALUE('N')
 
             GOTO       ENDCL
 
 ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
 
             DMPCLPGM
             MONMSG     MSGID(CPF0000 MCH0000)
 
/* Abnormal termination - batch job */
 
             IF         COND(&JOBTYPE = '0') THEN(DO)
               SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                           GOC011501 ended abnormally - see job log') +
                           TOMSGQ(MOPERQ MRUNQ)
               MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO
 
/* Abnormal termination - interactive job */
 
             IF         COND(&JOBTYPE = '1') THEN(DO)
                SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          GOC011501 ended abnormally - see job log') +
                          TOPGMQ(*EXT)
                MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO
 
 ENDCL:
 /********** RCLRSC                                                                  */ /*MD021155*/
 /********** MONMSG     MSGID(CPF0000 MCH0000)                                       */ /*MD021155*/
 /********** RCLRSC     LVL(*CALLER)                                                 */ /*MD021155*/
 /********** MONMSG     MSGID(CPF0000 MCH0000)                                       */ /*MD021155*/
 /********** RCLACTGRP  ACTGRP(*ELIGIBLE)                                            */ /*MD021155*/
 /********** MONMSG     MSGID(CPF0000 MCH0000)                                       */ /*MD021155*/
             ENDPGM
