/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas GO GPWTRAPPD Housekeeping - Start TS')          */
/*********************************************************************/
/*                                                                   */
/*       Midas - Global Processing                                   */
/*                                                                   */
/*       GOC2110T1 - Midas GO GPWTRAPPD Housekeeping - Start TS      */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2013           */
/*                                                                   */
/*       Last Amend No. MD023876A          Date 06Jan14              */
/*       Prev Amend No. MD023876 *CREATE   Date 14Nov13              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       MD023876A - Wrong location of task split objects            */
/*       MD023876 - GOC210000 Performance Issues                     */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ)
 
             DCL        VAR(&CNAME) TYPE(*CHAR) LEN(10) +
                          VALUE('GOC210010')
/**********  DCL        VAR(&DRIVER) TYPE(*CHAR) LEN(10) +           */                /*MD023876A*/
/**********               VALUE('GPWTRPDPD')                         */                /*MD023876A*/
             DCL        VAR(&DRIVER) TYPE(*CHAR) LEN(10) +
                          VALUE('GOWTRPDPD')                                           /*MD023876A*/
/**********  DCL        VAR(&INDEXFL) TYPE(*CHAR) LEN(10) +          */                /*MD023876A*/
/**********               VALUE('GPWTRPIPD')                         */                /*MD023876A*/
             DCL        VAR(&INDEXFL) TYPE(*CHAR) LEN(10) +
                          VALUE('GOWTRPIPD')                                           /*MD023876A*/
/**********  DCL        VAR(&CTRLFL) TYPE(*CHAR) LEN(10) +           */                /*MD023876A*/
/**********               VALUE('GPWTRPCPD')                         */                /*MD023876A*/
             DCL        VAR(&CTRLFL) TYPE(*CHAR) LEN(10) +
                          VALUE('GOWTRPCPD')                                           /*MD023876A*/
             DCL        VAR(&NAME1) TYPE(*CHAR) LEN(5) VALUE('WTRAP')
/**********  DCL        VAR(&RCVDTAQ) TYPE(*CHAR) LEN(10) +          */                /*MD023876A*/
/**********               VALUE('GPWTRPSVDQ')                        */                /*MD023876A*/
             DCL        VAR(&RCVDTAQ) TYPE(*CHAR) LEN(10) +
                          VALUE('GOWTRPSVDQ')                                          /*MD023876A*/
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DTAQLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NAME2_C) TYPE(*CHAR) LEN(5) VALUE('00001')
             DCL        VAR(&NAME2_N) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&LOOP) TYPE(*DEC) LEN(3 0) VALUE(1)
 
             DCL        VAR(&RTCD)  TYPE(*CHAR) LEN(7)
             DCL        VAR(&ERMS)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&FULLCHECK) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&ZOZONE)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&ZOSHTC)  TYPE(*CHAR) LEN(4)
             DCL        VAR(&ZORDNB)  TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&ZODNWD)  TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&ZOBCCY)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&ZONJOB)  TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&PROC)  TYPE(*CHAR) LEN(1)
 
             DCLF       FILE(CBCMPNLC)
             DCLF       FILE(SDSBNKTD) OPNID(SD)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2013')
 
/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Send message to MRUNQ. */
             SNDPGMMSG  MSG('GOC2110T1 - GPWTRAPPD Housekeep +
                           - Start Task Split') TOMSGQ(MRUNQ)
 
/* Get zone */
             CALL       PGM(GOGETZONE) PARM(&RTCD &ERMS &FULLCHECK +
                          &ZOZONE &ZOSHTC &ZORDNB &ZODNWD &ZOBCCY +
                          &ZONJOB)
             IF         COND(&RTCD *NE '       ') THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
/* Determine if it is this zone to do the housekeeping */
 
             RCVF       OPNID(SD)
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             CALL       PGM(GP210000) PARM(&RTCD &ERMS &ZOZONE +
                          &SD_BJMRDT &PROC)
             IF         COND(&RTCD *NE '       ') THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             IF         COND(&PROC *NE 'Y') THEN(DO)
                GOTO       CMDLBL(END)
             ENDDO
 
/* Determine full DTAQLIB library name. */
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(&DTAQLIB) VALUE(&SYSID *CAT 'DPLIB')
 
/* Clear index file. */
             CLRPFM     FILE(&INDEXFL)
             CLRPFM     FILE(&CTRLFL)
 
/* Remove all members from driving file. */
             RMVM       FILE(&DRIVER) MBR(*ALL)
             MONMSG     MSGID(CPF7301 CPF7303)
 
/* Clear main data queue. */
             CALL       PGM(QCLRDTAQ) PARM(&RCVDTAQ  '*LIBL')
 
/* Retrieve number of sub-tasks. */
CHAIN:
             RCVF
             CHGVAR     VAR(&NAME2_N) VALUE(&DHCSEQ)
             IF         COND((&DHCOTT *NE &CNAME) *OR (&NAME2_N *NE +
                          &CSEQ)) THEN(DO)
                GOTO       CMDLBL(CHAIN)
             ENDDO
 
/* Add all members and create data queues. */
             CHGVAR     VAR(&NAME2_C) VALUE(&DHCSEQ)
LOOP:
             IF         COND(&LOOP *GT &DHTSNO) THEN(DO)
                GOTO       CMDLBL(START)
             ENDDO
 
             CHGVAR     VAR(&MEMBER) VALUE(&NAME1 *CAT &NAME2_C)
             ADDPFM     FILE(&DRIVER) MBR(&MEMBER)
 
/* Create data queue if it doesn't already exist and set authorities. */
             CHKOBJ     OBJ(&DTAQLIB/&MEMBER) OBJTYPE(*DTAQ)
             MONMSG     MSGID(CPF9801) EXEC(DO)
                CRTDTAQ    DTAQ(&DTAQLIB/&MEMBER) MAXLEN(50)
             ENDDO
             CALL       PGM(QCLRDTAQ) PARM(&MEMBER &DTAQLIB)
 
             IF         COND(&LOOP *EQ 1) THEN(DO)
                CHGVAR     VAR(&NAME2_N) VALUE(01000)
                CHGVAR     VAR(&NAME2_N) VALUE(&NAME2_N + &CSEQ - 1)
             ENDDO
 
             CHGVAR     VAR(&LOOP) VALUE(&LOOP + 1)
             CHGVAR     VAR(&NAME2_N) VALUE(&NAME2_N + 1)
             CHGVAR     VAR(&NAME2_C) VALUE(&NAME2_N)
             GOTO       CMDLBL(LOOP)
 
START:
/* Populate control file of GPWTRAP batch */
             RUNSQL     SQL('insert into ' *CAT &CTRLFL *CAT +
                            ' select T_ID, COUNT(T_ID)       +
                              from GPWTRAPPD                 +
                              group by T_ID                  +
                              order by T_ID')                +
                          COMMIT(*NONE)
 
             GOTO       CMDLBL(END)
 
ABNOR:
             SNDPGMMSG  MSG('Program GOC2110T1 ended abnormally') +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)
             CHGJOB     SWS(XXXXXX11)
 
 END:
             ENDPGM
