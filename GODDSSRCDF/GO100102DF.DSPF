      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GO Audit Enquiry - Summary')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  GO100102DF - Audit Enquiry - Summary                         *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG2410            Date 30Apr04               *
      *                 CGP004  *CREATE    Date 25May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG2410 - Improve web-facing display                         *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF12
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A            DDACTN         1A  I  8  2AUTO(RA)
     A N65                                  DSPATR(HI UL)
     A  65                                  DSPATR(RI PC)
     A            AHINPT         4A  O  8  4DSPATR(HI)
     A            AHUSER        10A  O  8 10DSPATR(HI)
     A            AHCNT          7  0O  8 25EDTWRD('     0 ')
     A                                      DSPATR(HI)
     A**Start of change BUG2410
     A            AIDESC        40A  O  8 35DSPATR(HI)
     A**End of change BUG2410
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'GO100102'
     A                                  1 32'AUDIT ENQUIRY - SUMMARY'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4 57 'FROM'
     A            FRDATE         7A  O  4 62DSPATR(HI)
     A                                      OVRATR
     A                                  4 70 'TO'
     A            TODATE         7A  O  4 73DSPATR(HI)
     A                                      OVRATR
     A                                  5  4'Input'
     A                                  5 10'User'
     A                                  5 25'Count of Updates'
     A            DDINPT         4A  B  6  4DSPATR(HI UL PC)
     A                                      OVRDTA
     A            DDUSER        10A  B  6 10DSPATR(HI UL PC)
     A                                      OVRDTA
     A                                  7  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
      *
      * -------------------------------------------------------------------*
      *   NO DATA                                                          *
      * -------------------------------------------------------------------*
      *
     A          R NODATA
     A                                      PUTOVR
     A                                  8  2'                                  -
     A                                                                         -
     A                                               '
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit F12=Abandon               -
     A                                      Rollup  Rolldown'
     A                                      OVRATR
