      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GO Audit Enquiry - User')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  GO100106DF - Audit Enquiry - User                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGP004  *CREATE    Date 25May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      DSPSIZ(*DS4)
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF12
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A            DDACTX         1A  I  8  2
     A N65                                  DSPATR(HI UL)
     A  65                                  DSPATR(RI PC)
     A            AHINPT         4A  O  8  4DSPATR(HI)
     A            AHREFI        40A  O  8 10DSPATR(HI)
     A            AHACTN        10A  O  8 60DSPATR(HI)
     A            AHBRCA         3A  O  8 74DSPATR(HI)
     A            RUNDAT         7A  O  8 84DSPATR(HI)
     A            MACDAT         7A  O  8 98DSPATR(HI)
     A            AHTIME         6  0O  8110EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A            AHCCID        10  0O  8122DSPATR(HI)
     A            AHRDNB         5S 0H
     A            AHCID         17S 0H
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'GO100106'
     A                                  1 32'AUDIT ENQUIRY - USER'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  4 'USER:'
     A            I#USER        10A  O  4 10DSPATR(HI)
     A                                      OVRATR
     A            USER1         25A  O  4 21DSPATR(HI)
     A                                      OVRATR
     A                                  4 57 'FROM'
     A            FRDATE         7A  O  4 62DSPATR(HI)
     A                                      OVRATR
     A                                  4 70 'TO'
     A            TODATE         7A  O  4 73DSPATR(HI)
     A                                      OVRATR
     A                                  5  4'Input'
     A                                  5 10'<---------------ID--------------->'
     A                                  5 62'Action'
     A                                  5 73'Branch'
     A                                  5 83'Run Date'
     A                                  5 97'Machine Date  Time'
     A                                  5122'TRN'
     A            DDINPT         4A  B  6  4DSPATR(HI UL PC)
     A                                      OVRDTA
     A            DDREFI        40A  B  6 10DSPATR(HI UL PC)
     A                                      OVRDTA
     A            DDACTN        10A  B  6 60DSPATR(HI UL PC)
     A                                      OVRDTA
     A            DDBRCA         3A  B  6 74DSPATR(HI UL PC)
     A                                      OVRDTA
     A            DDRUND         7A  B  6 84DSPATR(HI UL PC)
     A                                      OVRDTA
     A            DDMACD         7A  B  6 98DSPATR(HI UL PC)
     A                                      OVRDTA
     A            DDCCID        10A  B  6122DSPATR(HI UL PC)
     A                                      OVRDTA
     A                                  7  1'                                  -
     A                                                                         -
     A                                                                         -
     A                                                              '
     A                                      DSPATR(UL)
      *
      * -------------------------------------------------------------------*
      *   NO DATA                                                          *
      * -------------------------------------------------------------------*
      *
     A          R NODATA
     A                                      PUTOVR
     A                                  8  2'                                  -
     A                                                                         -
     A                                               '
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                                                         -
     A                                                          '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit F12=Abandon               -
     A                                      Rollup  Rolldown'
     A                                      OVRATR
