      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GO Audit Update Overview Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  DSPF/GO100108DF - Audit Update Overview Enquiry              *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGP004  *CREATE    Date 25May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(*DS4)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      CF03
     A                                      CF08
     A                                      CF10
     A                                      CF12
     A          R MAIN
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      RTNDTA
     A                                      PUTOVR
     A                                      BLINK
     A                                      ROLLUP(05)
     A                                      ROLLDOWN(06)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'GO100108'
     A                                  1 32'AUDIT UPDATE OVERVIEW ENQUIRY'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A            DDBEFAFT      12A     2 40DSPATR(HI)
     A                                      OVRDTA
     A                                  3  1 'Input:'
     A            AHINPT         4A  O  3  9DSPATR(HI)
     A            AIDESC        40A  O  3 14DSPATR(HI)
     A                                  4  1 'Action:'
     A            AHACTN        10A  O  4  9DSPATR(HI)
     A                                  4 20 'ID:'
     A            AHREFI        40A  O  4 24DSPATR(HI)
     A                                  4 65 'User:'
     A            AHUSER        10A  O  4 71DSPATR(HI)
     A                                  4 82 'Run Date:'
     A            RUNDAT         7A  O  4 92DSPATR(HI)
     A                                  4100 'Machine Date:'
     A            MACDAT         7A  O  4114DSPATR(HI)
     A                                  4122 'Time:'
     A            DDSTIM         4  0O  4128EDTWRD('  :  ')
     A                                      DSPATR(HI)
     A            CTX          131A  O  6  1DSPATR(UL)
     A                                      OVRDTA
     A            SC1          131A  O  7  1
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC2          131A  O  8  1
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC3          131A  O  9  1
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC4          131A  O 10  1
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC5          131A  O 11  1
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC6          131A  O 12  1
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC7          131A  O 13  1
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC8          131A  O 14  1
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC9          131A  O 15  1
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC10         131A  O 16  1
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC11         131A  O 17  1
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC12         131A  O 18  1
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC13         131A  O 19  1
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC14         131A  O 20  1
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC15         131A  O 21  1
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 22  1'                                  -
     A                                                                         -
     A                                                                         -
     A                                                                '
     A                                      DSPATR(UL)
     A                                 23  2'Page:'
     A            SCRCNT         2S 0O 23  8DSPATR(HI)
     A                                      OVRDTA
     A                                 24  2'F3=Exit, F8=Field Values, F10=Befo-
     A                                      re/After, F12=Abandon    Rollup  Ro-
     A                                      lldown'
