      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_NM                                                        *
/*EXI *  MBR(ACCT)                                                    *
/*EXI *  TEXT('Midas GO Work Events')
/*EXI *  REUSEDLT(*YES)                                               *                 /*MD032884*/
/*XAI1*  ADDPFM FILE(GOWEVNTPD) MBR(FXDL) SHARE(*YES)                 *
/*XAI2*  ADDPFM FILE(GOWEVNTPD) MBR(LDNI) SHARE(*YES)                 *
/*XAI3*  ADDPFM FILE(GOWEVNTPD) MBR(NASP) SHARE(*YES)                 *
/*XAI4*  ADDPFM FILE(GOWEVNTPD) MBR(FRAS) SHARE(*YES)                 *
/*XAI5*  ADDPFM FILE(GOWEVNTPD) MBR(SIRS) SHARE(*YES)                 *
/*XAI6*  ADDPFM FILE(GOWEVNTPD) MBR(CIRS) SHARE(*YES)                 *
/*XAI7*  ADDPFM FILE(GOWEVNTPD) MBR(CFCO) SHARE(*YES)                 *
/*XAI8*  ADDPFM FILE(GOWEVNTPD) MBR(LOAN) SHARE(*YES)                 *
/*XAI9*  ADDPFM FILE(GOWEVNTPD) MBR(SBPS) SHARE(*YES)                 *
/*XAI0*  ADDPFM FILE(GOWEVNTPD) MBR(SCPS) SHARE(*YES)                 *
/*XAIA*  ADDPFM FILE(GOWEVNTPD) MBR(FBPS) SHARE(*YES)                 *
/*XAIB*  ADDPFM FILE(GOWEVNTPD) MBR(FCPS) SHARE(*YES)                 *
/*XAIC*  ADDPFM FILE(GOWEVNTPD) MBR(STRD) SHARE(*YES)                 *                   /*BG3512*/
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GOWEVNTPD - Zone Work Events                              *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 MD032884           Date 08Jun15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 25Nov05               *
      *                 BG3512             Date 29Jul04               *
      *                 CGL029             Date 01Sep03               *
      *                 CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  MD032884 - Performance enhancement for GOCXCLTS. Change      *
      *             PF attribute to have REUSEDLT(*YES).              *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BG3512 - Extract of SE Trades was missing                    *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGP001 - Global Processing                                   *
      *                                                               *
      *****************************************************************
      *
     A          R GOWEVNTD0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A            E_ZONE        10A         TEXT('Zone')
     A            E_MOD          2A         TEXT('Module')
     A            E_SMOD         4A         TEXT('Sub-Module')
      * Begin /CSD027/
     A**********  E_CNUM         6S 0       TEXT('Customer Number')                           CSD027
     A            E_CNUM         6A         TEXT('Customer Number')
      * End /CSD027/
     A            E_PTFR         4A         TEXT('Portfolio reference')
     A            E_BRCA         3A         TEXT('Booking Branch')
     A            E_BOOK         2A         TEXT('Book')
     A            E_SESN        10A         TEXT('Security Shortname')
     A            E_MKTI         1A         TEXT('Market Indicator')
      **Start of change CGL029
     A**********  E_TREF        20A         TEXT('Transaction Reference')                     CGL029
     A            E_TREF        26A         TEXT('Transaction Reference')
      **End of change CGL029
      *
     A            E_TVDI         1A         TEXT('Trade/Value Date')
      *
     A            E_PRDT         5S 0       TEXT('Processing Date')
     A            E_PRSQ         2S 0       TEXT('Processing Seq')
     A            E_EVTP         2A         TEXT('Event Type')
     A            E_EVRF         6A         TEXT('Event Reference')
      *
      ** Start of change CSD103
     A**********  E_BRTT         2S 0       TEXT('Base Rate Code')
     A            E_BRTT         2A         TEXT('Base Rate Code')
      ** End of change CSD103
     A            E_BRTE        11P 7       TEXT('Base Rate')
     A            E_RTSP        11P 7       TEXT('Rate/Spread')
     A            E_SPIN         1A         TEXT('Spread Ind')
     A            E_IRTP         1A         TEXT('Interest Rate Type')
     A            E_IRCH         1A         TEXT('Interest Rate Change')
     A            E_IRAT        11P 7       TEXT('Interest Rate')
      *
     A            E_ICMT         7A         TEXT('Int Calculation Method')
     A            E_INTC         1A         TEXT('Int Capitalization Ind')
     A            E_STAC         1A         TEXT('Stop accruals - Y/blank')
      *
     A            E_PRAM        15P 0       TEXT('Principal Amount')
     A            E_INTA        15P 0       TEXT('Interest Amount')
     A            E_TAMT        15P 0       TEXT('Total Amount')
     A            E_FEE         15P 0       TEXT('Fees')
      *
     A            E_NOML        15P 0       TEXT('Nominal')
     A            E_CCY          3A         TEXT('Currency')
     A            E_NCDP         1S 0       TEXT('Ccy Dec Places')
     A            E_NMDP         1S 0       TEXT('Nom Dec Places')
      *
     A            E_ACIN         1A         TEXT('Accrued Ind.')
      *
     A            E_SDVA        13P 8       TEXT('Amount per unit')
     A            E_CPDP        15P 0       TEXT('Current Partly Paid Price')
     A            E_CFCT        10P 9       TEXT('Current Factor')
      *
     A            E_SSET         1A         TEXT('Suppress Settlement Y')
     A            E_SETM         2A         TEXT('Settle Method')
      **Start of change CGL029
     A**********  E_SETA        12A         TEXT('Settle A/C')                                CGL029
     A            E_SETA        18A         TEXT('Settle A/C')
      **End of change CGL029
     A            E_SETB         3A         TEXT('Settle Branch')
     A            E_SETC         3A         TEXT('Settle Currency')
      *
     A            E_IO           1A         TEXT('In/Out - I/O/blank')
     A            E_NIND         5P 0       TEXT('Next Interest Date')
     A            E_NINA        15P 0       TEXT('Next Interest Amount')
      *
     A            E_PRCD         1A         TEXT('Processed - Y/blank')
