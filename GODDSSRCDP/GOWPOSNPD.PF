      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_NM                                                        *
/*EXI *  MBR(ACCT)                                                    *
/*EXI *  TEXT('Midas GO Work Positions')
/*XAI1*  ADDPFM FILE(GOWPOSNPD) MBR(FXDL) SHARE(*YES)                 *
/*XAI2*  ADDPFM FILE(GOWPOSNPD) MBR(LDNI) SHARE(*YES)                 *
/*XAI3*  ADDPFM FILE(GOWPOSNPD) MBR(NASP) SHARE(*YES)                 *
/*XAI4*  ADDPFM FILE(GOWPOSNPD) MBR(FRAS) SHARE(*YES)                 *
/*XAI5*  ADDPFM FILE(GOWPOSNPD) MBR(SIRS) SHARE(*YES)                 *
/*XAI6*  ADDPFM FILE(GOWPOSNPD) MBR(CIRS) SHARE(*YES)                 *
/*XAI7*  ADDPFM FILE(GOWPOSNPD) MBR(CFCO) SHARE(*YES)                 *
/*XAI8*  ADDPFM FILE(GOWPOSNPD) MBR(LOAN) SHARE(*YES)                 *
/*XAI9*  ADDPFM FILE(GOWPOSNPD) MBR(SBPS) SHARE(*YES)                 *
/*XAI0*  ADDPFM FILE(GOWPOSNPD) MBR(SCPS) SHARE(*YES)                 *
/*XAIA*  ADDPFM FILE(GOWPOSNPD) MBR(FBPS) SHARE(*YES)                 *
/*XAIB*  ADDPFM FILE(GOWPOSNPD) MBR(FCPS) SHARE(*YES)                 *
/*XAIC*  ADDPFM FILE(GOWPOSNPD) MBR(STRD) SHARE(*YES)                 *                   /*BG3512*/
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GOWPOSNPD - Zone Work Positions                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 25Nov05               *
      *  Prev Amend No. BG3512             Date 29Jul04               *
      *                 CGL029             Date 01Sep03               *
      *                 CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BG3512 - Extract of SE Trades was missing                    *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGP001 - Global Processing                                   *
      *                                                               *
      *****************************************************************
      *
     A          R GOWPOSND0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A            P_ZONE        10A         TEXT('Zone')
     A            P_MOD          2A         TEXT('Module')
     A            P_SMOD         4A         TEXT('Sub-Module')
      * Begin /CSD027/
     A**********  P_CNUM         6S 0       TEXT('Customer Number')                           CSD027
     A            P_CNUM         6A         TEXT('Customer Number')
      * End /CSD027/
     A            P_PTFR         4A         TEXT('Portfolio reference')
     A            P_BRCA         3A         TEXT('Booking Branch')
     A            P_BOOK         2A         TEXT('Book')
     A            P_SESN        10A         TEXT('Security Shortname')
     A            P_MKTI         1A         TEXT('Market Indicator')
      **Start of change CGL029
     A**********  P_TREF        20A         TEXT('Transaction Reference')                     CGL029
     A            P_TREF        26A         TEXT('Transaction Reference')
      **End of change CGL029
      *
     A            P_TVDI         1A         TEXT('Trade/Value Date')
     A            P_PSTP         1A         TEXT('Position Type')
     A            P_PDAT         5P 0       TEXT('Position Date')
     A            P_ASLI         1A         TEXT('Asset/Liability')
      *
     A            P_AITC        15P 0       TEXT('Accrued Int To Ctl Date')
     A            P_IPRD        15P 0       TEXT('Int Paid To Date')
     A            P_ICTD        15P 0       TEXT('Int Capitalized To Date')
     A            P_IWOD        15P 0       TEXT('Int Written Off To Date')
     A            P_AIAD        15P 0       TEXT('Accrued Int Adjustments')
     A            P_AITD        15P 0       TEXT('Accrued Interest To Date')
     A            P_NINA        15P 0       TEXT('Next Interest Amount')
     A            P_TOTI        15P 0       TEXT('Total Interest')
      *
     A            P_INAC        15P 0       TEXT('Interest Accrued')
      *
     A            P_SDAT         5P 0       TEXT('Start Date of Int Calc')
     A            P_EDAT         5P 0       TEXT('End Date of Int Calc')
     A            P_NDYS         5P 0       TEXT('Number of Days')
     A            P_YBAS         5P 0       TEXT('Year Basis')
      *
     A            P_NIND         5P 0       TEXT('Next Interest Date')
      *
     A            P_NOML        15P 0       TEXT('Nominal/Principal')
     A            P_CCY          3A         TEXT('Currency')
     A            P_NCDP         1S 0       TEXT('Nominal Ccy Dec Places')
     A            P_NMDP         1S 0       TEXT('Nominal Dec Places')
      *
     A            P_ONOM        15P 0       TEXT('Other Nominal/Principal')
     A            P_OCCY         3A         TEXT('Other Currency')
     A            P_OASL         1A         TEXT('Other Asset/Liability')
      *
     A            P_BRTT         2S 0       TEXT('Base Rate Code')
     A            P_BRTE        11P 7       TEXT('Base Rate')
     A            P_RTSP        11P 7       TEXT('Rate/Spread')
     A            P_SPIN         1A         TEXT('Spread Ind')
     A            P_IRAT        11P 7       TEXT('Interest/Discount Rate')
     A            P_CHIN         1A         TEXT('Change Ind')
      *
     A            P_CLRT        11P 7       TEXT('Ceiling Rate')
     A            P_FLRT        11P 7       TEXT('Floor Rate')
      *
     A            P_YRAT        11P 7       TEXT('Yield Rate')
      *
     A            P_INPM         1A         TEXT('(Up front) Int Pay Method')
     A            P_INTC         1A         TEXT('Int Capitalization Y/N')
     A            P_ICMT         7A         TEXT('Int Calc Method')
     A            P_IPY          2S 0       TEXT('Int Payments/Year')
     A            P_RDFC         1A         TEXT('Round Down Int')
      *
     A            P_REPT         1S 0       TEXT('Repayment Type')
     A            P_STAC         1A         TEXT('Stop Accruals')
     A            P_ACNG         1A         TEXT('Accrue Negative Balance')
      *
     A            P_CFCT        10P 9       TEXT('Current Factor')
     A            P_FUPP        15P 0       TEXT('Fully Paid Price')
      *
     A            P_MKPR        15P 8       TEXT('Market Price')
     A            P_MKVL        15P 0       TEXT('Market Value')
      *
     A            P_MKPB        15P 8       TEXT('Market Price - Bid')
     A            P_MKVB        15P 0       TEXT('Market Value - Bid')
      *
     A            P_MKPO        15P 8       TEXT('Market Price - Offer')
     A            P_MKVO        15P 0       TEXT('Market Value - Offer')
      *
     A            P_VCCY         3A         TEXT('Valuation Currency')
      *
     A            P_SPBS         1A         TEXT('Security Price Basis')
     A            P_YBON         1A         TEXT('Yen Bond?')
     A            P_SCHU         1A         TEXT('Schuldschein?')
     A            P_MORT         1A         TEXT('Mortgage backed bond?')
     A            P_PART         1A         TEXT('Partly paid bond?')
     A            P_ACUR         1S 0       TEXT('Accuracy')
      *
     A            P_ACIN         1A         TEXT('Accrue Interest')
     A            P_AMIN         1A         TEXT('Amortize Interest')
