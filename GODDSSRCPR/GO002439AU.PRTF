      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Expired Global Historic Spot Rates Rep-Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PRTF/GO002439AU - Midas Expired Global Historic Spot Rates   *
      *                    Report - Audit                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS014  *Create    Date 10Aug05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS014 - IAS21 The Effects of Changes in Foreign Exchange    *
      *           Rates                                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **********************************
      ***  Report Header Record Format *
      **********************************
      *
     A          R HEADAU
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2005')
      *
     A                                      SKIPB(001)
     A                                      SPACEA(001)
     A                                     6
     A                                      'REFERENCE GO002439P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    42
     A                                      'EXPIRED GLOBAL HISTORIC SPOT RATES-
     A                                       - AUDIT'
     A                                      SPACEB(002)
      **********************************
      ***  Report Detail Record Format *
      **********************************
      *
     A          R DTAILA1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    69
     A                                      'Calculated'
     A                                    27
     A                                      'Total No. of Records Before Update-
     A                                       :'
     A                                      SPACEB(002)
     A            RRNO           6S 0O    71
     A                                      EDTCDE(4)
     A                                    27
     A                                      'Total No. of Records Deleted :'
     A                                      SPACEB(002)
     A            RDLT           6S 0O    71
     A                                      EDTCDE(4)
     A                                    27
     A                                      'Total No. of Records in File :'
     A                                      SPACEB(002)
     A            RTOT           6S 0O    71
     A                                      EDTCDE(4)
      *
      *******************************
      ***  No Details Record Format *
      *******************************
      *
     A          R DTAILA2
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                    47
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      ***********************************
      ***  Database Error Record Format *
      ***********************************
      *
     A          R DTAILA3
     A                                      SPACEB(001)
     A                                    32
     A                                      '**********************************-
     A                                      ***********************************'
     A                                      SPACEB(003)
     A                                    32
     A                                      '*'
     A                                      SPACEB(001)
     A                                   100
     A                                      '*'
     A                                    32
     A                                      '*'
     A                                      SPACEB(001)
     A                                   100
     A                                      '*'
     A                                    32
     A                                      '*    FILE NAME    '''
     A                                      SPACEB(001)
     A            DBFILE         8A       51
     A                                    59
     A                                      ''''
     A                                   100
     A                                      '*'
     A                                    32
     A                                      '*    KEY          '''
     A                                      SPACEB(001)
     A            DBKEY         29        51
     A                                    80
     A                                      '''                   *'
     A                                    32
     A                                      '*    PROGRAM NAME '''
     A                                      SPACEB(001)
     A            DBPGM         10A       51
     A                                    61
     A                                      ''''
     A                                   100
     A                                      '*'
     A                                    32
     A                                      '*    AT           '''
     A                                      SPACEB(001)
     A            DBASE          3  0     51
     A                                    54
     A                                      ''''
     A                                   100
     A                                      '*'
     A                                    32
     A                                      '*'
     A                                      SPACEB(001)
     A                                   100
     A                                      '*'
     A                                    32
     A                                      '*'
     A                                      SPACEB(001)
     A                                   100
     A                                      '*'
     A                                    32
     A                                      '**********************************-
     A                                      ***********************************'
     A                                      SPACEB(001)
      *
      **********************************
      ***  End Report Record Format *
      **********************************
      *
     A          R ENDREAU
     A                                      SPACEB(001)
     A                                    51
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(001)
