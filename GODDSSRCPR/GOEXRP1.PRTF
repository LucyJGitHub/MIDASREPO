      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GO Daily Exception Report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/GOEXRP1 - Midas GO Exception Daily Report               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No.CSD086              Date 18Mar10               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No.BUG7841             Date 04Aug05               *
      *                CGP008  *CREATE     Date 15Sep04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD086 - Overridable Errors                                  *
      *  BUG7841- Fixed truncation of exception message.              *
      *  CGP008 - Exception reporting                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADER1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     6'REFERENCE GOEXRP1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     6'ZONE:'
     A            RPZONE        10A       12
     A                                    37'Daily Exceptions +
     A                                      Reported in Branch '
     A            RPBRCA         3A       73
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                    37'----------------------+
     A                                       -----------------'
     A                                      SPACEB(001)
      *
      ** Exception Details
      *
     A          R DETAIL1
     A                                     3'TRANSACTION REFERENCE:'
     A                                      SPACEB(002)
     A            RPTREF        50A        3
     A                                      SPACEB(001)
      *
      ** Start of change BUG7841
      *
     A                                     3'ACTION:'
     A                                      SPACEB(001)
     A            RPTACT        40A       12
      *
      ** Start of change CSD086
      *
     A                                    55'FORCE POST:'
     A            RPFORC         3A       67
      *
      ** End of change CSD086
      *
      *
      ** End of change BUG7841
      *
     A                                     3'EXCEPTION'
     A                                      SPACEB(001)
     A                                    14'MODULE'
     A                                    22'API'
     A                                    27'USER'
     A                                    38'WRITE'
     A                                    49'WRITE'
      *
      ** Start of change CSD086
      *
     A**********                          60'WARNING'                                         CSD086
     A                                    60'BUSINESS'
     A                                   110'BUSINESS ERROR'
      *
      ** End of change CSD086
      *
      *
     A                                    38'TIME'
     A                                      SPACEB(001)
     A                                    49'DATE'
      *
      ** Start of change CSD086
      *
     A**********                          60'MESSAGE'                                         CSD086
     A                                    60'ERROR'
     A                                   110'CLASSIFICATION'
      *
      ** End of change CSD086
      *
      *
      ** More Exception Details
      *
     A          R DETAIL2
     A            RPDESC       100A        3
     A                                      SPACEB(001)
      *
      ** Start of change CSD086
      *
     A            RPCLAS        12A      110
      *
      ** End of change CSD086
      *
     A            RPMOD          2A       14
     A                                      SPACEB(001)
     A            RPAPI          4A       22
     A            RPUSER        10A       27
     A            RPWTIME       10A       38
     A            RPWDAT        10A       49
     A            RPWARC        10A       60
      *
      ** Start of change BUG7841
      *
     A**********  RPWARNM      100A       71                                                 BUG7841
     A            RPWARNM       60A       71
     A            RPWARNM2      60A       71
     A                                      SPACEB(001)
     A            RPWARNM3      60A       71
     A                                      SPACEB(001)
      *
      ** End of change BUG7841
      *
      *
      ** Report Header Record Format
      *
     A          R NODETR1
     A                                    37'NO EXCEPTIONS REPORTED -
     A                                      TODAY IN ANY BRANCH'
     A                                      SPACEB(003)
     A                                    37'**********************+
     A                                       ********************'
     A                                      SPACEB(001)
