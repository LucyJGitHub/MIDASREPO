     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2006')
      *****************************************************************
/*S*D ***RPGBASEMOD****************************************************                       CCB023
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  GO000010 - Extract country management limit exposure from    *
      *             Equation                                          *
      *                                                               *
      *  Function:  This program updates country management limit     *
      *             exposure Source & Audit table                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. CCB023 *REDUNDANT Date 06Aug12                *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CRE026  *CREATE    Date 24May06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCB023 - COB Restructure - Input Cycle Termination           *
      *           Restructuring                                       *
      *  CRE026 - Consumer Banking                                    *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Use of indicators.                                           *
      *    XX    - Function of Indicator                              *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E  I N D E X                               *
      *                                                               *
      *  *PSSR      -  Program exception error routine                *
      *  *INZSR     -  Initialisation Subroutine                      *
      *  AU_POP - Populate Country Management limit exposure          *
      *  Audit table                                                  *
      *                                                               *
      *  The *INZSR subroutine will only get called automatically     *
      *  on entry to the module the first time it is run              *
      *  (unless you end the program with LR on).  Similarly          *
      *  D-spec initialisation only happens the first time.           *
      *                                                               *
      *                                                               *
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ F-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
      * Equation Country Management Limits exposure table
     FACAPF     IF   E             DISK    INFSR(*PSSR)
 
      * Midas Country Management Limits exposure table
     FT_MLEQCE  O    E             DISK    INFSR(*PSSR)
     F                                     RENAME(T_MLEQCE : MLEQCE)
 
      * Midas Country Management Limits exposure table Audit Trail
     FT_MLEQCEAUO    E             DISK    INFSR(*PSSR)
     F                                     RENAME(T_MLEQCEAU : MLEQCEAU)
 
      ** +--------------------------------------+
      ** ¦ Automatically included D-specs       ¦
      ** ¦ ==============================       ¦
      ** +--------------------------------------+
      ** The following /COPY line includes (among others) the LDA layout
      ** and the copyright array definition:
     D/COPY ZACPYSRC,STD_D_SPEC
 
      ** The following /COPY line includes all the defined fields in the
      ** PSDS.  They have meaningful names, prefixed by 'PS'.
     D/COPY ZACPYSRC,PSDS
 
      **------------------------------------------------------------------
      ** +--------------------------------------+
      ** ¦ Named constants                      ¦
      ** ¦ ===============                      ¦
      ** +--------------------------------------+
 
      ** +--------------------------------------+
      ** ¦ Arrays and Data Structures           ¦
      ** ¦ ==========================           ¦
      ** +--------------------------------------+
      * Equation Country Exposure data
     D EQCEXP        E DS                  EXTNAME(ACAPF)
      *
      * Midas Plus Country Exposure data
     D MICEXP        E DS                  EXTNAME(T_MLEQCE)
      *
      * Midas Plus Country Exposure Audit table Data
     D MICEXPAU      E DS                  EXTNAME(T_MLEQCEAU)
      *
      * 200 byte DS for incoming data from Access Program
     D DSFDY         E DS                  EXTNAME(DSFDY)
      *
      *DS for Bank data
     D SDBANK        E DS                  EXTNAME(SDBANKPD)
 
      ** +--------------------------------------+
      ** ¦ Declared variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+
      *
     D @RUN            S              1A
      * Field passed as parm to ZDATE2
     D W_CONST         S              1A
 
     D PDATE           S              6  0
     D PADATE          S              7A
 
      *****************************************************************
      /EJECT
      *****************************************************************
      *****************************************************************
      * MAIN PROCEDURE                                                *
      *****************************************************************
      *
      ** Read each record on Equation exposure file ACAPF
      *
     C                   READ      ACAPF
     C                   DOW       NOT %EOF(ACAPF)
      *
      ** Clear Source table DS & Audit table record format
      *
     C                   CLEAR                   MICEXP
     C                   CLEAR                   MICEXPAU
      *
      ** Call Mapping program to map EQ to Midas
      *
     C                   CALL      'GOEQCESET'
      *
      ** Input
      *
     C                   PARM                    EQCEXP
      *
      ** Output
      *
     C                   PARM                    MICEXP
      *
      ** Write record to Midas Country Management limit exposure Source table
      *
     C                   WRITE     MLEQCE
      *
      ** Populate Midas Country Management limit exposure Audit table
      *
     C                   EXSR      AU_POP
      *
      ** Write record to Midas Country Management limit exposure Audit table
      *
     C                   WRITE     MLEQCEAU
     C                   READ      ACAPF
     C                   ENDDO
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *****************************************************************
      /EJECT
      *****************************************************************
      *****************************************************************
      *                                                               *
      *  *INZSR - Program Initialisation routine                      *
      *                                                               *
      *  Called by: Implicitly on program activation                  *
      *                                                               *
      *  Calls: *PSSR                                                 *
      *                                                               *
      *****************************************************************
     C     *INZSR        BEGSR
      *
      ** Access bank info for Midas Run Date
      *
     C                   CALL      'AOBANKR0'
     C                   PARM      *BLANK        @RTCD
     C                   PARM      '*FIRST '     @OPTN
     C     SDBANK        PARM      SDBANK        DSFDY
      *
      ** ERROR
      *
     C     @RTCD         IFNE      *BLANKS
     C                   MOVEL     'GO000010'    DBPGM
     C                   MOVEL     'SDBANKPD'    DBFILE
     C                   MOVEL     '005'         DBASE
     C                   MOVEL     @OPTN         DBKEY
     C                   EXSR      *PSSR
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program exception error routine                     *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *  Called by: (**calling routines**)                            *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C     *PSSR         BEGSR
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN
     C                   DUMP
     C                   ENDIF
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *  AU_POP - Populate Country Management limit exposure          *
      *  Audit table                                                  *
      *                                                               *
      *  Called by: MAIN PROCESSING                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C     AU_POP        BEGSR
      *
     C                   EVAL      W_CONST = 'D'
      *
      ** Call ZDATE2 to get Midas Run date in DDMMYY format
      *
     C                   CALLB     'ZDATE2'
     C                   PARM                    BJRDNB
     C                   PARM                    W_CONST
     C                   PARM      *ZERO         PDATE
     C                   PARM      *BLANKS       PADATE
 
     C                   EVAL      CNAUDATE    = %DATE(PDATE:*DMY)
     C                   EVAL      CNAUCODE    = CNCODE
     C                   EVAL      CNAUBRCH    = CNBRCH
     C                   EVAL      CNAUCCY     = CNCCY
     C                   EVAL      CNAUINCODE  = CNINCODE
     C                   EVAL      CNAUFDATE   = CNFDATE
     C                   EVAL      CNAUTDATE   = CNTDATE
     C                   EVAL      CNAUEXPOS   = CNEXPOSURE
     C                   EVAL      CNAUOFFSET  = CNOFFSET
     C                   EVAL      CNAUZONE    = CNZONE
      *
     C                   ENDSR
**  CPY@
(c) Misys International Banking Systems Ltd. 2006
