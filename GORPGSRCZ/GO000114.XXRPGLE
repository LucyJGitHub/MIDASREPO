     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*STD****RPGBASEBND****************************************************                       CCB023
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  GO000114 - Global Processing data dowload, Account Codes     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CCB023  *REDUNDANT Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. 252816             Date 21Feb08               *
      *                 247082  *CREATE    Date 16Apr07               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCB023 - COB Restructure - Input Cycle Termination           *
      *           Restructuring                                       *
      *  252816 - issue USROPN to GPDWNLL0 to prevent creating dumps  *
      *           caused by opening a file which is already opened    *
      *  247082 - Include FF OTCO and OTCC in Midas Plus dashboard.   *
      *                                                               *
      *****************************************************************
      **
      ** +--------------------------------------+
      ** ¦ Files                                ¦
      ** ¦ =====                                ¦
      ** +--------------------------------------+
      *
     FGPCOWIPL  IF   E           K DISK    INFSR(*PSSR)
     FGPCCWIPL  IF   E           K DISK    INFSR(*PSSR)
     FGPZONEL2  IF   E           K DISK    INFSR(*PSSR)
     FGPDWNLL0  UF A E           K DISK    INFSR(*PSSR)
     F                                     USROPN                                             252816
      **
      ** +--------------------------------------+
      ** ¦ Arrays and Data Structures           ¦
      ** ¦ ==========================           ¦
      ** +--------------------------------------+
     D                 DS
     D  CountN                 1      5  0
     D  CountA                 1      5
      *
      * Program Status Data Structure
     D/COPY ZACPYSRC,PSDS
      *
      * Midas Local Data Area for database error reporting
     D LDA           E DS           256    EXTNAME(LDA) DTAARA(LDA)
      * The following fields are defined in the external file:
      *                                     134 141 DBFile
      *                                     142 170 DBKey
      *                                     171 180 DBPgm
      *                                     181 1830DBase
      *                                     184 193 DBMod
      *                                     194 203 DBProc
      ** +--------------------------------------+
      ** ¦ Declared variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+
      *
     D @RUN            S              1
     D Value           S             50
      *
      ** +--------------------------------------+
      ** ¦ End of D-specs                       ¦
      ** ¦ ==============                       ¦
      ** +--------------------------------------+
      *
      ** +--- Start of Main processing -----------------------------------+
      ** ¦                                                                ¦
      ** +----------------------------------------------------------------+
      *
      /COPY GOCPYSRC,GOUPD_PARM
      *
      * Get Zone Name from system ID.
     C     System_ID     CHAIN     GPZONED0                           90
      *
      * Unauthorised FF OTCO transactions
     C                   IF        Key_Desc = 'UnauthOTCO'
      *
     C                   EXSR      UpdOTCOCnt
      *
     C                   ENDIF
      *
      * Unauthorised FF OTCC transactions
     C                   IF        Key_Desc = 'UnauthOTCC'
      *
     C                   EXSR      UpdOTCCCnt
      *
     C                   ENDIF
      *
     C/COPY GOCPYSRC,GOUPD_DWNL
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      /EJECT
      *****************************************************************
      *                                                               *
      * UpdOTCOCnt - This does the read of the file containing        *
      *              unauthorised FF OTCO trans and updates the       *
      *              count accordingly.                               *
      *                                                               *
      * Called by: *MAIN                                              *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     UpdOTCOCnt    BEGSR
      *
      * If the Midas phase is not Input Cycle then set data to blank.
     C                   IF        MidasPhase <> 'A'
     C                   EVAL      Value = *blanks
     C                   ELSE
      *
     C                   EVAL      CountN = 0
      *
     C     *LOVAL        SETLL     T@COWIP
     C                   READ      T@COWIP                                01
      *
     C                   DOW       *IN01 = *OFF
     C                   IF        MSWZONE = ZOZONE
     C                   EVAL      CountN = CountN + 1
     C                   ENDIF
     C                   READ      T@COWIP                                01
     C                   ENDDO
      *
     C                   EVAL      Value = CountA
      *
     C                   ENDIF
      *
     C     UpdOTCOCntE   ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * UpdOTCCCnt - This does the read of the file containing        *
      *              unauthorised FF OTCC trans and updates the       *
      *              count accordingly.                               *
      *                                                               *
      * Called by: *MAIN                                              *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     UpdOTCCCnt    BEGSR
      *
      * If the Midas phase is not Input Cycle then set data to blank.
     C                   IF        MidasPhase <> 'A'
     C                   EVAL      Value = *blanks
     C                   ELSE
      *
     C                   EVAL      CountN = 0
      *
     C     *LOVAL        SETLL     T@CCWIP
     C                   READ      T@CCWIP                                01
      *
     C                   DOW       *IN01 = *OFF
     C                   IF        MSWZONE = ZOZONE
     C                   EVAL      CountN = CountN + 1
     C                   ENDIF
     C                   READ      T@CCWIP                                01
     C                   ENDDO
      *
     C                   EVAL      Value = CountA
      *
     C                   ENDIF
      *
     C     UpdOTCCCntE   ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR
      *
     C                   IF        @RUN = *BLANK
     C                   EVAL      @RUN = 'Y'
     C                   DUMP
     C                   ENDIF
      *
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
     C                   ENDSR
      *
      *****************************************************************
