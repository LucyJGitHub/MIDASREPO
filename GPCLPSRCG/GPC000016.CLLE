/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas GP Set up library list from GPBATCH jobd')      */
/*********************************************************************/
/*                                                                   */
/*       Midas - Global Processing                                   */
/*                                                                   */
/*       GPC000016 - Apply library list from GPBATCH to current job  */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CPK017  *CREATE    Date 31Oct03              */
/*       Prev Amend No. xxxxxx             Date ddMmmyy              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CPK017 - Midus Plus packaging.  New program.                */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&GPSBSID)
 
             DCL        VAR(&GPSBSID) TYPE(*CHAR) LEN(2)
 
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(2764)
             DCL        VAR(&INLLIBL) TYPE(*CHAR) LEN(2750)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBDNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBD) TYPE(*CHAR) LEN(20)
             DCL        VAR(&LEN) TYPE(*CHAR) LEN(4)
             DCL        VAR(&PARM) TYPE(*CHAR) LEN(1000)
             DCL        VAR(&OFFSET) TYPE(*DEC) LEN(9 0)
             DCL        VAR(&LIBCNT) TYPE(*DEC) LEN(9 0)
             DCL        VAR(&OFFLEN) TYPE(*DEC) LEN(9 0)
             DCL        VAR(&ERRCDE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&MSGDTA) TYPE(*CHAR) LEN(70)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2003')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                        CMDLBL(ABNOR))
 
             CHGVAR     VAR(&LIB) VALUE(&GPSBSID *CAT 'GXLIB')
 
             CHKOBJ     OBJ(&LIB/GPBATCH) OBJTYPE(*JOBD)
             MONMSG     MSGID(CPF9801 CPF9802 CPF9810 CPF9820 +
                          CPF9830 CPF9899) EXEC(GOTO CMDLBL(ABNOR))
 
 
/* Set up parameters for call to IBM API. */
             CHGVAR     VAR(&JOBDNAME) VALUE('GPBATCH')
             CHGVAR     VAR(&JOBD) VALUE(&JOBDNAME *CAT &LIB)
             CHGVAR     VAR(%BIN(&LEN 1 4)) VALUE(1000)
             CHGVAR     VAR(%BIN(&ERRCDE 1 4)) VALUE(0)
 
/* Use API to access the job description library list. */
             CALL       PGM(QWDRJOBD) PARM(&PARM &LEN JOBD0100 &JOBD +
                          &ERRCDE)
 
 INLLIBL:
             CHGVAR     &OFFSET %BIN(&PARM 361 4)
             CHGVAR     VAR(&OFFSET) VALUE(&OFFSET + 1)
             CHGVAR     VAR(&LIBCNT) VALUE(%BIN(&PARM 365 4))
             CHGVAR     VAR(&OFFLEN) VALUE(11 * &LIBCNT)
             CHGVAR     VAR(&INLLIBL) VALUE(%SST(&PARM &OFFSET +
                          &OFFLEN))
             MONMSG     MSGID(MCH3601)
 
             CHGVAR     VAR(&CMD) VALUE('CHGLIBL LIBL(' *CAT +
                             &INLLIBL *CAT ')')
 
             CALL       PGM(QCMDEXC) PARM(&CMD 2764)
 
             GOTO       CMDLBL(END)
 
 ABNOR:
             RCVMSG     MSGDTA(&MSGDTA) MSGID(&MSGID)
             IF         COND(&MSGID *NE '       ') THEN(DO)
                SNDPGMMSG MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(&MSGDTA) +
                          TOPGMQ(*EXT) MSGTYPE(*INFO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
 END:
             ENDPGM
