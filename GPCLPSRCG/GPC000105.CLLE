/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas GP Open Month End Copy Database')               */
/*********************************************************************/
/*                                                                   */
/*       Midas     - Global Processing                               */
/*                                                                   */
/*       GPC000105 - Open Month End Copy Database                    */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2005           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. BUG8394            Date 08Sep05              */
/*       Prev Amend No. BUG8328 *CREATE    Date 30Aug05              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       BUG8394- Remove T_MSWIP and T_MSWIP_H                       */
/*       BUG8328- OME Global Data Replication from Live to OME       */
/*                                                                   */
/*********************************************************************/
 
             PGM        PARM(&DUMGM &DUMGT &GMLIBO &GTALIBO &JBNM)
 
             DCL        VAR(&GMLIBO) TYPE(*CHAR) LEN(10)
             DCL        VAR(&GTALIBO) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DUMGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DUMGT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRFX ) TYPE(*CHAR) LEN(2 )                                  /*BUG8394*/
 
             DCL        VAR(&JBNM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2005')
 
             DCLF       FILE(GPUTRNPD)
 
/** Global Monitor Message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(NOCOPY))
 
/** Delete files that are not needed in global system set-up of OME */
 
             DLTF       FILE(&DUMGM/GPZONEPD)
             MONMSG     CPF0000
             DLTF       FILE(&DUMGT/GPSVALPD)
             MONMSG     CPF0000
             DLTF       FILE(&DUMGM/T_MSWIP)                                             /*BUG8394*/
             MONMSG     MSGID(CPF0000)                                                   /*BUG8394*/
             DLTF       FILE(&DUMGM/T_MSWIP_H)                                           /*BUG8394*/
             MONMSG     MSGID(CPF0000)                                                   /*BUG8394*/
 
/** Delete Universal Transaction file from dummy libraries */
 
 RCVF1:      RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(CPYGM))
             DLTF       FILE(&DUMGM/&TRFILE)
             MONMSG     CPF0000
             DLTF       FILE(&DUMGT/&TRFILE)
             MONMSG     CPF0000
             GOTO       CMDLBL(RCVF1)
 
/** Copy database files */
 
 CPYGM:      CPYDBLIB   FROMLIB(&DUMGM) TOLIB(&GMLIBO) SYSPFX(*NO)
 CPYGTA:     CPYDBLIB   FROMLIB(&DUMGT) TOLIB(&GTALIBO) SYSPFX(*NO)
 
             CHGVAR     VAR(&PRFX)  VALUE(%SST(&DUMGM 1 2))                              /*BUG8394*/
             CALL       PGM(GPR000107) PARM(&PRFX)                                       /*BUG8394*/
 
             CHGDTAARA  DTAARA(GOMESTAT) VALUE('S')
             GOTO       CMDLBL(ENDP)
 
NOCOPY:      CHGDTAARA  DTAARA(GOMESTAT) VALUE('F')
 
ENDP:        CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Misys International Banking Systems Ltd.')
             ENDPGM
