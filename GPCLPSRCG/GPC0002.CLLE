/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas GP Universal transactions update Mgr')          */
/*********************************************************************/
/*                                                                   */
/*       Midas - Global Processing Module                            */
/*                                                                   */
/*       GPC0002 - Universal Transactions Update Manager             */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/*       Last Amend No. MD051382           Date 10Jul18              */
/*       Prev Amend No. 259792             Date 19Aug14              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      BUG3963            Date 06Aug04              */
/*                      CSC020             Date 31Mar04              */
/*                      CGP001  *CREATE    Date 23May03              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD051382 - GP_UTU_MGR fails when GPJRN is changed with      */
/*                  SEQOPT(*RESET)                                   */
/*       259792 - When receiver is changed, use the updated last     */
/*                sequence number in order to retrieve the next set  */
/*                of journal entries to be processed.                */
/*       BUG3963 - When receiver changed or invalid sequence         */
/*                 number, use *CURCHAIN and retrieve current        */
/*                 receiver name.                                    */
/*       CSC020 - Journaling changes for MidasPlus                   */
/*       CGP001 - Global Processing                                  */
/*                                                                   */
/*********************************************************************/
             PGM

             DCL        VAR(&TEMPENTYP) TYPE(*CHAR) LEN(2)
             DCL        VAR(&JSEQN) TYPE(*DEC) LEN(10)
             DCL        VAR(&CMTRTN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CURRCV) TYPE(*CHAR) LEN(10)                                  /*CSC020*/
             DCL        VAR(&CURRCVNO) TYPE(*DEC) LEN(8)                                  /*CSC020*/
             DCL        VAR(&CURRCVNOA) TYPE(*CHAR) LEN(8)                                /*CSC020*/
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2003')
/**/                                                                                    /*MD051382*/
             DCL        VAR(&NEWSEQ) TYPE(*DEC)  LEN(10)                                /*MD051382*/
             DCL        VAR(&RCVNAM) TYPE(*CHAR) LEN(10)                                /*MD051382*/
             DCL        VAR(&RCVLIB) TYPE(*CHAR) LEN(10)                                /*MD051382*/
             DCL        VAR(&RCVNXN) TYPE(*CHAR) LEN(10)                                /*MD051382*/
             DCL        VAR(&RCVNXL) TYPE(*CHAR) LEN(10)                                /*MD051382*/
             DCL        VAR(&RCVSNL) TYPE(*DEC)  LEN(10)                                /*MD051382*/
             DCL        VAR(&RCVSNF) TYPE(*DEC)  LEN(10)                                /*MD051382*/
             DCL        VAR(&RCVQNM) TYPE(*CHAR) LEN(20)                                /*MD051382*/
             DCL        VAR(&INFDTA) TYPE(*CHAR) LEN(512)                               /*MD051382*/
             DCL        VAR(&INFDTL) TYPE(*UINT) LEN(4)   VALUE(512)                    /*MD051382*/
             DCL        VAR(&INFFMT) TYPE(*CHAR) LEN(8)   VALUE('RRCV0100')             /*MD051382*/
/**/                                                                                    /*MD051382*/

             DCLF       FILE(GPUTJSPD)

/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

             ALCOBJ     OBJ((GPUTJSPD *FILE *EXCLRD)) WAIT(30)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(DO)
             SNDPGMMSG  MSG('Attempt to start Universal Transactions +
                          Manager has failed. Probable cause: +
                          process is active already.') +
                          TOMSGQ(GPOPERQ)
             GOTO       CMDLBL(END)
             ENDDO

             CHGJOB SWS(00000000)

             CALL       PGM(GPCMTCTL) PARM('S' &CMTRTN)

             RCVF

             CHGVAR     VAR(&JSEQN) VALUE(&I#LASTSEQ)

/* Check if journal sequence exists. */
/* If journal sequence does not exist, set to restart at first entry */

 RERUN:
/*                                                                                         *CSC020*/
/*  Use *CURCHAIN to retrieve journal entry in case receiver has changed since the         *CSC020*/
/*  last time the job was active.                                                          *CSC020*/
/*                                                                                         *CSC020*/
/*********** RTVJRNE    JRN(GPJRN) FROMENT(&JSEQN) TOENT(&JSEQN) +
                          RTNENTTYP(&TEMPENTYP) ***************************************** /*CSC020*/
             RTVJRNE    JRN(GPJRN) RCVRNG(*CURCHAIN) FROMENT(&JSEQN) +
                          TOENT(&JSEQN) RTNENTTYP(&TEMPENTYP) +
                          RTNRCV(&CURRCV)                                                 /*CSC020*/
                MONMSG     MSGID(CPF0000) EXEC(DO)
/*                                                                                         *CSC020*/
/*  Start search from sequence number 1 because 0 is invalid.                              *CSC020*/
/*                                                                                         *CSC020*/
/*************     CHGVAR     VAR(&JSEQN) VALUE(0)                                         *CSC020*/
                   CHGVAR     VAR(&JSEQN) VALUE(1)                                        /*CSC020*/
/*************     RTVJRNE    JRN(GPJRN) FROMENT(*FIRST) +
                                TOENT(*FIRST) RTNSEQNBR(&JSEQN)  **************************BUG3963*/
                   RTVJRNE    JRN(GPJRN) RCVRNG(*CURCHAIN) FROMENT(*FIRST) +
                          TOENT(*FIRST) RTNSEQNBR(&JSEQN) +
                          RTNRCV(&CURRCV)                                                /*BUG3963*/
                   MONMSG     MSGID(CPF0000)
                ENDDO

                CHGVAR     VAR(&JSEQN) VALUE(&JSEQN + 1)
 RERUN2:                                                                                /*MD051382*/
/* Overrides */
             OVRDBF     FILE(GPULOGPD) SHARE(*NO) SEQONLY(*YES 1)
             OVRDBF     FILE(GPUTJSPD) SHARE(*NO)
             OVRDBF     FILE(GPUTJSPDN) TOFILE(GPUTJSPD) SHARE(*NO)

             RCVJRNE    JRN(GPJRN) EXITPGM(GPUTUPDMG) +
                          FILE((GPTRAPPD)) RCVRNG(*CURCHAIN) +
                          FROMENT(&JSEQN) JRNCDE((R) (C *IGNFILSLT) +
                          (U *IGNFILSLT) (J *IGNFILSLT)) +
                          ENTFMT(*TYPE1) DELAY(*NEXTENT)
                                                                                        /*MD051382*/
             MONMSG     MSGID(CPF7054) EXEC(DO)                                         /*MD051382*/
/* Get the latest journal sequence number */                                            /*MD051382*/
             CHGJOB     SWS(00000000)                                                   /*MD051382*/
             OPNDBF     FILE(GPUTJSPD) OPTION(*INP) OPNSCOPE(*JOB)                      /*MD051382*/
             POSDBF     OPNID(GPUTJSPD) POSITION(*START)                                /*MD051382*/
             CHGVAR     VAR(&JSEQN) VALUE(&I#LASTSEQ)                                   /*MD051382*/
             CLOF       OPNID(GPUTJSPD)                                                 /*MD051382*/
             CHGJOB     SWS(00000000)                                                   /*MD051382*/
/*           CHGVAR VAR(&NEWSEQ) VALUE(&JSEQN + 1)          */                          /*MD051382*/
             /* Retrieve journal receiver name of the last processed entry */           /*MD051382*/
                                                                                        /*MD051382*/
             RTVJRNE    JRN(GPJRN) RCVRNG(*CURCHAIN) FROMENT(&JSEQN) +
                          RTNRCV(&RCVNAM) RTNRCVLIB(&RCVLIB)                            /*MD051382*/
                                                                                        /*MD051382*/
             /* Retrieve sequence number of the last entry of this receiver */          /*MD051382*/
                                                                                        /*MD051382*/
             RTVJRNE    JRN(GPJRN) RCVRNG(&RCVLIB/&RCVNAM +
                          &RCVLIB/&RCVNAM) FROMENTLRG(*LAST) +
                          RTNSEQNBR(&RCVSNL)                                            /*MD051382*/
                                                                                        /*MD051382*/
             /* Is last processed entry the last entry of the receiver? */              /*MD051382*/
                                                                                        /*MD051382*/
             IF         COND(&JSEQN = &RCVSNL) THEN(DO)                                 /*MD051382*/
                                                                                        /*MD051382*/
             /* Retrieve next receiver name */                                          /*MD051382*/
                                                                                        /*MD051382*/
                   CHGVAR     VAR(&RCVQNM) VALUE(&RCVNAM *CAT &RCVLIB)                  /*MD051382*/
                                                                                        /*MD051382*/
                   CALLPRC    'QjoRtvJrnReceiverInformation' PARM(&INFDTA &INFDTL +
                                &RCVQNM &INFFMT)                                        /*MD051382*/
                                                                                        /*MD051382*/
                   CHGVAR     VAR(&RCVNXN) VALUE(%SST(&INFDTA 333 10))                  /*MD051382*/
                   CHGVAR     VAR(&RCVNXL) VALUE(%SST(&INFDTA 343 10))                  /*MD051382*/
                                                                                        /*MD051382*/
                   IF         ((&RCVNXN *NE ' ') *AND (&RCVNXL *NE ' ')) DO             /*MD051382*/
                                                                                        /*MD051382*/
                   /* Retrieve sequence number of next receiver first entry */          /*MD051382*/
                                                                                        /*MD051382*/
                   RTVJRNE    JRN(GPJRN) RCVRNG(&RCVNXL/&RCVNXN +
                                   &RCVNXL/&RCVNXN) FROMENTLRG(*FIRST) +
                                   RTNSEQNBR(&RCVSNF)                                   /*MD051382*/
                                                                                        /*MD051382*/
                   /* Was journal sequence number reset to 1? */                        /*MD051382*/
                                                                                        /*MD051382*/
                      IF         (&RCVSNF = 1) CHGVAR VAR(&NEWSEQ) VALUE(1)             /*MD051382*/
                                                                                        /*MD051382*/
                   ENDDO                                                                /*MD051382*/
                ENDDO                                                                   /*MD051382*/
                                                                                        /*MD051382*/
                CHGVAR     VAR(&JSEQN) VALUE(&NEWSEQ)                                   /*MD051382*/
/**/                                                                                    /*MD051382*/
             GOTO       CMDLBL(RERUN2)                                                  /*MD051382*/
             ENDDO                                                                      /*MD051382*/

             DLTOVR *ALL

/*                                                                                          CSC020*/
/*  If switch 6 is on, then journal receiver has been changed.                              CSC020*/
/*                                                                                          CSC020*/
             IF         COND(%SWITCH(XXXXX100)) THEN(DO)
                                                                                          /*259792*/
/* Get the latest journal sequence number */                                              /*259792*/
             OPNDBF     FILE(GPUTJSPD) OPTION(*INP) OPNSCOPE(*JOB)                        /*259792*/
             POSDBF     OPNID(GPUTJSPD) POSITION(*START)                                  /*259792*/
             RCVF                                                                         /*259792*/
             CHGVAR     VAR(&JSEQN) VALUE(&I#LASTSEQ)                                     /*259792*/
             CLOF       OPNID(GPUTJSPD)                                                   /*259792*/
                                                                                          /*259792*/
/**********************************************************************            /*CSC020 259792*/
/***Calculate*name*of*next*receiver*in*chain.**************************            /*CSC020 259792*/
/**********************************************************************            /*CSC020 259792*/
/**********     CHGVAR     VAR(&CURRCVNOA) VALUE(%SST(&CURRCV 3 8))                /*CSC020 259792*/
/**********     CHGVAR     VAR(&CURRCVNO) VALUE(&CURRCVNOA)                        /*CSC020 259792*/
/**********     CHGVAR     VAR(&CURRCVNO) VALUE(&CURRCVNO + 1)                     /*CSC020 259792*/
/**********     CHGVAR     VAR(&CURRCVNOA) VALUE(&CURRCVNO)                        /*CSC020 259792*/
/**********     CHGVAR     VAR(&CURRCV) VALUE('GR' *CAT &CURRCVNOA)                /*CSC020 259792*/
/*********      CHGVAR     VAR(&JSEQN) VALUE(0)                                             CSC020*/
/**********************************************************************            /*CSC020 259792*/
/***Retrieve*first*sequence*number*of*new*receiver.********************            /*CSC020 259792*/
/**********************************************************************            /*CSC020 259792*/
/**********     RTVJRNE    JRN(GPJRN) RCVRNG(&CURRCV) TOENT(*FIRST) +
                          RTNSEQNBR(&JSEQN)                                        /*CSC020 259792*/
                CHGJOB     SWS(00000000)
                GOTO       CMDLBL(RERUN)
             ENDDO


             IF COND(%SWITCH(XXXXXX11)) THEN(DO)
                ROLLBACK
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                             GPC0002 ended abnormally - see job log') +
                             TOMSGQ(GPOPERQ)
                GOTO       CMDLBL(END)
             ENDDO

             GOTO       CMDLBL(END)

 ABNOR:

/* Abnormal termination */
             CHGJOB     SWS(XXXXXX11)
             ROLLBACK
             MONMSG     MSGID(CPF0000 MCH0000)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          GPC0002 ended abnormally - see job log') +
                          TOMSGQ(GPOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)

 END:
             CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Misys International Banking Systems Ltd.')

             DLCOBJ     OBJ((GPUTJSPD *FILE *EXCLRD))
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)

             RCLRSC
             MONMSG     MSGID(CPF0000 MCH0000)
             RCLRSC     LVL(*CALLER)
             MONMSG     MSGID(CPF0000 MCH0000)

             ENDPGM
