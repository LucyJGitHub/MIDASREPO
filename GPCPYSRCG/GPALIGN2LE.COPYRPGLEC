      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  GPALIGN2LE- To validate and right align numeric fields       *
      *                                                               *
      *  (c) Finastra International Limited 2024                      *
      *                                                               *
      *  Last Amend No. MD062503 *CREATE   Date 11Apr24               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD062503 - Error due to ZALIGN not found                     *
      *                                                               *
      *****************************************************************

      **   ZALIGN SR. TO VALIDATE AND RIGHT-ALIGN NUMERIC FIELDS.

     C     ZALIGN        BEGSR                                                  *** ZALIGN ***

     C                   SETOFF                                       929399

      **   SAVE INPUT FIELD IN ARRAY, ZA1.

     C                   MOVEA     ZFIELD        ZA1

      **   CALCULATION TO DEFINE NUMBER STRUCTURE CONTROL FIELDS.

     C                   Z-ADD     ZADIG         ZADIG             2 0
     C                   Z-ADD     ZADEC         ZADEC             1 0

      **   CALCULATIONS TO DEFINE/CLEAR FIELDS.

     C                   MOVE      ' '           ZA2
     C                   MOVEA     ZA2           ZFIELD           16
     C                   Z-ADD     0             ZX                2 0
     C                   Z-ADD     0             ZY                2 0

      **   ENSURE REQUIRED STRUCTURE OF FIELD IS VALID.

     C     ZADIG         ADD       ZADEC         ZZ                2 0
     C     ZZ            COMP      15                                 99
     C   99              GOTO      ZAEND

      **   LOOP TO FIND DECIMAL POINT, BLANKS AND CHARACTERS.

     C     ZATAG1        TAG                                                    ** ZATAG1 TAG *
     C     ZX            ADD       1             ZX

      **   CHECK FOR DECIMAL POINT. ERROR IF SECOND DECIMAL POINT.

     C     ZA1(ZX)       COMP      '.'                                    90
     C   90
     CAN 93              SETON                                        99
     C   99              GOTO      ZAEND

      **   CHECK FOR 'M' CONSTANT. ERROR IF NOT LAST CHARACTER ENTERED

     C     ZA1(ZX)       COMP      'M'                                    96

     C   96              DO
     C     ZX            COMP      15                                 99  99
     C   99              GOTO      ZAEND
     C     ZX            ADD       1             ZX

     C     ZA1(ZX)       COMP      ' '                                9999
     C   99              GOTO      ZAEND
     C     ZX            SUB       1             ZX

     C     ZX            COMP      9                                  99
     C   99              GOTO      ZAEND

     C                   END

      **   CHECK FOR 'T' CONSTANT. ERROR IF NOT LAST CHARACTER ENTERED

     C     ZA1(ZX)       COMP      'T'                                    97
     C   97              DO

     C     ZX            COMP      15                                 99  99
     C   99              GOTO      ZAEND
     C     ZX            ADD       1             ZX

     C     ZA1(ZX)       COMP      ' '                                9999
     C   99              GOTO      ZAEND
     C     ZX            SUB       1             ZX

     C     ZX            COMP      12                                 99
     C   99              GOTO      ZAEND

     C                   END

      **   CLEAR ALPHAMERIC CONSTANT FROM ZA1

     C   96
     COR 97              MOVE      ' '           ZA1(ZX)

      **   CHECK FOR BLANKS. BYPASS FOR FIRST BLANK AFTER A DIGIT.

     C     ZA1(ZX)       COMP      ' '                                    91
     C   91
     CAN 92              GOTO      ZATAG2

      **   CHECK FOR NUMERIC, IF NOT '.' OR ' '.

     C  N90
     CANN91ZA1(ZX)       COMP      '0'                                  99
     C  N90
     CANN91
     CANN99ZA1(ZX)       COMP      '9'                                99
     C   99              GOTO      ZAEND                                        NOT NUMERIC

      **   STORE DIGITS IN ARRAY AND HOW MANY.
      **   ZY, TOTAL OF DIGITS IN THE INPUT FIELD.
      **   ZZ, TOTAL OF DIGITS TO THE LEFT OF DECIMAL POINT.

     C  N90
     CANN91ZY            ADD       1             ZY                   92
     C  N90
     CANN91              MOVE      ZA1(ZX)       ZA2(ZY)
     C   90              Z-ADD     ZY            ZZ                   93  93

     C     ZX            COMP      16                                   94      CHECK IF ALL
     C   94              GOTO      ZATAG1

     C     ZATAG2        TAG                                                    ** ZATAG2 TAG *

      **   IF 'M' SPECIFIED, MULTIPLY ZA1 BY 1 MILLION

     C   96ZZ            ADD       6             ZZ

      **   IF 'T' SPECIFIED, MULTIPLY ZA1 BY 1 THOUSAND

     C   97ZZ            ADD       3             ZZ

      **   FILL IN ZEROS IN ANY BLANKS LEFT OF DECIMAL POINT

     C   96
     CAN 93
     COR 97
     CAN 93ZZ            DOWGT     ZY
     C     ZY            ADD       1             ZY
     C                   MOVE      '0'           ZA2(ZY)
     C                   END

      **   IF CONSTANT SPECIFIED WITH NO DECIMAL POINT ZEROISE BLANKS

     C   96
     CANN93              DO        6
     C     ZY            ADD       1             ZY
     C                   MOVE      '0'           ZA2(ZY)
     C                   END

     C   97
     CANN93              DO        3
     C     ZY            ADD       1             ZY
     C                   MOVE      '0'           ZA2(ZY)
     C                   END

      **   IF NO DECIMAL POINT FOUND, SET TOTAL FIELD ZZ.

     C  N93              Z-ADD     ZY            ZZ

      **   CHECK FOR EMBEDDED BLANKS.

     C   91
     CAN 92              MOVEA     ZA1(ZX)       ZFIELD
     C   91
     CAN 92ZFIELD        COMP      ' '                                9999
     C   99              GOTO      ZAEND                                        EMBEDDED BLANK

      **   ENSURE THAT NUMBER OF DIGITS ENTERED EITHER SIDE
      **   OF DECIMAL POINT ARE NOT MORE THAN ALLOWED.

     C     ZZ            COMP      ZADIG                              99
     C     ZY            SUB       ZZ            ZX
     C  N99ZADEC         SUB       ZX            ZX                     9995
     C   99              GOTO      ZAEND
     C   95              GOTO      ZATAG4                                       NO TRAILING

      **   FILL THE TRAILING BLANKS WITH ZEROS.

     C     ZY            ADD       ZX            ZZ
     C     ZATAG3        TAG                                                    ** ZATAG3 TAG *
     C     ZY            ADD       1             ZY
     C                   MOVE      '0'           ZA2(ZY)
     C     ZY            COMP      ZZ                                   94
     C   94              GOTO      ZATAG3

      **   RIGHT-ALIGN THE VALUE BY MOVING BACK TO ARRAY, ZA1.

     C     ZATAG4        TAG                                                    ** ZATAG4 TAG *
     C                   MOVE      '0'           ZA1
     C                   Z-ADD     16            ZX
     C     ZATAG5        TAG                                                    ** ZATAG5 TAG *
     C     ZY            COMP      0                                      94
     C   94              GOTO      ZATAG6
     C                   MOVE      ZA2(ZY)       ZA1(ZX)
     C     ZY            SUB       1             ZY
     C     ZX            SUB       1             ZX
     C                   GOTO      ZATAG5
     C     ZATAG6        TAG                                                    ** ZATAG6 TAG *

      **   MOVE VALIDATED AND RIGHT-ALIGNED NUMBER BACK INTO ZFIELD.

     C                   MOVEA     ZA1           ZFIELD

     C                   SETOFF                                       9697

     C     ZAEND         ENDSR                                                  * ZAEND ENDSR *

