      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Global Menu items maintenance/enq subfile')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  DSPF/GP000009DF - Midas GP Global Menu Item Subfile          *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGP001 *CREATE     Date 01Mar04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP001 - Global Processing                                   *
      *                                                               *
      *****************************************************************
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A          R #SFLRCD                   SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(34 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1SEL          1   I  8  2
     A N34                                  DSPATR(HI UL)
     A  34                                  DSPATR(RI PC)
     A            #1SECL         4   O  8 77DSPATR(HI)
     A            #1SNAM        20   O  8  4DSPATR(HI)
     A            #1INAR        45   O  8 26DSPATR(HI)
     A            #1MODC         2   O  8 73DSPATR(HI)
     A            #1INDX        10   H
     A*=========================================================================
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF09(09 'Go to ''Add'' mode.')
     A                                      SETOF(99 'Global error flag')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A N25N80 81                            SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND(*MORE)
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'GP000009F1'
     A            ##USR         10A  O  1  2
     A            ##JOB         10A  O  2  2
     A            ##MRDT         7A  O  1 73
     A                                  4  4'Menu Item'
     A                                  4 26'Description'
     A                                  4 73'Mod'
     A                                  4 77'Sec'
     A                                  5  4'Short Code'
     A                                  5 77'Lvl'
     A            #2SNAM        20   B  6  4
     A                                      DSPATR(HI UL)
     A            #2INAR        45A  B  6 26
     A                                      DSPATR(HI UL)
     A            #2MODC         2A  B  6 73
     A                                      DSPATR(HI UL)
     A            #2SECL         4A  B  6 77
     A                                      DSPATR(HI UL)
     A  40                             02 32'Global Menu Items Maintenance'
     A                                      DSPATR(HI)
     A  41                             02 34'Global Menu Items Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 73
     A N25                                  OVRDTA
     A                                      EDTWRD('  :  :  ')
      *=========================================================================
     A          R #CMDTXT1
     A                                      TEXT('Command keys')
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##CTX1        78   O 23  2
     A N25                                  OVRDTA
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
