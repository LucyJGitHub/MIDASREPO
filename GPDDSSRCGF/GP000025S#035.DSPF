      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Select Override Profiles')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  DSPF/GP000025S# - Midas GP Select Override Profiles          *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CSD086  *CREATE    Date 17Mar10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD086 - Overridable Errors                                  *
      *                                                               *
      *****************************************************************
      *
     A          R GP000025S0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
      *
     A                                      SFL
     A  34                                  SFLNXTCHG
 
     A            DOPTN          1   B  7  4TEXT('Option')
     A                                      DSPATR(UL)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
 
     A            DOVPR         10      7  8TEXT('Override Profile')
     A                                      DSPATR(HI)
     A            DDESC         25      7 23TEXT('Description')
     A                                      DSPATR(HI)
     A            DFRPT          1      7 57TEXT('Force Post')
     A                                      DSPATR(HI)
      *
     A          R GP000025C0                TEXT('Subfile Control')
 
     A                                      SFLCTL(GP000025S0)
     A N33                                  ROLLUP(27)
     A N33                                  ROLLDOWN
     A                                      OVERLAY
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  33                                  SFLEND
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  31                                  SFLCLR
     A                                      CF03(03)
 
     A            DSFRC          4S 0H      SFLRCDNBR
 
     A            DUSER         10   O  1  2 TEXT('Current User')
     A                                  1 13'GP000025S1'
 
     A            DMRDT          7   O  1 72 TEXT('Midas Run Date')
 
     A            DWSID         10   O  2  2 TEXT('WorkStation ID')
 
     A                                  2 28'Select Override Profile'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
 
     A            DTIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
 
     A                                  5  7'Override Profile'
     A                                  5 29'Description'
     A                                  5 52'Force Post'
 
     A            DOVPRS        10   B  6  8TEXT('Override Profile')
     A                                      DSPATR(HI)
 
      *
     A          R GP000025S1                TEXT('Message Subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
 
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
 
      *
     A          R GP000025C1                TEXT('Message Subfile +
     A                                             Control')
 
     A                                      SFLCTL(GP000025S1)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  32                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
 
     A            D1PGMQ                    SFLPGMQ
 
      *
     A          R GP000025F0                TEXT('Command Keys')
 
     A                                 23  2'1=Select    F3=Exit'
