      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Users Security Report Selection')             *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  DSPF/GP004007DF - Midas GP Users Security Report Selection   *
      *                                                               *
      *  (c) Finastra International Limited 2011                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR892737           Date 12Jan12               *
      *                 CBF005  *CREATE    Date 04Jul11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR892737 - Permissions by User Report does not allow lower   *
      *             case characters on user profile                   *
      *  CBF005 - BFinputastructure: Single Security User Maintenance *
      *           Profile                                             *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CF03(01)
     A                                      CF12(02)
     A  03                                  CF10(04)
     A          R GP004007F1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2011')
     A            USRID         10A  O 01  2
     A                                 01 13'GP004007'
     A            SWSID         10A  O 02  2
     A                                 01 25'Users Security +
     A                                      Report Selection'
     A                                      DSPATR(UL HI)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A                                 08 06'1. Report for all user+
     A                                      s (no selection)'
     A                                 10 06'2. Report for a specif+
     A                                      ic user'
     A                                 18 06'Enter Option Number'
     A            SOPTN          1   B 18 27
     A  20                                  DSPATR(RI PC)
     A  05                                  DSPATR(PR)
      *
     A N03                             23 02'F3=End, or F12=Prev +
     A                                      screen'
     A  03                             23 02'F3=End, F12=Prev +
     A                                      screen, F10=Delete'
     A  10        EMSG          60A  O 24 02DSPATR(RI)
      *
     A          R GP004007F2
     A            USRID         10A  O 01  2
     A                                 01 13'GP004007'
     A            SWSID         10A  O 02  2
     A                                 01 26'Users Security +
     A                                      Report Selection'
     A                                      DSPATR(UL HI)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A                                 10 06'Enter user profile'
     A            SUSRP         10A  B 10 26
     A  21                                  DSPATR(RI PC)
     A  06                                  DSPATR(PR)
     A                                      CHECK(LC)                                       AR892737
     A N03                             23 02'F3=End, or F12=Prev +
     A                                      screen'
     A  03                             23 02'F3=End, F12=Prev +
     A                                      screen, F10=Delete'
     A  11        EMSG          60A  O 24 02DSPATR(RI)
     A
