      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Global system values maintenance/enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  DSPF/GP009501DF - Midas GP Global System Values              *
      *                    Maintenance/Enquiry                        *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK019R            Date 17Dec04               *
      *                 CPK019             Date 28May04               *
      *                 CGP001  *CREATE    Date 19Mar04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPK019R- Remove date and time from the screen as webfacing   *
      *           is not able to process the fields.                  *
      *  CPK019 - Can not use RUNDDAT in global layer.                *
      *  CGP001 - Global Processing.                                  *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
     A          R #CMDTXT1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A                                      OVRDTA
      *
     A          R #RCDDTL1                  TEXT('Details')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(87 'Amend, enable CF05')
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Return to previous pgm.')
     A  87                                  CA05(05 'Reset value to default')
     A                                      CA11(11 'Further information.')
      * SETOFFS
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(35 'Invalid: #1VAL')
      *
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'GP009501F1'
      * Start CPK019
     A**********  ##MRDT         7      1 72TEXT('Midas Run Date')                            CPK019
     A**********                            OVRDTA                                            CPK019
      * Start CPK019R
     A**********                        1 72DATE                                             CPK019R
     A**********                            EDTCDE(Y)                                        CPK019R
      * End CPK019R
      * End CPK019
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A  87                              2 33'Global System Values Maintenance'
     A                                      DSPATR(HI)
     A N87                              2 34'Global System Values Enquiry'
     A                                      DSPATR(HI)
      * Start CPK019
     A**********  ##TME          6  0   2 71TEXT('*TIME')
     A**********                            EDTWRD('  :  :  ')
     A**********                            OVRDTA
      * Start CPK019R
     A**********                        2 72TIME                                             CPK019R
     A**********                            EDTWRD('  :  :  ')                               CPK019R
      * End CPK019R
      * End CPK019
     A            #1JUST         1   H      TEXT('Justification')
     A            #1EMSG         7   H      TEXT('General Error Message')
     A            #1ALWB         1   H      TEXT('Allow Blanks')
     A                                  4  2'System Value.............:'
     A            #1SVAL        20   O  4 29TEXT('System Value')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Midas Module.............:'
     A            #1MMOD         2   O  5 29TEXT('Midas Module')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Midas Module Shortname...:'
     A            #1MMSN        10   O  6 29TEXT('Midas Module Shortname')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  7  2'Description..............:'
     A            #1DESC        50   O  7 29TEXT('Description')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  8  2'Current Setting..........:'
     A            #1SVL1        50   B  8 29TEXT('Value')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A N35 87                               DSPATR(UL)
     A                                      OVRATR
     A N87                                  DSPATR(PR)
     A            #1SVL2        50   B  9 29TEXT('Value')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A N35 87                               DSPATR(UL)
     A                                      OVRATR
     A N87                                  DSPATR(PR)
     A            #1SVL3        50   B 10 29TEXT('Value')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A N35 87                               DSPATR(UL)
     A                                      OVRATR
     A N87                                  DSPATR(PR)
     A            #1SVL4        50   B 11 29TEXT('Value')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A N35 87                               DSPATR(UL)
     A                                      OVRATR
     A N87                                  DSPATR(PR)
     A                                 12  2'Default Value............:'
     A            #1DFT1        50   O 12 29TEXT('Default value')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            #1DFT2        50   O 13 29TEXT('Default value')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            #1DFT3        50   O 14 30TEXT('Default value')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            #1DFT4        50   O 15 29TEXT('Default value')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 16  2'Valid Values.............:'
     A            #1HELP        20   O 16 29TEXT('User Help Info')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 17  2'Attributes...............:'
     A            #1ATTR        10   O 17 29TEXT('Attributes')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 18  2'Data String Length.......:'
     A            #1DLEN         3  0O 18 29TEXT('Data String Length')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                      EDTCDE(1)
     A                                 19  2'Suppress Update..........:'
     A            #1SUPP        20   O 19 29TEXT('Suppress Update')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 20  2'Introduced By............:'
     A            #1SWCH         6   O 20 29TEXT('Introduced By')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 21  2'Last Change Date/Type....:'
     A            #1LCD          7   O 21 29TEXT('Last Change Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  61                             21 38'Insert'
     A                                      DSPATR(HI)
     A N61                             21 38'Amend'
     A                                      DSPATR(HI)
      *
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A                                      OVRDTA
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
