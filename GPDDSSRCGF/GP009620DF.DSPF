      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Midas WIP config subfile')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processisng Module                            *
      *                                                               *
      *  DSPF/GP009620DF - Display Midas WIP config Subfile           *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. MD054209  *CREATE  Date 22Aug19               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD054209 - Deliverable Data Split                            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2019')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
      *
     A            #1APID         9  0H      TEXT('API Unique Id')
     A            #1SEL          1   B  8  2
     A                                      VALUES('E' +
     A                                             'A'
     A                                                 )
     A                                      CHECK(AB)
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A            #1APIN        10      8  4TEXT('API Name')
     A                                      DSPATR(HI)
     A            #1NBOW        10      8 29TEXT('New BO Writing')
     A                                      DSPATR(HI)
     A            #1ABOW        10      8 46TEXT('Amend BO Writing')
     A                                      DSPATR(HI)
     A            #1AUTL        10      8 61TEXT('Authoriztion Lvl')
     A                                      DSPATR(HI)
     A            #1ZONE        10      8 16TEXT('Zone')
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(30 'Invalid: #2UNII')
     A                                      SETOF(31 'Invalid: #2APIN')
     A                                      SETOF(32 'Invalid: #2NBOW')
     A                                      SETOF(33 'Invalid: #2ABOW')
     A                                      SETOF(34 'Invalid: #2AUTL')
     A                                      SETOF(35 'Invalid: #2ZONE')
      *.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'GP009620F1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 25'WIP Configuration Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  4'API Name'
     A                                  4 29'New Bck Office'
     A                                  4 46'Amend Bck Off'
     A                                  4 61'Authorization    '
     A                                  4 16'Zone'
     A                                  5 29'Writing'
     A                                  5 46'Writing'
     A                                  5 61'Level'
     A                                  6 29'Workflow'
     A                                  6 46'Workflow'
     A            #2UNII         9  0H      TEXT('API Unique Id')
     A            #2APIN        10   B  7  4TEXT('API Name')
     A                                      OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2NBOW        10   B  7 29TEXT('New BO Worflow')
     A                                      OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2ABOW        10   B  7 46TEXT('Amend BO Workflow')
     A                                      OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2AUTL        10   B  7 61TEXT('Auth Level')
     A                                      OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2ZONE        10   B  7 16TEXT('Zone')
     A                                      OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A                                      OVRATR
      *=========================================================================
     A          R #SFLRCE                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A                                      INDTXT(40 'Suppress update')
     A  84                                  SFLNXTCHG
      *
     A            #1SEL          1   B  8  2
     A                                      VALUES('E' +
     A                                                 )
     A                                      CHECK(AB)
     A  38N40                               DSPATR(RI PC)
     A N38N40                               DSPATR(UL HI)
     A  40                                  DSPATR(ND PR)
     A            #1APID         9  0H      TEXT('WIP Unique ID')
     A            #1APIN        10      8  4TEXT('API Name')
     A                                      DSPATR(HI)
     A            #1NBOW        10      8 29TEXT('New BO Writing')
     A                                      DSPATR(HI)
     A            #1ABOW        10      8 46TEXT('Amend BO Writing')
     A                                      DSPATR(HI)
     A            #1AUTL        10      8 60TEXT('Authoriztion Lvl')
     A                                      DSPATR(HI)
     A            #1ZONE        10      8 16TEXT('Zone')
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #SFLCTE                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCE)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2APIN')
     A                                      SETOF(32 'Invalid: #2NBOW')
     A                                      SETOF(33 'Invalid: #2ABOW')
     A                                      SETOF(34 'Invalid: #2AUTL')
     A                                      SETOF(35 'Invalid: #2ZONE')
      *.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'GP009620F1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 25'WIP Configuration Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  4'API Name'
     A                                  4 29'New Bck Office'
     A                                  4 46'Amend Bck Off'
     A                                  4 61'Authorization    '
     A                                  4 16'Zone'
     A                                  5 29'Writing'
     A                                  5 46'Writing'
     A                                  5 61'Level'
     A                                  6 29'Workflow'
     A                                  6 46'Workflow'
     A            #2APIN        10   B  7  4TEXT('API Name')
     A                                      OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2NBOW        10   B  7 29TEXT('New BO Worflow')
     A                                      OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2ABOW        10   B  7 46TEXT('Amend BO Workflow')
     A                                      OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2AUTL        10   B  7 60TEXT('Auth Level')
     A                                      OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2ZONE        10   B  7 16TEXT('Zone')
     A                                      OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A                                      OVRATR
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A                                      OVRDTA
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
