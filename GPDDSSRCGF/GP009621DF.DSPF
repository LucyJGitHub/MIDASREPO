      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Midas WIP config maintenance')                *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processisng Module                            *
      *                                                               *
      *  DSPF/GP009621DF - Display Midas WIP config maintenance       *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. MD054209  *CREATE  Date 01Seg19               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD054209 - Deliverable Data Split                            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2019')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  86                                  PUTOVR
     A                                      INDTXT(89 'ADD mode.')
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CA09(09 'Change mode.')
     A                                      CA05(05 'Reset.')
     A                                      CA12(12 'Key screen.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #1VAL')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'GP009621F1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A  87                              2 29'WIP Configuration Maintenance'
     A                                      DSPATR(HI)
     A N87                              2 31'WIP Configuration Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'API Name.................:'
     A            #1APIN        30   B  4 29TEXT('API Name')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT1                  TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2019')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A                                      OVRDTA
      *=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(87 'Amend, enable CF05')
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Return to previous pgm.')
     A  87                                  CA05(05 'Reset value to default')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(32 'Invalid: #1NBOW')
     A                                      SETOF(33 'Invalid: #1ABOW')
     A                                      SETOF(34 'Invalid: #1AUTL')
     A                                      SETOF(35 'Invalid: #1ZONE')
      * ........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'GP009621F1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A  87                              2 29'WIP Configuration Maintenance'
     A                                      DSPATR(HI)
     A N87                              2 31'WIP Configuration Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A            #1UNII         9  0H      TEXT('Unique Id')
     A                                  4  2'API Name........................:'
     A            #1APIN        30   O  4 37TEXT('API Name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'New Back Office Write Workflow..:'
     A            #1NBOW        30   B  6 37TEXT('New BO Write Workflow')
     A                                      OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A N32 87                               DSPATR(UL)
     A                                      OVRATR
     A N87                                  DSPATR(PR)
     A                                  7  2'Amend Back Office Write Workflow:'
     A            #1ABOW        30   B  7 37TEXT('Amend BO Write Workflow')
     A                                      OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A N33 87                               DSPATR(UL)
     A                                      OVRATR
     A N87                                  DSPATR(PR)
     A                                  8  2'Authorization Level.............:'
     A            #1AUTL        30   B  8 37TEXT('Authorization Level')
     A                                      OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A N34 87                               DSPATR(UL)
     A                                      OVRATR
     A N87                                  DSPATR(PR)
     A                                  5  2'Zone............................:'
     A            #1ZONE        10   O  5 37TEXT('Zone')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A                                      OVRDTA
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
