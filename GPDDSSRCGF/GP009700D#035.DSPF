      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Display WAS Config Table')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/GP009700D# - WAS Configuration Definition Table         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Last Amend No. MD023899 *CREATE   Date 02Dec13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD023899 - BF PRB Multiple JMS listeners auto starting       *
      *             during COB                                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *================================================================
      *
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
      *
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1WSVR        25      8  5TEXT('WAS Server')
     A                                      DSPATR(HI)
     A            #1TRNL         1      8 36TEXT('Transaction Listener')
     A                                      DSPATR(HI)
     A            #1COBL         1      8 48TEXT('COB Listener')
     A                                      DSPATR(HI)
     A            #1REQD        10      8 55TEXT('Request Data Qeue')
     A                                      DSPATR(HI)
     A            #1RSPD        10      8 69TEXT('Response Data Qeue')
     A                                      DSPATR(HI)
      *================================================================
      *
     A          R #SFLCTL                   TEXT('Subfile control.')
      *
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2WSVR')
     A                                      SETOF(32 'Invalid: #2TRNL')
     A                                      SETOF(33 'Invalid: #2COBL')
     A                                      SETOF(34 'Invalid: #2REQD')
     A                                      SETOF(35 'Invalid: #2RSPD')
      *.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'GP009700D#'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 20'WAS Configuration -
     A                                      Definition Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
     A            #2TRNL         1   H      TEXT('Transaction Listener')
     A            #2COBL         1   H      TEXT('COB Listener')
     A            #2REQD        10   H      TEXT('Request Data Queue')
     A            #2RSPD        10   H      TEXT('Responce Data Queue')
     A                                  4 14'WAS'
     A                                  4 32'Transaction'
     A                                  4 47'COB'
     A                                  4 54'Request Data'
     A                                  4 68'Response Data'
     A N25                                  OVRDTA
     A                                  5 13'Server'
     A                                  5 33'Listener'
     A                                  5 45'Listener'
     A                                  5 57'Queue'
     A                                  5 70'Queue'
     A N89        #2WSVR        25   B  6  5TEXT('WAS Server')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *================================================================
      *
     A          R #CMDTXT1                  TEXT('Command keys')
      *
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu   F5=Refresh'
      *================================================================
      *
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
      *
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
      *
