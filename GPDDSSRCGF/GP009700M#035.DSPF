      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP WAS Config Maintenance')                      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/GP009700M# - WAS Configuration Definition Table         *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD023899B          Date 17Jan14               *
      *                 MD023899 *CREATE   Date 02Dec13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD023899B - Screen locks when option other than 'D' is used  *
      *  MD023899 - BF PRB Multiple JMS listeners auto starting       *
      *             during COB                                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
      *
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A                                      INDTXT(33 'Invalid: #1WSVR')
     A                                      INDTXT(34 'Invalid: #1TRNL')
     A                                      INDTXT(35 'Invalid: #1COBL')
     A                                      INDTXT(36 'Invalid: #1REQD')
     A                                      INDTXT(37 'Invalid: #1RSPD')
     A  84                                  SFLNXTCHG
      *
     A            #1DBRC        51   H      TEXT('Existing record fields')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B  8  2
      **Start of change MD023899B
     A**********                            VALUES('D' +                                   MD023899B
     A**********                                       )                                   MD023899B
     A**********                            CHECK(AB)                                      MD023899B
      **End of change MD023899B
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
      *
     A            #1WSVR        25   B  8  5TEXT('WAS Server')
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
      *
     A            #1TRNL         1   B  8 36TEXT('Transaction Listener')
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
      *
     A            #1COBL         1   B  8 48TEXT('COB Listener')
     A  87                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N87N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
      *
     A            #1REQD        10   B  8 55TEXT('Request Data Qeue')
     A  87                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N87N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
      *
     A            #1RSPD        10   B  8 69TEXT('Response Data Qeue')
     A  87                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N87N37                               DSPATR(UL)
     A N37                                  DSPATR(HI)
      *
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(16)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
      *
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
      *
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2WSVR')
      *.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'GP009700M'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 20'WAS Configuration -
     A                                      Definition Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4 14'WAS'
     A                                  4 32'Transaction'
     A                                  4 47'COB'
     A                                  4 54'Request Data'
     A                                  4 68'Response Data'
     A N25                                  OVRDTA
     A                                  5 13'Server'
     A                                  5 33'Listener'
     A                                  5 45'Listener'
     A                                  5 57'Queue'
     A                                  5 70'Queue'
     A N89        #2WSVR        25   B  6  5TEXT('WAS Server')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A  89                             23  2'F3=Main Menu   F5=Refresh   F9=Go-
     A                                       to ''Change'' mode'
     A N89                             23  2'D=Delete   F3=Main Menu   F5=Refr-
     A                                      esh   F9=Go to ''Add'' mode'
     A N25                                  OVRDTA
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
