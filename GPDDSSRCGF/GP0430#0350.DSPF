      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP SAR Maintenance: password entry window')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  GP0430#0 - Switchable Features Maintenance program:          *
      *             password window.                                  *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. MD056555 *CREATE   Date 31Aug20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD056555 - Deliverable Data Split for SAR group              *
      *                                                               *
      *****************************************************************
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'Reset')
     A                                      CF12(12 'Previous')
     A          R WINDOWF
     A                                      WINDOW(10 19 7 56)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2020')
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1  1'GP0430#0'
     A                                      DSPATR(HI)
     A  69                              2  1'Feature ID:      '
     A  69        SFNUM          6   O  2 20
     A                                  3  1'Enter password:  '
     A                                      OVRATR
     A N69                              5  1'F3=Main Menu'
     A N69                              5 15'F5=Refresh'
     A N69                              5 39'F12=Previous'
     A  69                              5  1'F12=Skip to Next'
     A            PASS          16   B  3 20DSPATR(ND)
      *=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(6)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      WINDOW(WINDOWF)
     A                                      SFLPAG(1) SFLSIZ(2)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
