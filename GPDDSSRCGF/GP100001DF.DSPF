      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Global User Security')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  DSPF/GP100001DF - Global User Security                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGP001  *CREATE    Date 26May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP001 - Global Processing                                   *
      *                                                               *
      *****************************************************************
     A                                      CHGINPDFT(HI)
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R GP100001F1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      CF03
      *
     A            USRID         10A  O 01 02
     A                                 01 13'GP100001'
     A            SWSID         10A  O 02 02
     A                                 01 26'GLOBAL USER SECURITY'
     A                                      DSPATR(UL HI)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
      *
     A                                 03 06'User Profile'
     A            SUSER         10A  B 03 20
     A                                      DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                 03 33'Action'
     A            SACTN          1A  B 03 41
     A                                      DSPATR(HI)
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(UL)
      *
     A                                 05 06'Enter Y against those details requ-
     A                                      ired for this user:'
      *
     A            SBBRQ          1A  B 09 16
     A                                      DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(UL)
     A                                 09 21'Booking Branches'
     A  25 53                          09 51'No details exist'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A            SACRQ          1A  B 12 16
     A                                      DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(UL)
     A                                 12 21'Action Codes'
     A  25 56                          12 51'No details exist'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A                                 23 02'A=Amend  E=Enquire'
     A                                 24 02'F3=End'
      *
     A            ERCD          78   O 21 02
     A  80                                  DSPATR(RI HI)
     A            ERMS          78   O 22 02
     A  80                                  DSPATR(RI HI)
      *
     A          R GP100001F3                TEXT('Blank screen')
     A                                  1  2' '
     A                                 24 79' '
