      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Global User Security - Booking Branches')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  DSPF/GP100002DF - Global User Security - Booking Branches    *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGP001  *CREATE    Date 26May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP001 - Global Processing                                   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      CF03
     A  24                                  CF10
     A                                      CF12
      *
     A          R GP100002F1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SFL
     A  12                                  SFLNXTCHG
      *
     A            SSLCT          1   B  8 11TEXT('Select?')
     A                                      DSPATR(HI)
     A  23
     AO 24                                  DSPATR(PR)
     A  21N31
     AO 22N31                               DSPATR(UL)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
      *
     A            SBRCA          3   O  8 16TEXT('Branch Code')
     A                                      DSPATR(HI)
      *
     A            SBRNM         30   O  8 22TEXT('Branch Name')
     A                                      DSPATR(HI)
      *
     A          R GP100002F2                TEXT('Subfile controller')
     A                                      SFLCTL(GP100002F1)
     A                                      SFLSIZ(100)
     A                                      SFLPAG(12)
     A                                      SFLDSPCTL
     A  10                                  SFLDSP
     A  11                                  SFLEND
     A                                      OVERLAY
     A            SDREC          3  0H      SFLRCDNBR
      *
     A N10                             20  2'  THERE ARE NO BRANCH CODES DEFINE-
     A                                      D ON THE SYSTEM                    -
     A                                             '
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A  30        SERCD         78   O 21  2DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A                                 23  1'F3=End, or F12=I-
     A                                      nitial screen'
     A  24                             23 31', or F10=Delete'
      *
     A          R GP100002F3                TEXT('Screen Header')
      *
     A            USRID         10A  O 01  2
     A                                 01 13'GP100002'
     A            SWSID         10A  O 02  2
     A                                  1 24'GLOBAL USER SECURITY - BOOKING BRA-
     A                                      NCHES'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A                                  3  6'User Profile'
     A            SUSER         10   O  3 20DSPATR(HI)
     A                                  3 33'Action'
     A            SACTN          1   O  3 41DSPATR(HI)
     A  26                              3 55'DELETED'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A  21
     AO 22                              5  6'Enter Y against those Branches per-
     A                                      mitted for this user:'
     A  23
     AO 24                              5  6'The user has authority to those Br-
     A                                      anches with Y entered against them:'
     A                                  7 15'Code'
     A                                      DSPATR(UL)
     A                                  7 23'Name'
     A                                      DSPATR(UL)
