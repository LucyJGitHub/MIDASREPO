      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Global User Security - Action Codes')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  DSPF/GP100003DF - Global User Security - Action Codes        *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGP001  *CREATE    Date 26May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP001 - Global Processing                                   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF12
     A          R GP100003F1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SFL
     A  09                                  SFLNXTCHG
      *
     A            SSLCT          1   B  8 11TEXT('Select?')
     A                                      DSPATR(HI)
     A N12N13                               DSPATR(UL)
     A  12
     AO 13                                  DSPATR(RI PC)
      *
     A            SBRCA          3   O  8 16TEXT('Branch Code')
     A                                      DSPATR(HI)
      *
     A            SBRNM         30   O  8 22TEXT('Branch Name')
     A                                      DSPATR(HI)
      *
     A            SMSG          23   O  8 56TEXT('Error message')
     A  13                                  DSPATR(HI)
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(ND)
      *
     A          R GP100003F2                TEXT('Subfile controller for Branch-
     A                                       selection screen')
     A                                      SFLCTL(GP100003F1)
     A                                      SFLSIZ(100)
     A                                      SFLPAG(11)
     A  05                                  SFLCLR
     A  06                                  SFLDSPCTL
     A  07                                  SFLDSP
     A  08                                  SFLEND
     A                                      OVERLAY
     A            SDREC1         4  0H      SFLRCDNBR
      *
     A N07                             20  2'  THE USER IS NOT AUTHORISED TO AN-
     A                                      Y BOOKING BRANCHES                 -
     A                                             '
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A  11        SERCD         78   O 21  2DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A                                 23  1'F3=End  F12=Initial screen'
      *
     A          R GP100003F3                TEXT('Branch selection screen heade-
     A                                      r')
      *
     A            USRID         10A  O 01  2
     A                                 01 13'GP100003'
     A            SWSID         10A  O 02  2
     A                                  1 26'GLOBAL USER SECURITY - ACTION CODE-
     A                                      S'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A                                  3  6'User Profile'
     A            SUSER         10   O  3 20DSPATR(HI)
     A                                  3 33'Action'
     A            SACTN          1   O  3 41DSPATR(HI)
     A                                  5  5'Enter Y against those Booking Bran-
     A                                      ches requiring Action Code maintena-
     A                                      nce.'
      *
     A                                  7 15'Code'
     A                                      DSPATR(UL)
     A                                  7 23'Name'
     A                                      DSPATR(UL)
      *
     A          R GP100003F4                TEXT('Subfile for Action Codes deta-
     A                                      ils')
     A                                      SFL
     A  19                                  SFLNXTCHG
      *
     A            SINDX         10   H      TEXT('Unique Code')
     A            SSNAM         20   O  9  2TEXT('Menu Item Code')
     A                                      DSPATR(HI)
     A            SINAR         33   O  9 23TEXT('Menu Item Narrative')
     A  72                                  DSPATR(RI)
      *
     A            SPACD         10   O  9 57TEXT('Applicable A/Code')
     A                                      DSPATR(HI)
     A            SACAD         10   B  9 68TEXT('Action Code?')
     A                                      DSPATR(HI)
     A  23
     AO 24                                  DSPATR(PR)
     A  21N31N32
     AO 22N31N32                            DSPATR(UL)
     A  31
     AO 32                                  DSPATR(RI PC)
      *
     A          R GP100003F5                TEXT('Subfile controller for Action-
     A                                       Codes details')
     A  70                                  CF02
     A  24                                  CF10
     A N23N24N29                            CF13(71)
     A                                      SFLCTL(GP100003F4)
     A                                      SFLSIZ(11)
     A                                      SFLPAG(11)
     A N17N16                               SFLCLR
     A  16                                  SFLDSPCTL
     A  17                                  SFLDSP
     A  82                                  SFLEND
     A N82N29                               ROLLUP(67)
     A N29N34                               ROLLDOWN(68)
     A                                      OVERLAY
      *
     A  29                             20  2'  THE USER IS NOT AUTHORISED TO AN-
     A                                      Y MENU ITEMS WITH ACTION CODES     -
     A                                             '
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A  30        SERCD         78   O 21  2DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A                                 23  1'F3=End, or F12=Init-
     A                                      ial screen'
     A  24                             23 31', or F10=Delete'
     A  70                             24  1'or F2=Branch selection screen'
     A N23N24N29                       23 31'F13=Authorise to all undefined +
     A                                      items.'
      *
     A          R GP100003F6                TEXT('Action Codes Details Screen H-
     A                                      eader')
     A            USRID         10A  O 01  2
     A                                 01 13'GP100003'
     A            SWSID         10A  O 02  2
     A                                  1 26'GLOBAL USER SECURITY - ACTION CODE-
     A                                      S'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A                                  3  6'User Profile'
     A            SUSER         10   O  3 20DSPATR(HI)
     A                                  3 33'Action'
     A            SACTN          1   O  3 41DSPATR(HI)
     A                                  3 48'Branch'
     A N70                                  DSPATR(ND)
     A            SBRCA          3   O  3 56DSPATR(HI)
     A N70                                  DSPATR(ND)
     A  26                              3 70'DELETED'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A  21
     AO 22                              5  6'For each menu item select the Acti-
     A                                      on Codes permitted for this user:'
     A  21
     AO 22                              6  6'Items with defaulted action codes +
     A                                      have highlighted text.'
     A  23
     AO 24                              5  5'The user has authority to those Ac-
     A                                      tion Codes in the A/CODES ALLOWED f-
     A                                      ield:'
     A                                  7  2'M e n u    I t e m s'
     A                                  7 57'Applicable'
     A                                  7 68'A/Codes'
     A                                  8  2'Short Code'
     A                                      DSPATR(UL)
     A                                  8 23'Narrative'
     A                                      DSPATR(UL)
     A                                  8 57'A/Codes'
     A                                      DSPATR(UL)
     A                                  8 68'Allowed'
     A                                      DSPATR(UL)
