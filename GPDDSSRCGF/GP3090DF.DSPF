      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Switchable features by init pgms')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  GP3090DF - Global Switchable Features by init pgms           *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. MD056555 *CREATE   Date 31Aug20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD056555 - Deliverable Data Split for SAR group              *
      *                                                               *
      *****************************************************************
     A N44                                  CA09
     A                                      CA05
     A N40                                  CA10
     A N41                                  CA03
     A N43                                  CA12
     A                                      INDARA
     A*=========================================================================
     A          R GP3090MF0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2020')
     A            DFDUSR        10A     1  2
     A                                  1 13'GP3090M'
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  2 26'Global Switchable'
     A                                      DSPATR(HI)
     A                                  2 44'by init pgms'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A          R GP3090MF1                 OVERLAY
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 12'F5=Refresh'
     A                                      COLOR(BLU)
      *
     A          R GP3090MS0                 SFL
     A  27                                  SFLNXTCHG
     A            DFAORD         1   B 12  2
     A                                      VALUES('D' +
     A                                                 )
     A                                      CHECK(AB)
     A  51                                  DSPATR(PR ND)
     A  32                                  DSPATR(RI PC)
     A N87N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A            DKPNAM        10   B 12  4TEXT('Program Name')
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            DKPNOW         1   B 12 30TEXT('Call pgm now?')
     A**87                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A            DKPRVC         1     12 46TEXT('Previously called')
     A                                      DSPATR(HI)
     A            DKHCAL         1   H      TEXT('To be called')
     A*=========================================================================
     A          R GP3090MC0                 TEXT('Subfile control.')
     A                                      SFLCTL(GP3090MS0)
     A  26                                  SFLCLR
     A  22                                  SFLDSPCTL
     A  23                                  SFLDSP
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(6)
     A N24
     AO 24                                  SFLEND(*MORE)
     A                                      OVERLAY
     A                                  4  7'Feature Number...:'
     A                                      DSPATR(HI)
     A            DFSAR2         6      4 26TEXT('Switchable Feature Number')
     A                                      DSPATR(HI)
     A                                  5  7'Description......:'
     A                                      DSPATR(HI)
     A            DFSARD        50      5 26TEXT('Description')
     A                                      DSPATR(HI)
     A                                  7  4'Initialisation         Call       -
     A                                          Previously '
     A                                      DSPATR(HI)
     A                                  8  4'Program              Program Now ?-
     A                                           Called ? '
     A                                      DSPATR(HI)
     A                                  9 27'(Y/N)            (Y/N)    '
     A                                      DSPATR(HI)
     A N89        DFSRCH        10   B 10  4TEXT('Menu Item Short Code')
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A*=========================================================================
     A          R GP3090MF3                 OVERLAY
     A                                 12 30'No details to display.'
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R GP3090MS1                 TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
     A          R GP3090MC1                 TEXT('Program messages')
     A                                      SFLCTL(GP3090MS1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
     A*=========================================================================
