      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Switchable features maintenance')             *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  GP3100DF - Midas SC Switchable features maintenance          *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. MD056555 *CREATE   Date 31Aug20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD056555 - Deliverable Data Split for SAR group              *
      *                                                               *
      *****************************************************************
      *
     A                                      CA09
     A                                      CA05
     A N40                                  CA10
     A                                      CA03
     A N43                                  CA12
     A                                      INDARA
     A          R GP3100MF0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2020')
     A            DFDUSR        10A     1  2
     A                                      DSPATR(HI)
     A                                  1 13'GP3100M'
     A                                      DSPATR(HI)
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  2 22'Global Switchable Features -'
     A                                  2 51'Maintenance'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A          R GP3100MF1                 OVERLAY
      *
     A          R GP3100MF3                 OVERLAY
     A                                 12 30'No details to display.'
     A                                      DSPATR(HI)
     A          R GP3100MF5                 OVERLAY
     A N89                             21  2'D=Delete P=Print *=Disable/Enable '
     A N89                             22  2'6=Menu Items  8=Initialisation Pro-
     A                                      grams'
     A                                 23  2'F3=Exit  F5=Refresh  F9=Switch on -
     A                                       F11=Print  F15=Inactive'
      *
     A          R GP3100MS0                 SFL
     A  27                                  SFLNXTCHG
     A            DFAORD         1A  B  8  5
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            #1SARN         6   O  8 10TEXT('Switchable Feature Number')
     A                                      DSPATR(HI)

     A            #1SARD        50   O  8 21TEXT('Description')

     A                                      DSPATR(HI)
     A            #1DSTS         8A     8 73TEXT('Disabled Status')
     A N54                                  DSPATR(ND)
     A  54                                  DSPATR(RI)

     A          R GP3100MC0                 SFLCTL(GP3100MS0)
     A  26                                  SFLCLR
     A  22                                  SFLDSPCTL
     A  23                                  SFLDSP
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(12)
     A N24
     AO 24                                  SFLEND(*MORE)
     A                                      OVERLAY
      *
     A                                  4  8'Switchable    Description of Feat-
     A                                      ure'
     A                                      DSPATR(HI)
     A                                  5  8'Feature No.'
     A                                      DSPATR(HI)
     A N89        DFSRCH         6   B  6 10TEXT('Switchable Feature Number')
     A                                      DSPATR(HI)

     A          R GP3100MS1                 TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R GP3100MC1                 TEXT('Program messages')
     A                                      SFLCTL(GP3100MS1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
