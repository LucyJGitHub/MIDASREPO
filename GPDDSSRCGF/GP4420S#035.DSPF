      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Select weighting code select record')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  DSPF/GP4420S# - Midas Weighting Code desc selection          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGP001             Date 08Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001  *CREATE    Date 06Oct99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP001 - Global Processing.                                  *
      *           - SD442* programs renamed to GP442* series.         *
      *  CPB001 - 'Private Banking' Module                            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,GP4420S#99
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,GP4420S#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(33 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B  8  3
     A                                      VALUES('1' +
     A                                                 )
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            #1WEIG         2      8  8TEXT('Weight Code')
     A                                      DSPATR(HI)
     A            #1DESC        30      8 17TEXT('Weight Description')
     A                                      DSPATR(HI)
     A            #1PERC         3      8 53TEXT('Percentage')
     A                                      DSPATR(HI)
     A            #1CUML         1      8 68TEXT('Cumulate perc')
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,GP4420S#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,GP4420S#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2WEIG')
     A                                      SETOF(32 'Invalid: #2DESC')
     A                                      SETOF(34 'Invalid: #2PERC')
     A                                      SETOF(35 'Invalid: #2CUML')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      * Start of change CGP001
     A**********                        1 13'SD4420S1'                                        CGP001
     A                                  1 13'GP4420S1'
      * End of change CGP001
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 29'Select Weighting Code'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
     A                                  4  5'Weighting'
     A                                  4 17'Weighting Description'
     A                                  4 50'Percentage'
     A                                  4 65'Cumulate'
     A                                  5  5'  Code  '
     A                                  5 65'Percent'
     A            #2WEIG         2   B  6  8TEXT('Weight Sector')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2DESC        30   B  6 17TEXT('Weight Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2PERC         3   B  6 53TEXT('Percentagee')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2CUML         1   B  6 68TEXT('Cumulate perc')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,GP4420S#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,GP4420S#94
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select request   F3=Exit'
     A*=========================================================================
     A*/COPY WNCPYSRC,GP4420S#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,GP4420S#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,GP4420S#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,GP4420S#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,GP4420S#89
