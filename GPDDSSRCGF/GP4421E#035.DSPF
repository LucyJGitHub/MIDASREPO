      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Weighting code display')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  DSPF/GP4421E# - Midas Weighting Code Enq                     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGP001             Date 08Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001  *CREATE    Date 30Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP001 - Global Processing.                                  *
      *           - SD442* programs renamed to GP442* series.         *
      *  CPB001 - 'Private Banking' Module                            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,GP4421E#99
     A          R #RCDKEY                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,GP4421E#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      * Start of change CGP001
     A**********                        1 13'SD4421E1'                                        CGP001
     A                                  1 13'GP4421E1'
      * End of change CGP001
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 30'Weighting Code Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  5  2'Weighting Code'
     A            #1WEIG         2      5 18TEXT('Weight Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,GP4421E#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,GP4421E#96
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A*=========================================================================
     A*/COPY WNCPYSRC,GP4421E#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,GP4421E#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      * Start of change CGP001
     A**********                        1 13'SD4421E2'                                        CGP001
     A                                  1 13'GP4421E2'
      * End of change CGP001
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 30'Weighting Code Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A                                  5  2'Weighting Code.......:'
     A            #1WEIG         2      5 25TEXT('Weight Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Weighting Description:'
     A            #1DESC        30      6 25TEXT('Weight Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Percentage...........:'
     A            #1PERC         3      7 25TEXT('Percentage')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Cumulate Percent.....:'
     A            #1CUML         1      8 25TEXT('Cumulate Perc')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,GP4421E#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,GP4421E#92
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main menu   F12=Previous'
     A*=========================================================================
     A*/COPY WNCPYSRC,GP4421E#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,GP4421E#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,GP4421E#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,GP4421E#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,GP4421E#87
