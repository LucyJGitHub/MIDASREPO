      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Audit File Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  DSPF/GPAU0001DF - Audit File Maintenance                     *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGP004  *CREATE    Date 25May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      CF03
     A                                      CF06
     A                                      CF12
     A                                      PRINT
     A          R CLEARSCR
     A                                  1  2 ' '
     A          R SUBFLRE2                  SFL
     A            SSACTN         1A  B  8  2
     A N15                                  DSPATR(HI UL)
     A  15                                  DSPATR(PR)
     A            SSNAR         50A  O  8  4
     A            SSPGM         10A  H
     A          R SUBFLCT2                  SFLCTL(SUBFLRE2)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLSIZ(1400)
     A                                      SFLPAG(0014)
     A                                      BLINK
     A  11                                  SFLDSP
     A  12                                  SFLDSPCTL
     A N11N12                               SFLCLR
     A  13                                  SFLEND
     A            USRID         10   O  1  2
     A            DATE           7   O  1 71
     A            TITLE         40   O  1 20
     A            WKSID         10   O  2  2
     A                                  2 31'AUDIT FILE MAINTENANCE'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME
     A                                  4  2'File Name'
     A            AFFILE        10A  O  4 12
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 23'Record ID'
     A            AFRCID        15A  O  4 33
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A            AFDESC        40A  O  4 39
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  6  2'Select with any character the type-
     A                                       of maintenance to be performed    -
     A                                                '
     A                                  7  2'X Description                     -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A          R EXITREQ
     A                                      PUTOVR
     A            USRID         10   O  1  2
     A            DATE           7   O  1 71
     A            TITLE         40   O  1 20
     A            WKSID         10   O  2  2
     A                                  2 31'AUDIT FILE MAINTENANCE'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME
     A                                  4  2'File Name'
     A            AFFILE        10A  O  4 12
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 23'Record ID'
     A            AFRCID        15A  O  4 33
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A            AFDESC        40A  O  4 39
     A                                  7  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 12  2'YOU HAVE REQUESTED THAT MAINTENANC-
     A                                      E OF THIS FILE IS TO BE ABANDONED-
     A                                      .         '
     A                                 13  2'ENTER "YES" TO CONFIRM THAT THIS I-
     A                                      S YOUR INTENTION.'
     A            ANSWER         3A  B 13 55
     A                                      DSPATR(HI UL)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'CK/3=Exit'
     A          R SUBMESS
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'CK/3=Exit, CK/6=Update,'
     A                                 23 26'CK/12=Abandon'
     A            DDERR        076A  O 24  2
     A                                      OVRDTA
     A                                      DSPATR(HI)
