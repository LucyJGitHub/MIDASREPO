      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Amend an Audit File')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  DSPF/GPAU0003DF - Amend an Audit File                        *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGP004  *CREATE    Date 25May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      CF03
     A                                      CF12
     A                                      PRINT
     A          R CLEARSCR
     A                                  1  2 ' '
     A          R HEADINGS
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            USRID         10   O  1  2
     A            WKSID         10   O  2  2
     A            TITLE         40   O  1 20
     A                                  2 25'AMEND AN AUDIT FILE'
     A                                      DSPATR(HI UL)
     A            DATE           7   O  1 71
     A                                  2 71TIME
     A          R FIRSTSCR
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A                                  4  2'File Name'
     A            AFFILE        10A  B  4 12DSPATR(HI)
     A N41N71                               DSPATR(UL)
     A  41                                  DSPATR(PC RI)
     A  71                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  4 23'Record ID'
     A            AFRCID        15A  B  4 33DSPATR(HI)
     A N42N71                               DSPATR(UL)
     A  42                                  DSPATR(PC RI)
     A  71                                  DSPATR(PR)
     A                                      OVRDTA
     A          R SECNDSCR
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A                                  6  2'Description'
     A            SSDESC        40A  B  6 18CHECK(LC)
     A N43                                  DSPATR(UL)
     A  43                                  DSPATR(HI RI)
     A  70                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  8  2'Owned By'
     A            SSUSRI        10A  O  8 18
     A                                      OVRDTA
     A                                 10  2'Input'
     A            SSINPT         4A  B 10 18
     A N44                                  DSPATR(UL)
     A  44                                  DSPATR(PC HI RI)
     A                                      OVRDTA
     A                                 12  2'Record ID Field'
     A            SSRIFD        10A  O 12 18
     A                                      OVRDTA
     A                                 14  2'Report Layout'
     A            SSLAYO         1A  B 14 18
     A N46                                  DSPATR(UL)
     A  46                                  DSPATR(PC HI RI)
     A                                      OVRDTA
     A                                 16  2'Field Spacing'
     A            SSFDSP         1A  B 16 18
     A N47                                  DSPATR(UL)
     A  47                                  DSPATR(PC HI RI)
     A                                      OVRDTA
     A                                 18  2'Authority'
     A            SSAUTH         1A  B 18 18
     A N48                                  DSPATR(UL)
     A  48                                  DSPATR(PC HI RI)
     A                                      OVRDTA
     A                                 20  2'Active'
     A            SSACTV         1A  B 20 18
     A N49                                  DSPATR(UL)
     A  49                                  DSPATR(PC HI RI)
     A                                      OVRDTA
     A          R SUBMESS
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDHELP       076A  O 23  2
     A                                      OVRDTA
     A            DDERR        076A  O 24  2
     A                                      OVRDTA
     A                                      DSPATR(HI)
