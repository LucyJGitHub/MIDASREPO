      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Delete an Audit File')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  DSPF/GPAU0004DF - Delete an Audit File                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGP004  *CREATE    Date 25May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A                                      PRINT
     A          R CLEARSCR
     A                                  1  2 ' '
     A          R HEADINGS
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            USRID         10   O  1  2
     A            WKSID         10   O  2  2
     A            TITLE         40   O  1 20
     A                                  2 24'DELETE AN AUDIT FILE'
     A                                      DSPATR(HI UL)
     A            DATE           7   O  1 71
     A                                  2 71TIME
     A          R FIRSTSCR
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A                                  4  2'File Name'
     A            AFFILE        10A  B  4 12DSPATR(HI)
     A N41N71                               DSPATR(UL)
     A  41                                  DSPATR(PC RI)
     A  71                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  4 23'Record ID'
     A            AFRCID        15A  B  4 33DSPATR(HI)
     A N42N71                               DSPATR(UL)
     A  42                                  DSPATR(PC RI)
     A  71                                  DSPATR(PR)
     A                                      OVRDTA
     A          R SECNDSCR
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A                                  6  2'Description'
     A            SSDESC        40A  O  6 18
     A                                      OVRDTA
     A                                  8  2'Owned By'
     A            SSUSRI        10A  O  8 18
     A                                      OVRDTA
     A                                 10  2'Input'
     A            SSINPT         4A  O 10 18
     A                                      OVRDTA
     A                                 12  2'Record ID Field'
     A            SSRIFD        10A  O 12 18
     A                                      OVRDTA
     A                                 14  2'Report Layout'
     A            SSLAYO         1A  O 14 18
     A                                      OVRDTA
     A                                 16  2'Field Spacing'
     A            SSFDSP         1A  O 16 18
     A                                      OVRDTA
     A                                 18  2'Authority'
     A            SSAUTH         1A  O 18 18
     A                                      OVRDTA
     A                                 20  2'Active'
     A            SSACTV         1A  O 20 18
     A                                      OVRDTA
     A          R SUBMESS
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDHELP       076A  O 23  2
     A                                      OVRDTA
     A            DDERR        076A  O 24  2
     A                                      OVRDTA
     A                                      DSPATR(HI)
