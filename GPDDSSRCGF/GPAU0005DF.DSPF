      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Audit File Selection')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  DSPF/GPAU0005DF - Audit File Selection                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGP004  *CREATE    Date 25May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF12
     A          R SUBFLREC                  SFL
     A            SSACTN         1A  I  8  2DSPATR(HI UL)
     A                                      AUTO(RA)
     A            SSFILE        10A  O  8  4
     A            SSRCID        15A  O  8 15
     A            AFDESC        40A  O  8 31
     A            AFINPT         4A  O  8 72
     A            AFACTV         1A  O  8 79
     A            AFFILE        10A  H
     A            AFRCID        15A  H
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0015)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            USRID         10   O  1  2
     A            WKSID         10   O  2  2
     A            TITLE         40   O  1 20
     A                                  2 31'AUDIT FILE SELECTION'
     A                                      DSPATR(HI UL)
     A            DATE           7   O  1 71
     A                                  2 71TIME
     A                                  4  4'File Name'
     A                                  4 15'Record ID'
     A                                  4 31'Description'
     A                                  4 71'Input'
     A                                  4 78'Act'
     A            AFFILE        10A  B  6  4DSPATR(HI UL)
     A            AFRCID        15A  B  6 15DSPATR(HI UL)
     A          R SUBMESS
     A                                      PUTOVR
     A                                 23  2'CK/3=Exit, CK12=Previous, Rollup-
     A                                      ,Rolldown'
     A            DDERR        076A  O 24  2OVRDTA
     A                                      DSPATR(HI)
