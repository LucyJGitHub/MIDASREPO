      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Field Selection')                             *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  DSPF/GPAU0006DF - Field Selection                            *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. AR1058381          Date 11May23               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGP004  *CREATE    Date 25May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR1058381 - Audit file maintenance crashes on webfaced       *
      *              screen. Convert hardcoded column header & footer *
      *              functions into variables. (Child: AR1058383)     *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      CF03
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF12
     A                                      PRINT
     A          R SUBFLRE2                  SFL
     A            SSSELT         1A  B  8  3
     A N72                                  DSPATR(UL)
     A  72                                  DSPATR(PR)
     A            SSSNAM        10A  O  8  8
     A            AFFDES        30A  O  8 19
     A            SSFFMT         6A  O  8 65
     A            AFSNAM        10A  H
     A            WWWSEQ         5S 0H
     A            WWPROT         1A  H
     A          R SUBFLCT2                  SFLCTL(SUBFLRE2)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLSIZ(1400)
     A                                      SFLPAG(0014)
     A                                      BLINK
     A  11                                  SFLDSP
     A  12                                  SFLDSPCTL
     A N11N12                               SFLCLR
     A  13                                  SFLEND
     A            USRID         10   O  1  2
     A            DATE           7   O  1 71
     A            TITLE         40   O  1 20
     A            WKSID         10   O  2  2
     A                                  2 30'WORK FIELD SELECTION'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME
     A                                  4  2'File Name'
     A            AFFILE        10A  O  4 12
     A                                      DSPATR(HI)
     A                                  4 23'Record ID'
     A            AFRCID        15A  O  4 33
     A                                      DSPATR(HI)
     A            AFDESC        40A  O  4 49
     A                                      DSPATR(HI)
     A                                  6  2'Select with any character fields t-
     A                                      o be used in the report definition.-
     A                                                '
      **Start of change AR1058381
     A**********                        7  2' X    Field      Name             -           AR1058381
     A**********                                                          Size -           AR1058381
     A**********                                     '                                     AR1058381
     A            HEADER1       78A  O  7  2
      **End of change AR1058381
     A                                      DSPATR(UL)
     A          R SUBMESS
     A                                      PUTOVR
      **Start of change AR1058381
     A**********                       22  2'                                  -           AR1058381
     A**********                                                               -           AR1058381
     A**********                                     '                                     AR1058381
     A            FOOTER1       78A  O 22  2
      **End of change AR1058381
     A                                      DSPATR(UL)
      **Start of change AR1058381
     A**********                       23  2'CK/3=Exit, CK/6=Update, CK/7=Set a-           AR1058381
     A**********                            ll to NOT selected, CK/8=Set all to-           AR1058381
     A**********                             selected'                                     AR1058381
     A            FOOTER2       78A  O 23  2
      **End of change AR1058381
     A            DDERR        076A  O 24  2
     A                                      OVRDTA
     A                                      DSPATR(HI)
