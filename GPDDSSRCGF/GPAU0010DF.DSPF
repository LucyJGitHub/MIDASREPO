      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Report Field Selection')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  DSPF/GPAU0010DF - Report Field Selection                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGP004  *CREATE    Date 25May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      CF03
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF12
     A                                      PRINT
     A          R SUBFLRE2                  SFL
     A            SSSEQN         5A  B  8  2DSPATR(UL)
     A            SSSPCB         1A  B  8 13DSPATR(UL)
     A            SSSNAM        10A  O  8 22
     A            AFFDES        30A  O  8 33
     A            SSFFMT         6A  O  8 65
     A            AFSNAM        10A  H
     A          R SUBFLCT2                  SFLCTL(SUBFLRE2)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLSIZ(1400)
     A                                      SFLPAG(0014)
     A                                      BLINK
     A  11                                  SFLDSP
     A  12                                  SFLDSPCTL
     A N11N12                               SFLCLR
     A  13                                  SFLEND
     A            USRID         10   O  1  2
     A            DATE           7   O  1 71
     A            TITLE         40   O  1 20
     A            WKSID         10   O  2  2
     A                                  2 25'REPORT FIELD SELECTION'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME
     A                                  4  2'File Name'
     A            AFFILE        10A  O  4 12
     A                                      DSPATR(HI)
     A                                  4 23'Record ID'
     A            AFRCID        15A  O  4 33
     A                                      DSPATR(HI)
     A            AFDESC        40A  O  4 49
     A                                      DSPATR(HI)
     A N14                              6  2'Select with any character or numbe-
     A                                      r fields to appear on the report.  -
     A                                                '
     A  14                              6  2'No fields have yet been selected a-
     A                                      s work fields. Return to the previo-
     A                                      us screen.'
     A                                  7  2' Seq  Space Before  Field     Name-
     A                                                                    Size -
     A                                               '
     A                                      DSPATR(UL)
     A          R SUBMESS
     A                                      PUTOVR
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'CK/3=Exit, CK/6=Update, CK/7=Set a-
     A                                      ll to NOT selected, CK/8=Set all to-
     A                                       selected'
     A            DDERR        076A  O 24  2
     A                                      DSPATR(HI)
