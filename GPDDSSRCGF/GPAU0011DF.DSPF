      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Report Field Maintenance')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  DSPF/GPAU0011DF - Report Field Maintenance                   *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. AR1058381          Date 11May23               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGP004  *CREATE    Date 25May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR1058381 - Audit file maintenance crashes on webfaced       *
      *              screen. Convert hardcoded column header & footer *
      *              functions into variables. (Child: AR1058383)     *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(*DS4)
     A                                      INDARA
     A                                      CF03
     A                                      CF06
     A                                      CF12
     A                                      PRINT
     A          R SUBFLRE2                  SFL
     A            SSSNAM        10A  O  7  2
     A            SSEDTW        26A  B  7 13
     A N41N60                               DSPATR(UL)
     A  41N60                               DSPATR(HI RI PC)
     A     60                               DSPATR(PR)
     A            SSFDES        30A  B  7 40CHECK(LC)
     A N42                                  DSPATR(UL)
     A  42                                  DSPATR(HI RI PC)
     A            SSFFMT         6A  O  8 12
     A            SSCOL1        17A  B  7 73CHECK(LC)
     A N43                                  DSPATR(UL)
     A  43                                  DSPATR(HI RI PC)
     A            SSCOL2        17A  B  8 73CHECK(LC)
     A                                      DSPATR(UL)
     A            SSCOL3        17A  B  9 73CHECK(LC)
     A                                      DSPATR(UL)
     A            SSREFF         1A  B  7 96
     A N44                                  DSPATR(UL)
     A  44                                  DSPATR(HI RI PC)
     A            SSBRCF         1A  B  7107
     A N45                                  DSPATR(UL)
     A  45                                  DSPATR(HI RI PC)
     A            AFSNAM        10A  H
     A          R SUBFLCT2                  SFLCTL(SUBFLRE2)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLSIZ(0500)
     A                                      SFLPAG(0005)
     A                                      BLINK
     A  11                                  SFLDSP
     A  12                                  SFLDSPCTL
     A N11N12                               SFLCLR
     A  13                                  SFLEND
     A            PRRN2          4  0H      SFLRCDNBR(CURSOR)
     A            USRID         10   O  1  2
     A            DATE           7   O  1 71
     A            TITLE         40   O  1 20
     A            WKSID         10   O  2  2
     A                                  2 25'REPORT FIELD MAINTENANCE'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME
     A                                  4  2'File Name'
     A            AFFILE        10A  O  4 12
     A                                      DSPATR(HI)
     A                                  4 23'Record ID'
     A            AFRCID        15A  O  4 33
     A                                      DSPATR(HI)
     A            AFDESC        40A  O  4 49
     A                                      DSPATR(HI)
     A  14                              5  2'No fields have yet been selected a-
     A                                      s work fields. Return to the previo-
     A                                      us screen.'
      **Start of change AR1058381
     A**********                        6  2' Field     Edit Word/Size         -           AR1058381
     A**********                                Description                    -           AR1058381
     A**********                              Column Headings    Reference    B-           AR1058381
     A**********                            ranch '                                        AR1058381
     A            HEADER1        7A  O  6  2
     A                                      DSPATR(UL)
     A            HEADER2       16A  O  6 13
     A                                      DSPATR(UL)
     A            HEADER3       13A  O  6 40
     A                                      DSPATR(UL)
     A            HEADER4       17A  O  6 73
     A                                      DSPATR(UL)
     A            HEADER5       11A  O  6 92
     A                                      DSPATR(UL)
     A            HEADER6        8A  O  6105
      **End of change AR1058381
     A                                      DSPATR(UL)
     A          R SUBMESS
     A                                      PUTOVR
      **Start of change AR1058381
     A**********                       22  2'                                  -           AR1058381
     A**********                                                               -           AR1058381
     A**********                                                               -           AR1058381
     A**********                                  '                                        AR1058381
     A            FOOTER1      110A  O 22  2
      **End of change AR1058381
     A                                      DSPATR(UL)
      **Start of change AR1058381
     A**********                       23  2'CK/3=Exit, CK/6=Update, CK/12=Prev-           AR1058381
     A**********                            ious screen                        -           AR1058381
     A**********                                                               -           AR1058381
     A**********                                  '                                        AR1058381
     A            FOOTER2      110A  O 23  2
      **End of change AR1058381
     A            DDERR        076A  O 24  2
     A                                      OVRDTA
     A                                      DSPATR(HI)
