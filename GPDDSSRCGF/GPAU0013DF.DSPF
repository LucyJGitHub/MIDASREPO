      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Report Layout Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  DSPF/GPAU0013DF - Report Layout Maintenance                  *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGP004  *CREATE    Date 25May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(*DS4)
     A                                      INDARA
     A                                      PRINT
     A          R MAIN                      TEXT('MAIN SCREEN')
     A                                      CF03
     A                                      CF06
     A                                      CF10
     A                                      CF12
     A                                      RTNDTA
     A                                      PUTOVR
     A                                      BLINK
     A                                      ROLLUP(05)
     A                                      ROLLDOWN(06)
     A            USRID         10   O  1  2
     A            WKSID         10   O  2  2
     A            TITLE         40   O  1 20
     A                                  2 25'LAYOUT MAINTENANCE'
     A                                      DSPATR(HI UL)
     A            DATE           7   O  1 71
     A                                  2 71TIME
     A                                  4  2'File Name'
     A            AFFILE        10A  O  4 12
     A                                      DSPATR(HI)
     A                                  4 23'Record ID'
     A            AFRCID        15A  O  4 33
     A                                      DSPATR(HI)
     A            AFDESC        40A  O  4 49
     A                                      DSPATR(HI)
     A            CTX          131A  O  6  1DSPATR(UL)
     A                                      OVRDTA
     A            SC1          131A  B  7  1CHECK(LC)
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC2          131A  B  8  1CHECK(LC)
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC3          131A  B  9  1CHECK(LC)
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC4          131A  B 10  1CHECK(LC)
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC5          131A  B 11  1CHECK(LC)
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC6          131A  B 12  1CHECK(LC)
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC7          131A  B 13  1CHECK(LC)
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC8          131A  B 14  1CHECK(LC)
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC9          131A  B 15  1CHECK(LC)
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC10         131A  B 16  1CHECK(LC)
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC11         131A  B 17  1CHECK(LC)
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC12         131A  B 18  1CHECK(LC)
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC13         131A  B 19  1CHECK(LC)
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC14         131A  B 20  1CHECK(LC)
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A            SC15         131A  B 21  1CHECK(LC)
     A  07                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 22  1'                                  -
     A                                                                         -
     A                                                                         -
     A                                                                '
     A                                      DSPATR(UL)
     A                                 23  2'Page:'
     A            SCRCNT         2S 0O 23  8DSPATR(HI)
     A                                      OVRDTA
     A                                 23 11'From Line/Position:'
     A                                      DSPATR(BL)
     A  21                                  DSPATR(ND)
     A                                      OVRATR
     A            FMLPA          6A  O 23 31
     A                                      DSPATR(BL)
     A  21                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 23 39'To Line/Position:'
     A                                      DSPATR(BL)
     A  22                                  DSPATR(ND)
     A                                      OVRATR
     A            TOLPA          6A  O 23 57
     A                                      DSPATR(BL)
     A  22                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 24  2'Rollup, Rolldown,'
     A                                 24 20'CK/3=Exit, CK/6=Update,'
     A                                 24 44'CK/12=Abandon'
     A          R ACTION
     A                                      TEXT('ACTION SCREEN')
     A                                      CF03
     A                                      CF12
     A                                      RTNDTA
     A                                      PUTOVR
     A                                      BLINK
     A            USRID         10   O  1  2
     A            WKSID         10   O  2  2
     A            TITLE         40   O  1 20
     A                                  2 25'LAYOUT MAINTENANCE'
     A                                      DSPATR(HI UL)
     A            DATE           7   O  1 71
     A                                  2 71TIME
     A                                  4  2'File Name'
     A            AFFILE        10A  O  4 12
     A                                      DSPATR(HI)
     A                                  4 23'Record ID'
     A            AFRCID        15A  O  4 33
     A                                      DSPATR(HI)
     A            AFDESC        40A  O  4 49
     A                                      DSPATR(HI)
     A                                  7 20'Action'
     A            SSACTN         1A  B  7 36
     A N71N41                               DSPATR(UL)
     A N71 41                               DSPATR(PC HI RI)
     A  71                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 20'Field'
     A            AFSNAM        10A  B  8 36
     A N72N42                               DSPATR(UL)
     A N72 42                               DSPATR(PC HI RI)
     A  72                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 20'Description'
     A            AFFDES        30A  O  9 36
     A                                      OVRDTA
     A                                 10 20'Format'
     A            SSFFMT         6A  O 10 36
     A                                      OVRDTA
     A                                 11 20'Edit Word'
     A            SSEDTW        26A  B 11 36
     A N43                                  DSPATR(UL)
     A  43                                  DSPATR(PC HI RI)
     A                                      OVRDTA
     A                                 12 20'Column Headings'
     A            AFCOL1        17A  O 12 36
     A            AFCOL2        17A  O 13 36
     A            AFCOL3        17A  O 14 36
     A                                      OVRDTA
     A          R CLEAR                     TEXT('CLEAR SCREEN')
     A                                  1  2' '
     A          R MESSAG
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'CK/3=Exit,'
     A                                 23 13'CK/12=Abandon'
     A            DDERR        076A  O 24  2
     A                                      OVRDTA
     A                                      DSPATR(HI)
