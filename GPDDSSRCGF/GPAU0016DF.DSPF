      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP Action Selection - 2')                        *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  DSPF/GPAU0015DF - Action Selection - 2                       *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. AR1058381          Date 11May23               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGP004  *CREATE    Date 25May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR1058381 - Audit file maintenance crashes on webfaced       *
      *              screen. Convert hardcoded column header & footer *
      *              functions into variables. (Child: AR1058383)     *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      CF03
     A                                      CF06
     A                                      CF12
     A                                      PRINT
     A          R SUBFLRE1                  SFL
     A            SSSIAO         3A  B  8  5
     A N41                                  DSPATR(UL)
     A  41                                  DSPATR(PC HI RI)
     A            SSBFAF         1A  B  8 16
     A N42                                  DSPATR(UL)
     A  42                                  DSPATR(PC HI RI)
     A            SSSFLD        10A  B  8 23
     A N43                                  DSPATR(UL)
     A  43                                  DSPATR(PC HI RI)
     A            SSSCMP         2A  B  8 38
     A N44                                  DSPATR(UL)
     A  44                                  DSPATR(PC HI RI)
     A            SSSCPV        30A  B  8 48
     A N45                                  DSPATR(UL)
     A  45                                  DSPATR(PC HI RI)
     A          R SUBFLCT1                  SFLCTL(SUBFLRE1)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLSIZ(0600)
     A                                      SFLPAG(0006)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A  03                                  SFLEND
     A                                      ROLLUP(05)
     A            USRID         10   O  1  2
     A            WKSID         10   O  2  2
     A            TITLE         40   O  1 20
     A                                  2 30'ACTION SELECTION'
     A                                      DSPATR(HI UL)
     A            DATE           7   O  1 71
     A                                  2 71TIME
     A                                  4  2'File Name'
     A            AFFILE        10A  O  4 12
     A                                      DSPATR(HI)
     A                                  4 23'Record ID'
     A            AFRCID        15A  O  4 33
     A                                      DSPATR(HI)
     A            AFDESC        40A  O  4 49
     A                                      DSPATR(HI)
     A                                  5 23'Action'
     A            AFACTN        10A  O  5 33
     A                                      DSPATR(HI)
     A                                  6 13'BEFORE'
     A                                  7  2'IF/AND/OR'
     A                                  7 13'/AFTER'
     A                                  7 24'FIELD'
     A                                  7 35'COMPARISON'
     A                                  7 48'COMPARISON VALUE'
     A            PRRN1          4  0H      SFLRCDNBR(CURSOR)
     A          R SUBFLRE2                  SFL
     A            AFSNAM        10A  O 15  8
     A            AFFDES        30A  O 15 19
     A            SSFFMT         6A  O 15 63
     A            AFFLDN         5S 0H
     A          R SUBFLCT2                  SFLCTL(SUBFLRE2)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLSIZ(0800)
     A                                      SFLPAG(0008)
     A                                      BLINK
     A  11                                  SFLDSP
     A  12                                  SFLDSPCTL
     A N11N12                               SFLCLR
     A  13                                  SFLEND
      **Start of change AR1058381
     A**********                       14  2'      Field      Name             -           AR1058381
     A**********                                                     Edit Size -           AR1058381
     A**********                                     '                                     AR1058381
     A            HEADER1       78A  O 14  2
      **End of change AR1058381
     A                                      DSPATR(UL)
     A          R SUBMESS
     A                                      OVERLAY
     A                                      PUTOVR
      **Start of change AR1058381
     A**********                       23  2'CK/3=Exit, CK/6=Update,'                      AR1058381
     A**********                       23 26'CK/12=Abandon'                                AR1058381
     A            FOOTER1       23A  O 23  2
     A            FOOTER2       13A  O 23 26
      **End of change AR1058381
     A            DDERR        076A  O 24  2
     A                                      OVRDTA
     A                                      DSPATR(HI)
