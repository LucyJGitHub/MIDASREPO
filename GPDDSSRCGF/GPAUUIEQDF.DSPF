      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GP User Input Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  GPAUUIEQDF - Audit User Input Enquiry                        *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGP004  *CREATE    Date 25May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      DSPSIZ(*DS4)
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF12
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A            DDACTN         1A  I  7  2AUTO(RA)
     A N65                                  DSPATR(HI UL)
     A  65                                  DSPATR(RI PC)
     A            AIDATEX        6  0O  7  4EDTWRD('  /  /  ')  DSPATR(HI)
     A            AITIME         6  0O  7 13EDTWRD('  :  :  ')  DSPATR(HI)
     A            AIJBNO         6S 0O  7 22DSPATR(HI)
     A            AIRDVL         1A  O  7 29DSPATR(HI)
     A            AIZONE        10A  O  7 31DSPATR(HI)
     A            AIUSER        10A  O  7 42DSPATR(HI)
     A            AITTYP         4A  O  7 53DSPATR(HI)
     A            AIUPYN         1A  O  7 58DSPATR(HI)
     A            DDBUFF        72A  O  7 60DSPATR(HI)
     A            AIDATE         6S 0H
     A            AISEQN         2S 0H
     A            AIJOBN        10A  H
     A            AIJUSR        10A  H
     A            AITRID       300A  H
     A            AIBUFF      9999A  H
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0015)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            DDUSR         10A  O  1  2OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'GPAUUIEQ'
     A                                  1 32'USER INPUT ENQUIRY'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDRDAT         7A  O  1 72OVRDTA
     A            DDJOB         10A  O  2  2OVRDTA
     A            DDMTIM         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  3  4'<POSITION TO>'
     A                                  4  4'Date'
     A                                  4 13'Time'
     A                                  3 22'<-------------FILTER ON-------------
     A                                      ->'
     A                                  4 22'Job No'
     A                                  4 29'V'
     A                                  4 31'Zone'
     A                                  4 42'User'
     A                                  4 53'Type'
     A                                  4 58'U'
     A                                  4 60'Data'
     A            DDDATE         6A  B  5  4DSPATR(HI UL PC)
     A                                      OVRDTA
     A            DDTIME         6A  B  5 13DSPATR(HI UL)
     A                                      OVRDTA
     A            DDJBNO         6A  B  5 22DSPATR(HI UL)
     A                                      OVRDTA
     A            DDRDVL         1A  B  5 29DSPATR(HI UL)
     A                                      OVRDTA
     A            DDZONE        10A  B  5 31DSPATR(HI UL)
     A                                      OVRDTA
     A            DDUSER        10A  B  5 42DSPATR(HI UL)
     A                                      OVRDTA
     A            DDTTYP         4A  B  5 53DSPATR(HI UL)
     A                                      OVRDTA
     A            DDUPYN         1A  B  5 58DSPATR(HI UL)
     A                                      OVRDTA
     A                                  6  2'                                  -
     A                                                                         -
     A                                                                         -
     A                                                                '
     A                                      DSPATR(UL)
      *
      * -------------------------------------------------------------------*
      *   NO DATA                                                          *
      * -------------------------------------------------------------------*
      *
     A          R NODATA
     A                                      PUTOVR
     A                                  7  2'                                  -
     A                                                                         -
     A                                               '
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A            DDMSG         70     22  2OVRDTA DSPATR(HI)
     A                                 23  2'                                  -
     A                                                                         -
     A                                                                         -
     A                                                                '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit F12=Abandon               -
     A                                        Rollup  Rolldown'
      *
      * -------------------------------------------------------------------*
      *   CLEAR                                                            *
      * -------------------------------------------------------------------*
      *
     A          R CLEAR                     TEXT('CLEAR SCREEN')
     A                                  1  2' '
      *
      * -------------------------------------------------------------------*
      *   DETAILS                                                          *
      * -------------------------------------------------------------------*
      *
     A          R DETAILS
     A                                      RTNDTA
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSR         10A  O  1  2OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'GPAUUIEQ'
     A                                  1 32'USER INPUT ENQUIRY'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDRDAT         7A  O  1 72OVRDTA
     A            DDJOB         10A  O  2  2OVRDTA
     A            DDMTIM         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  3  2'Date'
     A            AIDATEX        6  0   3  7EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A                                  3 16'Time'
     A            AITIME         6  0   3 21EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A                                  3 30'Job Number'
     A            AIJBNO         6  0   3 41DSPATR(HI)
     A                                  3 48'Zone'
     A            AIZONE        10      3 53DSPATR(HI)
     A                                  3 64'User'
     A            AIUSER        10      3 69DSPATR(HI)
     A                                  3 80'Type'
     A            AITTYP         4      3 85DSPATR(HI)
     A                                  4  7'*...+....1....+....2....+....3-
     A                                      ....+....4....+....5....+....6-
     A                                      ....+....7....+....8....+....9-
     A                                      ....+....'
     A                                  5  2'   1'
     A            DDBF01       100A  O  5  7DSPATR(HI)
     A                                  5108' 100'
     A                                  6  2' 101'
     A            DDBF02       100A  O  6  7DSPATR(HI)
     A                                  6108' 200'
     A                                  7  2' 201'
     A            DDBF03       100A  O  7  7DSPATR(HI)
     A                                  7108' 300'
     A                                  8  2' 301'
     A            DDBF04       100A  O  8  7DSPATR(HI)
     A                                  8108' 400'
     A                                  9  2' 401'
     A            DDBF05       100A  O  9  7DSPATR(HI)
     A                                  9108' 500'
     A                                 10  2' 501'
     A            DDBF06       100A  O 10  7DSPATR(HI)
     A                                 10108' 600'
     A                                 11  2' 601'
     A            DDBF07       100A  O 11  7DSPATR(HI)
     A                                 11108' 700'
     A                                 12  2' 701'
     A            DDBF08       100A  O 12  7DSPATR(HI)
     A                                 12108' 800'
     A                                 13  2' 801'
     A            DDBF09       100A  O 13  7DSPATR(HI)
     A                                 13108' 900'
     A                                 14  2' 901'
     A            DDBF10       100A  O 14  7DSPATR(HI)
     A                                 14108'1000'
     A                                 15  2'1001'
     A            DDBF11       100A  O 15  7DSPATR(HI)
     A                                 15108'1100'
     A                                 16  2'1101'
     A            DDBF12       100A  O 16  7DSPATR(HI)
     A                                 16108'1200'
     A                                 17  2'1201'
     A            DDBF13       100A  O 17  7DSPATR(HI)
     A                                 17108'1300'
     A                                 18  2'1301'
     A            DDBF14       100A  O 18  7DSPATR(HI)
     A                                 18108'1400'
     A                                 19  2'1401'
     A            DDBF15       100A  O 19  7DSPATR(HI)
     A                                 19108'1500'
     A                                 20  2'1501'
     A            DDBF16       100A  O 20  7DSPATR(HI)
     A                                 20108'1600'
     A                                 21  2'1601'
     A            DDBF17       100A  O 21  7DSPATR(HI)
     A                                 21108'1700'
     A                                 22  2'1701'
     A            DDBF18       100A  O 22  7DSPATR(HI)
     A                                 22108'1800'
     A                                 23  2'1801'
     A            DDBF19       100A  O 23  7DSPATR(HI)
     A                                 23108'1900'
     A                                 24  7'*...+....1....+....2....+....3-
     A                                      ....+....4....+....5....+....6-
     A                                      ....+....7....+....8....+....9-
     A                                      ....+....'
