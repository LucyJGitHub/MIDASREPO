      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_RU                                                        *
/*EXI *  TEXT('Midas GP Global Audit Changes - Header')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GPAUCHPD - Global Audit Changes - Header                  *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGP004  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP004 - Audit Reporting                                     *
      *                                                               *
      *****************************************************************
      *
     A          R GPAUCHD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A            AHZONE        10          TEXT('Zone')
     A                                      COLHDG('Zone')
     A            AHCID         17P 0       TEXT('Audit Changes ID')
     A                                      COLHDG('Audit' +
     A                                             'Changes' +
     A                                             'ID')
     A            AHRDNB         5P 0       TEXT('Run Day Number')
     A                                      COLHDG('Run Day' +
     A                                             'Number')
     A            AHINPT         4          TEXT('Input')
     A                                      COLHDG('Input')
     A            AHBRCA         3          TEXT('Branch')
     A                                      COLHDG('Branch')
     A            AHREFI        40          TEXT('Reference ID')
     A                                      COLHDG('Reference ID')
     A            AHACTN        10          TEXT('Action')
     A                                      COLHDG('Action')
     A            AHUSER        10          TEXT('User')
     A                                      COLHDG('User')
      *
     A            AHFILE        10          TEXT('File')
     A                                      COLHDG('File')
     A            AHRCID        15          TEXT('Record ID')
     A                                      COLHDG('Record ID')
     A            AHENTT         2          TEXT('Entry Type')
     A                                      COLHDG('Entry Type')
     A            AHJNBR         6S 0       TEXT('Job Number')
     A                                      COLHDG('Job Number')
     A            AHPGM         10          TEXT('Program')
     A                                      COLHDG('Program')
     A            AHDATE         6S 0       TEXT('Machine Date YYMMDD')
     A                                      COLHDG('Machine' 'Date' 'YYMMDD')
     A            AHTIME         6S 0       TEXT('Time')
     A                                      COLHDG('Time')
     A            AHCCID        10S 0       TEXT('Commit Cycle ID')
     A                                      COLHDG('Commit Cycle ID')
     A            AHRLBK         1A         TEXT('Rollback')
     A                                      COLHDG('Rollback')
     A            AHTTYP         4A         TEXT('Transaction Type')
     A                                      COLHDG('Transaction Type')
     A            AHDELT         1A         TEXT('Delete')
     A                                      COLHDG('Delete')
      *
     A          K AHZONE
     A          K AHRDNB
     A          K AHBRCA
     A          K AHINPT
     A          K AHCCID
     A          K AHCID
