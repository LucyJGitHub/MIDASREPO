      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_RU                                                        *
/*EXI *  TEXT('Midas GP Global cashflows')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GPCASHPD - Global Cashflows                               *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 25Nov05               *
      *                 BUG5221            Date 13Dec04               *
      *                 BUG3860            Date 19Aug04               *
      *                 CGL029             Date 01Sep03               *
      *                 CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG5221 - Addition of fields                                 *
      *  BUG3860 - Add alpha net settlement customer                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGP001 - Global Processing                                   *
      *                                                               *
      *****************************************************************
     A          R GOCASHD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *---------------------------------------------------------------------
     A*
     A            C_ID          15S 0       TEXT('Identifier')
     A                                      COLHDG('Identifier')
      *
      *---------------------------------------------------------------------
      *
     A            C_DATE         5P 0       TEXT('Cashflow Date')
     A                                      COLHDG('Cashflow' 'Date')
     A            C_ASLI         1A         TEXT('Asset/Liability')
     A                                      COLHDG('Asset/' 'Liability')
     A            C_EVTP         2A         TEXT('Event Type')
     A                                      COLHDG('Event' 'Type')
     A            C_EVRF         6A         TEXT('Event Reference')
     A                                      COLHDG('Event' 'Reference')
      *
     A            C_CATG         1A         TEXT('Cashflow Category')
     A                                      COLHDG('Cashflow' 'Category')
     A            C_AMNT        15P 0       TEXT('Cashflow Amount')
     A                                      COLHDG('Cashflow' 'Amount')
     A            C_CCY          3A         TEXT('Cashflow Currency')
     A                                      COLHDG('Cashflow' 'Currency')
     A            C_IO           1A         TEXT('Cashflow In/Out')
     A                                      COLHDG('Cashflow' 'In/Out')
     A            C_DUEI         1A         TEXT('Due Ind. (Y/N)')
     A                                      COLHDG('Due Ind.' '(Y/N)')
      *
     A            C_SETM         2A         TEXT('Settlement Method')
     A                                      COLHDG('Settlement' 'Method')
     A**Start of change CGL029
     A**********  C_SETA        12A         TEXT('Settlement Account')                        CGL029
     A            C_SETA        18A         TEXT('Settlement Account')
     A**End of change CGL029
     A                                      COLHDG('Settlement' 'Account')
     A            C_SETB         3A         TEXT('Settlement Branch')
     A                                      COLHDG('Settlement' 'Branch')
     A            C_SETC         3A         TEXT('Settlement Currency')
     A                                      COLHDG('Settlement' 'Currency')
     A            C_NTRF        19A         TEXT('Net Reference')
     A                                      COLHDG('Net Reference')
      * Begin /CSD027/
     A**********  C_NTSC         6S 0       TEXT('Net Settlement Customer')                   CSD027
     A            C_NTSC         6A         TEXT('Net Settlement Customer')
      * End /CSD027/
     A                                      COLHDG('Net Settlement' 'Customer')
     A**Start of change BUG3860
     A            C_NTCA         6A         TEXT('Net Settlement Cust (Alpha)')
     A                                      COLHDG('Net Settlemnt' 'Cust Alpha')
     A**End of change BUG3860
     A**Start of change BUG5521
     A            C_MOD          2A         TEXT('Module')
     A                                      COLHDG('Module')
     A            C_TRTP         5A         TEXT('Transaction Type')
     A                                      COLHDG('Transaction' 'Type')
     A            C_TRST         5A         TEXT('Transaction Sub Type')
     A                                      COLHDG('Transaction' 'Sub Type')
     A            C_ZONE        10A         TEXT('Zone')
     A                                      COLHDG('Zone')
     A            C_RCSA         6A         TEXT('Risk Customer (Alpha)')
     A                                      COLHDG('Risk Customer' '(Alpha)')
     A            C_AMTDEC      23S 8       TEXT('Cashflow Amount')
     A                                      COLHDG('Cashflow' 'Amount')
     A**End of change BUG5521
