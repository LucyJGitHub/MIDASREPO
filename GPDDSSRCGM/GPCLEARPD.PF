      *****************************************************************
/*S*D ***PFBASE********************************************************                     BUG27914
/*E*S ***PF_RU*********************************************************                     BUG27914
/*STD *  PFBASE                                                       *            BUG27914 BUG28163
/*EXS *  PF_RU                                                        *            BUG27914 BUG28163
/*EXI *  TEXT('Midas GP Items in Clearing Detail File')               *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GPCLEARPD - Midas GP Items in Clearing Detail File        *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. BUG28163           Date 05Oct10               *
      *                 BUG27914           Date 09Jul10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CAP203  *CREATE    Date 02Feb10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG28163 - Forward movements does not appear on PABE         *
      *  BUG27914 - RAME Design Changes                               *
      *  CAP203 - Teller Related APIs - Account Movements             *
      *                                                               *
      *****************************************************************
      *
      *Start of change BUG28163
     A**********R APOSTPDF                  FORMAT(GPAPOSTPD)                               BUG28163
     A                                      REF(MFRFL)
     A          R APOSTPDF                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
     A            RECI      R
     A            CNUM           6A         TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
     A            CCY       R
     A            ACODQQ    R               REFFLD(ACODQQ)
     A            ACSQ      R
     A            IPDN      R
     A            ACNO      R
     A            PSTD      R
     A            VALD      R
     A            TRAT      R
     A            PNAR      R
     A            PSTA      R
     A            DRCR      R
     A            ASOC           6A         TEXT('Associated customer')
     A                                      COLHDG('Assoctd.            ' +
     A                                      'Customer')
     A            CHQN      R
     A            SPOS      R
     A            BRCA      R
     A            RIND      R
     A            REJC      R
     A            DPMT      R
     A            RRNM      R
     A            ZZ009     R
     A            ATYP      R
     A            ZZ002     R
     A            EXIN      R
     A            PRIN      R
     A            GETP      R
     A            ACUM      R
     A            PRFC      R
     A            PTIM      R
     A            VOIN      R
     A            MGTT      R
     A            ACSC      R
     A            LTAI      R
     A            ZZ007     R
     A            RINO      R
     A            SWCR      R
     A            DLREF     R
     A            FACO      R
     A            DBCNUM         6A         TEXT('dealt/borrower customer no.')
     A                                      COLHDG('dealt/              ' +
     A                                      'borrower            ' +
     A                                      'cust.no.')
     A            PREF      R
     A            OTRFQQ        15A
     A            OTST      R
     A            OTTP      R
     A            SDCB      R
     A            SDLB      R
     A            BOKC      R
     A            NITMS     R               REFFLD(NOITMS)
     A            REBI      R
     A            CHGA      R
     A            ZZ019     R
     A            TLIN      R               REFFLD(IND)
     A                                      TEXT('Teller Input Ind.')
     A                                      COLHDG('TLR' +
     A                                             'I/P.'+
     A                                             'Ind')
     A            TRCCY     R
     A            STYP      R
     A            CQRI      R
     A            PBTT      R
     A            ORBR      R
     A            ORAMT     R               REFFLD(AMNM)
     A                                      TEXT('Original Amt - Giro')
     A                                      COLHDG('ORG' +
     A                                             'AMT'+
     A                                             'GETT')
     A            XRFI      R               REFFLD(MNTH)
     A                                      TEXT('Extension File Reference ID')
     A                                      COLHDG('Extension' +
     A                                             'File' +
     A                                             'Reference ID')
     A            XRFN      R               REFFLD(CHRN)
     A                                      TEXT('Extension File Reference No.')
     A                                      COLHDG('Extension' +
     A                                             'File' +
     A                                             'Reference Number')
     A            TRST      R               REFFLD(TRSU)
     A                                      TEXT('Trans Sub-type')
     A                                      COLHDG('Transaction' +
     A                                      'Sub-type')
     A            ACKEY     R               REFFLD(ACKY)
     A            FEEFLG    R               REFFLD(FPIN)
     A                                      TEXT('Fee Flag')
     A                                      COLHDG('Fee Flag')
     A            FEEVAL    R               REFFLD(FEAM)
     A                                      TEXT('Fee Value')
     A                                      COLHDG('Fee Value')
     A            FEEPCT    R               REFFLD(WTRR)
     A                                      TEXT('Fee Percent')
     A                                      COLHDG('Fee Percent')
     A            RORACT    R               REFFLD(RCPN)
     A                                      TEXT('Receipt - Origin +
     A                                      Account')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Account')
     A            RORCTY    R               REFFLD(CCOD)
     A                                      TEXT('Receipt - Origin +
     A                                      Country')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Country')
     A            RORBNK    R               REFFLD(BARF)
     A                                      TEXT('Receipt - Origin +
     A                                      Bank')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Bank')
     A            RORCDE    R               REFFLD(SRON)
     A                                      TEXT('Receipt - Origin +
     A                                      Code')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Code')
     A            PORACT    R               REFFLD(PYNM)
     A                                      TEXT('Pay - Origin +
     A                                      Account')
     A                                      COLHDG('Pay - Origin +
     A                                      Account')
     A            PORCTY    R               REFFLD(CCOD)
     A                                      TEXT('Pay - Origin +
     A                                      Country')
     A                                      COLHDG('Pay - Origin +
     A                                      Country')
     A            PORBNK    R               REFFLD(BARF)
     A                                      TEXT('Pay - Origin Bank')
     A                                      COLHDG('Pay - Origin Bank')
     A            PORCDE    R               REFFLD(SRON)
     A                                      TEXT('Pay - Origin Code')
     A                                      COLHDG('Pay - Origin Code')
     A            DSTACT    R               REFFLD(ACCID)
     A                                      TEXT('Destination Account')
     A                                      COLHDG('Destination +
     A                                      Account')
     A            DSTCTY    R               REFFLD(CCOD)
     A                                      TEXT('Destination Country')
     A                                      COLHDG('Destination' +
     A                                      'Country')
     A            DSTBNK    R               REFFLD(BARF)
     A                                      TEXT('Destination Bank')
     A                                      COLHDG('Destination Bank')
     A            SETDAT    R               REFFLD(SLDT)
     A                                      TEXT('Settlement Date')
     A                                      COLHDG('Settlement Date')
     A            TICKER    R               REFFLD(ACCM)
     A                                      TEXT('Ticker')
     A                                      COLHDG('Ticker')
     A            EXCHNG    R               REFFLD(NTRY)
     A                                      TEXT('Exchange')
     A                                      COLHDG('Exchange')
     A            NOMINL    R               REFFLD(CDAM)
     A                                      TEXT('Nominal')
     A                                      COLHDG('Nominal')
     A            FDTRID    R               REFFLD(TRNO)
     A                                      TEXT('Detail Transaction +
     A                                      ID')
     A                                      COLHDG('Detail' +
     A                                      'Transaction ID')
     A            ACOD      R               REFFLD(ACOD)
     A            OTRF          21A
     A**End of change BUG28163
      *
