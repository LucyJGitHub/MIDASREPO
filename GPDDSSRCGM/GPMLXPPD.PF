      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas GP ML Exposure Summary (Counterparty)')          *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GPMLXPPD - ML Exposure Summary (Counterparty)             *
      *                                                               *
      *  (c) Finastra Systems Technology, Inc. 2019                   *
      *                                                               *
      *  Last Amend No. DAI103             Date 25Jun19               *
      *  Prev Amend No. NADC01  *CREATE    Date 17Nov09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  DAI103 - Upgraded MPlus 121 NADC01 ML Extracts to FB 2.1     *
      *  NADC01 - ML EXTRACTS FOR MIZUHO                              *
      *                                                               *
      *****************************************************************
     A          R GPMLXPD0                  TEXT('(c) Finastra Systems +
     A                                      Technology, Inc. 2019')
      *
     A            MELIMDEF       1A         TEXT('Limit Defined')
     A                                      COLHDG('Limit Defined')
     A            MELIMITID     15P 0       TEXT('Limit ID')
     A                                      COLHDG('Limit' +
     A                                      'ID')
     A            MEUTSRTYPE     2P 0       TEXT('Utilisor Type')
     A                                      COLHDG('Utilisor' +
     A                                      'Type')
     A            MEUTSRCODE    10          TEXT('Utilisor Code')
     A                                      COLHDG('Utilisor' +
     A                                      'Code')
     A            MECUSTNAME    28          TEXT('Customer & Report Name')
     A                                      COLHDG('Customer' +
     A                                      '& Name')
     A            MEBRCA         3A         TEXT('Booking Branch')
     A                                      COLHDG('Booking' +
     A                                      'Branch')
     A            MECNCD         2A         TEXT('Country Code')
     A                                      COLHDG('Country Code')
     A            MECNCDNAM     33A         TEXT('Country Code & Name')
     A                                      COLHDG('Country & Name')
     A            MEINST        10A         TEXT('Global Instrument Code')
     A                                      COLHDG('Global Inst.' +
     A                                      'Code')
     A            MEGLCAT        5A         TEXT('GL Category')
     A                                      COLHDG('GL' +
     A                                      'Cat')
     A            METIEXUSD     15P 0       TEXT('Total Inst Exp in USD')
     A                                      COLHDG('Total Inst Exp' +
     A                                      'in USD')
     A            MEPEXPUSD     15P 0       TEXT('Cpty Exposure in USD')
     A                                      COLHDG('Cpty Exp' +
     A                                      'in USD')
     A            METDIFUSD     15P 0       TEXT('Total Difference in USD')
     A                                      COLHDG('Total Difference' +
     A                                      'in USD')
     A            MELIMTUSD     15P 0       TEXT('Limit in USD')
     A                                      COLHDG('Limit ' +
     A                                      'in USD')
     A            MEEXPDATE     10A         TEXT('Limit Expiry Date')
     A                                      COLHDG('Limit' 'Expiry Date')
     A            MELIMDEFX      1A         TEXT('Limit Defined - Country')
     A                                      COLHDG('Limit Defined')
      *
     A          K MECNCD
     A          K MEINST
     A          K MEGLCAT
      *
