      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  REUSEDLT(*YES)                                               *                       CGP016
/*EXI *  TEXT('Midas GP Global customer details')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GPCUSGPD - Global Customer details                        *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGP016             Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG4521  *CREATE   Date 14Oct04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved from GPDDSSRCGM                               *
      *  BUG4521- Global customer details                             *
      *                                                               *
      *****************************************************************
      *
     A          R GPCUSGD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A            CUSCUST        6          TEXT('Customer Number')
     A                                      COLHDG('Customer Number')
     A            CUSGLOB       10          TEXT('Global Customer ID')
     A                                      COLHDG('Global' +
     A                                             'Customer' +
     A                                             'ID')
     A            CUSCSSN       10          TEXT('Customer Shortname')
     A                                      COLHDG('Customer Shortname' )
     A            CUSNA1        35          TEXT('Cust. Name & Address 1')
     A                                      COLHDG('Cust. Name' +
     A                                             '& Address 1' )
     A            CUSNA2        35          TEXT('Cust. Name & Address 2')
     A                                      COLHDG('Cust. Name' +
     A                                             '& Address 2' )
     A            CUSNA3        35          TEXT('Cust. Name & Address 3')
     A                                      COLHDG('Cust. Name' +
     A                                             '& Address 3' )
     A            CUSNA4        35          TEXT('Cust. Name & Address 4')
     A                                      COLHDG('Cust. Name' +
     A                                             '& Address 4' )
     A            CUSRNM        20          TEXT('Customer Report Name')
     A                                      COLHDG('Customer Report Name' )
     A            CUSRTN        10          TEXT('Customer Report Town')
     A                                      COLHDG('Customer Report Town' )
     A            CUSBRCD        3          TEXT('Branch')
     A                                      COLHDG('Branch')
     A            CUSNBI         1          TEXT('Bank/non-bank Indicator')
     A                                      COLHDG('Bank/non-bank' +
     A                                             'Indicator' )
     A            CUSICD        10          TEXT('Global Industry Code')
     A                                      COLHDG('Global' +
     A                                             'Industry Code' )
     A            CUSACO        10          TEXT('Global Account Officer Code')
     A                                      COLHDG('Global' +
     A                                             'Account Officer' +
     A                                             'Code' )
     A            CUSCNCZ        2          TEXT('Country of Citizenship')
     A                                      COLHDG('Country of' +
     A                                             'Citizenship' )
     A            CUSCOLC        2          TEXT('Country of Location')
     A                                      COLHDG('Country of' +
     A                                             'Location' )
     A            CUSACTIVE      1          TEXT('STATUS')
     A                                      COLHDG('Status')
