      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GP Work in Progress Housekeeping Report')        *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PRTF/GP000110P1 - Midas GP Work in Progress Housekeeping     *
      *                    Report                                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2011            *
      *                                                               *
      *  Last Amend No. CSF011  *CREATE    Date 12Sep11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSF011 - Customer Name on Inputs                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
 
     A          R HEADP1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2011')
 
     A                                      SKIPB(003)
     A                                     6'REFERENCE GP000110P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
 
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
 
     A                                    50'WORK IN PROGRESS PURGE +
     A                                       REPORT'
     A                                      SPACEB(001)
     A                                    50'-----------------------------'
     A                                      SPACEB(001)
 
     A                                     2'WIP ID'
     A                                      SPACEB(002)
     A                                    21'WIP ACTION'
     A                                    40'API NAME'
     A                                    54'WIP STATUS'
     A                                    73'TRANSACTION REFERENCE'
     A                                     2'INPUT USER'
     A                                      SPACEB(001)
     A                                    21'TIMESTAMP'
     A                                    54'AUTHORISE USER'
     A                                    73'TIMESTAMP'
     A                                     2'----------'
     A                                      SPACEB(001)
     A                                    21'----------'
     A                                    40'--------'
     A                                    54'--------------'
     A                                    73'---------------------'
 
     A          R DETAIL                    SPACEB(001)
     A            RSWID         15  0      2EDTCDE(Z)
     A            RSWACT        15A       21
     A            RSWAPI        10A       40
     A            RSWSTAT       15A       54
     A            RSWTREF       50        73
     A            RSWINPUSER    10A        2SPACEB(001)
     A            RSWINPTS        Z       21
     A            RSWAUTHS      10A       54
     A            RSWAUTHTS       Z       73
 
      *-------------------*
      ***  Report Trailer *
      *-------------------*
 
     A          R TRAILP1
     A                                    51'***  END OF REPORT ***'
     A                                      SPACEB(003)
