      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GP Roles By User Report')                        *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PRTF/GP004002P1 - Midas GP Roles By User Report              *
      *                                                               *
      *  (c) Finastra International Limited 2011                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CBF005  *CREATE    Date 04Jul11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CBF005 - BF Infrastructure: Single Security User Maintenance *
      *           Profile                                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2011')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GP004002P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    60'Roles By User'
     A                                      SPACEA(1)
      *
     A                                    60'-------------'
     A                                      SPACEA(3)
      *
      *--------------------------------------*
      ***  Detail Record Format: Branch      *
      *--------------------------------------*
      *
     A          R DETAIL1
     A  10                                80'Branch:'
     A  10        RBRCD          3        90
     A  10        RBRNM         30       100
     A  10                                  SPACEA(2)
     A  11                               119'Consolidated'
     A  11                                  SPACEA(2)
      *
      *--------------------------------------*
      ***  Detail Record Format: Role        *
      *--------------------------------------*
      *
     A          R DETAIL2
     A                                     6'User:'
     A                                      SPACEB(1)
     A            RUSNM         30        15
     A                                      SPACEA(2)
      *
     A                                    15'Role'
     A                                    50'Role Description'
     A                                      SPACEA(1)
     A                                    15'----'
     A                                    50'----------------'
     A                                      SPACEA(1)
      *
      *--------------------------------------*
      ***  Detail Record Format: User        *
      *--------------------------------------*
      *
     A          R DETAIL3
     A            RGNAM         30        15
     A            RGDNM         60        50
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    55'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
