      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas GP Artefacts By Role Report - Audit')            *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PRTF/GP004004AU - Midas GP Artefacts By Role Report - Audit  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2011            *
      *                                                               *
      *  Last Amend No. AR892737C          Date 01Feb12               *
      *  Prev Amend No. CBF005  *CREATE    Date 04Jul11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR892737C - Permissions by User Report does not allow lower  *
      *              case characters on user profile input            *
      *  CBF005 - BF Infrastructure: Single Security User Maintenance *
      *           Profile                                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2011')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE GP004004AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    54'Artefacts By Role - Audit'
     A                                      SPACEA(1)
      *
     A                                    54'-------------------------'
     A                                      SPACEA(3)
      *
      *--------------------------------*
      ***  File Control                *
      *--------------------------------*
      *
     A          R CONTROL
      *
     A N11                                20'Total no. of +
     A                                      Branch extracted:'
     A*N11******  AU_CTR1        5  0     60EDTCDE(3)                                      AR892737C
     A N11        AU_CTR1       10  0     60EDTCDE(1)                                      AR892737C
     A N11                                  SPACEA(1)
     A                                    20'Total no. of +
     A                                      Roles extracted:'
     A**********  AU_CTR2        5  0     60EDTCDE(3)                                      AR892737C
     A            AU_CTR2       10  0     60EDTCDE(1)                                      AR892737C
     A                                      SPACEA(1)
     A                                    20'Total no. of +
     A                                      Artefacts extracted:'
     A**********  AU_CTR3        5  0     60EDTCDE(3)                                      AR892737C
     A            AU_CTR3       10  0     60EDTCDE(1)                                      AR892737C
     A                                      SPACEA(1)
      *
      *--------------------------------*
      *** No Details                   *
      *--------------------------------*
      *
     A          R NODTLS
     A                                    53'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *--------------------------------*
      *** Database Error               *
      *--------------------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
