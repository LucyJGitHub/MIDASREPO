      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GP User/Role Entitlement Audit Report')          *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  GP004012P1 - Midas GP User/Role Entitlement Audit Report     *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR1029172          Date 06Sep12               *
      *                 CBF022 *C *CREATE  Date 16Jul12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR1029172 - Auditable Report contains some descrepancies     *
      *  CBF022 - BF Infrastructure: Auditability                     *
      *                                                               *
      *****************************************************************
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2012')
      *
     A                                      SKIPB(1)
     A                                     6
     A                                      'REFERENCE GP004012P1'
      *
     A            BJURPT        53        37
     A                                   101
     A                                      'DATE'
      *
     A            BJMRDT         7       106
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    47
     A                                      'USER/ROLE ENTITLEMENT AUDIT REPORT'
     A                                      SPACEA(001)
      *
     A                                    47
     A                                      '----------------------------------'
     A                                      SPACEA(002)
      *
     A          R DETAIL1
     A                                     4
     A                                      'User Identifier        :'
     A            RUSID         32A       30
     A                                      TEXT('User Identifier')
     A                                     4
     A                                      'User Name              :'
     A                                      SPACEB(001)
     A            RNAME         52A       30
     A                                      TEXT('User Name')
     A                                     4
     A                                      'Last Change User       :'
     A                                      SPACEB(002)
     A            RUSER         30A       30
     A                                      TEXT('Last Change User')
     A                                    68
     A                                      'Last Authorising User       :'
     A            RAUTH         30A       99
     A                                      TEXT('Last authorising user')
     A                                     4
     A                                      'Change Date            :'
     A                                      SPACEB(001)
     A            RCDAT         26A       30
     A                                      TEXT('Change date')
     A                                    68
     A                                      'Authorisation Date          : '
     A            RAUDT         26A       99
     A                                      TEXT('Authorisation date')
     A                                     4
     A                                      'Last Change Action     :'
     A                                      SPACEB(001)
     A            RACTN         10A       30
     A                                      TEXT('Last change action')
     A                                     4
     A                                      'Change Timestamp       :'
     A                                      SPACEB(001)
     A            RTIME         26A       30
     A                                      TEXT('Change timestamp')
     A                                    68
     A                                      'Authorisation Timestamp     :'
     A            RATIM         26A       99
     A                                      TEXT('Authorisation Timestamp')
      ** Start of Change AR1029172
     A                                      SPACEA(001)
      ** End of Change AR1029172
      *
     A          R HEADP2
     A                                      SPACEA(001)
     A                                     4
     A                                      'ENTITLEMENT DETAILS :'
     A                                     6
     A                                      'Entitlement Type'
     A                                      SPACEB(002)
     A                                     6
     A                                      '-----------------------------------
     A                                      ------------------'
     A                                      SPACEB(001)
      *
     A          R DETAIL2
     A                                      SPACEA(001)
     A            RENTL         52A        6
     A                                      TEXT('Entitlement Type')
      *
     A          R HEADP3
     A                                      SPACEA(001)
     A                                     8
     A                                      'ROLE/VALUE DETAILS :'
     A                                    10
     A                                      'Role Name'
     A                                      SPACEB(002)
     A                                    31
     A                                      'Entitlement Value'
     A                                    53
     A                                      'Entitlement Description'
     A                                    10
     A                                      '----------------'
     A                                      SPACEB(001)
     A                                    31
     A                                      '-----------------'
     A                                    53
     A                                      '-----------------------------------
     A                                      -------------------------------'
      *
     A          R DETAIL3
     A                                      SPACEA(001)
     A            RROLE         16A       10
     A                                      TEXT('Role Name')
     A            RENTV         13A       31
     A                                      TEXT('Entitlement Value')
     A            REDES         65A       53
     A                                      TEXT('Entitlement Description')
      *
     A          R TRAILP1
     A                                    52
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
