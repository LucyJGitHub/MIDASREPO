      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GP Role Audit Report')                           *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  GP004013P1 - Midas GP Role Audit Report                      *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CBF022 *CREATE     Date 16Jul12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CBF022 - BF Infrastructure: Auditability                     *
      *                                                               *
      *****************************************************************
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2012')
      *
     A                                      SKIPB(1)
     A                                     6
     A                                      'REFERENCE GP004013P1'
      *
     A            BJURPT        53        37
     A                                   101
     A                                      'DATE'
      *
     A            BJMRDT         7       106
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    52
     A                                      'ROLE AUDIT REPORT'
     A                                      SPACEA(001)
      *
     A                                    52
     A                                      '-----------------'
     A                                      SPACEA(002)
      *
     A          R DETAIL1
     A                                     4
     A                                      'Role Name              :'
     A            RROLE         32A       30
     A                                      TEXT('Role Name')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Description            :'
     A            RDESC         32A       30
     A                                      TEXT('Description')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Last Change User       :'
     A            RUSER         30A       30
     A                                      TEXT('Last Change User')
     A                                    68
     A                                      'Last Authorising User   :'
     A            RAUTH         30A       94
     A                                      TEXT('Last authorising user')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Change Date            :'
     A            RCDAT         26A       30
     A                                      TEXT('Change date')
     A                                    68
     A                                      'Authorisation Date      : '
     A            RAUDT         26A       94
     A                                      TEXT('Authorisation date')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Last Change Action     :'
     A            RACTN         10A       30
     A                                      TEXT('Last change action')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Change Timestamp       :'
     A            RTIME         26A       30
     A                                      TEXT('Change timestamp')
     A                                    68
     A                                      'Authorisation Timestamp :'
     A            RATIM         26A       94
     A                                      TEXT('Authorisation Timestamp')
     A                                      SPACEA(002)
     A                                     4
     A                                      'Display Name           :'
     A            RDISP         62A       30
     A                                      TEXT('Display Name')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Parent Role Identifier :'
     A            RPARE         22A       30
     A                                      TEXT('Parent Role Identifier')
     A                                      SPACEA(001)
      *
     A          R HEADP2
     A                                     4
     A                                      'ROLES PERMISSIONS :'
     A                                      SPACEA(002)
     A                                     6
     A                                      'Permission Name'
     A                                      SPACEA(001)
     A                                     6
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ---------------------'
     A                                      SPACEA(001)
      *
     A          R DETAIL2
     A            RPERM         90A        6
     A                                      SPACEA(001)
      *
     A          R TRAILP1
     A                                    52
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
