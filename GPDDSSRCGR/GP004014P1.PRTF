      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GP Permission Audit Report')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  GP004014P1 - Midas GP Permission Audit Report                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CBF022 *CREATE     Date 16Jul12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CBF022 - BF Infrastructure: Auditability                     *
      *                                                               *
      *****************************************************************
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
      *
     A                                      SKIPB(1)
     A                                     6
     A                                      'REFERENCE GP004014P1'
      *
     A            BJURPT        53        37
     A                                   101
     A                                      'DATE'
      *
     A            BJMRDT         7       106
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    52
     A                                      'PERMISSION AUDIT REPORT'
     A                                      SPACEA(001)
      *
     A                                    52
     A                                      '-----------------------'
     A                                      SPACEA(002)
      *
     A          R DETAIL1
     A                                     4
     A                                      'Permission Name        :'
     A            RPER1         86A       30
     A                                      TEXT('Permission Name1')
     A                                      SPACEA(001)
     A            RPER2         86A       30
     A                                      TEXT('Permission Name2')
     A                                      SPACEA(001)
     A            RPER3         86A       30
     A                                      TEXT('Permission Name3')
     A                                      SPACEA(002)
     A                                     4
     A                                      'Last Change User       :'
     A            RUSER         30A       30
     A                                      TEXT('Last Change User')
     A                                    68
     A                                      'Last Authorising User   :'
     A            RAUTH         30A       94
     A                                      TEXT('Last authorising user')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Change Date            :'
     A            RCDAT         26A       30
     A                                      TEXT('Change date')
     A                                    68
     A                                      'Authorisation Date      : '
     A            RAUDT         26A       94
     A                                      TEXT('Authorisation date')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Last Change Action     :'
     A            RACTN         10A       30
     A                                      TEXT('Last change action')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Change Timestamp       :'
     A            RTIME         26A       30
     A                                      TEXT('Change timestamp')
     A                                    68
     A                                      'Authorisation Timestamp :'
     A            RATIM         26A       94
     A                                      TEXT('Authorisation Timestamp')
     A                                      SPACEA(002)
     A                                     4
     A                                      'Short Description      :'
     A            RSHO1         86A       30
     A                                      TEXT('Short Description1')
     A                                      SPACEA(001)
     A            RSHO2         86A       30
     A                                      TEXT('Short Description2')
     A                                      SPACEA(001)
     A            RSHO3         86A       30
     A                                      TEXT('Short Description3')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Long Description       :'
     A            RLON1         86A       30
     A                                      TEXT('Long Description1')
     A                                      SPACEA(001)
     A            RLON2         86A       30
     A                                      TEXT('Long Description2')
     A                                      SPACEA(001)
     A            RLON3         86A       30
     A                                      TEXT('Long Description3')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Host                   :'
     A            RHOS1         51A       30
     A                                      TEXT('Host              ')
     A                                      SPACEA(001)
     A            RHOS2         51A       30
     A                                      TEXT('Host Module       ')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Host Module            :'
     A            RHMO1         51A       30
     A                                      TEXT('Host Module       ')
     A                                      SPACEA(001)
     A            RHMO2         51A       30
     A                                      TEXT('Host Module       ')
     A                                      SPACEA(001)
      *
     A          R HEADP2
     A                                      SPACEA(001)
     A                                     4
     A                                      'AVAILABLE ZONES:'
     A                                     6
     A                                      'Zone Identifier'
     A                                      SPACEB(002)
     A                                    76
     A                                      'Allow In COB'
     A                                     6
     A                                      '-----------------------------------
     A                                      -------------------------------'
     A                                      SPACEB(001)
     A                                    76
     A                                      '------------'
      *
     A          R DETAIL2
     A                                      SPACEA(001)
     A            RZONE         10A        6
     A            RDESC         50A       21
     A            RALOW          2A       81
      *
     A          R TRAILP1
     A                                    52
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
