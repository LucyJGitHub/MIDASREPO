      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GP Artefacts Permission Audit Report')           *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  GP004015P1 - Midas GP Artefacts Permission Audit Report      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CBF022 *CREATE     Date 16Jul12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CBF022 - BF Infrastructure: Auditability                     *
      *                                                               *
      *****************************************************************
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
      *
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE GP004015P1'
      *
     A            BJURPT        53        37
     A                                   101
     A                                      'DATE'
      *
     A            BJMRDT         7       106
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    46
     A                                      'ARTEFACTS PERMISSION AUDIT REPORT'
     A                                      SPACEA(001)
      *
     A                                    46
     A                                      '---------------------------------'
     A                                      SPACEA(002)
      *
     A          R DETAIL1
     A                                     4
     A                                      'Permission Name        :'
     A            RPERM         22A       30
     A                                      TEXT('Permission Name')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Description            :'
     A            RDES1         86A       30
     A                                      TEXT('Description1')
     A                                      SPACEA(001)
     A            RDES2         86A       30
     A                                      TEXT('Description2')
     A                                      SPACEA(001)
     A            RDES3         86A       30
     A                                      TEXT('Description3')
     A                                      SPACEA(002)
     A                                     4
     A                                      'Last Change User       :'
     A            RUSER         30A       30
     A                                      TEXT('Last Change User')
     A                                    68
     A                                      'Last Authorising User       :'
     A            RAUTH         30A       99
     A                                      TEXT('Last authorising user')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Change Date            :'
     A            RCDAT         26A       30
     A                                      TEXT('Change date')
     A                                    68
     A                                      'Authorisation Date          : '
     A            RAUDT         26A       99
     A                                      TEXT('Authorisation date')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Last Change Action     :'
     A            RACTN         10A       30
     A                                      TEXT('Last change action')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Change Timestamp       :'
     A            RTIME         26A       30
     A                                      TEXT('Change timestamp')
     A                                    68
     A                                      'Authorisation Timestamp     :'
     A            RATIM         26A       99
     A                                      TEXT('Authorisation Timestamp')
     A                                      SPACEA(003)
      *
     A          R HEADP2
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                     4
     A                                      'ASSIGNED ARTEFACTS :'
     A                                      SPACEA(002)
     A                                     6
     A                                      'Artefact Name'
     A                                      SPACEA(001)
     A                                     6
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ---------------------'
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 002
     A*%%CL 001
     A*%%CL 001
     A*%%***********************************************************************
      *
     A          R DETAIL2
     A            RARTF         90A        6
     A                                      SPACEA(001)
      *
     A          R TRAILP1
     A                                    52
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
