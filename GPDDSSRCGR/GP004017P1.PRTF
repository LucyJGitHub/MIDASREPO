      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GP Event Group Audit Report')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  GP004017P1 - Midas GP Event Group Audit Report               *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CBF022 *CREATE     Date 16Jul12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CBF022 - BF Infrastructure: Auditability                     *
      *                                                               *
      *****************************************************************
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2012')
      *
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE GP004017P1'
     A            BJURPT        53        37
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7       106
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    52
     A                                      'EVENT GROUP AUDIT REPORT'
     A                                      SPACEA(001)
     A                                    52
     A                                      '------------------------'
     A                                      SPACEA(002)
      *
     A          R DETAIL1
     A                                     4
     A                                      'Event Group            :'
     A            REGRP        102A       30
     A                                      TEXT('Event Group')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Event Group Name       :'
     A            RENAM        102A       30
     A                                      TEXT('Event Group name')
     A                                      SPACEA(002)
     A                                     4
     A                                      'Last Change User       :'
     A            RUSER         30A       30
     A                                      TEXT('Last Change User')
     A                                    65
     A                                      'Last Authorising User        :'
     A            RAUTH         30A       97
     A                                      TEXT('Last authorising user')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Change Date            :'
     A            RCDAT         26A       30
     A                                      TEXT('Change date')
     A                                    65
     A                                      'Authorisation Date           : '
     A            RAUDT         26A       97
     A                                      TEXT('Authorisation date')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Last Change Action     :'
     A            RACTN         10A       30
     A                                      TEXT('Last change action')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Change Timestamp       :'
     A            RTIME         26A       30
     A                                      TEXT('Change timestamp')
     A                                    65
     A                                      'Authorisation Timestamp      :'
     A            RATIM         26A       97
     A                                      TEXT('Authorisation Timestamp')
     A                                      SPACEA(002)
      *
     A          R HEADP2
     A                                      SPACEA(001)
     A                                     4
     A                                      'ALLOCATED EVENT CODES:'
     A                                     6
     A                                      'Event Code'
     A                                      SPACEB(001)
     A                                    21
     A                                      'Event Code Description'
     A                                   111
     A                                      'Module'
     A                                     6
     A                                      '----------'
     A                                      SPACEB(001)
     A                                    21
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ----------------'
     A                                   111
     A                                      '-----------------'
      *
     A          R DETAIL2
     A                                      SPACEA(001)
     A            REVID          9A        6
     A                                      TEXT('Event Code Identifier')
     A            REDES         85A       21
     A                                      TEXT('Event Code Description')
     A            RMODU         17A      111
     A                                      TEXT('Module')
      *
     A          R TRAILP1
     A                                    52
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
