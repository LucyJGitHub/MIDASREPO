      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GP Event Group Roles Report')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  GP004018P1 - Midas GP Event Group Roles Report               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CBF022 *CREATE     Date 16Jul12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CBF022 - BF Infrastructure: Auditability                     *
      *                                                               *
      *****************************************************************
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
      *
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE GP004018P1'
      *
     A            BJURPT        53        37
     A                                   101
     A                                      'DATE'
      *
     A            BJMRDT         7       106
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    52
     A                                      'EVENT GROUP ROLES REPORT'
     A                                      SPACEA(001)
      *
     A                                    52
     A                                      '------------------------'
     A                                      SPACEA(002)
      *
     A          R DETAIL1
     A                                     4
     A                                      'Event Group            :'
     A            REGRP        102A       30
     A                                      TEXT('Event Group')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Event Group Name       :'
     A            RENAM        102A       30
     A                                      TEXT('Event Group name')
     A                                      SPACEA(002)
     A                                     4
     A                                      'Last Change User       :'
     A            RUSER         30A       30
     A                                      TEXT('Last Change User')
     A                                    68
     A                                      'Last Authorising User       :'
     A            RAUTH         30A       99
     A                                      TEXT('Last authorising user')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Change Date            :'
     A            RCDAT         26A       30
     A                                      TEXT('Change date')
     A                                    68
     A                                      'Authorisation Date          : '
     A            RAUDT         26A       99
     A                                      TEXT('Authorisation date')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Last Change Action     :'
     A            RACTN         10A       30
     A                                      TEXT('Last change action')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Change Timestamp       :'
     A            RTIME         26A       30
     A                                      TEXT('Change timestamp')
     A                                    68
     A                                      'Authorisation Timestamp     :'
     A            RATIM         26A       99
     A                                      TEXT('Authorisation Timestamp')
     A                                      SPACEA(003)
     A          R HEADP2
     A                                      SPACEA(001)
     A                                     4
     A                                      'ASSIGNED ROLES :'
     A                                     6
     A                                      'Role Name'
     A                                      SPACEB(002)
     A                                    43
     A                                      'Description'
     A                                     6
     A                                      '--------------------------------'
     A                                      SPACEB(001)
     A                                    43
     A                                      '-----------------------------------
     A                                      ----------------------------'
      *
     A          R DETAIL2
     A                                      SPACEA(001)
     A            RROLE         32A        6
     A            RDESC         62A       43
      *
     A          R TRAILP1
     A                                    52
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
