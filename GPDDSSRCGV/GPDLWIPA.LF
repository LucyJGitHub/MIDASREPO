      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas GP Dealing WIP details')                         *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  LF/GPDLWIPA - Reversed deals that are on the WIP view.       *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR990939 *CREATE   Date 23Nov12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR990939 - Deal in complete status is not reflected in Midas *
      *             Dashboard if sent via API.  Fix is to introduce   *
      *             new LF to check WIP records for reversed deals.   *
      *             (Child: AR990940)                                 *
      *                                                               *
      *****************************************************************
     A          R T@MSWIP                   PFILE(T_MSWIP)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2012')
     A            MSWID
     A            MSWACT        30          TEXT('WIP Action')
     A            MSWTREF       15          TEXT('Transaction Ref')
     A            MSWAPI                    TEXT('API Name')
     A            MSWSTAT       32          TEXT('WIP Status')
     A            MSWZONE       10          TEXT('Zone')
      *
     A          K MSWTREF
     A          K MSWZONE                   DESCEND
      *
      ** Omit WIP Actions not equal to 'TR_REVERSE'
      *
     A          O MSWACT                    COMP(NE 'TR_REVERSE')
      *
      ** Select only those records that appear on a users WIP list
      *
     A          S MSWAPI                    VALUES('SIRS' 'CIRS' 'FRA ' +
     A                                      'CACF' 'LDNI' 'NASP' 'NASS' 'FXDL' +
     A                                      'DEAM')
     A            MSWSTAT                   COMP(NE 'AUTHORISED')
     A            MSWSTAT                   COMP(NE 'REJECTED')
     A            MSWSTAT                   COMP(NE 'NOT_APPROVED')
     A            MSWSTAT                   COMP(NE 'INCOMPLETE')
