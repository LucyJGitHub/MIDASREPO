     *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas GP Lending WIP details')                         *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  LF/GPLEWPL1 - Lending WIP Details                            *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. MD051428 *CREATE   Date 07Jul22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD051428 - Unauthorised Transaction Report LE2320P1 does not *
      *             report a reversal requiring authorisation of      *
      *             repayment schedule that is still in WIP. Include  *
      *             unauthorised reversed repayment schedule in the   *
      *             LE2320P1 report.                                  *
      *                                                               *
      *****************************************************************
     A          R T@MSWIP                   PFILE(T_MSWIP)
     A                                      TEXT('(c) Finastra Limited +
     A                                      International 2022')
     A
     A            MSWID                     TEXT('WIP Identifier')
     A            MSWACT        30          TEXT('WIP Action')
     A            MSWTREF       50          TEXT('Transaction Ref')
     A            MSWAPI        32          TEXT('API Name')
     A            MSWSTAT       32          TEXT('WIP Status')
     A            MSWZONE       10          TEXT('Zone')
      *
     A          K MSWTREF
     A          K MSWAPI
     A          K MSWZONE
      *
      ** Omit WIP Actions not equal to 'TR_REVERSE'
      *
     A          O MSWACT                    COMP(NE 'TR_REVERSE')
      *
      ** Select only those records that appear on a user's WIP list
      *
     A          S MSWAPI                    VALUES('CLIP' 'RPSC' 'LOAM' 'MAPY' +
     A                                             'PTPF' 'AIAJ' 'AMAJ' 'FEEM' +
     A                                             'FEAD' 'FEST' 'FCIP' 'FAMD')
     A            MSWSTAT                   COMP(NE 'AUTHORISED')
     A            MSWSTAT                   COMP(NE 'REJECTED')
     A            MSWSTAT                   COMP(NE 'NOT_APPROVED')
     A            MSWSTAT                   COMP(NE 'INCOMPLETE')
      *
