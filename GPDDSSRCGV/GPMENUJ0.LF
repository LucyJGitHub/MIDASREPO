      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas GP Global menu action codes by unique code')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  LF/GPMENUJ0 - Global Menu Action Codes by Unique Code        *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD055681           Date 31Jul20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                         *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD055681 - Deliverable Data Split for SAR                    *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP001 - Global Processing                                   *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @MENUJ0                   JFILE(GPMENUPD GPSARIPD +
      * Start of Change MD055681
     A**********                                  GPSARDPD GPMMIDPD)
     A                                            GPSRDXTD GPMMIDPD)
      * End of Change MD055681
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(MIINDX ADINDX)
      *
     A          J                           JOIN(2 3)
     A                                      JFLD(ADSARN SARN)
      *
     A          J                           JOIN(1 4)
     A                                      JFLD(MIMMSN FOMMSN)
      *
     A            MIINDX
     A            MISECL
     A            MIAAC1
     A            MIAAC2
     A            MIAAC3
     A            MIAAC4
     A            MIAAC5
     A            MIAAC6
     A            MIAAC7
     A            MIAAC8
     A            MIAAC9
     A            MIAAC10
     A            MIALLAC                   CONCAT(MIAAC1 MIAAC2 MIAAC3 +
     A                                      MIAAC4 MIAAC5 MIAAC6 MIAAC7 +
     A                                      MIAAC8 MIAAC9 MIAAC10)
      *
      * Fields included to allow join to function
      *
     A            ADINDX
     A            ADSARN
     A            ADAVAL
     A            SARN
     A            MIMMSN
     A            FOMMSN
     A            FOMMIN
      * Start of Change MD055681
     A            ENABLD
      * End of Change MD055681
      *
      * Key field - Unique Code
      *
     A          K MIINDX
      *
      * Selection
      *
      * Select if no SARI record and module is on and has action codes
     A          S ADSARN                    COMP(EQ ' ')
     A            FOMMIN                    COMP(EQ 'Y')
     A            MIALLAC                   COMP(NE '          ')
      *
      * Select if SARI record and feature present and show if present
      * and module is on and has action codes
     A          S ADSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
      * Start of Change MD055681
     A            ENABLD                    COMP(EQ 'Y')
      * End of Change MD055681
     A            ADAVAL                    COMP(EQ 'Y')
     A            FOMMIN                    COMP(EQ 'Y')
     A            MIALLAC                   COMP(NE '          ')
      *
      * Select if SARI record and feature not present and show if not
      * present and module is on and has action codes
     A          S ADSARN                    COMP(NE ' ')
     A            SARN                      COMP(EQ ' ')
     A            ADAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
     A            MIALLAC                   COMP(NE '          ')
