      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas Global menu action codes for User Securities')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  LF/GPMENUJ1 - Global Menu Action Codes for                   *
      *                User Securities                                *
      *                                                               *
      *  (c) Finastra International Limited 2006                      *
      *                                                               *
      *  Last Amend No. MD056923           Date 05Oct20               *
      *  Prev Amend No. MD055681           Date 31Jul20               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CRE026B *CREATE    Date 28Dec06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD056923 - Correct ENABLD selection                          *
      *  MD055681 - Deliverable Data Split for SAR                    *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE026B - User Security Profile                              *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @MENUJ1                   JFILE(GPMENUPD GPSARIPD +
      * Start of Change MD055681
     A**********                                  GPSARDPD GPMMIDPD +
     A                                            GPSRDXTD GPMMIDPD +
      * End of Change MD055681
     A                                            GPMTXTPD GPAMUSPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2006')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(MIINDX ADINDX)
      *
     A          J                           JOIN(2 3)
     A                                      JFLD(ADSARN SARN)
      *
     A          J                           JOIN(1 4)
     A                                      JFLD(MIMMSN FOMMSN)
      *
     A          J                           JOIN(1 5)
     A                                      JFLD(MIINDX MIINDX)
      *
     A          J                           JOIN(4 6)
     A                                      JFLD(FOMMIN DDACTN)
      *
     A            MIINDX                    JREF(1)
     A            MISECL
     A            MISNAM
     A            MIINAR
     A            ALLOWACT                  CONCAT(MIAAC1 MIAAC2 MIAAC3 +
     A                                      MIAAC4 MIAAC5 MIAAC6 MIAAC7 +
     A                                      MIAAC8 MIAAC9 MIAAC10)
      *
      * This field is added to facilitate the Java view to provide
      * with an extra field namely Selected Action Code
      *
     A            SELECTACT                 RENAME(DDUSRP)
     A                                      COLHDG('SELECTACT')
     A            MIDEL
      *
      * Fields included to allow join to function
      *
     A            ADINDX
     A            ADSARN
     A            ADAVAL
     A            SARN
     A            MIMMSN
     A            FOMMSN
     A            FOMMIN
      * Start of Change MD055681
     A            ENABLD
      * End of Change MD055681
      *
      * Key field - Unique Code
      *
     A          K MIINDX
      *
      * Selection
      *
      * Select if no SARI record and module is on and has action codes
     A          S ADSARN                    COMP(EQ ' ')
     A            FOMMIN                    COMP(EQ 'Y')
     A            ALLOWACT                  COMP(NE '          ')
     A            MIDEL                     COMP(NE 'Y')
      *
      * Select if SARI record and feature present and show if present
      * and module is on and has action codes
     A          S ADSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
      * Start of Change MD055681
      * Start of Change MD056923
     A**********  ENABLD                    COMP(EQ 'Y')
     A            ENABLD                    COMP(EQ 'E')
      * End of Change MD056923
      * End of Change MD055681
     A            ADAVAL                    COMP(EQ 'Y')
     A            FOMMIN                    COMP(EQ 'Y')
     A            ALLOWACT                  COMP(NE '          ')
     A            MIDEL                     COMP(NE 'Y')
      *
      * Select if SARI record and feature not present and show if not
      * present and module is on and has action codes
     A          S ADSARN                    COMP(NE ' ')
     A            SARN                      COMP(EQ ' ')
     A            ADAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
     A            ALLOWACT                  COMP(NE '          ')
     A            MIDEL                     COMP(NE 'Y')
