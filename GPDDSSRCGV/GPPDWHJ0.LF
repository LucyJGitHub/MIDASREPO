      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMWF                                                      *
/*EXI *  TEXT('Midas GP Midas system pulldown lists (headers)')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  LF/GPPDWHJ0 - Midas System Pulldown Lists (Headers)          *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD055681           Date 31Jul20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CUP101R            Date 21Oct05               *
      *                 CUP101             Date 11Jul05               *
      *                 BUG1662 *CREATE    Date 29Mar04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD055681 - Deliverable Data Split for SAR                    *
      *  MD046248 - Finastra Rebranding                               *
      *  CUP101R - This file should share format with GPPDWHPD.       *
      *  CUP101  - This file should share format with GZSDPDWHPD.     *
      *  BUG1662 - No Global pull down file                           *
      *                                                               *
      *****************************************************************
     A                                      UNIQUE
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @PDWHJ0                   JFILE(GPPDWHPD GPSARWPD +
      * Start of Change MD055681
     A**********                            GPSARDPD GPMMIDPD)
     A                                      GPSRDXTD GPMMIDPD)
      * End of Change MD055681
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(GHHIND GTHIND)
     A          J                           JOIN(2 3)
     A                                      JFLD(GTSARN SARN)
     A          J                           JOIN(1 4)
     A                                      JFLD(GHMMSN FOMMSN)
      *
     A            GHHIND
     A            GHMMID
     A            GLGRUP
     A            GHDESC
     A            GHMMSN
      *
     A            GTSARN
     A            FOMMIN
     A            GTAVAL
     A            SARN
     A            GTDIND
     A            GHZONE
      * Start of Change MD055681
     A            ENABLD
      * End of Change MD055681
      *
     A          K GHHIND
      *
      * Select if there is no associated switchability and the Midas
      * module is present
      *
     A          S GTSARN                    COMP(EQ ' ')
     A            FOMMIN                    COMP(EQ 'Y')
     A            GTDIND                    COMP(EQ 0)
      *
     A          S GTSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
      * Start of Change MD055681
     A            ENABLD                    COMP(EQ 'Y')
      * End of Change MD055681
     A            GTAVAL                    COMP(EQ 'Y')
     A            FOMMIN                    COMP(EQ 'Y')
     A            GTDIND                    COMP(EQ 0)
      *
     A          S GTSARN                    COMP(NE ' ')
     A            SARN                      COMP(EQ ' ')
     A            GTAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
     A            GTDIND                    COMP(EQ 0)
