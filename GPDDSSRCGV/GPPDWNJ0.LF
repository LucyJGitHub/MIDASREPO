      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMWF                                                      *
/*EXI *  TEXT('Midas GP Midas system pulldown lists (details)')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  LF/GPPDWNJ0 - Midas System Pulldown Lists (Details)          *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD056923           Date 05Oct20               *
      *  Prev Amend No. MD055681           Date 31Jul20               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CUP101R            Date 21Oct05               *
      *                 CUP101             Date 11Jul05               *
      *                 BUG1662 *CREATE    Date 29Mar04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD056923 - Correct ENABLD selection                          *
      *  MD055681 - Deliverable Data Split for SAR                    *
      *  MD046248 - Finastra Rebranding                               *
      *  CUP101R - This file should share format with GPPDWHPD.       *
      *  CUP101  - This file should share format with GZSDPDWHPD.     *
      *  BUG1662 - No Global pull down file                           *
      *                                                               *
      *****************************************************************
     A                                      UNIQUE
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @PDWNJ0                   JFILE(GPPDWNPD GPSARWPD +
      * Start of Change MD055681
     A**********                            GPSARDPD GPMMIDPD GPPDWHPD)
     A                                      GPSRDXTD GPMMIDPD GPPDWHPD)
      * End of Change MD055681
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(GLHIND GTHIND)
     A                                      JFLD(GLDIND GTDIND)
     A          J                           JOIN(2 3)
     A                                      JFLD(GTSARN SARN)
     A          J                           JOIN(1 4)
     A                                      JFLD(GLMMSN FOMMSN)
     A          J                           JOIN(1 5)
     A                                      JFLD(GLHIND GHHIND)
      *
     A            GHMMID
     A            GLGRUP
     A            GLHIND
     A            GLDIND
     A            GLLANG
     A            GLVDTA
     A            GLDESC
     A            GLMMSN
      *
     A            GTSARN
     A            FOMMIN
     A            GTAVAL
     A            SARN
     A            GLZONE
      * Start of Change MD055681
     A            ENABLD
      * End of Change MD055681
      *
     A          K GLHIND
     A          K GLDIND
      *
      * Select if there is no associated switchability and the Midas
      * module is present
      *
     A          S GTSARN                    COMP(EQ ' ')
     A            FOMMIN                    COMP(EQ 'Y')
      *
     A          S GTSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
      * Start of Change MD055681
      * Start of Change MD056923
     A**********  ENABLD                    COMP(EQ 'Y')                                    MD056923
     A            ENABLD                    COMP(EQ 'E')
      * End of Change MD056923
      * End of Change MD055681
     A            GTAVAL                    COMP(EQ 'Y')
     A            FOMMIN                    COMP(EQ 'Y')
      *
     A          S GTSARN                    COMP(NE ' ')
     A            SARN                      COMP(EQ ' ')
     A            GTAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
