      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMWF                                                      *
/*EXI *  TEXT('Midas GP Global Available Midas system values')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  LF/GPSVALJ0 - Available Midas GP Global System Values        *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGP001  *CREATE    Date 04Mar04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP001 - Global Processing                                   *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @SVALJ0                   JFILE(GPSVALPD GPSARVPD +
     A                                      GPSARDPD GPMMIDPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(GISVAL GSSVAL)
     A          J                           JOIN(2 3)
     A                                      JFLD(GSSARN SARN)
     A          J                           JOIN(1 4)
     A                                      JFLD(GIMMSN FOMMSN)
      *
     A            GISVAL
     A            GIMMOD
     A            GIMMSN
     A            GIDESC
     A            GIVAL
     A            GIDFT
     A            GIHELP
     A            GIATTR
     A            GIDLEN
     A            GISUPP
     A            GISWCH
     A            GIJUST
     A            GIALWB
     A            GIEMSG
     A            GILCD
     A            GILCT
      *
     A            GSSARN
     A            SARN
     A            GSAVAL
     A            FOMMIN
      *
     A          K GISVAL
      *
      * Select if there is no associated switchability and the Midas
      * module is present
      *
     A          S GSSARN                    COMP(EQ ' ')
     A            FOMMIN                    COMP(EQ 'Y')
      *
     A          S GSSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
     A            GSAVAL                    COMP(EQ 'Y')
     A            FOMMIN                    COMP(EQ 'Y')
     A          S GSSARN                    COMP(NE ' ')
     A            SARN                      COMP(EQ ' ')
     A            GSAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
