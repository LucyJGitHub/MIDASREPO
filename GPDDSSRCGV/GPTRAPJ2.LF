      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas GP Joined File for GPTRAPPD and GPCASHPD')       *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing                                    *
      *                                                               *
      *  LF/GPTRAPJ2 - Joined File for GPTRAPPD and GPCASHPD          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER045  *CREATE    Date 20Jul09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank              *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R GPTRPSJ2                  JFILE(GPTRAPPD GPCASHPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2009')
     A          J                           JOIN(GPTRAPPD GPCASHPD)
     A                                      JFLD(T_ID     C_ID)
      *
     A            T_ZONE                    TEXT('Zone')
     A                                      COLHDG('Zone')
     A            T_SMOD                    TEXT('Sub-Module')
     A                                      COLHDG('Sub-Mod')
     A            T_TR6A                    TEXT('Transaction Ref (6 Alpha)')
     A                                      COLHDG('Tran Ref')
     A            C_DATE                    TEXT('Cashflow Date')
     A                                      COLHDG('Cashflow' 'Date')
     A            C_EVTP                    TEXT('Event Type')
     A                                      COLHDG('Event' 'Type')
     A            C_CATG                    TEXT('Cashflow Category')
     A                                      COLHDG('Cashflow' 'Category')
     A            C_IO                      TEXT('Cashflow In/Out')
     A                                      COLHDG('Cashflow' 'In/Out')
     A            C_AMNT                    TEXT('Cashflow Amount')
     A                                      COLHDG('Cashflow' 'Amount')
      *
     A          K T_ZONE
     A          K T_SMOD
     A          K T_TR6A
      *
     A          O C_CATG                    VALUES('I' 'C')
     A          O C_AMNT                    COMP(EQ 0)
      *
     A          S T_SMOD                    VALUES('LDNI' 'LOAN')
      *
