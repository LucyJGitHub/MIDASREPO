      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMWF                                                      *
/*EXI *  TEXT('Midas GP Active trigger pgms by file/time/typ/seq')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  LF/GPTRIGJ0 - Active Trigger Pgms by File/Time/Type/Seq      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CUP029             Date 21Oct04               *
      *  Prev Amend No. CGP001  *CREATE    Date 09Mar04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP029 - Change to join GPSARTPD on File/Time/Event/Seq.     *
      *  CGP001 - Global Processing Project:                          *
      *           Required to add triggers to global files            *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @TRIGJ0                   JFILE(GPTRIGPD GPSARTPD +
     A                                      GPSARDPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A          J                           JOIN(1 2)
      **Start of Change CUP029
     A**********                            JFLD(TRUNQI GUUNQI)
     A                                      JFLD(TRFILE GUFILE)
     A                                      JFLD(TRTIME GUTIME)
     A                                      JFLD(TREVNT GUEVNT)
     A                                      JFLD(TRSEQ  GUSEQ)
      **End of Change CUP029
     A          J                           JOIN(2 3)
     A                                      JFLD(GUSARN SARN)
      *
     A            TRACTV
     A            TRFILE
     A            TRTIME
     A            TREVNT
     A            TRSEQ
     A            TRPGM
     A            TRPGML
     A            TRUNQI
     A            TRMMSN
      *
     A            GUSARN
     A            SARN
     A            GUAVAL
      *
     A          K TRFILE
     A          K TRTIME
     A          K TREVNT
     A          K TRSEQ
      *
      * Select if there is no associated switchability and the Midas
      * module is present
      *
     A          S GUSARN                    COMP(EQ ' ')
      *
     A          S GUSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
     A            GUAVAL                    COMP(EQ 'Y')
      *
     A          S GUSARN                    COMP(NE ' ')
     A            SARN                      COMP(EQ ' ')
     A            GUAVAL                    COMP(EQ 'N')
