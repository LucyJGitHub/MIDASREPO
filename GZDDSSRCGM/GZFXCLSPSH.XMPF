      *****************************************************************
/*S*D ***PFBASE********************************************************                       CGP016
/*E*S ***PF_STD********************************************************                       CGP016
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZFXCLSPSH - CLS Pay-in Schedule Header                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CGP016  *MOVED     Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAP086             Date 08Jun05               *
      *                 CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved to GZDDSSRCGP                                 *
      *  CAP086 - Introduce Auto Authorisation to the API's           *
      *           without it.                                         *
      *  CGP001 - Global Processing                                   *
      *           - New file - Global version of FXCLSPSH.            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R FXCLPHD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            PYRECI         1          TEXT('Record Id')
     A                                      COLHDG('Record Id')
     A            PYCCY          3          TEXT('CLS Currency')
     A                                      COLHDG('CLS Currency')
     A            PYNOST         2          TEXT('CLS Dummy Nostro')
     A                                      COLHDG('CLS Dummy Nostro')
     A            PYVALD         5P 0       TEXT('Value Date')
     A                                      COLHDG('Value Date')
     A            PYSEQN         2S 0       TEXT('CLS Sequence number')
     A                                      COLHDG('CLS Sequence' +
     A                                             'number')
     A            PYTIMD         5          TEXT('CLS Time diff. from +
     A                                            CET')
     A                                      COLHDG('CLS Time diff.' +
     A                                             'from CET')
     A            PYPRFC         4          TEXT('CLS Profit Centre')
     A                                      COLHDG('CLS Profit Centre')
     A            PYTOTP        17  0       TEXT('CLS Total Amount +
     A                                            (Pay)')
     A                                      COLHDG('CLS Total Amount' +
     A                                             '(Pay)')
     A            PYTOTR        17  0       TEXT('CLS Total Amount +
     A                                            (Receive)')
     A                                      COLHDG('CLS Total Amount' +
     A                                             '(Receive)')
     A            PYNSBP        17  0       TEXT('CLS Nostro Balance +
     A                                            Total (Pay)')
     A                                      COLHDG('CLS Nostro' +
     A                                             'Balance (Pay)')
     A            PYNSBR        17  0       TEXT('CLS Nostro Balance +
     A                                            Total (Receive)')
     A                                      COLHDG('CLS Nostro' +
     A                                             'Balance (Receive)')
     A            PYSNTF         1          TEXT('CLS Authorise +
     A                                           Pay-in Schedule Flag')
     A                                      COLHDG('CLS Authorise' +
     A                                          'Pay-in Schedule Flag')
     A            PYPOST         1          TEXT('Amount Posted')
     A                                      COLHDG('Amount Posted')
     A            PYTIMS    R               TEXT('Timestamp')
     A                                      REFFLD(TIMESTMP)
     A            PYUSER        10          TEXT('Inserting/ +
     A                                      Amending User')
     A                                      COLHDG('Inserting/' +
     A                                           'Amending User')
     A            FXZONE        10A         TEXT('Zone')
     A                                      COLHDG('Zone')
     A**Start of change CAP086
     A            PYAUTH         1A         TEXT('Automatic Authorisation')
     A                                      COLHDG('Automatic' 'Authorisation')
     A**End of change CAP086
