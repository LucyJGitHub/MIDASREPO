      *****************************************************************
/*S*D ***PFBASE********************************************************                       CGP016
/*E*S ***PF_STD********************************************************                       CGP016
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZGLCOLAPD - Collateral Allocation Details                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CGP016  *MOVED     Date 06Aug12               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG11623           Date 27Jun06               *
      *                 CSD027             Date 05Dec05               *
      *                 CLE040             Date 05Jul04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved to GZDDSSRCGP                                 *
      *  CLE148 - Alpha Loan Reference                                *
      *  BUG11623 - Sequence No. column should in included            *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE040 - Collateralised Lending Phase 2                      *
      *  CLE025 - Credit Lines                                        *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGP001 - Global Processing                                   *
      *           - New file - Global version of GLCOLAPD.            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R GLCOLAD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A            CACREF        10          TEXT('Collateral +
     A                                      Reference')
     A                                      COLHDG('Collateral' +
     A                                             'Reference')
     A            CAFRNT        20          TEXT('Front Office +
     A                                      Transaction Id')
     A                                      COLHDG('Front Office' +
     A                                             'Transaction Id')
     A            CAREPA         1          TEXT('Repair Location')
     A                                      COLHDG('Repair Location')
     A            CATMST        26          TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
     A            CABRCA         3          TEXT('Allocation Branch +
     A                                      Code')
     A                                      COLHDG('Allocation' +
     A                                             'Branch Code')
      * Begin /CSD027/
     A**********  CACNUM         6S 0       TEXT('Collateral +                                CSD027
     A            CACNUM         6A         TEXT('Collateral +
      * End /CSD027/
     A                                      Allocation Customer')
     A                                      COLHDG('Collateral' +
     A                                             'Allocation' +
     A                                             'Customer')
     A            CAAPCT         1          TEXT('Allocation Amount/ +
     A                                      Percentage Indicator')
     A                                      COLHDG('Allocation' +
     A                                             'Amt/Percentage' +
     A                                             'Indicator')
     A            CAPCNT         5P 2       TEXT('Allocation +
     A                                      Percentage')
     A                                      COLHDG('Allocation' +
     A                                             'Percentage')
     A            CAAMT         15P 0       TEXT('Allocation Amount')
     A                                      COLHDG('Allocation Amount')
     A            CAPRTY         3S 0       TEXT('Allocation Priority')
     A                                      COLHDG('Allocation' +
     A                                             'Priority')
     A            CAMODS         2          TEXT('Module Secured')
     A                                      COLHDG('Module Secured')
      * Begin /CSD027/
     A**********  CAFCUS         6S 0       TEXT('Secured Facility +                          CSD027
     A            CAFCUS         6A         TEXT('Secured Facility +
      * End /CSD027/
     A                                      Customer')
     A                                      COLHDG('Secured Facility' +
     A                                             'Customer')
     A            CAFTYP         3S 0       TEXT('Secured Facility +
     A                                      Type')
     A                                      COLHDG('Secured Facility' +
     A                                             'Type')
     A            CAFSEQ         2S 0       TEXT('Secured Facility +
     A                                      Sequence Number')
     A                                      COLHDG('Secured Facility' +
     A                                             'Sequence Number')
      **Start of change CLE148
     A**********  CALNLM         6S 0       TEXT('Secured Loan/Limit')                        CLE148
     A            CALNLM         6A         TEXT('Secured Loan/Limit')
      **End of change CLE148
     A                                      COLHDG('Secured' +
     A                                             'Loan/Limit')
     A            CASBRC         3          TEXT('Secured Account +
     A                                      Branch')
     A                                      COLHDG('Secured Account' +
     A                                             'Branch')
      * Begin /CSD027/
     A**********  CASCNM         6S 0       TEXT('Secured Account +                           CSD027
     A            CASCNM         6A         TEXT('Secured Account +
      * End /CSD027/
     A                                      Customer Number')
     A                                      COLHDG('Secured Account' +
     A                                             'Customer Number')
     A            CASCCY         3          TEXT('Secured Account +
     A                                      Currency')
     A                                      COLHDG('Secured Account' +
     A                                             'Currency')
     A**Start of Change CGL029
     A**********  CASACD         4S 0       TEXT('Secured Account +                           CGL029
     A**********                            Code')                                            CGL029
     A**********                            COLHDG('Secured Account' +                        CGL029
     A**********                                   'Code')                                    CGL029
     A            QQSACD    R               REFFLD(ACODQQ)
     A**End of Change CGL029
     A            CASSEQ         2S 0       TEXT('Secured Account +
     A                                      Sequence Number')
     A                                      COLHDG('Secured Account' +
     A                                             'Sequence Number')
     A            CASACN        10S 0       TEXT('Secured Retail +
     A                                      Account Number')
     A                                      COLHDG('Secured Retail' +
     A                                             'Account Number')
     A            CALCD          5P 0       TEXT('Last Change Date')
     A                                      COLHDG('Last Change Date')
     A            CACHTP         1          TEXT('Type of Last Change')
     A                                      COLHDG('Type of Last' +
     A                                             'Change')
     A            CAZONE        10A         TEXT('Zone')
     A                                      COLHDG('Zone')
     A**Start of Change CGL029
     A            CASACD    R               REFFLD(ACOD)
     A                                      TEXT('Secured Account +
     A                                      Code')
     A                                      COLHDG('Secured Account' +
     A                                             'Code')
     A**End of Change CGL029
      **Start of change CLE025
     A            CAACMT        15P 0       TEXT('Actual Allocation Amount')
     A                                      COLHDG('Actual Alloc Amount')
     A            CATSTP          Z         TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
      **End of change CLE025
      **Start of change CLE040
     A            CAFBRC         3A         TEXT('Secured Facility Branch')
     A                                      COLHDG('Secured Facility' +
     A                                             'Branch')
      **End of change CLE040
      **Start of change BUG11623
     A            CASEQN         2S 0       TEXT('Sequence Number')
     C                                      COLHDG('Seq No.')
      **End of change BUG11623
