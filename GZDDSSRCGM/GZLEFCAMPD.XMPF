      *****************************************************************
/*S*D ***PFBASE********************************************************                       CGP016
/*E*S ***PF_STD********************************************************                       CGP016
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZLEFCAMPD - Facility Amendment Transaction File          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CGP016  *MOVED     Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CAP086             Date 08Jun05               *
      *                 CLE106             Date 01Aug04               *
      *                 CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved to GZDDSSRCGP                                 *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CAP086 - Introduce Auto Authorisation to the API's           *
      *           without it.                                         *
      *  CLE106 - Syndications Manager.                               *
      *  CGP001 - Global Processing                                   *
      *           - New file - Global version of LEFCAMPD.            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LEFCAMPF                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A            RECI      R
     A            BRCA      R
      * Begin /CSD027/
     A**********  CNUM      R               TEXT('Facility Customer +                         CSD027
     A            CNUM           6A         TEXT('Facility Customer +
      * End /CSD027/
     A                                            Number')
     A                                      COLHDG('Fclty Cust' +
     A                                             'Number')
     A            FACT      R
     A            FCNO      R
     A            SQNO      R               REFFLD(FACT)
     A                                      TEXT('Amendment Sequence +
     A                                            Number')
     A                                      COLHDG('Amend Seq.' +
     A                                             'Number')
     A            VLDT      R
     A            FATP      R               REFFLD(AMTP)
     A                                      TEXT('Facility Amend Type')
     A                                      COLHDG('Fclty Amend' +
     A                                             'Type')
     A            ACCY      R               TEXT('Amendment Currency')
     A                                      COLHDG('Amend' 'Ccy')
     A            AAMT      R               TEXT('Amendment Amount')
     A                                      COLHDG('Amend' 'Amt')
     A            RVCR      R
     A            NDEX      R               REFFLD(MFED)
     A                                      TEXT('New Expiry Date')
     A                                      COLHDG('New Expiry' +
     A                                             'Date')
     A            ASTS      R               TEXT('Record Status')
     A                                      COLHDG('Rec' 'Sts')
     A            IUSR      R               REFFLD(RSTU)
     A                                      TEXT('Insert User')
     A                                      COLHDG('Insert' 'User')
     A            AUSR      R               REFFLD(RSTU)
     A                                      TEXT('Amend User')
     A                                      COLHDG('Amend' 'User')
     A            XUSR      R               REFFLD(RSTU)
     A                                      TEXT('Authorise User')
     A                                      COLHDG('Auth.' 'User')
     A            ORED      R
     A            TLCD      R               TEXT('Transaction Last +
     A                                            Change Date')
     A                                      COLHDG('Trans Last' +
     A                                             'Chnge Date')
     A            CHTP      R
     A            PCRF      R               REFFLD(ZZ015)
     A                                      TEXT('PC Transaction +
     A                                            Reference')
     A                                      COLHDG('PC Trans' +
     A                                             'Reference')
     A            PCOB      R               REFFLD(BRCA)
     A                                      TEXT('PC Originating +
     A                                            Branch')
     A                                      COLHDG('PC Orig' +
     A                                             'Branch')
     A            GASSA     R               REFFLD(SPIN)
     A                                      TEXT('Generated by +
     A                                            Assignment')
     A                                      COLHDG('Gen by' +
     A                                             'Assgn?')
      *
     A            AUTH      R               REFFLD(CCIO)
     A                                      TEXT('Authorised Indicator')
     A                                      COLHDG('Auth' 'Id')
      *
     A            SWRI      R               REFFLD(CCIO)
     A                                      TEXT('Fee Regeneration +
     A                                            Indicator')
     A                                      COLHDG('Fee' 'Regen' 'Id')
     A            FRNT      R               REFFLD(FOTRNID)
     A            AFRT      R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            REPA      R               REFFLD(RPRLOC)
      ** Timestamp
     A            STMP      R               REFFLD(TIMESTMP)
     A**Start of change CAP086
     A            AUTO           1A         TEXT('Automatic Authorisation')
     A                                      COLHDG('Automatic' 'Authorisation')
     A**End of change CAP086
      **Start of change CLE106
     A            NPRT           1A         TEXT('Non-Pro Rata Indicator')
     A                                      COLHDG('Non-Pro Rata Ind')
      **End of change CLE106
