      *****************************************************************
/*S*D ***PFBASE********************************************************                       CGP016
/*E*S ***PF_STD********************************************************                       CGP016
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZLEFCLTQD - Facilities Extension File                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CGP016  *MOVED     Date 06Aug12               *
      *  Prev Amend No. CLE147             Date 11Oct11               *
      *                 CER049             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAP185             Date 21Apr06               *
      *                 CLE042             Date 06Sep05               *
      *                 CLE106             Date 01Aug04               *
      *                 CLE041             Date 19Oct04               *
      *                 CLE040             Date 05Jul04               *
      *                 CLE039  *CREATE    Date 20Oct03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved to GZDDSSRCGP                                 *
      *  CLE147 - Aggregate Facility                                  *
      *  CER049 - Stamp Tax                                           *
      *  CAP185 - MQ Series Interface                                 *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CLE106 - Syndications Manager. Additional fields.            *
      *  CLE041 - Collateralised Lending Phase 2B                     *
      *  CLE040 - Collateralised Lending Phase 2                      *
      *  CLE039 - General LE Enhancement                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R LEFCLTD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A            FCXBRC         3A         TEXT('Branch')
     A                                      COLHDG('Branch')
     A            FCXFCU         6A         TEXT('Customer Number')
     A                                      COLHDG('Customer' 'Number')
     A            FCXFTP         3A         TEXT('Facility Type')
     A                                      COLHDG('Facility' 'Type')
     A            FCXFNO         2A         TEXT('Facility Number')
     A                                      COLHDG('Facility' 'Number')
     A            FCXPOR         4S 0       TEXT('Portfolio')
     A                                      COLHDG('Portfolio')
     A            FCXLVR         3S 0       TEXT('Required Loan to Value Ratio')
     A                                      COLHDG('Required' 'Loan to Value' +
     A                                      'Ratio')
     A            FCXLVA         3S 0       TEXT('Loan to Value Ratio +
     A                                      Alert Level')
     A                                      COLHDG('Loan to Value' 'Ratio' +
     A                                      'Alert Level')
     A            FCXLVW         3S 0       TEXT('Loan to Value Warning Level')
     A                                      COLHDG('Loan to Value' 'Ratio' +
     A                                      'Warning Level')
     A            FCXALV         3S 0       TEXT('Advance Loan to Value Ratio')
     A                                      COLHDG('Advance' 'Loan to Value' +
     A                                      'Ratio')
     A            FCXOML        13P 0       TEXT('Overall Margin Dealing Limit')
     A                                      COLHDG('Overall Margin' +
     A                                      'Dealing Limit')
     A            FCXMST         3A         TEXT('FX Margin Type')
     A                                      COLHDG('FX' 'Margin' 'Type')
     A            FCXSTC         1A         TEXT('Subject to Call')
     A                                      COLHDG('Subject' 'to' 'Call')
     A            FCXPWF         3S 0       TEXT('Percentage over Required +
     A                                      LTV for First Call')
     A                                      COLHDG('Percentage over' +
     A                                      'Required LTV' +
     A                                      'for First Call')
     A            FCXPIS         3S 0       TEXT('Percentage Increment for +
     A                                      Subsequent Call')
     A                                      COLHDG('Percentage' +
     A                                      'Increment for' 'Subsequent Call')
     A            FCXDEA         1A         TEXT('Drawdown Cannot Exceed +
     A                                      Available')
     A                                      COLHDG('Drawdown' 'Cannot Exceed' +
     A                                      'Available')
     A            FCXCGP        20P 0       TEXT('Customer Group')
     A                                      COLHDG('Customer' 'Group')
     A            FCXCOF        13P 0       TEXT('Current Offset Amount')
     A                                      COLHDG('Current' 'Offset' 'Amount')
     A            FCXOF1        13P 0       TEXT('Offsets Day 1')
     A                                      COLHDG('Offsets' 'Day 1')
     A            FCXOF2        13P 0       TEXT('Offsets Day 2')
     A                                      COLHDG('Offsets' 'Day 2')
     A            FCXOF3        13P 0       TEXT('Offsets Day 3')
     A                                      COLHDG('Offsets' 'Day 3')
     A            FCXOF4        13P 0       TEXT('Offsets Day 4')
     A                                      COLHDG('Offsets' 'Day 4')
     A            FCXOF5        13P 0       TEXT('Offsets Day 5')
     A                                      COLHDG('Offsets' 'Day 5')
     A            FCXOF6        13P 0       TEXT('Offsets Day 6')
     A                                      COLHDG('Offsets' 'Day 6')
     A            FCXOF7        13P 0       TEXT('Offsets Day 7')
     A                                      COLHDG('Offsets' 'Day 7')
     A            FCXOF8        13P 0       TEXT('Offsets Day 8')
     A                                      COLHDG('Offsets' 'Day 8')
     A            FCXOF9        13P 0       TEXT('Offsets Day 9')
     A                                      COLHDG('Offsets' 'Day 9')
     A            FCXO10        13P 0       TEXT('Offsets Day 10')
     A                                      COLHDG('Offsets' 'Day 10')
     A            FCXCV1        15P 0       TEXT('Collateral Lending +
     A                                      Value Day 1')
     A                                      COLHDG('Collateral' +
     A                                      'Lending Value' 'Day 1')
     A            FCXCV2        15P 0       TEXT('Collateral Lending +
     A                                      Value Day 2')
     A                                      COLHDG('Collateral' +
     A                                      'Lending Value' 'Day 2')
     A            FCXCV3        15P 0       TEXT('Collateral Lending +
     A                                      Value Day 3')
     A                                      COLHDG('Collateral' +
     A                                      'Lending Value' 'Day 3')
     A            FCXCV4        15P 0       TEXT('Collateral Lending +
     A                                      Value Day 4')
     A                                      COLHDG('Collateral' +
     A                                      'Lending Value' 'Day 4')
     A            FCXCV5        15P 0       TEXT('Collateral Lending +
     A                                      Value Day 5')
     A                                      COLHDG('Collateral' +
     A                                      'Lending Value' 'Day 5')
     A            FCXCV6        15P 0       TEXT('Collateral Lending +
     A                                      Value Day 6')
     A                                      COLHDG('Collateral' +
     A                                      'Lending Value' 'Day 6')
     A            FCXCV7        15P 0       TEXT('Collateral Lending +
     A                                      Value Day 7')
     A                                      COLHDG('Collateral' +
     A                                      'Lending Value' 'Day 7')
     A            FCXCV8        15P 0       TEXT('Collateral Lending +
     A                                      Value Day 8')
     A                                      COLHDG('Collateral' +
     A                                      'Lending Value' 'Day 8')
     A            FCXCV9        15P 0       TEXT('Collateral Lending +
     A                                      Value Day 9')
     A                                      COLHDG('Collateral' +
     A                                      'Lending Value' 'Day 9')
     A            FCXC10        15P 0       TEXT('Collateral Lending +
     A                                      Value Day 10')
     A                                      COLHDG('Collateral' +
     A                                      'Lending Value' 'Day 10')
     A            FCXMR1        13P 0       TEXT('FX Margin Requirement +
     A                                      Day 1')
     A                                      COLHDG('FX Margin' +
     A                                      'Requirement' 'Day 1')
     A            FCXMR2        13P 0       TEXT('FX Margin Requirement +
     A                                      Day 2')
     A                                      COLHDG('FX Margin' +
     A                                      'Requirement' 'Day 2')
     A            FCXMR3        13P 0       TEXT('FX Margin Requirement +
     A                                      Day 3')
     A                                      COLHDG('FX Margin' +
     A                                      'Requirement' 'Day 3')
     A            FCXMR4        13P 0       TEXT('FX Margin Requirement +
     A                                      Day 4')
     A                                      COLHDG('FX Margin' +
     A                                      'Requirement' 'Day 4')
     A            FCXMR5        13P 0       TEXT('FX Margin Requirement +
     A                                      Day 5')
     A                                      COLHDG('FX Margin' +
     A                                      'Requirement' 'Day 5')
     A            FCXMR6        13P 0       TEXT('FX Margin Requirement +
     A                                      Day 6')
     A                                      COLHDG('FX Margin' +
     A                                      'Requirement' 'Day 6')
     A            FCXMR7        13P 0       TEXT('FX Margin Requirement +
     A                                      Day 7')
     A                                      COLHDG('FX Margin' +
     A                                      'Requirement' 'Day 7')
     A            FCXMR8        13P 0       TEXT('FX Margin Requirement +
     A                                      Day 8')
     A                                      COLHDG('FX Margin' +
     A                                      'Requirement' 'Day 8')
     A            FCXMR9        13P 0       TEXT('FX Margin Requirement +
     A                                      Day 9')
     A                                      COLHDG('FX Margin' +
     A                                      'Requirement' 'Day 9')
     A            FCXM10        13P 0       TEXT('FX Margin Requirement +
     A                                      Day 10')
     A                                      COLHDG('FX Margin' +
     A                                      'Requirement' 'Day 10')
     A            FCXLVL         1A         TEXT('LTV Level')
     A                                      COLHDG('LTV' 'Level')
      **Start of change CLE041
     A**********  FCXCLV         5S 2       TEXT('Current LTV')                               CLE041
     A**********                            COLHDG('Current' 'LTV')                           CLE041
     A**********  FCXPLV         5S 2       TEXT('Previous LTV')                              CLE041
     A**********                            COLHDG('Previous' 'LTV')                          CLE041
     A            FCXCLV        15P 2       TEXT('Current LTV')
     A                                      COLHDG('Current' 'LTV')
     A            FCXPLV        15P 2       TEXT('Previous LTV')
     A                                      COLHDG('Previous' 'LTV')
      **End of change CLE041
     A            FCXCMI         1A         TEXT('Collateral Call Made +
     A                                      Indicator')
     A                                      COLHDG('Collateral' 'Call Made' +
     A                                      'Indicator')
     A            FCTSTP          Z         TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
      **Start of change CLE040
     A            FCXCCD         5P 0       TEXT('Collateral Call +
     A                                      Generated Date')
     A                                      COLHDG('Collateral Call' +
     A                                      'Generated' 'Date')
     A            FCXNCG         3S 0       TEXT('Number of Calls +
     A                                      Already Generated')
     A                                      COLHDG('Number of Calls' +
     A                                      'Already' 'Generated')
      **End of change CLE040
      **Start of Change CLE106
     A            FCXAFA        13P 0       TEXT('Agent Fee Amount')
     A                                      COLHDG('Agent Fee Amount')
     A            FCXFAS         1A         TEXT('Agent Fee Amount Sign')
     A                                      COLHDG('Agent Fee' +
     A                                      'Amount Sign')
     A            FCXAFC         2A         TEXT('Agent Fee Code')
     A                                      COLHDG('Agent Fee Code')
     A            FCXSLT         2A         TEXT('Sub-participation Loan Type')
     A                                      COLHDG('Sub-participation' +
     A                                      'Loan Type')
     A            FCXSLS         2A         TEXT('Sub-participation Loan STyp')
     A                                      COLHDG('Sub-participation' +
     A                                      'Loan STyp')
     A            FCXFBC         2S 0       TEXT('Fee Base Rate Code')
     A                                      COLHDG('Fee Base Rate Code')
     A            FCXFBR        11P 7       TEXT('Fee Base Rate')
     A                                      COLHDG('Fee Base Rate')
     A            FCXFSR        11P 7       TEXT('Fee Spread Rate')
     A                                      COLHDG('Fee Spread Rate')
     A            FCXSRS         1A         TEXT('Fee Spread Rate Sign')
     A                                      COLHDG('Fee Spread Rate Sign')
     A            FCXFFR        11P 7       TEXT('Fee Fixed Rate')
     A                                      COLHDG('Fee Fixed Rate')
     A            FCXRLT         2A         TEXT('Risk Participation Loan Type')
     A                                      COLHDG('Risk Participation' +
     A                                      'Loan Type')
     A            FCXRLS         2A         TEXT('Risk Participation SubType')
     A                                      COLHDG('Risk Participation' +
     A                                      'SubType')
     A            FCXPFR        11P 7       TEXT('Late Pay Penalty Fixed Rate')
     A                                      COLHDG('Late Pay Penalty' +
     A                                      'Fixed Rate')
     A            FCXPRT        11P 7       TEXT('Late Pay Penalty Base Rate')
     A                                      COLHDG('Late Pay Penalty' +
     A                                      'Base Rate')
     A            FCXPSR        11P 7       TEXT('Late Pay Penalty Sprd Rate')
     A                                      COLHDG('Late Pay Penalty' +
     A                                      'Sprd Rate')
     A            FCXAMT        13P 0       TEXT('Current Facility Amount +
     A                                      Due to Assignment')
     A                                      COLHDG('Current Facility ' +
     A                                      'Amount Due to ' +
     A                                      'Assignment')
      **End of Change CLE106
     A*                                              Begin /CLE042/
     A            FCXSLC         4A         TEXT('Sub-participation Loan Class')
     A                                      COLHDG('Sub-participation' +
     A                                      'Loan Class')
     A            FCXRLC         4A         TEXT('Risk Participation Class')
     A                                      COLHDG('Risk Participation' +
     A                                      'Class')
     A*                                              End   /CLE042/
      **Start of change CAP185
     A            FCFRID        20A         TEXT('Front office ID')
     A                                      COLHDG('Front' +
     A                                             'ofc ID')
      **End of change CAP185
      **Start of change CER049
     A            FCTAX          1A         TEXT('Stamp Tax Liable')
     A                                      COLHDG('Stamp Tax' +
     A                                             'Liable')
     A            FCPRIN         1A         TEXT('Prin Tax Flag')
     A                                      COLHDG('Prin Tax' +
     A                                             'Flag')
     A            FCPDTE         5P 0       TEXT('Prin Tax Start Date')
     A                                      COLHDG('Prin Tax' +
     A                                             'Start Date')
     A            FCDBRC         3A         TEXT('Debit A/c Branch')
     A                                      COLHDG('Debit A/c' +
     A                                             'Branch')
     A            FCDCST         6A         TEXT('Debit A/c Cust.')
     A                                      COLHDG('Debit A/c' +
     A                                             'Cust.')
     A            FCDCCY         3A         TEXT('Debit A/c Ccy.')
     A                                      COLHDG('Debit A/c' +
     A                                             'Ccy.')
     A            FCDACD        10S 0       TEXT('Debit A/c Code')
     A                                      COLHDG('Debit A/c' +
     A                                             'Code')
     A            FCDASC         2S 0       TEXT('Debit A/c Seq.')
     A                                      COLHDG('Debit A/c' +
     A                                             'Seq.')
     A            FCSTAF        13P 0       TEXT('Stamp Tax Amt.')
     A                                      COLHDG('Stamp Tax' +
     A                                             'Amount')
     A            FCCDTE         5P 0       TEXT('Control Date')
     A                                      COLHDG('Control' +
     A                                             'Date')
      **End of change CER049
      **Start of change CLE147
     A            FCOAFL         1A         TEXT('Previous CA +
     A                                            Change Flag')
     A                                      COLHDG('Prev CA Change' +
     A                                             'Flag')
     A            FCOANM         6A         TEXT('Previous CA +
     A                                            Customer')
     A                                      COLHDG('Prev CA Customer')
     A            FCOAFT         3A         TEXT('Previous CA +
     A                                            Facility Type')
     A                                      COLHDG('Prev CA Facility' +
     A                                              'Type')
     A            FCOAFN         2A         TEXT('Previous CA +
     A                                            Facility No.')
     A                                      COLHDG('Prev CA Facility' +
     A                                              'No.')
     A            FCCHFL         1A         TEXT('Credit Agreement +
     A                                            Change Flag')
     A                                      COLHDG('CA Change' +
     A                                             'Flag')
     A            FCCANM         6A         TEXT('Credit Agreement +
     A                                            Customer')
     A                                      COLHDG('CA Customer')
     A            FCCAFT         3A         TEXT('Credit Agreement +
     A                                            Facility Type')
     A                                      COLHDG('CA Facility' +
     A                                              'Type')
     A            FCCAFN         2A         TEXT('Credit Agreement +
     A                                            Facility No.')
     A                                      COLHDG('CA Facility' +
     A                                              'No.')
     A            FCAXTN         1A         TEXT('Attach / Detach +
     A                                            Code')
     A                                      COLHDG('Attach / Detach' +
     A                                             'Code')
      **End of change CLE147
