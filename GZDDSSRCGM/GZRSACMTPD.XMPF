      *****************************************************************
/*S*D ***PFBASE********************************************************                       CGP016
/*E*S ***PF_RUNM*******************************************************                       CGP016
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZRSACMTPD - Midas GZ Account Movements File              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. CGP016  *MOVED     Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CAP203  *CREATE    Date 02Feb10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved to GZDDSSRCGP                                 *
      *  CAP203 - Teller Related APIs - Account Movements             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R RSACMTPO                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2010')
      *
     A            CUSN           6A         TEXT('Customer Number')
     A                                      COLHDG('Cust' +
                                                   'No.')
     A            CCYD           3A         TEXT('Currency Code')
     A                                      COLHDG('CCY' +
     A                                             'Code')
     A            ACDEQQ         4S 0       TEXT('Old Account code')
     A                                      COLHDG('Old' +
     A                                             'Acc.' +
     A                                             'Code')
     A            ASNC           2S 0       TEXT('Account Sequence')
     A                                      COLHDG('A/C' +
     A                                             'Seq.')
     A            BRCA           3A         TEXT('Branch Code - Alpha')
     A                                      COLHDG('Brch' +
     A                                             'Code')
     A            PTDT           5P 0       TEXT('Posting Date')
     A                                      COLHDG('Post' +
     A                                             'Date')
     A            VUDT           5P 0       TEXT('Value Date')
     A                                      COLHDG('Val' +
     A                                             'Date')
     A            TRYP           2A         TEXT('Transaction Type')
     A                                      COLHDG('Tran' +
     A                                             'Type')
     A            NRTC           2A         TEXT('Narrative Code')
     A                                      COLHDG('Narr' +
     A                                             'Code')
     A            NRTD          30A         TEXT('Narrative Description')
     A                                      COLHDG('Narr' +
     A                                             'Desc')
     A            TNMR           6A         TEXT('Transaction Number')
     A                                      COLHDG('Tran' +
     A                                             'No.')
     A            CQNR           8S 0       TEXT('Cheque Number')
     A                                      COLHDG('Cheq' +
     A                                             'No.')
     A            MVAM          13P 0       TEXT('Movement Amount')
     A                                      COLHDG('Movt' +
     A                                             'Amnt')
     A            DORC           1S 0       TEXT('Debit or Credit (0 or 1)')
     A                                      COLHDG('DR/' +
     A                                             'CR')
     A            FUNDQQ        15A
     A            TBID           3A         TEXT('Teller ID/Batch No.')
     A                                      COLHDG('Tellr' +
     A                                             'ID/Bt' +
     A                                             'No.')
     A            APBI           1A         TEXT('Appeared On Passbook Ind.')
     A                                      COLHDG('PAS' +
     A                                             'BK' +
     A                                             'IND.')
     A            PASS           2A         TEXT('Passbook Transaction Type')
     A                                      COLHDG('PAS' +
     A                                             'BOOK' +
     A                                             'TYPE')
     A            XRFI           3A         TEXT('Extension File Reference ID')
     A                                      COLHDG('Extension' +
     A                                             'File' +
     A                                             'Reference ID')
     A            XRFN          10S 0       TEXT('Extension File Reference No.')
     A                                      COLHDG('Extension' +
     A                                             'File' +
     A                                             'Reference Number')
     A            ACDE          10S 0       TEXT('Account code')
     A                                      COLHDG('Acc.' +
     A                                             'Code')
     A            FUND          21A
     A            OTR1          20A         TEXT('Other Transaction Reference')
     A                                      COLHDG('Other Transaction' +
     A                                             'Number')
     A            CMOD           2A         TEXT('Module')
     A                                      COLHDG('Module')
     A            ETYP           4A         TEXT('Event Type')
     A                                      COLHDG('Event Type')
     A            MTYP           1A         TEXT('Movement Type')
     A                                      COLHDG('Movement Type')
      *
