      *****************************************************************
/*S*D ***PFBASE********************************************************                       CGP016
/*E*S ***PF_STD********************************************************                       CGP016
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZSDACODPD - Account Codes                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CGP016  *MOVED     Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CPB004             Date 11Jul02               *
      *                 CGL029             Date 01Sep03               *
      *                 CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved to GZDDSSRCGP                                 *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  CPB004 - Add identifier to indicate if account is to be      *
      *           transferred, maintained and closed in TOF.          *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGP001 - Global Processing                                   *
      *           - New file - Global version of SDACODPD.            *
      *                                                               *
      *****************************************************************
     A**Start of change CGL029
     A                                      REF(MFRFL)
     A**End of change CGL029
     A          R SDACODD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A**Start of change CGL029
     A**********  A5ACCD         4          TEXT('Account Code')                              CGL029
     A**********                            COLHDG('Account' +                                CGL029
     A**********                                   'code' )                                   CGL029
     A**********  A5CIAD         4          TEXT('Credit Int. Accrual Dr.')                   CGL029
     A**********                            COLHDG('Credit Int.' +                            CGL029
     A**********                                   'Accrual Dr.' )                            CGL029
     A**********  A5CIAC         4          TEXT('Credit Int. Accrual Cr.')                   CGL029
     A**********                            COLHDG('Credit Int.' +                            CGL029
     A**********                                   'Accrual Cr.' )                            CGL029
     A**********  A5DIAC         4          TEXT('Debt Interest Accrual Cr.')                 CGL029
     A**********                            COLHDG('Debt Interest' +                          CGL029
     A**********                                   'Accrual Cr.' )                            CGL029
     A**********  A5DIAD         4          TEXT('Debt Interest Accrual Dr.')                 CGL029
     A**********                            COLHDG('Debt Interest' +                          CGL029
     A**********                                   'Accrual Dr.' )                            CGL029
     A**********  A5ICIC         4          TEXT('Interest Capital in Cr.')                   CGL029
     A**********                            COLHDG('Interest' +                               CGL029
     A**********                                   'Capital in Cr.' )                         CGL029
     A**********  A5ICID         4          TEXT('Interest Capital in Debt')                  CGL029
     A**********                            COLHDG('Interest' +                               CGL029
     A**********                                   'Capital in Debt' )                        CGL029
     A            QQACCD         4A
     A            QQCIAD         4A
     A            QQCIAC         4A
     A            QQDIAC         4A
     A            QQDIAD         4A
     A            QQICIC         4A
     A            QQICID         4A
     A**End of change CGL029
     A            A5LCD          5  0       TEXT('Last Change Date')
     A                                      COLHDG('Last' +
     A                                             'Change Date' )
     A                                      EDTCDE(3)
     A            A5TYLC         1          TEXT('Type of Last Change')
     A                                      COLHDG('Type of' +
     A                                             'Last Change' )
     A            A5ACTY         1          TEXT('Account type')
     A                                      COLHDG('Acc' +
     A                                             'Typ' )
     A                                      DFT('G')
     A            A5ACCN        25          TEXT('Account Code Name')
     A                                      COLHDG('Account Code Name' )
     A            A5SRLC         5  0       TEXT('Stat Returns BS L/C')
     A                                      COLHDG('Stat Returns' +
     A                                             'BS L/C' )
     A                                      EDTCDE(3)
     A            A5SRFC         5  0       TEXT('Stat Returns BS F/C')
     A                                      COLHDG('Stat Returns' +
     A                                             'BS F/C' )
     A                                      EDTCDE(3)
     A            A5TOIN         1          TEXT('Title Only Indicator')
     A                                      COLHDG('Title Only' +
     A                                             'Indicator' )
     A                                      DFT('N')
     A            A5RPCD        10  0       TEXT('Reporting Code')
     A                                      COLHDG('Reporting' +
     A                                             'Code' )
     A                                      EDTCDE(3)
     A            A5ACSC         2          TEXT('Account Section')
     A                                      COLHDG('Account' +
     A                                             'Section' )
     A            A5ACST         1          TEXT('Account Sub-type')
     A                                      COLHDG('Account' +
     A                                             'Sub-type' )
     A                                      DFT(' ')
     A            A5ACPR         2S 0       TEXT('Acct Post Retention Prd')
     A                                      COLHDG('Acct Post' +
     A                                             'Retention Prd' )
     A                                      EDTCDE(3)
     A            A5TRFD         1          TEXT('A/C transferred daily')
     A                                      COLHDG('A/C transferred' +
     A                                             'daily' )
     A**Start of change CGL029
     A**********  A5DIPD         4          TEXT('Dr Int pay A/C code Dr')                    CGL029
     A**********                            COLHDG('Dr Int pay' +                             CGL029
     A**********                                   'A/C code Dr' )                            CGL029
     A**********  A5DIPC         4          TEXT('Dr Int pay A/C code Cr')                    CGL029
     A**********                            COLHDG('Dr Int pay' +                             CGL029
     A**********                                   'A/C code Cr' )                            CGL029
     A**********  A5CIPC         4          TEXT('Cr Int pay A/C code Cr')                    CGL029
     A**********                            COLHDG('Cr Int pay' +                             CGL029
     A**********                                   'A/C code Cr' )                            CGL029
     A**********  A5CISU         4          TEXT('Cr Int pay A/C code Sub')                   CGL029
     A**********                            COLHDG('Cr Int pay' +                             CGL029
     A**********                                   'A/C code Sub' )                           CGL029
     A**********  A5CINS         4          TEXT('Cr Int pay A/C code N-Sub')                 CGL029
     A**********                            COLHDG('Cr Int pay A/C' +                         CGL029
     A**********                                   'code N-Sub' )                             CGL029
     A**********  A5WTAC         4          TEXT('Withholding Tax A/C code')                  CGL029
     A**********                            COLHDG('Withholding' +                            CGL029
     A**********                                   'Tax A/C code' )                           CGL029
     A            QQDIPD         4A
     A            QQDIPC         4A
     A            QQCIPC         4A
     A            QQCISU         4A
     A            QQCINS         4A
     A            QQWTAC         4A
     A**End of change CGL029
     A            A5MISU        13  0       TEXT('Min Int subject to tax')
     A                                      COLHDG('Min Int' +
     A                                             'subject to tax' )
     A                                      EDTCDE(3)
     A            A5ACGC        10          TEXT('Midas Export Value ACGC')
     A                                      COLHDG('Midas Export' +
     A                                             'Value ACGC' )
     A            A5INNR         3          TEXT('PM Instrument Code (Lndr)')
     A                                      COLHDG('PM Instrument' +
     A                                             'Code (Lndr)' )
     A            A5MAPC         1          TEXT('Man A/C Anal Profit Cntr')
     A                                      COLHDG('Man A/C Anal' +
     A                                             'Profit Cntr' )
     A            A5MABK         1          TEXT('Man A/C Anal Book')
     A                                      COLHDG('Man A/C' +
     A                                             'Anal Book' )
     A            A5MAAC         1          TEXT('Man A/C Anal Ass Cust')
     A                                      COLHDG('Man A/C Anal' +
     A                                             'Ass Cust' )
     A            A5MATR         1          TEXT('Man A/C Anal Trans Ref')
     A                                      COLHDG('Man A/C Anal' +
     A                                             'Trans Ref' )
     A            A5MAPR         1          TEXT('Man A/C Anal Prod')
     A                                      COLHDG('Man A/C' +
     A                                             'Anal Prod' )
     A            A5FNAC         1          TEXT('Funding Account')
     A                                      COLHDG('Funding' +
     A                                             'Account' )
     A            A5FNBR         2          TEXT('Funding Base Rate')
     A                                      COLHDG('Funding' +
     A                                             'Base Rate' )
     A**Start of change CGL029
     A**********  A5FNIC         4          TEXT('Funding Int A/C Code')                      CGL029
     A**********                            COLHDG('Funding Int' +                            CGL029
     A**********                                   'A/C Code' )                               CGL029
     A**********  A5FNAA         4          TEXT('Funding Accrual A/C Code')                  CGL029
     A**********                            COLHDG('Funding Accrual' +                        CGL029
     A**********                                   'A/C Code' )                               CGL029
     A            QQFNIC         4A
     A            QQFNAA         4A
     A**End of change CGL029
     A            A5CAAC         1          TEXT('Capitalise to this A/C')
     A                                      COLHDG('Capitalise' +
     A                                             'to this A/C' )
     A            A5TIAT         2          TEXT('TI Account Type')
     A                                      COLHDG('TI Account' +
     A                                             'Type' )
     A            A5TFCT         1          TEXT('TF Exposure Account')
     A                                      COLHDG('TF Exposure' +
     A                                             'Account' )
     A            A5VOST         1A         TEXT('Vostro Account')
     A                                      COLHDG('Vostro' +
     A                                             'Account' +
     A                                             'Y/N Flag' )
     A            A5LORO         1A         TEXT('Loro Accounts')
     A                                      COLHDG('Loro' +
     A                                             'Accounts' +
     A                                             'Y/N Flag' )
     A            A5NARR        50          TEXT('Account Narrative')
     A                                      COLHDG('Account' +
     A                                             'Narrative')
     A**Start of change CGL029
     A**********  A5TRDA         4S 0       TEXT('Transitory Debit A/C Code')                 CGL029
     A**********                            COLHDG('Transitory Debit' +                       CGL029
     A**********                                   'A/C Code')                                CGL029
     A**********  A5TRCA         4S 0       TEXT('Transitory Credit A/C Code')                CGL029
     A**********                            COLHDG('Transitory Credit' +                      CGL029
     A**********                                   'A/C Code')                                CGL029
     A            QQTRDA    R               REFFLD(ACODQQ)
     A            QQTRCA    R               REFFLD(ACODQQ)
     A**End of change CGL029
     A            A5ZONE        10A         TEXT('Zone')
     A                                      COLHDG('Zone')
     A**Start of change CGL029
     A            A5ACCD        10A         TEXT('Account Code')
     A            A5CIAD        10A         TEXT('Credit Int. Accrual Dr.')
     A            A5CIAC        10A         TEXT('Credit Int. Accrual Cr.')
     A            A5DIAC        10A         TEXT('Debt Interest Accrual Cr.')
     A            A5DIAD        10A         TEXT('Debt Interest Accrual Dr.')
     A            A5ICIC        10A         TEXT('Interest Capital in Cr.')
     A            A5ICID        10A         TEXT('Interest Capital in Debt')
     A            A5DIPD        10A         TEXT('Dr Int pay A/C code Dr')
     A            A5DIPC        10A         TEXT('Dr Int pay A/C code Cr')
     A            A5CIPC        10A         TEXT('Cr Int pay A/C code Cr')
     A            A5CISU        10A         TEXT('Cr Int pay A/C code Sub')
     A            A5CINS        10A         TEXT('Cr Int pay A/C code N-Sub')
     A            A5WTAC        10A         TEXT('Withholding Tax A/C code')
     A            A5FNIC        10A         TEXT('Funding Int A/C Code')
     A            A5FNAA        10A         TEXT('Funding Accrual A/C Code')
     A            A5TRDA    R               REFFLD(ACOD)
     A                                      TEXT('Transitory Debit A/C Code')
     A            A5TRCA    R               REFFLD(ACOD)
     A                                      TEXT('Transitory Credit A/C Code')
     A**End of change CGL029
      * Start CPB004
     A            A5TRAN         1          TEXT('To be transferred to TOF')
     A                                      COLHDG('To transfer' +
     A                                             'to TOF' )
      * End CPB004
      **Start of change CSD031
     A            A5IUSR        10A         TEXT('Insert User Profile')
     A                                      COLHDG('Insert User Profile')
     A            A5AUSR        10A         TEXT('Amend User Profile')
     A                                      COLHDG('Amend User Profile')
     A            A5XUSR        10A         TEXT('Authorise User Profile')
     A                                      COLHDG('Authorise ' 'User Profile')
     A            A5AACC         1A         TEXT('Automatically accept w/in +
     A                                      zone')
     A                                      COLHDG('Automatically accept' +
     A                                      ' w/in zone')
     A            A5EACC         5P 0       TEXT('Accept by Effective Date')
     A                                      COLHDG('Accept by ' +
     A                                      'Effective Date')
     A            A5GPDT         1A         TEXT('Central Data indicator')
     A                                      COLHDG('Central Data ind.')
      **End of change CSD031
