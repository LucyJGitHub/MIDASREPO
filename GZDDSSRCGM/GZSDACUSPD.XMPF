      *****************************************************************
/*S*D ***PFBASE********************************************************                       CGP016
/*E*S ***PF_STD********************************************************                       CGP016
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PF/GZSDACUSPD -  Midas GZ Additional customer details        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      *  Last Amend No. CGP016    *MOVED   Date 06Aug12               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      *                 AR742563           Date 06Apr11               *
      *                 CER059             Date 19Jul10               *
      *                 CER045             Date 20Jul09               *
      *                 CER054             Date 07Apr09               *
      *                 BUG22529  *CREATE  Date 05Feb09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved to GZDDSSRCGP                                 *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  AR742563 - Global Replication : GZSDACUSPD vs SDACUSPD       *
      *             (Child: AR742564)                                 *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank              *
      *  CER054 - German Features - Church Tax                        *
      *  BUG22529 - Global Additional Customer Details                *
      *                                                               *
      *****************************************************************
     A          R SDACUSD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            E5CUST         6          TEXT('Customer Number')
     A                                      COLHDG('Customer' +
     A                                             'Number' )
     A            E5LCD          5  0       TEXT('Last Change Date')
     A                                      COLHDG('Last' +
     A                                             'Change Date' )
     A                                      EDTCDE(3)
     A            E5TYLC         1          TEXT('Type of Last Change')
     A                                      COLHDG('Type of' +
     A                                             'Last Change' )
     A            E5ALTN        12          TEXT('Alternative Number')
     A                                      COLHDG('Alternative' +
     A                                             'Number' )
     A            E5IDNO        15          TEXT('Identity Number')
     A                                      COLHDG('Identity' +
     A                                             'Number' )
     A            E5FXNB        12          TEXT('Fax Number')
     A                                      COLHDG('Fax Number' )
     A            E5TXNB        12          TEXT('Telex Number')
     A                                      COLHDG('Telex Number' )
     A            E5DOLV         5  0       TEXT('Date of Last Visit')
     A                                      COLHDG('Date of' +
     A                                             'Last Visit' )
     A                                      EDTCDE(3)
     A            E5PRVO         1          TEXT('Provisional Opening')
     A                                      COLHDG('Provisional' +
     A                                             'Opening' )
     A            E5MDTL         5          TEXT('Mail Details')
     A                                      COLHDG('Mail' +
     A                                             'Details' )
     A            E5CNA1        70          TEXT('Cust. Name & Address 1')
     A                                      COLHDG('Cust. Name' +
     A                                             '& Address 1' )
     A            E5CNA2        70          TEXT('Cust. Name & Address 2')
     A                                      COLHDG('Cust. Name' +
     A                                             '& Address 2' )
     A            E5CNA3        35          TEXT('Cust. Name & Address 3')
     A                                      COLHDG('Cust. Name' +
     A                                             '& Address 3' )
     A            E5CNA4        35          TEXT('Cust. Name & Address 4')
     A                                      COLHDG('Cust. Name' +
     A                                             '& Address 4' )
     A            E5CWL1        20          TEXT('Code Word One')
     A                                      COLHDG('Code Word 1' )
     A            E5CWL2        20          TEXT('Code Word Two')
     A                                      COLHDG('Code Word 2' )
     A            E5CWL3        20          TEXT('Code Word Three')
     A                                      COLHDG('Code Word 3' )
     A            E5CWL4        20          TEXT('Code Word Four')
     A                                      COLHDG('Code Word 4' )
     A            E5BRTH         5  0       TEXT('Date of Birth')
     A                                      COLHDG('Date of' +
     A                                             'Birth' )
     A                                      EDTCDE(3)
     A            E5CWL5        20          TEXT('Code Word Five')
     A                                      COLHDG('Code Word 5' )
     A            E5BRT1         6          TEXT('Date of Brith DAF 1')
     A                                      COLHDG('Date of' 'Birth' 'DAF 1')
     A            E5BRT2         2          TEXT('Date of Brith DAF 2')
     A                                      COLHDG('Date of' 'Birth' 'DAF 2')
     A            E5CITY        35A         TEXT('City')
     A                                      COLHDG('City')
     A            E5ZIPC        10A         TEXT('Zip Code')
     A                                      COLHDG('Zip Code')
     A            E5TINO        25A         TEXT('Tax Identification Number')
     A                                      COLHDG('Tax Identification ' +
     A                                      'Number')
     A            E5IDTP         2A         TEXT('Identification Type')
     A                                      COLHDG('Identification Type')
     A            E5ISCT         2A         TEXT('Issuing Country')
     A                                      COLHDG('Issuing Country')
     A            E5ISDT         8A         TEXT('Issuing Date')
     A                                      COLHDG('Issuing Date')
     A            E5DBTH         8A         TEXT('Birth Date (YYYYMMDD)')
     A                                      COLHDG('Birth Date ' +
     A                                      '(YYYYMMDD)')
     A            E5BTHC         2A         TEXT('Place of Birth - Country')
     A                                      COLHDG('Place of Birth - ' +
     A                                      'Country')
     A            E5BTHT        35A         TEXT('Place of Birth - Town')
     A                                      COLHDG('Place of Birth - ' +
     A                                      'Town')
     A            E5JATP         1A         TEXT('Joint Account Type')
     A                                      COLHDG('Joint Account Type')
     A            E5REPI         1A         TEXT('Replication Indicator')
     A                                      COLHDG('Replication ' +
     A                                      'Indicator')
     A            E5FRNT        20A         TEXT('Front Office Transaction ID')
     A                                      COLHDG('Front Office ' +
     A                                      'Transaction ID')
     A            E5CSSN        10A         TEXT('Customer Shortname')
     A                                      COLHDG('Customer' +
     A                                      'Shortname')
     A            E5REPA         1A         TEXT('Repair Location')
     A                                      COLHDG('Repair Location')
     A            E5TMST          Z         TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
     A            E5FL01        50          TEXT('Field 01 details')
     A                                      COLHDG('Field 01 details')
     A            E5FL02        50          TEXT('Field 02 details')
     A                                      COLHDG('Field 02 details')
     A            E5FL03        50          TEXT('Field 03 details')
     A                                      COLHDG('Field 03 details')
     A            E5FL04        50          TEXT('Field 04 details')
     A                                      COLHDG('Field 04 details')
     A            E5FL05        50          TEXT('Field 05 details')
     A                                      COLHDG('Field 05 details')
     A            E5FL06        50          TEXT('Field 06 details')
     A                                      COLHDG('Field 06 details')
     A            E5FL07        50          TEXT('Field 07 details')
     A                                      COLHDG('Field 07 details')
     A            E5FL08        50          TEXT('Field 08 details')
     A                                      COLHDG('Field 08 details')
     A            E5FL09        50          TEXT('Field 09 details')
     A                                      COLHDG('Field 09 details')
     A            E5FL10        50          TEXT('Field 10 details')
     A                                      COLHDG('Field 10 details')
     A            E5ISCD        35          TEXT('Issuer Code')
     A                                      COLHDG('Issuer Code')
     A            E5TITL        35A         TEXT('Title')
     A                                      COLHDG('Title')
     A            E5EXDT         5P 0       TEXT('Expiry Date')
     A                                      EDTCDE(3)
     A                                      COLHDG('Expiry Date')
     A            E5REM1        40A         TEXT('Remarks 1')
     A                                      COLHDG('Remarks 1')
     A            E5REM2        40A         TEXT('Remarks 2')
     A                                      COLHDG('Remarks 2')
     A            E5REM3        40A         TEXT('Remarks 3')
     A                                      COLHDG('Remarks 3')
     A            E5S24C         1A         TEXT('Subject to §24c')
     A                                      COLHDG('Subject to §24c')
     A            E5LNID         2A         TEXT('Language ID')
     A                                      COLHDG('Language ID')
     A            E5CTYP        10A         TEXT('Correspondent Type')
     A                                      COLHDG('Correspondent' +
     A                                             'Type')
     A            E5ZCFG         1A         TEXT('Zero Confirmation +
     A                                           Flag')
     A                                      COLHDG('Zero Cnfrm.' +
     A                                             'Flag')
     A            E5FXCU         1A         TEXT('FX Customer')
     A                                      COLHDG('FX Customer')
     A            E5SECU         1A         TEXT('Securities Customer')
     A                                      COLHDG('Securities Cust.')
     A            E5SMBR         1A         TEXT('Staff Member')
     A                                      COLHDG('Staff Member')
     A            E5MDNM        70A         TEXT('Maiden Name')
     A                                      COLHDG('Maiden Name')
     A            E5SHRF        10A         TEXT('Spouse Reference')
     A                                      COLHDG('Spouse' +
     A                                             'Reference')
     A            E5PIND         1A         TEXT('Partner/Associate +
     A                                          Indicator')
     A                                      COLHDG('Partnr/Associate' +
     A                                              'Indicator')
     A            E5EXTH         6P 0       TEXT('Exemption Threshold')
     A                                      COLHDG('Exmptn. Threshold')
     A            E5EADT         5P 0       TEXT('Exemption Threshold +
     A                                          Approval Date')
     A                                      EDTCDE(3)
     A                                      COLHDG('Exmptn Threshold' +
     A                                             'Approval Date')
     A            E5EVDT         5P 0       TEXT('Exemption Threshold +
     A                                          Value Date')
     A                                      EDTCDE(3)
     A                                      COLHDG('Exmptn Threshold' +
     A                                             'Value Date')
     A            E5ETXT         5P 0       TEXT('Exemption Threshold +
     A                                          Expiry Date')
     A                                      EDTCDE(3)
     A                                      COLHDG('Exmptn Threshold' +
     A                                             'Expiry Date')
     A            E5DCID        20A         TEXT('Identification +
     A                                          Document No/Id')
     A                                      COLHDG('Identification' +
     A                                             'Document No/Id')
     A            E5DIPL        40A         TEXT('Identification +
     A                                          Document Issued Place')
     A                                      COLHDG('Identification' +
     A                                             'Doc. Issued Place')
     A            E5CDCL        40A         TEXT('Characteristic of +
     A                                          Demand/Claim')
     A                                      COLHDG('Characteristic' +
     A                                             'of Demand/Claim')
     A            E5ZONE        10A         TEXT('Zone')
     A                                      COLHDG('Zone')
      ** Start of change CER054
     A            E5STCD         2A         TEXT('State Code')
     A                                      COLHDG('State Code')
      **Start of change AR742563
     A**********  E5RLAF         2A         TEXT('Religious +                               AR742563
     A            E5RLAF         4A         TEXT('Religious +
      **End of change AR742563
     A                                          Affiliation Code')
     A                                      COLHDG('Religious' +
     A                                             'Affiliation Code')
      ** End of change CER054
      **Start of change CER045
     A            E5BSCC         2P         TEXT('Borrower Sector +
     A                                          Classification Code')
     A                                      COLHDG('Borrower Sector' +
     A                                             'Classification Code')
     A            E5ECAC         3A         TEXT('Economic Activity Code')
     A                                      COLHDG('Economic Activity' +
     A                                             'Code')
     A            E5BIST         3P         TEXT('Borrower Sector +
     A                                          Classification/BISTA')
     A                                      COLHDG('Borrower Sector' +
     A                                             'Classification/BISTA')
     A            E5ABSK         1A         TEXT('Additional Borrowers +
     A                                          Statistics Key')
     A                                      COLHDG('Additional Borrowers' +
     A                                             'Statistics Key')
     A            E5BEXT         1A         TEXT('Customer Required for +
     A                                          Bundesbank Reporting')
     A                                      COLHDG('Customer Required' +
     A                                              'for Bundesbank' +
     A                                             'Reporting')
      **End of change CER045
      **Start of change CLE134
     A            E5PDCU         1A         TEXT('Basel II Pass Due Customer')
     A                                      COLHDG('Basel II' +
     A                                             'Pass Due' +
     A                                             'Customer')
      **End of change CLE134
