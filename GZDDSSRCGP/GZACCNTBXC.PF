      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  REUSEDLT(*YES)                                               *                       CGP016
/*EXI *  TEXT('Midas GZ Account details extension')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZACCNTBXC - Account details extension                    *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. CER050             Date 16Jun19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CGL165             Date 17Feb15               *
      *                 CGP016             Date 06Aug12               *
      *                 CER055             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CGL029             Date 01Sep03               *
      *                 CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER050 - Annualised Percentage Rate                          *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL165 - Dual Withholding Tax                                *
      *  CGP016 - Global database - Reuse deleted records             *
      *         - Moved from GZDDSSRCGM                               *
      *  CER055 - Penalty Interest on Exceeding Overdraft Limit       *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGP001 - Global Processing                                   *
      *           - New file - Global version of ACCNTBXC.            *
      *                                                               *
      *****************************************************************
     A                                      UNIQUE
     A                                      REF(MFRFL)
     A          R ACCNTBD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      * Begin /CSD027/
     A**********  ATCNUM    R               REFFLD(CNUM)                                      CSD027
     A            ATCNUM         6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            ATCCY     R               REFFLD(CCY)
     A**Start of change CGL029
     A**********  ATACOD    R               REFFLD(ACOD)                                      CGL029
     A            QQACOD    R               REFFLD(ACODQQ)
     A**End of change CGL029
     A            ATACSQ    R               REFFLD(ACSQ)
     A            ATCRST    R
     A            ATSUBO    R
     A            ATITAX    R               REFFLD(MTAX)
     A            ATSTCI    R
     A            ATCHIN    R
     A            ATCHAM    R
     A            ZZ100     R
     A            ATBRCA    R               REFFLD(BRCA)
     A            ATAODR    R               REFFLD(AODR)
     A            ATITRD    R               REFFLD(ITRD)
     A            ATORTC    R               REFFLD(ORTC)
     A            ATOLVD    R               REFFLD(OLVD)
     A            ATPOLC    R               REFFLD(POLCD)
     A            ATPOLE    R               REFFLD(POLE)
     A            ATFRNT    R               REFFLD(FOTRNID)
     A            ATAFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            ATREPA    R               REFFLD(RPRLOC)
      ** Timestamp
     A            ATTMST    R               REFFLD(TIMESTMP)
     A            ATACNO    R               REFFLD(ACNO)
     A            ATOVRD    R               REFFLD(OVRC)
     A                                      TEXT('Debit Margin Overridden')
     A            ATOVRC    R               REFFLD(OVRC)
     A                                      TEXT('Credit Margin Overridden')
     A            ATDRMG        11P 7       TEXT('Debit Margin Rate')
     A            ATCRMG        11P 7       TEXT('Credit Margin Rate')
     A            ATDRMD         5P 0       TEXT('Debit Margin Change Date')
     A            ATCRMD         5P 0       TEXT('Credit Margin Change Date')
     A            ATDMBR         5A         TEXT('Debit Margin Base Rate')
     A            ATCMBR         5A         TEXT('Credit Margin Base Rate')
      *
     A**Start of change CGL029
     A            ATACOD    R               REFFLD(ACOD)
     A**End of change CGL029
      **Start of change CER055
     A            ATDOBE         5P 0       TEXT('Date of Overdraft Balance +
     A                                            Excess')
     A                                      COLHDG('Date of Overdraft' +
     A                                             'Balance Excess')
     A            ATNDYE         4P 0       TEXT('No. of Days in Excess')
     A                                      COLHDG('No.of Days in Excess')
     A            ATNDYO         4P 0       TEXT('No. of Days in Excess +
     A                                            since A/c Opening')
     A                                      COLHDG('No.of Days in Excess' +
     A                                             'since A/c Opening')
     A            ATPOLA        11P 0       TEXT('Previous Overdraft Limit')
     A                                      COLHDG('Previous Overdraft' +
     A                                             'Limit')
      **End of change CER055
      **Start of change CGL165
     A            ATTXC1         2A         TEXT('Main Tax Rate Code')
     A                                      COLHDG('Main Tax' 'Rate Code')
     A            ATTXC2         2A         TEXT('Secondary Tax Rate Code')
     A                                      COLHDG('Secondary Tax' 'Rate Code')
      **End of change CGL165
      ** Start of change CER050
     A            ATAPRC         1A         TEXT('APR Calculation Required')
     A                                      COLHDG('APR' 'Reqd')
      ** End of change CER050
     A          K ATCNUM
     A          K ATCCY
     A          K ATACOD
     A          K ATACSQ
     A          K ATBRCA
