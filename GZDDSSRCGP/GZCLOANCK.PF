      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  REUSEDLT(*YES)                                               *                       CGP016
/*EXI *  TEXT('Midas GZ Customer loans B record')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZCLOANCK - Customer Loans B Record                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CGP016  *MOVED     Date 06Aug12               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CGL029             Date 01Sep03               *
      *                 CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP016 - Global Database - Reuse of deleted records          *
      *         - Moved from GZDDSSRCGM                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGP001 - Global Processing                                   *
      *           - New file - Global version of CLOANCK.             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R CLOANCKF                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A            RECI      R
      **Start of change CLE148
     A**********  LNRF      R                                                                 CLE148
     A            LNRF           6A         TEXT('Loan number')
     A                                      COLHDG('Loan' 'Number')
      **End of change CLE148
     A            RCDT      R
     A            MRIN      R
     A            NAR1      R
     A            NAR2      R
     A            NAR3      R
     A            NAR4      R
     A            OPAM      R
     A            FRBC      R               REFFLD(FRBC0 )
     A            YPLN      R
     A            YFLN      R
     A            YWLN      R
     A            ABLN      R               REFFLD(ABLN0 )
     A            YPLC      R
     A            YFLC      R
     A            YWLC      R
     A            ABLC      R
     A            WWLN      R
     A            IWLN      R
     A            PWLN      R
     A            ZZ004     R
     A            RDST      R
     A            SPI1      R
     A            SPI2      R
     A            SPI3      R
     A            YILN      R
     A            F57U      R
     A**Start of change CGL029
     A**********  ORVD      R                                                                 CGL029
     A            ORVDQQ        12A
     A**End of change CGL029
     A            THRN      R
     A            NPRAM     R
     A            NDAM      R
     A            RLBR      R
     A            ZZ009     R
     A            ORED      R
     A            LCD       R
     A            CHTP      R
     A            TNLU      R
     A            RRST      R               REFFLD(RSTM)
     A**Start of change CGL029
     A**********  RRON      R               REFFLD(RONS)                                      CGL029
     A            RRONQQ        12A
     A**End of change CGL029
     A            RRIB      R               REFFLD(RIBN)
     A            RRBA      R               REFFLD(RIBA)
      * Begin /CSD027/
     A**********  RROB      R               REFFLD(ROBN)                                      CSD027
     A**********  RROC      R               REFFLD(ROCN)                                      CSD027
     A            RROB           6A
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            RROC           6A
     A                                      TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            NCCY      R               REFFLD(CCY)
     A                                      TEXT('New Currency')
     A                                      COLHDG('New' +
     A                                             'Ccy')
     A            NCPA      R               REFFLD(OPAM)
     A                                      TEXT('New Currency +
     A                                            Principal Amount')
     A                                      COLHDG('New Currency' +
     A                                             'Principal Amount')
     A            NFCE      R               REFFLD(SPOT)
     A                                      TEXT('New Facility Ccy +
     A                                            Exch Rate')
     A                                      COLHDG('New Fac Ccy' +
     A                                             'Exchange Rate')
     A            NFMD      R               REFFLD(SPIN)
     A                                      TEXT('New Facility Ccy +
     A                                            Mult/Div Ind')
     A                                      COLHDG('New Fac Ccy' +
     A                                             'Mult/Div')
     A            NLRA      R               REFFLD(OPAM)
     A                                      TEXT('New Loan Ccy +
     A                                            Repayment Amount')
     A                                      COLHDG('New Loan Ccy' +
     A                                             'Repayment Amount')
     A            RSTS      R               REFFLD(SPIN)
     A                                      TEXT('Rollover Status +
     A                                            Indicator')
     A                                      COLHDG('Roll' +
     A                                             'Sts')
     A            RIUS      R               REFFLD(FACO)
     A                                      TEXT('Rollover Insert +
     A                                            User Profile')
     A                                      COLHDG('Rollover' +
     A                                             'Insert User')
     A            RAUS      R               REFFLD(FACO)
     A                                      TEXT('Rollover Amend +
     A                                            User Profile')
     A                                      COLHDG('Rollover' +
     A                                             'Amend User')
     A            RXUS      R               REFFLD(FACO)
     A                                      TEXT('Rollover Authorise +
     A                                            User Prof.')
     A                                      COLHDG('Rollover' +
     A                                             'Auth User')
     A            RPCR      R               REFFLD(ZZ015)
     A                                      TEXT('Rollover - PC +
     A                                            Trans Ref.')
     A                                      COLHDG('Rollover -' +
     A                                             'PC Trans Ref')
     A            RLSB      R               REFFLD(OSDB)
     A                                      TEXT('Pay - Rollover +
     A                                            Settle Branch')
     A                                      COLHDG('Pay Set' +
     A                                             'Branch')
     A            RPST      R               REFFLD(PSTM)
     A                                      TEXT('Pay - Roll +
     A                                            Settlement Method')
     A                                      COLHDG('Pay Set' +
     A                                             'Method')
     A**Start of change CGL029
     A**********  RPON      R               REFFLD(PONS)                                      CGL029
     A**********                            TEXT('Pay - Rollover +                            CGL029
     A**********                                  Our Nostro')                                CGL029
     A**********                            COLHDG('Pay Rollover' +                           CGL029
     A**********                                   'Our Nostro')                              CGL029
     A            RPONQQ        12A
     A**End of change CGL029
     A            RPIB      R               REFFLD(PIBN)
     A                                      TEXT('Pay - Roll +
     A                                            Intermediary Bank')
     A                                      COLHDG('Pay Roll' +
     A                                             'Inter Bank')
     A            RPBA      R               REFFLD(PIBA)
     A                                      TEXT('Pay - Roll +
     A                                            Intermediary Bank')
     A                                      COLHDG('Pay Rollover' +
     A                                             'Intermediary Bank')
      * Begin /CSD027/
     A**********  RPOB      R               REFFLD(POBN)                                      CSD027
     A            RPOB           6A
      * End /CSD027/
     A                                      TEXT('Pay - Roll +
     A                                            Ordering Bank No')
     A                                      COLHDG('Pay Roll' +
     A                                             'Ord Bank')
      * Begin /CSD027/
     A**********  RPOC      R               REFFLD(POCN)                                      CSD027
     A            RPOC           6A
      * End /CSD027/
     A                                      TEXT('Pay - Roll +
     A                                            Ordering Customer')
     A                                      COLHDG('Pay Roll' +
     A                                             'Ord Cust')
     A            RRCR      R               REFFLD(RCRN)
     A                                      TEXT('Roll Receiver +
     A                                            Corresp No')
     A                                      COLHDG('Roll Rec' +
     A                                             'Corresp')
     A            RRCA      R               REFFLD(RCRA)
     A                                      TEXT('Roll Receiver +
     A                                            Corresp A/C line')
     A                                      COLHDG('Rollover Receiver' +
     A                                             'Correspondent A/C')
     A            RRVN      R               REFFLD(RVNO)
     A                                      TEXT('Rollover Receiver +
     A                                            Number')
     A                                      COLHDG('Roll Rec' +
     A                                             'Number')
     A            RAWB      R               REFFLD(AWBN)
     A                                      TEXT('Rollover A/C With +
     A                                            Bank No')
     A                                      COLHDG('Roll A/C' +
     A                                             'w/ Bank')
     A            RAWA      R               REFFLD(AWBA)
     A                                      TEXT('Roll A/C With Bank +
     A                                            A/C Line')
     A                                      COLHDG('Rollover A/C' +
     A                                             'w/ Bank A/C line')
     A            RBEN      R               REFFLD(BENN)
     A                                      TEXT('Rollover Beneficiary +
     A                                            Number')
     A                                      COLHDG('Roll Ben' +
     A                                             'Number')
     A            RBNA      R               REFFLD(BENA)
     A                                      TEXT('Rollover Beneficiary +
     A                                            A/C Line')
     A                                      COLHDG('Rollover Beneficiary' +
     A                                             'Account Line')
     A            RDP1      R               REFFLD(DTP1)
     A                                      TEXT('Rollover Details +
     A                                            of Pay 1')
     A                                      COLHDG('Rollover Details' +
     A                                             'of Pay 1')
     A            RDP2      R               REFFLD(DTP2)
     A                                      TEXT('Rollover Details +
     A                                            of Pay 2')
     A                                      COLHDG('Rollover Details' +
     A                                             'of Pay 2')
     A            RDP3      R               REFFLD(DTP3)
     A                                      TEXT('Rollover Details +
     A                                            of Pay 3')
     A                                      COLHDG('Rollover Details' +
     A                                             'of Pay 3')
     A            RDP4      R               REFFLD(DTP4)
     A                                      TEXT('Rollover Details +
     A                                            of Pay 4')
     A                                      COLHDG('Rollover Details' +
     A                                             'of Pay 4')
     A            RDCH      R               REFFLD(DCHG)
     A                                      TEXT('Rollover Details +
     A                                            of Charges')
     A                                      COLHDG('Rollover Details' +
     A                                             'of Charges')
     A            RBB1      R               REFFLD(BTB1)
     A                                      TEXT('Rollover Bank +
     A                                            to Bank Info 1')
     A                                      COLHDG('Rollover Bank' +
     A                                             'to Bank Info 1')
     A            RBB2      R               REFFLD(BTB2)
     A                                      TEXT('Rollover Bank +
     A                                            to Bank Info 2')
     A                                      COLHDG('Rollover Bank' +
     A                                             'to Bank Info 2')
     A            RBB3      R               REFFLD(BTB3)
     A                                      TEXT('Rollover Bank +
     A                                            to Bank Info 3')
     A                                      COLHDG('Rollover Bank' +
     A                                             'to Bank Info 3')
     A            RBB4      R               REFFLD(BTB4)
     A                                      TEXT('Rollover Bank +
     A                                            to Bank Info 4')
     A                                      COLHDG('Rollover Bank' +
     A                                             'to Bank Info 4')
     A            RBB5      R               REFFLD(BTB5)
     A                                      TEXT('Rollover Bank +
     A                                            to Bank Info 5')
     A                                      COLHDG('Rollover Bank' +
     A                                             'to Bank Info 5')
     A            RBB6      R               REFFLD(BTB6)
     A                                      TEXT('Rollover Bank +
     A                                            to Bank Info 6')
     A                                      COLHDG('Rollover Bank' +
     A                                             'to Bank Info 6')
     A            RCVM      R               REFFLD(CVMR)
     A                                      TEXT('Roll Cover Message +
     A                                            Required')
     A                                      COLHDG('Roll Cvr' +
     A                                             'Msg Req')
     A            NBCE      R               REFFLD(SPOT)
     A                                      TEXT('New Base Currency +
     A                                            Exch Rate')
     A                                      COLHDG('New Base Ccy' +
     A                                             'Exchange Rate')
     A            NBMD      R               REFFLD(SPIN)
     A                                      TEXT('New Base Currency +
     A                                            Mult/Div Ind')
     A                                      COLHDG('New Base Ccy' +
     A                                             'Mult/Div')
     A            RRSC      R               REFFLD(CCY)
     A                                      TEXT('Roll Rec Settlement +
     A                                            Ccy')
     A                                      COLHDG('Roll Rec Sett Ccy')
     A            RPSC      R               REFFLD(CCY)
     A                                      TEXT('Roll Pay Settlement +
     A                                            Ccy')
     A                                      COLHDG('Roll Pay Sett Ccy')
     A            RICY      R               REFFLD(CCY)
     A                                      TEXT('Roll IN Ccy in +
     A                                            Fld 72')
     A                                      COLHDG('Roll IN Ccy')
     A            ECIN           1A         TEXT('Euro Conversion Ind')
     A                                      COLHDG('Euro Conv Ind')
     A            RPSM      R               REFFLD(RSTM)
     A                                      TEXT('Roll Pay Settlement +
     A                                            Method')
     A                                      COLHDG('Roll Pay Sett +
     A                                              Method')
     A**Start of change CGL029
     A**********  RPOS      R               REFFLD(RONS)                                      CGL029
     A**********                            TEXT('Roll Pay Our Nostro')                       CGL029
     A**********                            COLHDG('Roll Pay Our Nost')                       CGL029
     A            RPOSQQ        12A
     A**End of change CGL029
     A            RPTS      R               REFFLD(TSTN)
     A                                      TEXT('Roll Pay Their Nost')
     A                                      COLHDG('Roll Pay Their +
     A                                              Nost')
     A            RPSB      R               REFFLD(BRCA)
     A                                      TEXT('Roll Pay Settlement +
     A                                            Branch')
     A                                      COLHDG('Roll Pay Settle +
     A                                              Brch')
     A            RFAO      R               REFFLD(FACO)
     A                                      TEXT('Roll For account of')
     A                                      COLHDG('Roll For A/c of')
     A            FSFYLB         5A         TEXT('Forward Yield Curve for +
     A                                      Rollover')
     A                                      COLHDG('Fwd Yld Curve for' +
     A                                             'Rollover')
     A            RRSR          13P 8
     A                                      TEXT('Receipt Currency +
     A                                         Exchange Rate')
     A                                      COLHDG('Receipt' 'Currency'+
     A                                         'Exchange' 'Rate')
     A            RRSI           1A
     A                                      TEXT('Receipt Currency +
     A                                         Exchange Rate Indicator')
     A                                      COLHDG('Receipt' 'Currency'+
     A                                         'Exchange' 'Rate'+
     A                                         'Indicator')
     A            RPSR          13P 8
     A                                      TEXT('Payment Currency +
     A                                         Exchange Rate')
     A                                      COLHDG('Payment' 'Currency'+
     A                                         'Exchange' 'Rate')
     A            RPSI           1A
     A                                      TEXT('Payment Currency +
     A                                         Exchange Rate Indicator')
     A                                      COLHDG('Payment' 'Currency'+
     A                                         'Exchange' 'Rate'+
     A                                         'Indicator')
     A            RSTA           1A
     A                                      TEXT('Rollover Settlement +
     A                                           Allocation Indicator')
     A                                      COLHDG('Rollover Settlement' +
     A                                           'Allocation Indicator')
     A            FRNT      R               REFFLD(FOTRNID)
     A            AFRT      R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            REPA      R               REFFLD(RPRLOC)
     A            TMST      R               REFFLD(TIMESTMP)
     A**Start of change CGL029
     A            ORVD          18A
     A            RRON          18A
     A            RPON          18A         TEXT('Pay - Rollover Our Nostro')
     A            RPOS          18A         TEXT('Roll Pay Our Nostro')
     A**End of change CGL029
