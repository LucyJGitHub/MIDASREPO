      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  REUSEDLT(*YES)                                               *                       CGP016
/*EXI *  TEXT('Midas GZ Money market & loans deals details')          *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZDEALSDC - Deals File - Tkngs/Plcngs/Tme Deps/Loans      *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. CER050             Date 16Jun19               *
      *                 MD046248           Date 27Oct17               *
      *                 CGL165             Date 17Feb15               *
      *                 CDL096             Date 22Sep14               *
      *                 CDL094             Date 30May14               *
      *                 CGP016  *MOVED     Date 06Aug12               *
      *                 CRE073             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER054             Date 07Apr09               *
      *                 CER048             Date 19May08               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 30Mar06               *
      *                 CSD027             Date 05Dec05               *
      *                 233324             Date 02May05               *
      *                 CDL038             Date 10May05               *
      *                 CAS011             Date 16May05               *
      *                 CDL033             Date 10Feb05               *
      *                 BUG3644 *CREATE    Date 12Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data.    *
      *  CER050 - Annualised Percentage Rate                          *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL165 - Dual Withholding Tax                                *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *  CDL094 - Enhance  Receive Settlement Instructions.           *
      *           Add Receive Bank to Bank Info Line 1 - 6.           *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved from GZDDSSRCGM                               *
      *  CRE073 - Interest Rate Rounding                              *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER054 - German Feature - Church Tax                         *
      *  CER048 - German Features - Taxes                             *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus                                          *
      *  CDL049 - Addition of a Reference Rate field                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  233324 - Add new fields in DEALSDC for EU Tax.               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAS011 - EIR/AC Accounting Upgrade to MP1                    *
      *  CDL033 - Floating Rate CDs Issued                            *
      *  BUG3644 - Serious Error in Collateralised Lending            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R DEALSDCF                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A            RECI      R
     A            DLNO      R
     A            RCDT      R
     A            RCDC      R
      * Begin /CSD027/
     A**********  CNUM      R                                                                 CSD027
     A            CNUM           6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            BRCA      R
     A            DTYP      R
     A            DLST      R
     A            DDAT      R
     A            VDAT      R
     A            MDAT      R
     A            CDAS      R
     A            BRKC      R
     A            BAGE      R
     A            CCY       R
     A            PCPL      R
     A            INTC      R
     A            ZZ006     R
      * Start of change CSD103
     A**********  BRTT      R
     A            BRTT           2A
     A                                      TEXT('Base Rate Code')
     A                                      COLHDG('Base Rate           ' +
     A                                      'Code')
      * End of change CSD103
     A            BRTE      R
     A            RTSP      R
     A            INTR      R
     A            NOTD      R
     A            RNNA      R
      * Begin /CSD027/
     A**********  CHNA      R                                                                 CSD027
     A            CHNA           6A
     A                                      TEXT('Cnt holder of negotiable ass')
     A                                      COLHDG('Cnt hold            ' +
     A                                      'Of neg              ' +
     A                                      'Assets')
      * End /CSD027/
     A            FACT      R
     A            FCNO      R
     A            OPAM      R
     A            BCXR      R
     A            LCXR      R
     A            SDST      R
     A            OSDAQQ        12A
     A            TSTN      R
     A            MDST      R
     A            OMDAQQ        12A
     A            TMAN      R
     A            RODN      R
     A            RODA      R
     A            RBDN      R
     A            RBDA      R
     A            ICBS      R
     A            IPFR      R
     A            TOTI      R
     A            NIDT      R
     A            IBDT      R
     A            SLID      R
     A            AIPD      R
     A            DLRC      R
     A            IACD      R
     A            AITC      R
     A            AIAN      R
     A            AIAP      R
     A            IPRD      R
     A            IPRM      R
     A            ICTD      R
     A            ZZ009     R
     A            DAVD      R
     A            DASN      R
     A            FACO      R
     A            SPI       R
     A            INTF      R
     A            ZZ008     R
     A            TAXI      R
     A            TAXD      R
     A            TAXM      R
     A            DNSI      R
     A            DSTI      R
     A            CNFI      R
     A            ACEI      R
     A            TLXI      R
     A            ZZ009A    R               REFFLD(ZZ009)
     A            F57U      R
     A            LSWC      R
     A            LSWS      R
     A            ZZ002     R
     A            ORED      R
     A            LCD       R
     A            CHTP      R
     A            TNLU      R
     A            DBLN      R
     A            DRID      R
     A            DDMR      R
     A            BOKC      R
     A            LNKDN     R
     A            RSTM      R
     A            RONSQQ        12A
     A            RIBN      R
     A            RIBA      R
      * Begin /CSD027/
     A**********  ROBN      R                                                                 CSD027
     A**********  ROCN      R                                                                 CSD027
     A            ROBN           6A
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            ROCN           6A
     A                                      TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            PSTM      R
     A            PONSQQ        12A
     A            PIBN      R
     A            PIBA      R
      * Begin /CSD027/
     A**********  POBN      R                                                                 CSD027
     A**********  POCN      R                                                                 CSD027
     A            POBN           6A
     A                                      TEXT('pay - ordering bank no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            POCN           6A
     A                                      TEXT('pay - ordering customer no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            RCRN      R
     A            RCRA      R
     A            RVNO      R
     A            AWBN      R
     A            AWBA      R
     A            BENN      R
     A            BENA      R
     A            DTP1      R
     A            DTP2      R
     A            DTP3      R
     A            DTP4      R
     A            DCHG      R
     A            BTB1      R
     A            BTB2      R
     A            BTB3      R
     A            BTB4      R
     A            BTB5      R
     A            BTB6      R
     A            ORBR      R
     A            PRFC      R
     A            ROBR      R
     A            POBR      R
     A            CVMR      R
     A            NACSG     R
     A            JRDFC     R
     A            JFLID     R
     A            FUID      R
     A            AURO      R
     A            IPDS      R
     A            ROFQ      R
     A            RLDN      R
     A            CNNA      R
     A            IMET      R
     A            INSAQQ        15A
     A            INOS      R
     A            WRPI      R
     A            PTFR      R               REFFLD(PTFR)
     A            FXRD      R
     A            HLEX      R
     A            FSRP      R               REFFLD(RTSP)
     A                                      TEXT('Funding Spread/Rate')
     A                                      COLHDG('Funding' +
     A                                             'Spread' +
     A                                             'Rate')
     A            FSGN      R               REFFLD(SPIN)
     A                                      TEXT('Funding Sign')
     A                                      COLHDG('Funding' +
     A                                             'Sign')
     A            FPRC      R               REFFLD(PRFC)
     A                                      TEXT('Funding Profit Centre')
     A                                      COLHDG('Funding' +
     A                                             'Profit' +
     A                                             'Centre')
     A            TCOF      R               REFFLD(AABC)
     A                                      TEXT('Total Cost of Funds')
     A                                      COLHDG('Total' +
     A                                             'Cost of' +
     A                                             'Funds')
     A            ACPD      R               REFFLD(AABC)
     A                                      TEXT('Accrued COF Posted to Date')
     A                                      COLHDG('Accrued' +
     A                                             'Cost of Funds' +
     A                                             'Posted To Date')
     A            ACDA      R               REFFLD(AABC)
     A                                      TEXT('Accrued COF to Control Date')
     A                                      COLHDG('Accrued' +
     A                                             'Cost of Funds' +
     A                                             'to Control Date')
     A            ACAP      R               REFFLD(AABC)
     A                                      TEXT('Accrued COF Adjustmnt Posted')
     A                                      COLHDG('Accrued' +
     A                                             'Cost of Funds' +
     A                                             'Adjustment Posted')
     A            ACAN      R               REFFLD(AABC)
     A                                      TEXT('Accrued COF Adj Not Posted')
     A                                      COLHDG('Accrued Cost of' +
     A                                             'Funds Adjustment' +
     A                                             'Not Posted')
     A            COFD      R               REFFLD(AABC)
     A                                      TEXT('COF Paid/Received to Date')
     A                                      COLHDG('Cost of Funds' +
     A                                             'Paid/Received' +
     A                                             'to Date')
     A            COFM      R               REFFLD(AABC)
     A                                      TEXT('COF Paid/Received this Month')
     A                                      COLHDG('Cost of Funds' +
     A                                             'Paid/Received' +
     A                                             'this Month')
     A            CFCD      R               REFFLD(AABC)
     A                                      TEXT('COF Capitalised to Date')
     A                                      COLHDG('Cost of Funds' +
     A                                             'Capitalised' +
     A                                             'to Date')
     A            FCTD      R               REFFLD(PSTD)
     A                                      TEXT('Funding Accrual +
     A                                            Control Date')
     A                                      COLHDG('Funding Accrual' +
     A                                             'Control Date')
     A            STCY           3          TEXT('Settlement Currency')
     A                                      COLHDG('Settlement' +
     A                                             'Currency')
     A            IC72           3          TEXT('In Currency in +
     A                                           Field 72')
     A                                      COLHDG('In Currency in' +
     A                                             'Field 72')
     A            ISCY           3          TEXT('Interest Currency')
     A                                      COLHDG('Interest' +
     A                                             'Currency')
     A            II72           3          TEXT('Interest In Ccy in +
     A                                           Field 72')
     A                                      COLHDG('Interest In Ccy' +
     A                                             'in Field 72')
     A            RTFC           3          TEXT('Rollover To/From Currency')
     A                                      COLHDG('Rollover To/From' +
     A                                             'Currency')
     A            FCGC           2          TEXT('Fiduciary Charge Code')
     A                                      COLHDG('Fiduciary Charge' +
     A                                             'Code')
     A            FCGD          20          TEXT('Fiduciary Charge Code +
     A                                      Description')
     A                                      COLHDG('Fiduciary Charge' +
     A                                             'Code Description')
     A            FCGA          13  0       TEXT('Fiduciary Charge Amount')
     A                                      COLHDG('Fiduciary' +
     A                                             'Charge Amount')
     A            FCGT           9  0       TEXT('Fiduciary Tax on Charge +
     A                                      Amount')
     A                                      COLHDG('Fiduciary Tax on' +
     A                                             'Charge Amount')
     A            FCMC           2          TEXT('Fiduciary Commission code')
     A                                      COLHDG('Fiduciary Commission' +
     A                                             'Code')
     A            FCMD          20          TEXT('Fiduciary Commission Code +
     A                                      Description')
     A                                      COLHDG('Fiduciary Commission' +
     A                                             'Code Description')
     A            FCMA          13  0       TEXT('Fiduciary Commission Amount')
     A                                      COLHDG('Fiduciary Commission' +
     A                                             'Amount')
     A            FCMT           9  0       TEXT('Fiduciary Tax on Commission +
     A                                      Amount')
     A                                      COLHDG('Fiduciary Tax on' +
     A                                             'Commission Amount')
     A            FTST           1          TEXT('Fiduciary Trust Agreement +
     A                                      Status')
     A                                      COLHDG('FIduciary Trust' +
     A                                             'Agreement Status')
     A            FRN1          60          TEXT('Restriction Narrative 1')
     A                                      COLHDG('Restriction' +
     A                                             'Narrative 1')
     A            FRN2          60          TEXT('Restriction Narrative 2')
     A                                      COLHDG('Restriction' +
     A                                             'Narrative 2')
     A            FRN3          60          TEXT('Restriction Narrative 3')
     A                                      COLHDG('Restriction' +
     A                                             'Narrative 3')
     A            FRN4          60          TEXT('Restriction Narrative 4')
     A                                      COLHDG('Restriction' +
     A                                             'Narrative 4')
     A            FACTC         13  0       TEXT('Accrued Commission to +
     A                                      Control Date')
     A                                      COLHDG('Accrued Commission' +
     A                                             'to Control Date')
     A            FACPD         13  0       TEXT('Accrued Commission +
     A                                      posted to-date')
     A                                      COLHDG('Accrued Commission' +
     A                                             'posted to-date')
     A            FCPRD         13  0       TEXT('Commission Paid/Received +
     A                                      to-date')
     A                                      COLHDG('Commission Paid/' +
     A                                             'Received to-date')
     A            REPI           1          TEXT('Replication Indicator')
     A                                      COLHDG('Replication' 'Indicator')
     A            OMDT           5P 0       TEXT('Original Maturity Date')
     A                                      COLHDG('Original' 'Maturity Date')
     A            PENA          13P 0       TEXT('Penalty Charged')
     A                                      COLHDG('Penalty' 'Charged')
     A            BUSR          10A         TEXT('Breakdown User Id')
     A                                      COLHDG('Breakdown' 'User Id')
     A            PRIN           1A         TEXT('Record Printed')
     A                                      COLHDG('Record' 'Printed')
     A            AUTH           1A         TEXT('Automatic Authorisation')
     A                                      COLHDG('Automatic' 'Authorisation')
     A            TRTP           2          TEXT('Transaction Type')
     A                                      COLHDG('Transaction'          +
     A                                             'Type')
     A            COLF           1          TEXT('Collateral Flag')
     A                                      COLHDG('Collateral Flag')
     A            COLA          15P 0       TEXT('Collateral Amount')
     A                                      COLHDG('Collateral Amount')
     A            FSOYLC         5          TEXT('Our/Buy Yield Curve')
     A                                      COLHDG('Our/Buy' +
     A                                             'Yield Curve')
     A            FSURPL        13P 0       TEXT('Unrealized Profit/Loss')
     A                                      COLHDG('Unrealized' +
     A                                             'Profit/Loss')
     A            FSTUPL        13P 0       TEXT('Today''s Unrealized P/L')
     A                                      COLHDG('Today''s' +
     A                                             'Unrealized P/L')
     A            FSPUPL        13P 0       TEXT('Unrealized P/L Posted +
     A                                           to Date')
     A                                      COLHDG('Unrealized P/L' +
     A                                             'Posted to Date')
     A            FSYUPL        13P 0       TEXT('Unrealized P/L at Year End')
     A                                      COLHDG('Unrealized P/L' +
     A                                             'at Year End')
     A            FSEFUP        13P 0       TEXT('Effective Fair Value +
     A                                            Hedge Unrealized +
     A                                            Profit/Loss Posted')
     A                                      COLHDG('Effective Fair Value' +
     A                                            'Hedge Unrealized' +
     A                                            'Profit/Loss Posted')
     A            FSECUP        13P 0       TEXT('Effective Cash Flow +
     A                                            Hedge Unrealized +
     A                                            Profit/Loss Posted')
     A                                      COLHDG('Effective Cash Flow' +
     A                                            'Hedge Unrealized' +
     A                                            'Profit/Loss Posted')
     A            FSLQPR        11P 7       TEXT('Liquidity/Premium Rate')
     A                                      COLHDG('Liquidity/Premium' +
     A                                      'Rate')
     A            FSLQPS         1A         TEXT('Liquidity/Premium Sign')
     A                                      COLHDG('Liquidity/Premium' +
     A                                      'Sign')
     A            FSAIPN    R               REFFLD(AIPD)
     A                                      TEXT('AIPD Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('AIPD Using' +
     A                                           'Net-Treasury-Rate')
     A            FSIPRN    R               REFFLD(IPRD)
     A                                      TEXT('IPRD Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('IPRD Using' +
     A                                           'Net-Treasury-Rate')
     A            FSAIAN    R               REFFLD(AIAP)
     A                                      TEXT('AIAP Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('AIAP Using' +
     A                                           'Net-Treasury-Rate')
     A            FSAITN    R               REFFLD(AITC)
     A                                      TEXT('AITC Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('AITC Using' +
     A                                           'Net-Treasury-Rate')
     A            FSAINN    R               REFFLD(AIAN)
     A                                      TEXT('AIAN Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('AIAN Using' +
     A                                           'Net-Treasury-Rate')
     A            FSIPMN    R               REFFLD(IPRM)
     A                                      TEXT('IPRM Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('IPRM Using' +
     A                                           'Net-Treasury-Rate')
     A            FSICTN    R               REFFLD(ICTD)
     A                                      TEXT('ICTD Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('ICTD Using' +
     A                                           'Net-Treasury-Rate')
     A            FSTOTN    R               REFFLD(TOTI)
     A                                      TEXT('TOTI Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('TOTI Using' +
     A                                           'Net-Treasury-Rate')
     A            FSPAMT    R               REFFLD(PCPL)
     A                                      TEXT('PCPL Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('PCPL Using' +
     A                                           'Net-Treasury-Rate')
     A            OSDA          18A
     A            OMDA          18A
     A            RONS          18A
     A            PONS          18A
     A            INSA          21A
     A            FSROLI         1A         TEXT('Rollover Indicator')
     A                                      COLHDG('Rollover' 'Indicator')
     A            FSSOFD        20A         TEXT('Source of Funds')
     A                                      COLHDG('Source of Funds')
     A            FSBLKA         1A         TEXT('Blocked Indicator')
     A                                      COLHDG('Blocked' 'Indicator')
     A            FSCAND         5P 0       TEXT('Cancellation Date')
     A                                      COLHDG('Cancellation' 'Date')
     A            FSPNPR         1A         TEXT('Penalty Pay/Receive')
     A                                      COLHDG('Penalty' 'Pay/Receive')
     A            FSFBRT         1A         TEXT('Fix Base Rate')
     A                                      COLHDG('Fix' 'Base' 'Rate')
     A            FSRMAC         1A         TEXT('Receipt - More A/C')
     A                                      COLHDG('Receipt' 'More A/C')
     A            FSPMAC         1A         TEXT('Pay - More A/C')
     A                                      COLHDG('Pay' 'More A/C')
     A            BCAM          15P 0       TEXT('Blocked Collateral Amount')
     A                                      COLHDG('Blocked Collateral' +
     A                                             '      Amount')
      **Start of change CDL033
     A            XKRCDT    R               REFFLD(LCD)
     A                                      TEXT('Rate Change Date')
     A                                      COLHDG('Rate Change Date')
     A            XKRCFR         1A         TEXT('Rate Change Frequency')
     A                                      COLHDG('Rate Change Freq.')
     A            XKRCDM    R               REFFLD(DLUP)
     A                                      TEXT('Rate Change Days')
     A                                      COLHDG('Rate Change Days')
     A            XKSTRF         1A         TEXT('Short Term Rate Flag')
     A                                      COLHDG('Short Term Rate' +
     A                                             'Flag')
     A            XKSTRT    R               REFFLD(INTR)
     A                                      TEXT('Short Term Rate')
     A                                      COLHDG('Short Term Rate')
     A            XKAILC    R               REFFLD(AITC)
     A                                      TEXT('Accrd Int as of +
     A                                            Last Int Pymnt/Chg +
     A                                            Date')
     A                                      COLHDG('Accrd Int as of' +
     A                                             'Last Int Pymnt/Chg' +
     A                                             'Date')
     A            XKACDA    R               REFFLD(AITC)
     A                                      TEXT('Accrd COF as of +
     A                                            Last Int Pymnt/Chg +
     A                                            Date')
     A                                      COLHDG('Accrd COF as of' +
     A                                             'Last Int Pymnt/Chg' +
     A                                             'Date')
     A            XKSTVF         1A         TEXT('Short Term Rate on +
     A                                            Value Date Flag')
     A                                      COLHDG('Short Term Rate' +
     A                                             'on Value Date' 'Flag')
     A            XKBSVF         1A         TEXT('Fixed Base Rate Applied +
     A                                            on Value Date Flag')
     A                                      COLHDG('Fixed Base Rate' +
     A                                             'Applied on' +
     A                                             'Value Date Flag')
     A            XKFXDM    R               REFFLD(DLUP)
     A                                      TEXT('Rate Fix Days')
     A                                      COLHDG('Rate Fix Days')
     A            XKINFD    R               REFFLD(UINFD)
     A                                      TEXT('Rate Fix Date')
     A                                      COLHDG('Rate Fix Date')
     A            XKINFF         1A         TEXT('Rate Fix Date Processed Flag')
     A                                      COLHDG('Rate Fix Date' +
     A                                             'Processed Flag')
     A            XKBSRF    R               REFFLD(INTR)
     A                                      TEXT('Fixed Base Rate')
     A                                      COLHDG('Fixed Base Rate')
     A            XKFRCI    R               REFFLD(CNFI)
     A                                      TEXT('Floating Rate CD Indicator')
     A                                      COLHDG('FRCD' 'Ind')
     A            XKFYLD         5A
     A                                      TEXT('Forward Yield Curve')
     A                                      COLHDG('Forward Yield Curve')
      **End of change CDL033
      **Start of change CAS011
     A            TFDP          15P 0       TEXT('Total Fees/Discount/Premium')
     A                                      COLHDG('Total Fees/Discount/' +
     A                                      'Premium')
     A            TFDPA         15P 0       TEXT('Total Fees/Discount/Premium +
     A                                       Adj. to Yield')
     A                                      COLHDG('Total Fees/Discount/' +
     A                                      'Premium Adj. to' +
     A                                      'Yield')
     A            TFAM          15P 0       TEXT('Total Amortising Fee Adj. +
     A                                       to Yield')
     A                                      COLHDG('Total Amortising Fee' +
     A                                      'Adj. to Yield')
     A            TFAC          15P 0       TEXT('Total Accruing Fee Adj. +
     A                                       to Yield')
     A                                      COLHDG('Total Accruing Fee' +
     A                                      'Adj. to Yield')
     A            TFOF          15P 0       TEXT('Total One off Fee Adj. +
     A                                        to Yield')
     A                                      COLHDG('Total One off Fee' +
     A                                      'Adj. to Yield')
     A            AFDP          15P 0       TEXT('Non-Linear Amortise F/D/P +
     A                                       to Date')
     A                                      COLHDG('Non-Linear Amortise' +
     A                                      'F/D/P to Date')
     A            CFDP          15P 0       TEXT('Accrued F/D/P to Date')
     A                                      COLHDG('Accrued F/D/P' +
     A                                      'to Date')
     A            AMDS          15P 0       TEXT('Non-Linear Amortised +
     A                                       Discount to Date')
     A                                      COLHDG('Non-Linear Amortised' +
     A                                      'Discount to Date')
     A            AMADJ         15P 0       TEXT('Non-Linear Amortisation +
     A                                       Adjustment Posted')
     A                                      COLHDG('Non-Linear' +
     A                                      'Amortisation' +
     A                                      'Adjustment Posted')
     A            ANADJ         15P 0       TEXT('Non-Linear Amortisation +
     A                                       Adjustment not yet Posted')
     A                                      COLHDG('Non-Linear' +
     A                                      'Amortisation' +
     A                                      'Adj. not yet Posted')
     A            DSAM          15P 0       TEXT('Discount amount to be +
     A                                       amortised for the EIR period')
     A                                      COLHDG('Discount amount' +
     A                                      'to be amortised for' +
     A                                      'the EIR period')
     A            AMCOST        15P 0       TEXT('Amortised Cost')
     A                                      COLHDG('Amortised Cost')
     A            YACOST        15P 0       TEXT('Yesterday Amortised Cost')
     A                                      COLHDG('Yesterday Amortised ' +
     A                                      'Cost')
     A            YAINT         15P 0       TEXT('UP/UL Non Linear Amort')
     A                                      COLHDG('UP/UL Non Linear ' +
     A                                      'Amortisation')
      **End of change CAS011
     A*                                              Begin /CDL038/
     A            CLAS           4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
     A**Start of change 233324
     A            EUTO          13P 0       TEXT('EU Tax in Orig Ccy +
     A                                      Deducted to Date')
     A                                      COLHDG('EU Tax in Orig Ccy ' +
     A                                      'Deducted to Date')
     A            EUTB          13P 0       TEXT('EU Tax in Base Ccy +
     A                                      Deducted to Date')
     A                                      COLHDG('EU Tax in Base Ccy ' +
     A                                      'Deducted to Date')
     A            EUTX          13P 0       TEXT('EU Tax in Tax Ccy +
     A                                      Deducted to Date')
     A                                      COLHDG('EU Tax in Tax Ccy ' +
     A                                      'Deducted to Date')
     A            EUCY           3A         TEXT('EU Tax Currency Code')
     A                                      COLHDG('EU Tax Currency Code')
     A            COLC           2A         TEXT('Country of Location')
     A                                      COLHDG('Country of Location')
     A**End of change 233324
     A**Start of change CDL049
     A            FSRRAT        13P 8       TEXT('Reference Rate')
     A                                      COLHDG('Reference Rate')
     A**End of change CDL049
      **Start of change CER020
     A            ISDT      R               REFFLD(FDAT)
     A                                      TEXT('Initial Start Date')
     A                                      COLHDG('Initial Start' +
     A                                             'Date')
      *
     A            FMDT      R               REFFLD(FDAT)
     A                                      TEXT('Final Maturity Date')
     A                                      COLHDG('Final Maturity' +
     A                                             'Date')
      *
     A            ORDN      R               REFFLD(DLNO)
     A                                      TEXT('Original Deal +
     A                                            Number')
     A                                      COLHDG('Original Deal' +
     A                                             'Number')
      **End of change CER020
      **Start of change CER048
     A            STXO          15P 0       TEXT('Secondary Tax in +
     A                                      Orig Ccy Deducted to Date')
     A                                      COLHDG('Secondary Tax' +
     A                                      'in Orig Ccy' +
     A                                      'Deducted to Date')
     A            STXS          15P 0       TEXT('Secondary Tax in +
     A                                      Set Ccy Deducted to Date')
     A                                      COLHDG('Secondary Tax' +
     A                                      'in Set Ccy' +
     A                                      'Deducted to Date')
     A            STXT          15P 0       TEXT('Secondary Tax in +
     A                                      Tax Ccy Deducted to Date')
     A                                      COLHDG('Secondary Tax' +
     A                                      'in Tax Ccy' +
     A                                      'Deducted to Date')
     A            STXB          15P 0       TEXT('Secondary Tax in +
     A                                      Base Ccy Deducted to Date')
     A                                      COLHDG('Secondary Tax' +
     A                                      'in Base Ccy' +
     A                                      'Deducted to Date')
      **End of change CER048
      **Start of change CER054
     A            TTXO          15P 0       TEXT('Third Tax in Orig +
     A                                      Orig Ccy Deducted to Date')
     A                                      COLHDG('Third Tax in' +
     A                                      'Orig Ccy' +
     A                                      'Deducted to Date')
      *
     A            TTXS          15P 0       TEXT('Third Tax in Set +
     A                                      Ccy Deducted to Date')
     A                                      COLHDG('Third Tax in' +
     A                                      'Set Ccy' +
     A                                      'Deducted to Date')
      *
     A            TTXT          15P 0       TEXT('Third Tax in Tax +
     A                                      Ccy Deducted to Date')
     A                                      COLHDG('Third Tax in' +
     A                                      'Tax Ccy' +
     A                                      'Deducted to Date')
      *
     A            TTXB          15P 0       TEXT('Third Tax in Base +
     A                                      Ccy Deducted to Date')
     A                                      COLHDG('Third Tax in' +
     A                                      'Base Ccy' +
     A                                      'Deducted to Date')
      **End of change CER054
      **Start of change CRE073
     A            CNSP          11P 7       TEXT('Contractual Spread')
     A                                      COLHDG('Contractual' 'Spread')
     A            RDMT           7A         TEXT('Rounding Method')
     A                                      COLHDG('Rounding' 'Method')
     A            RDFD           4A         TEXT('Rounding Fraction/Decimal')
     A                                      COLHDG('Rounding' 'Fraction/+
     A                                      Decimal')
      **End of change CRE073
      ** Start of change CDL094
     A            RBTB1         35          TEXT('Recv Bank to Bank Info 1')
     A                                      COLHDG('Recv Bank' 'to Bank Info 1')
     A            RBTB2         35          TEXT('Recv Bank to Bank Info 2')
     A                                      COLHDG('Recv Bank' 'to Bank Info 2')
     A            RBTB3         35          TEXT('Recv Bank to Bank Info 3')
     A                                      COLHDG('Recv Bank' 'to Bank Info 3')
     A            RBTB4         35          TEXT('Recv Bank to Bank Info 4')
     A                                      COLHDG('Recv Bank' 'to Bank Info 4')
     A            RBTB5         35          TEXT('Recv Bank to Bank Info 5')
     A                                      COLHDG('Recv Bank' 'to Bank Info 5')
     A            RBTB6         35          TEXT('Recv Bank to Bank Info 6')
     A                                      COLHDG('Recv Bank' 'to Bank Info 6')
      ** End of change CDL094
      ** Start of change CDL096
     A            C1PC      R               REFFLD(CCYD)
     A                                      TEXT('Currency 1 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 1 for' +
     A                                             'Payment Convention')
     A            C2PC      R               REFFLD(CCYD)
     A                                      TEXT('Currency 2 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 2 for' +
     A                                             'Payment Convention')
     A            C3PC      R               REFFLD(CCYD)
     A                                      TEXT('Currency 3 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 3 for' +
     A                                             'Payment Convention')
     A            C4PC      R               REFFLD(CCYD)
     A                                      TEXT('Currency 4 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 4 for' +
     A                                             'Payment Convention')
     A            C5PC      R               REFFLD(CCYD)
     A                                      TEXT('Currency 5 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 5 for' +
     A                                             'Payment Convention')
     A            C6PC      R               REFFLD(CCYD)
     A                                      TEXT('Currency 6 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 6 for' +
     A                                             'Payment Convention')
     A            C7PC      R               REFFLD(CCYD)
     A                                      TEXT('Currency 7 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 7 for' +
     A                                             'Payment Convention')
     A            C8PC      R               REFFLD(CCYD)
     A                                      TEXT('Currency 8 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 8 for' +
     A                                             'Payment Convention')
     A            C9PC      R               REFFLD(CCYD)
     A                                      TEXT('Currency 9 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 9 for' +
     A                                             'Payment Convention')
     A            C0PC      R               REFFLD(CCYD)
     A                                      TEXT('Currency 10 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 10 for' +
     A                                             'Payment Convention')
     A            PYDC      R               REFFLD(PYTP)
     A                                      TEXT('Payment Date +
     A                                            Convention')
     A                                      COLHDG('Payment Date' +
     A                                             'Convention')
     A            AMTC      R               REFFLD(PYTP)
     A                                      TEXT('Amount +
     A                                            Convention')
     A                                      COLHDG('Amount' +
     A                                             'Convention')
      ** End of change CDL096
      ** Start of change CGL165
     A            TXC1           2A         TEXT('Main Tax Rate Code')
     A                                      COLHDG('Main Tax' 'Rate Code')
     A            TXC2           2A         TEXT('Secondary Tax Rate Code')
     A                                      COLHDG('Secondary Tax' 'Rate Code')
      ** End of change CGL165
      ** Start of change CER050
     A            APRC           1A         TEXT('APR Calculation Required')
     A                                      COLHDG('APR Flag')
      ** End of change CER050
