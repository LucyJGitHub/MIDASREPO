      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  REUSEDLT(*YES)                                               *                       CGP016
/*EXI *  TEXT('Midas GZ Money Market & Loans Deals Extension Detl')   *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZDLDLDCQD - Money Market and Loans Deals Extension Detail*
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGP016             Date 06Aug12               *
      *                 CER049             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAP185             Date 21Apr06               *
      *                 CSD027             Date 05Dec05               *
      *                 CLE040             Date 05Jul04               *
      *                 BUG3035            Date 03Jun04               *
      *                 CLE025  *CREATE    Date 20Oct03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved from GZDDSSRCGM                               *
      *  CER049 - Stamp Tax                                           *
      *  CAP185 - MQ Series Interface                                 *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE040 - Collateralised Lending Phase 2                      *
      *  BUG3035 - Error on calculation when a rate change is done    *
      *  CLE025 - Credit Lines                                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R DLDLDCD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A            F1BRCA         3A         TEXT('Branch Code')
     A                                      COLHDG('Branch Code')
     A            F1DLNO         6A         TEXT('Deal Number')
     A                                      COLHDG('Deal Number')
      * Begin /CSD027/
     A**********  F1FCCU         6S 0       TEXT('Facility Customer')                         CSD027
     A            F1FCCU         6A         TEXT('Facility Customer')
      * End /CSD027/
     A                                      COLHDG('Facility Customer')
     A            F1FACT         3S 0       TEXT('Facility Type')
     A                                      COLHDG('Facility Type')
     A            F1FCNO         2S 0       TEXT('Facility Sequence')
     A                                      COLHDG('Facility Sequence')
      * Begin /CSD027/
     A**********  F1OFCC         6S 0       TEXT('Original Facility Customer')                CSD027
     A            F1OFCC         6A         TEXT('Original Facility Customer')
      * End /CSD027/
     A                                      COLHDG('Original Facility' +
     A                                             '    Customer')
     A            F1OFAC         3S 0       TEXT('Original Facility Type')
     A                                      COLHDG('Original Facility' +
     A                                             '      Type')
     A            F1OFCN         2S 0       TEXT('Original Facility Sequence')
     A                                      COLHDG('Original Facility' +
     A                                             '    Sequence')
     A            F1FACI         1A         TEXT('Facility Attached Yesterday?')
     A                                      COLHDG('Facility Attached' +
     A                                             '   Yesterday?')
     A**Start of change BUG3035
     A            F1TEXO        15P 0       TEXT('Total Exposure/Offset Posted')
     A                                      COLHDG('Total Exposure/' +
     A                                             ' Offset Posted')
     A            F1TEOF        15P 0       TEXT('Total Exposure/Offset Posted +
     A                                            in FCCY')
     A                                      COLHDG('Tot. Exposure/Offset' +
     A                                             '  Posted in FCCY')
     A            F1TEOC        15P 0       TEXT('Total Exposure/Offset Posted +
     A                                            in CA CCY')
     A                                      COLHDG('Tot. Exposure/Offset' +
     A                                             '  Posted in CA CCY')
     A**End of change BUG3035
      **Start of change CLE040
     A            F1BCIN         1A         TEXT('Blocked Collateral Ind.')
     A                                      COLHDG('Blocked Collateral' +
     A                                             'Indicator')
      **End of change CLE040
      **Start of change CAP185
     A            F1FRID        20A         TEXT('Front Office ID')
     A                                      COLHDG('Front' +
     A                                             'Ofc ID')
      **End of change CAP185
      ** Start of change CER049
     A            F1TAX          1A         TEXT('Stamp Tax Liable')
     A                                      COLHDG('Stamp Tax' +
     A                                             'Liable')
     A            F1PRIN         1A         TEXT('Prin Tax Flag')
     A                                      COLHDG('Prin Tax' +
     A                                             'Flag')
     A            F1PDTE         5P 0       TEXT('Prin Tax Start Date')
     A                                      COLHDG('Prin Tax' +
     A                                             'Start Date')
     A            F1AVBL         1A         TEXT('Av. Bal. Flag')
     A                                      COLHDG('Av. Bal.' +
     A                                             'Flag')
     A            F1ADTE         5P 0       TEXT('Av. Bal. Start Date')
     A                                      COLHDG('Av. Bal.' +
     A                                             'Start Date')
     A            F1SINT         1A         TEXT('Int Tax Flag')
     A                                      COLHDG('Int Tax' +
     A                                             'Flag')
     A            F1IDTE         5P 0       TEXT('Int Tax Start Date')
     A                                      COLHDG('Int Tax' +
     A                                             'Start Date')
     A            F1DBRC         3A         TEXT('Debit A/c Branch')
     A                                      COLHDG('Debit A/c' +
     A                                             'Branch')
     A            F1DCST         6A         TEXT('Debit A/c Cust.')
     A                                      COLHDG('Debit A/c' +
     A                                             'Cust.')
     A            F1DCCY         3A         TEXT('Debit A/c Ccy.')
     A                                      COLHDG('Debit A/c' +
     A                                             'Ccy.')
     A            F1DACD        10S 0       TEXT('Debit A/c Code')
     A                                      COLHDG('Debit A/c' +
     A                                             'Code')
     A            F1DASC         2S 0       TEXT('Debit A/c Seq.')
     A                                      COLHDG('Debit A/c' +
     A                                             'Seq.')
     A            F1STAB        13P 0       TEXT('Tax On Av. Bal.')
     A                                      COLHDG('Tax On' +
     A                                             'Av. Bal.')
     A            F1STAI        13P 0       TEXT('Tax On Interest')
     A                                      COLHDG('Tax On' +
     A                                             'Interest')
     A            F1TAXE        13P 0       TEXT('Tax On AB - Month')
     A                                      COLHDG('Tax On' +
     A                                             'AB - Month')
     A            F1DTYP         2A         TEXT('Deal Type')
     A                                      COLHDG('Deal' +
     A                                             'Type')
     A            F1DSTP         2A         TEXT('Deal Sub-Type')
     A                                      COLHDG('Deal' +
     A                                             'Sub-Type')
     A            F1CDTE         5P 0       TEXT('Control Date')
     A                                      COLHDG('Control' +
     A                                             'Date')
      ** End of change CER049
