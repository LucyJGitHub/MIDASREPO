      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_NMMXM32                                                   *
/*EXI *  MBR(OTC)                                                     *
/*EXI *  REUSEDLT(*YES)                                               *                       CGP016
/*EXI *  TEXT('Midas GZ Transaction details - exchange traded')       *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZFFEXTRPD - Transaction Details - Exchange Traded        *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGP016             Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CAP086             Date 08Jun05               *
      *                 CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved from GZDDSSRCGM                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CAP086 - Introduce Auto Authorisation to the API's           *
      *           without it.                                         *
      *  CGP001 - Global Processing                                   *
      *           - New file - Global version of TRANSD.              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R TRANSDF
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A            RECI      R
     A            TNBR      R
     A            BRCA      R
      * Begin /CSD027/
     A**********  BOCO      R                                                                 CSD027
     A**********  CUSC      R                                                                 CSD027
     A            BOCO           6A
     A                                      TEXT('Broker Code')
     A                                      COLHDG('Broker              ' +
     A                                      'Code')
     A            CUSC           6A
     A                                      TEXT('Customer Code')
     A                                      COLHDG('Customer            ' +
     A                                      'Code')
      * End /CSD027/
     A            BOKC      R
     A            ISTT      R
     A            YRNO      R
     A            MTHN      R
     A            PCAL      R
     A            STRP      R               REFFLD(STRPNW)
     A            TRTY      R
     A            ULTT      R
     A            TRSD      R
     A            NUCO      R
     A            NOCO      R
     A            NOBO      R
     A            COPR      R               REFFLD(COPRNW)
     A            TNAR      R
     A            TBLO      R
     A            LNKR      R
     A            CLSR      R
     A            ECPI      R
     A            CBPI      R
     A            ISCY      R
     A            FCDA      R
     A            FCIN      R
     A            FBIN      R
     A            LCD       R
     A            CHTP      R
     A            TNLU      R
     A            ORBR      R
     A            PRFC      R               TEXT('Profit Centre - Non PCA')
     A                                      COLHDG('Profit' 'Centre - Non PCA')
     A            ORED      R
     A            VALD           5S 0       TEXT('Value Date')
     A            PTFR      R
     A            ACNI      R               REFFLD(ACCI)
     A                                      COLHDG('Acc' 'ind')
     A                                      TEXT('Accounting indicator')
     A            MABK      R               REFFLD(AMNT)
     A                                      TEXT('Margin Booked for Broker')
     A                                      COLHDG('Marg' 'Book for' 'Broker')
     A*
     A            MACU      R               REFFLD(AMNT)
     A                                      TEXT('Margin Booked for Customer')
     A                                      COLHDG('Marg' 'Book for' 'Cust')
     A*
     A            CUUN      R               REFFLD(AMNT)
     A                                      TEXT('Current Unrealised')
     A                                      COLHDG('Current' 'Unrealised')
     A*
     A            UNPL      R               REFFLD(AMNT)
     A                                      TEXT('Unrealised P/L Posted')
     A                                      COLHDG('Unrealised' 'P/L posted')
     A*
     A            UPLS      R               REFFLD(AMNT)
     A                                      TEXT('Unrealised P/L Suspens')
     A                                      COLHDG('Unrealised' 'P/L Suspens')
     A            MHEX      R               REFFLD(HLEX)
     A            BPRC      R               REFFLD(PRFC)
     A                                      TEXT('Book Profit Centre')
     A                                      COLHDG('Book' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            TPRC      R               REFFLD(PRFC)
     A                                      TEXT('Trans Profit Centre')
     A                                      COLHDG('Trans' +
     A                                             'Profit' +
     A                                             'Centre')
     A            PYRC      R               REFFLD(ZZ001)
     A                                      TEXT('Pay/Receive Indicator')
     A                                      COLHDG('Pay/Rec' +
     A                                             'Ind.')
      ** Front office transaction ID
     A            FRNT      R               REFFLD(FOTRNID)
 
      ** Associated front office transaction ID
     A            AFRT      R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
 
      ** Transaction repair location
     A            REPA      R               REFFLD(RPRLOC)
 
      ** Timestamp
     A            TMST      R               REFFLD(TIMESTMP)
     A            PLSPTP    R               REFFLD(STRPNW)
     A                                      TEXT('Midas-Plato - Spot Price')
     A                                      COLHDG('PL Spot Price')
     A            CENT           2  0
     A            COVE           1A         TEXT('Covered')
     A                                      COLHDG('Covered')
     A            HETR           1A         TEXT('Hedged/Traded')
     A                                      COLHDG('Hedged/Traded')
     A            BLUP           1A         TEXT('Block Underlying Pos')
     A                                      COLHDG('Block Underlying Pos')
     A            REPI           1A         TEXT('Replication Indicator')
     A                                      COLHDG('Replication Ind')
      *
     A            FSURPL        13P 0       TEXT('Unrealized Profit/Loss')
     A                                      COLHDG('Unrealized' +
     A                                             'Profit/Loss')
     A            FSTUPL        13P 0       TEXT('Todays Unrealized  +
     A                                            Profit/Loss Posting Amount')
     A                                      COLHDG('Todays Unrealized' +
     A                                             'P/L Posting Amt')
     A            FSPUPL        13P 0       TEXT('Previous Unrealized +
     A                                            Profit/Loss')
     A                                      COLHDG('Previous Unrealized' +
     A                                             'Profit/Loss')
     A            FSYUPL        13P 0       TEXT('Unrealized Profit/Loss +
     A                                            at Year-End')
     A                                      COLHDG('Unrealized P/L' +
     A                                             'at Year-End')
     A**Start of change CAP086
     A            AUTH           1A
     A                                      TEXT('Automatic +
     A                                           Authorisation')
     A                                      COLHDG('Automatic' +
     A                                           'Authorisation')
     A**End of change CAP086
