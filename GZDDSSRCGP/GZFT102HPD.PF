      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_SN                                                        *
/*EXI *  REUSEDLT(*YES)                                               *                       CGP016
/*EXI *  TEXT('Midas GZ CCT header')                                  *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZFT102HPD - CCT Header                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CGP016  *MOVED     Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CSW209             Date 01Apr09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CSW207             Date 07Jun07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved from GZDDSSRCGM                               *
      *  CSW209 - SWIFT 2009 Changes                                  *
      *  CSW207 - SWIFT 2007 Changes                                  *
      *  CGP001 - Global Processing                                   *
      *           - New file - Global version of FT102HPD.            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R FT102HD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A            CCTID     R               REFFLD(ACID)
     A                                      TEXT('CCT Id')
     A                                      COLHDG('CCT Id')
     A            STID      R               TEXT('Sender Id')
     A                                      COLHDG('Sender Id')
     A            FILREF    R               REFFLD(ACOR)
     A                                      TEXT('File Reference')
     A                                      COLHDG('File Reference')
     A            CCTTYP    R               REFFLD(PYTP)
     A                                      TEXT('Transfer Type')
     A                                      COLHDG('Transfer Type')
     A            CCTTST    R               REFFLD(PYST)
     A                                      TEXT('Transfer Sub-Type')
     A                                      COLHDG('Transfer Sub-Type')
     A            BRCA      R               TEXT('Booking Branch')
     A                                      COLHDG('Booking Branch')
     A            ORBR      R
     A            BOKC      R
     A            PRFC      R
     A            CCTBOC    R               REFFLD(ACOR)
     A                                      TEXT('Bank Operation Code')
     A                                      COLHDG('Bank Operation +
     A                                      Code')
     A            SINST     R               REFFLD(AWIL)
     A                                      TEXT('Sending Institution')
     A                                      COLHDG('Sending +
     A                                      Institution')
     A            SINSTT    R               REFFLD(TYPG)
     A                                      TEXT('Sending Institution +
     A                                      Type')
     A                                      COLHDG('Sending Inst' +
     A                                      'Customer Type')
     A            CCTDST    R               REFFLD(AWIL)
     A                                      TEXT('Destination')
     A                                      COLHDG('Destination')
     A            DSTTYP    R               REFFLD(TYPG)
     A                                      TEXT('Destination  +
     A                                      Type')
     A                                      COLHDG('Destination' +
     A                                      'Type')
     A            TOCUS1    R               REFFLD(POBA)
     A                                      TEXT('Ordering Customer 1')
     A                                      COLHDG('Ordering Customer +
     A                                      1')
     A            TOCUS2    R               REFFLD(POBA)
     A                                      TEXT('Ordering Customer 2')
     A                                      COLHDG('Ordering Customer +
     A                                      2')
     A            TOCUS3    R               REFFLD(POBA)
     A                                      TEXT('Ordering Customer 3')
     A                                      COLHDG('Ordering Customer +
     A                                      3')
     A            TOCUS4    R               REFFLD(POBA)
     A                                      TEXT('Ordering Customer 4')
     A                                      COLHDG('Ordering Customer +
     A                                      4')
     A            CUSTTH    R               REFFLD(TYPG)
     A                                      TEXT('Ordering Customer +
     A                                      Type')
     A                                      COLHDG('Ordering ' +
     A                                      'Customer Type')
     A            OINST3    R               REFFLD(POBA)
     A                                      TEXT('Ordering +
     A                                      Institution')
     A                                      COLHDG('Ordering +
     A                                      Institution')
     A            OINST1    R               REFFLD(SWID)
     A                                      TEXT('Ordering +
     A                                      Institution')
     A                                      COLHDG('Ordering +
     A                                      Institution')
     A            ORDITP    R               REFFLD(TYPG)
     A                                      TEXT('Ordering Institution +
     A                                      Type')
     A                                      COLHDG('Ordering ' +
     A                                      'Institution Type')
     A            TRNSTP    R               REFFLD(SWRF)
     A                                      TEXT('Transaction Type +
     A                                      Code')
     A                                      COLHDG('Transaction ' +
     A                                      'Type Code')
     A            REGRP1    R               REFFLD(CABN)
     A                                      TEXT('Regulatory +
     A                                      Reporting')
     A                                      COLHDG('Regulatory +
     A                                      Reporting')
     A            REGRP2    R               REFFLD(CABN)
     A                                      TEXT('Regulatory +
     A                                      Reporting')
     A                                      COLHDG('Regulatory +
     A                                      Reporting')
     A            REGRP3    R               REFFLD(CABN)
     A                                      TEXT('Regulatory +
     A                                      Reporting')
     A                                      COLHDG('Regulatory +
     A                                      Reporting')
     A            CCTCHG    R               REFFLD(CHINL)
     A                                      TEXT('Charges')
     A                                      COLHDG('Charges')
     A            CCTRTE    R               REFFLD(EXRATE)
     A                                      TEXT('TFR/PAY Exchange Rate')
     A                                      COLHDG('TFR/Pay X Rate')
     A            BASTFR    R               REFFLD(EXRATE)
     A                                      TEXT('BASE/TFR Exchange Rate')
     A                                      COLHDG('BASE/TFR X Rate')
     A            CCTAMT    R               REFFLD(M5DLAM)
     A                                      TEXT('Transfer Amount (Total)')
     A                                      COLHDG('Total Transfer Amnt')
     A            CCTCCY    R               REFFLD(BCCY)
     A                                      TEXT('Total transfer Currency')
     A                                      COLHDG('Total Transfer CCy')
     A            TTVDTE    R               REFFLD(CHAP)
     A                                      TEXT('Total Transfer +
     A                                      Value Date')
     A                                      COLHDG('Total Transfer ' +
     A                                      'Value Date')
     A            DBTVDT    R               REFFLD(CHAP)
     A                                      TEXT('Debit Value Date')
     A                                      COLHDG('Debit Value Date')
     A            SUMCCT    R               REFFLD(COLH)
     A                                      TEXT('Sum of Amounts')
     A                                      COLHDG('Sum of Amounts')
     A            SUMSCG    R               REFFLD(COLH)
     A                                      TEXT('Sum of Senders +
     A                                      Charges')
     A                                      COLHDG('Sum of Senders ' +
     A                                      'Charges')
     A            SUMRCH    R               REFFLD(COLH)
     A                                      TEXT('Sum of Receivers +
     A                                      Charges')
     A                                      COLHDG('Sum of ' +
     A                                      'Receivers Charges')
     A            SNDCOR    R               REFFLD(RCRA)
     A                                      TEXT('Senders +
     A                                      Correspondent')
     A                                      COLHDG('Senders ' +
     A                                      'Correspondent')
     A            SNDTYP    R               REFFLD(TYPG)
     A                                      TEXT('Senders Correspondent+
     A                                      Type')
     A                                      COLHDG('Senders ' +
     A                                      'Corr Type')
     A            RCVCOR    R               REFFLD(RCRA)
     A                                      TEXT('Receivers +
     A                                      Correspondent')
     A                                      COLHDG('Receivers ' +
     A                                      'Correspondent')
     A            RCVTYP    R               REFFLD(TYPG)
     A                                      TEXT('Receivers Corr       +
     A                                      Type')
     A                                      COLHDG('Receivers' +
     A                                      'Corr Type')
     A            ADNLI1    R               REFFLD(FIN1)
     A                                      TEXT('Additional +
     A                                      Information')
     A                                      COLHDG('Additional ' +
     A                                      'Information')
     A            ADNLI2    R               REFFLD(FIN1)
     A                                      TEXT('Additional +
     A                                      Information')
     A                                      COLHDG('Additional ' +
     A                                      'Information')
     A            ADNLI3    R               REFFLD(FIN1)
     A                                      TEXT('Additional +
     A                                      Information')
     A                                      COLHDG('Additional ' +
     A                                      'Information')
     A            ADNLI4    R               REFFLD(FIN1)
     A                                      TEXT('Additional +
     A                                      Information')
     A                                      COLHDG('Additional ' +
     A                                      'Information')
     A            ADNLI5    R               REFFLD(FIN1)
     A                                      TEXT('Additional +
     A                                      Information')
     A                                      COLHDG('Additional ' +
     A                                      'Information')
     A            ADNLI6    R               REFFLD(FIN1)
     A                                      TEXT('Additional +
     A                                      Information')
     A                                      COLHDG('Additional ' +
     A                                      'Information')
     A            SWPRIO    R               REFFLD(MCIS)
     A                                      TEXT('Swift Priority')
     A                                      COLHDG('Swift Priority')
     A            INUSER    R               REFFLD(RGUD)
     A                                      TEXT('Input User')
     A                                      COLHDG('Input User')
     A            INDATE    R               REFFLD(LASD)
     A                                      TEXT('Input Date')
     A                                      COLHDG('Input Date')
     A            INTIME    R               REFFLD(LATM)
     A                                      TEXT('Input Time')
     A                                      COLHDG('Input Time')
     A            UPDUSR    R               REFFLD(RGUD)
     A                                      TEXT('Update User')
     A                                      COLHDG('Update User')
     A            UPDDTE    R               REFFLD(LASD)
     A                                      TEXT('Update Date')
     A                                      COLHDG('Update Date')
     A            UPDTIM    R               REFFLD(LATM)
     A                                      TEXT('Update Time')
     A                                      COLHDG('Update Time')
     A            AUP1      R               REFFLD(RGUD)
     A                                      TEXT('Authorising User 1')
     A                                      COLHDG('Authorising User +
     A                                      1')
     A            AUP2      R               REFFLD(RGUD)
     A                                      TEXT('Authorising User 2')
     A                                      COLHDG('Authorising User +
     A                                      2')
     A            CDUAL     R               REFFLD(CRLF)
     A                                      TEXT('Dual Authorisation +
     A                                      Required')
     A                                      COLHDG('Dual Auth +
     A                                      Required')
     A            AUIN      R               REFFLD(CRLF)
     A                                      TEXT('Authorised +
     A                                      Indicator')
     A                                      COLHDG('Authorised +
     A                                      Indicator')
     A            BAUTH          4          TEXT('BATCH Authorised +
     A                                      Number')
     A                                      COLHDG('BATCH Authorised' +
     A                                      'Number')
     A            BATUSR    R               REFFLD(RGUD)
     A                                      TEXT('Batch report user')
     A                                      COLHDG('Batch report user')
     A            AKDRIP    R               REFFLD(CRLF)
     A                                      TEXT('Debit Input Date +
     A                                      Account Key')
     A                                      COLHDG('Debit Input ' +
     A                                      'Date Account Key')
     A            AKDRDR    R               REFFLD(CRLF)
     A                                      TEXT('Debit Debit Date +
     A                                      Account Key')
     A                                      COLHDG('Debit Debit ' +
     A                                      'Date Account Key')
     A            ADVCRT    R               REFFLD(CRLF)
     A                                      TEXT('Advice Created')
     A                                      COLHDG('Advice Created')
     A            CCTTDB    R               REFFLD(M5DLAM)
     A                                      TEXT('Total Debit')
     A                                      COLHDG('Total Debit')
     A            GEN102    R               REFFLD(TSKS)
     A                                      TEXT('MT102 Message +
     A                                      Generated')
     A                                      COLHDG('MT102 Message' +
     A                                      'Generated')
     A            AKEY1C    R               REFFLD(TSKS)
     A                                      TEXT('AK Hand Chg Created')
     A                                      COLHDG('AK Hand Chg Crt')
     A            AKEY1L    R               REFFLD(TSKS)
     A                                      TEXT('All AK Generated')
     A                                      COLHDG('All AK Generated')
     A            AKEYID    R               REFFLD(TSKS)
     A                                      TEXT('Account Key +
     A                                      Generated: input date')
     A                                      COLHDG('Account Key ' +
     A                                      'Generated: input ' +
     A                                      'date')
     A            AKEYDD    R               REFFLD(TSKS)
     A                                      TEXT('Account Key +
     A                                      Generated: debit date')
     A                                      COLHDG('Account Key ' +
     A                                      'Generated: debit ' +
     A                                      'date')
     A            AKEYCH    R               REFFLD(TSKS)
     A                                      TEXT('Account Key +
     A                                      Generated: credit date')
     A                                      COLHDG('Account Key ' +
     A                                      'Generated: credit ' +
     A                                      'date')
     A            CSEQNO         3  0       TEXT('Current Sequence +
     A                                      Number')
     A                                      COLHDG('Current Sequence' +
     A                                      'Number')
     A            CHMSGR    R               REFFLD(MSGR)
     A                                      TEXT('CCT Message +
     A                                      reference')
     A                                      COLHDG('CCT Message ' +
     A                                      'reference')
     A            FXSSCD    R               REFFLD(DBFL)
     A                                      TEXT('FX Spread set code.')
     A                                      COLHDG('FX Spread set code.')
     A            SSTCHC    R               REFFLD(DBFL)
     A                                      TEXT('S.W.I.F.T. standard +
     A                                            charge code.')
     A                                      COLHDG('S.W.I.F.T.' +
     A                                             'standard' +
     A                                             'charge code.')
     A            SPRCHC    R               REFFLD(DBFL)
     A                                      TEXT('S.W.I.F.T. priority +
     A                                            charge code.')
     A                                      COLHDG('S.W.I.F.T.' +
     A                                             'priority' +
     A                                             'charge code.')
     A            SWSTAC    R               REFFLD(CHGS)
     A                                      TEXT('S.W.I.F.T. standard +
     A                                            charge.')
     A                                      COLHDG('S.W.I.F.T.' +
     A                                             'standard' +
     A                                             'charge.')
     A            SWPRIR    R               REFFLD(CHGS)
     A                                      TEXT('S.W.I.F.T. priority +
     A                                            charge.')
     A                                      COLHDG('S.W.I.F.T.' +
     A                                             'priority' +
     A                                             'charge.')
     A            VSSTCH    R               REFFLD(CGAS)
     A                                      TEXT('VAT on SWIFT +
     A                                            standard charge.')
     A                                      COLHDG('VAT on SWIFT' +
     A                                             'standard charge.')
     A            VSPRCH    R               REFFLD(CGAS)
     A                                      TEXT('VAT on SWIFT +
     A                                            priority charge.')
     A                                      COLHDG('VAT on SWIFT' +
     A                                             'priority charge.')
      ** Start of Change CSW207
     A            TOCUS5    R               REFFLD(POBA)
     A                                      TEXT('Ordering Customer 5')
     A                                      COLHDG('Ordering Customer +
     A                                      5')
      ** End of Change CSW207
      **Start of change CSW209
     A            CCNWRK         6A         TEXT('Network')
     A                                      COLHDG('Network')
     A            CCMTPY         3A         TEXT('Message Type')
     A                                      COLHDG('Message Type')
     A            CCVALF         8A         TEXT('Incoming Validation Flag')
     A                                      COLHDG('Incoming' 'Validation Flag')
     A            CCOMVF         8A         TEXT('Outgoing Validation Flag +
     A                                      (Main)')
     A                                      COLHDG('Outgoing Validation ' +
     A                                      'Flag (Main)')
     A            CCOCVF         8A         TEXT('Outgoing Validation Flag +
     A                                      (Cover)')
     A                                      COLHDG('Outgoing Validation ' +
     A                                      'Flag (Cover)')
      **End of change CSW209
