      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  REUSEDLT(*YES)                                               *                       CGP016
/*EXI *  TEXT('Midas GZ (F&O) instrument types')                      *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZINTYPD - (F&O) Instrument Types                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CGP016  *MOVED     Date 06Aug12               *
      *  Prev Amend No. CAP208             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved from GZDDSSRCGM                               *
      *  CAP208 - F&O Market Instrument API                           *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGP001 - Global Processing                                   *
      *           - New file - Global version of INTYPD.              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R INTYPDF
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A            RECI      R
     A            ISTT      R
     A            ISTI      R
     A            ISTN      R
     A            ISPT      R
     A            ISCY      R
     A            QOTC      R
     A            TKDM      R
     A            MNPF      R               REFFLD(MNPFNW)
     A            TKVL      R
      * Begin /CSD027/
     A**********  DBRK      R                                                                 CSD027
     A            DBRK           6A
     A                                      TEXT('Default Broker')
     A                                      COLHDG('Default             ' +
     A                                      'Broker')
      * End /CSD027/
     A            BCPT      R
     A            BCMC      R
     A            BCMS      R
     A            BCGP      R
     A            BCGS      R
     A            CCPT      R
     A            CCMC      R
     A            CCMS      R
     A            CCGP      R
     A            CCGS      R
     A            CTAM      R
     A            CMRF      R
     A            OTHC      R
     A            STRC      R
     A            PRRF      R
     A            ULPD      R
     A            FTRF      R
     A            PUPF      R
     A            FTDF      R
     A            SEDF      R
     A            SPMF      R
     A            FTDT      R
     A            SETD      R
     A            SPDT      R
     A            TFPD      R
     A            FSPM      R
     A            ISMQ      R
     A            LCD       R
     A            CHTP      R
     A            TNLU      R
     A            CNTT      R
     A            AEIN      R               REFFLD(IND)
     A                                      TEXT('American/European +
     A                                      option indicator')
     A                                      COLHDG('Amer.' '/Eur.' )
     A            ALPD      R               REFFLD(UPER)
     A                                      TEXT('Actual Underlying Period')
     A            CCAM      R               REFFLD(AMNT)
     A                                      TEXT('Counter Amount')
     A            CTCY      R               REFFLD(CCY)
     A                                      TEXT('Counter Currency')
     A            FTTM      R               REFFLD(STTM)
     A                                      TEXT('Final Transaction Time')
     A            INNR      R
     A            PLMIIT         1A
     A                                      TEXT('Underlying Instrument Type')
     A            PLMITM         4A
     A                                      TEXT('Underlying Term to Maturity')
     A            PLMIMD         5P 0
     A                                      TEXT('Underlying Maturity Date')
     A            PLMICP        11P 7
     A                                      TEXT('Underlying Coupon Rate')
     A            PLMICF         1A
     A                                      TEXT('Underlying Coupon Frequency')
     A            PLMIPC         1A
     A                                      TEXT('Underlying Price Convention')
     A            PLMIVT         1A
     A                                      TEXT('Options Volatility Type')
     A            PLMIPM         1A
     A                                      TEXT('Options Pricing Method')
     A            PLMIPG         8A
     A                                      TEXT('Pricing Group')
     A            PLMIRG         8A
     A                                      TEXT('Reporting Group')
     A            PLMICR         4A
     A                                      TEXT('Credit Rating')
     A            FRTK           1A         TEXT('Fractional Ticks Ind')
     A                                      COLHDG('Frac' 'Ticks' 'Ind')
     A            UNSE          10A         TEXT('Underlying Security')
     A                                      COLHDG('Underlying Security')
     A            TECO           3A         TEXT('Economic Sector')
     A                                      COLHDG('Economic Sector')
     A            TNAT           3A         TEXT('Nature Code')
     A                                      COLHDG('Nature Code')
     A            CRIK           2A         TEXT('Country of Risk')
     A                                      COLHDG('Country of Risk')
     A            CCYR           3A         TEXT('Currency of Risk')
     A                                      COLHDG('Currency of Risk')
     A            TWEI           2S 0       TEXT('Weighting Code')
     A                                      COLHDG('Weighting Code')
     A            FFZONE        10A         TEXT('Zone')
     A                                      COLHDG('Zone')
      ** Start of changes CAP208
     A            FFACTN         1A         TEXT('Action Code')
     A                                      COLHDG('Action' 'Code')
     A            FFTMSP    R               REFFLD(TIMESTMP)
     A                                      TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
      ** End of changes CAP208
