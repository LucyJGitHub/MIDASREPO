      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Loan Events Modeller Rework file')            *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/GZLELEMRPD - Loan Events Modeller Rework File             *
      *                                                               *
      *  (c) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE068  *CREATE    Date 02Aug16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE068 - Loan Event Modeller                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LELEMRWF                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2016')
     A            RECI      R
     A                                      TEXT('Record Indicator')
     A                                      COLHDG('Record' +
     A                                             'Indicator')
     A            BRCA      R
     A                                      TEXT('Loan Branch')
     A                                      COLHDG('Loan' +
     A                                             'Branch')
     A            LNRF           6A         TEXT('Loan number')
     A                                      COLHDG('Loan' 'Number')
     A            ATYP      R               REFFLD(AMTP)
     A                                      TEXT('Loan Event Type')
     A                                      COLHDG('Loan Event' +
     A                                             'Type')
     A            VDAT      R               REFFLD(VDAT0 )
     A                                      TEXT('Value Date')
     A            LASN      R               REFFLD(LASN0)
     A                                      TEXT('Sequence')
     A                                      COLHDG('Sequence')
     A            AMNT      R               REFFLD(DAPRAM)
     A                                      TEXT('Amount')
     A            AUTO      R
     A            SPI1      R
     A            SPI2      R
     A            SPI3      R
     A            STAL           1A
     A                                      TEXT('Settlement Allocation +
     A                                           Indicator')
     A                                      COLHDG('Settlement' +
     A                                           'Allocation Indicator')
     A            RSCY      R               REFFLD(CCY)
     A                                      TEXT('Receipt - Settlement +
     A                                           Currency')
     A                                      COLHDG('Receipt +
     A                                              Sett. ccy')
     A            RSTM      R
     A            RONX          21A
     A                                      TEXT('Receipt - Our Nostro')
     A                                      COLHDG('Receipt             ' +
     A                                      'Our Nostro')
     A            RIBN      R   10A
     A                                      TEXT('Receipt - Intermediary '+
     A                                      'Bank')
     A                                      COLHDG('Receipt             ' +
     A                                      'Intermediary     ' +
     A                                      'Bank')
     A            RIBA      R
     A            ROBN          10A
     A                                      TEXT('Receipt - Ordering Bank')
     A                                      COLHDG('Receipt             ' +
     A                                      'Ordering         ' +
     A                                      'Bank')
     A            ROCN          10A
     A                                      TEXT('Receipt - Ordering Customer')
     A                                      COLHDG('Receipt             ' +
     A                                      'Ordering            ' +
     A                                      'Customer')
     A            REXR          13P 8
     A                                      TEXT('Receipt Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receipt' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A            REXI           1A
     A                                      TEXT('Receipt Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Receipt' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
     A            PSCY           3A
     A                                      TEXT('Pay - Settlement Currency')
     A                                      COLHDG('Pay' 'Settlement' +
     A                                           'Currency')
     A            PSTM      R
     A            PONX          21A
     A                                      TEXT('Pay - Our Nostro')
     A            RVNO          10A
     A                                      TEXT('Receiver number')
     A            CVMR      R               REFFLD(CVMR)
     A                                      TEXT('Cover MT202 required')
     A            POCN          10A
     A                                      TEXT('Pay - Ordering Customer')
     A                                      COLHDG('Pay                 ' +
     A                                      'Ordering            ' +
     A                                      'Customer')
     A            POBN          10A
     A                                      TEXT('Pay - Ordering Bank')
     A                                      COLHDG('Pay                 ' +
     A                                      'Ordering            ' +
     A                                      'Bank')
     A            RCRN          10A
     A                                      TEXT('Receiver Corr.')
     A                                      COLHDG('Receiver Corr.')
     A            RCRA      R               REFFLD(RCRA)
     A                                      TEXT('Receiver Corr. A/C Line')
     A            PIBN          10A
     A                                      TEXT('Pay - Intermediary Bank')
     A                                      COLHDG('Pay                 ' +
     A                                      'Intermediary        ' +
     A                                      'Bank')
     A            PIBA      R
     A            AWBN          10A
     A                                      TEXT('A/C with Bank')
     A                                      COLHDG('A/C with Bank')
     A            AWBA      R
     A            BENN          10A
     A                                      TEXT('Beneficiary')
     A                                      COLHDG('Beneficiary')
     A            BENA      R
     A            DTP1      R
     A            DTP2      R
     A            DTP3      R
     A            DTP4      R
     A            DCHG      R
     A            IC72           3A
     A                                      TEXT('In Currency Field 72')
     A                                      COLHDG('In Currency         ' +
     A                                      'Field 72            ')
     A
     A            BTB1      R
     A            BTB2      R
     A            BTB3      R
     A            BTB4      R
     A            BTB5      R
     A            BTB6      R
     A            PEXR          13P 8
     A                                      TEXT('Pay Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A            PEXI           1A
     A                                      TEXT('Pay Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                          'Currency' 'Exchange Rate'+
     A                                           'Ind')
     A            ASTS           1A
     A                                      TEXT('Loan Amend Status Indicator')
     A                                      COLHDG('Loan Amend ' +
     A                                          'Status Indicator')
     A            CUTN          15A
     A                                      TEXT('Loan UTN')
     A                                      COLHDG('Loan UTN')
     A            LUTN          15A
     A                                      TEXT('Loan Event UTN')
     A                                      COLHDG('Loan Event UTN')
     A            CHGD           1A
     A                                      TEXT('Change Indicator')
     A                                      COLHDG('Change Indicator')
     A            REPT      R               TEXT('Repayment Type')
     A                                      COLHDG('Repayment' +
     A                                             'Type')
     A            PSDAT     R               REFFLD(VDAT0 )
     A                                      TEXT('Period Start Date')
     A            PEDAT     R               REFFLD(VDAT0 )
     A                                      TEXT('Period End Date')
     A            INTER     R
     A                                      REFFLD(CPAM)
     A                                      TEXT('Interest')
     A            PAREP     R
     A                                      REFFLD(CPAM)
     A                                      TEXT('Principal Amount Repayed')
     A            OUTST     R
     A                                      REFFLD(CPAM)
     A                                      TEXT('Outstanding')
     A
     A            CURR      R               REFFLD(CCY)
     A                                      TEXT('Currency')
     A                                      COLHDG('Ccy')
     A            ACINT     R
     A                                      REFFLD(CPAM)
     A                                      TEXT('Accrued Interest')
