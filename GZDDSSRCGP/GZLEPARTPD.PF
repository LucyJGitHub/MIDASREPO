      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  REUSEDLT(*YES)                                               *                       CGP016
/*EXI *  TEXT('Midas GZ Participant details files')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZLEPARTPD - Participant Details Files                    *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CGP016             Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CLE106             Date 01Aug04               *
      *                 CGL029             Date 01Sep03               *
      *                 CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved from GZDDSSRCGM                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *           THE FOLLOWING FILES SHOULD HAVE THE SAME LAYOUT:    *
      *           GZLEPARTPD = LEVRPRTPD = LEVASGNPD                  *
      *  CLE106 - Syndications Manager                                *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGP001 - Global Processing                                   *
      *           - New file - Global version of LEPARTPD.            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LEPARTD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A            BRCA      R
      * Begin /CSD027/
     A**********  CNUM      R                                                                 CSD027
     A            CNUM           6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            FACT      R
     A            FCNO      R
      *
      * Begin /CSD027/
     A**********  MNUM      R               REFFLD(CNUM)                                      CSD027
     A            MNUM           6A
      * End /CSD027/
      **Start of change CLE106
     A**********                            TEXT('Member Customer Number')                    CLE106
     A**********                            COLHDG('Member' +                                 CLE106
     A**********                                   'Customer')                                CLE106
     A                                      TEXT('Assigned to Customer')
     A                                      COLHDG('Assigned to' +
     A                                             'Customer')
      **End of change CLE106
      *
      * Begin /CSD027/
     A**********  INUM      R               REFFLD(CNUM)                                      CSD027
     A            INUM           6A
      * End /CSD027/
      **Start of change CLE106
     A**********                            TEXT('Introduced By')                             CLE106
     A**********                            COLHDG('Introduced By')                           CLE106
     A                                      TEXT('Assigned From Customer')
     A                                      COLHDG('Assigned From' +
     A                                             'Customer')
      **End of change CLE106
      *
     A            JDTE      R               REFFLD(DDAT)
      ** Start of change CLE106
     A**********                            TEXT('Joining Date')
     A**********                            COLHDG('Joining' +
     A**********                                   'Date')
     A                                      TEXT('Assignment Date')
     A                                      COLHDG('Assignment' +
     A                                             'Date')
      **End of change CLE106
      *
     A            CCY       R
      *
     A            SHTP      R               REFFLD(SPIN)
     A                                      TEXT('Share Type')
     A                                      COLHDG('Share' +
     A                                             'Type')
      *
     A            SHPC      R               REFFLD(QTDA)
     A                                      TEXT('Share Percentage')
     A                                      COLHDG('Share' +
     A                                             'Percentage')
      *
     A            SHAM      R               REFFLD(CPAM)
     A                                      TEXT('Share Amount')
     A                                      COLHDG('Share' +
     A                                             'Amount')
      *
      ** Start of change CSD103
     A**********  FEBR      R               REFFLD(FCNO)
     A            FEBR           2A
      ** End of change CSD103
     A                                      TEXT('Fee Base Rate')
     A                                      COLHDG('Fee Base' +
     A                                             'Rate')
      *
     A            FERA      R               REFFLD(INTR)
     A                                      TEXT('Fee Rate/Spread')
     A                                      COLHDG('Fee Rate/Spread')
      *
     A            FERS      R               REFFLD(SPIN)
     A                                      TEXT('Fee Rate Sign')
     A                                      COLHDG('Fee Rate' +
     A                                             'Sign')
      *
     A            PTAI      R               REFFLD(SPIN)
     A                                      TEXT('Participant +
     A                                            Assignment Ind.')
     A                                      COLHDG('Part/Assg' +
     A                                             'Ind?')
      *
     A            DRTP      R               REFFLD(LTYP)
     A                                      TEXT('Default Risk Part +
     A                                            Loan Type')
     A                                      COLHDG('Def Risk' +
     A                                             'Loan Type')
      *
     A            DRST      R               REFFLD(SUTP)
     A                                      TEXT('Default Risk Part +
     A                                            Loan Subtype')
     A                                      COLHDG('Def Risk' +
     A                                             'Loan Styp')
      *
     A            VDTC      R               REFFLD(VDAT)
     A                                      TEXT('Value Date of This +
     A                                            Change')
     A                                      COLHDG('Value Date' +
     A                                             'of this Chg')
      *
     A            OSDB      R               TEXT('Receipt - Settle +
     A                                            Branch')
     A                                      COLHDG('Rec Set' +
     A                                             'Branch')
      *
     A            OMDB      R               TEXT('Pay - Settle +
     A                                            Branch')
     A                                      COLHDG('Pay Set' +
     A                                             'Branch')
      *
     A            RSTM      R
     A**Start of change CGL029
     A**********  RONS      R                                                                 CGL029
     A            RONSQQ        12A
     A**End of change CGL029
     A            RIBN      R
     A            RIBA      R
      * Begin /CSD027/
     A**********  ROBN      R                                                                 CSD027
     A**********  ROCN      R                                                                 CSD027
     A            ROBN           6A
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            ROCN           6A
     A                                      TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            PSTM      R
     A**Start of change CGL029
     A**********  PONS      R                                                                 CGL029
     A            PONSQQ        12A
     A**End of change CGL029
     A            PIBN      R
     A            PIBA      R
      * Begin /CSD027/
     A**********  POBN      R                                                                 CSD027
     A**********  POCN      R                                                                 CSD027
     A            POBN           6A
     A                                      TEXT('pay - ordering bank no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            POCN           6A
     A                                      TEXT('pay - ordering customer no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            RCRN      R
     A            RCRA      R
     A            RVNO      R
     A            AWBN      R
     A            AWBA      R
     A            BENN      R
     A            BENA      R
     A            DTP1      R
     A            DTP2      R
     A            DTP3      R
     A            DTP4      R
     A            DCHG      R
     A            BTB1      R
     A            BTB2      R
     A            BTB3      R
     A            BTB4      R
     A            BTB5      R
     A            BTB6      R
     A            CVMR      R
      *
     A            PCRF      R               REFFLD(ZZ015)
     A                                      TEXT('PC Transaction +
     A                                            Reference')
     A                                      COLHDG('PC Trans' +
     A                                             'Reference')
      *
     A            PCOB      R               REFFLD(BRCA)
     A                                      TEXT('PC Originating +
     A                                            Branch')
     A                                      COLHDG('PC Orig' +
     A                                             'Branch')
      *
     A            SCCY      R               REFFLD(CCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
     A            ICCY      R               REFFLD(CCY)
     A                                      TEXT('IN Currency in +
     A                                            Fld 72')
     A                                      COLHDG('IN Ccy')
     A*                                              Begin /CLE042/
     A            TYLC           1          TEXT('Type of Last Change')
     A                                      COLHDG('Type of' +
     A                                             'Last Change' )
     A*                                              End   /CLE042/
     A            FRNT      R               REFFLD(FOTRNID)
     A            AFRT      R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            REPA      R               REFFLD(RPRLOC)
      ** Timestamp
     A            STMP      R               REFFLD(TIMESTMP)
     A            REXR          13P 8
     A                                      TEXT('Receive Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A            REXI           1A
     A                                      TEXT('Receive Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
     A            PSCY           3A
     A                                      TEXT('Pay Settlement Currency')
     A                                      COLHDG('Pay' 'Settlement' +
     A                                           'Currency')
     A            PEXR          13P 8
     A                                      TEXT('Pay Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A            PEXI           1A
     A                                      TEXT('Pay Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                          'Currency' 'Exchange Rate'+
     A                                           'Ind')
     A**Start of change CGL029
     A            RONS          18A
     A            PONS          18A
     A**End of change CGL029
     A**Start of change CLE106
     A            PRTR           1A
     A                                      TEXT('Participant Type')
     A                                      COLHDG('Participant' +
     A                                           'Type')
     A            AFEE          13P 0
     A                                      TEXT('Agent Fee Amount')
     A                                      COLHDG('Agent Fee' +
     A                                           'Amount')
     A            AGFS           1A
     A                                      TEXT('Agent Fee Amount Sign')
     A                                      COLHDG('Agent Fee' +
     A                                           'Amount Sign')
     A            AFCD           2A
     A                                      TEXT('Agent Fee Code')
     A                                      COLHDG('Agent Fee Code')
     A
     A            ASFE          13P 0
     A                                      TEXT('Assignee Fee Amount')
     A                                      COLHDG('Assignee Fee' +
     A                                           'Amount')
     A            ASFS           1A
     A                                      TEXT('Assignee Fee Amount Sign')
     A                                      COLHDG('Assignee Fee' +
     A                                           'Amount Sign')
     A            ASCD           2A
     A                                      TEXT('Assignee Fee Code')
     A                                      COLHDG('Assignee Fee' +
     A                                           'Code')
     A            GTYP           1A
     A                                      TEXT('Generation Type')
     A                                      COLHDG('Generation' +
     A                                           'Type')
     A            SEQN           2S 0       TEXT('Sequence Number')
     A                                      COLHDG('Sequence Number')
     A**End of change CLE106
     A*                                              Begin /CLE042/
     A            DRCL           4A         TEXT('Default Risk Part +
     A                                            Loan Class')
     A                                      COLHDG('Def Risk' +
     A                                             'Loan Class')
     A*                                              End   /CLE042/
