      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  REUSEDLT(*YES)                                               *                       CGP016
/*EXI *  TEXT('Midas GZ Loan amendments file')                        *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZLOAMSDK - Loan Amendments File                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CGP016  *MOVED     Date 06Aug12               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CLE106             Date 01Aug04               *
      *                 CLE036             Date 19Sep03               *
      *                 CAP086             Date 08Jun05               *
      *                 CGL029             Date 01Sep03               *
      *                 CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved from GZDDSSRCGM                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *           Field RLON may contain loan reference or customer   *
      *           number.                                             *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CLE106 - Syndications Manager. Amended to add Default Sub-   *
      *           participation Loan Type/Sub-type and Base Rate.     *
      *  CLE036 - Lending Enhancements for Nordea Phase 1 Priority 2: *
      *           Allow repayments in facility currency.              *
      *  CAP086 - Introduce Auto Authorisation to the API's           *
      *           without it.                                         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGP001 - Global Processing                                   *
      *           - New file - Global version of LOAMSDK.             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LOAMSDKF                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A            RECI      R
      **Start of change CLE148
     A**********  LNRF      R                                                                 CLE148
     A            LNRF           6A         TEXT('Loan number')
     A                                      COLHDG('Loan' 'Number')
      **End of change CLE148
     A            VDAT      R               REFFLD(VDAT0 )
     A            LASN      R               REFFLD(LASN0 )
     A            MRIN      R
     A            LTYP      R
     A            SUTP      R
     A            PTYP      R
     A            AMTP      R
     A            PRSQ      R
     A            BRTT      R
     A            RTSP      R
     A            CCY       R
     A            PRAM      R
     A            INTA      R
     A            WTXA      R
     A            FEAM      R
     A            TAMT      R
     A            BRCA      R
      * Begin /CSD027/
     A**********  CNUM      R                                                                 CSD027
     A            CNUM           6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            REPT      R
     A            AUTO      R
     A            STAI      R
     A            SETP      R
     A**Start of change CGL029
     A**********  OSAC      R                                                                 CGL029
     A            OSACQQ        12A
     A**End of change CGL029
     A            TSEN      R
     A            FACO      R
     A            SPI1      R
     A            SPI2      R
     A            SPI3      R
     A            LAIN      R
     A            BRTE      R
     A            SPIN      R
     A            CALC      R
     A            WTIN      R
     A            LLAG      R
     A            LWOI      R
     A            XAVD      R               REFFLD(XAVD0 )
     A            XASQ      R
      * Begin /CSD027/
     A**********  FCUS      R                                                                 CSD027
     A            FCUS           6A
     A                                      TEXT('Facility customer')
     A                                      COLHDG('Facility            ' +
     A                                      'Customer')
      * End /CSD027/
     A            FTYP      R               REFFLD(FTYP0 )
     A            FSEQ      R
      * Begin /CSD027/
     A**********  RLON      R                                                                 CSD027
     A            RLON           6A
     A                                      TEXT('Related loan number')
     A                                      COLHDG('Related' 'Loan' 'Number')
      * End  /CSD027/
     A            FECD      R
     A            PONI      R               REFFLD(PONI0 )
     A            ACTI      R
     A            CNFY      R
     A            TLXY      R
     A            CBLY      R
     A            INTC      R
     A            NACD      R
     A            ACBI      R
     A            ZZ001     R
     A            LSWCC     R
     A            LSWSC     R
     A            OSBR      R
     A            FCLB      R
     A            LSMD      R
     A            REBT      R
     A            ZZ034     R
     A            ORED      R
     A            LCD       R
     A            CHTP      R
     A            TNLU      R
     A            RSTM      R
     A**Start of change CGL029
     A**********  RONS      R                                                                 CGL029
     A            RONSQQ        12A
     A**End of change CGL029
     A            RIBN      R
     A            RIBA      R
      * Begin /CSD027/
     A**********  ROBN      R                                                                 CSD027
     A**********  ROCN      R                                                                 CSD027
     A            ROBN           6A
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            ROCN           6A
     A                                      TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            PSTM      R
     A**Start of change CGL029
     A**********  PONS      R                                                                 CGL029
     A            PONSQQ        12A
     A**End of change CGL029
     A            PIBN      R
     A            PIBA      R
      * Begin /CSD027/
     A**********  POBN      R                                                                 CSD027
     A**********  POCN      R                                                                 CSD027
     A            POBN           6A
     A                                      TEXT('pay - ordering bank no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            POCN           6A
     A                                      TEXT('pay - ordering customer no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            RCRN      R
     A            RCRA      R
     A            RVNO      R
     A            AWBN      R
     A            AWBA      R
     A            BENN      R
     A            BENA      R
     A            DTP1      R
     A            DTP2      R
     A            DTP3      R
     A            DTP4      R
     A            DCHG      R
     A            BTB1      R
     A            BTB2      R
     A            BTB3      R
     A            BTB4      R
     A            BTB5      R
     A            BTB6      R
     A            CVMR      R
     A            FSRP      R               REFFLD(RTSP)
     A                                      TEXT('Funding Spread/Rate')
     A                                      COLHDG('Funding' +
     A                                             'Spread' +
     A                                             'Rate')
      *
     A            FSGN      R               REFFLD(SPIN)
     A                                      TEXT('Funding Sign')
     A                                      COLHDG('Funding' +
     A                                             'Sign')
      *
     A            FPRC      R               REFFLD(PRFC)
     A                                      TEXT('Funding Profit Centre')
     A                                      COLHDG('Funding' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            COFA      R               REFFLD(AABC)
     A                                      TEXT('COF Amount')
     A                                      COLHDG('COF' +
     A                                             'Amount' +
     A                                             'Manual Adjustment')
      *
     A            IRCF      R               REFFLD(SPIN)
     A                                      TEXT('Interest Rate Change Flag')
     A                                      COLHDG('Interest Rate' +
     A                                             'Change' +
     A                                             'Flag')
      *
     A            FRCF      R               REFFLD(SPIN)
     A                                      TEXT('Funding Rate Change Flag')
     A                                      COLHDG('Funding Rate' +
     A                                             'Change' +
     A                                             'Flag')
     A            DFTP      R               REFFLD(LTYP)
     A                                      TEXT('Default Part. Loan +
     A                                            Type')
     A                                      COLHDG('Def Part' +
     A                                             'Loan Type')
      *
     A            DFST      R               REFFLD(SUTP)
     A                                      TEXT('Default Part. Loan +
     A                                            Subtype')
     A                                      COLHDG('Def Part' +
     A                                             'Loan Styp')
      *
     A            MNSG      R               REFFLD(SPIN)
     A                                      TEXT('Manual/System +
     A                                            Generated')
     A                                      COLHDG('Man/Sys' +
     A                                             'Gen.')
      *
     A            GASS      R               REFFLD(SPIN)
     A                                      TEXT('Generated by +
     A                                            Assignment')
     A                                      COLHDG('Gen by' +
     A                                             'Assgn?')
      *
     A            GPRT      R               REFFLD(SPIN)
     A                                      TEXT('Generated by +
     A                                            Principal Tfr')
     A                                      COLHDG('Prin' +
     A                                             'Tfr?')
      *
     A            NRLI      R               REFFLD(SPIN)
     A                                      TEXT('New Receiving Loan +
     A                                            Indicator')
     A                                      COLHDG('New Rec' +
     A                                             'Loan?')
      *
      **Start of change CLE148
     A**********  ROLN      R               REFFLD(LNRF)                                      CLE148
     A            ROLN           6A
      **End of change CLE148
     A                                      TEXT('Receiving/Originating +
     A                                            Loan')
     A                                      COLHDG('Rec/Orig' +
     A                                             'Loan')
      *
     A            ROSN      R               REFFLD(XASQ)
     A                                      TEXT('Receiving/Originating +
     A                                            Seq No.')
     A                                      COLHDG('Rec/Orig' +
     A                                             'Seq No')
      *
     A            ROBR      R               REFFLD(BRCA)
     A                                      TEXT('Receiving/Originating +
     A                                            Branch')
     A                                      COLHDG('Rec/Orig' +
     A                                             'Branch')
      *
      * Begin /CSD027/
     A**********  RORC      R               REFFLD(CNUM)                                      CSD027
     A            RORC           6A
      * End /CSD027/
     A                                      TEXT('Receiving/Originating +
     A                                            Cust')
     A                                      COLHDG('Rec/Orig' +
     A                                             'Customer')
      *
     A            PDGN      R               REFFLD(SPIN)
     A                                      TEXT('PD A/C Key Generation')
     A                                      COLHDG('PD A/C' +
     A                                             'Gen')
      *
     A            GRIN      R               REFFLD(SPIN)
     A                                      TEXT('Grace Days Indicator')
     A                                      COLHDG('Grace' +
     A                                             'Days?')
      *
     A            RLCY      R               REFFLD(CCY)
     A                                      TEXT('MC New Currency')
     A                                      COLHDG('MC New' +
     A                                             'Ccy')
      *
     A            PNAM      R               REFFLD(CPAM)
     A                                      TEXT('Penalty Amount')
     A                                      COLHDG('Penalty Amount')
      *
     A            PAST      R               REFFLD(SPIN)
     A                                      TEXT('Past Due Indicator')
     A                                      COLHDG('Past' +
     A                                             'Due?')
      *
     A            ASTS      R               REFFLD(SPIN)
     A                                      TEXT('Loan Amend Status +
     A                                            Indicator')
     A                                      COLHDG('Status')
      *
     A            IUSR      R               REFFLD(FACO)
     A                                      TEXT('Insert User Profile')
     A                                      COLHDG('Ins User' +
     A                                             'Profile')
      *
     A            AUSR      R               REFFLD(FACO)
     A                                      TEXT('Amend User Profile')
     A                                      COLHDG('Amd User' +
     A                                             'Profile')
      *
     A            XUSR      R               REFFLD(FACO)
     A                                      TEXT('Authorise User +
     A                                            Profile')
     A                                      COLHDG('Auth User' +
     A                                             'Profile')
      *
     A            PCRF      R               REFFLD(ZZ015)
     A                                      TEXT('PC Transaction +
     A                                            Reference')
     A                                      COLHDG('PC Trans' +
     A                                             'Reference')
      *
     A            PCOB      R               REFFLD(BRCA)
     A                                      TEXT('PC Originating +
     A                                            Branch')
     A                                      COLHDG('PC Orig' +
     A                                             'Branch')
      *
     A            MTPD      R               REFFLD(SPIN)
     A                                      TEXT('Matched to Past +
     A                                            Due')
     A                                      COLHDG('Mathced' +
     A                                             'Past Due')
      *
     A            PDDI      R               REFFLD(SPIN)
     A                                      TEXT('Payment Due Day +
     A                                            Indicator')
     A                                      COLHDG('Pay Due' +
     A                                             'Day Ind')
      *
     A            PTDI      R               REFFLD(SPIN)
     A                                      TEXT('Payment Taken Day +
     A                                            Indicator')
     A                                      COLHDG('Pay Taken' +
     A                                             'Day Ind')
     A            SCCY      R               REFFLD(CCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
     A            ICCY      R               REFFLD(CCY)
     A                                      TEXT('IN Currency in +
     A                                            Fld 72')
     A                                      COLHDG('IN Ccy')
     A            ECIN           1A         TEXT('Euro Conversion Ind')
     A                                      COLHDG('Euro Conv Ind')
     A            REPI           1          TEXT('Replication Indicator')
     A                                      COLHDG('Replication' 'Indicator')
     A            CHDU      R               REFFLD(AMNT)
     A                                      TEXT('Charge Amount due for +
     A                                            the period')
     A                                      COLHDG('Charge Amount' +
     A                                             'due for the' +
     A                                             'period')
     A            INTN      R               REFFLD(INTA)
     A                                      TEXT('INTA Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('INTA Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
     A            FSPRAM    R               REFFLD(PRAM)
     A                                      TEXT('PRAM Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('PRAM Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
      *
     A            REXR          13P 8
     A                                      TEXT('Receive Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A            REXI           1A
     A                                      TEXT('Receive Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
     A            PSCY           3A
     A                                      TEXT('Pay Settlement Currency')
     A                                      COLHDG('Pay' 'Settlement' +
     A                                           'Currency')
     A            PEXR          13P 8
     A                                      TEXT('Pay Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A            PEXI           1A
     A                                      TEXT('Pay Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                          'Currency' 'Exchange Rate'+
     A                                           'Ind')
      *
     A            STAL           1A
     A                                      TEXT('Settlement Allocation +
     A                                           Indicator')
     A                                      COLHDG('Settlement' +
     A                                           'Allocation Indicator')
      *
     A            FRNT      R               REFFLD(FOTRNID)
     A            AFRT      R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            REPA      R               REFFLD(RPRLOC)
     A            TMST      R               REFFLD(TIMESTMP)
     A**Start of change CGL029
     A            OSAC          18A
     A            RONS          18A
     A            PONS          18A
     A**End of change CGL029
     A**Start of change CAP086
     A            AUTH           1A
     A                                      TEXT('Automatic +
     A                                           Authorisation')
     A                                      COLHDG('Automatic' +
     A                                           'Authorisation')
     A**End of change CAP086
      **Start of change CLE036
     A            FRCY           3A
     A                                      TEXT('Fclty Rept Ccy')
     A                                      COLHDG('Fac ' +
     A                                           'Rept' +
     A                                           'Ccy' )
     A            FRAM      R               REFFLD(CPAM)
     A                                      TEXT('Fclty Ccy Princ Amt')
     A                                      COLHDG('Fac Ccy' +
     A                                              'Principal' +
     A                                              'Amount')
     A            FINT      R               REFFLD(INTA)
     A                                      TEXT('Fclty Ccy Int Amt')
     A                                      COLHDG('Fac Ccy' +
     A                                              'Interest' +
     A                                              'Amount')
     A            FTXA      R               REFFLD(WTXA)
     A                                      TEXT('Fclty Ccy Tax Amt')
     A                                      COLHDG('Fac Ccy' +
     A                                              'Tax' +
     A                                              'Amount')
     A            FTOT      R               REFFLD(TAMT)
     A                                      TEXT('Fclty Ccy Total Amt')
     A                                      COLHDG('Fac Ccy' +
     A                                              'Total' +
     A                                              'Amount')
     A            FPEN      R               REFFLD(CPAM)
     A                                      TEXT('Fclty Ccy Penalty Amt')
     A                                      COLHDG('Fac Ccy' +
     A                                              'Penalty' +
     A                                              'Amount')
      **End of change CLE036
      **Start of change CLE106
     A            BASR          11P 7       TEXT('Base Rate')
     A                                      COLHDG('Base Rate')
     A            SLTP      R               REFFLD(LTYP)
     A                                      TEXT('Default Sub Part. +
     A                                            Loan Type')
     A                                      COLHDG('Def Sub Part' +
     A                                             'Loan Type')
     A            SLST      R               REFFLD(SUTP)
     A                                      TEXT('Default Sub Part. +
     A                                            Loan Subtype')
     A                                      COLHDG('Def Sub Part' +
     A                                             'Loan Styp')
      **End of change CLE106
     A*                                              Begin /CLE042/
     A            CLAS           4A         TEXT('Loan Class')
     A                                      COLHDG('Loan' 'Class')
     A            DFCL           4A         TEXT('Default Part. Loan +
     A                                            Class')
     A                                      COLHDG('Def Part' +
     A                                             'Loan Class')
     A            SLCL           4A         TEXT('Default Sub Part. +
     A                                            Loan Class')
     A                                      COLHDG('Def Sub Part' +
     A                                             'Loan Class')
     A*                                              End   /CLE042/
