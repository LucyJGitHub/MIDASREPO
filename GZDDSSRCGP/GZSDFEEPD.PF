      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  REUSEDLT(*YES)                                               *                       CGP016
/*EXI *  TEXT('Midas GZ Fee codes')                                   *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  PF/GZSDFEEPD - Customer Lending Fee Codes                    *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. CER050             Date 16Jun19               *
      *                 MD046248           Date 27Oct17               *
      *                 CLE164             Date 18Aug14               *
      *                 CGP016             Date 06Aug12               *
      *                 AR1022007          Date 01Aug12               *
      *                 CLE134             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG1381 *CREATE    Date 19Mar04               *
      *                 Xnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  CER050 - Annualised Percentage Rate                          *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE164 - CLE134 Enhancement F (Repayment Methodology)        *
      *  CGP016 - Global database - Re-use deleted records            *
      *         - Moved from GZDDSSRCGM                               *
      *  AR1022007 - CC001 - Incorporate SM007 in PDP                 *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  BUG1381- Global Processing                                   *
      *           - New file - Global version of SDFEEPD.             *
      *                                                               *
      *****************************************************************
     A          R SDFEED0                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A            AUFECD         2          TEXT('Fee Code')
     A                                      COLHDG('Fee' +
     A                                             'Code' )
     A            AULCD          5  0       TEXT('Last Change Date')
     A                                      COLHDG('Last' +
     A                                             'Change Date' )
     A                                      EDTCDE(3)
     A            AUTYLC         1          TEXT('Type of Last Change')
     A                                      COLHDG('Type of' +
     A                                             'Last Change' )
     A            AUFENV        20          TEXT('Fee Narrative')
     A                                      COLHDG('Fee Narrative' )
     A            AUADJY         1          TEXT('Adjustment to Yield')
     A                                      COLHDG('Adjustment to Yield')
     A            AUZONE        10A         TEXT('Zone')
     A                                      COLHDG('Zone')
      **Start of change CLE134
     A            AUCLTY         2A         COLHDG('Past Due' 'Call Loan' +
     A                                             'Loan Type')
     A            AUCLST         2A         COLHDG('Past Due' 'Call Loan' +
     A                                             'Sub Type')
     A            AUCLCL         4A         COLHDG('Past Due' 'Call Loan' +
     A                                             'Class')
      * Start of change CSD103
     A**********  AUCBSR         2S 0       COLHDG('Past Due' 'Default' +       CSD103
     A**********                                   'Base Rate Code')
     A            AUCBSR         2A         COLHDG('Past Due' 'Default' +
     A                                             'Base Rate Code')
      * End of change CSD103
     A            AUCBRT        11P 7       COLHDG('Past Due' 'Default' +
     A                                             'Base Rate')
     A            AUCRSP        11P 7       COLHDG('Past Due' 'Default' +
     A                                             'Spread')
     A            AUCRIN         1A         COLHDG('Spread' 'Rate Indicator')
     A            AUBSRI         1A         COLHDG('Base Rate' +
     A                                             'Change Indicator')
     A            AUICBS         1S 0       COLHDG('Interest' +
     A                                             'Calculation' 'Basis')
     A            AUOVRA         1A         COLHDG('Overdraft' +
     A                                             'Payment' 'Allowed')
     A            AUFAMU         1A         COLHDG('Facility' 'Amount' +
     A                                              'Utilisation')
      *
     A            AUFSFA         1A         COLHDG('Suspend' 'Fee' 'Allowed')
     A            AUSFAM        13P 0       COLHDG('Suspended' 'Fee Amount' +
     A                                             'Maximum')
     A            AUSFPM         3P 0       COLHDG('Suspended' 'Fee Payments' +
     A                                             'Maximum')
      *
     A            AUFPYA         1A         COLHDG('Partial' +
     A                                             'Payment' 'Allowed')
     A            AUPSIN         1A         COLHDG('Partial' 'Indicator')
     A            AUPSLM        13P 0       COLHDG('Partial' 'Limit')
      *
     A            AUFEMC         1A         COLHDG('Early' 'Maturity' +
     A                                             'Condition')
      *
     A            AUGDPR         2S 0       COLHDG('Grace' 'Period')
      *
      **Start of change CLE164
     A**********  AUPCKO         5P 0       COLHDG('Pre-checking' 'Order')                    CLE164
     A            QQAUPC         5P 0       COLHDG('Replaced by AUPGID')
      **End of change CLE164
      **End of change CLE134
      **Start of change AR1022007
     A            AUPCSP        11P 7       COLHDG('Past Due' 'Default' +
     A                                             'Contractual Spread')
     A            AUPCSG         1A         COLHDG('Past Due' 'Default' +
     A                                             'Contractual Sp. Sign')
     A            AUPFMT         7A         COLHDG('Past Due' 'Default' +
     A                                             'Rounding Method')
     A            AUPRFD         4A         COLHDG('Past Due' 'Default' +
     A                                             'Rounding Frac/Dec')
      **End of change AR1022007
      **Start of change CLE164
     A            AUPGID         3A         COLHDG('PDP Priority' 'Group ID')
      **End of change CLE164
      ** Start of change CER050
     A            AUIAPR         1A         COLHDG('Include' 'APR' 'in calc.')
     A            AUMINF        13S 0       COLHDG('Min. Fee Amt')
      ** End of change CER050
