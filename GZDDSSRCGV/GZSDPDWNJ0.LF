      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMWF                                                      *
/*EXI *  TEXT('Midas GZ Midas system pulldown lists (details)')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/GZSDPDWNJ0 - Midas System Pulldown Lists (Details)        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CUP101R            Date 02Nov05               *
      *  Prev Amend No. CUP101             Date 11Jul05               *
      *                 BUG2304            Date 30Apr04               *
      *                 CGP001             Date 23May03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP101R - Recompiled                                         *
      *  CUP101  - This file should share format with GZSDPDWNPD.     *
      *  BUG2304 - Various file layout changes. Recompile.            *
      *  CGP001 - Global Processing                                   *
      *                                                               *
      *****************************************************************
     A                                      UNIQUE
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @PDWNJ0                   JFILE(GZSDPDWNPD GZSCSARWPD +
     A                                      GZSCSARDPD GZSDMMIDPD GZSDPDWHPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(GLZONE GTZONE)
     A                                      JFLD(GLHIND GTHIND)
     A                                      JFLD(GLDIND GTDIND)
     A          J                           JOIN(2 3)
     A                                      JFLD(GTZONE ZONE)
     A                                      JFLD(GTSARN SARN)
     A          J                           JOIN(1 4)
     A                                      JFLD(GLZONE FOZONE)
     A                                      JFLD(GLMMSN FOMMSN)
     A          J                           JOIN(1 5)
     A                                      JFLD(GLZONE GHZONE)
     A                                      JFLD(GLHIND GHHIND)
      *
     A            GHMMID
     A            GLGRUP
     A            GLHIND
     A            GLDIND
     A            GLLANG
     A            GLVDTA
     A            GLDESC
     A            GLMMSN
      *
     A            GTSARN
     A            FOMMIN
     A            GTAVAL
     A            SARN
      *
     A            GLZONE
      *
      *
     A          K GLZONE
     A          K GLHIND
     A          K GLDIND
      *
      * Select if there is no associated switchability and the Midas
      * module is present
      *
     A          S GTSARN                    COMP(EQ ' ')
     A            FOMMIN                    COMP(EQ 'Y')
      *
     A          S GTSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
     A            GTAVAL                    COMP(EQ 'Y')
     A            FOMMIN                    COMP(EQ 'Y')
      *
     A          S GTSARN                    COMP(NE ' ')
     A            SARN                      COMP(EQ ' ')
     A            GTAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
