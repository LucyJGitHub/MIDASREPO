      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas GP Global menu action codes by unique code')     *
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  LF/GZSFMENUJ0 - Menu Action Codes by Unique Code             *
      *                                                               *
      *  (c) Finastra International Limited 2011                      *
      *                                                               *
      *  Last Amend No. AR812504 *CREATE   Date 29Nov23               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR812504 - Incorrect/missing action codes in SF00350P1 and   *
      *             and Midas User Maintenance. Fix is to introduce   *
      *             a global counterpart of SFMENUJ0 used by AMUS.    *
      *             (Child: AR812505)                                 *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @MENUJ0                   JFILE(GZSFMENUPD GZSCSARIPD +
     A                                            GZSCSARDPD GZSDMMIDPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2011')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(MIINDX ADINDX)
      *
     A          J                           JOIN(2 3)
     A                                      JFLD(ADSARN SARN)
      *
     A          J                           JOIN(1 4)
     A                                      JFLD(MIMMSN FOMMSN)
      *
     A            MIINDX
     A            MISECL
     A            MIAAC1
     A            MIAAC2
     A            MIAAC3
     A            MIAAC4
     A            MIAAC5
     A            MIAAC6
     A            MIAAC7
     A            MIAAC8
     A            MIAAC9
     A            MIAAC10
     A            MIALLAC                   CONCAT(MIAAC1 MIAAC2 MIAAC3 +
     A                                      MIAAC4 MIAAC5 MIAAC6 MIAAC7 +
     A                                      MIAAC8 MIAAC9 MIAAC10)
      *
      ** Fields included to allow join to function
      *
     A            ADINDX
     A            ADSARN
     A            ADAVAL
     A            SARN
     A            MIMMSN
     A            FOMMSN
     A            FOMMIN
      *
      ** Key field - Unique Code
      *
     A          K MIINDX
      *
      ** Selection
      *
      ** Select if no SARI record and module is on and has action codes
      *
     A          S ADSARN                    COMP(EQ ' ')
     A            FOMMIN                    COMP(EQ 'Y')
     A            MIALLAC                   COMP(NE '          ')
      *
      ** Select if SARI record and feature present and show if present
      ** and module is on and has action codes
      *
     A          S ADSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
     A            ADAVAL                    COMP(EQ 'Y')
     A            FOMMIN                    COMP(EQ 'Y')
     A            MIALLAC                   COMP(NE '          ')
      *
      ** Select if SARI record and feature not present and show if not
      ** present and module is on and has action codes
      *
     A          S ADSARN                    COMP(NE ' ')
     A            SARN                      COMP(EQ ' ')
     A            ADAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
     A            MIALLAC                   COMP(NE '          ')
