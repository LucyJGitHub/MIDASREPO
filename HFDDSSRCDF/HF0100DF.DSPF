      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas HF HAF items maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility Module
     A*                                                               *
     A*  DSPF/HF0100DF - HF Items Maintenance Screen.                 *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 28Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 083164             Date 03Apr96               *
     A*                 S01379             DATE 18SEP92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Reverse image is showing in Webface without error.  *
     A*  083164 - Any incorrect value entered in item field is        *
     A*           defaulted to zero then error message saying that    *
     A*           the field cannot be zero is diplayed.               *
     A*  S01379 - New screen for HAF.                                 *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      CF03(01 'EXIT')
     A                                      CF12(03)
     A  81                                  CF22(05)
     A  26                                  CF10(27)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A*--------------------------------------------------------------
     A          R HF0100F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      PUTOVR
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                 01 13'HF0100'
     A            SWSID     R        O 02  2REFFLD(WSTI)
     A                                 02 25'Items Maintenance'
     A                                       DSPATR(UL HI)
     A            SRUNA     R        O 01 72REFFLD(RUNA)
     A                                 02 71TIME
     A*
     A                                  4  2'Action'
     A            SACT           1   O  4 10DSPATR(HI)
     A                                       OVRDTA
     A                                  4 16'Item'
     A*********              START OF CHANGE               /*083164*/
     A*********   SITEM          3N 0O  4 22DSPATR(HI)
     A            SITEM          3A  O  4 22DSPATR(HI)
     A*********              END OF CHANGE                 /*083164*/
     A                                       OVRDTA
     A                                  4 29'HAF Item  '
     A            WSFILE         9   O  4 41DSPATR(HI)
     A                                       OVRDTA
      *
     A                                  6  2'Retention period'
     A            SRETA          4N 0B  6 36DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N61N25                               DSPATR(UL)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
      *
     A                                  7  2'Key start position in buffer'
     A            SSTRP          4N 0B  7 36DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N62N25                               DSPATR(UL)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
      *
     A                                  8  2'Key length in buffer'
     A            SLENB          4N 0B  8 36DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N63N25                               DSPATR(UL)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
      *
     A                                  9  2'Item text'
     A            STEXT         25   B  9 36CHECK(LC)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A N64N25                               DSPATR(UL)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
      *
     A                                 10  2'Key text'
     A            STEXK         15   B 10 36CHECK(LC)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A N65N25                               DSPATR(UL)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
      *
     A                                 11  2'COB-Audit'
     A            SAUDI          1A  B 11 36DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N66N25                               DSPATR(UL)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 11 49'Y/N'
      *
     A                                 13  2'File   Enabl. Name'
     A                                 13 27'Text'
      *
     A                                 14  3'1'
     A            SFE1           1A  B 14 11DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N71N25                               DSPATR(UL)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A            SFI1          10A  B 14 16DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A            STX1          50A  O 14 27DSPATR(HI)
     A                                       OVRDTA
      *
     A                                 15  3'2'
     A            SFE2           1A  B 15 11DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N72N25                               DSPATR(UL)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A            SFI2          10A  B 15 16DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A            STX2          50A  O 15 27DSPATR(HI)
     A                                       OVRDTA
      *
     A                                 16  3'3'
     A            SFE3           1A  B 16 11DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N73N25                               DSPATR(UL)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A            SFI3          10A  B 16 16DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A            STX3          50A  O 16 27DSPATR(HI)
     A                                       OVRDTA
      *
     A                                 17  3'4'
     A            SFE4           1A  B 17 11DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N74N25                               DSPATR(UL)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A            SFI4          10A  B 17 16DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A            STX4          50A  O 17 27DSPATR(HI)
     A                                       OVRDTA
      *
     A                                 18  3'5'
     A            SFE5           1A  B 18 11DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N75N25                               DSPATR(UL)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A            SFI5          10A  B 18 16DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A            STX5          50A  O 18 27DSPATR(HI)
     A                                       OVRDTA
      *
     A                                 19  3'6'
     A            SFE6           1A  B 19 11DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N76N25                               DSPATR(UL)
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A            SFI6          10A  B 19 16DSPATR(HI)
     A                                       OVRDTA
     A  25                                  DSPATR(PR)
     A N25                                  DSPATR(UL)
     A            STX6          50A  O 19 27DSPATR(HI)
     A                                       OVRDTA
      *
     A                                 20 02'This file flagged for deletion ton-
     A                                      ight.                            '
      ** Start of change CAAA03
     A**********                            DSPATR(RI HI)                                     CAAA03
     A  28                                  DSPATR(RI HI)
      ** End of change CAAA03
     A N28                                  DSPATR(ND)
     A                                      OVRATR
     A                                 21 02'Subsequent amendment of this file -
     A                                      will stop the system deleting it.'
      ** Start of change CAAA03
     A**********                            DSPATR(RI HI)                                     CAAA03
     A  28                                  DSPATR(RI HI)
      ** End of change CAAA03
     A N28                                  DSPATR(ND)
     A                                      OVRATR
      *
      *-----------------------------------------------------
     A          R HF0100F2                  TEXT('MESSAGE')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Cancel'
     A  81                             23 23'F22=Update'
     A  26                             23 34'F10=Delete'
      *
      *--------------------------------------------------------------
     A          R HF0100F3                  TEXT('SUBFILE DETAILS')
     A                                      SFL
     A            XITEM          3Y 0O  9  2
     A            XFFILE         9A  O  9  7
     A            XTEXT         25A  O  9 18
     A            XDELET        11A  O  9 52
     A N29                                  DSPATR(ND)
     A  29                                  DSPATR(RI)
      *
      *--------------------------------------------------------------
     A          R HF0100F4                  TEXT('SUBFILE CONTROL')
     A                                      SFLCTL(HF0100F3)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      PUTOVR
     A                                      OVERLAY
     A  40                                  SFLDSP
     A                                      SFLDSPCTL
     A  41                                  SFLCLR
     A  31                                  SFLEND
      *
     A                                  7  2'Item'
     A                                      DSPATR(UL)
     A                                  7  7'Item name'
     A                                      DSPATR(UL)
     A                                  7 18'Text                     '
     A                                      DSPATR(UL)
      *
      *--------------------------------------------------------------
     A          R HF0100F5                  TEXT('TOP OF SCREEN')
     A                                      OVERLAY
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                 01 13'HF0100'
     A            SWSID     R        O 02  2REFFLD(WSTI)
     A                                 02 25'Items Maintenance'
     A                                       DSPATR(UL HI)
     A            SRUNA     R        O 01 72REFFLD(RUNA)
     A                                 02 71TIME
      *
     A                                  4  2'Action'
     A            SACT           1A  B  4 11DSPATR(HI)
     A N50                                  DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A                                  4 16'(I,A,D,E)'
      *
     A                                  5  2'Item'
     A*********              START OF CHANGE               /*083164*/
     A*********   SITEM          3N 0B  5 11DSPATR(HI)
     A            SITEM          3A  B  5 11DSPATR(HI)
     A*********              END OF CHANGE                 /*083164*/
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
      *
     A                                  5 16'HAF Item  '
     A            WSFILE         9A  B  5 31DSPATR(HI)
     A N52                                  DSPATR(UL)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
      *
      *-----------------------------------------------------------------------
     A          R HF0100F6                  TEXT('MESSAGE')
     A                                      OVERLAY
     A                                 23  2'F3=Exit   '
      *
      *-----------------------------------------------------------------------
     A          R HF0100S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      *-----------------------------------------------------------------------
     A          R HF0100C0                  TEXT('MESSAGE SFL CTL')
     A                                      OVERLAY
     A                                      SFLCTL(HF0100S0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  59
     AON59                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
