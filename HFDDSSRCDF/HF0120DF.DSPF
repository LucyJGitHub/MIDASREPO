      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas HF Exit routines to call')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility Module
     A*                                                               *
     A*  DSPF/HF0120DF - HF Exit routines to call                     *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 046074             Date 21Oct92               *
      *                 S01379             Date 16Sep92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  046074 - Errors in command key and validation processing     *
     A*  S01379 - New screen for HAF.                                 *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A*                                                    /046074/
     A***********                           CF03(01 'EXIT')
     A***********                           CF05(05 'REFRESH')
     A***********                           CF10(10 'DELETE')
     A***********                           CF12(12 'CANCEL')
     A                                      CF03(03 'EXIT')
     A N20N21                               CF05(05 'REFRESH')
     A  20                                  CF10(10 'DELETE')
     A N15                                  CF12(12 'CANCEL')
     A*                                                    /046074/
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
     A          R HF0120F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                      CLRL(9)
     A                                 12  1' '
     A**************************************************************************
     A          R HF0120F2                  TEXT('DETAILS')
     A                                      OVERLAY
      *
      *
     A*                                                    /046074/
     A***********                      10  2'Program name'
     A*********** EXPGMS        10A  B 10 18
     A N15                             10  2'Program name'
     A            EXPGMS        10A  B 10 18
     A  15                                  DSPATR(ND)
     A*                                                    /046074/
     A N09                                  DSPATR(UL PC MDT)
     A  09                                  DSPATR(PR)
     A  72                                  DSPATR(RI)
     A**************************************************************************
     A          R HF0120F3                  TEXT('SUBFILE ENQUIRY DETAILS')
     A                                      SFL
     A            EXMOD          6A  O 12 02
     A            SXITEM         9A  O 12 12
     A            EXFIL         10A  O 12 25
     A            EXPGM         10A  O 12 39
     A**************************************************************************
     A          R HF0120F4                  TEXT('SUBFILE CONTROL')
     A                                      SFLCTL(HF0120F3)
     A                                      SFLSIZ(200)
     A                                      SFLPAG(9)
     A                                      OVERLAY
     A  06                                  SFLCLR
     A                                      SFLDSPCTL
     A  07                                  SFLDSP
     A  07                                  SFLEND
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                 01 13'HF0120'
     A            SWSID     R        O 02  2REFFLD(WSTI)
     A                                 02 26'Exit Routines to Call'
     A                                       DSPATR(UL HI)
     A            SRUNA     R        O 01 72REFFLD(RUNA)
     A                                 02 71TIME
     A*
     A                                  5  2'Action'
     A            ACT            1A  B  5 18
     A N08                                  DSPATR(UL)
     A  08                                  DSPATR(PR)
     A  80                                  DSPATR(PC RI)
     A                                  5 30'(A,D,E,I)'
     A*
     A                                  7  2'Mode'
     A            EXMODS         6A  B  7 18
     A N08                                  DSPATR(UL)
     A  08                                  DSPATR(PR)
     A  74                                  DSPATR(PC RI)
     A                                  7 30'(IPCBAT IPCONL EODAUD)'
     A                                  8  2'Item'
     A            SITEM          9A  B  8 18
     A N08                                  DSPATR(UL)
     A  08                                  DSPATR(PR)
     A  73                                  DSPATR(PC RI)
     A*
     A                                  9  2'File name'
     A            EXFILS        10A  B  9 18
     A N08                                  DSPATR(UL)
     A  08                                  DSPATR(PR)
     A*                                                    /046074/
     A**79*******
     A*O*76N60***                            DSPATR(PC RI)
     A  79                                  DSPATR(PC RI)
     A*                                                    /046074/
     A**************************************************************************
     A          R HF0120F5                  TEXT('FOOTER')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A*                                                    /046074/
     A***********                      23 11'F5=Refresh'
     A***********                      23 23'F12=Cancel'
     A N20N21                          23 11'F5=Refresh'
     A N15                             23 23'F12=Cancel'
     A*                                                    /046074/
     A  20                             23 35'F10=Delete'
     A**************************************************************************
     A          R HF0120S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
     A*
     A**************************************************************************
     A          R HF0120C0                  TEXT('MESSAGE SFL CTL')
     A                                      OVERLAY
     A                                      SFLCTL(HF0120S0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  59
     AON59                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
