      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas HF Items per user maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility Module
     A*                                                               *
     A*  DSPF/HF0130DF - HF Items by User Maintenance                 *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01379             DATE 22SEP92               *
     A*  PREV AMEND NO. Ennnnn                  ddmmmyy               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  S01379 - New screen for HAF.                                 *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A                                      CA03(01 'EXIT')
     A                                      CF12(05 'INIT')
     A  04                                  CF10(10 'DEL')
      *
      *----------------------------------------------------------------
     A          R HF0130F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  35                                  SFLNXTCHG
     A*
     A            OPTION         1A  B  8  3
     A N54                                  DSPATR(UL)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A  53
     AO 04                                  DSPATR(PR)
     A            SITEM          3N 0O  8  7
     A            SLOGI          9A  O  8 11
     A            STEXT         25A  O  8 23
     A*----------------------------------------------------------------
     A          R HF0130F2                  TEXT('SUBFILE CONTROL')
     A                                      SFLCTL(HF0130F1)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      PUTOVR
     A                                      OVERLAY
     A  60                                  SFLDSP
     A                                      SFLDSPCTL
     A  61                                  SFLCLR
     A  31                                  SFLEND
     A N60                                  ERASE(HF0130F1)
      *
      *
     A                                  7  2'Y/N'
     A                                      DSPATR(UL)
     A                                  7  6'Item'
     A                                      DSPATR(UL)
     A                                  7 11'Item Name'
     A                                      DSPATR(UL)
     A                                  7 23'Text                     '
     A                                      DSPATR(UL)
     A            PRRN1          3  0H      SFLRCDNBR(CURSOR)
     A*----------------------------------------------------------------
     A          R HF0130F3                  TEXT('FOOTER')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Cancel'
     A  04                             23 23'F10=Delete'
     A*----------------------------------------------------------------
     A          R HF0130F4                  TEXT('TOP OF SCREEN')
     A                                      OVERLAY
     A                                      PUTOVR
      *
      *
     A            SUSER     R        O 01 02REFFLD(USER)
     A                                 01 13'HF0130'
     A            SRUNA     R        O 01 72REFFLD(RUNA)
     A            SWSID     R        O 02 02REFFLD(WSID)
     A                                 02 25'Items by User Maintenance'
     A                                       DSPATR(UL HI)
     A                                 02 71TIME
     A*
     A                                  4  2'Action:'
     A            SACT           1A  B  4 11DSPATR(HI)
     A                                      OVRDTA
     A N50                                  DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  50N52                               DSPATR(PC)
     A  52
     AO 04                                  DSPATR(PR)
     A                                  4 13'(I,A,D,E)'
     A*
     A                                  5  2'User..:'
     A            SUSR          10A  B  5 11DSPATR(HI)
     A                                      OVRDTA
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(RI)
     A  51N52                               DSPATR(PC)
     A  52
     AO 04                                  DSPATR(PR)
     A*-----------------------------------------------------------------------
     A          R HF0130S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
     A*
     A*-----------------------------------------------------------------------
     A          R HF0130C0                  TEXT('MESSAGE SFL CTL')
     A                                      OVERLAY
     A                                      SFLCTL(HF0130S0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  59
     AON59                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
