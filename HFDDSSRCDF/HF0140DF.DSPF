      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas HF Reporting groups maint screen 1')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility Module
     A*                                                               *
     A*  DSPF/HF0140DF - Reporting Groups Maintenance Screen          *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01379             DATE 16SEP92               *
     A*  PREV AMEND NO.                         ddmmmyy               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  S01379 - New screen for HAF.                                 *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      CF03(03 'EXIT')
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *--------------------------------------------------------------
     A          R HF0140F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      OVERLAY
     A                                      PUTOVR
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                 01 13'HF0140'
     A            SWSID     R        O 02  2REFFLD(WSTI)
     A                                 02 25'Reporting Group Maintenance'
     A                                       DSPATR(UL HI)
     A            SRUNA     R        O 01 72REFFLD(RUNA)
     A                                 02 71TIME
      *
     A                                  4  2'Action . . :'
     A            SACT           1A  B  4 16DSPATR(HI)
     A N50                                  DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A                                  4 31'(A,I)'
      *
     A                                  6  2'Indicator  :'
     A            SIND           1A  B  6 16DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A                                  6 31'(I,C)'
      *
     A                                  7  2'Item name  :'
     A            SITEM          9A  B  7 16DSPATR(HI)
     A N52                                  DSPATR(UL)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
      *
      *-----------------------------------------------------------------------
     A          R HF0140F2                  TEXT('MESSAGE')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
      *
      *-----------------------------------------------------------------------
     A          R HF0140S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      *-----------------------------------------------------------------------
     A          R HF0140C0                  TEXT('MESSAGE SFL CTL')
     A                                      OVERLAY
     A                                      SFLCTL(HF0140S0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  59
     AON59                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
