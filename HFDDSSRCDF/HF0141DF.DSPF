      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas HF Reporting groups maint screen 2')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility Module
     A*                                                               *
     A*  DSPF/HF0141DF - Reporting Groups Maintenance Screen          *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01379             DATE 29SEP92               *
     A*  PREV AMEND NO.                         ddmmmyy               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  S01379 - New screen for HAF.                                 *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      CF03(01 'EXIT')
     A                                      CF12(03 'CANC')
     A                                      CF05(05 'INIT')
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *--------------------------------------------------------------
      *
     A          R HF0141F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SFL
     A            XOPT           1A  B  8  2
     A N50                                  DSPATR(PC)
     A  50                                  DSPATR(PR)
     A            XNPO           3N 0O  8  4
     A            XUPO          10A  O  8  8
     A            XUSR          10A  O  8 19
     A            XOUQ          10A  O  8 30
     A            XQUL          10A  O  8 41
     A            XCOP           2N 0O  8 52
     A            XHOL           4A  O  8 55
     A            XSAV           4A  O  8 60
     A            XOUP           1N 0O  8 66
      *
      *--------------------------------------------------------------
     A          R HF0141F2                  TEXT('SUBFILE CONTROL')
     A                                      SFLCTL(HF0141F1)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      PUTOVR
     A                                      OVERLAY
     A  40                                  SFLDSP
     A                                      SFLDSPCTL
     A  41                                  SFLCLR
     A  31                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
      *
     A                                  6  2'A No  User'
     A                                  6 19'Usr-data'
     A                                  6 30'Outq-name  Outq-lib'
     A                                  6 51'Cpy Hold Save Pty'
     A                                  7  2'- --  ----'
     A                                  7 19'--------'
     A                                  7 30'-------------------'
     A                                  7 51'--- ---- ---- ---'
     A
      *--------------------------------------------------------------
     A          R HF0141F3                  TEXT('SCREEN 1')
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                 01 13'HF0141'
     A            SWSID     R        O 02  2REFFLD(WSTI)
     A                                 02 25'Reporting Group Maintenance'
     A                                       DSPATR(UL HI)
     A            SRUNA     R        O 01 72REFFLD(RUNA)
     A                                 02 71TIME
      *
     A                                  4  2'Action'
     A            SACT           1   O  4 10DSPATR(HI)
     A                                  4 16'Item name'
     A            SITEM          9A  O  4 27DSPATR(HI)
     A  45                              4 42'EOD-audit'
     A                                      DSPATR(HI)
     A N45                              4 42'Event-proc'
     A                                      DSPATR(HI)
      *
      *--------------------------------------------------------------
     A          R HF0141F4                  TEXT('INPUT LINE')
     A                                      OVERLAY
      *
      *
     A            SOPT           1   O 20  2DSPATR(HI)
      *
     A            SNPO           3N 0B 20  4DSPATR(HI)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(RI)
     A  50N71N72                            DSPATR(PC)
     A  71                                  DSPATR(PR)
      *
      * USER NAME
     A            SUPO          10A  B 20  8DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI)
     A  50 71N72                            DSPATR(PC)
      *
      * USER DATA
     A            SUSR          10A  B 20 19DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI)
     A  62 72                               DSPATR(PC)
      *
      * OUTQ-NAME
     A            SOUQ          10A  B 20 30DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI)
     A  63 72                               DSPATR(PC)
      *
      * OUTQ-LIB
     A            SQUL          10A  B 20 41DSPATR(HI)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(RI)
      *
      * NO. OF COPIES
     A            SCOP           2N 0B 20 52DSPATR(HI)
     A N65                                  DSPATR(UL)
     A  65                                  DSPATR(RI)
     A  65 72                               DSPATR(PC)
      *
      * HOLD SPOOL FILE
     A            SHOL           4A  B 20 55DSPATR(HI)
     A N66                                  DSPATR(UL)
     A  66                                  DSPATR(RI)
     A  66 72                               DSPATR(PC)
      *
      * SAVE SPOOL FILE
     A            SSAV           4A  B 20 60DSPATR(HI)
     A N67                                  DSPATR(UL)
     A  67                                  DSPATR(RI)
     A  67 72                               DSPATR(PC)
      *
      * OUTPUT PRIORITY
     A            SOUP           1N 0B 20 66DSPATR(HI)
     A N68                                  DSPATR(UL)
     A  68                                  DSPATR(RI)
     A  68 72                               DSPATR(PC)
      *                                                                -------
      *-----------------------------------------------------------------------
     A          R HF0141F5                  TEXT('MESSAGE')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F12=Cancel'
      *                                                                -------
      *-----------------------------------------------------------------------
     A          R HF0141F6                  TEXT('CLEAR SCREEN')
     A                                  1  2' '
      *
      *-----------------------------------------------------------------------
     A          R HF0141S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      *-----------------------------------------------------------------------
     A          R HF0141C0                  TEXT('MESSAGE SFL CTL')
     A                                      OVERLAY
     A                                      SFLCTL(HF0141S0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  59
     AON59                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
