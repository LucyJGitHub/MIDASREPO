      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas HF Program updates be omitted')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility Module
     A*                                                               *
     A*  DSPF/HF0150DF - HF Program updates to omit                   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01379             Date 29Sep92               *
      *                                    Date                       *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01379 - New screen for HAF.                                 *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      CF03(01 'EXIT')
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      *--------------------------------------------------------------
     A          R HF0150F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SFL
     A            PGMN          10A  O  8  2
     A            PGMT          50A  O  8 20
      *
      *--------------------------------------------------------------
     A          R HF0150F2                  TEXT('SUBFILE CONTROL')
     A                                      SFLCTL(HF0150F1)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      PUTOVR
     A                                      OVERLAY
     A  40                                  SFLDSP
     A                                      SFLDSPCTL
     A  41                                  SFLCLR
     A  31                                  SFLEND
     A N40                                  ERASE(HF0150F1)
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                 01 13'HF0150'
     A            SWSID     R        O 02  2REFFLD(WSTI)
     A                                 02 25'Program updates to be omitted'
     A                                       DSPATR(UL HI)
     A            SRUNA     R        O 01 72REFFLD(RUNA)
     A                                 02 71TIME
      *
     A                                  4  2'Action'
     A            SACT           1A  B  4 16DSPATR(HI)
     A                                  4 30'(D,I)'
      *
     A                                  5  2'Program'
     A            SPGM          10A  B  5 16DSPATR(HI)
      *
     A                                  7  2'Omit Program'
     A                                      DSPATR(UL)
     A                                  7 20'Text                              -
     A                                                      '
     A                                      DSPATR(UL)
      *
      *                                                                -------
      *-----------------------------------------------------------------------
     A          R HF0150F3                  TEXT('MESSAGE')
     A                                      OVERLAY
     A                                 23  2'F3=Exit   '
      *                                                                -------
      *-----------------------------------------------------------------------
     A          R HF0150S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      *-----------------------------------------------------------------------
     A          R HF0150C0                  TEXT('MESSAGE SFL CTL')
     A                                      OVERLAY
     A                                      SFLCTL(HF0150S0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  59
     AON59                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
