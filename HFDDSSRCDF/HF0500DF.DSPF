      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas HF Select an item Screen 1')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility Module
     A*                                                               *
     A*   DSPF/HF0500DF - Select on item                              *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01379             Date 02Oct92               *
      *                                    Date                       *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01379 - New screen for HAF.                                 *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      CHGINPDFT
     A                                      CF03(01)
     A                                      CF17(02)
     A                                      CF18(03)
     A                                      CF19(04)
     A                                      CF05(05)
     A                                      CF20(06)
     A                                      CF21(07)
     A                                      PRINT(MPRINT)
      *--------------------------------------------------------------
     A          R HF0500F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      OVERLAY
     A                                      PUTOVR
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                 01 13'HF0500'
     A            SWSID     R        O 02  2REFFLD(WSTI)
     A                                 02 25'Selection by Item'
     A                                       DSPATR(UL HI)
     A            SRUNA     R        O 01 72REFFLD(RUNA)
     A                                 02 71TIME
      *
     A                                  4  2'General selection:'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 27'Commit:'
     A            JOCCID        10S 0B  4 36
     A                                      DSPATR(UL)
     A                                  5  7'User:'
     A            JOUSER        10   O  5 14
     A                                      DSPATR(UL)
     A                                  5 27'Program:'
     A            JOPGM         10   O  5 36
     A                                      DSPATR(UL)
     A                                  5 49'Record mode:'
     A            MOD            1   O  5 63
     A                                      DSPATR(UL)
     A                                  5 66'(A,I,C,R)'
     A                                  6  2'Specific selection:'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            TAX1          25A  O  7  8
     A            TBX1          15A  O  7 35
     A            SEL1          25A  O  7 53
      *
     A                                 08  2'Physical files  excluded:'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            S1             1   B 09  2
     A N51                                  DSPATR(ND)
     A N51                                  DSPATR(PR)
     A  51                                  DSPATR(UL)
     A            F1            10   O 09  4
     A N51                                  DSPATR(ND)
      *
     A            S2             1   B 09 15
     A N52                                  DSPATR(ND)
     A N52                                  DSPATR(PR)
     A  52                                  DSPATR(UL)
     A            F2            10   O 09 17
     A N52                                  DSPATR(ND)
      *
     A            S3             1   B 09 28
     A N53                                  DSPATR(ND)
     A N53                                  DSPATR(PR)
     A  53                                  DSPATR(UL)
     A            F3            10   O 09 30
     A N53                                  DSPATR(ND)
      *
     A            S4             1   B 09 41
     A N54                                  DSPATR(ND)
     A N54                                  DSPATR(PR)
     A  54                                  DSPATR(UL)
     A            F4            10   O 09 43
     A N54                                  DSPATR(ND)
      *
     A            S5             1   B 09 54
     A N55                                  DSPATR(ND)
     A N55                                  DSPATR(PR)
     A  55                                  DSPATR(UL)
     A            F5            10   O 09 56
     A N55                                  DSPATR(ND)
      *
     A            S6             1   B 09 67
     A N56                                  DSPATR(ND)
     A N56                                  DSPATR(PR)
     A  56                                  DSPATR(UL)
     A            F6            10   O 09 69
     A N56                                  DSPATR(ND)
      *
      *-----------------------------------------------------
     A          R HF0500F2                  TEXT('MESSAGE')
     A                                 23  2'F3=Exit'
     A                                 23 10'F5=Refresh'
     A                                 23 21'F17=Todays Data'
     A                                 23 38'F18=Historical Data'
     A                                 23 58'F19=Restored Data'
     A                                 22 38'F20=By File'
     A                                 22 53'F21=Processed Entries'
      *--------------------------------------------------------------
     A          R HF0500F3                  TEXT('SUBFILE DETAILS')
     A                                      SFL
     A            OPT            1A  B  7  4DSPATR(UL)
     A            TAX           25A  O  7  8
     A            TBX           15A  O  7 35
     A            SEL           25A  B  7 53DSPATR(UL)
     A            FILN          10A  H
     A            STRP           4N 0H
     A            LENB           4N 0H
      *
      *--------------------------------------------------------------
     A          R HF0500F4                  TEXT('SCREEN HEADER')
     A                                      PUTOVR
     A                                      OVERLAY
     A                                      SFLCTL(HF0500F3)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  41                                  SFLCLR
     A  40                                  SFLDSP
     A  31                                  SFLEND
     A                                      SFLDSPCTL
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                 01 13'HF0500'
     A            SWSID     R        O 02  2REFFLD(WSTI)
     A                                 02 25'Selection by Item'
     A                                       DSPATR(UL HI)
     A            SRUNA     R        O 01 72REFFLD(RUNA)
     A                                 02 71TIME
      *
     A                                  4  2'General selection:'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 27'Commit:'
     A            JOCCID        10S 0B  4 36
     A                                      DSPATR(UL)
     A                                  5  7'User:'
     A            JOUSER        10   B  5 14
     A                                      DSPATR(UL)
     A                                  5 27'Program:'
     A            JOPGM         10   B  5 36
     A                                      DSPATR(UL)
     A                                  5 49'Record mode:'
     A            MOD            1   B  5 63
     A  49                                  DSPATR(RI)
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
     A                                  5 66'(A,I,C,R)'
     A                                  6  2'Specific selection:'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
      *
      *-----------------------------------------------------
     A          R HF0500F5                  TEXT('MESSAGE')
     A                                 22  2'Enter 1 to select'
     A                                 23  2'F3=Exit'
     A                                 23 10'F5=Refresh'
     A                                 23 21'F17=Todays Data'
     A                                 23 38'F18=Historical Data'
     A                                 23 58'F19=Restored Data'
     A                                 22 38'F20=By File'
     A                                 22 53'F21=Processed Entries'
      *
      *-----------------------------------------------------------------------
     A          R HF0500S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      *-----------------------------------------------------------------------
     A          R HF0500C0                  TEXT('MESSAGE SFL CTL')
     A                                      OVERLAY
     A                                      SFLCTL(HF0500S0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  59
     AON59                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
