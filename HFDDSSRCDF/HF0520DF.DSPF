      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas HF Select specified file for enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility Module
     A*                                                               *
     A*  DSPF/HF0520DF - Select Non-permanent Physical File           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 046082             Date 29Oct92               *
      *                 S01379             Date 02Oct92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  046082 - Include validation of the file name.                *
     A*         - Change indicators to correspond to function keys.   *
     A*         - Add message subfile processing.                     *
     A*  S01379 - New screen for HAF.                                 *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
      *                            Start of change /046082/
     A************                          CF03(01)
     A************                          CF13(03)
     A************                          CF12(05)
     A                                      CF03(03)
     A                                      CF13(13)
     A                                      CF12(12)
      *                              End of change /046082/
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A*--------------------------------------------------------------
     A          R HF0520F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                 01 13'HF0520'
     A            SWSID     R        O 02  2REFFLD(WSTI)
     A                                 02 25'Select Non-permanent Physical File'
     A                                       DSPATR(UL HI)
     A            SRUNA     R        O 01 72REFFLD(RUNA)
     A                                 02 71TIME
      *
     A                                  4  2'Selections:'
     A                                      DSPATR(UL)
     A                                  6  4'Physical file:'
     A            NPEF          10   B  6 20
     A                                      DSPATR(UL)
      *                            Start of change /046082/
     A  50                                  DSPATR(RI PC)
     A                                      OVRDTA
      *                              End of change /046082/
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Cancel'
     A                                 23 23'F13=Start '
      *
      *                            Start of change /046082/
      *-----------------------------------------------------------------------
      *
     A          R HF0520S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      *-----------------------------------------------------------------------
      *
     A          R HF0520C0                  TEXT('MESSAGE SFL CTL')
     A                                      OVERLAY
     A                                      SFLCTL(HF0520S0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  59
     AON59                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
      *                              End of change /046082/
