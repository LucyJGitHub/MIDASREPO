      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas HF Display file changes')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility Module
     A*                                                               *
     A*   DSPF/HF0530DF - HF Display file changes                     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *   Last Amend No. 240378            Date 19Jun06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *   Prev Amend No. S01379            Date 16SEP92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *   240378 - To resolve Application Error in Midasplus, insert  *
      *            display attribute (PC) to field SCAO.              *
     A*   S01379 - HISTORY AND AUDIT FACILITY                         *
     A*            NEW FILE CREATED FOR SAR                           *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      CF03(21)
     A                                      CF08(22)
     A                                      CF07(23)
     A                                      CF17(24)
     A                                      CF12(25)
     A                                      CF02(26)
     A                                      CF18(27)
     A                                      CF11(28)
     A                                      CF16(29)
     A                                      CF14(30)
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      **---------------------------------------------------------------
      *
     A          R HF0530F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CLRL(21)
     A                                      SLNO(01)
     A                                      BLINK
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'HF0530'
     A            SWSID     R        O  2  2REFFLD(WSTI)
     A                                  2 25'Display file changes'
     A                                      DSPATR(UL HI)
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A                                  2 71TIME
      *
     A                                  4 10'Changes for file :'
     A                                      DSPATR(HI)
     A            JOOBJ         10   O  4 30
     A                                      DSPATR(RI)
     A  44                                  DSPATR(UL)
     A            JOLIB         10   O  4 43
     A                                      DSPATR(HI)
     A            JOMBR         10   O  4 56
     A                                      DSPATR(HI)
      *
     A                                  6  1'Job :'
     A            JOJOB         10   O  6  8
     A                                      DSPATR(HI)
     A  43                                  DSPATR(UL)
     A            JOUSER        10   O  6 21
     A                                      DSPATR(HI)
     A  42                                  DSPATR(UL)
     A            JONBR          6  0O  6 34
     A                                      DSPATR(HI)
     A  47                                  DSPATR(UL)
     A            JOPGM         10   O  6 43
     A                                      DSPATR(HI)
     A  41                                  DSPATR(UL)
     A            JOCCID        10  0O  6 56EDTCDE(3)
     A                                      DSPATR(HI)
     A  49                                  DSPATR(UL)
     A            KCHG           8   O  6 70
     A                                      DSPATR(HI)
     A  48                                  DSPATR(UL)
      *
     A                                  7  1'Date:'
     A  45                                  DSPATR(UL)
     A            JODATE         6   B  7  8CHANGE(31)
     A                                      DSPATR(HI)
     A                                  7 17'Time:'
     A  46                                  DSPATR(UL)
     A            JOTIME         6  0B  7 24CHANGE(31)
     A                                      DSPATR(HI)
     A                                  7 34'Fields excluded:'
     A            EXOK           3  0O  7 51
      *
     A                                  8  3'Fct'
     A                                      DSPATR(UL)
     A                                  8  7'Field Name/Description'
     A                                      DSPATR(UL)
     A                                  8 30'Before/After Image................+
     A                                      ......'
     A                                      DSPATR(UL)
     A                                  8 73'Offset'
     A                                      DSPATR(UL)
      *
      **---------------------------------------------------------------
      *
     A          R HF0530F2                  TEXT('FILE IMAGES SFL')
     A                                      SFL
      *
     A            WINDOW         3A  B  9  3
     A  14                                  DSPATR(ND PR)
     A  16                                  DSPATR(HI)
     A            NAME          20A  O  9  7
     A N02                                  DSPATR(HI)
     A  02                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A            JOUT          40A  O  9 30
     A  02                                  DSPATR(RI)
     A            SCAO           5   O  9 73
      **Start of change 240378
     A                                      DSPATR(PC)
      **End of change 240378
      *
      **---------------------------------------------------------------
      *
     A          R HF0530F3                  TEXT('FILE IMAGES CTL')
     A                                      SFLCTL(HF0530F2)
     A                                      SFLSIZ(013)
     A                                      SFLPAG(012)
     A  96                                  SFLDSP
     A  95                                  SFLDSPCTL
     A N96                                  SFLCLR
     A                                      OVERLAY
     A            ZEILE          3  0H      SFLRCDNBR
     A                                 22  2'F2=Save'
     A                                 22 14'F3=Exit'
     A                                 22 26'F7=Prev'
     A                                 22 38'F8=Next'
     A                                 22 50'F11=Print'
     A                                 22 62'F12=Cancel'
     A                                 23  2'F14=Defn'
     A                                 23 14'F16=Scan'
     A                                 23 26'F17=Incl'
     A N15                             23 38'F18=Change'
     A  15                             23 38'F18=Order'
     A            DRRN           6  0O 23 62
     A            RTOT           6  0O 23 72
      *
      **----------------------------------------------------------------------
      *
     A          R HF0530S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      **----------------------------------------------------------------------
      *
     A          R HF0530C0                  TEXT('MESSAGE SFL CTL')
     A                                      OVERLAY
     A                                      SFLCTL(HF0530S0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  59
     AON59                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
