      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas HF Print parameters for print')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility Module
     A*                                                               *
     A*  DSPF/HF0540DF - HF Print parameters for print                *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01379             DATE 16SEP92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  S01379 - HISTORY AND AUDIT FACILITY                          *
     A*           NEW FILE CREATED FOR SAR                            *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      CHGINPDFT
     A                                      CF12(12 'CANCEL')
     A                                      CF11(11 'PRINT')
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      **---------------------------------------------------------------
      *
     A          R HF0540F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A                                      PUTOVR
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'HF0540'
     A            SWSID     R        O  2  2REFFLD(WSTI)
     A                                  2 25'Select Print Parameters'
     A                                       DSPATR(UL HI)
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A                                  2 71TIME
      *
     A                                  4  4'Start printing at......:'
     A                                      DSPATR(HI)
     A            START          8   B  4 30
     A                                      OVRDTA
     A  51                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A                                  4 50'*TOP/*CURRENT'
      *
     A                                  5  4'Nbr of recs to print...:'
     A                                      DSPATR(HI)
     A            NPRI           6  0B  5 30EDTCDE(Z)
     A                                      OVRDTA
     A  52                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A                                  5 50'<NUMBER>'
      *
     A                                  6  4'Consider scan conds....:'
     A                                      DSPATR(HI)
     A            COSC           1   B  6 30
     A                                      OVRDTA
     A  53                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A                                  6 50'Y/N'
      *
     A                                  7  4'Which fields to print..:'
     A                                      DSPATR(HI)
     A            PRITEM         8   B  7 30
     A                                      OVRDTA
     A  54                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A                                  7 50'*ALL/*CHANGED'
      *
     A                                  9  4'Output queue...........:'
     A                                      DSPATR(HI)
     A            OUTQN         10   B  9 30
     A                                      OVRDTA
     A  55                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A                                  9 50'Name/Library'
      *
     A            OUTQL         10   B 10 35
     A                                      OVRDTA
     A  55                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
      *
     A                                 11  4'Nbr of copies to print.:'
     A                                      DSPATR(HI)
     A            COPI           2  0B 11 30EDTCDE(Z)
     A                                      OVRDTA
     A  56                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A                                 11 50'<NUMBER>'
      *
     A                                 12  4'Hold Spool file ? .....:'
     A                                      DSPATR(HI)
     A            HOLD           4   B 12 30
     A                                      OVRDTA
     A  57                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A                                 12 50'*YES/*NO'
      *
     A                                 13  4'Save Spool file ? .....:'
     A                                      DSPATR(HI)
     A            SAVE           4   B 13 30
     A                                      OVRDTA
     A  58                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A                                 13 50'*YES/*NO'
      *
     A                                 23  2'F11=Print'
     A                                 23 13'F12=Cancel'
      *
      **----------------------------------------------------------------------
      *
     A          R HF0540S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      **----------------------------------------------------------------------
      *
     A          R HF0540C0                  TEXT('MESSAGE SFL CTL')
     A                                      OVERLAY
     A                                      SFLCTL(HF0540S0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  59
     AON59                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
