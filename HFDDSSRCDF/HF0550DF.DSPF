      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas HF Scan parameters')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility Module
     A*                                                               *
     A*  DSPF/HF0550DF - HF Input Scan Parameters                     *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 046094             Date 28Oct92               *
      *                 S01379             Date 22Sep92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  046094 - Change text from 'Scan file name' to                *
     A*           'Scan condition name'                               *
     A*  S01379 - New screen for HAF.                                 *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A                                      CF12
     A  50                                  CF14
     A N50                                  CF22
      *
      *----------------------------------------------------------------
      *
     A          R HF0550F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  10                                  SFLNXTCHG
      *
     A            REVERS         1   B  5  1
     A N31                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
     A            AOR            1   B  6  3
     A  07                                  DSPATR(PR)
     A  21                                  DSPATR(RI PC)
     A N21N07                               DSPATR(UL)
     A            FIELD         10   B  6  6
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(UL)
     A            FILE          10   B  6 19
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(UL)
     A            TEST1          2   B  6 32
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(UL)
     A            VAL1          40   B  6 37
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(UL)
     A            FTEX          20   B  7  6
     A                                      DSPATR(PR)
     A                                      DSPATR(HI)
     A            TEST2          2   B  7 32
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(UL)
     A            VAL2          40   B  7 37
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(UL)
      *
      *----------------------------------------------------------------
     A          R HF0550F2                  TEXT('SUBFILE CONTROL')
     A                                      SFLCTL(HF0550F1)
     A                                      SFLSIZ(16)
     A                                      SFLPAG(4)
     A                                      PUTOVR
     A                                      OVERLAY
     A  02                                  SFLDSP
     A  03                                  SFLDSPCTL
     A  04                                  SFLCLR
     A  06                                  SFLINZ
     A  08                                  SFLEND
     A                                      SFLDROP(CF06)
     A                                      SFLRNA
      *
      *
     A            DSPREC         4  0H      SFLRCDNBR
      *
     A            SUSER     R        O 01 02REFFLD(USER)
     A                                 01 13'HF0550'
     A            SRUNA     R        O 01 72REFFLD(RUNA)
     A            SWSID     R        O 02 02REFFLD(WSID)
     A                                 02 25'Define Scan Conditions:'
     A                                       DSPATR(UL HI)
     A            SCAPAR        10   O  2 50DSPATR(RI)
     A                                 02 71TIME
     A                                  3 03'A/O FIELD'
     A                                  3 19'FILE'
     A                                  3 32'TEST VALUE BEFORE/AFTER'
      *
      *----------------------------------------------------------------
     A          R HF0550F3                  TEXT('FOOTER')
     A                                      OVERLAY
     A                                 23  2'F12=Cancel'
     A  50                             23 14'F14=Definitions'
     A N50                             23 31'F22=Accept'
      *
     A*----------------------------------------------------------------
     A          R HF0550F4                  TEXT('TOP OF SCREEN')
     A                                      OVERLAY
     A                                      PUTOVR
      *
      *
     A            SUSER     R        O 01 02REFFLD(USER)
     A                                 01 13'HF0550'
     A            SRUNA     R        O 01 72REFFLD(RUNA)
     A            SWSID     R        O 02 02REFFLD(WSID)
     A                                 02 25'Define Scan Conditions File'
     A                                       DSPATR(UL HI)
     A                                 02 71TIME
      *                            Start of change /046094/
     A***********                       6  2'Scan file name...........:'
     A                                  6  2'Scan condition name......:'
      *                              End of change /046094/
     A                                      DSPATR(HI)
     A            SCAPAR        10   B  6 30
     A                                      DSPATR(UL)
     A                                  8  2'Number of records to scan:'
     A                                      DSPATR(HI)
     A            MAXSCN         6  0B  8 30
     A                                      DSPATR(UL)
      *
      *-----------------------------------------------------------------------
     A          R HF0550S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      *-----------------------------------------------------------------------
     A          R HF0550C0                  TEXT('MESSAGE SFL CTL')
     A                                      OVERLAY
     A                                      SFLCTL(HF0550S0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  59
     AON59                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
