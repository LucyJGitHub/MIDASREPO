      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas HF Include/exclude fields')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility Module
     A*                                                               *
     A*   DSPF/HF0560DF - Include/Exclude Fields Screen               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    LAST AMEND NO. S01379       DATE 05OCT92                   *
     A*    PREV AMEND NO.                   ddmmmyy                   *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  S01379 - New screen for HAF.                                 *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      PRINT(MPRINT)
     A                                      CF12(21)
     A                                      INDARA
     A*--------------------------------------------------------------
     A          R HF0560F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                      CLRL(22)
     A                                      SLNO(01)
     A                                      BLINK
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                 01 13'HF0560'
     A            SWSID     R        O 02  2REFFLD(WSTI)
     A                                 02 25'Include/Exclude Fields'
     A                                       DSPATR(UL HI)
     A            SRUNA     R        O 01 72REFFLD(RUNA)
     A                                 02 71TIME
     A*
     A                                  5  3'Fct'
     A                                      DSPATR(UL)
     A                                  5  7'Field Name'
     A                                      DSPATR(UL)
     A                                  5 20'Text'
     A                                      DSPATR(UL)
     A                                  5 43'File'
     A                                      DSPATR(UL)
     A*--------------------------------------------------------------
      *
     A          R HF0560F2                  SFL
     A            WINDOW         1A  B  6  3
     A  16                                  DSPATR(HI)
     A            NAME          10A  O  6  7
     A  06                                  DSPATR(HI)
     A            TEXT          20A  O  6 20
     A            FILE          10A  O  6 43
     A            EXCL          12A  O  6 56
     A N06                                  DSPATR(ND)
     A*--------------------------------------------------------------
      *
     A          R HF0560F3                  SFLCTL(HF0560F2)
     A                                      SFLSIZ(017)
     A                                      SFLPAG(016)
     A  96                                  SFLDSP
     A  95                                  SFLDSPCTL
     A N96                                  SFLCLR
     A                                      OVERLAY
     A            ZEILE          3  0H      SFLRCDNBR
     A                                 23  2'F12=Cancel'
     A*
     A*-----------------------------------------------------------------------
     A          R HF0560S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
     A*
     A*-----------------------------------------------------------------------
     A          R HF0560C0                  TEXT('MESSAGE SFL CTL')
     A                                      OVERLAY
     A                                      SFLCTL(HF0560S0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  59
     AON59                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
