      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas HF Midas information screen')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility                       *
     A*                                                               *
     A*  HFC0010DF - MIDAS INFORMATION SCREEN                         *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 071613             Date 06Jun94               *
      *                 S01379             Date 22Sep92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  071613 - Remove 'BIS' references.                            *
     A*  S01379 - Created for HAF product.                            *
     A*                                                               *
     A*****************************************************************
      *
      *
      ******************************************
      * FORMAT     ¦ USED BY PGM ¦ INDICATORS  *
      ******************************************
      * BLANK                                  *
      *========================================*
      * ENTER                                  *
      *========================================*
      * JOBLOG                                 *
      *========================================*
      * OPTION                                 *
      *========================================*
      * OPTION2                                *
      *========================================*
      * RETRY                                  *
      ******************************************
      *
     A                                      PRINT(MPRINT)
     A          R HEADER                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      KEEP
     A            USER          10   O  1  2
     A                                  1 13'HFC0010'
      *                                    Start of change /071613/
     A**********                        1 26'B I S  ¦  M i d a s  ¦  A B S'
     A**********                            DSPATR(HI)
      *                                      End of change /071613/
     A            DATE           7   O  1 72
     A            WSID          10   O  2  2
      *                                    Start of change /071613/
     A**********                        2 26'M i d a s  Information Screen'
     A                                  2 29'Midas Information Screen'
      *                                      End of change /071613/
     A                                      DSPATR(HI UL)
     A                                  2 71TIME
     A                                  4  2'From:'
     A            FPGM          10   O  4  8
      *
     A          R MIDASMSG
     A                                      OVERLAY
     A                                      KEEP
     A            LINE1         50      7 16
     A            LINE2         50      8 16
     A            LINE3         50      9 16
     A            LINE4         50     10 16
     A            LINE5         50     11 16
     A            LINE6         50     12 16
     A            LINE7         50     13 16
     A            LINE8         50     14 16
     A            LINE9         50     15 16
     A            LINE10        50     16 16
      *
     A          R TRAILER                   TEXT('COPYRIGHT STATEMENT')
     A                                      OVERLAY
     A                                      KEEP
     A                                 24  2'(c) Finastra International +
     A                                      Limited +
     A                                      2001'
      *
     A          R BLANK
     A                                      OVERLAY
     A                                      KEEP
      *
     A          R ENTER
     A                                      OVERLAY
     A                                      KEEP
     A                                 23  2'Press enter to continue.'
      *
     A          R JOBLOG
     A                                      OVERLAY
     A                                      KEEP
      *
     A                                 18 19'Do you wish to display the +
     A                                      joblog? (Y/N)'
      *
     A            REPLY1         1   B 18 61
     A                                      CHECK(ME)
     A                                      VALUES('Y' 'N')
      *
     A          R OPTION
     A                                      OVERLAY
     A                                      KEEP
     A  01                             17 15'Do you wish to re-open input cycle'
     A  01                             17 50'to transmit them?'
     A                                 18 22'Please enter required option (Y/N)'
     A            REPLY1         1   B 18 58
     A                                      CHECK(ME)
     A                                      VALUES('Y' 'N')
      *
     A          R OPTION2
     A                                      OVERLAY
     A                                      KEEP
     A                                 18 22'Please enter required option :'
     A            REPLY1         1   B 18 58
      *
     A          R RETRY
     A                                      OVERLAY
     A                                      KEEP
     A                                 18 25'Do you wish to re-try? (Y/N)'
     A            REPLY1         1   B 18 55
     A                                      CHECK(ME)
     A                                      VALUES('Y' 'N')
