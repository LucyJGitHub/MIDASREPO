     A*****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas HF Display Journal File Layout - YYMMDD')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility                           *
     A*                                                               *
     A*  PF/HFJRNXXPD - DISPLAY JOURNAL FILE LAYOUT                   *
     A*                 USED AS FORMAT FOR OPNQRYF IN HFC0200         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 099185    *CREATED DATE 10JUL96               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  099185 - If HAF extract file XX contains entries for 2 days, *
     A*           extracted data is not in chronological order. Fix   *
     A*           is to change format of OPNQRYF to be keyed on field *
     A*           YYMMDD, which means that file is in correct date    *
     A*           order.  New file definition containing date from    *
     A*           JODATE reformatted as YYMMDD created.               *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
     A          R QJORDJE                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A            JOENTL    R               REFFLD(JENT)
     A                                      COLHDG('Length of entry')
     A            JOSEQN    R               REFFLD(JSEQ)
     A                                      COLHDG('Sequence number')
     A            JOCODE    R               REFFLD(JCOD)
     A                                      COLHDG('Journal Code')
     A            JOENTT    R               REFFLD(JTYP)
     A                                      COLHDG('Entry Type')
     A            JODATE    R               REFFLD(JDAT)
     A                                      COLHDG('Date of entry')
     A            JOTIME    R               REFFLD(JTIM)
     A                                      COLHDG('Time of entry')
     A            JOJOB     R               REFFLD(JJOB)
     A                                      COLHDG('Name of Job')
     A            JOUSER    R               REFFLD(JUSE)
     A                                      COLHDG('Name of User')
     A            JONBR     R               REFFLD(JNUM)
     A                                      COLHDG('Number of Job')
     A            JOPGM     R               REFFLD(JPGM)
     A                                      COLHDG('Name of Program')
     A            JOOBJ     R               REFFLD(JOBJ)
     A                                      COLHDG('Name of Object')
     A            JOLIB     R               REFFLD(JLIB)
     A                                      COLHDG('Objects Library')
     A            JOMBR     R               REFFLD(JMBR)
     A                                      COLHDG('Name of Member')
     A            JOCTRR    R               REFFLD(JCNT)
     A                                      COLHDG('Count or rel rec no')
     A            JOFLAG    R               REFFLD(JFLG)
     A                                      COLHDG('Flag: 1 or 0')
     A            JOCCID    R               REFFLD(JCID)
     A                                      COLHDG('Commit cycle id')
     A            JORES     R               REFFLD(ZZ008)
     A                                      COLHDG('Not used')
     A            JOESD       9874A         TEXT('Entry Data')
     A                                      COLHDG('Entry Data')
     A            YYMMDD    R               REFFLD(JDAT)
