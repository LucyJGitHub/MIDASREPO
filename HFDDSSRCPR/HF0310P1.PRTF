      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas HF Printer file for audit reports')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility Module
     A*                                                               *
     A*  PRTF/HF0310P1 - HAF Print Program                            *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. NADF10             Date 05Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01379             Date 16Sep92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  NADF10 - Recompiled printer file.Applied for MD-56296.       *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01379 - HISTORY AND AUDIT FACILITY                          *
     A*           NEW FILE CREATED FOR SAR                            *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      **---------------------------------------------------------------
      *
     A          R HF0310F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'HF0310P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   100'DATE'
     A            BJMRDT    R            105REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(3)
      *
     A                                    55'JOURNAL AUDIT REPORT'
     A                                      SPACEA(1)
     A                                    55'--------------------'
     A                                      SPACEA(2)
      *
      **---------------------------------------------------------------
      *
     A          R HF0310F1                  TEXT('JOURNALLED FILE')
     A                                      SPACEB(1)
      *
     A                                     5'QUERY ON COMMIT CYCLE.......'
     A            JCOM          10S 0O    35EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     5'   "     USER...............'
     A            JUSER         10   O    35
     A                                      SPACEA(1)
      *
     A                                     5'   "     PROGRAM............'
     A            JPGM          10   O    35
     A                                      SPACEA(1)
      *
     A                                     5'   "     FILE...............'
     A            JFILE         10   O    35
     A            JFTEX         25   O    47
     A                                      SPACEA(1)
      *
     A                                     5'RECORD MODE.................'
     A            RECMOD         1   O    35
     A                                      SPACEA(1)
      *
     A                                     5'QUERY PARAMETER.............'
     A            JPARM         60   O    35
     A                                      SPACEA(1)
      *
     A                                     5'FILES EXCLUDED..............'
     A            JEXCL1        10   O    35
     A            JEXCL2        10   O    46
     A            JEXCL3        10   O    57
     A            JEXCL4        10   O    68
     A            JEXCL5        10   O    79
     A            JEXCL6        10   O    90
     A                                      SPACEA(1)
      *
     A                                     5'PRINT REQUESTED FROM........'
     A            STR            8   O    35
     A                                      SPACEA(1)
      *
     A                                     5'NUMBER OF RECORDS TO PRINT:.'
     A            NPRI           6  0O    35
     A                                      SPACEA(1)
      *
     A                                     5'SCAN CONDITIONS CONSIDERED?.'
     A            COSC           1   O    35
     A            SCAO          10   O    37
     A                                      SPACEA(1)
      *
     A                                     5'FIELDS PRINTED:.............'
     A            PRITEM         8   O    35
     A                                      SPACEA(1)
      *
     A                                     5'PRINT MODE:.................'
     A            MODE           6   O    35
     A                                      SPACEA(1)
      *
      **---------------------------------------------------------------
      *
     A          R HF0310F2                  TEXT('JOURNAL DETAILS HEADER')
     A                                      SPACEB(2)
      *
     A                                     1DATE
     A                                      EDTCDE(Y)
     A                                    12'PRINT AUDIT FOR FILE '
     A            JOOBJ         10   O    33
     A  44                                  UNDERLINE
      *
     A            JOLIB         10   O    44
     A            JOMBR         10   O    55
      *
     A                                    72'EVENT DATE/TIME:'
     A            JODATE         6   O    88
     A  45                                  UNDERLINE
     A            JOTIME         6  0O    98
     A  46                                  UNDERLINE
      *
     A            JOCCID        10  0O   107EDTCDE(3)
     A  49                                  UNDERLINE
      *
     A            KCHG          10   O   119
     A  48                                  UNDERLINE
     A                                      SPACEA(2)
      *
     A                                     7'Field Name/Description'
     A                                    30'Before/After Image................+
     A                                      ......'
     A            JOJOB         10   O    72
     A  43                                  UNDERLINE
     A            JOUSER        10   O    83
     A  42                                  UNDERLINE
     A            JONBR          6  0O    94
     A  47                                  UNDERLINE
     A            JOPGM         10   O   103
     A  41                                  UNDERLINE
      *
     A                                   118'SEQUENCE '
     A            SEQQ           6S  O   127
     A                                      SPACEA(1)
      *
     A                                     7'----------------------'
     A                                    30'----------------------------------+
     A                                      ------'
     A                                      SPACEA(1)
      *
      **---------------------------------------------------------------
      *
     A          R HF0310F3                  TEXT('JOURNAL DETAILS')
     A                                      SPACEA(1)
     A  03                                  SPACEB(1)
      *
     A  20                                 1'**'
      *
     A            NAME          20A  O     7
     A  32                                  UNDERLINE
      *
     A            JOUT         100A  O    30
      *
      **---------------------------------------------------------------
      *
     A          R HF0310F4                  TEXT('END OF REPORT')
     A                                      SPACEB(5)
     A                                    52'*** END OF REPORT ***'
      *
      **---------------------------------------------------------------
      *
     A          R HF0310F5                  TEXT('NO ITEMS FOUND')
     A                                      SPACEB(2)
     A                                     1DATE
     A                                      EDTCDE(Y)
     A                                    52'*** NO ITEMS FOUND ***'
      *
      **---------------------------------------------------------------
      *
     A          R HF0310F6                  TEXT('FIELDS EXCLUDED')
     A                                      SPACEB(1)
     A                                    58'Fields excluded:'
     A            EXOK           3  0O    89
     A                                      SPACEA(1)
