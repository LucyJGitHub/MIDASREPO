      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas HF Items deleted today report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - History and Audit Facility Module
     A*                                                               *
     A*  PRTF/HF0580P1 - HAF items deleted today report.              *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 081700             Date 13Feb95               *
      *                 S01379             Date 02Oct92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  081700 - Print P1 report only if details to report.          *
     A*  S01379 - HISTORY AND AUDIT FACILITY                          *
     A*           NEW FILE CREATED FOR SAR                            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      **---------------------------------------------------------------
      *
     A          R HF0580F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *
     A                                     6'REFERENCE'
     A                                    16'HF0580P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   100'DATE'
     A            BJMRDT    R            105REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(3)
      *
     A                                    43'HAF ITEMS DELETED TODAY AUDIT REPO-
     A                                      RT'
     A                                      SPACEA(1)
     A                                    43'-----------------------------------
     A                                      --'
     A                                      SPACEA(3)
      *
      *****************************************************************
     A          R HF0580F2                  TEXT('FILE HEADER')
     A                                      SPACEB(4)
     A                                    10'Item Name....:'
     A            FFILE9         9A       +1
     A                                   +10'Item Number....:'
     A            ITEM           3S       +1EDTCDE(3)
     A                                   +10'Item Text....:'
     A            TEXT          25A       +1
     A                                      SPACEA(2)
      *
     A                                    10'Deleted by'
     A                                      SPACEA(1)
     A                                    10'Job Name.....:'
     A            HFJOB         10A       +1
     A                                    +9'User Name..:'
     A            HFUSER        10A       +1
     A                                    +7'Job Number...:'
     A            HFNUMB         6A       +1
     A                                    +9'User Text..:'
     A            USER1         25A       +1
     A                                    10'Date.........:'
     A                                      SPACEB(1)
     A            HFDATE         7A       +1
     A                                   +12'Time.......:'
     A            HFTIMX         6S       +1EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    10'Key Text.....:'
     A            TEXK          15A       +1
     A                                    +4'Key Start Position..:'
     A            STRPOS         4S 0     +1EDTCDE(3)
     A                                    +4'Key Length...:'
     A            LENBUF         4S 0     +1EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'Retention Days..:'
     A            RETAIN         4S 0     +1EDTCDE(3)
     A                                   +12'COB Audit Report....:'
     A  11                                67'YES'
     A                                      SPACEA(2)
     A N11                                67'NO'
     A                                      SPACEA(2)
      *
     A          R HF0580F3                  TEXT('FILE DETAILS')
     A                                      SPACEB(1)
      *
     A                                    10'File Name.......:'
     A            FFORM         10A       29
     A                                    44'File is Currently...:'
     A  10                                67'ENABLED'
     A N10                                67'DISABLED'
      *
     A          R HF0580F4                  TEXT('END OF ITEM')
     A                                      SPACEB(3)
     A                                      SPACEA(1)
     A                                    10'                            '
      *
     A          R HF0580F5                  TEXT('REPORT TRAILER')
     A                                      SPACEB(3)
     A**12********                        50'*** NO DETAILS TO REPORT ***'081700
     A************                          SPACEA(3)                     081700
     A                                    55'*** END OF REPORT ***'
