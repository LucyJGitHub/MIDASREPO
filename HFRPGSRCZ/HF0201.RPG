     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas HF Retrieve max length of DSPJRN records')
      *****************************************************************
      *                                                               *
      *  Midas - History and Audit Facility                           *
      *                                                               *
      *  HF0201 - Retrieve Max length of files defined for DSPJRN     *
      *                                                               *
      *  Called By: HFC0200 - Extract journal entries in COB          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 088064             Date 12JUL95               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  088064 - Retrive the maximum length for a DSPJRN command.    *
      *                                                               *
      *****************************************************************
     FHFFILEPDIP  E                    DISK
      *
     E                    CPY@    1   1 80
      *
      ** Array containing copyright statement.
      *
     IINPUT       DS
     I                                        1  20 USSPC
     I                                        1  10 SPCNM
     I                                       11  20 SPCLB
     I                                       21  28 FMTNA
     I                                       29  48 FILLI
     I                                       29  38 FILNA
     I                                       39  48 FILLB
     I                                       49  49 OVRID
     I            DS
     I                                    B   1   40LNDTA
     I                                    B   5   80STPOS
     I                                    B   9  120LNSPC
     I                                    B  13  160LNVAR
     ILIST        DS
     I                                    B  25  280RCDLGT
     IRCVVR       DS
     I                                    B   1   40OFSET
     I                                    B   5   80HDRSZ
     I                                    B   9  120NOENT
     I                                    B  13  160SZENT
      *
     C           *ENTRY    PLIST
     C                     PARM           LGTH    4
      *
      ** Set up copyright parameter.
      *
     C                     MOVEACPY@      ACT@   80
      *
      ** Create user space for record format list.
      *
     C                     MOVEL'RCDSPC  'SPCNM
     C                     MOVEL'QTEMP   'SPCLB
     C                     MOVEL'*LIBL'   FILLB
     C                     MOVELFFORM     FILNA
     C           USRSP     IFEQ ' '
     C                     Z-ADD1         SAVLGT  40
      *
     C                     CALL 'QUSCRTUS'             99
     C                     PARM           USSPC
     C                     PARM *BLANK    ATTRIB 10
     C                     PARM 1024      LNSPC
     C                     PARM *BLANK    VALUE   1
     C                     PARM '*ALL    'PUBAUT 10
     C                     PARM *BLANK    TXT    50
     C                     MOVE 'Y'       USRSP   1
     C                     ENDIF
      *
      ** Generate record format list.
      *
     C                     CALL 'QUSLRCD'
     C                     PARM           USSPC
     C                     PARM 'RCDL0200'FMTNA
     C                     PARM           FILLI
     C                     PARM '0'       OVRID
      *
     C                     CALL 'QUSRTVUS'
     C                     PARM           USSPC
     C                     PARM 125       STPOS
     C                     PARM 16        LNDTA
     C                     PARM           RCVVR
     C                     Z-ADDOFSET     STPOS
     C                     ADD  1         STPOS
     C                     Z-ADDSZENT     LNDTA
      *
     C                     CALL 'QUSRTVUS'
     C                     PARM           USSPC
     C                     PARM           STPOS
     C                     PARM           LNDTA
     C                     PARM           LIST
      *
     C           RCDLGT    IFGT SAVLGT
     C                     Z-ADDRCDLGT    SAVLGT
     C                     MOVE SAVLGT    LGTH
     C                     ENDIF
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
