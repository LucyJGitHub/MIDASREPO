     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas HF Dispatch the journal entries')
     F*****************************************************************
     F*                                                               *
     F*  Midas - HISTORY AND AUDIT FACILITY
     F*                                                               *
     F*  HF0400 - HAF Dispatch the Journal Entries                    *
     F*                                                               *
     F*  Function : TO dispatch journal entries by RCVJRNE            *
     F*  (I/C BAT)                                                    *
     F*                                                               *
     F*  Called By: HFC0410 - Build string for Event Processing       *
     F*                                                               *
     F*  Calls    : HFC0430 - Override parameters for Batch Processing*
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG9789            Date 05Jan06               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 204329             Date 28Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 S01379             Date 16Sep92               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG9789- Apply Gems 232027 to fix value out of range for     *
      *           string error.                                       *
      *  204329 - Extract only actual length of journal.              *
     F*  S01379 - HISTORY AND AUDIT FACILITY                          *
     F*           NEW PROGRAM CREATED FOR SAR.                        *
     F*                                                               *
     F*****************************************************************
      *
     FHFFILEL2IF  E           K        DISK
      ** Files to use by Physical file
      *
     FJOURNAL O   F    4000            DISK                           UC
      ** Journal entries
      *
      /SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*  25    - JOURNAL FILE NOT FOUND ON HFFILEL2                   *
     F*                                                               *
     F*****************************************************************
      /SPACE 3
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      *
      /SPACE 3
      *
     IJRNENT      DS                           4000
     I                                        1   50JOENTL
     I                                        6  150JOSEQL
     I                                       16  66 ENTL
     I                                       67  76 JOFILE
     I                                       77 125 JO2
      *
     IJRNOUT      DS                           4000                                           204329
      *                                                                                       204329
     IJRNX        DS                           4000
     I                                        1 256 #XXL
      *
     IJRNBAT     UDS                             10
     I                                        1  100#JOSEQ
      *
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
      /EJECT
      *
      ** Entry parameters from EXITPGM of RCVJRNE command
      *
     C           *ENTRY    PLIST
     C                     PARM           JRNENT
     C                     PARM           CALLCD  1
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      BIS@   80
      *
      ** Update journal batch dataarea with journal entry sequence number
      *
     C           *NAMVAR   DEFN           JRNBAT
     C           *LOCK     IN   *NAMVAR
     C                     Z-ADDJOSEQL    #JOSEQ
     C                     OUT  *NAMVAR
      *
      ** Check journal file on files to use - exit if file does not exist
      *
     C           JOFILE    SETLLHFFILEL2                 25
     C           *IN25     IFEQ '0'
     C                     GOTO END
     C                     END
      *
     C           LOOP      TAG
     C                     MOVE *BLANKS   JRNOUT                                              204329
     C                     READ HFFILEL2                 25
     C   25                GOTO END
      *
      ** Exit if journal file not the same as file format
      *
     C           JOFILE    IFNE FFORM
     C                     GOTO END
     C                     END
      *
      ** Calculate string for OVRDBF parameter -
      ** (OVRDBF FILE(JOURNAL) TOFILE(FFILE) MBR(JRNBATCH))
      *
     C                     CALL 'HFC0430'
     C                     PARM           STRG   80
     C                     PARM           FFILE
      *
      ** Perform OVRDBF
      *
     C                     Z-ADD80        PLEN   155
     C                     CALL 'QCMDEXC'
     C                     PARM           STRG
     C                     PARM           PLEN
     C                     OPEN JOURNAL
      *                                                                                       204329
     C           JOENTL    IFGT 0                                                            BUG9789
     C           JOENTL    SUBSTJRNENT    JRNOUT                                              204329
     C                     ELSE                                                              BUG9789
     C                     MOVEL*BLANKS   JRNOUT                                             BUG9789
     C                     MOVELJRNENT    JRNOUT                                             BUG9789
     C                     ENDIF                                                             BUG9789
     C                     EXCPT
     C                     CLOSEJOURNAL
      *
      ** Read the next file format
      *
     C                     GOTO LOOP
      *
      ** Exit program
      *
     C           END       TAG
     C                     RETRN
      *
     C/EJECT
      *
     OJOURNAL E
     O**********               JRNENT  4000                                                   204329
     O                         JRNOUT  4000                                                   204329
     O/EJECT
**  CPY@
(c) Finastra International Limited 2001
