      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IBLC 2002 Installation Control Data')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  DSPF/IL0200DF - IBLC INSTALLATION CONTROL DATA               *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX043             Date 23Jul01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(59 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03
      *
      *----------------*
      *  Dummy Record  *
      *----------------*
      *
     A          R DUMMY                     TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2005')
     A                                      ASSUME
     A                                  1  2' '
      *
      *-------------------------*
      *  Error message Subfile  *
      *-------------------------*
      *
     A          R IL0200S0                  SFL
     A                                      TEXT('Subfile Record Format')
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R IL0200C0                  SFLCTL(IL0200S0)
     A                                      TEXT('Subfile Control Record')
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A N40                                  SFLEND
     A                                      SFLINZ
     A            DDPGMQ                    SFLPGMQ
     A                                 23  2'F3=Exit'
      *
      *----------------*
      *  Input Screen  *
      *----------------*
     A          R IL0200X0
     A                                      TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2005')
     A                                      PUTOVR
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2REFFLD(JOUSER)
     A            SJOB      R        O  1 13REFFLD(JOPGM)
     A            SRUNA     R        O  1 74REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
      *
     A                                  2 25'BCL 2002 Installation Control Data'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Folder Path :'
     A                                  5  2'Limit of Registration. . . . . . .-
     A                                       :'
     A                                  5 61'Euro'
     A                                  6  2'Print Routing Keys Found . . (Y/N)-
     A                                       :'
     A                                  7  2'Use Dummy Issuer . . . . . . (Y/N)-
     A                                       :'
     A                                  8  2'Activation Date. . . . . . . . . .-
     A                                       :'
     A                                 10  2'BCL reporting interfaced with'
     A                                 10 42'CURRENT'
     A                                      DSPATR(UL)
     A N50                             10 59'WANTED'
     A                                      DSPATR(UL)
      *
     A                                 12  2'General ledger . . . . . . . . . .-
     A                                       :'
     A                                 13  2'Retail . . . . . . . . . . . . . .-
     A                                       :'
     A                                 14  2'Dealing  . . . . . . . . . . . . .-
     A                                       :'
     A                                 15  2'Customer lending . . . . . . . . .-
     A                                       :'
     A                                 16  2'Securities trading . . . . . . . .-
     A                                       :'
     A                                 17  2'Funds transfer . . . . . . . . . .-
     A                                       :'
     A                                 18  2'Future options . . . . . . . . . .-
     A                                       :'
     A                                 19  2'Portfolio Management . . . . . . .-
     A                                       :'
     A                                 20  2'Run C.O.B. Components. . . . . . .-
     A                                       :'
     A N50                             12 53'¦'
     A N50                             13 53'¦'
     A N50                             14 53'¦'
     A N50                             15 53'¦'
     A N50                             16 53'¦'
     A N50                             17 53'¦'
     A N50                             18 53'¦'
     A N50                             19 53'¦'
     A N50                             20 53'¦'
      *
     A            SFLDP         63A  B  4 18
     A  50                                  DSPATR(PR)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A N20                                  DSPATR(UL)
     A N20                                  DSPATR(HI)
     A                                      OVRDTA
      *
     A            SLIMR         15A  B  5 45CHECK(RB)
     A  50                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A N21                                  DSPATR(HI)
     A                                      OVRDTA
      *
     A            SPRID          1A  B  6 45CHECK(RB)
     A  50                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A N22                                  DSPATR(HI)
     A                                      OVRDTA
      *
     A            SDMIS          1A  B  7 45CHECK(RB)
     A  50                                  DSPATR(PR)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A N23                                  DSPATR(UL)
     A N23                                  DSPATR(HI)
     A                                      OVRDTA
      *
     A            SACTD          6Y 0B  8 45EDTWRD('  /  /  ')
     A  50                                  DSPATR(PR)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A N24                                  DSPATR(UL)
     A N24                                  DSPATR(HI)
     A                                      OVRDTA
      *
     A            SGL01          1A  O 12 45DSPATR(HI)
     A            SRE01          1A  O 13 45DSPATR(HI)
     A            SDE01          1A  O 14 45DSPATR(HI)
     A            SCL01          1A  O 15 45DSPATR(HI)
     A            SST01          1A  O 16 45DSPATR(HI)
     A            SFT01          1A  O 17 45DSPATR(HI)
     A            SFO01          1A  O 18 45DSPATR(HI)
     A            SPM01          1A  O 19 45DSPATR(HI)
     A            SCB01          1A  O 20 45DSPATR(HI)
      *
     A            SGL02          1A  B 12 61
      **Start of Change CER001
     A  50                                  DSPATR(ND)
      **End of Change CER001
     A  51                                  DSPATR(PR)
     A N30N51                               DSPATR(HI)
     A N30N51                               DSPATR(UL)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SRE02          1A  B 13 61
      **Start of Change CER001
     A  50                                  DSPATR(ND)
      **End of Change CER001
     A  52                                  DSPATR(PR)
     A N31N52                               DSPATR(HI)
     A N31N52                               DSPATR(UL)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SDE02          1A  B 14 61
      **Start of Change CER001
     A  50                                  DSPATR(ND)
      **End of Change CER001
     A  53                                  DSPATR(PR)
     A N32N53                               DSPATR(HI)
     A N32N53                               DSPATR(UL)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SCL02          1A  B 15 61
      **Start of Change CER001
     A  50                                  DSPATR(ND)
      **End of Change CER001
     A  54                                  DSPATR(PR)
     A N33N54                               DSPATR(HI)
     A N33N54                               DSPATR(UL)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SST02          1A  B 16 61
      **Start of Change CER001
     A  50                                  DSPATR(ND)
      **End of Change CER001
     A  55                                  DSPATR(PR)
     A N34N55                               DSPATR(HI)
     A N34N55                               DSPATR(UL)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SFT02          1A  B 17 61
      **Start of Change CER001
     A  50                                  DSPATR(ND)
      **End of Change CER001
     A  56                                  DSPATR(PR)
     A N35N56                               DSPATR(HI)
     A N35N56                               DSPATR(UL)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SFO02          1A  B 18 61
      **Start of Change CER001
     A  50                                  DSPATR(ND)
      **End of Change CER001
     A  57                                  DSPATR(PR)
     A N36N57                               DSPATR(HI)
     A N36N57                               DSPATR(UL)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SPM02          1A  B 19 61
      **Start of Change CER001
     A  50                                  DSPATR(ND)
      **End of Change CER001
     A  60                                  DSPATR(PR)
     A N37N60                               DSPATR(HI)
     A N37N60                               DSPATR(UL)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SCB02          1A  B 20 61
      **Start of Change CER001
     A  50                                  DSPATR(ND)
      **End of Change CER001
     A  61                                  DSPATR(PR)
     A N29N61                               DSPATR(HI)
     A N29N61                               DSPATR(UL)
     A  29                                  DSPATR(RI)
     A  29                                  DSPATR(PC)
     A                                      OVRDTA
      *
