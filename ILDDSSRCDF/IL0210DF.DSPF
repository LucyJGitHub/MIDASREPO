      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IBLC 2002 Operation Code Invite Display')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  DSPF/IL0210DF - IBLC OPERATION CODES MAINTENANCE             *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX043             Date 25Jun01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(56 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03
      *
     A          R DUMMY                     TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2005')
     A                                      ASSUME
     A                                  1  2' '
      *
      *--------------------------*
      *  Error Messages subfile  *
      *--------------------------*
      *
     A          R IL0210S0                  SFL
      *
     A                                      TEXT('Subfile Record Format')
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R IL0210C0                  SFLCTL(IL0210S0)
     A                                      TEXT('Subfile Control Record')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N65                                  SFLEND
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A            DDPGMQ                    SFLPGMQ
      *
     A  86                             23  2'F3=Exit                           -
     A                                                                         -
     A                                               '
     A  87                             23  2'F3=Exit   F5=Initial screen       -
     A                                                                         -
     A                                               '
     A  88                             23  2'F3=Exit   F5=Initial Screen   F10=-
     A                                      Delete Record                      -
     A                                               '
      *
      *----------------*
      *  First Screen  *
      *----------------*
      *
     A          R IL0210X0
      *
     A                                      BLINK
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A            SJOB      R        O  1 13REFFLD(JOPGM)
     A            SRUNA     R        O  1 74REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
      *
     A                                  2 22'IBLC 2002 Operation Codes - Main-
     A                                      tenance'
     A                                      DSPATR(HI  UL)
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Action Code . :'
     A                                  4 28'Operation Code . :'
     A                                  4 58'Or Review from . :'
      *
     A            SACTN          1A  B  4 18
     A N31                                  DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
      *
     A            SOPCO          3A  B  4 47CHECK(RZ)
     A N32                                  DSPATR(HI)
     A N32                                  DSPATR(UL)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
      *
     A            SOPCOR         3A  B  4 77DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      CHECK(RZ)
      *
     A                                  6  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
      *
      *----------------------------*
      *   Select screen - subfile  *
      *----------------------------*
      *
     A          R IL0210S1                  SFL
      *
     A            BACTN          1A  B  7  4
     A N31                                  DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A            BOPCO          3A  O  7 14DSPATR(HI)
     A            BNARX         40A  O  7 24
      *
     A          R IL0210C1                  SFLCTL(IL0210S1)
     A                                      SFLSIZ(0300)
     A                                      SFLPAG(0015)
     A                                      OVERLAY
     A                                      CA05
     A                                      BLINK
     A N61                                  SFLDSPCTL
     A N60N61                               SFLDSP
     A  61                                  SFLCLR
     A  62                                  SFLEND(*MORE)
     A N62                                  ROLLUP(25)
      *
      *
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A            SJOB      R        O  1 13REFFLD(JOPGM)
     A            SRUNA     R        O  1 74REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
      *
     A                                  2 22'BCL 2002 Operation Codes - Mainten-
     A                                      ance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4 50'Review from :'
     A            SOPCOR         3A  B  4 64DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      CHECK(RZ)
     A                                  5  2'Action'
     A                                      DSPATR(HI)
     A                                  5 11'Operation'
     A                                      DSPATR(HI)
     A                                  5 24'Description (Narrative)'
     A                                      DSPATR(HI)
      *
     A                                  6  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
      *
      *------------------*
      *   Input Screen   *
      *------------------*
      *
     A          R IL0210X2
     A                                      OVERLAY
     A  88                                  CA10
     A                                      CA05
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'IL0210'
     A            SRUNA     R        O  1 74REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
      *
     A                                  2 22'BCL 2002 Operation Codes - Mainten-
     A                                      ance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Action Code . . .:'
     A                                  5  2'Operation code  .:'
     A                                  6  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
      *
     A            SACTN          1A  O  4 24DSPATR(HI)
     A                                  4 43'DELETED'
     A  64                                  DSPATR(HI)
     A  64                                  DSPATR(BL)
     A N64                                  DSPATR(ND)
      *
     A            SOPCO          3A  O  5 24DSPATR(HI)
     A                                  8  2'Narrative . . . .:'
     A                                 10  2'Limit to register Country Code'
     A                                 11  9'Lower limit for Bank Own Account  +
     A                                      :'
     A                                 12  9'Lower limit for Other transaction +
     A                                      :'
     A                                 14  2'Limit to register Identification'
     A                                 15  9'Lower limit for Bank Own Account  +
     A                                      :'
     A                                 16  9'Lower limit for Other transaction +
     A                                      :'
     A                                 18  2'Validity period :'
     A                                 18 27'(Included)   To :'
     A                                 18 52'(Excluded)'
      *
     A            SNARF         40A  B  8 21
     A N31                                  DSPATR(HI)
     A N31N57                               DSPATR(UL)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A  57                                  DSPATR(PR)
     A                                      CHECK(LC)
      *
     A            SLCBK         16A  B 11 45
     A  57                                  DSPATR(PR)
     A N33                                  DSPATR(HI)
     A N33N57                               DSPATR(UL)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
      *
     A            SLCNB         16A  B 12 45
     A  57                                  DSPATR(PR)
     A N34                                  DSPATR(HI)
     A N34N57                               DSPATR(UL)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
      *
     A            SLIBK         16A  B 15 45
     A  57                                  DSPATR(PR)
     A N35                                  DSPATR(HI)
     A N35N57                               DSPATR(UL)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
      *
     A            SLINB         16A  B 16 45
     A  57                                  DSPATR(PR)
     A N36                                  DSPATR(HI)
     A N36N57                               DSPATR(UL)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
      *
     A            SVFRM          6A  B 18 20
     A  57                                  DSPATR(PR)
     A N37                                  DSPATR(HI)
     A N37N57                               DSPATR(UL)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
      *
     A            SVLTO          6A  B 18 45
     A  57                                  DSPATR(PR)
     A N38                                  DSPATR(HI)
     A N38N57                               DSPATR(UL)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
      *
