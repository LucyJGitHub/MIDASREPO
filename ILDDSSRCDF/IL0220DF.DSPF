      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IBLC 2002 Deals/Loans Types omission Display')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  DSPF/IL0220DF - IBLC 2002 Deals/LOANS Types Omission         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX043             Date 18Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(56 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03
      *
      *------------------*
      *   DUMMY  RECORD  *
      *------------------*
      *
     A          R DUMMY                     TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2005')
     A                                      ASSUME
     A                                  1  2' '
      *
      *----------------------------*
      *   error Messages subfile   *
      *----------------------------*
      *
     A          R IL0220S0                  SFL
     A                                      TEXT('Subfile Record Format')
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R IL0220C0                  SFLCTL(IL0220S0)
     A                                      TEXT('Subfile Control Record')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N65                                  SFLEND
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A            DDPGMQ                    SFLPGMQ
      *
     A                                 23  2'F3=Exit'
      *
      *-----------------------------*
      *   Input screen -  Subfile   *
      *-----------------------------*
      *
     A          R IL0220S1                  SFL
      *
     A            BMODI          2A  O  6 10
     A            BTYPE          2A  O  6 14
     A            BSTYPE         2A  O  6 18
      **Start of Change CER001
     A**********  BBLANK         7A  O  6 21DSPATR(ND)                                        CER001
     A            BCLAS          4A  O  6 22
     A N50                                  DSPATR(ND)
     A            BBLANK         3A  O  6 26DSPATR(ND)
      **End of Change CER001
      *
     A          R IL0220C1                  SFLCTL(IL0220S1)
      *
     A                                      SFLSIZ(0300)
     A                                      SFLPAG(0045) SFLLIN(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CA05
     A                                      BLINK
     A N61                                  SFLDSPCTL
     A N60N61                               SFLDSP
     A  61                                  SFLCLR
     A  62                                  SFLEND(*MORE)
     A N62                                  ROLLUP(25)
      *
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A            SJOB      R        O  1 13REFFLD(JOPGM)
     A            SRUNA     R        O  1 74REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
      *
     A                                  2 22'Deals/Loans Types Omission Mainten-
     A                                      ance'
     A                                      DSPATR(HI  UL)
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  2'Action Code :'
     A            SACTN          1A  B  4 16
     A N31                                  DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(RI)
     A N32
     AON33
     AON34                                  DSPATR(PC)
      *
      **Start of Change CER001
     A**********                        4 19'Module Id. :'                                    CER001
     A**********  SMODI          2   B  4 32                                                  CER001
     A                                  4 18'Module Id :'
     A            SMODI          2   B  4 30
      **End of Change CER001
     A N32                                  DSPATR(HI)
     A N32                                  DSPATR(UL)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A                                      OVRDTA
      *
      **Start of Change CER001
     A**********                        4 36'Deals/Loans Type :'                              CER001
     A**********  STYPE          2   B  4 55                                                  CER001
     A                                  4 33'Deals/Loans Type :'
     A            STYPE          2   B  4 52
      **End of Change CER001
     A N33                                  DSPATR(HI)
     A N33                                  DSPATR(UL)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                      OVRDTA
      *
      **Start of Change CER001
     A**********                        4 59'Subtype :'                                       CER001
     A**********  SSTYP          2   B  4 69                                                  CER001
     A                                  4 55'Subtype :'
     A            SSTYP          2   B  4 65
      **Start of Change CER001
     A N34                                  DSPATR(HI)
     A N34                                  DSPATR(UL)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A                                      OVRDTA
      **Start of Change CER001
     A                                  4 68'Class :'
     A    N50                               DSPATR(ND)
     A                                      OVRATR
     A            SCLAS          4   B  4 76
     A N35 50                               DSPATR(HI)
     A N35 50                               DSPATR(UL)
     A  35 50                               DSPATR(RI)
     A  35 50                               DSPATR(PC)
     A    N50                               DSPATR(PR ND)
     A                                      OVRDTA
      **End of Change CER001
      *
      **Start of Change CER001
     A**********                        5  2'                                  -
     A**********                                                               -
     A**********                                    '
     A                                  5  2'                                  -
     A                                                                         -
     A                                               '
      **Start of Change CER001
     A                                      DSPATR(UL)
      *
