      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IBLC FF 2002 Routing Keys Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  DSPF/IL0245DF - Futures & Options routing keys maintenance   *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX043             Date 27Jun01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(56 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03
      *
      *----------------------------*
      *   Error Messages subfile   *
      *----------------------------*
      *
     A          R IL0245S0                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R IL0245C0                  SFLCTL(IL0245S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A N65                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
      *
     A  86                             23  2'F3=Exit                           -
     A                                                                         -
     A                                               '
     A N86                                  DSPATR(ND)
      *
     A  87                             23  2'F3=Exit  F5=Initial screen        -
     A                                                                         -
     A                                               '
     A N87                                  DSPATR(ND)
      *
     A  88                             23  2'F3=Exit  F5=Initial screen        -
     A                                                                         -
     A                                               '
     A N88                                  DSPATR(ND)
      *
     A  89                             23  2'F3=Exit  F10=Delete this record   -
     A                                      F5=Initial screen                  -
     A                                               '
     A N89                                  DSPATR(ND)
      *
      *-------------------------------------------------*
      *   Input Screen - For Futures & Options Module   *
      *-------------------------------------------------*
      *
     A          R IL0245X0                  TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2005')
     A                                      PUTOVR
     A                                      OVERLAY
     A                                      CA10
     A                                      CA05
      *
     A            SUSER     R        O  1  2REFFLD(JOUSER)
     A            SJOB      R        O  1 13REFFLD(JOPGM)
     A            SRUNA     R        O  1 74REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
      *
     A N09                              2 20'Futures & options routing keys mai-
     A                                      ntenance'
     A                                      DSPATR(HI UL)
      *
     A  09                              2 25'Futures & options routing keys'
     A                                      DSPATR(HI UL)
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
      *
     A N09                              5  2'Action . . . . . . . . . . . . . .-
     A                                       :'
     A                                  6  2'Processing type  . . . . . . . . .-
     A                                       :'
     A                                  7  2'Event type . . . . . . . . . . . .-
     A                                       :'
     A                                  8  2'Market type  . . . . . . . . . . .-
     A                                       :'
     A                                  9  2'Broker type  . . . . . . . . . . .-
     A                                       :'
     A                                 10  2'Customer type  . . . . . . . . . .-
     A                                       :'
     A                                 11  2'Broker code  . . . . . . . . . . .-
     A                                       :'
     A                                 12  2'Settlement type  . . . . . . . . .-
     A                                       :'
     A                                 13  2'Event amount type  . . . . . . . .-
     A                                       :'
     A N09                             18  5'Debit operation code . : 1)'
     A N09                             18 43'Credit operation code . : 1)'
     A N09                             19 30'2)'
     A N09                             20 30'3)'
     A N09                             19 69'2)'
     A N09                             20 69'3)'
      *
     A            SACTN          1A  B  5 40
     A  09                                  DSPATR(ND)
     A  49                                  DSPATR(PR)
     A N68                                  DSPATR(HI)
     A N68                                  DSPATR(UL)
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SPTYP          1A  B  6 40
     A  50                                  DSPATR(PR)
     A N69                                  DSPATR(HI)
     A N69                                  DSPATR(UL)
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SEVTP          1A  B  7 40
     A  50                                  DSPATR(PR)
     A N70                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SMTYP          1A  B  8 40
     A  50                                  DSPATR(PR)
     A N71                                  DSPATR(HI)
     A N71                                  DSPATR(UL)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SBTYP          2A  B  9 40
     A  50                                  DSPATR(PR)
     A N72                                  DSPATR(HI)
     A N72                                  DSPATR(UL)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SCTYP          2A  B 10 40
     A  50                                  DSPATR(PR)
     A N73                                  DSPATR(HI)
     A N73                                  DSPATR(UL)
     A  73                                  DSPATR(RI)
     A  73                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SBCOD          1A  B 11 40
     A  50                                  DSPATR(PR)
     A N75                                  DSPATR(HI)
     A N75                                  DSPATR(UL)
     A  75                                  DSPATR(RI)
     A  75                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SSETT          1A  B 12 40
     A  50                                  DSPATR(PR)
     A N76                                  DSPATR(HI)
     A N76                                  DSPATR(UL)
     A  76                                  DSPATR(RI)
     A  76                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SEVNT          2A  B 13 40
     A  50                                  DSPATR(PR)
     A N77                                  DSPATR(HI)
     A N77                                  DSPATR(UL)
     A  77                                  DSPATR(RI)
     A  77                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SDRR1          3A  B 18 34
     A  09                                  DSPATR(ND)
     A  51                                  DSPATR(PR)
     A N41                                  DSPATR(HI)
     A N41                                  DSPATR(UL)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SDRR2          3A  B 19 34
     A  09                                  DSPATR(ND)
     A  51                                  DSPATR(PR)
     A N42                                  DSPATR(HI)
     A N42                                  DSPATR(UL)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SDRR3          3A  B 20 34
     A  09                                  DSPATR(ND)
     A  51                                  DSPATR(PR)
     A N43                                  DSPATR(HI)
     A N43                                  DSPATR(UL)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SCRR1          3A  B 18 73
     A  09                                  DSPATR(ND)
     A  51                                  DSPATR(PR)
     A N44                                  DSPATR(HI)
     A N44                                  DSPATR(UL)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SCRR2          3A  B 19 73
     A  09                                  DSPATR(ND)
     A  51                                  DSPATR(PR)
     A N45                                  DSPATR(HI)
     A N45                                  DSPATR(UL)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SCRR3          3A  B 20 73
     A  09                                  DSPATR(ND)
     A  51                                  DSPATR(PR)
     A N46                                  DSPATR(HI)
     A N46                                  DSPATR(UL)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A                                      OVRDTA
      *
      *-----------------------------*
      *   Select screen - subfile   *
      *-----------------------------*
      *
     A          R IL0245S1                  SFL
      *
     A            SACTN          1A  B  7  2
     A  09                                  DSPATR(PR ND)
     A N31                                  DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A                                      CHECK(ER)
      *
     A            SSPTY          1A  O  7  8
     A            SSEVT          1A  O  7 10
     A            SSMTY          1A  O  7 12
     A            SSBTY          2A  O  7 14
     A            SSCTY          2A  O  7 17
     A            SSBCO          1A  O  7 22
     A            SSSET          1A  O  7 24
     A            SSEVN          2A  O  7 26
     A            SSDR1          3A  O  7 38
     A            SSDR2          3A  O  7 44
     A            SSDR3          3A  O  7 50
     A            SSCR1          3A  O  7 61
     A            SSCR2          3A  O  7 67
     A            SSCR3          3A  O  7 73
      *
      *----------------------*
      *   Control  subfile   *
      *----------------------*
      *
     A          R IL0245C1                  SFLCTL(IL0245S1)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      OVERLAY
     A                                      CA05
     A  61                                  SFLDSPCTL
     A  60                                  SFLDSP
     A N60N61                               SFLCLR
     A  62                                  SFLEND(*MORE)
     A N62                                  ROLLUP(25)
     A N63                                  ROLLDOWN(26)
      *
     A            SUSER     R        O  1  2REFFLD(JOUSER)
     A            SJOB      R        O  1 13REFFLD(JOPGM)
     A            SRUNA     R        O  1 74REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
      *
     A N09                              2 20'Futures & options routing keys mai-
     A                                      ntenance'
     A                                      DSPATR(HI UL)
      *
     A  09                              2 25'Futures & options routing keys'
     A                                      DSPATR(HI UL)
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
      *
     A N09                              5  2'A     Routing keys                -
     A                                      Debit operation code  Credit operat-
     A                                      ion code'
     A                                      DSPATR(HI)
      *
     A  09                              5  2'      Routing keys                -
     A                                      Debit operation code  Credit operat-
     A                                      ion code'
     A                                      DSPATR(HI)
      *
     A                                  4  8'Review from :'
      *
     A                                  6  2'                                  -
     A                                         1     2     3          1     2  -
     A                                         3     '
     A                                      DSPATR(HI UL)
      *
     A            SREVI         20   O  4 23
      *
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
      *
