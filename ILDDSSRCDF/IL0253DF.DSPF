      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IBLC 2002 Routing Key SE Duplication')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  DSPF/IL0254DF - Routing keys SE Duplication                  *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX043             Date 28Jun01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(56 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03
      *
      *----------------------------*
      *   Error Messages subfile   *
      *----------------------------*
      *
     A          R IL0253S0                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R IL0253C0                  SFLCTL(IL0253S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A N65                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
      *
     A                                 23  2'F3=Exit  F5=Initial screen        -
     A                                                                         -
     A                                               '
      *
      *--------------------------------------------------*
      *   Input Screen - For Securities Trading Module   *
      *--------------------------------------------------*
      *
     A          R IL0253X0                  TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2005')
     A                                      PUTOVR
     A                                      OVERLAY
     A                                      CA05
      *
     A            SUSER     R        O  1  2REFFLD(JOUSER)
     A            SJOB      R        O  1 13REFFLD(JOPGM)
     A            SRUNA     R        O  1 74REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
      *
     A                                  2 19'Securities trading routing -
     A                                      keys duplication'
     A                                      DSPATR(HI UL)
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  6  2'Processing type   . . . . . . . . -
     A                                      .:'
     A                                  7  2'Trade type  . . . . . . . . . . . -
     A                                      .:'
     A                                  8  2'Event type  . . . . . . . . . . . -
     A                                      .:'
     A                                  9  2'Counterparty type . . . . . . . . -
     A                                      .:'
     A                                 10  2'Issuer type . . . . . . . . . . . -
     A                                      .:'
     A                                 11  2'Duration < 1 year . . . . . . . . -
     A                                      .:'
     A                                 12  2'Cross currency  . . . . . . . . . -
     A                                      .:'
     A                                 13  2'Settlement type . . . . . . . . . -
     A                                      .:'
     A                                 14  2'Event amount type . . . . . . . . -
     A                                      .:'
     a                                  4 40 'From'
     A                                 06 48']'
     A                                 07 48']'
     A                                 08 48']'
     A                                 09 48']'
     A                                 10 48']'
     A                                 11 48']'
     A                                 12 48']'
     A                                 13 48']'
     A                                 14 48']'
     A                                  4 55 'To  '
      *
     A            SPTYP          1A  B  6 40
     A  50                                  DSPATR(PR)
     A N69                                  DSPATR(HI)
     A N69                                  DSPATR(UL)
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            STRTY          1A  B  7 40
     A  50                                  DSPATR(PR)
     A N70                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SEVTY          1A  B  8 40
     A  50                                  DSPATR(PR)
     A N71                                  DSPATR(HI)
     A N71                                  DSPATR(UL)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SCTTY          2A  B  9 40
     A  50                                  DSPATR(PR)
     A N72                                  DSPATR(HI)
     A N72                                  DSPATR(UL)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SISTY          2A  B 10 40
     A  50                                  DSPATR(PR)
     A N73                                  DSPATR(HI)
     A N73                                  DSPATR(UL)
     A  73                                  DSPATR(RI)
     A  73                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SDURA          1A  B 11 40
     A  50                                  DSPATR(PR)
     A N74                                  DSPATR(HI)
     A N74                                  DSPATR(UL)
     A  74                                  DSPATR(RI)
     A  74                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SCRCU          1A  B 12 40
     A  50                                  DSPATR(PR)
     A N75                                  DSPATR(HI)
     A N75                                  DSPATR(UL)
     A  75                                  DSPATR(RI)
     A  75                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SSETY          1A  B 13 40
     A  50                                  DSPATR(PR)
     A N76                                  DSPATR(HI)
     A N76                                  DSPATR(UL)
     A  76                                  DSPATR(RI)
     A  76                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SEATY          2A  B 14 40
     A  50                                  DSPATR(PR)
     A N77                                  DSPATR(HI)
     A N77                                  DSPATR(UL)
     A  77                                  DSPATR(RI)
     A  77                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SPTYPD         1A  B  6 55
     A  50                                  DSPATR(PR)
     A N69                                  DSPATR(HI)
     A N69                                  DSPATR(UL)
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            STRTYD         1A  B  7 55
     A  50                                  DSPATR(PR)
     A N70                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SEVTYD         1A  B  8 55
     A  50                                  DSPATR(PR)
     A N71                                  DSPATR(HI)
     A N71                                  DSPATR(UL)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SCTTYD         2A  B  9 55
     A  50                                  DSPATR(PR)
     A N72                                  DSPATR(HI)
     A N72                                  DSPATR(UL)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SISTYD         2A  B 10 55
     A  50                                  DSPATR(PR)
     A N73                                  DSPATR(HI)
     A N73                                  DSPATR(UL)
     A  73                                  DSPATR(RI)
     A  73                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SDURAD         1A  B 11 55
     A  50                                  DSPATR(PR)
     A N74                                  DSPATR(HI)
     A N74                                  DSPATR(UL)
     A  74                                  DSPATR(RI)
     A  74                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SCRCUD         1A  B 12 55
     A  50                                  DSPATR(PR)
     A N75                                  DSPATR(HI)
     A N75                                  DSPATR(UL)
     A  75                                  DSPATR(RI)
     A  75                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SSETYD         1A  B 13 55
     A  50                                  DSPATR(PR)
     A N76                                  DSPATR(HI)
     A N76                                  DSPATR(UL)
     A  76                                  DSPATR(RI)
     A  76                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            SEATYD         2A  B 14 55
     A  50                                  DSPATR(PR)
     A N77                                  DSPATR(HI)
     A N77                                  DSPATR(UL)
     A  77                                  DSPATR(RI)
     A  77                                  DSPATR(PC)
     A                                      OVRDTA
      *
