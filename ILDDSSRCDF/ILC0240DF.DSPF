      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IBLC 2002 Routing Keys maintenance selection')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  DSPF/ILC0240DF - Routing keys maintenance selection          *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX043             Date 25Jun01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     A
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      *----------------*
      *  Dummy Record  *
      *----------------*
      *
     A          R DUMMY                     TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      ASSUME
     A                                  1  2' '
      *
      *----------------*
      *  Input Screen  *
      *----------------*
      *
     A          R ILC0240X0
     A                                      CA03(03)
     A                                      OVERLAY
     A            USER          10A  O  1  2
     A            JOB           10A  O  1 13
     A            DATUM          7A  O  1 74
     A            WSID          10A  O  2  2
      *
     A                                  2 24'Routing keys maintenance selection'
     A                                      DSPATR(HI)
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  8 20'1.'
     A  11                                  DSPATR(ND)
     A                                  9 20'2.'
     A  12                                  DSPATR(ND)
     A                                 10 20'3.'
     A  13                                  DSPATR(ND)
     A                                 11 20'4.'
     A  14                                  DSPATR(ND)
     A                                 12 20'5.'
     A  15                                  DSPATR(ND)
     A                                 13 20'6.'
     A  16                                  DSPATR(ND)
      *
     A                                  8 32'Dealing'
     A  11                                  DSPATR(ND)
     A                                  9 32'Negotiable Assets'
     A  12                                  DSPATR(ND)
     A                                 10 32'Customer Lending'
     A  13                                  DSPATR(ND)
     A                                 11 32'Securities Trading'
     A  14                                  DSPATR(ND)
     A                                 12 32'Funds Transfers'
     A  15                                  DSPATR(ND)
     A                                 13 32'Futures & Options'
     A  16                                  DSPATR(ND)
      *
     A                                 19 32'Enter option :'
     A            OPTION         1N  B 19 47DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A                                 24 02'F3=Exit'
      *
