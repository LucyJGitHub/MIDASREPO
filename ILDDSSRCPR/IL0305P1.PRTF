      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IBLC 2002 ICD - Report')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  PRTF/IL0305P1 - IBLC 2002 - ICD Record - Print file          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX043             Date 23Oct01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2005')
     A                                      SKIPB(3)
     A                                     6'REFERENCE IL0305P1'
     A            BJURPT    R             39REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    55'IBLC I.C.D. 2002 - REPORT'
     A                                      SPACEA(1)
     A                                    55'-------------------------'
     A                                      SPACEA(3)
      *
      *--------------------------------*
      ***  Report DETAILS              *
      *--------------------------------*
      *
     A          R DETAIL
     A                                      SPACEB(001)
     A                                     2'Folder path                       -
     A                                      :'
     A            RFLDP         63A  O    +2
     A                                      SPACEA(001)
     A                                     2'Limit of Registration             -
     A                                      :'
     A            RLIMR         22A  O    +2
     A                                    +1'Euros'
     A                                      SPACEA(001)
     A                                     2'Print routing keys found report   -
     A                                      :'
     A            RPRID          1A  O    +2
     A                                      SPACEA(001)
     A                                     2'Use Dummy Issuer                  -
     A                                      :'
     A            RDMIS          1A  O    +2
     A                                      SPACEA(001)
     A                                     2'Activation date                   -
     A                                      :'
     A            RACTD          6S 0O    +2EDTWRD('  /  / 0')
     A                                      SPACEA(001)
     A                                     2'Interfaced modules                -
     A                                      :'
      *
      *----------------------------------*
      ***  Interfaced modules ON detail  *
      *----------------------------------*
      *
     A          R MODP1
     A            RMODU         30A  O    39
     A                                      SPACEA(001)
      *
      *----------------------------------*
      ***  End of report record format   *
      *----------------------------------*
      *
     A          R ENDP1                     SPACEB(002)
     A                                    50'****   END OF REPORT    ****'
     A                                      SPACEA(001)
