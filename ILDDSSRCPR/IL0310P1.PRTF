      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IBLC 2002 Operation Codes Detail List')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  PRTF/IL0310P1 - Operation Codes - List                       *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX043             Date 28Jun01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2005')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE IL0310P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    47'IBLC 2002 - OPERATION CODES '
     A  41                                78'FULL LIST'
     A                                      SPACEA(1)
     A  42                                78'AUDIT LIST'
     A                                      SPACEA(1)
      *
     A                                    47'---------------------------'
     A  41                                78'---------'
     A                                      SPACEA(2)
     A  42                                78'----------'
     A                                      SPACEA(2)
      *
     A                                    62'LOWER LIMIT AMOUNT'
     A                                      SPACEA(1)
     A                                     1'OPERATION'
     A                                    11'OPERATION NAME'
     A                                    55'COUNTRY CODE'
     A                                    75'IDENTIFICATION'
     A                                    95'LAST ENTRY'
     A                                   108'L A S T  A C T I V I T Y'
     A                                      SPACEA(1)
     A                                     3'CODE'
     A                                    55'-FOR BANK OWN A/C'
     A                                    75'-FOR BANK OWN A/C'
     A                                    95'USER ID  '
     A                                   108'ACTION    DATE'
     A                                      SPACEA(1)
     A                                    55'-FOR PRIVATE'
     A                                    75'-FOR PRIVATE'
     A                                      SPACEA(1)
     A                                     1'--------'
     A                                    11'--------------------'
     A                                    31'--------------------'
     A                                    55'----------------'
     A                                    75'----------------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            ROPCO          3         4
     A            RNARF         40        11
     A            RCLNB         15        55
     A            RILNB         15        75
     A            RULUP         10        95
     A            RCHTP          6       108
     A            RDATE          7       116
     A                                      SPACEA(1)
     A            RNARD         40        11
     A            RCLBK         15        56
     A            RILBK         15        76
     A    N43                               SPACEA(2)
     A     43                             95'Valid from :'
     A     43     RVFRM          8       108
     A     43                            117'to'
     A     43     RVLTO          8       120
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
