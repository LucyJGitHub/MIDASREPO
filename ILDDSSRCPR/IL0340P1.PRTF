      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IBLC 2002 Routing Keys Detail List')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  PRTF/IL0340P1 - ROUTING KEYS - LIST                          *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX043             Date 28Jun01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2005')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE IL0340P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    47'IBLC 2002 - ROUTING KEYS '
     A  41                                75'FULL LIST'
     A                                      SPACEA(1)
     A  42                                75'AUDIT LIST'
     A                                      SPACEA(1)
      *
     A                                    47'------------------------'
     A  41                                75'---------'
     A                                      SPACEA(2)
     A  42                                75'----------'
     A                                      SPACEA(2)
     A                                     5'CRITERIA :'
     A            RCRIT         35        16
     A                                      SPACEA(1)
      *
     A                                    50'DEBIT'
     A                                    72'CREDIT'
     A                                      SPACEA(1)
     A                                    45'OPERATION CODES'
     A                                    67'OPERATION CODES'
     A                                    89'LAST ENTRY'
     A                                   107'L A S T  A C T I V I T Y'
     A                                      SPACEA(1)
     A                                     5'MODULE ID'
     A                                    17'ROUTING KEY'
     A                                    45'   1   2   3   '
     A                                    67'   1   2   3    '
     A                                    89' USER ID  '
     A                                   107'ACTION    DATE    TIME'
     A                                      SPACEA(1)
     A                                     5'---------'
     A                                    17'---------------------'
     A                                    45'---------------'
     A                                    67'---------------'
     A                                    89'----------'
     A                                   107'------------------------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RMODI          2         8
     A N43        RRKEY         20        17
     A  43        RRKEY2        18        17
     A            RDOC1          3        47
     A            RDOC2          3        51
     A            RDOC3          3        55
     A            RCOC1          3        69
     A            RCOC2          3        73
     A            RCOC3          3        77
     A            RULUP         10        89
     A            RCHTP          6       107
     A            RDLUP          2  0    115
     A            RMLUP          3       117
     A            RYLUP          2  0    120
     A            RTLUP          6  0    123EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
