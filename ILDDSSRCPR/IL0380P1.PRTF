      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IBLC 2002 Daily Items Report')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  PRTF/IL0380P1 - Daily IBLC 2002 Items report                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX043             Date 28Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *---------------------------------*
      ***  Report Header Record Format  *
      *---------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys  +
     A                                      International Banking +
     A                                      Systems Ltd. 2005')
     A                                      SKIPB(3)
     A                                     6'REFERENCE IL0380P1'
     A            BJURPT    R             39REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    55'DAILY IBLC ITEMS REPORT'
     A                                      HIGHLIGHT
     A                                      SPACEA(1)
     A                                    55'-----------------------'
     A                                      HIGHLIGHT
      *
      *----------------------------------------------------*
      ***  Report header by branch/currency record format  *
      *----------------------------------------------------*
      *
     A          R BRCHP1
     A                                      SPACEB(1)
     A                                     1'BRANCH   :'
     A            RBRCH          3        12
     A                                      HIGHLIGHT
     A                                      SPACEA(1)
     A                                     1'CURRENCY :'
     A            RCURR          3        12
     A                                      HIGHLIGHT
     A                                       SPACEA(2)
     A                                     1'OPER.'
     A                                   +07'AMOUNT DEBIT'
     A                                   +10'AMOUNT CREDIT'
     A                                    +6'MOD'
     A                                    +1'TRANSACTION NUMBER'
     A                                    +6'CTRY.'
     A                                    +3'IDENTIFICAT.'
     A                                    +4'NARRATIVE'
      *
     A                                      SPACEA(1)
     A                                     1'Code'
     A                                    54'Id.'
     A                                    83'Cd.'
      *
     A                                      SPACEA(1)
     A                                     1'----------------------------------+
     A                                      -----------------------------------+
     A                                      -----------------------------------+
     A                                      --------------------------------'
     A                                      SPACEA(1)
      *
      *----------------------------------*
      ***  Report details record format  *
      *----------------------------------*
      *
     A          R DETAIL
     A                                      SPACEB(1)
     A            ROPCO          3         2
     A            RAMNTD        22        +3
     A            RAMNTC        22        +1
     A            RMODI          2        +1
     A            RTRAN         16        +2
     A            RSEQ           6        +1
     A            RCTRY          2        +2
     A            RIDCO          2        +5
     A            RIDNB          9        +1
     A            RTXT          23        +4
      *
      ***  *IN85 : When Original CCY <> Currency of Reporting
      *
     A     85                               SPACEA(1)
     A     85                             54'Amount in Original Currency :'
     A     85     RTOAMN        22        +1
     A     85     RTOCUR         3        +1
      *
      *--------------------------------------------------*
      ***  Report Subtotal by transaction record format  *
      *--------------------------------------------------*
      *
     A          R SUBTP1
     A                                      SPACEB(1)
     A                                     8'----------------------'
     A                                    +1'----------------------'
     A                                     1'SUBT :'
     A                                      SPACEB(1)
     A            RTOTD         22         8
     A            RTOTC         22        +1
     A                                      SPACEA(1)
      *
      *--------------------------------------------*
      ***  Report total by currency record format  *
      *--------------------------------------------*
      *
     A          R TOTLP1
     A                                      SPACEB(1)
     A                                     8'----------------------'
     A                                    +1'----------------------'
     A                                     1'TOTAL :'
     A                                      SPACEB(1)
     A            RTOCD         22         8
     A            RTOCC         22        +1
     A                                      SPACEA(1)
      *
      *------------------------------*
      ***  Report end record format  *
      *------------------------------*
      *
     A          R ENDP1
     A                                      SPACEB(3)
     A                                    50
     A                                      '***  END OF REPORT FOR BRANCH '
     A            EBRCH          3   O    81
     A                                    86'***'
      *
