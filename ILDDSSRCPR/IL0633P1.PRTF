      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IBLC 2002 RTV Loans with MDATE Changed')         *
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  PRTF/IL0633P1 - Retrieve loans with maturity date changed    *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027A            Date 12May06               *
      *                 BUG10409           Date 04Feb06               *
      *                 CER001             Date 25Apr05               *
      *                 ULX043             Date 28Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027A - Change Customer Number to alpha, post 103 build.   *
      *  BUG10409 - Reference name change to IL0663P1 instead of      *
      *             BL0633P1                                          *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
      *
     A                                      SKIPB(3)
      **Start of change BUG10409
     A**********                           6'REFERENCE BL0633P1'                            BUG10409
     A                                     6'REFERENCE IL0633P1'
      **End of change BUG10409
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    50'Loans with Maturity date Changed'
     A                                      SPACEA(1)
     A                                    50'--------------------------------'
     A                                      SPACEA(3)
      *
     A                                     2'REFERENCE'
     A                                    +4'CUSTOMER'
     A                                    +4'TYPE/SUBTYPE'
     A                                   +10'AMOUNT'
     A                                   +12'CCY'
     A                                    +4'VALUE DATE'
     A                                    +4'MATURITY BEFORE'
     A                                    +4'MATURITY AFTER'
     A                                      SPACEA(1)
     A                                     1'----------------------------------+
     A                                      -----------------------------------+
     A                                      -----------------------------------+
     A                                      -------------------------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail record format *
      *-------------------------*
      *
     A          R DETAP1
     A                                      SPACEB(1)
      **Start of change CLE148
     A**********  RLNRF          6  0      2                                                  CLE148
     A            RLNRF          6A        2
      **End of change CLE148
      * Start /CSD027A/
     A**********  RCUST          6  0     +6                                                 CSD027A
     A            RCUST          6        +6
      * End /CSD027A/
     A            RTYPE          2        +8
     A            RSTYP          2        +4
     A            RAMNT         21        +4
     A            RCCY           3        +6
     A            RVDAT          6  0     +5
     A                                      EDTCDE(Y)
     A            RMBEF          6  0     +9
     A                                      EDTCDE(Y)
     A            RMAFT          6  0    +10
     A                                      EDTCDE(Y)
      *
      *--------------------------------*
      ***  End of report record format *
      *--------------------------------*
      *
     A          R ENDP1
     A                                    54'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
