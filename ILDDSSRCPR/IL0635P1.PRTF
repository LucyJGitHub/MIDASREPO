      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IBLC 2002 RTV Customer with COLC changed')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  PRTF/IL0635P1 - Rtv Customers with country of loc. changed   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX043             Date 28Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2005')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE IL0635P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    45'Customers with Country of Locati+
     A                                      on changed'
     A                                      SPACEA(1)
     A                                    45'--------------------------------+
     A                                      ----------'
     A                                      SPACEA(3)
      *
     A                                    24'LOCATION'
     A                                      SPACEA(1)
     A                                     2'CUSTOMER'
     A                                    +3'SHORTNAME'
     A                                    +2'OLD'
     A                                    +2'NEW'
     A                                    +3'REPORT TOWN'
     A                                    +3'NEW ADDRESS'
     A                                      SPACEA(1)
     A                                     1'----------------------------------+
     A                                      -----------------------------------+
     A                                      -----------------------------------+
     A                                      -------------------------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail record format *
      *-------------------------*
      *
     A          R DETAP1
     A                                      SPACEB(1)
     A            RCUST          6         3
     A            RCSSN         10        +4
     A            RLBEF          2        +2
     A            RLAFT          2        +2
     A            RTOWN         10        +4
     A            RADR1         35        +4
     A            RADR2         35        +3
     A                                      SPACEA(1)
      *
     A            RADR3         35        49
     A            RADR4         35        +3
     A                                      SPACEA(1)
      *
      *--------------------------------*
      ***  End of report record format *
      *--------------------------------*
      *
     A          R ENDP1
     A                                    54'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
