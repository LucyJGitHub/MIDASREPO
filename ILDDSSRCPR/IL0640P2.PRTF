      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IBLC 2002 DL Extraction - RK not Found')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  PRTF/IL0640P2 - DL Extraction - Routing Key Not Found List   *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX043             Date 31Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2                    TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2005')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE IL0640P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    34'IBLC 2002 - Dealing Module Extract+
     A                                      ion - Routing Keys not found Report'
     A                                      SPACEA(1)
     A                                    34'----------------------------------+
     A                                      -----------------------------------'
     A                                      SPACEA(3)
      *--------------------------------*
      ***  Detail header record format *
      *--------------------------------*
      *
     A          R DETHEA
     A                                      SPACEB(3)
     A                                    50'LIST IF ROUTING KEYS NOT FOUND'
     A                                      SPACEA(1)
     A                                    50'------------------------------'
     A                                      SPACEA(1)
     A                                    17'DEAL'
      **Start of Change CER001
     A**********                          55'A/C KEY'                                         CER001
     A**********                          70'FILE AMOUNT'                                     CER001
     A                                    58'A/C KEY'
     A                                    76'FILE AMOUNT'
      **End of Change CER001
     A                                      SPACEA(1)
     A                                    17'----'
      **Start of Change CER001
     A**********                          55'-------'                                         CER001
     A**********                          70'-----------'                                     CER001
     A                                    58'-------'
     A                                    76'-----------'
      **End of Change CER001
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail record format *
      *-------------------------*
      *
     A          R DETAIL
     A                                      SPACEB(1)
     A            RDLNO          6S 0     17
     A            RMODI          2        +2
     A            RDTYP          2        +2
     A            REVTP          1        +2
     A            RCTYP          2        +2
     A            RITYP          2        +2
     A            RDURA          1        +2
     A            RSETT          1        +2
     A            REVNT          2        +2
      **Start of Change CER001
     A**********  RAKEY         10        55                                                  CER001
     A**********  RAMNT         15        70                                                  CER001
     A            RAKEY         14        55
     A            RAMNT         15        74
      **End of Change CER001
     A            KWECCY         3        +2
      *
      *--------------------------------*
      ***  End of report record format *
      *--------------------------------*
      *
     A          R ENDP2
     A                                    54'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      *----------------------------------------*
      ***  No missing record key record format *
      *----------------------------------------*
      *
     A          R NOLIP2
     A                                    49'***  NO MISSING ROUTING KEYS  ***'
     A                                      SPACEB(3)
      *
