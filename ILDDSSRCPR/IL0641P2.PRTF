      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IBLC 2002 LE Extraction - RK not found')         *
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  PRTF/IL0641P2 - LE Extraction - Routing Key Not Found List   *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CER001             Date 25Apr05               *
      *                 ULX043             Date 31Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2                    TEXT('(c) Finastra International +
     A                                      Limited 2005')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE IL0641P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    34'IBLC 2002 - Lending Module Extract+
     A                                      ion - Routing Keys not found Report'
     A                                      SPACEA(1)
     A                                    34'----------------------------------+
     A                                      -----------------------------------'
     A                                      SPACEA(3)
      *
      *--------------------------------*
      ***  Detail header record format *
      *--------------------------------*
      *
     A          R DETHEA
     A                                      SPACEB(3)
     A                                    50'LIST IF ROUTING KEYS NOT FOUND'
     A                                      SPACEA(1)
     A                                    50'------------------------------'
     A                                      SPACEA(1)
     A                                    17'LOAN'
     A                                    55'A/C KEY'
     A                                    70'FILE AMOUNT'
     A                                      SPACEA(1)
     A                                    17'----'
     A                                    55'-------'
     A                                    70'-----------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail record format *
      *-------------------------*
      *
     A          R DETAIL
     A                                      SPACEB(1)
      **Start of change CLE148
     A**********  RLNNO          6S 0     17                                                  CLE148
     A            RLNNO          6A       17
      **End of change CLE148
     A            RMODI          2        +2
     A            RPTYP          2        +2
     A            REVTP          1        +2
     A            RCTYP          2        +2
     A            RDURA          1        +2
     A            RSETT          1        +2
     A            REVNT          2        +2
     A            RAKEY         10        55
     A            RAMNT         15        70
     A            KWECCY         3        +2
      *
      *--------------------------------*
      ***  End of report record format *
      *--------------------------------*
      *
     A          R ENDP2
     A                                    54'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      *----------------------------------------*
      ***  No missing record key record format *
      *----------------------------------------*
      *
     A          R NOLIP2
     A                                    49'***  NO MISSING ROUTING KEYS  ***'
     A                                      SPACEB(3)
      *
