      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IBLC 2002 SE Extraction - RK found')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  PRTF/IL0643P1 - SE Extraction - Routing Key Found List       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX043             Date 18Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2005')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE IL0643P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    32'IBLC 2002 - Securities Trading Mod+
     A                                      ule Extraction - Routing Keys foun+
     A                                      d Report'
     A                                      SPACEA(1)
      *
     A                                    32'----------------------------------+
     A                                      ----------------------------------+
     A                                      --------'
     A                                      SPACEA(3)
      *
      *--------------------------------*
      ***  Detail header record format *
      *--------------------------------*
      *
     A          R DETHP1
     A                                      SPACEB(3)
     A                                    53'LIST OF ROUTING KEYS USED'
     A                                      SPACEA(1)
     A                                    53'-------------------------'
     A                                      SPACEA(1)
     A                                    04'SECURITY'
     A                                      SPACEA(1)
     A                                    04'--------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail record format *
      *-------------------------*
      *
     A          R DETAP1
     A                                      SPACEB(1)
     A            RSESH         10        02
     A            RMODI          2        +2
     A            RPTYP          1        +2
     A            RTTYP          1        +2
     A            REVTP          1        +2
     A            RCTYP          2        +2
     A            RITYP          2        +2
     A            RDURA          1        +2
     A            RCCCY          1        +2
     A            RSETT          1        +2
     A            REVNT          2        +2
     A            RDRR1          3        +2
     A  40                                +1'*'
     A            RDRR2          3        +2
     A  41                                +1'*'
     A            RDRR3          3        +2
     A  42                                +1'*'
     A            RCRR1          3        +2
     A  43                                +1'*'
     A            RCRR2          3        +2
     A  44                                +1'*'
     A            RCRR3          3        +2
     A  45                                +1'*'
     A            RAKEY         20        +2
     A            ROWN           1        +2
     A            RAMNT         15        +2
      *
      *----------------------------*
      ***  Not extracted message ***
      *----------------------------*
      *
      ***  'Under limit of Registration '
      *
     A          R REJAP1
     A                                      SPACEB(1)
      *
     A                                    10'Limit of Registration is not reach+
     A                                       ed ('
     A            RLCCY         22        +0
     A            REVCCY         3        +1
     A                                    +0'). Event Amount is '
     A            REAMT         22        +0
     A            RCCYEV         3        +1
     A                                    +0'.'
      *
      ***  'Operation Code not yet or no more valid. '
      *
     A          R REJBP1
     A                                      SPACEB(1)
      *
     A                                    10'Not Extracted : Operation Code ('
     A            RCODE          3        +0
     A                                    +0') is not valid.'
      *
      *--------------------------------*
      ***  End of report record format *
      *--------------------------------*
      *
     A          R ENDP1
     A                                    52'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      *----------------------------------------*
      ***  No record record format *
      *----------------------------------------*
      *
     A          R NOLIP1
     A                                      SPACEB(003)
     A                                    50
     A                                      '** NO DETAILS TO REPORT **'
      *
