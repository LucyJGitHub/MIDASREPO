      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IBLC 2002 F&O Extraction - RK found')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  PRTF/IL0645P1 - F&O Extraction - Routing Key Found List      *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX043             Date 02Oct01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE IL0645P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    34'IBLC 2002 - Futures&Options Modul+
     A                                      e Extraction - Routing Keys found -
     A                                      Report'
     A                                      SPACEA(1)
     A                                    34'---------------------------------+
     A                                      -----------------------------------
     A                                      ------'
     A                                      SPACEA(3)
      *
      *--------------------------------*
      ***  Detail header record format *
      *--------------------------------*
      *
     A          R DETHP1
     A                                      SPACEB(3)
     A                                    53'LIST OF ROUTINE KEYS USED'
     A                                      SPACEA(1)
     A                                    53'-------------------------'
     A                                      SPACEA(1)
     A                                    10'TRADE'
     A                                      SPACEA(1)
     A                                    10'-----'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail record format *
      *-------------------------*
      *
     A          R DETAP1
     A                                      SPACEB(1)
     A            RTNBR          6S 0     10
     A            RMODI          2        +2
     A            RPTYP          1        +2
     A            REVTP          1        +2
     A            RMTYP          1        +2
     A            RBTYP          2        +2
     A            RCTYP          2        +2
     A            RBCOD          1        +2
     A            RSETT          1        +2
     A            REVNT          2        +2
     A** Start of Change CER001
     A**********  ACD1           4  0O    +3                                                  CER001
     A            ACD1          10  0O    +3
     A** End of Change CER001
     A            PRF1           1  0O    +2
     A** Start of Change CER001
     A**********  ACD4           4  0O    +3                                                  CER001
     A            ACD4          10  0O    +3
     A** End of Change CER001
     A            PRF4           1  0O    +2
     A            RDRR1          3        +2
     A            RDRR2          3        +2
     A            RDRR3          3        +2
     A            RCRR1          3        +2
     A            RCRR2          3        +2
     A            RCRR3          3        +2
     A            RAKEY         14        +2
     A            ROWN           1        +2
     A            RAMNT         15        +2
      *
      *----------------------------*
      ***  Not extracted message ***
      *----------------------------*
      *
      ***  'Under limit of Registration '
      *
     A          R REJAP1
     A                                      SPACEB(1)
      *
     A                                    10'Limit of Registration is not reach+
     A                                       ed ('
     A            RLCCY         22        +0
     A            REVCCY         3        +1
     A                                    +0'). Event Amount is '
     A            REAMT         22        +0
     A            RCCYEV         3        +1
     A                                    +0'.'
      *
      ***  'Operation Code not yet or no more valid. '
      *
     A          R REJBP1
     A                                      SPACEB(1)
      *
     A                                    10'Not Extracted : Operation Code ('
     A            RCODE          3        +0
     A                                    +0') is not valid.'
      *
      *--------------------------------*
      ***  End of report record format *
      *--------------------------------*
      *
     A          R ENDP1
     A                                    54'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      *----------------------------------------*
      ***  No record record format *
      *----------------------------------------*
      *
     A          R NOLIP1
     A                                      SPACEB(003)
     A                                    50
     A                                      '** NO DETAILS TO REPORT **'
      *
