      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IBLC 2002 Synoptique Report')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  PRTF/IL0680P1 - SYNOPTIQUE Report                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX043             Date 31Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys  +
     A                                      International Banking +
     A                                      Systems Ltd. 2005')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE IL0680P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    34'IBLC 2002 - synoptique report'
     A                                      SPACEA(1)
     A                                    34'------------------------------'
     A                                      SPACEA(3)
      *
      *
      *
     A          R HEADP2
      *
     A                                      SPACEB(2)
     A                                    10'BRCH'
     A                                    15'CCY'
     A                                    19'MODULE'
     A                                    26'CODE'
     A                                    32'TRANSACTION NO'
     A                                    53'COUNTRY'
     A                                    63'IDENTIFICATION'
     A                                    94'AMOUNT'
     A                                   107'AMOUNT IN EUR'
     A                                      SPACEA(1)
     A                                    10'----'
     A                                    15'---'
     A                                    19'------'
     A                                    26'----'
     A                                    32'--------------'
     A                                    53'-------'
     A                                    63'--------------'
     A                                    94'------'
     A                                   107'-------------'
      *
     A                                      SPACEA(2)
      *-------------------------*
      ***  Detail record format *
      *-------------------------*
      *
     A          R DETAP1
     A                                      SPACEB(1)
     A            RBRCH          3        10
     A            RCURR          3        15
     A            RMODI          2        20
     A            ROPCO          3        26
     A            RTEXT         16        32
     A            RCTRY          2        55
     A            RIDCO          2        63
     A                                    65'/'
     A            RIDNB          9        66
     A            RTAMN         22        80
     A            ROAMNB        22        99
      *
     A            RERRO         80A       26
     A                                      SPACEB(1)
      *------------------------------*
      ***  Report end record format  *
      *------------------------------*
      *
     A          R ENDP1
     A                                      SPACEB(3)
     A                                    50
     A                                      '***  END OF REPORT ***'
      *
