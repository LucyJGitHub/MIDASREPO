      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IBLC 2002 Routing keys found - List')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  PRTF/IL0695P2 - IBLC 2002 Routing keys Found  - List         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX043             Date 06Oct01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBC 2002 Reporting                                  *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2                    TEXT('(c) Misys  +
     A                                      International Banking +
     A                                      Systems Ltd. 2005')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE IL0695P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    38'IBLC 2002 - Routing Key Found+
     A                                       - Audit List'
     A                                      SPACEA(1)
     A                                    38'-----------------------------+
     A                                      -------------'
     A                                      SPACEA(3)
      *
     A                                   100'CODES DEBIT'
     A                                    +1'CODES CREDIT'
     A                                      SPACEA(1)
     A                                     2'MODULE'
     A                                    +4'TRANSACTION'
     A                                    +4'ROUTING KEY'
     A                                   +10'ACCOUNT KEY'
     A                                   +18'AMOUNT'
     A                                    +8'CURRENCY'
     A                                    +1' 1   2   3 '
     A                                    +1' 1   2   3  '
     A                                    +1'RVRS'
     A                                      SPACEA(1)
      *
     A                                     1'----------------------------------+
     A                                      -----------------------------------+
     A                                      -----------------------------------+
     A                                      -------------------------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail record format *
      *-------------------------*
      *
     A          R DETAP2
     A                                      SPACEB(1)
     A            RMODI          2         4
     A            RTRNB         17        +3
     A            RRKEY         20        +1
     A            RAKEY         20        +1
     A            RAMNT         22        +1
     A            RECCY          3        +3
     A            RDRR1          3        +3
     A            RDRR2          3        +1
     A            RDRR3          3        +1
     A            RCRR1          3        +1
     A            RCRR2          3        +1
     A            RCRR3          3        +1
     A            RRVRS          1        +3
      *
      *--------------------------------*
      ***  End of report record format *
      *--------------------------------*
      *
     A          R ENDP2
     A                                    54'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NOLIP2
     A                                      SPACEB(3)
     A                                    50'*** NO DETAILS TO REPORT ***'
      *
