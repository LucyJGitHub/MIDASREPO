     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IBLC 2002 Operation Code validation')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  RPG/IL0410    - IBLC OPERATION CODE VALIDATION               *
      *                                                               *
      *  Function:  This program valide IBLC operation code and       *
      *             return Lower limit amount for Country code and    *
      *             Identification converted in transaction currency. *
      *                                                               *
      *  Remark 1 : Special use of PLIMT parameter :                  *
      *               - *BANK for bank own account validation         *
      *               - *NBANK otherwise                              *
      *                                                               *
      *  Remark 2 : Special values in lower limits :                  *
      *             If lower limit returned by ILCODEPD are           *
      *             0 or 99999999999999 (*HIVAL) bypass convertion    *
      *             in the currency passed as parameter.              *
      *                                                               *
      *  (phase(s)) All                                               *
      *                                                               *
      *  Called By: FT0045WL - Outgoing Payment Ext Details Window    *
      *             FT0055WL - Incoming PAyment Ext details Window    *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX043             Date 28Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     FILCODEPDIF  E           K        DISK         KINFSR *PSSR
      ***  IBLC 2002 Operation codes   Retrieval index     Prefix CO.
      *
     FIL0410AUO   E                    PRINTER      KINFSR *PSSR      UC
      ***  Audit report
      *
      /EJECT
      *****************************************************************
     E                    POWER   1   7  7 3             Power Array
      *** Conversion Routine Array (SR/ZCCYCN)
      *
     E                    CPY@    1   1 80               Copyright Array
      ***  Copyrights Array
      *
      /EJECT
      *****************************************************************
      *
     ILDA       E DSLDA
      ***  Midas Local Data Area
      *
     IPSDS       SDS
      ***  Program status data structure
      *
     ISDBANK    E DSSDBANKPD
      ***  Bank General Details
      *
     ISDGELR    E DSSDGELRPD
      ***  General Ledger ICD Details
      *
     ISDCURR    E DSSDCURRPD
      ***  Currency Details
      *
     IDSSDY     E DSDSSDY
      ***  DS for access programs, long data structure
      *
     IDSFDY     E DSDSFDY
      ***  DS for access programs, short data structure
      *
      /EJECT
      *****************************************************************
      *                 M A I N    P R O C E S S I N G                *
      *****************************************************************
      *
      ***  IF Operation Code is Filled, validate it
      *
     C           PCODE     IFNE *BLANKS
      *
      ***  Access IBLC operation code file
      *
     C           PCODE     CHAINILCODEPD             80
      *
      ***  If record not found exit program with PERID '1'
      *
     C   80                MOVEL'1'       PERID
     C   80                GOTO EXIT
      *
      ***  Operation Code is no more valid at date of Input
      *
     C           BJRDNB    IFLT COVFRM
     C           BJRDNB    ORGE COVLTO
     C                     MOVE '2'       PERID
     C                     ENDIF
      *
     C                     ENDIF
      *
      ***  Returns Limits in transaction Currency
      *
     C                     CALL 'AOCURRR0'                 'From' ccy
     C                     PARM *BLANKS   @RTCD   7        is
     C                     PARM '*KEY   ' @OPTN   7        EURO
     C                     PARM BKEURO    @CCY    3
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  If record not found exit program with Database error
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE            ****************
     C                     MOVEL'SDCURRL0'DBFILE           * DB ERROR 003 *
     C                     MOVELBKEURO    DBKEY     P      ****************
     C                     OUT  LDA
     C                     EXSR SRDBER
     C                     ENDIF                           -E1
      *
     C                     MOVELA6CYCD    ZCCYF            ZCCYCN Parms
     C                     Z-ADDA6SPRT    ZRATEF
     C                     MOVELA6MDIN    ZMDINF
     C                     Z-ADDA6NBDP    ZCDPF
      *
      ***  Access to transaction currency informations
      *
     C                     CALL 'AOCURRR0'                 'To' ccy
     C                     PARM *BLANKS   @RTCD   7        is
     C                     PARM '*KEY   ' @OPTN   7        Transction Ccy
     C                     PARM PTCCY     @CCY    3
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  If record not found exit program with Database error
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE            ****************
     C                     MOVEL'SDCURRL0'DBFILE           * DB ERROR 004 *
     C                     MOVELPTCCY     DBKEY            ****************
     C                     OUT  LDA
     C                     EXSR SRDBER
     C                     ENDIF
      *
     C                     MOVELA6CYCD    ZCCYT            ZCCYCN Parms
     C                     Z-ADDA6SPRT    ZRATET
     C                     MOVELA6MDIN    ZMDINT
     C                     Z-ADDA6NBDP    ZCDPT
      *
      ***  Convert Lower Limit for Country, If Operation code was Passed
      ***  Convert Lower Limit for Identification, If Operation code was Passed
      *
     C           PCODE     IFNE *BLANKS
      *
      ***  Country Limit
      *
     C           PLIMT     IFEQ '*BANK'
     C                     Z-ADDCOLCBK    ZAMTF            Bank Own Account
     C                     ELSE
     C                     Z-ADDCOLCNB    ZAMTF            otherwise
     C                     ENDIF
      *
      ***  Execute conversion routine if value is different than 0 or *HIVAL
      *
     C           ZAMTF     IFNE *HIVAL
     C           ZAMTF     ANDNE0
     C                     EXSR ZCCYCN
     C                     ELSE
     C                     Z-ADDZAMTF     ZAMTT
     C                     ENDIF
      *
     C                     Z-ADDZAMTT     PCLIM            Returns
      *
      ***  Identification Limit
      *
     C           PLIMT     IFEQ '*BANK'
     C                     Z-ADDCOLIBK    ZAMTF            Bank Own Account
     C                     ELSE
     C                     Z-ADDCOLINB    ZAMTF            Otherwise
     C                     ENDIF
      *
      ***  Execute conversion routine if value is different than 0 or *HIVAL
      *
     C           ZAMTF     IFNE *HIVAL
     C           ZAMTF     ANDNE0
     C                     EXSR ZCCYCN
     C                     ELSE
     C                     Z-ADDZAMTF     ZAMTT
     C                     ENDIF
      *
     C                     Z-ADDZAMTT     PILIM            Returns
      *
     C                     ELSE
      *
      ***  Returns Limit for Registration
      *
     C           PLIMT     IFEQ '*BANK'
     C                     Z-ADD0         ZAMTF            Bank Own Account
     C                     ELSE
     C                     Z-ADDPLREG     ZAMTF            Limit of Registration
     C                     ENDIF
      *
      ***  Execute conversion routine if value is different than 0 or *HIVAL
      *
     C           ZAMTF     IFNE *HIVAL
     C           ZAMTF     ANDNE0
     C                     EXSR ZCCYCN
     C                     ELSE
     C                     Z-ADDZAMTF     ZAMTT
     C                     ENDIF
      *
     C                     Z-ADDZAMTT     PLREG            Returns
      *
     C                     ENDIF
      *
      *****************************************************************
      *              E N D    O F    P R O C E S S I N G              *
      *****************************************************************
      *
      ***  Return to calling program
      *
     C           EXIT      TAG
      *
     C                     MOVEL*ON       *INLR
     C                     RETRN
      *
      /EJECT
      *****************************************************************
      * ZCCYCN - Convert an amount in one currency to another currency*
      *****************************************************************
      *
      /COPY ZSRSRC,ZCCYCN
      *
      /EJECT
      *****************************************************************
      * SRDBER - Database error suboutine                             *
      *****************************************************************
      *
     C           SRDBER    BEGSR
      *
      ***  Audit list
      *
     C                     OPEN IL0410AU
     C                     MOVEL'IL0410'  DBPGM     P
     C                     MOVELPCPGM     DBCPGM 10
     C                     MOVEL'1'       PDBID
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     WRITEBOTTOM
     C                     CLOSEIL0410AU
      *
     C                     EXSR *PSSR
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * *PSSR - Program exception error routine                       *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * *INZSR - Initial subroutine                                   *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
      ***  Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Define parameters list
      *
     C           *ENTRY    PLIST
      ***  Input parameters
     C                     PARM           PCODE   3        IBLC Operation code
     C                     PARM           PLIMT   6        Limit type
     C                     PARM           PTCCY   3        Transaction currency
     C                     PARM           PCPGM  10        Calling program
      ***  Output parameters
     C                     PARM           PERID   1        Error Id
     C                     PARM           PDBID   1        Database error Id
     C                     PARM           PCLIM  150       Country limit
     C                     PARM           PILIM  150       Identification limit
     C                     PARM           PLREG  150       Limit of Registration
      *
      ***  Define LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
      ***  Prepare LDA
      *
     C                     MOVEL'IL0410'  DBPGM     P
      *
      ***  Reset output parameters
      *
     C                     MOVEL'0'       PERID
     C                     MOVEL'0'       PDBID
     C                     Z-ADD0         PCLIM
     C                     Z-ADD0         PILIM
     C*** never plreg      Z-ADD0         PLREG            input/output
      *
      ***  Access Bank Details file
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  If record not found exit program with Database error
      *
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ****************
     C                     MOVEL'SDBANKL1'DBFILE           * DB ERROR 001 *
     C                     MOVEL'*none'   DBKEY            ****************
     C                     OUT  LDA
     C                     EXSR SRDBER
     C                     ENDIF
      *
      **  Retrieve the Euro Currency in the General Ledger ICD
     C                     CALL 'AOGELRR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDGELR    PARM SDGELR    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVEL'SDGELRPD'DBFILE    P      * DB ERROR 02 *
     C                     MOVEL'*FIRST'  DBKEY     P      ***************
     C                     OUT  LDA
     C                     EXSR SRDBER
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
**  POWER - ARRAY OF POWERS FOR CURRENCY CONVERSION
0000001
0000010
0000100
0001000
0010000
0100000
1000000
**  CPY@
(c) Finastra International Limited 2005
