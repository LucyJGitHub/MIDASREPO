     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IBLC 2002 Retrieve Customer Number')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC Module                                          *
      *                                                               *
      *  RPG/IL0421    - IBLC RETRIEVE CUSTOMER NUMBER                *
      *                                                               *
      *  Function:  This program retrieve customer number depending   *
      *             entry type (Retail, Partial, Full nostro, Etc...) *
      *                                                               *
      *  (phase(s)) All                                               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027A            Date 11May06               *
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 217849             Date 20Nov02               *
      *                 UPG402             Date 04Oct04               *
      *                 IL0010             Date 07Dec01               *
      *                 ULX043  *CREATE    Date 13Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027A - Conversion Of Customer Number to Alpha.            *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  217849 - Emergency fix on call                               *
      *  UPG402 - Recompile due to R4.02 upgrade                      *
      *  IL0010 - CFT004 - IBAN Account Numbers                       *
      *  ULX043 - IBLC 2002                                           *
      *                                                               *
      *****************************************************************
      *
     FSDCUSTL7IF  E           K        DISK                           UC
     FSDNOSTL1IF  E           K        DISK                           UC
      ***  MIDAS ABS Nostro Details
      *
     FACNUM   IF  E           K        DISK                           UC
      ***  MIDAS ABS Retail accounts
      *                                                                   IL0010
     FACCNTL6 IF  E           K        DISK                           UC  IL0010
      ***  MIDAS IBAN Customer Account                                    IL0010
     F            ACCNTABF                          KRENAMERTVACC
      *
      /EJECT
      *-------------------------------------------------------------------------
     E                    CPY@    1   1 80               COPYRIGHT  ARRAY
      /EJECT
      *-------------------------------------------------------------------------
      * Ds to retreive details from paramaters passed
     IPARTS       DS
      * Full GL a/c number
     I                                        1   6 WUCNUM
     I**********                             10  13 WUACD1                                    CER001
     I                                       16  25 WUACD1                                    CER001
     I                                        1  12 WUSWID
      * Partial a/c number (Customer is the same)
     I**********                              7  10 WUACD2                                    CER001
     I                                        7  16 WUACD2                                    CER001
      * Short nostro - nostro number
     I                                        1   2 WUNNB1
      * Full nostro - currency and nostro number
     I                                        1   3 WUNCC
     I                                        4   5 WUNNB2
      * IBAN Account Number                                               IL0010
     I                                        1  34 WIBAN                 IL0010
     IDSFDY     E DSDSFDY                                                 IL0010
      **  First DS for access programs, short DS                          IL0010
      *
      /EJECT
      *-------------------------------------------------------------------------
      * Main processing
      *-------------------------------------------------------------------------
      *
      ***  If first call
      *
     C           POPCL     IFEQ 0
     C           ##OPEN    ANDEQ*BLANKS                                        217849
     C                     OPEN SDNOSTL1
     C                     OPEN ACNUM
     C                     OPEN SDCUSTL7
     C           CFT004    IFEQ 'Y'                                       IL0010
     C                     OPEN ACCNTL6                                   IL0010
     C                     ENDIF                                          IL0010
     C                     Z-ADD1         POPCL
     C                     MOVEL'Y'       ##OPEN  1                             217849
     C                     END
      *
      ***  If close pgm
      *
     C           POPCL     IFEQ 9
     C           ##OPEN    IFEQ 'Y'                                             217849
     C                     CLOSESDNOSTL1
     C                     CLOSEACNUM
     C                     CLOSESDCUSTL7
     C                     ENDIF                                                217849
     C                     MOVEL*BLANKS   ##OPEN                                217849
     C           CFT004    IFEQ 'Y'                                       IL0010
     C                     CLOSEACCNTL6                99                 IL0010
     C                     ENDIF                                          IL0010
     C                     SETON                       LR
     C                     RETRN
     C                     END
      *
      ***  Initialize work fields
      *
     C                     MOVE *BLANKS   PCUST
     C                     MOVE PCCY      WWCCY   3
     C                     MOVE PTYPE     WWTYPE  1
     C                     MOVELPLINE     PARTS
     C                     MOVE 'N'       PFOUND
      *
      ***  Find correct settlement account
      *
     C                     EXSR SETTLE
      *-------------------------------------------------------------------------
      * End of processing
      *-------------------------------------------------------------------------
      *
      ***  Return to caller
      *
     C                     RETRN
      /EJECT
      *-------------------------------------------------------------------------
      * SETTLE - Find correct settlement account
      *-------------------------------------------------------------------------
      *
     C           SETTLE    BEGSR
     C                     SELEC
      *
      ***  If short nostro - access nostro details
      *
     C           WWTYPE    WHEQ 'N'
     C                     MOVELWWCCY     KCYCD
     C                     MOVELWUNNB1    KNONB
     C           KNOST     CHAINSDNOSTL1             90
      *
      ***  If found - set customer and account code
      *
     C           *IN90     IFEQ *OFF
     C                     MOVELA7ACCD    PACCD
     C                     MOVELA7CUST    PCUST
     C                     MOVE 'Y'       PFOUND
     C                     ENDIF
      *
      ***  If full nostro - setup key and access nostro details
      *
     C           WWTYPE    WHEQ 'F'
     C                     MOVELWUNCC     KCYCD
     C                     MOVELWUNNB2    KNONB
     C           KNOST     CHAINSDNOSTL1             90
      *
      ***  If found - set customer and account code
      *
     C           *IN90     IFEQ *OFF
     C                     MOVELA7ACCD    PACCD
     C                     MOVELA7CUST    PCUST
     C                     MOVE 'Y'       PFOUND
     C                     ENDIF
      *
      ***  If Retail - access account master
      *
     C           WWTYPE    WHEQ 'R'
     C                     MOVELPLINE     KACNO  100
     C           KACNO     CHAINACNUM                90
      *
      ***  If found - set customer and account code
      *
     C           *IN90     IFEQ *OFF
     C                     MOVELACOD      PACCD
     C                     MOVE CNUM      PCUST
     C                     MOVE 'Y'       PFOUND
     C                     ENDIF
      *
      ***  If partial account - retreive a/c code and customer
      *
     C           WWTYPE    WHEQ 'P'
     C                     MOVELWUCNUM    PCUST
     C                     MOVELWUACD2    PACCD
     C                     MOVE 'Y'       PFOUND
      *
      ***  If General Ledger account - retreive a/c code and customer
      *
     C           WWTYPE    WHEQ 'G'
     C                     MOVELWUCNUM    PCUST
     C                     MOVELWUACD1    PACCD
     C                     MOVE 'Y'       PFOUND
      *
      ***  If customer type is 'C' - retrieve only customer number
      *
     C           WWTYPE    WHEQ 'C'                        Customer number
     C                     MOVELWUCNUM    PCUST
     C                     MOVEL*BLANKS   PACCD
     C                     MOVE 'Y'       PFOUND
      *
      ***  If customer type is 'S' - retrieve only customer number
      *
     C           WWTYPE    WHEQ 'S'                        SWIFT ID
     C                     MOVELWUSWID    KEY12  12
     C           KEY12     CHAINSDCUSTL7             85
     C           *IN85     IFEQ *OFF
     C                     MOVELBBCUST    PCUST
     C                     MOVEL*BLANKS   PACCD
     C                     MOVE 'Y'       PFOUND
     C                     ENDIF
      *
      ***  If customer type is 'I' - retrieve By IBAN Account Nr.         IL0010
      *                                                                   IL0010
     C           WWTYPE    WHEQ 'I'                        IBAN           IL0010
     C           CFT004    ANDEQ'Y'                        with feature   IL0010
     C           WIBAN     CHAINACCNTL6              90                   IL0010
      *                                                                   IL0010
      ***  If found, send Account code and Customer number                IL0010
      *                                                                   IL0010
     C           *IN90     IFEQ *OFF                                      IL0010
     C                     MOVELACOD      PACCD                           IL0010
     C                     MOVE CNUM      PCUST                           IL0010
     C                     MOVE 'Y'       PFOUND                          IL0010
     C                     ENDIF                                          IL0010
      *                                                                   IL0010
     C                     ENDSL
     C                     ENDSR
      /EJECT
      *-------------------------------------------------------------------------
      * *INZSR - Initial subroutine
      *-------------------------------------------------------------------------
      *
     C           *INZSR    BEGSR
      *
      ***  Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Define parameters list
      *
     C           *ENTRY    PLIST
      *
      ***  Input parameters
      *
     C                     PARM           PCCY    3        Currency
     C                     PARM           PTYPE   1        Type
     C                     PARM           PLINE  35        Customer
     C                     PARM           POPCL   10       Open/Close Id
      *
      ***  Output parameters
      *
     C**********           PARM           PACCD   4        A/c code                           CER001
     C                     PARM           PACCD  10        A/c code                           CER001
     C                     PARM           PCUST   6        Customer number
     C                     PARM           PFOUND  1        Result (Y/N)
      *
      ***  Define Klist to access LF/ACCNT
      *
     C           KACN      KLIST
     C**********           KFLD           KCNUM   60                                         CSD027A
     C                     KFLD           KCNUM   6                                          CSD027A
     C                     KFLD           KCCY    3
     C**********           KFLD           KACOD   40                                          CER001
     C                     KFLD           KACOD  100                                          CER001
     C                     KFLD           KACSQ   20
      *
      ***  Key list to access nostro details
      *
     C           KNOST     KLIST
     C                     KFLD           KCYCD   3
     C                     KFLD           KNONB   2
      *                                                                   IL0010
      ***  Access CFT004 - IBAN Account Numbers                           IL0010
      *                                                                   IL0010
     C                     MOVE 'N'       CFT004  1                       IL0010
     C                     CALL 'AOSARDR0'             99                 IL0010
     C                     PARM *BLANKS   PRTRN   7                       IL0010
     C*******************  PARM '*KEY   ' POPTN   7                 IL0010IL0010
     C                     PARM '*VERIFY' POPTN   7                       IL0010
     C                     PARM 'CFT004'  PSARD   6                       IL0010
     C                     PARM           DSFDY                           IL0010
      *                                                                   IL0010
     C           PRTRN     IFEQ *BLANKS                    found          IL0010
     C           *IN99     ANDEQ*OFF                       no error       IL0010
     C                     MOVE 'Y'       CFT004                          IL0010
     C                     ENDIF                                          IL0010
      *
     C                     ENDSR
      /EJECT
      *-------------------------------------------------------------------------
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
