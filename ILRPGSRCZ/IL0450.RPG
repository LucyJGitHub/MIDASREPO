     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IBLC 2002 Access Customer Extension')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  RPG/IL0450    - ACCESS IBLC CUSTOMER DETAILS EXTENDED FILE   *
      *                                                               *
      *  Function:  This program access to IBLC customer details      *
      *             Extended file and return on IBFROM parameter      *
      *             the extended informations to be printed           *
      *                                                               *
      *  (phase(s)) *all                                              *
      *                                                               *
      *  Called By: SD0010P - Customer Details Aud/Req report         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX043             Date 17Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     F***SDCUSTYLIF  E           K        DISK                                                CER001
     FSDCUX1L0IF  E           K        DISK                                                   CER001
      ***  IBLC Customer details       Retrieval index     Prefix CU.
      *
      /EJECT
      *-------------------------------------------------------------------------
     E                    CPY@    1   1 80               COPYRIGHT  ARRAY
      /EJECT
      *-------------------------------------------------------------------------
     IIBFROM      DS
      * Extended informations from IBLC 2002
     I                                        1 132 IBFRO1
     I                                      133 264 IBFRO2
     I                                      265 396 IBFRO3
     I                                      367 528 IBFRO4
     I                                      529 660 IBFRO5
     I            DS
      * Define fields narratives
     I              'IBLC 2002 :'         C         FN00
     I              'Customer type ..:'   C         FN01
     I              'Identification Code -C         FN02
     I              '.:'
     I              'Identification Nr  :'C         FN03
     I              'Immatriculation Code-C         FN04
     I              '.:'
     I              'Immatriculation Nr :'C         FN05
      *
     ILINE1       DS                            132
     I                                        2  12 L1DS00
     I                                       18  34 L1DS01
     I                                       36  37 CUTYPE
     I                                       37  37 CUTYP2
     I                                       56  77 L1DS03
     I                                       79  80 CUIDCO
     I                                       93 112 L1DS05
     I                                      114 122 CUIDEN
      *
     ILINE2       DS                            132
     I                                       56  77 L2DS01
     I                                       79  80 CUIMCO
     I                                       93 112 L2DS03
     I                                      114 122 CUIMNB
      *
      /EJECT
      *-------------------------------------------------------------------------
      * Main processing
      *-------------------------------------------------------------------------
      *
      ***  Access extended currency record
      *
     C********** PCNUM     CHAINSDCUSTYL             89                                       CER001
     C           PCNUM     CHAINSDCUX1L0             89                                       CER001
      *
     C           *IN89     IFEQ *OFF
      *
      ***  Set up extended line(s)
      *
     C                     MOVELFN00      L1DS00    P
     C                     MOVELFN01      L1DS01    P
     C                     MOVELFN02      L1DS03    P
     C                     MOVELFN03      L1DS05    P
      *
     C                     MOVELLINE1     IBFRO1    P
      *
     C                     MOVELFN04      L2DS01
     C                     MOVELFN05      L2DS03
      *
     C                     MOVE *BLANKS   IBFRO2
      *
      ***  Immatriculation filed for Branch Resident or
      ***  for OPC
      *
     C           CUTYPE    IFEQ 'BR'                       Branch
     C           CUIMCO    ORNE *BLANKS
     C           CUIMNB    ORNE *BLANKS
     C                     MOVELLINE2     IBFRO2    P
     C                     ENDIF
      *
     C                     MOVE *BLANKS   IBFRO3
     C                     MOVE *BLANKS   IBFRO4
     C                     MOVE *BLANKS   IBFRO5
      *
      ***  Setup return parameter
      *
     C                     MOVE '0'       PFAIL
      *
     C                     ELSE
     C                     MOVE '1'       PFAIL
     C                     ENDIF
      *-------------------------------------------------------------------------
      * End of processing
      *-------------------------------------------------------------------------
      *
      ***  Return to caller program
      *
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *-------------------------------------------------------------------------
      * Initial subroutine
      *-------------------------------------------------------------------------
      *
     C           *INZSR    BEGSR
      *
      ***  Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Define parameters list
      *
     C           *ENTRY    PLIST
     C                     PARM           PCNUM   6
     C                     PARM           IBFROM
     C                     PARM           PFAIL   1
      *
     C                     ENDSR
      /EJECT
      *-------------------------------------------------------------------------
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
