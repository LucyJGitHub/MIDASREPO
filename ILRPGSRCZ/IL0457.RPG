     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IBLC 2002 Access Outgoing Payments Ext. file')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  RPG/IL0457    - ACCESS IBLC OUTGOING PAYMENTS EXTENDED FILE  *
      *                                                               *
      *  Function:  This program access to IBLC Outgoing payments     *
      *             extended file and return on BLLINE parameter      *
      *             the extended informations to be printed           *
      *                                                               *
      *  (phase(s)) *all                                              *
      *                                                               *
      *  Called By: FT0510  - Outgoing Payments Aud/Req report        *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX043             Date 25Jul01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     F***OTPAYDYLIF  E           K        DISK                                                CER001
     FFTOPX1L0IF  E           K        DISK                                                   CER001
      ***  IBLC Outgoing payments      Retrieval index     Prefix OP
      *
      /EJECT
      *-------------------------------------------------------------------------
     E                    CPY@    1   1 80               COPYRIGHT  ARRAY
      /EJECT
      *-------------------------------------------------------------------------
     IILLINE      DS                            396
      * Extended informations from IBLC 95
     I                                        1 132 ILLIN1
     I                                      133 264 ILLIN2
     I                                      265 396 ILLIN3
     I            DS
      * Define fields narratives
     I              'Ordering Type'       C         FN01
     I              'Beneficiary Type'    C         FN02
     I              'IBLC Operation Code' C         FN03
     I              'Country Code'        C         FN04
     I              'Identification code' C         FN05
     I              'Identification numb- C         FN06
     I              'er'
     ILINE1       DS                            132
     I                                       10  26 L1DS01
     I                                       33  34 L1DS02
     I                                       36  56 L1DS03
     I                                       59  60 L1DS04
     I                                       62  81 L1DS05
     I                                       91  93 L1DS06
     ILINE2       DS                            132
     I                                       10  21 L2DS01
     I                                       34  35 L2DS02
     I                                       36  55 L2DS03
     I                                       60  61 L2DS04
     I                                       62  83 L2DS05
     I                                       91  99 L2DS06
      ***  Define lines structures
      *
      /EJECT
      *-------------------------------------------------------------------------
      * Main processing
      *-------------------------------------------------------------------------
      *
      ***  Access extended currency record
      *
     C********** PPREF     CHAINOTPAYDYL             89                                       CER001
     C           PPREF     CHAINFTOPX1L0             89                                       CER001
      *
     C           *IN89     IFEQ *OFF
      *
      ***  Set up extended line(s)
      *
     C                     MOVELFN01      L1DS01    P
     C                     MOVELOPORTY    L1DS02    P
     C                     MOVELFN02      L1DS03    P
     C                     MOVELOPBETY    L1DS04    P
     C                     MOVELFN03      L1DS05    P
     C                     MOVELOPOPCO    L1DS06    P
     C                     MOVELLINE1     ILLIN1
      *
     C                     MOVELFN04      L2DS01    P
     C                     MOVELOPCOCO    L2DS02    P
     C                     MOVELFN05      L2DS03    P
     C                     MOVELOPIDCO    L2DS04    P
     C                     MOVELFN06      L2DS05    P
     C                     MOVELOPIDEN    L2DS06    P
      *
     C                     MOVELLINE1     ILLIN1
     C                     MOVELLINE2     ILLIN2
     C                     ENDIF
      *-------------------------------------------------------------------------
      * End of processing
      *-------------------------------------------------------------------------
      *
      ***  Return to caller program
      *
     C                     RETRN
      /EJECT
      *-------------------------------------------------------------------------
      * Initial subroutine
      *-------------------------------------------------------------------------
      *
     C           *INZSR    BEGSR
      *
     C                     MOVEACPY@      CPY@2  80        Copyrights
      *
      ***  Define parameters list
      *
     C           *ENTRY    PLIST
     C                     PARM           PPREF  15
     C                     PARM           ILLINE
      *
     C                     ENDSR
      /EJECT
      *-------------------------------------------------------------------------
**  CPY@
(c) Finastra International Limited 2005
