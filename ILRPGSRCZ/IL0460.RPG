     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IBLC 2002 Access Cheques to be Paid Ext.file')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  RPG/IL0460    - ACCESS IBLC CHEQUES TO BE PAID EXT. FILE     *
      *                                                               *
      *  Function:  This program access to IBLC cheques to be paid    *
      *             Extended file and return on BLLINE parameter      *
      *             the extended informations to be printed           *
      *                                                               *
      *  (phase(s)) *all                                              *
      *                                                               *
      *  Called By: FT0550 - Cheques to be Paid Audit & List          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX043             Date 25Jul01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     F***CQPACYL IF  E           K        DISK                                                CER001
     FFTCPX1L0IF  E           K        DISK                                                   CER001
      *IBLC Cheques to be Paid Ext.    Retrieval index     Prefix CP.
      *
      /EJECT
      *-------------------------------------------------------------------------
     E                    CPY@    1   1 80               COPYRIGHT  ARRAY
      /EJECT
      *-------------------------------------------------------------------------
     IILLINE      DS                            396
      * Extended informations from IBLC 2002
     I                                        1 132 ILLIN1
     I                                      133 264 ILLIN2
     I                                      265 396 ILLIN3
     I            DS
      * Define fields narratives
     I              'Ordering Type . . .- C         FN01
     I              ' . :'
     I              'Beneficiary Type  .- C         FN02
     I              ' . :'
     I              'IBLC Operation Code- C         FN03
     I              ' . :'
     I              'Country Code  . . .- C         FN04
     I              ' . :'
     I              'Identification code- C         FN05
     I              ' . :'
     I              'Identification numb- C         FN06
     I              'er :'
     ILINE1       DS                            132
     I                                       10  32 L1DS01
     I                                       34  35 L1DS02
     I                                       37  59 L1DS03
     I                                       61  62 L1DS04
     I                                       64  86 L1DS05
     I                                       91  93 L1DS06
     ILINE2       DS                            132
     I                                       10  32 L2DS01
     I                                       34  35 L2DS02
     I                                       37  59 L2DS03
     I                                       61  62 L2DS04
     I                                       64  86 L2DS05
     I                                       88  96 L2DS06
      ***  Define lines structures
      *
      /EJECT
      *-------------------------------------------------------------------------
      * Main processing
      *-------------------------------------------------------------------------
      *
      ***  Access extended currency record
      *
     C********** KKEY1     CHAINCQPACYL              89                                       CER001
     C           KKEY1     CHAINFTCPX1L0             89                                       CER001
      *
     C           *IN89     IFEQ *OFF
      *
      ***  Set up extended line(s)
      *
     C                     MOVELFN01      L1DS01    P
     C                     MOVELCPORTY    L1DS02    P
     C                     MOVELFN02      L1DS03    P
     C                     MOVELCPBETY    L1DS04    P
     C                     MOVELFN03      L1DS05    P
     C                     MOVELCPOPCO    L1DS06    P
      *
     C                     MOVELFN04      L2DS01    P
     C                     MOVELCPCOCO    L2DS02    P
     C                     MOVELFN05      L2DS03    P
     C                     MOVE CPIDCO    L2DS04    P
     C                     MOVELFN06      L2DS05    P
     C                     MOVE CPIDEN    L2DS06    P
      *
     C                     MOVELLINE1     ILLIN1    P
     C                     MOVELLINE2     ILLIN2    P
      * Setup return parameter
     C                     MOVE '0'       PFAIL
      *
     C                     ELSE
     C                     MOVE '1'       PFAIL
     C                     ENDIF
      *-------------------------------------------------------------------------
      * End of processing
      *-------------------------------------------------------------------------
      *
      ***  Return to caller program
      *
     C                     RETRN
      /EJECT
      *-------------------------------------------------------------------------
      * Initial subroutine
      *-------------------------------------------------------------------------
      *
     C           *INZSR    BEGSR
      *
     C                     MOVEACPY@      CPY@2  80        Copyrights
      * Key list
     C           KKEY1     KLIST
     C                     KFLD           PPREF
     C                     KFLD           PCQSQ
      *
      ***  Define parameters list
      *
     C           *ENTRY    PLIST
     C                     PARM           PPREF  15
     C                     PARM           PCQSQ   20
     C                     PARM           ILLINE
     C                     PARM           PFAIL   1
      *
     C                     ENDSR
      /EJECT
      *-------------------------------------------------------------------------
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
