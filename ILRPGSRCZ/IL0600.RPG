     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IBLC 2002 Update data area')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  IL0600 - IBLC 2002 Update Data Area                          *
      *                                                               *
      *  Function: This program updates the data area with the        *
      *            module flag and run date.                          *
      *                                                               *
      *  Called By: CLP/ILC0600 - IBLC 2002 Data Area Update          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX043             Date 28Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     FSDBANKL1IF  E           K        DISK         KINFSR *PSSR
      ***  Bank details file                               Prefix BJ
      *
     FILICDRPDIF  E                    DISK         KINFSR *PSSR
      ***  IBLC Installation control                       Prefix IC
      *
      /EJECT
      *****************************************************************
      *
     E                    ZMNM   12  12  3               ZDATE2 SR. ARRAY
      *
     E                    CPY@    1   1 80               Copyright
      ***  Array for copyright, fields validations
      *
      /EJECT
      *****************************************************************
     ILDA       EUDSLDA                         256
      ***  Local data area for database error details
      *
     IICINTF      DS                              2
      ***  DATA STRUCTURE FOR BITS TEST
     I                                        1   1 CHR1
     I                                        2   2 CHR2
      *
     IILSTAT    EUDSILSTAT
      ***  IBLC Data area
      *
     I            DS
      ***  MIDAS RUN DATE
     I                                        1   7 BJMRDT
     I                                        1   20WWBJD
     I                                        3   5 WWBJM
     I                                        6   70WWBJY
      /EJECT
      *****************************************************************
      ***                    M A I N L I N E S                      ***
      *****************************************************************
      *
      ***  Test Modules Flag
      *
     C                     EXSR #MODL                      TEST MODULES
      *
      ***  Check Rundate and Activation Date
      *
     C                     EXSR #DATE                      RUN DATE
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      /EJECT
      *****************************************************************
      *  #MODL  : Test modules                                        *
      *  Called by :                                                  *
      *  Calls     : Mainlines                                        *
      *****************************************************************
      *
     C           #MODL     BEGSR
      *
      ***  Reset every Module to NO
      *
     C                     MOVE 'N'       ILCOB
     C                     MOVE 'N'       ILGLMO
     C                     MOVE 'N'       ILREMO
     C                     MOVE 'N'       ILDLMO
     C                     MOVE 'N'       ILLEMO
     C                     MOVE 'N'       ILSEMO
     C                     MOVE 'N'       ILFTMO
     C                     MOVE 'N'       ILFFMO
     C                     MOVE 'N'       ILPMMO
      *
      ***  Retrieved module for which IBLC is Activated
      ***  General ledger
      *
     C                     MOVE *OFF      *IN30
     C                     TESTB'0'       CHR1           30
     C           *IN30     IFEQ *ON
     C                     MOVE 'Y'       ILGLMO
     C                     ENDIF
      *
      ***  Retail
      *
     C                     TESTB'1'       CHR1           30
     C           *IN30     IFEQ *ON
     C                     MOVE 'Y'       ILREMO
     C                     ENDIF
      *
      ***  Dealing
      *
     C                     TESTB'2'       CHR1           30
     C           *IN30     IFEQ *ON
     C                     MOVE 'Y'       ILDLMO
     C                     ENDIF
      *
      ***  Customer lending
      *
     C                     TESTB'3'       CHR1           30
     C           *IN30     IFEQ *ON
     C                     MOVE 'Y'       ILLEMO
     C                     ENDIF
      *
      ***  Securities trading
      *
     C                     TESTB'4'       CHR1           30
     C           *IN30     IFEQ *ON
     C                     MOVE 'Y'       ILSEMO
     C                     ENDIF
      *
      ***  Funds transfers
      *
     C                     TESTB'5'       CHR1           30
     C           *IN30     IFEQ *ON
     C                     MOVE 'Y'       ILFTMO
     C                     ENDIF
      *
      ***  Futures and options
      *
     C                     TESTB'6'       CHR1           30
     C           *IN30     IFEQ *ON
     C                     MOVE 'Y'       ILFFMO
     C                     ENDIF
      *
      ***  Portfolio management
      *
     C                     TESTB'1'       CHR2           30
     C           *IN30     IFEQ *ON
     C                     MOVE 'Y'       ILPMMO
     C                     ENDIF
      *
      ***  Run IBLC (C.O.B. components)
      ***  If Activation Date is REACHED and Asked in ICD.
      *
     C                     TESTB'7'       CHR2           30
     C           *IN30     IFEQ *ON
     C           ICACTD    ANDLEBJRDNB                     Activ. date Reached
     C                     MOVE 'Y'       ILCOB
     C                     ELSE
      *
      ***  Ensure nothing will start
      *
     C                     MOVE 'N'       ILCOB
     C                     MOVE 'N'       ILGLMO
     C                     MOVE 'N'       ILREMO
     C                     MOVE 'N'       ILDLMO
     C                     MOVE 'N'       ILLEMO
     C                     MOVE 'N'       ILSEMO
     C                     MOVE 'N'       ILFTMO
     C                     MOVE 'N'       ILFFMO
     C                     MOVE 'N'       ILPMMO
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *  #DATE  : Routine to setup date of accounting                 *
      *  ------                                                       *
      *  Called by : Mainlines                                        *
      *  Calls     :                                                  *
      *****************************************************************
      *
     C           #DATE     BEGSR
      *
      ***  Set up DAY
      *
     C                     Z-ADDWWBJD     WWBJDP  20       DAY
     C                     Z-ADDWWBJDP    ILDADD           DAY
      *
      ***  Set up MONTH
      *
     C                     Z-ADD1         X       20       X : INDEX
     C           WWBJM     LOKUPZMNM,X                   2626 : EQUAL
     C           *IN26     IFEQ *ON
     C                     Z-ADDX         ILDAMM           MONTH
     C                     END
      *
      ***  Set up YEAR
      *
     C                     Z-ADDWWBJY     ILDAYY           YEAR
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *  *INZSR - Initial Subroutine - Started automatically          *
      *  Called by : at Pgm start                                     *
      *  Calls     :                                                  *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
      ***  Copyrigth
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Prepare LDA:
      *
     C                     MOVEL'IL0600'  DBPGM
      *
      ***  Installation control data
      *
     C                     READ SDBANKL1                 99
      *
      ***  handle database error
      *
     C           *IN99     IFEQ *ON
     C                     Z-ADD1         DBASE            ****************
     C                     MOVEL'SDBANKL1'DBFILE           * DB ERROR 001 *
     C                     MOVEL'*FIRST'  DBKEY     P      ****************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  IBLC installation control
      *
     C           1         SETLLILICDRPD
     C                     READ ILICDRPD                 99
      *
      ***  handle database error
      *
     C           *IN99     IFEQ *ON
     C                     Z-ADD2         DBASE            ****************
     C                     MOVEL'ILICDRPD'DBFILE           * DB ERROR 002 *
     C                     MOVEL'*FIRST'  DBKEY     P      ****************
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *  *PSSR  : Routine to handle Leave when databse error occurs   *
      *  ------                                                       *
      *  Called by : *INZSR                                          *
      *  Calls     :                                                  *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           DBASE     IFEQ 0
     C                     Z-ADD999       DBASE
     C                     ENDIF
      *
     C           @RUN      IFNE 'Y'
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
