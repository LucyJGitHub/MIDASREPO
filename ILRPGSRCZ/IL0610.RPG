     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IBLC 2002 Select Tables for Printing')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  IL0610 - select file(s) for printing                         *
      *                                                               *
      *  Function: This program prints IBLC details amended           *
      *            during input cycle.                                *
      *                                                               *
      *  Called By: ILC0610 - IBLC 2002 - I/C Amended details Report  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX043             Date 08Oct01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     FILFCTLPAUF  E           K        DISK
      ***  IBLC Trailer details
      *
      /EJECT
      *****************************************************************
      *
      ***  Array containing Copyright statement
      *
     E                    CPY@    1   1 80
      /EJECT
     ILDA       EUDSLDA                         256
      ***  Local data area
      *
      /EJECT
      *****************************************************************
      *                       M A I N L I N E S                       *
      *****************************************************************
      *
      ***  Read until EOF
      *
     C           *LOVAL    SETLLILFCTLA0
     C                     READ ILFCTLA0                 30
      *
     C           *IN30     DOWEQ'0'
      *
      ***   Check Reporting Indicator, (set to 'Y' duirng I/C changes)
      *
     C           FCCPTI    IFEQ 'Y'
      *
      ***   Call Reporting Program using Right parameters
      *
     C                     MOVE FCFNCT    PPFIL            File
     C                     MOVE FCRPCT    PPGMN            REP. Progr
     C                     MOVE *ZERO     PRETC            Ret. code
      *
     C                     CALL 'ILC0611'              99
     C                     PARM           PPFIL  10
     C                     PARM           PPGMN   8
     C                     PARM           PRETC   1
      *
      ***   Handle Return Error
      *
     C           PRETC     IFEQ '1'
     C           *IN99     OREQ *ON                        PGM not Found
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ****************
     C                     MOVEL'ILC0611' DBFILE    P      * DB ERROR 001 *
     C           FCFNCT    CAT  FCRPCT:1  DBKEY     P      ****************
     C                     MOVEL'IL0610'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***   Reset Reporting Flag to 'N' and updat
      *
     C                     MOVE *BLANK    FCCPTI
     C                     MOVE *ZERO     FCNAMD
     C                     MOVE *ZERO     FCNDLT
     C                     UPDATILFCTLA0
     C                     ENDIF
      *
      ***  Next Record
      *
     C                     READ ILFCTLA0                 30
     C                     ENDDO
      *
      *** End Program
      *
     C                     SETON                         LR
     C                     RETRN
      *
      /EJECT
      *****************************************************************
      *  *INZSR - Initial processing : Reset static fields            *
      *  ------                        access standing data           *
      *  Called by : First RPG Cycle   automatically                  *
      *  Calls     : *PSSR                                            *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
      ***  Copyrights
      *
     C                     MOVEACPY@      CPY@2  80
      *
     C           *NAMVAR   DEFN           LDA
     C                     UNLCKLDA
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * *PSSR   - Program exception error routine                     *
      *           Called automatically if a program error occurs,     *
      *           or directly by the program code using EXSR.         *
      *           This subroutine DUMPs the program just once.        *
      * Called by :                                                   *
      * Calls     :                                                   *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           DBASE     IFEQ 0
     C                     Z-ADD999       DBASE
     C                     ENDIF
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
** CPY@
(c) Misys International Banking Systems Ltd. 2005
