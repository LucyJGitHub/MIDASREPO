     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IBLC 2002 RTV Customer with COLC changed')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  RPG/IL0634    - Rtv Customers with Country of Loc. changed   *
      *                                                               *
      *  Function: This program is called by ILC0632 wich is always   *
      *            active. This RPG produces a list of customers      *
      *            with country of location changed, depending on     *
      *            some criteria.                                     *
      *                                                               *
      *  Called By: CLP/ILC0632  - Retrieve Loans and Customer changes*
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX043             Date 28Aug01               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     FILLOCCPDO   E           K        DISK         KINFSR *PSSR
      *
      /EJECT
      *----------------------------------------------------------------
      *
      ***  Array for compilation - copyright insertion
      *
     E                    CPY@    1   1 80
      *
      /EJECT
      *----------------------------------------------------------------
      *
      ***  Data structure for journal entry (DATA FROM POSITION 126)
      *    (125+N) depend of release version
      *
     IJRNENT      DS                           5000
     I                                        6  150JOSEQN
     I                                       17  18 JOENTT
     I                                       31  40 JOBNAM
     I                                       41  50 USRNAM
     I                                       51  56 JOBNUM
     I                                      108 1170COMCYC
     I                                      118 125 RESERV
      *
     I            DS
     I                                        1   40RCDLGT
     I                                        1   4 LGTH
      *
      ***  Working Data Structure
      *
     IPSDS       SDS
      ***  Program status data structure for WS/user ID'S
     I                                     *PROGRAM PGM
     I                                      244 253 JOB
     I                                      254 263 USER
      *
     ILDA       E DSLDA                         256
      *
      ***  Data structure for customer's file
      *
     I@SDDS     E DSSDCUSTPD
     ISDBANK    E DSSDBANKPD
     IDSFDY     E DSDSFDY
      ***  Standing Data - Bank Details
      /EJECT
      *****************************************************************
      * MAIN LINE
      *****************************************************************
      *
      ***  Move data of receive journal entry to DS externe
      *
     C                     CALL 'IL0632A'
     C                     PARM 'SDCUSTPD'PARM1  10
     C                     PARM *BLANKS   LGTH    4
     C           RCDLGT    SUBSTJRNENT:126@SDDS
      *
      ***  Execute only if parameters in RCVJRNE are correct
      *
     C           @CALL     IFEQ '1'
      *
      ***  Save customer details before update.
      *
     C           JOENTT    IFEQ 'UB'
     C                     EXSR #SAVE
      *
      ***  Move customer details to ILLOCCPD if change of location.
      ***  Location change must be from non-resident to resident or vice versa.
      *
     C                     ELSE
     C           BBCOLC    IFNE WWLOCA
     C           BBCOLC    IFEQ BJCNCD
     C           WWLOCA    OREQ BJCNCD
     C                     EXSR #FILE
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     RETRN
      ****************************************************************
      *  *INZSR - Initial Processing                                 *
      *  ------                                                      *
      *  Called by : Main lines                                      *
      *  Calls     :                                                 *
      ****************************************************************
      *
     C           *INZSR    BEGSR
      *
     C                     MOVEACPY@      CPY@2  80        Copyrights
      *
     C           *ENTRY    PLIST
     C                     PARM           JRNENT
     C                     PARM           @CALL   1
      *
      ***  Prepare LDA
      *
     C           *NAMVAR   DEFN           LDA
     C                     MOVELPGM       DBPGM
      *
      ***  Reset indicators
      *
     C                     MOVE *OFF      *IN
      *
      ***  Retrieve Bank details
      *
     C                     CALL 'AOBANKR0'             99
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST'  POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Database error in case of Problem
      *
     C           PRTCD     IFNE *BLANKS
     C           *IN99     OREQ *ON
      *
     C           *LOCK     IN   LDA
     C                     Z-ADD33        DBASE            ****************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 033 *
     C                     MOVEL'*FIRST'  DBKEY            ****************
     C                     MOVELPGM       DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Check system data format (DDMMYY or MMDDYY):
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98            ON => MMDDYY
     C                     ENDIF
      *
     C                     ENDSR
      *
      /EJECT
      ****************************************************************
      *  #SAVE - Save subroutine                                     *
      *  ------                                                      *
      *  Called by : Main lines                                      *
      *  Calls     :                                                 *
      ****************************************************************
      *
     C           #SAVE     BEGSR
      *
      ***  Save customer, shortname and country of location
      *
     C                     MOVE BBCUST    WWCUST  6
     C                     MOVE BBCSSN    WWCSSN 10
     C                     MOVE BBCOLC    WWLOCA  2
      *
     C                     ENDSR
      *
      /EJECT
      ****************************************************************
      *  #FILE - Move customer details to ILLOCCPD                   *
      *  ------                                                      *
      *  Called by : Main lines                                      *
      *  Calls     :                                                 *
      ****************************************************************
      *
     C           #FILE     BEGSR
      *
      ***  Store in file ILLOCCPD
      *
     C                     MOVE BBCUST    LCCUST
     C                     MOVE BBCSSN    LCCSSN
     C                     MOVE WWLOCA    LCCLOB
     C                     MOVE BBCOLC    LCCLOA
     C                     MOVE BBCNA1    LCCNA1
     C                     MOVE BBCNA2    LCCNA2
     C                     MOVE BBCNA3    LCCNA3
     C                     MOVE BBCNA4    LCCNA4
     C                     MOVE BBCRTN    LCCRTN
      *
     C                     WRITEILLOCCD0
      *
     C                     ENDSR
      *
      /EJECT
      ****************************************************************
      *  *PSSR  - error Subroutine                                   *
      *  -------                                                     *
      *  Called by : on FILE error                                   *
      *              *INZSR                                          *
      ****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
     C           DBASE     IFEQ 0
     C                     Z-ADD999       DBASE
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
      *
     C                     DUMP
      *
     C                     RETRN
      *
     C                     ENDSR                           ** *PSSR **
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2005
