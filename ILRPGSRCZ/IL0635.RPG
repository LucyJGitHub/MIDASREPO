     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IBLC 2002 Print Customer with COLC changed')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  RPG/IL0635    - Print Customers with Country of Loc. changed *
      *                                                               *
      *  Function: This program is called by ILC0632 wich is always   *
      *            active. This RPG produces a list of loans with     *
      *            maturity date changed, depending on some criteria. *
      *                                                               *
      *  Called By: CLP/ILC0632  - Retrieve Loans with Maturity Date  *
      *                            Changed                            *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX043             Date 28Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     FILLOCCPDIF  E           K        DISK         KINFSR *PSSR
     FIL0635P1O   E             66     PRINTER      KINFDS SPOOL1
     FIL0635AUO   E                    PRINTER      KINFDS SPOOLU
      *
      /EJECT
      *----------------------------------------------------------------
      *
      ***  Array for compilation - copyright insertion
      *
     E                    CPY@    1   1 80
      *
      /EJECT
      *----------------------------------------------------------------
      *
      ***  Working Data Structure
      *
     IPSDS       SDS
      ***  Program status data structure for WS/user ID'S
     I                                     *PROGRAM PGM
     I                                      244 253 JOB
     I                                      254 263 USER
      *
     ILDA       E DSLDA                         256
      ***  Local data area
      *
     ISPOOLU      DS
      *
      **  File Information Data Structure - AU report
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
     ISPOOL1      DS
      *
      **  File Information Data Structure - P1 report
     I                                       83  92 SFIL1
     I                                    B 123 1240SFNUM1
      *
     ISDBANK    E DSSDBANKPD
      ***  Standing Data - Bank Details
     IDSFDY     E DSDSFDY
      *
      /EJECT
      *****************************************************************
      * MAIN LINE
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           @RSEQ   5
     C                     PARM           @RLEV   1
     C                     PARM           @RENT   3
      *
     C           *LOVAL    SETLLILLOCCPD
     C                     READ ILLOCCD0                 60
      *
     C           *IN60     IFEQ *ON
     C                     WRITEHEADAU
     C                     WRITENODTL1
     C                     ENDIF
      *
     C           *IN60     DOWEQ*OFF
     C                     EXSR #PRINT
      *
     C                     READ ILLOCCD0                 60
     C                     ENDDO
      *
     C                     WRITEENDP1
      *
     C                     MOVE '1'       *INLR
     C                     RETRN
      *
      /EJECT
      ****************************************************************
      *  *INZSR - Initial Processing                                 *
      *  ------                                                      *
      *  Called by : Main lines                                      *
      *  Calls     :                                                 *
      ****************************************************************
      *
     C           *INZSR    BEGSR
      *
     C                     MOVEACPY@      CPY@2  80        Copyrights
      *
      ***  Prepare LDA
      *
     C           *NAMVAR   DEFN           LDA
     C                     MOVELPGM       DBPGM
      *
      ***  Reset indicators
      *
     C                     MOVE *OFF      *IN
      *
      ***  Retrieve Bank details
      *
     C                     CALL 'AOBANKR0'             99
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST'  POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Database error in case of Problem
      *
     C           PRTCD     IFNE *BLANKS
     C           *IN99     OREQ *ON
      *
     C           *LOCK     IN   LDA
     C                     Z-ADD33        DBASE            ****************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 033 *
     C                     MOVEL'*FIRST'  DBKEY            ****************
     C                     MOVELPGM       DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Check system data format (DDMMYY or MMDDYY):
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98            ON => MMDDYY
     C                     ENDIF
      *
      ***  Record Audit file IL0635AU on RCF
      *
     C                     Z-ADDSFNUMU    ZSNUM
     C                     MOVELSFILEU    SFILE
      *
     C                     EXSR SRZSFL
      *
      ***  Record Printer file IL0635P1 on RCF
      *
     C                     Z-ADDSFNUM1    ZSNUM
     C                     MOVELSFIL1     SFILE
      *
     C                     EXSR SRZSFL
     C                     WRITEHEADP1
      *
     C                     ENDSR
      *
      /EJECT
      ****************************************************************
      *  #PRINT - Print subroutine                                   *
      *  ------                                                      *
      *  Called by : Main lines                                      *
      *  Calls     :                                                 *
      ****************************************************************
      *
     C           #PRINT    BEGSR
      *
      ***  Customer Number, Shortname, Location Before and After
      *
     C                     MOVE LCCUST    RCUST
     C                     MOVE LCCSSN    RCSSN
     C                     MOVE LCCLOB    RLBEF
     C                     MOVE LCCLOA    RLAFT
     C                     MOVE LCCRTN    RTOWN
      *
      ***  Customer full name & Address
      *
     C                     MOVE LCCNA1    RADR1
     C                     MOVE LCCNA2    RADR2
     C                     MOVE LCCNA3    RADR3
     C                     MOVE LCCNA4    RADR4
      *
      ***  Check Overflow
      *
     C           *IN66     IFEQ *ON
     C                     WRITEHEADP1
     C                     MOVE *OFF      *IN66
     C                     ENDIF
      *
     C                     WRITEDETAP1
      *
     C                     ENDSR
      *
      /EJECT
      ****************************************************************
      *  *PSSR  - error Subroutine                                   *
      *  -------                                                     *
      *  Called by : on FILE error                                   *
      *              *INZSR                                          *
      ****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
     C           DBASE     IFEQ 0
     C                     Z-ADD999       DBASE
     C                     ENDIF
      *
     C                     WRITEHEADAU
     C                     WRITEDBERROR
      *
     C                     SETON                     U7U8LR
      *
     C                     DUMP
      *
     C                     RETRN
      *
     C                     ENDSR                           ** *PSSR **
      *
      /EJECT
      *****************************************************************
      * SRZSFL - Subroutine to call ZSFILE
      *****************************************************************
      *
     C           SRZSFL    BEGSR
      *
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   @RSEQ   5
     C                     PARM           @RENT   3
     C                     PARM           SFILE  10
     C                     PARM           ZSNUM   60
     C                     PARM           ZSERR   1
      *
      ***  If an error occurred during ZSFILE processing,
      ***  return to the calling program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2005
