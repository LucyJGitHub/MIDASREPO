     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IBLC 2002 Non working days Items Addition')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  IL0653 - AWND Items Addition                                 *
      *                                                               *
      *  Function: This program is called by ILC0653 which is call    *
      *            at end of month followed by a non-working days.    *
      *            This RPG program fill the accounting date at next  *
      *             working date.                                     *
      *                                                               *
      *  Called By: CLP/ILC0653  - AWND Items Addition                *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX043             Date 28Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     FILDINWPDIP  E           K        DISK         KINFSR *PSSR
      ***  ITEMS at EOD follwed by non-working days
     F            ILITEMD0                          KRENAMEREADITEM
      *
     FILITEMPDO   E                    DISK         KINFSR *PSSR
      ***  IBLC 2002 items                                 Prefix IT
      *
      /EJECT
      *****************************************************************
      *
      ***  Standard Subroutine Arrays
      *
     E                    ZMNM   12  12  3               ZDATE2 SR. ARRAY
      *
      ***  Array containing Copyright statement
      *
     E                    CPY@    1   1 80
      *
      /EJECT
      *****************************************************************
      *
     ILDA       E DSLDA                         256
      ***  Data structure for data-base processing
      *
     IPSDS       SDS
      ***  Program status data structure for WS/user ID'S
     I                                     *PROGRAM PGM
      *
     IMRDTDS      DS
      ***  Data structure to use BJMRDT
     I                                        1   2 WDLUP
     I                                        3   5 WMLUP
     I                                        6   7 WYLUP
      *
     ISDBANK    E DSSDBANKPD
      ***  Standing Data - Bank Details
     IDSFDY     E DSDSFDY
      **  First DS for access programs, short DS
      *
      *****************************************************************
      * MAIN LINE
      *****************************************************************
      *
     C           WDLUP     CAT  WTEMP2:0  WTEMP6  6        DDMMYY
     C           WTEMP6    CAT  WYLUP:0   WTEMP6
     C                     MOVE WTEMP6    ITACTD
      *
     C                     WRITEILITEMD0
      *
     CLR                   RETRN
      /EJECT
      ****************************************************************
      *  *INZSR - initial subroutine                                 *
      *  -------                                                     *
      *  Called by :                                                 *
      *                                                              *
      ****************************************************************
      *
     C           *INZSR    BEGSR
      *
      *** Copyright
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Prepare LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
      ***  Retrieve Bank details
      *
     C                     CALL 'AOBANKR0'             99
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST'  POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Database error in case of Problem
      *
     C           PRTCD     IFNE *BLANKS
     C           *IN99     OREQ *ON
      *
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ****************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 001 *
     C                     MOVEL'*FIRST'  DBKEY            ****************
     C                     MOVELPGM       DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE BJMRDT    MRDTDS
     C                     Z-ADD1         #J      20       RETRIEVE DATE
     C           WMLUP     LOKUPZMNM,#J                  62
     C                     MOVE '00'      WTEMP2  2
     C                     MOVE #J        WTEMP2
      *
     C                     ENDSR                           ** *INZSR**
      /EJECT
      ****************************************************************
      *  *PSSR  - error Subroutine                                   *
      *  -------                                                     *
      *  Called by : on FILE error                                   *
      *                                                              *
      ****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           DBASE     IFEQ 0
     C                     Z-ADD999       DBASE
     C                     ENDIF
     C                     MOVELPGM       DBPGM
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(c) Finastra International Limited 2005
