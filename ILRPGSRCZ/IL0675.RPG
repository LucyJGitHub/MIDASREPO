     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IBLC 2002 transaction item sequence')            *
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  IL0675 - IBLC 2002 - Items sequence update                   *
      *                                                               *
      *  Function: This program increment sequence number by 1,       *
      *            ensure amounts are positive,                       *
      *            country code entered are ISO Ctry code.            *
      *                                                               *
      *  Called By: CLP/ILC0675 - IBLC 2002 Items sequence update     *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER045             Date 20Jul09               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CER001             Date 25Apr05               *
      *                 ULX043             Date 28Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank (Recompile)  *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     FILITEML1UF  E           K        DISK
      ***  IBLC 2002 Items to report
      *
     FSDCTRYPDIF  E           K        DISK
      ***  SD - Country codes
      *
     E/EJECT
      *****************************************************************
      ***  Compile Time Arrays
      *****************************************************************
      *
      ***  Array containing Copyright statement
      *
     E                    CPY@    1   1 80
      *
      *****************************************************************
      ***  Runtime Arrays
      *****************************************************************
      *
      ***  Working Arrays
      *
     E                    CTY       400  2   ISO     2
      *
      /EJECT
      *****************************************************************
      *                       M A I N L I N E S                       *
      *****************************************************************
      *
      ***  Copyrights
      *
     C                     MOVEACPY@      CPY@2  80
      *
      *** Fill a array with the Iso code
      *
     C                     READ SDCTRYPD                 99
     C                     Z-ADD1         X       30
      *
     C           *IN99     DOWEQ'0'
     C                     MOVE A4CNCD    CTY,X
     C                     MOVE A4ISOC    ISO,X
     C                     ADD  1         X
     C                     READ SDCTRYPD                 99
     C                     ENDDO
      *
      ***  First read
      *
     C                     Z-ADD0         WITSQ   60       Sequence is unique
      *
     C           *LOVAL    SETLLILITEML1
     C                     READ ILITEML1                 80
      *
      ***  Process until LR indicator = *off
      *
     C           *IN80     DOWEQ*OFF
      *
      *** Sequence increase
      *
     C           WITSQ     ADD  1         WITSQ
     C                     Z-ADDWITSQ     ITITSQ
      *
      *** IBLC amount must be positive
      *
     C           ITAMNT    IFLT 0
     C           ITAMNT    MULT -1        ITAMNT
      *
     C           ITOAMN    IFLT 0
     C                     Z-SUBITOAMN    ITOAMN
     C                     ENDIF
      *
     C           ITAMNB    IFLT 0
     C                     Z-SUBITAMNB    ITAMNB
     C                     ENDIF
      *
     C           ITDRCR    IFEQ 'D'
     C                     MOVE 'C'       ITDRCR
     C                     ELSE
     C                     MOVE 'D'       ITDRCR
     C                     ENDIF
      *
     C                     ENDIF
      *
      *** Replace the country code by the Iso code
      *
     C           ITCOCO    IFNE *BLANKS
     C                     Z-ADD1         X       30
     C           ITCOCO    LOKUPCTY,X                    99 = Found
     C   99                MOVE ISO,X     ITCOCO
     C                     ENDIF
      *
      ***  Update item
      *
     C                     UPDATILITEMD0
      *
      ***  Read next record
      *
     C                     READ ILITEML1                 80
      *
     C                     ENDDO
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      *               E N D    O F    M A I N L I N E S               *
      *****************************************************************
      /EJECT
**  CPY@
(c) Finastra International Limited 2005
