      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IR Voce/Sottovoce Codes Maintenance')            *
      *****************************************************************
      *                                                               *
      *  Midas European Reporting                                     *
      *                                                               *
      *  IR0018DF - Voce/Sottovoce Codes Maintenance                  *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 22Dec20               *
      *  Prev Amend No. UIT021             Date 27JAN97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  UIT021 : BOI Monthly Reporting                               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *          F U N C T I O N   O F   I N D I C A T O R S          *
      *          -------------------------------------------          *
      *                                                               *
      *  *IN25  -  SFLEND on message subfile                          *
      *                                                               *
      *  *IN31  -  Selection field in error                           *
      *  *IN32  -  Voce code entered in error                         *
      *  *IN33  -  Sottovoce code entered in error                    *
      *  *IN34  -  Narrative in error                                 *
      *  *IN35  -  Frequency in error                                 *
      *                                                               *
      *  *IN79  -  Protect key fields in amend/enquire modes          *
      *  *IN80  -  SFLCLR                                             *
      *  *IN81  -  SFLDSP                                             *
      *  *IN82  -  SFLEND                                             *
      *  *IN85  -  Protect other fields in enquire mode               *
      *  *IN87  -  Protect select field in enquire mode               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HELP
     A                                      HLPDOC('H020@000' 'ERITINS' 'ABSENG-
     A                                      /ITL')
      *
      *=========================================================================
     A          R #SFLRCD
     A                                      TEXT('(C) Midas-Kapiti Internationa-
     A                                      l Ltd. 1997')
     A                                      SFL
     A  84                                  SFLNXTCHG
     A            #0DBRC        42   H      TEXT('Existing record fields')
      *
     A            #0SEL          1   B  6  2TEXT('Selection')
     A  89                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N89N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
      *
     A            #0VOCE         5   B  6  8TEXT('BOI Voce Code')
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N79N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
      *
     A            #0SOTV         2   B  6 16TEXT('BOI Sottovoce Code')
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N79N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
      *
     A            #0NARR        30   B  6 33TEXT('Narrative')
     A                                      CHECK(LC)
     A  85                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N85N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
      *
     A            #0FREQ         1   B  6 70TEXT('Frequency')
     A  85                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N85N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
      *
      *=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SFLPAG(15) SFLSIZ(16)
     A                                      BLINK OVERLAY
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      *
     A                                      CA03
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05
     A  44                                  CF09
      *
     A          H                           HLPARA(06 002 20 002)
     A                                      HLPDOC('H020@001' 'ERITINS' 'ABSENG-
     A                                      /ITL')
     A          H                           HLPARA(06 008 20 012)
     A                                      HLPDOC('H020@002' 'ERITINS' 'ABSENG-
     A                                      /ITL')
     A          H                           HLPARA(06 016 20 017)
     A                                      HLPDOC('H020@003' 'ERITINS' 'ABSENG-
     A                                      /ITL')
     A          H                           HLPARA(06 033 20 062)
     A                                      HLPDOC('H020@004' 'ERITINS' 'ABSENG-
     A                                      /ITL')
     A          H                           HLPARA(06 070 20 070)
     A                                      HLPDOC('H020@005' 'ERITINS' 'ABSENG-
     A                                      /ITL')
     A          H                           HLPARA(22 001 22 080)
     A                                      HLPDOC('H020@006' 'ERITINS' 'ABSENG-
     A                                      /ITL')
      *
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##PGM         10      1  2TEXT('*PROGRAM')
     A            ##JOB         10      1 52TEXT('*JOB')
     A            ##AANA         7      1 63TEXT('midas rundate')
     A                                  1 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 23'BOI Voce-Sottovoce Codes +
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A  81                              5  7'Voce - Sottovoce codes'
     A  81                              5 33'Narrative'
     A  81                              5 66'Frequency'
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY
     A            #0CMD         60     22  2
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(06)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
