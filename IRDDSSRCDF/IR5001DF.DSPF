      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IR Internal Tables Report - Prompt')             *
      *****************************************************************
      *                                                               *
      *  Midas - Standing data module                                 *
      *                                                               *
      *  DSPF/IR5001DF - Internal Tables Report - Prompt              *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 10Feb21               *
      *  Prev Amend No. UIT022  *Create    Date 17jan97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   LUC139 - UCI Italian returns upgrade to FM2.1               *
      *   UIT022 - Telegramma decadale                                *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
     A                                      HELP
     A                                      HLPDOC(H030@000 ERITINS ABSENG/ITL)
      *
     A          R IR5001F0                  TEXT('(C) Midas-Kapiti +
     A                                      International Ltd. 1997')
      *
     A                                      PUTOVR
      *
      ***  Help Tag for 'Table code'.
      *
     A          H                           HLPARA(06 02 06 31)
     A                                      HLPDOC(H030@001 ERITINS ABSENG/ITL)
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'IR5001'
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 27'Internal Tables Report - Prompt'
     A                                      DSPATR(UL HI)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  6  2'Table code . . . . . :'
     A            STABL          3A  B  6 26DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  6 40'Enter 999 for all tables.'
      *
     A                                 23  3'F3=Exit'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGM                       SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            PGM                       SFLPGMQ
