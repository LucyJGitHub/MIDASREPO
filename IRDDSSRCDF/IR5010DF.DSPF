      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IR Select Internal table - Display')             *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module - Italy.                   *
      *                                                               *
      *  DSPF/IR5010DF - Select Internal table - Display              *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 10Feb21               *
      *  Prev Amend No. MMI001             Date 31OCT97               *
      *                 UIT022  *Create    Date 14Jan97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  MMI001 - Upgrade MIDAS Italy to DBA2                         *
      *  UIT022 - Telegramma Decadale                                 *
      *                                                               *
      *****************************************************************
      *   U S E   O F   I N D I C A T O R S                           *
      *                                                               *
      *   *IN21            Subfile - SFLINZ                           *
      *   *IN22            Subfile - SFLDSPCTL                        *
      *   *IN23            Subfile - SFLDSP                           *
      *   *IN24            Subfile - SFLCLR                           *
      *   *IN25            Subfile - Rollup have been pressed         *
      *   *IN26            Subfile - SFLNXTCHG                        *
      *   *IN27            Subfile - SFLEND                           *
      *   *IN29            Message subfile - SFLEND                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      INDARA
     A                                      HELP
     A                                      HLPDOC(H027@000 ERITINS ABSENG/ITL)
     A                                      CA03
     A                                      CA05
     A                                      CF12
     A N52                                  CF09
      *
      *-----------------------------------*
      ***  Top of screen record Format  ***
      *-----------------------------------*
      *
     A          R $TOPTRA
     A                                      TEXT('HEADER SCREEN')
     A            $USRI         10A  O  1  2TEXT('User Id.')
     A            $JOBN         10A  O  1 13TEXT('Program name')
     A                                  1 67'DATE'
     A            $RUNA          7A  O  1 72TEXT('Run Date')
     A            $WRKI         10A  O  2 02TEXT('Workstation')
     A                                  2 67'TIME'
     A                                  2 72TIME
     A                                  2 18'Statutory Returns - Internal tabl-
     A                                      e maintenance'
     A                                      DSPATR(HI UL)
      *
      *-------------------------------------*
      ***  Subfile details record Format  ***
      *-------------------------------------*
      *
     A          R $SFLRCD1                  SFL
     A                                      TEXT('SFL rec')
     A  26                                  SFLNXTCHG
     A            WSVIN         20A  H
     A            SELE           1A  B  6  2DSPATR(UL)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A  30                                  DSPATR(ND)
     A            $$INTC         3   O  6  4
     A  30                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            $$PNAR        30A  O  6  8
     A  30                                  DSPATR(ND)
     A            WRECN          4S 0H
      *
      *-------------------------------------*
      ***  Subfile control record Format  ***
      *-------------------------------------*
      *
     A          R $SFLCTL1                  SFLCTL($SFLRCD1)
     A N27                                  ROLLUP(25 'Next page.')
     A                                      KEEP
     A                                      CSRLOC($NLIG      $NCOL)
     A                                      OVERLAY
     A                                      PUTOVR
     A  23                                  SFLDSP
     A  22                                  SFLDSPCTL
     A  21                                  SFLINZ
     A  24                                  SFLCLR
     A  27                                  SFLEND
     A                                      SFLSIZ(0032)
     A                                      SFLPAG(0030)
     A                                      SFLLIN(0005)
      *
      ***  Help tag for 'Key Code Header search field'
      *
     A          H                           HLPARA(04 04 04 06)
     A                                      HLPDOC(H027@001 ERITINS ABSENG/ITL)
      *
      ***  Help tag for 'Key Description Header search field'
      *
     A          H                           HLPARA(04 08 04 37)
     A                                      HLPDOC(H027@002 ERITINS ABSENG/ITL)
      *
      ***  Help tag for 'Action Code'
      *
     A          H                           HLPARA(06 02 20 02)
     A                                      HLPDOC(H027@003 ERITINS ABSENG/ITL)
     A          H                           HLPARA(06 43 20 43)
     A                                      HLPDOC(H027@003 ERITINS ABSENG/ITL)
      *
      ***  Help tag for 'Command Keys'
      *
     A          H                           HLPARA(23 02 23 80)
     A                                      HLPDOC(H027@004 ERITINS ABSENG/ITL)
      *
     A            WRECN          4S 0H      SFLRCDNBR
     A            $NLIG          3S 0H
     A            $NCOL          3S 0H
     A            $CINTC         3   B  4  4DSPATR(UL)
     A            $CPNAR        30   B  4  8DSPATR(UL)
      *
      *--------------------------------------*
      ***  Bottom of screen record Format  ***
      *--------------------------------------*
      *
     A          R $CMDTXT1
     A                                      TEXT('Command Line')
     A                                      OVERLAY
     A                                      PUTOVR
      *
      ***  Actions authorized  : 52 = *ON : Enquiry mode
      *
     A                                 22  2'H=Table header'
     A N52                             22 19'A=Amend'
     A N52                             22 29'E=Enquire'
     A N52                             22 41'D=Delete'
     A  52                             22 19'E=Enquire'
      *
      ***  Function keys authorized
      *
     A                                 23  2'F3=Exit   F5=Refresh'
     A N52                             23 25'F9=Add   F12=Previous'
     A  52                             23 25'F12=Previous'
      *
      *---------------------------------------------*
      ***  Error messages Subfile records Format  ***
      *---------------------------------------------*
      *
     A          R $MSGRCD                   SFL
     A                                      SFLMSGRCD(24)
     A            $MSGKY                    SFLMSGKEY
     A            $PGMQ                     SFLPGMQ
      *
     A          R $MSGCTL                   SFLCTL($MSGRCD)
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A N29                                  SFLEND
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A            $PGMQ                     SFLPGMQ
