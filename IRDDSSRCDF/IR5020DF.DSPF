      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IR Update Internal Table Header')                *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module - Italy.                   *
      *                                                               *
      *  DSPF/IR5020DF - Update Internal Table Header                 *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 10Feb21               *
      *  Prev Amend No. UIT022  *Create    Date 22Jan97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  UIT022 - Telegramma Decadale                                 *
      *                                                               *
      *****************************************************************
      *   Function of indicators                                      *
      *   ----------------------                                      *
      *   *IN30 : Table code - Error indicator                        *
      *   *IN31 : Table name - Error indicator                        *
      *   *IN32 : *ON when Enquire   (protect fields)                 *
      *   *IN33 : *ON when Insert    (title and table code)           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CA03
     A N32                                  CF05
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
     A                                      HELP
     A                                      HLPDOC(H028@000 ERITINS ABSENG/ITL)
      *
     A          R IR5020F0                  TEXT('(C) Midas-Kapiti +
     A                                      International Ltd. 1997')
      *
     A                                      PUTOVR
      *
      ***  Help Tag for 'Table code'.
      *
     A          H                           HLPARA(04 06 04 21)
     A  33                                  HLPDOC(H028@001 ERITINS ABSENG/ITL)
     A          H                           HLPARA(02 15 02 58)
     A N33                                  HLPDOC(H028@001 ERITINS ABSENG/ITL)
      *
      ***  Help Tag for 'Table name'.
      *
     A          H                           HLPARA(05 06 05 48)
     A                                      HLPDOC(H028@002 ERITINS ABSENG/ITL)
      *
      ***  Help Tag for 'Header for key code'
      *
     A          H                           HLPARA(09 19 13 38)
     A                                      HLPDOC(H028@003 ERITINS ABSENG/ITL)
      *
      ***  Help Tag for 'Header for key description'
      *
     A          H                           HLPARA(09 39 13 78)
     A                                      HLPDOC(H028@004 ERITINS ABSENG/ITL)
      *
      ***  Help Tag for 'Table comments'
      *
     A          H                           HLPARA(16 06 17 78)
     A                                      HLPDOC(H028@005 ERITINS ABSENG/ITL)
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'IR5020'
      *
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
      ***  Title in Amend Mode
      *
     A N33                              2 20'Statutory Returns - Header of tabl-
     A                                      e'
     A                                      DSPATR(HI UL)
     A N33        $INTC1         3A  O  2 56DSPATR(HI UL)
      *
      ***  Title in Insert mode
      *
     A  33                              2 20'Statutory Returns - Insert a new t-
     A                                      able'
     A                                      DSPATR(HI UL)
      *
      ***  in Insert mode, table code must be entered.
      *
     A  33                              4  6'Table code :'
     A  33        $INTC2         3A  B  4 19DSPATR(HI)
     A  33N30                               DSPATR(UL)
     A  33 30                               DSPATR(RI PC BL)
     A  33                                  OVRDTA
     A N33                                  DSPATR(ND)
      *
     A                                  5  6'Table Name :'
     A            $$TITL        30   B  5 19DSPATR(HI)
     A N32N31                               DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  7 19'Key'
     A                                      DSPATR(HI)
     A                                  7 39'Details'
     A                                      DSPATR(HI)
      *
     A                                  8 19'Description'
     A                                      DSPATR(HI)
     A                                  8 39'Description'
     A                                      DSPATR(HI)
      *
      ***  5 narratives as title of Key code (18A)
      ***  and 5 narratives as title of Key code description (40A)
      *
     A                                 09  6'Header . . :'
     A            $CLHK1        18A  B 09 19DSPATR(HI)
     A N32                                  DSPATR(UL)
     A                                      CHECK(LC)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
     A            $CLHD1        40A  B 09 39DSPATR(HI)
     A N32                                  DSPATR(UL)
     A                                      CHECK(LC)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A            $CLHK2        18A  B 10 19DSPATR(HI)
     A N32                                  DSPATR(UL)
     A                                      CHECK(LC)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
     A            $CLHD2        40A  B 10 39DSPATR(HI)
     A N32                                  DSPATR(UL)
     A                                      CHECK(LC)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A            $CLHK3        18A  B 11 19DSPATR(HI)
     A N32                                  DSPATR(UL)
     A                                      CHECK(LC)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
     A            $CLHD3        40A  B 11 39DSPATR(HI)
     A N32                                  DSPATR(UL)
     A                                      CHECK(LC)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A            $CLHK4        18A  B 12 19DSPATR(HI)
     A N32                                  DSPATR(UL)
     A                                      CHECK(LC)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
     A            $CLHD4        40A  B 12 39DSPATR(HI)
     A N32                                  DSPATR(UL)
     A                                      CHECK(LC)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A            $CLHK5        18A  B 13 19DSPATR(HI)
     A N32                                  DSPATR(UL)
     A                                      CHECK(LC)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
     A            $CLHD5        40A  B 13 39DSPATR(HI)
     A N32                                  DSPATR(UL)
     A                                      CHECK(LC)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 16  6'Comments . :'
     A            $COMM1        60A  B 16 19DSPATR(HI)
     A N32                                  DSPATR(UL)
     A                                      CHECK(LC)
     A  32                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A            $COMM2        60A  B 17 19DSPATR(HI)
     A N32                                  DSPATR(UL)
     A                                      CHECK(LC)
     A  32                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      OVRDTA
      *
      ***  Bottom of screen
      *
     A  32                             23  3'F3=End   F12=previous'
     A N32                             23  3'F3=End   F5=Refresh    F12=previou-
     A                                      s'
      *
      *-------------------------------------------------*
      ***  Subfile of error messages records formats  ***
      *-------------------------------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGM                       SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            PGM                       SFLPGMQ
