      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IR Update Internal table - display')             *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module - Italy.                   *
      *                                                               *
      *  DSPF/IR5030DF - Update Internal table - display              *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 10Feb21               *
      *  Prev Amend No. MMI001             Date 31OCT97               *
      *                 UIT022  *Create    Date 14Jan97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  MMI001 - Upgrade MIDAS ITALY to DBA2                         *
      *  UIT022 - Telegramma Decadale                                 *
      *                                                               *
      *****************************************************************
      *   U S E   O F   I N D I C A T O R S                           *
      *                                                               *
      *   *IN21            Subfile - SFLINZ                           *
      *   *IN22            Subfile - SFLDSPCTL                        *
      *   *IN23            Subfile - SFLDSP                           *
      *   *IN24            Subfile - SFLCLR                           *
      *   *IN25            Subfile - Rollup have been pressed         *
      *   *IN26            Subfile - SFLNXTCHG                        *
      *   *IN27            Subfile - SFLEND                           *
      *   *IN29            Message subfile - SFLEND                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      HELP
     A                                      HLPDOC(H029@000 ERITINS ABSENG/ITL)
     A                                      INDARA
     A                                      CF03
     A N31N34                               CF04
     A N31N34                               CA05
     A  34                                  CF10
     A                                      CF12
      *
      *-----------------------------------*
      ***  Top of screen record Format  ***
      *-----------------------------------*
      *
     A          R $TOPTRA
     A                                      TEXT('Top of screen')
     A                                      CHANGE(20 'Change indicator')
     A            $USRI         10A  O  1  2TEXT('User Id.')
     A            $JOBN         10A  O  1 13TEXT('Program name')
     A                                  1 67'DATE'
     A            $RUNA          7A  O  1 72TEXT('Date traitement')
      *
     A            $WRKI         10A  O  2  2TEXT('Identifiant Ecran')
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A                                  2 15'Statutory Returns - '
     A                                      DSPATR(HI UL)
     A            $HINTC         3A  O  2 36DSPATR(HI UL)
      *
      ***  Rem (25A instead of 30A)
      *
     A            $$TITL        25A  O  2 40DSPATR(HI UL)
      *
      *-------------------------------------*
      ***  Subfile details record Format  ***
      *-------------------------------------*
      *
     A          R $SFLRCD1                  SFL
     A                                      CHANGE(10 'Change indicator')
     A  26                                  SFLNXTCHG
     A            WSVIN         20A  H
     A            WRECN          4S 0H
      *
      ***  Key code field
      *
     A            $$INTK        18A  B  9 20
     A N34
     AA 32N31N33                            DSPATR(HI)
     A N34
     AAN32N31                               DSPATR(UL)
     A  32
     AO 34
     AO 31                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
      *
      ***  Key code description
      *
     A            $$PNAR        40A  B  9 39
     A N31N34                               DSPATR(UL)
     A  32N34                               DSPATR(PC)
     A  34
     AO 31                                  DSPATR(PR)
      *
      *-------------------------------------*
      ***  Subfile control record Format  ***
      *-------------------------------------*
      *
     A          R $SFLCTL1                  SFLCTL($SFLRCD1)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A N27                                  ROLLUP(25 'Next page.')
     A                                      CHANGE(20 'Change indicator')
     A                                      KEEP
     A                                      CSRLOC($NLIG      $NCOL)
     A                                      OVERLAY
     A                                      PUTOVR
     A  23                                  SFLDSP
     A  22                                  SFLDSPCTL
     A  21                                  SFLINZ
     A  24                                  SFLCLR
     A  27                                  SFLEND
      *
      ***  Help tag for 'Key code Header lines'
      *
     A          H                           HLPARA(04 20 08 37)
     A                                      HLPDOC(H029@001 ERITINS ABSENG/ITL)
      *
      ***  Help tag for 'Key code description Header lines'
      *
     A          H                           HLPARA(04 39 08 78)
     A                                      HLPDOC(H029@002 ERITINS ABSENG/ITL)
      *
      ***  Help tag for 'Key code details'
      *
     A          H                           HLPARA(09 18 18 37)
     A                                      HLPDOC(H029@003 ERITINS ABSENG/ITL)
     A          H                           HLPARA(09 39 18 78)
     A                                      HLPDOC(H029@004 ERITINS ABSENG/ITL)
      *
      ***  Help tag for 'Key code description Header lines'
      *
     A          H                           HLPARA(20 01 21 71)
     A                                      HLPDOC(H029@005 ERITINS ABSENG/ITL)
      *
      ***  Help tag for 'Positioning field'
      *
     A          H                           HLPARA(07 01 08 17)
     A N34                                  HLPDOC(H029@006 ERITINS ABSENG/ITL)
     A            WRECN          4S 0H      SFLRCDNBR
     A            $NLIG          3S 0H
     A            $NCOL          3S 0H
     A            $CLHK1        18A  O  4 20DSPATR(HI)
     A            $CLHD1        40A  O  4 39DSPATR(HI)
     A            $CLHK2        18A  O  5 20DSPATR(HI)
     A            $CLHD2        40A  O  5 39DSPATR(HI)
     A            $CLHK3        18A  O  6 20DSPATR(HI)
     A            $CLHD3        40A  O  6 39DSPATR(HI)
      *
      ***  Key positioning not active when delete mode is active
      *
     A                                  7  1'Position to'
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(ND)
     A            $CLHK4        18A  O  7 20DSPATR(HI)
     A            $CLHD4        40A  O  7 39DSPATR(HI)
     A            ##INTK        18   B  8  1
     A N34                                  DSPATR(UL)
     A  34                                  DSPATR(ND PR)
     A            $CLHK5        18A  O  8 20DSPATR(HI)
     A            $CLHD5        40A  O  8 39DSPATR(HI)
      *
      *--------------------------------------*
      ***  Bottom of screen record Format  ***
      *--------------------------------------*
      *
     A          R $CMDTXT1
     A                                      TEXT('Bottom of screen')
     A                                      CHANGE(20 'Change indicator')
     A                                      OVERLAY
     A                                      PUTOVR
      *
      ***  Comment lines
      *
     A  51                             20  2'Comment :'
     A  51        $HCOM1        60A  O 20 12
     A  51        $HCOM2        60A  O 21 12
      *
      ***  Function keys authorized
      *
     A                                 23  2'F3=Exit'
      *
      ***  Amend mode
      *
     A N31N34                          23 12'F4=Delete line   F5=Refresh   F12-
     A                                      =Previous'
      *
      ***  Enquiry mode
      *
     A  31N34                          23 12'F12=Previous'
      *
      ***  Delete mode
      *
     A  31 34                          23 12'F10=Confirm deletion   F12=Previo-
     A                                      us'
      *
      *---------------------------------------------*
      ***  Error messages Subfile records Format  ***
      *---------------------------------------------*
      *
     A          R $MSGRCD                   SFL
     A                                      SFLMSGRCD(24)
     A            $MSGKY                    SFLMSGKEY
     A            $PGMQ                     SFLPGMQ
      *
     A          R $MSGCTL                   SFLCTL($MSGRCD)
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A N29                                  SFLEND
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A            $PGMQ                     SFLPGMQ
