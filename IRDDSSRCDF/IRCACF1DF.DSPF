/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IR Caps/Collars/Floors Details Screen')
      *****************************************************************
      *                                                               *
      *  Midas - FRA/IRS Module                                       *
      *                                                               *
      *  IRCACF1DF - Caps/Collars/Floors Details Display              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CAP055  *CREATE    Date 13Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAP055 - APIs for FRA/IRS Caps/Collars/Floors                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(23)
      *
      ** Main Display Screen
      *
     A          R IRCACF1F0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      OVERLAY PUTOVR OVRATR
      *
      *
      ** Header Data
      *
     A            #0USER        10A  O  1  2OVRDTA
     A                                  1 13'IRCACF1'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            #0WSID        10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A            #0TITL        20A  O  2 30
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A            #0TIME         6Y 0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('Current Time')
      *
      ** Deal Number
      *
     A                                  3  2'Deal No.'
     A            #0DLNO         6   O  3 11TEXT('Deal Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      ** Action Code
      *
     A                                  3 19'Action'
     A            #0ACCD         1   O  3 26TEXT('Action Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      ** Deal Type
      *
     A                                  3 29'Type'
     A            #0DTYP         2   O  3 34TEXT('Deal Type')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      ** Deal Sub Type
      *
     A                                  3 38'Sub-type'
     A            #0DLST         2   B  3 47TEXT('Deal Sub-Type')
     A                                      DSPATR(HI)
     A N13N70                               DSPATR(UL)
     A  13                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                      OVRATR
      *
      ** Deal Date
      *
     A                                  3 51'Deal Date'
     A            #0DDAT         6   B  3 61TEXT('Deal Date')
     A                                      DSPATR(HI)
     A N14N70                               DSPATR(UL)
     A  14                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA OVRATR
      *
      ** Booking Branch Code
      *
     A N80                              3 68'Bk.Branch'
     A                                      OVRATR
     A  80                                  DSPATR(ND)
     A N80        #0BRCA         3   B  3 78TEXT('Booking Branch Code')
     A                                      OVRDTA OVRATR
     A                                      DSPATR(HI)
     A  15                                  DSPATR(RI PC)
     A N15N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
      *
      ** Broker
      *
     A                                  4  2'Broker'
     A            #0BRKC         4   B  4 11TEXT('Broker Code')
     A                                      OVRDTA OVRATR
     A                                      DSPATR(HI)
     A N16N70                               DSPATR(UL)
     A  16                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
      *
      ** Brokerage
      *
     A                                  4 19'Brokerage'
     A            #0BAGE        14   B  4 29TEXT('Brokerage')
     A                                      OVRDTA OVRATR
     A                                      DSPATR(HI)
     A N17N70                               DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
      *
      ** Linked Reference Number
      *
     A                                  4 46'Linked Reference Number'
     A            #0LNKD         6   B  4 71TEXT('Linked Ref Number')
     A                                      OVRDTA OVRATR
     A                                      DSPATR(HI)
     A N18N70                               DSPATR(UL)
     A  18                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
      *
      ** Customer Number
      *
     A                                  5  2'Customer'
     A            #0CNUM        10   B  5 11TEXT('Customer Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N19N70                               DSPATR(UL)
     A  19                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Value Date
      *
     A                                  5 23'Value date'
     A            #0VDAT         6   B  5 34TEXT('Value Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N20N70                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Maturity Date
      *
     A                                  5 42'Maturity date'
     A            #0MDAT         6   B  5 56TEXT('Maturity Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N21N70                               DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** A/C Sequence Number
      *
     A                                  5 64'A/c seq.no.'
     A            #0CDAS         2   B  5 76TEXT('Cust. Deal. Acc. Seq.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N22N70                               DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Currency
      *
     A                                  6  2'Currency'
     A            #0CUCY         3   B  6 11TEXT('Currency')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N23N70                               DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Amount
      *
     A                                  6 16'Amount'
     A            #0PAMT        14   B  6 23TEXT('Amount')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N24N70                               DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Federal Funds Indicator
      *
     A                                  6 38'F/f'
     A            #0FEFI         1   B  6 42TEXT('Fed. Funds Ind.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N25N70                               DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Base Currency Rate
      *
     A                                  6 46'Base currency rate'
     A            #0BCXR        14   B  6 65TEXT('Base CCY Rate')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N26N70                               DSPATR(UL)
     A  26                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Book Code
      *
     A                                  7  2'Book'
     A            #0BOKC         2   B  7  7TEXT('Book Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N27N70                               DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Interest Calculation Basis
      *
     A                                  7 11'Calc.meth.'
     A            #0ICBS         1   B  7 22TEXT('Interest Calc. Basis')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N28N70                               DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Taxable Indicator
      *
     A                                  7 25'Taxable'
     A            #0TAXI         1   B  7 33TEXT('Taxable Ind.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N29N78N70                            DSPATR(UL)
     A  29                                  DSPATR(RI PC)
     A  70
     AO 78                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Profit Centre
      *
     A                                  7 37'Profit Centre'
     A  82                                  DSPATR(ND)
     A                                      OVRATR
     A            #0PRFC         4   B  7 51TEXT('Profit Centre')
     A                                      OVRDTA
     A  82                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N30N12N70                            DSPATR(UL)
     A  30                                  DSPATR(RI PC)
     A  70
     AO 82
     AO 12                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Originating Branch
      *
     A N80N81                           7 57'Originating Branch'
     A N80N81     #0ORBR         3   B  7 76TEXT('Originating Branch')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N31N70                               DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *                                              Begin /CDL038/
     A                                  8  2'Class'
     A  08                                  DSPATR(ND)
     A            #0CLAS         4A  B  8  8
     A                                      DSPATR(HI)
     A N09N70                               DSPATR(UL)
     A  09                                  DSPATR(RI PC)
     A  70
     AO 08                                  DSPATR(PR)
     A  08                                  DSPATR(ND)
     A                                      OVRDTA
      *                                              End   /CDL038/
      *
      ** Buy/Sell Indicator
      *
     A                                 10  2'Buy/Sell'
     A            #0BYSL         1   B 10 11TEXT('Buy/Sell Indicator')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N32N70                               DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRATR
      *
      ** Base Rate Code
      *
     A                                 12  2'Base rate'
     A            #0BRTT         2   B 12 12TEXT('Ref Base Rate Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N33N79N70                            DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A  70
     AO 79                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Spread/Rate
      *
     A                                 12 16'Spread'
     A            #0RTSP        12   B 12 23TEXT('Ref Spread/Rate')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N34N70N84                            DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A  70
     AO 84                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Spread Indicator
      *
     A                                 12 37'Spread ind'
     A            #0SPIN         1   B 12 49TEXT('Ref Spread Ind')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N35N70N84                            DSPATR(UL)
     A  35                                  DSPATR(RI PC)
     A  70
     AO 84                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Rate Fix Days
      *
     A                                 12 52'Rate fix days'
     A            #0FRFD         2   B 12 66TEXT('Rate fix days')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N36N70N77                            DSPATR(UL)
     A  36                                  DSPATR(RI PC)
     A  70
     AO 77                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Rate Change Date
      *
     A                                 13  2'Rate Change Date'
     A            #0NICD         6   B 13 20TEXT('Rate Change Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N37N70                               DSPATR(UL)
     A  37                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Rate Change Frequency
      *
     A                                 13 28'Freq.'
     A            #0ICFR         1   B 13 34TEXT('Rate Change Frequency')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N38N70                               DSPATR(UL)
     A  38                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Rate Change Day in Month
      *
     A                                 13 37'Day'
     A            #0ICFD         2   B 13 41TEXT('Ref Rate Change Day in Mnth')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N39N70                               DSPATR(UL)
     A  39                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Interest Payment Day in Month
      *
     A                                 14  2'Int. payment Date'
     A            #0NIPD         6   B 14 20TEXT('Ref Int. Payment Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N40N70                               DSPATR(UL)
     A  40                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Interest Payment Frequency
      *
     A                                 14 28'Freq.'
     A            #0IPFR         1   B 14 34TEXT('Ref Int. Payment Frequency')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N41N70                               DSPATR(UL)
     A  41                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Interest Payment Day in Month
      *
     A                                 14 37'Day'
     A            #0IPFD         2   B 14 41TEXT('Ref Int. Pay. Day in Month')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N42N70                               DSPATR(UL)
     A  42                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Short Term Rate
      *
     A            #0NARR        15   O 14 45TEXT('Short Term/Curr.Ave.')
     A                                      OVRDTA
     A            #0EINR        12   B 14 61TEXT('Short Term Rate')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N43N70N76                            DSPATR(UL)
     A  43                                  DSPATR(RI PC)
     A  70
     AO 76                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Ceiling Rate
      *
     A
     A                                 16  2'Ceiling rate'
     A                                      OVRATR
     A  62                                  DSPATR(ND)
     A            #0CRAT        12   B 16 15TEXT('Ceiling Rate')
     A                                      OVRDTA
     A  62                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N44N70                               DSPATR(UL)
     A  44                                  DSPATR(RI PC)
     A  62
     AO 70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Floor Rate
      *
     A
     A                                 16 28'Floor Rate'
     A                                      OVRATR
     A  60                                  DSPATR(ND)
     A            #0FRAT        12   B 16 40TEXT('Floor Rate')
     A                                      OVRDTA
     A  60                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N45N70                               DSPATR(UL)
     A  45                                  DSPATR(RI PC)
     A  60
     AO 70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Average Rate Method
      *
     A                                 16 54'Average rate method'
     A            #0AVRM         1   B 16 74TEXT('Average Rate Method')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N46N70                               DSPATR(UL)
     A  46                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
     A                                 17  2'Premium :'
     A  86                                  DSPATR(ND)
      *
      ** Premium Value Date
      *
     A                                 17 12'Value date'
     A  86                                  DSPATR(ND)
     A            #0PUPVD        6   B 17 23TEXT('Premium Value Date')
     A                                      OVRDTA
     A  86                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N47N70                               DSPATR(UL)
     A  47                                  DSPATR(RI PC)
     A  86
     AO 70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Premium Amount
      *
     A                                 17 31'Amount'
     A  86                                  DSPATR(ND)
     A            #0PUPFE       14   B 17 38TEXT('Premium Amount')
     A                                      OVRDTA
     A  86                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N48N70                               DSPATR(UL)
     A  48                                  DSPATR(RI PC)
     A  86
     AO 70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRATR
      *
      ** Counter Party Narrative
      *
     A            #0CPTX        17   O 18  2TEXT('Counterparty Narrative')
     A                                      OVRATR
     A                                      OVRDTA
     A N67                                  DSPATR(ND)
      *
      ** Counterparty  Status
      *
     A            #0CPST        15   O 18 20TEXT('Counterparty Status')
     A                                      OVRATR
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N67                                  DSPATR(ND)
      *
      ** Broker Narrative
      *
     A            #0BKTX        11   O 18 42TEXT('Broker Narrative')
     A                                      OVRATR
     A                                      OVRDTA
     A N67                                  DSPATR(ND)
      *
      ** Broker Status
      *
     A            #0BKST        15   O 18 54TEXT('Broker Status')
     A                                      OVRATR
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N67                                  DSPATR(ND)
      *
      ** Main Screen Footer
      *
     A          R IRCACF1F1                 TEXT('Screen Footer')
     A                                      OVERLAY PUTOVR
      *
     A            #1STAT        24     23 45OVRDTA
     A  87                                  DSPATR(RI)
     A N87                                  DSPATR(ND)
      *
     A                                 24  2'F3=Exit F12=Cancel'
     A                                    +1'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                    +1'F8=Next'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                    +1'F10=Confirm Del'
     A  73                                  DSPATR(HI)
     A N73                                  DSPATR(ND)
     A                                      OVRATR
     A                                    +1'F14=Settlements'
     A N74                                  DSPATR(ND)
     A                                      OVRATR
     A                                    +1'F15=Select'
     A N75                                  DSPATR(ND)
     A                                      OVRATR
      *
      ** Subfile details for program messages
      *
     A          R IRCACF1S0                 TEXT('Program Messages Subfile')
     A                                      SFL SFLMSGRCD(22)
     A            #0SFKY                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
      ** Subfile control for messages
      *
     A          R IRCACF1C0                 TEXT('Program Messages Control')
     A                                      SFLCTL(IRCACF1S0)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            #0PGMQ                    SFLPGMQ
