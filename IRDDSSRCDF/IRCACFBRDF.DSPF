/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IR Caps/Collars/Floors - Browse')
      *****************************************************************
      *                                                               *
      *  Midas - FRA/IRS Dealing Module                               *
      *                                                               *
      *  IRCACFBRDF - Caps/Collars/Floors Browse Display File         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CIR008             Date 13Mar02               *
      *  Prev Amend No. CAP055  *CREATE    Date 13Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CIR008 - FRA/IRS Deals Authorisation                         *
      *  CAP055 - APIs for FRA/IRS Caps/Collars/Floors                *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
     A                                      INDARA
 
      * -------------------------------------------------------------------*
      *   DEAL NUMBER SUBFILE                                              *
      * -------------------------------------------------------------------*
 
     A          R IRCACFBS0                 SFL
     A  84                                  SFLNXTCHG
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A            #0ACTN         1A  B  8  3TEXT('Action Code')
     A                                      DSPATR(UL)
     A            #0DLNO    R     A  O  8  6REFFLD(DLNO)
     A                                      DSPATR(HI)
     A            #0DTYP    R        O  8 14REFFLD(DTYP)
     A            #0CCY     R        O  8 18REFFLD(CCY)
     A            #0AMNT        17A  O  8 23TEXT('Amount')
     A            #0VDAT    R     A  O  8 42TEXT('Value Date')
     A                                      REFFLD(CHGD)
     A            #0MDAT    R     A  O  8 51TEXT('Maturity Date')
     A                                      REFFLD(CHGD)
     A            #0CNUM    R     A  O  8 60REFFLD(CNUM)
     A**Start of change CIR008
     A**********  #0TEXT         7A  O  8 70TEXT('Status')                                    CIR008
     A            #0TEXT        10A  O  8 70TEXT('Status')
     A**End of change CIR008
     A N85                                  DSPATR(ND)
     A  85                                  DSPATR(RI)
 
      * -------------------------------------------------------------------*
      *   DEAL NUMBER SUBFILE CONTROL                                      *
      * -------------------------------------------------------------------*
 
     A          R IRCACFBC0                 SFLCTL(IRCACFBS0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('DEAL NUMBER CONTROL')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  86                                  SFLEND
     A  87                                  SFLCLR
     A N86                                  ROLLUP(88)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            #0SFRN         4S 0H      SFLRCDNBR
     A            #0USER        10A  O  1  2OVRDTA
     A                                  1 13'IRCACFB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            #0WID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 20'Interest Rate +
     A                                      CAPS/COLLARS/FLOORS Enquiry'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Sel'
     A                                  4  6'Deal'
     A                                  4 13'Deal'
     A                                  4 18'Ccy'
     A                                  4 30'Amount'
     A                                  4 42'Value'
     A                                  4 50'Maturity'
     A                                  4 60'Customer'
     A                                  5  6'Number'
     A                                  5 13'Type'
     A                                  5 42'Date'
     A                                  5 51'Date'
     A                                  5 60'Number'
     A            #0DEAL    R     A  B  6  6REFFLD(DLNO)
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            #0TYPE    R     A  B  6 14REFFLD(DTYP)
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
 
      * -------------------------------------------------------------------*
      *   FRONT OFFICE ID SUBFILE                                          *
      * -------------------------------------------------------------------*
 
     A          R IRCACFBS1                 SFL
     A  84                                  SFLNXTCHG
     A                                      TEXT('FRONT OFFICE ID SUBFILE')
     A            #0ACTN         1A  B  8  2TEXT('Action Code')
     A                                      DSPATR(UL)
     A            #0FOID        20A  O  8  4TEXT('Front Office ID')
     A                                      DSPATR(HI)
     A            #0DLNO    R     A  O  8 25REFFLD(DLNO)
     A            #0DTYP    R        O  8 32REFFLD(DTYP)
     A            #0CCY     R        O  8 35REFFLD(CCY)
     A            #0AMNT        17A  O  8 39TEXT('Amount')
     A            #0VDAT    R     A  O  8 57TEXT('Value Date')
     A                                      REFFLD(CHGD)
     A            #0MDAT    R     A  O  8 64TEXT('Maturity Date')
     A                                      REFFLD(CHGD)
     A**Start of change CIR008
     A**********  #0TEXT         7A  O  8 71TEXT('Status')                                    CIR008
     A            #0TEXT        10A  O  8 71TEXT('Status')
     A**End of change CIR008
     A N85                                  DSPATR(ND)
     A  85                                  DSPATR(RI)
      *
      * -------------------------------------------------------------------*
      *   FRONT OFFICE ID SUBFILE CONTROL                                  *
      * -------------------------------------------------------------------*
      *
     A          R IRCACFBC1                 SFLCTL(IRCACFBS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('FRONT OFFICE ID CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  86                                  SFLEND
     A  87                                  SFLCLR
     A N86                                  ROLLUP(88)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            #0SFRN         4S 0H      SFLRCDNBR
     A            #0USER        10A  O  1  2OVRDTA
     A                                  1 13'IRCACFB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            #0WID         10A  O  2  2OVRDTA
     A                                  2 20'Interest Rate +
     A                                      CAPS/COLLARS/FLOORS Enquiry'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Sel'
     A                                  4  6'Front Office'
     A                                  4 25'Deal'
     A                                  4 32'Dl'
     A                                  4 35'Ccy'
     A                                  4 46'Amount'
     A                                  4 57'Value'
     A                                  4 63'Maturity'
     A                                  5  6'Identifier'
     A                                  5 25'Number'
     A                                  5 32'Ty'
     A                                  5 57'Date'
     A                                  5 63'Date'
     A            #1FRNT        20A  B  6  4TEXT('Front Office ID')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            #1DEAL    R     A  B  6 25REFFLD(DLNO)
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            #1TYPE    R     A  B  6 32REFFLD(DTYP)
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
      *
      * -------------------------------------------------------------------*
      *   SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R IRCACFBF0                 TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further +
     A                                      records'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
