/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IR Caps/Collars/Floors - Key Screen')
      *****************************************************************
      *                                                               *
      *  Midas - FRA/IRS Dealing Module                               *
      *                                                               *
      *  IRCACFIDF - Caps/Collars/Floors Initial Screen Display       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CAP055  *CREATE    Date 13Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP055 - APIs for FRA/IRS Caps/Collars/Floors                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CF03
     A  71                                  CF05
     A  72                                  CF07
     A  72                                  CF08
     A  73                                  CF12
     A  74                                  CF15
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(22)
      *
      ** Initial Details
      *
     A          R IRCACFIF0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      OVERLAY PUTOVR
      *
      *
      *
      *
     A            #0USER        10A  O  1  2OVRDTA
     A                                  1 13'IRCACFI'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            #0WSID        10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 16'FORWARD RATE AGREEMENTS +
     A                                      /INTEREST RATE SWAPS'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A            #0TIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A          R IRCACFIF1                 OVERLAY PUTOVR
      *
     A                                 24  2'Or enter Deal no.'
     A            #0DLNO         6   B 24 20TEXT('New Deal Number')
     A                                      OVRDTA
     A N11                                  DSPATR(UL HI)
     A  11                                  DSPATR(RI PC)
     A                                      OVRATR
      *
     A                                 24 28'Action'
     A            #0ACCD         1   B 24 35TEXT('New Action Code')
     A                                      OVRDTA
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(UL HI)
     A  68
     AO 10                                  DSPATR(PC)
     A                                      OVRATR
      *
     A                                 24 38'and Deal type'
     A            #0DTYP         2   B 24 52TEXT('New Deal Type')
     A                                      OVRDTA
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(UL HI)
     A                                      OVRATR
      *
     A                                 23  2'F3=Exit F5=Refresh F7=Prev +
     A                                      F8=Next F12=Cancel F15=Select'
      *
      ** Subfile Details for Program Messages
      *
     A          R IRCACFIS0                 TEXT('Program Message Subfile')
     A                                      SFL SFLMSGRCD(22)
     A            #0SFKY                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
      ** Subfile Message Control
      *
     A          R IRCACFIC0                 TEXT('Msg Subfile Control')
     A                                      SFLCTL(IRCACFIS0)
     A                                      SFLPAG(1) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            #0PGMQ                    SFLPGMQ
      *
