/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IR Caps/Collars/Floors - Repair Browse')
      *****************************************************************
      *                                                               *
      *  Midas - FRA/IRS Module                                       *
      *                                                               *
      *  IRCACFRPDF - Invalid Caps/Collars/Floors Browse              *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CAP055  *CREATE    Date 13Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP055 - APIs for FRA/IRS Caps/Collars/Floors                *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R IRICACFS0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A            #0OPTN         1A  B  8  2TEXT('Select Option')
     A                                      DSPATR(UL)
     A            #0ACCD    R        O  8  6REFFLD(ACTO)
     A                                      DSPATR(HI)
     A            #0FOID    R     A  O  8  9REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A            #0DTYP    R        O  8 30REFFLD(DTPE)
     A            #0CUCY    R        O  8 35REFFLD(CCY)
     A            #0PAMT    R        O  8 39REFFLD(AM14)
     A            #0VDAT    R     A  O  8 54REFFLD(CHGD)
     A                                      TEXT('Value Date')
     A            #0MDAT    R     A  O  8 61REFFLD(CHGD)
     A                                      TEXT('Maturity Date')
     A            #0CNUM    R        O  8 70REFFLD(CSNM)
     A            #0FOAS    R        H      REFFLD(FOTRNID)
     A            #0TMES    R        H      REFFLD(TIMESTMP)
     A            #0DLNO    R     A  H      REFFLD(DEAL)
     A            #0DDAT    R     A  H      REFFLD(CHGD)
     A            #0BRSN    R        H      REFFLD(BRSN)
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN CONTROL                                            *
      * -------------------------------------------------------------------*
      *
     A          R IRICACFS1                 TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(IRICACFS0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  21                                  SFLEND
     A  22                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N21                                  ROLLUP(23)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            #1SFRN         4S 0H      SFLRCDNBR
     A                                  1  2'IRCACFR'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            #1TIME         6Y 0O  1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            #1WID          3A  O  1 77TEXT('WORKSTATION ID')
     A                                  3 24'IRS DEALS - Invalid Deal Maintenan-
     A                                      ce'
     A                                  4 37'ENQUIRY'
     A                                  6  1'Sel'
     A                                  6  5'Act'
     A                                  6  9'Front Office ID'
     A                                  6 30'Deal'
     A                                  6 35'Ccy'
     A                                  6 39'Amount'
     A                                  6 54'Value'
     A                                  6 61'Maturity'
     A                                  6 70'Customer'
     A                                  7 30'Type'
     A                                  7 54'Date'
     A                                  7 61'Date'
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R IRICACFF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * END MESSAGE                                                      *
      * -------------------------------------------------------------------*
      *
     A          R IRICACFEND                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
     A                                      PUTOVR
     A                                  1  2'IRCACFR'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            #1TIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            #1WID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'IRS DEALS - Invalid Deal Maintenan+
     A                                      ce'
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
     A            #0FOTX        40A    24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
