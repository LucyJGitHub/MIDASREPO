/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas IR Cross ccy IRS browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS MODULE                                       *
     A*                                                               *
     A*  IRCIRSBDF - CROSS CURRENCY IRS BROWSE                        *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.03 ----------------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CIR008             Date 13Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CAP004             Date 31Aug98               *
      *                 CAP003  *CREATE    Date 27Apr98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CIR008 - FRA/IRS Deals Authorisation                         *
     A*  CAP004 - API's phase 3.                                      *
     A*           Allow browse by Front Office Id.                    *
     A*  CAP003 - Conversion of transaction inputs into modular       *
     A*           structure to use as APIs.                           *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R IRCIRSS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDDEAL    R     A     8  8REFFLD(DEAL)
     A                                      DSPATR(HI)
     A            DDCCY1         3A     8 15TEXT('OUR CCY')
     A            DDSAMN1       17A     8 19TEXT('OUR AMOUNT')
     A            DDCCY2         3A     8 37TEXT('THEIR CCY')
     A            DDSAMN2       17A     8 41TEXT('THEIR AMOUNT')
     A            DDVDAT    R     A     8 59TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
     A            DDMDAT    R     A     8 66TEXT('MATURITY DATE')
     A                                      REFFLD(CHGD)
     A            DDCNUM    R     A     8 73REFFLD(CNUM)
     A*******     DDTEXT         7A     8 72TEXT('"DELETED" OR CP/BK STATUS')
     A*N95***                               DSPATR(ND)
     A**95***                               DSPATR(RI)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R IRCIRSS0                   TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
      *                                                                             /*Start CAP004*/
     A                                      CF11
      *                                                                             /* End  CAP004*/
     A                                      SFLCTL(IRCIRSS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
      *                                                                             /*Start CAP004*/
     A**********************************1  2'Ref: IRCIRS'
     A                                  1  2'IRCIRS'
      *                                                                             /* End  CAP004*/
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
      *                                                                             /*Start CAP004*/
     A*******                           3 20'SINGLE CURRENCY INTEREST RATE +
     A*******                               SWAPS ENQUIRY'
     A                                  3 20'CROSS CURRENCY INTEREST RATE +
     A                                      SWAPS ENQUIRY'
      *                                                                             /* End  CAP004*/
     A*/COPY WNCPYSRC,IRCIRSBD01
     A                                  6  2'Select'
     A                                  6  9'Deal'
     A                                  6 15'Our'
     A                                  6 27'Our'
     A                                  6 37'Thr'
     A                                  6 49'Their'
     A                                  6 60'Value'
     A                                  6 67'Mat'
     A                                  6 74'Cust'
     A                                  7 10'No'
     A                                  7 15'Ccy'
     A                                  7 26'Amount'
     A                                  7 37'Ccy'
     A                                  7 48'Amount'
     A                                  7 61'Date'
     A                                  7 67'Date'
     A                                  7 75'No'
     A*/COPY WNCPYSRC,IRCIRSBD02
     A*
      *                                                                             /*Start CAP004*/
     A          R IRCIRSS3                  TEXT('SELECT SCREEN SUBFILE')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFODN        20A     8  7TEXT('FRONT OFFICE ID.')
     A                                      DSPATR(HI)
     A            DDDEAL    R     A     8 29REFFLD(DEAL)
     A            DDVDAT    R     A     8 36TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
     A            DDMDAT    R     A     8 43TEXT('MATURITY DATE')
     A                                      REFFLD(CHGD)
     A            DDCNUM    R     A     8 50REFFLD(CNUM)
     A**Start of change CIR008
     A**********  DDTEXT         7A     8 72TEXT('"DELETED" OR CP/BK STATUS')
     A            DDTEXT        12A     8 67TEXT('"DELETED" OR CP/BK STATUS')
     A**End of change CIR008
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
     A*
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R IRCIRSS2                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLCTL(IRCIRSS3)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'IRCIRSB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 20'CROSS CURRENCY INTEREST RATE +
     A                                      SWAPS ENQUIRY'
     A*/COPY WNCPYSRC,IRCIRSBD03
     A                                  6  2'Select'
     A                                  6 10'Front Office'
     A                                  6 30'Deal'
     A                                  6 36'Value'
     A                                  6 44'Mat'
     A                                  6 51'Cust'
     A                                  7 10'Identifier'
     A                                  7 31'No'
     A                                  7 37'Date'
     A                                  7 44'Date'
     A                                  7 52'No'
     A*/COPY WNCPYSRC,IRCIRSBD04
      *                                                                             /* End  CAP004*/
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R IRCIRSF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
      *                                                                             /*Start CAP004*/
     A********                         24  2'F3=Exit F12=Cancel'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
      *                                                                             /* End  CAP004*/
