      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IR Fees, Buy-Outs & Processing Method Maint')
      *****************************************************************
      *                                                               *
      *  Midas - FRA/IRS Dealing Module                               *
      *                                                               *
      *  IRFEBODF - Fees, Buy-Outs & Processing Method Maintenance    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. CAP041  *CREATE    Date 10Jan02               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CAP041 - APIs for IRS Fees/Buy-Outs/Processing Method/Next   *
      *           Principal Change Date.                              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A N52                                  CA03(03 'Exit')
     A  24                                  CF08(08 'Next Screen')
     A                                      CF12(12 'Previous')
     A                                      CHGINPDFT
      *
     A          R IRFEBOD1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            ##PGM         10A  H
     A                                  1 14'SINGLE CURRENCY INTEREST -
     A                                      RATE SWAPS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 63TIME
     A                                  1 74'WID'
     A            SWSID          3A  O  1 78TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  3  2'Deal no.'
     A            SDLNO          6A  O  3 11TEXT('Deal No.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  3 19'Action'
     A            SACTN          1A  O  3 26TEXT('Action Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  3 29'Type'
     A            SDTYP          2A  O  3 34TEXT('Deal Type')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  3 38'Sub-type'
     A            SDLST          2A  O  3 47TEXT('Deal Sub-Type')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  3 51'Deal date'
     A            SDDAT          6A  O  3 61TEXT('Deal Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  3 68'Bk.Branch'
     A                                      OVRATR
     A N83                                  DSPATR(ND)
     A            SBRCA          3A  O  3 78TEXT('Booking Branch Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N83                                  DSPATR(ND)
     A                                  4  2'Broker'
     A            SBRKC          4A  O  4 11TEXT('Broker Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 19'Brokerage'
     A            SBAGE         14A  O  4 29TEXT('Brokerage Amount')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 46'Linked reference number'
     A            SLNKDN         6A  O  4 71TEXT('Linked Deal No.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Customer'
     A            SCNUM         10A  O  5 11TEXT('Customer No.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 23'Value date'
     A            SVDAT          6A  O  5 34TEXT('Value Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 42'Maturity date'
     A            SMDAT          6A  O  5 56TEXT('Maturity Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 64'A/c seq.no.'
     A            SCDAS          2A  O  5 76TEXT('Account Seq. No.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Currency'
     A            SUCUCY         3A  O  6 11TEXT('Currency')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  6 16'Amount'
     A            SUPAMT        14A  O  6 23TEXT('Amount')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Up-Front Fee    :'
     A                                  8 20'Value date'
     A            SUFVD          6   B  8 31TEXT('Up-Front Value Date')
     A                                      OVRDTA
     A N40                                  DSPATR(HI)
     A  52
     AO 29
     AO 26                                  DSPATR(PR)
     A N52
     AAN29
     AAN26                                  DSPATR(UL)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A                                  8 39'Amount'
     A            SUFEE         14   B  8 46TEXT('Up-Front Fee')
     A                                      OVRDTA
     A N41                                  DSPATR(HI)
     A  52
     AO 29
     AO 26                                  DSPATR(PR)
     A N52
     AAN29
     AAN26                                  DSPATR(UL)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A                                  8 62'Pay/Rec'
     A            SUFPR          1   B  8 70TEXT('Up-Front - Pay/Rec')
     A                                      OVRDTA
     A N42                                  DSPATR(HI)
     A  52
     AO 29
     AO 26                                  DSPATR(PR)
     A N52
     AAN29
     AAN26                                  DSPATR(UL)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A                                  9  2'Buy-Out Details :'
     A                                  9 20'Value date'
     A            SBOVD          6   B  9 31TEXT('Buy-Out Value Date')
     A                                      OVRDTA
     A N43                                  DSPATR(HI)
     A  52
     AO 29
     AO 27                                  DSPATR(PR)
     A N52
     AAN29
     AAN27                                  DSPATR(UL)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A                                  9 39'Amount'
     A            SBUYA         14   B  9 46TEXT('Buy-Out Amount')
     A                                      OVRDTA
     A N44                                  DSPATR(HI)
     A  52
     AO 29
     AO 27                                  DSPATR(PR)
     A N52
     AAN29
     AAN27                                  DSPATR(UL)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A                                  9 62'Pay/Rec'
     A            SBOPR          1   B  9 70TEXT('Buy-Out - Pay/Rec')
     A                                      OVRDTA
     A N45                                  DSPATR(HI)
     A  52
     AO 29
     AO 27                                  DSPATR(PR)
     A N52
     AAN29
     AAN27                                  DSPATR(UL)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A                                 11  2'Next principal change -
     A                                      date'
     A            SONPCD         6   B 11 29TEXT('Next principal chg')
     A                                      OVRDTA
     A N46                                  DSPATR(HI)
     A  52
     AO 28                                  DSPATR(PR)
     A N52
     AAN28                                  DSPATR(UL)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A                                 13  1'Our details'
     A                                      DSPATR(UL)
     A                                 14  2'Interest Processing -
     A                                      method'
     A            SOIPM          1   B 14 32TEXT('Our Process method')
     A                                      OVRDTA
     A N47                                  DSPATR(HI)
     A  25
     AO 52
     AO 28                                  DSPATR(PR)
     A N25
     AAN52
     AAN28                                  DSPATR(UL)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A                                 15  2'Unsettled Interest'
     A                                      OVRATR
     A N37
     AON32                                  DSPATR(ND)
     A            SOUSI         15   O 15 32TEXT('Our Unsettled Int')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N37
     AON32                                  DSPATR(ND)
     A                                 16  2'Unsettled Interest -
     A                                      Adjustment'
     A                                      OVRATR
     A N37
     AON32                                  DSPATR(ND)
     A            SOUSA         15   O 16 32TEXT('Our Unsettled Adj.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N37
     AON32                                  DSPATR(ND)
     A                                 18  1'Their details'
     A                                      DSPATR(UL)
     A                                 19  2'Interest Processing -
     A                                      method'
     A            STIPM          1   B 19 32TEXT('Their Proc. method')
     A                                      OVRDTA
     A N49                                  DSPATR(HI)
     A  25
     AO 52
     AO 28                                  DSPATR(PR)
     A N25
     AAN52
     AAN28                                  DSPATR(UL)
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A                                 20  2'Unsettled Interest'
     A                                      OVRATR
     A N38
     AON32                                  DSPATR(ND)
     A            STUSI         15   O 20 32TEXT('Their Unsettled Int')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N38
     AON32                                  DSPATR(ND)
     A                                 21  2'Unsettled Interest -
     A                                      Adjustment'
     A                                      OVRATR
     A N38
     AON32                                  DSPATR(ND)
     A            STUSA         15   O 21 32TEXT('Their Unsettled Adj')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N38
     AON32                                  DSPATR(ND)
     A                                 23  2'F3=Exit  -
     A                                      F12=Previous screen'
     A                                      OVRATR
     A  52                                  DSPATR(ND)
     A  24                             23 32'F8=Next Screen'
     A                                      OVRATR
      *
     A          R IRFEBOD2
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            ##PGM         10A  H
     A                                  1 21'CROSS-CURRENCY INTEREST -
     A                                      RATE SWAPS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 63TIME
     A                                  1 74'WID'
     A            SWSID          3A  O  1 78TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  3  2'Deal no.'
     A            SDLNO          6A  O  3 11TEXT('Deal No.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  3 19'Action'
     A            SACTN          1A  O  3 26TEXT('Action Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  3 29'Type'
     A            SDTYP          2A  O  3 34TEXT('Deal Type')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  3 38'Sub-type'
     A            SDLST          2A  O  3 47TEXT('Deal Sub-Type')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  3 51'Deal date'
     A            SDDAT          6A  O  3 61TEXT('Deal Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  3 68'Bk.Branch'
     A                                      OVRATR
     A N83                                  DSPATR(ND)
     A            SBRCA          3A  O  3 78TEXT('Booking Branch Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N83                                  DSPATR(ND)
     A                                  4  2'Broker'
     A            SBRKC          4A  O  4 11TEXT('Broker Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 19'Brokerage'
     A            SBAGE         14A  O  4 29TEXT('Brokerage Amount')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 46'Linked reference number'
     A            SLNKDN         6A  O  4 71TEXT('Linked Deal No.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Customer'
     A            SCNUM         10A  O  5 11TEXT('Customer No.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 23'Value date'
     A            SVDAT          6A  O  5 34TEXT('Value Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 42'Maturity date'
     A            SMDAT          6A  O  5 56TEXT('Maturity Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 64'A/c seq.no.'
     A            SCDAS          2A  O  5 76TEXT('Account Seq. No.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Up-Front Fee    :'
     A                                  8 20'Value date'
     A            SUFVD          6A  B  8 31TEXT('Up-Front Value Date')
     A                                      OVRDTA
     A N40                                  DSPATR(HI)
     A  52
     AO 29
     AO 26                                  DSPATR(PR)
     A N52
     AAN29
     AAN26                                  DSPATR(UL)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A                                  8 39'Amount'
     A            SUFEE         14A  B  8 46TEXT('Up-Front Fee')
     A                                      OVRDTA
     A N41                                  DSPATR(HI)
     A  52
     AO 29
     AO 26                                  DSPATR(PR)
     A N52
     AAN29
     AAN26                                  DSPATR(UL)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A                                  8 62'Pay/Rec'
     A            SUFPR          1A  B  8 70TEXT('Up-Front - Pay/Rec')
     A                                      OVRDTA
     A N42                                  DSPATR(HI)
     A  52
     AO 29
     AO 26                                  DSPATR(PR)
     A N52
     AAN29
     AAN26                                  DSPATR(UL)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A                                  9  2'Buy-Out Details :'
     A                                  9 20'Value date'
     A            SBOVD          6A  B  9 31TEXT('Buy-Out Value Date')
     A                                      OVRDTA
     A N43                                  DSPATR(HI)
     A  52
     AO 29
     AO 27                                  DSPATR(PR)
     A N52
     AAN29
     AAN27                                  DSPATR(UL)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A                                  9 39'Amount'
     A            SBUYA         14A  B  9 46TEXT('Buy-Out Amount')
     A                                      OVRDTA
     A N44                                  DSPATR(HI)
     A  52
     AO 29
     AO 27                                  DSPATR(PR)
     A N52
     AAN29
     AAN27                                  DSPATR(UL)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A                                  9 62'Pay/Rec'
     A            SBOPR          1A  B  9 70TEXT('Buy-Out - Pay/Rec')
     A                                      OVRDTA
     A N45                                  DSPATR(HI)
     A  52
     AO 29
     AO 27                                  DSPATR(PR)
     A N52
     AAN29
     AAN27                                  DSPATR(UL)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A                                 11  1'Our details'
     A                                      DSPATR(UL)
     A                                 12  2'Currency'
     A            SUCUCY         3A  O 12 11TEXT('Our Currency')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 12 16'Amount'
     A            SUPAMT        14A  O 12 23TEXT('Our Amount')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 13  2'Next principal change -
     A                                      date'
     A            SONPCD         6A  B 13 32TEXT('Our next prin. chg')
     A                                      OVRDTA
     A N46                                  DSPATR(HI)
     A  52
     AO 28                                  DSPATR(PR)
     A N52
     AAN28                                  DSPATR(UL)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A                                 13 40'Interest Processing -
     A                                      method'
     A            SOIPM          1A  B 13 67TEXT('Our Process method')
     A                                      OVRDTA
     A N47                                  DSPATR(HI)
     A  25
     AO 52
     AO 28                                  DSPATR(PR)
     A N25
     AAN52
     AAN28                                  DSPATR(UL)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A                                 14  2'Unsettled Interest'
     A                                      OVRATR
     A N37
     AON32                                  DSPATR(ND)
     A            SOUSI         15A  O 14 32TEXT('Our Unsettled Int')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N37
     AON32                                  DSPATR(ND)
     A                                 15  2'Unsettled Interest -
     A                                      Adjustment'
     A                                      OVRATR
     A N37
     AON32                                  DSPATR(ND)
     A            SOUSA         15A  O 15 32TEXT('Our Unsettled Adj.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N37
     AON32                                  DSPATR(ND)
     A                                 17  1'Their details'
     A                                      DSPATR(UL)
     A                                 18  2'Currency'
     A            STCUCY         3A  O 18 11TEXT('Their Currency')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 18 16'Amount'
     A            STPAMT        14A  O 18 23TEXT('Their Amount')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 19  2'Next principal change -
     A                                      date'
     A            STNPCD         6A  B 19 32TEXT('Their Next prin chg')
     A                                      OVRDTA
     A N48                                  DSPATR(HI)
     A  52
     AO 28                                  DSPATR(PR)
     A N52
     AAN28                                  DSPATR(UL)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A                                 19 40'Interest Processing -
     A                                      method'
     A            STIPM          1A  B 19 67TEXT('Their Proc. method')
     A                                      OVRDTA
     A N49                                  DSPATR(HI)
     A  25
     AO 52
     AO 28                                  DSPATR(PR)
     A N25
     AAN52
     AAN28                                  DSPATR(UL)
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A                                 20  2'Unsettled Interest'
     A                                      OVRATR
     A N38
     AON32                                  DSPATR(ND)
     A            STUSI         15A  O 20 32TEXT('Their Unsettled Int')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N38
     AON32                                  DSPATR(ND)
     A                                 21  2'Unsettled Interest -
     A                                      Adjustment'
     A                                      OVRATR
     A N38
     AON32                                  DSPATR(ND)
     A            STUSA         15A  O 21 32TEXT('Their Unsettled Adj')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N38
     AON32                                  DSPATR(ND)
     A                                 23  2'F3=Exit  -
     A                                      F12=Previous screen'
     A                                      OVRATR
     A  52                                  DSPATR(ND)
     A  24                             23 32'F8=Next Screen'
     A                                      OVRATR
      *
     A          R IRFEBOD3
      *                                     KEEP
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            ##PGM         10A  H
      *
     A            SNARR         20A  O  1 30DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  1 63TIME
     A                                  1 74'WID'
     A            SWSID          3A  O  1 78TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  3  2'Deal no.'
     A            SDLNO          6A  O  3 11TEXT('Deal No.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  3 19'Action'
     A            SACTN          1A  O  3 26TEXT('Action Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  3 29'Type'
     A            SDTYP          2A  O  3 34TEXT('Deal Type')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  3 38'Sub-type'
     A            SDLST          2A  O  3 47TEXT('Deal Sub-Type')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  3 51'Deal date'
     A            SDDAT          6A  O  3 61TEXT('Deal Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Broker'
     A            SBRKC          4A  O  4 11TEXT('Broker Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 19'Brokerage'
     A            SBAGE         14A  O  4 29TEXT('Brokerage Amount')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 46'Linked reference number'
     A            SLNKDN         6A  O  4 71TEXT('Linked Deal No.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Customer'
     A            SCNUM         10A  O  5 11TEXT('Customer No.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 23'Value date'
     A            SVDAT          6A  O  5 34TEXT('Value Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 42'Maturity date'
     A            SMDAT          6A  O  5 56TEXT('Maturity Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 64'A/c seq.no.'
     A            SCDAS          2A  O  5 76TEXT('Account Seq. No.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Currency'
     A            SUCUCY         3A  O  6 11TEXT('Currency')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  6 16'Amount'
     A            SUPAMT        14A  O  6 23TEXT('Amount')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Up-Front Fee    :'
     A                                  8 20'Value date'
     A            SUFVD          6   B  8 31TEXT('Up-Front Value Date')
     A                                      OVRDTA
     A N40                                  DSPATR(HI)
     A  52
     AO 29
     AO 26                                  DSPATR(PR)
     A N52
     AAN29
     AAN26                                  DSPATR(UL)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A                                  8 39'Amount'
     A            SUFEE         14   B  8 46TEXT('Up-Front Fee')
     A                                      OVRDTA
     A N41                                  DSPATR(HI)
     A  52
     AO 29
     AO 26                                  DSPATR(PR)
     A N52
     AAN29
     AAN26                                  DSPATR(UL)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A                                  8 62'Pay/Rec'
     A            SUFPR          1   B  8 70TEXT('Up-Front - Pay/Rec')
     A                                      OVRDTA
     A N42                                  DSPATR(HI)
     A  52
     AO 29
     AO 26                                  DSPATR(PR)
     A N52
     AAN29
     AAN26                                  DSPATR(UL)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A                                  9  2'Buy-Out Details :'
     A                                  9 20'Value date'
     A            SBOVD          6   B  9 31TEXT('Buy-Out Value Date')
     A                                      OVRDTA
     A N43                                  DSPATR(HI)
     A  52
     AO 29
     AO 27                                  DSPATR(PR)
     A N52
     AAN29
     AAN27                                  DSPATR(UL)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A                                  9 39'Amount'
     A            SBUYA         14   B  9 46TEXT('Buy-Out Amount')
     A                                      OVRDTA
     A N44                                  DSPATR(HI)
     A  52
     AO 29
     AO 27                                  DSPATR(PR)
     A N52
     AAN29
     AAN27                                  DSPATR(UL)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A                                  9 62'Pay/Rec'
     A            SBOPR          1   B  9 70TEXT('Buy-Out - Pay/Rec')
     A                                      OVRDTA
     A N45                                  DSPATR(HI)
     A  52
     AO 29
     AO 27                                  DSPATR(PR)
     A N52
     AAN29
     AAN27                                  DSPATR(UL)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A                                 11  2'Next principal change -
     A                                      date'
     A            SONPCD         6   B 11 29TEXT('Next principal chg')
     A                                      OVRDTA
     A N46                                  DSPATR(HI)
     A  52
     AO 28                                  DSPATR(PR)
     A N52
     AAN28                                  DSPATR(UL)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A                                 13  1'Our details'
     A                                      DSPATR(UL)
     A                                 14  2'Interest Processing -
     A                                      method'
     A            SOIPM          1   B 14 32TEXT('Our Process method')
     A                                      OVRDTA
     A N47                                  DSPATR(HI)
     A  25
     AO 52
     AO 28                                  DSPATR(PR)
     A N25
     AAN52
     AAN28                                  DSPATR(UL)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A                                 15  2'Unsettled Interest'
     A                                      OVRATR
     A N33
     AON32                                  DSPATR(ND)
     A            SOUSI         15   O 15 32TEXT('Our Unsettled Int')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N33
     AON32                                  DSPATR(ND)
     A                                 16  2'Unsettled Interest -
     A                                      Adjustment'
     A                                      OVRATR
     A N33
     AON32                                  DSPATR(ND)
     A            SOUSA         15   O 16 32TEXT('Our Unsettled Adj.')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N33
     AON32                                  DSPATR(ND)
     A                                 18  1'Their details'
     A                                      DSPATR(UL)
     A                                 19  2'Interest Processing -
     A                                      method'
     A            STIPM          1   B 19 32TEXT('Their Proc. method')
     A                                      OVRDTA
     A N49                                  DSPATR(HI)
     A  25
     AO 52
     AO 28                                  DSPATR(PR)
     A N25
     AAN52
     AAN28                                  DSPATR(UL)
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A                                 20  2'Unsettled Interest'
     A                                      OVRATR
     A N34
     AON32                                  DSPATR(ND)
     A            STUSI         15   O 20 32TEXT('Their Unsettled Int')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N34
     AON32                                  DSPATR(ND)
     A                                 21  2'Unsettled Interest -
     A                                      Adjustment'
     A                                      OVRATR
     A N34
     AON32                                  DSPATR(ND)
     A            STUSA         15   O 21 32TEXT('Their Unsettled Adj')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N34
     AON32                                  DSPATR(ND)
      *
     A                                 23  2'F3=Exit  -
     A                                      F12=Previous screen'
     A                                      OVRATR
     A  52                                  DSPATR(ND)
     A  24                             23 32'F8=Next Screen'
     A                                      OVRATR
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
