/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas IR Forward rate agreements browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS MODULE                                       *
     A*                                                               *
     A*  IRFRABDF - FORWARD RATE AGREEMENTS ISSUED BROWSE            *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.03 ----------------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CIR008             Date 13Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CAP004             Date 31Aug98               *
      *                 CAP003  *CREATE    Date 01Nov97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CIR008 - FRA/IRS Deals Authorisation                         *
     A*  CAP004 - API's phase 3.                                      *
     A*           Allow browse by Front Office Id.                    *
     A*  CAP003 - Conversion of MM inputs into modular structure to   *
     A*           use as APIs.                                        *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R IRFRAS1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDDEAL    R     A     8  8REFFLD(DEAL)
     A                                      DSPATR(HI)
     A            DDCCY     R           8 21REFFLD(CCY)
     A            DDSAMN        17A     8 25TEXT('AMOUNT')
     A            DDVDAT    R     A     8 43TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
     A            DDMDAT    R     A     8 51TEXT('MATURITY DATE')
     A                                      REFFLD(CHGD)
     A            DDCNUM    R     A     8 59REFFLD(CNUM)
     A**Start of change CIR008
     A**********  DDTEXT         7A     8 72TEXT('"DELETED" OR CP/BK STATUS')
     A            DDTEXT        12A     8 67TEXT('"DELETED" OR CP/BK STATUS')
     A**End of change CIR008
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R IRFRAS0                   TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
      * Start CAP004
     A                                      CF11
      * End CAP004
     A                                      SFLCTL(IRFRAS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
      * Start CAP004
     A*****************                 1  2'Ref: IRFRA'
     A                                  1  2'IRFRAB'
      * End CAP004
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 28'FORWARD RATE AGREEMENTS ENQUIRY'
     A*/COPY WNCPYSRC,IRFRABDF01
     A                                  6  2'Select'
     A                                  6  9'Deal'
     A                                  6 21'Ccy'
     A                                  6 32'Amount'
     A                                  6 44'Value'
     A                                  6 50'Maturity'
     A                                  6 60'Cust'
     A                                  7 10'No'
     A                                  7 45'Date'
      * Start CAP004
     A*******                           7 50'/Notice'
     A                                  7 52'Date'
     A                                  7 61'No'
     A*/COPY WNCPYSRC,IRFRABDF02
      * End CAP004
     A*
      * Start CAP004
     A          R IRFRAS3                   TEXT('SELECT SCREEN SUBFILE')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A**Start of change CIR008
     A**********  DDFODN        20A     8  7TEXT('FRONT OFFICE ID.')
     A            DDFODN        20A     8  5TEXT('FRONT OFFICE ID.')
     A**End of change CIR008
     A                                      DSPATR(HI)
     A**Start of change CIR008
     A**********  DDDEAL    R     A     8 28REFFLD(DEAL)
     A**********  DDCCY     R           8 35REFFLD(CCY)
     A**********  DDSAMN        17A     8 39TEXT('AMOUNT')
     A**********  DDVDAT    R     A     8 57TEXT('VALUE DATE')
     A            DDDEAL    R     A     8 26REFFLD(DEAL)
     A            DDCCY     R           8 33REFFLD(CCY)
     A            DDSAMN        17A     8 37TEXT('AMOUNT')
     A            DDVDAT    R     A     8 55TEXT('VALUE DATE')
     A**End of change CIR008
     A                                      REFFLD(CHGD)
     A**Start of change CIR008
     A**********  DDMDAT    R     A     8 65TEXT('MATURITY DATE')
     A            DDMDAT    R     A     8 62TEXT('MATURITY DATE')
     A**End of change CIR008
     A                                      REFFLD(CHGD)
     A**Start of change CIR008
     A**********  DDTEXT         7A     8 72TEXT('"DELETED" OR CP/BK STATUS')
     A            DDTEXT        12A     8 69TEXT('"DELETED" OR CP/BK STATUS')
     A**End of change CIR008
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R IRFRAS2                   TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLCTL(IRFRAS3)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'IRFRAB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 28'FORWARD RATE AGREEEMENTS ENQUIRY'
     A**Start of change CIR008
     A**********                        6  2'Select'
     A**********                        6 10'Front Office'
     A**********                        6 30'Deal'
     A**********                        6 35'Ccy'
     A**********                        6 46'Amount'
     A**********                        6 57'Value'
     A**********                        6 64'Maturity'
     A**********                        7 10'Identifier'
     A**********                        7 31'No'
     A**********                        7 57'Date'
     A**********                        7 66'Date'
     A*/COPY WNCPYSRC,IRFRABDF03
     A                                  6  2'Sel'
     A                                  6  8'Front Office'
     A                                  6 28'Deal'
     A                                  6 33'Ccy'
     A                                  6 44'Amount'
     A                                  6 55'Value'
     A                                  6 61'Maturity'
     A                                  7  8'Identifier'
     A                                  7 29'No'
     A                                  7 55'Date'
     A                                  7 63'Date'
     A*/COPY WNCPYSRC,IRFRABDF04
     A**End of change CIR008
      * End CAP004
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R IRFRAF1                   TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
      * Start CAP004
     A********                         24  2'F3=Exit F12=Cancel'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
      * End CAP004
