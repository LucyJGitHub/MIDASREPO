      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IR Forward rate agreements maintenance')         *
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS Module                                       *
     A*                                                               *
     A*  DSPF/IRFRADF - SCREEN FORMATS FOR FRA/IRS MAINTENANCE        *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. CAS006             Date 21Jan03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 195822             Date 18Jul01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 186458             Date 27Nov00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CIR005             Date 21Jan00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSW097             Date 02Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP003  *CREATE    Date 01Jun98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAS006 - Hedge Accounting Phase B                            *
      *  195822 - Cursor still goes to the business ccy fields when   *
      *           tab is pressed eventhough Business Ccy Convention   *
      *           enhancement is not installed                        *
      *  186458 - Display Confirmation Matching status.  Patterned    *
      *           after fix 179380.                                   *
      *  CIR005 - FRA/IRS Business Day Conventions                    *
     A*  CSW097 - MT34*, MT36* message generation.                    *
     A*  CAP003 - Conversion of transaction inputs into modular       *
     A*           structure to use as APIs.                           *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A**************************************************************************
     A*
     A*  FR DEAL TYPE - INSERT/AMEND SCREEN (B)
     A*
     A*                                                                   S01203
     A*
     A*  ALL DEAL TYPES - INITIAL SCREEN (A)
     A*
     A** INITIAL SCREEN ALL DEAL TYPES
     A*                                                                   S01203
     A*
     A* -------------------------------------------------------------------*
     A* * MESSAGE SUBFILE FORMAT                                           *
     A* -------------------------------------------------------------------*
     A*
     A          R IRFRAS1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MESSAGE SUBFILE CONTROL                                          *
     A* -------------------------------------------------------------------*
     A*
     A          R IRFRAS0                   TEXT('MSG SUBFILE CONTRL')
     A                                      CA03
     A                                      CF12
     A     89 72                            CA06
     A                                      CF07
     A                                      CF08
     A                                      CF10
     A     73 72                            CF14
     A                                      CF15
     A                                      SFLCTL(IRFRAS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A*
     A*
     A* -------------------------------------------------------------------*
     A*   MAIN DISPLAY SCREEN                                              *
     A* -------------------------------------------------------------------*
     A*
     A          R IRFRAD1
     A                                      CA03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*                                     Begin /CSW097/
     A*                                     End   /CSW097/
     A*
     A*
     A*
     A*
      *                                              Begin /CIR005/
      *                                              End   /CIR005/
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A                                  1  6'IRFRAD1'
     A                                  1 26'FORWARD RATE AGREEMENTS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            STIME          6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  1 74'WID'
     A            SWSID          3   O  1 78TEXT('WORKSTATION ID')
     A                                  3  2'Deal no.'
     A            SDLNO          6   B  3 11TEXT('DEAL NO.')
     A                                      DSPATR(HI)
     A N11N69                               DSPATR(UL)
     A  70
     AO 11N69                               DSPATR(PC)
     A  11N69                               DSPATR(RI)
     A  69                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  3 19'Action'
     A            SACCD          1   B  3 26TEXT('ACTION CODE')
     A                                      DSPATR(HI)
     A N10N69                               DSPATR(UL)
     A  10N69                               DSPATR(PC RI)
     A  69                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  3 29'Type'
     A            SDTYP          2   O  3 34TEXT('DEAL TYPE')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  3 38'Sub-type'
     A            SDLST          2   B  3 47TEXT('DEAL SUB-TYPE')
     A                                      DSPATR(HI)
     A N13N70                               DSPATR(UL)
     A  13                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  3 51'Deal date'
     A            SDDAT          6   B  3 61TEXT('DEAL DATE')
     A                                      DSPATR(HI)
     A N14N70                               DSPATR(UL)
     A  14                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A N80                              3 68'Bk.Branch'
     A  80                                  DSPATR(ND)
     A*
     A N80        SBRCA          3   B  3 78TEXT('BOOKING BRANCH CODE')
     A                                      DSPATR(HI)
     A N15N70                               DSPATR(UL)
     A  15                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  4  2'Broker'
     A            SBRKC          4   B  4 11TEXT('BROKER CODE')
     A                                      DSPATR(HI)
     A N16N70                               DSPATR(UL)
     A  16                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  4 19'Brokerage'
     A            SBAGE         14   B  4 29TEXT('BROKERAGE AMOUNT')
     A                                      DSPATR(HI)
     A N17N70                               DSPATR(UL)
     A  17                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  4 46'Linked reference number'
     A            SLNKDN         6   B  4 71TEXT('LINKED DEAL NO.')
     A                                      DSPATR(HI)
     A N18N70                               DSPATR(UL)
     A  18                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  5  2'Customer'
     A            SCNUM         10   B  5 11TEXT('CUSTOMER NO.')
     A                                      DSPATR(HI)
     A N19N70                               DSPATR(UL)
     A  19                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  5 23'Value date'
     A            SVDAT          6   B  5 34TEXT('VALUE DATE')
     A                                      DSPATR(HI)
     A N20N70                               DSPATR(UL)
     A  20                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  5 42'Maturity date'
     A            SMDAT          6   B  5 56TEXT('MATURITY DATE')
     A                                      DSPATR(HI)
     A N21N70                               DSPATR(UL)
     A  21                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  5 64'A/c seq.no.'
     A            SCDAS          2   B  5 76TEXT('CUST. DEAL. ACC. SEQ. NO.')
     A                                      DSPATR(HI)
     A N22N70                               DSPATR(UL)
     A  22                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  6  2'Currency'
     A            SUCUCY         3   B  6 11TEXT('CURRENCY')
     A                                      DSPATR(HI)
     A N23N70                               DSPATR(UL)
     A  23                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  6 16'Amount'
     A            SUPAMT        14   B  6 23TEXT('AMOUNT')
     A                                      DSPATR(HI)
     A N24N70                               DSPATR(UL)
     A  24                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  6 38'F/f'
     A            SUFFI          1   B  6 42TEXT('FED. FUNDS IND.')
     A                                      DSPATR(HI)
     A N25N70                               DSPATR(UL)
     A  25                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  6 46'Base currency rate'
     A            SUBCXR        14   B  6 65TEXT('BASE CCY RATE')
     A                                      DSPATR(HI)
     A N26N70                               DSPATR(UL)
     A  26                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  7  2'Book'
     A            SBOKC          2   B  7  7TEXT('BOOK CODE')
     A                                      DSPATR(HI)
     A N27N70                               DSPATR(UL)
     A  27                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A*
     A                                  7 11'Calc.meth.'
     A            SUICBS         1   B  7 22TEXT('INTEREST CALC. BASIS')
     A                                      DSPATR(HI)
     A N28N70                               DSPATR(UL)
     A  28                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  7 25'Taxable'
     A            STAXI          1   B  7 33TEXT('TAXABLE IND.')
     A                                      DSPATR(HI)
     A N29N70                               DSPATR(UL)
     A  29                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  7 36'Rate fix days'
     A            SUFRFD         2   B  7 50TEXT('RATE FIX DAYS')
     A                                      DSPATR(HI)
     A N30N70                               DSPATR(UL)
     A  30                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A N82                              7 54'Profit Centre'
     A  82                                  DSPATR(ND)
     A N82        SPRFC          4   B  7 68TEXT('PROFIT CENTRE')
     A                                      DSPATR(HI)
     A N31N12N70                            DSPATR(UL)
     A  31                                  DSPATR(PC RI)
     A  70
     AO 12                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A N80N81                           8  2'Originating Branch'
     A  81                                  DSPATR(ND)
     A N80N81     SORBR          3   B  8 21TEXT('ORIGINATING BRANCH')
     A                                      DSPATR(HI)
     A N32N70                               DSPATR(UL)
     A  32                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
      *                                              Begin /CDL038/
     A                                  8 25'Class'
     A  90                                  DSPATR(ND)
     A            SCLAS          4A  B  8 31
     A                                      DSPATR(HI)
     A N50N70                               DSPATR(UL)
     A  50                                  DSPATR(PC RI)
     A  70
     AO 90                                  DSPATR(PR)
     A  90                                  DSPATR(ND)
     A                                      OVRDTA
      *                                              End   /CDL038/
     A                                 10  2'Buy/Sell'
     A            SBYSL          1   B 10 12TEXT('BUY/SELL INDICATOR')
     A                                      DSPATR(HI)
     A N33N70                               DSPATR(UL)
     A  33                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A*                                     Begin /CSW097/
     A        83                        8 25'Collateral Agreement Indicator'
     A        83  SCAIN          1   B  8 56TEXT('COLLATERAL AGREEMENT IND.')
     A        83                            DSPATR(HI)
     A N37N70 83                            DSPATR(UL)
     A  37    83                            DSPATR(PC RI)
     A  70    83                            DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A*                                     End   /CSW097/
     A*
     A                                 10 15'Sett. Formula'
     A            SSETF          1   B 10 30TEXT('SETTLEMENT FORMULA')
     A                                      DSPATR(HI)
     A N34N70                               DSPATR(UL)
     A  34                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 12  2'Reference Rate'
     A            SRFRTE         2   B 12 17TEXT('REFERENCE RATE')
     A                                      DSPATR(HI)
     A N35N70                               DSPATR(UL)
     A  35                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 12 21'Contract Rate'
     A            SCRTE         12   B 12 36TEXT('CONTRACT RATE')
     A                                      DSPATR(HI)
     A N36N70                               DSPATR(UL)
     A  36                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
      **Start of Change CAS006
     A  76                             14  2'Risk Value Factor'
     A            SRKVL         12   B 14 20TEXT('RISK VALUE FACTOR')
     A N76                                  DSPATR(ND)
     A  76                                  DSPATR(HI)
     A N48N70 76                            DSPATR(UL)
     A  48 76                               DSPATR(PC RI)
     A  70
     AON76                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A  76                             14 34'Sign'
     A            SRKSN          1   B 14 39TEXT('SIGN')
     A N76                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N49N70 76                            DSPATR(UL)
     A  49 76                               DSPATR(PC RI)
     A  70
     AON76                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
      **End of Change CAS006
      *                                              Begin /CIR005/
      **Start of Change CAS006
     A**********                       14  2'Currencies for Rate'
     A                                 16  2'Currencies for Rate'
      **End of Change CAS006
     A N67                                  DSPATR(ND)
      **Start of Change CAS006
     A**********                       14 22'Fixing Convention:'
     A                                 16 22'Fixing Convention:'
      **End of Change CAS006
     A N67                                  DSPATR(ND)
      **Start of Change CAS006
     A**********  SCYR1          3   B 14 41TEXT('Currency Rate 1')
     A            SCYR1          3   B 16 41TEXT('Currency Rate 1')
      **End of Change CAS006
     A N67                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N38N70                               DSPATR(UL)
     A  38                                  DSPATR(PC RI)
      **Start of change 195822
     A**70******                            DSPATR(PR)
     A N67
     AO 70                                  DSPATR(PR)
      **End of change 195822
     A                                      DSPATR(MDT)
     A                                      OVRDTA
      **Start of Change CAS006
     A**********  SCYR2          3   B 14 45TEXT('Currency Rate 2')
     A            SCYR2          3   B 16 45TEXT('Currency Rate 2')
      **End of Change CAS006
     A N67                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N39N70                               DSPATR(UL)
     A  39                                  DSPATR(PC RI)
      **Start of change 195822
     A**70******                            DSPATR(PR)
     A N67
     AO 70                                  DSPATR(PR)
      **End of change 195822
     A                                      DSPATR(MDT)
     A                                      OVRDTA
      **Start of Change CAS006
     A**********  SCYR3          3   B 14 49TEXT('Currency Rate 3')
     A            SCYR3          3   B 16 49TEXT('Currency Rate 3')
      **End of Change CAS006
     A N67                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N40N70                               DSPATR(UL)
     A  40                                  DSPATR(PC RI)
      **Start of change 195822
     A**70******                            DSPATR(PR)
     A N67
     AO 70                                  DSPATR(PR)
      **End of change 195822
     A                                      DSPATR(MDT)
     A                                      OVRDTA
      **Start of Change CAS006
     A**********  SCYR4          3   B 14 53TEXT('Currency Rate 4')
     A            SCYR4          3   B 16 53TEXT('Currency Rate 4')
      **End of Change CAS006
     A N67                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N41N70                               DSPATR(UL)
     A  41                                  DSPATR(PC RI)
      **Start of change 195822
     A**70******                            DSPATR(PR)
     A N67
     AO 70                                  DSPATR(PR)
      **End of change 195822
     A                                      DSPATR(MDT)
     A                                      OVRDTA
      **Start of Change CAS006
     A**********  SCYR5          3   B 14 57TEXT('Currency Rate 5')
     A            SCYR5          3   B 16 57TEXT('Currency Rate 5')
      **End of Change CAS006
     A N67                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N42N70                               DSPATR(UL)
     A  42                                  DSPATR(PC RI)
      **Start of change 195822
     A**70******                            DSPATR(PR)
     A N67
     AO 70                                  DSPATR(PR)
      **End of change 195822
     A                                      DSPATR(MDT)
     A                                      OVRDTA
      **Start of Change CAS006
     A**********  SCYR6          3   B 14 61TEXT('Currency Rate 6')
     A            SCYR6          3   B 16 61TEXT('Currency Rate 6')
      **End of Change CAS006
     A N67                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N43N70                               DSPATR(UL)
     A  43                                  DSPATR(PC RI)
      **Start of change 195822
     A**70******                            DSPATR(PR)
     A N67
     AO 70                                  DSPATR(PR)
      **End of change 195822
     A                                      DSPATR(MDT)
     A                                      OVRDTA
      **Start of Change CAS006
     A**********  SCYR7          3   B 14 65TEXT('Currency Rate 7')
     A            SCYR7          3   B 16 65TEXT('Currency Rate 7')
      **End of Change CAS006
     A N67                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N44N70                               DSPATR(UL)
     A  44                                  DSPATR(PC RI)
      **Start of change 195822
     A**70******                            DSPATR(PR)
     A N67
     AO 70                                  DSPATR(PR)
      **End of change 195822
     A                                      DSPATR(MDT)
     A                                      OVRDTA
      **Start of Change CAS006
     A**********  SCYR8          3   B 14 69TEXT('Currency Rate 8')
     A            SCYR8          3   B 16 69TEXT('Currency Rate 8')
      **End of Change CAS006
     A N67                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N45N70                               DSPATR(UL)
     A  45                                  DSPATR(PC RI)
      **Start of change 195822
     A**70******                            DSPATR(PR)
     A N67
     AO 70                                  DSPATR(PR)
      **End of change 195822
     A                                      DSPATR(MDT)
     A                                      OVRDTA
      **Start of Change CAS006
     A**********  SCYR9          3   B 14 73TEXT('Currency Rate 9')
     A            SCYR9          3   B 16 73TEXT('Currency Rate 9')
      **End of Change CAS006
     A N67                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N46N70                               DSPATR(UL)
     A  46                                  DSPATR(PC RI)
      **Start of change 195822
     A**70******                            DSPATR(PR)
     A N67
     AO 70                                  DSPATR(PR)
      **End of change 195822
     A                                      DSPATR(MDT)
     A                                      OVRDTA
      **Start of Change CAS006
     A**********  SCYR0          3   B 14 77TEXT('Currency Rate 10')
     A            SCYR0          3   B 16 77TEXT('Currency Rate 10')
      **End of Change CAS006
     A N67                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N47N70                               DSPATR(UL)
     A  47                                  DSPATR(PC RI)
      **Start of change 195822
     A**70******                            DSPATR(PR)
     A N67
     AO 70                                  DSPATR(PR)
      **End of change 195822
     A                                      DSPATR(MDT)
     A                                      OVRDTA
      *                                              End   /CIR005/
     A**Start of change 186458
      **Start of Change CAS006
     A**********  SCPTX         17   O 16 02TEXT('Counterparty Narrative')
     A            SCPTX         17   O 18 02TEXT('Counterparty Narrative')
      **End of Change CAS006
     A                                      OVRATR
     A                                      OVRDTA
     A N66                                  DSPATR(ND)
      **Start of Change CAS006
     A**********  SCPST         15   O 16 20TEXT('Counterparty Status')
     A            SCPST         15   O 18 20TEXT('Counterparty Status')
      **End of Change CAS006
     A                                      OVRATR
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N66                                  DSPATR(ND)
      **Start of Change CAS006
     A**********  SBKTX         11   O 16 42TEXT('Broker Narrative')
     A            SBKTX         11   O 18 42TEXT('Broker Narrative')
      **End of Change CAS006
     A                                      OVRATR
     A                                      OVRDTA
     A N66                                  DSPATR(ND)
      **Start of Change CAS006
     A**********  SBKST         15   O 16 54TEXT('Broker Status')
     A            SBKST         15   O 18 54TEXT('Broker Status')
      **End of Change CAS006
     A                                      OVRATR
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N66                                  DSPATR(ND)
     A**End of change 186458
     A*/COPY WNCPYSRC,IRH00001
     A**************************************************************************
     A* -------------------------------------------------------------------*
     A* * MAIN SCREEN FOOTER                                               *
     A* -------------------------------------------------------------------*
     A*
     A          R IRFRAF1                   TEXT('SCREEN FOOTER')
     A                                      CA03
     A                                      CF12
     A                                      CF07
     A                                      CF08
     A                                      CF10
     A                                      CF14
     A                                      CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A*
     A            DDFOTX        40A    23  2OVRDTA
     A                                      DSPATR(HI)
     A            DDSTS         24     23 45OVRDTA
     A  79                                  DSPATR(RI)
     A N79                                  DSPATR(ND)
     A*
     A                                 24  2'F3=Exit F12=Cancel'
     A                                 24 22'F8=Next'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 31'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 40'F14=Settlements'
     A N74                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 57'F15=Select'
     A N75                                  DSPATR(ND)
     A                                      OVRATR
