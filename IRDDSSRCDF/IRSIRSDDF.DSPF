      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IR Single currency IRS maintenance')             *
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS Module                                       *
     A*                                                               *
     A*  IRSIRSDDF - SCREEN FORMATS FOR SINGLE CCY IRS MAINTENANCE    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. CLE025             Date 01Mar04               *
      *                 CAS005             Date 16Dec02               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 209167             Date 23Sep02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 186458             Date 27Nov00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSW097             Date 02Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP003  *CREATE    Date 27Apr98               *
     A*                                                               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CLE025 - Credit Line                                         *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  209167 - Change condition for protect of Profit Center field.*
      *  CAS001 - Net Present Value (NPV) Accounting.                 *
      *  186458 - Display Confirmation Matching status.  Patterned    *
      *           after fix 179380.                                   *
     A*  CSW097 - MT34*, MT36* message generation.                    *
     A*  CAP003 - Conversion of transaction inputs into modular       *
     A*           structure to use as APIs.                           *
     A*                                                               *
     A*****************************************************************
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A**************************************************************************
     A*
     A*  FR DEAL TYPE - INSERT/AMEND SCREEN (B)
     A*
     A*                                                                   S01203
     A*
     A*  ALL DEAL TYPES - INITIAL SCREEN (A)
     A*
     A** INITIAL SCREEN ALL DEAL TYPES
     A*                                                                   S01203
     A*
     A* -------------------------------------------------------------------*
     A* * MESSAGE SUBFILE FORMAT                                           *
     A* -------------------------------------------------------------------*
     A*
     A          R IRSIRSS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MESSAGE SUBFILE CONTROL                                          *
     A* -------------------------------------------------------------------*
     A*
     A          R IRSIRSS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CA03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A                                      SFLCTL(IRSIRSS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A*
     A*
     A* -------------------------------------------------------------------*
     A*   MAIN DISPLAY SCREEN                                              *
     A* -------------------------------------------------------------------*
     A*
     A          R IRSIRSD1
     A                                      CA03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A*
     A*                                     Begin /CSW097/
     A*                                     End   /CSW097/
     A*                                     Begin /CSW097/
     A*                                     End   /CSW097/
     A** Start of change CAS001
     A** End of change CAS001
     A*
     A                                  1  6'IRSIRSD1'
     A                                  1 21'SINGLE CURRENCY INTEREST +
     A                                      RATE SWAP'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            STIME          6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  1 74'WID'
     A            SWSID          3   O  1 78TEXT('WORKSTATION ID')
     A                                  3  2'Deal no.'
     A            SDLNO          6   B  3 11TEXT('DEAL NO.')
     A                                      DSPATR(HI)
     A N11N69                               DSPATR(UL)
     A  70
     AO 11N69                               DSPATR(PC)
     A  11N69                               DSPATR(RI)
     A  69                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  3 19'Action'
     A            SACCD          1   B  3 26TEXT('ACTION CODE')
     A                                      DSPATR(HI)
     A N10N69                               DSPATR(UL)
     A  10N69                               DSPATR(PC RI)
     A  69                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  3 29'Type'
     A            SDTYP          2   O  3 34TEXT('DEAL TYPE')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  3 38'Sub-type'
     A            SDLST          2   B  3 47TEXT('DEAL SUB-TYPE')
     A                                      DSPATR(HI)
     A N13N70                               DSPATR(UL)
     A  13                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  3 51'Deal date'
     A            SDDAT          6   B  3 61TEXT('DEAL DATE')
     A                                      DSPATR(HI)
     A N14N70                               DSPATR(UL)
     A  14                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A N80                              3 68'Bk.Branch'
     A  80                                  DSPATR(ND)
     A*
     A N80        SBRCA          3   B  3 78TEXT('BOOKING BRANCH CODE')
     A                                      DSPATR(HI)
     A N15N70                               DSPATR(UL)
     A  15                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  4  2'Broker'
     A            SBRKC          4   B  4 11TEXT('BROKER CODE')
     A                                      DSPATR(HI)
     A N16N70                               DSPATR(UL)
     A  16                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  4 19'Brokerage'
     A            SBAGE         14   B  4 29TEXT('BROKERAGE AMOUNT')
     A                                      DSPATR(HI)
     A N17N70                               DSPATR(UL)
     A  17                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  4 46'Linked reference number'
     A            SLNKDN         6   B  4 71TEXT('LINKED DEAL NO.')
     A                                      DSPATR(HI)
     A N18N70                               DSPATR(UL)
     A  18                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  5  2'Customer'
     A            SCNUM         10   B  5 11TEXT('CUSTOMER NO.')
     A                                      DSPATR(HI)
     A N19N70                               DSPATR(UL)
     A  19                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  5 23'Value date'
     A            SVDAT          6   B  5 34TEXT('VALUE DATE')
     A                                      DSPATR(HI)
     A N20N70                               DSPATR(UL)
     A  20                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  5 42'Maturity date'
     A            SMDAT          6   B  5 56TEXT('MATURITY DATE')
     A                                      DSPATR(HI)
     A N21N70                               DSPATR(UL)
     A  21                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  5 64'A/c seq.no.'
     A            SCDAS          2   B  5 76TEXT('CUST. DEAL. ACC. SEQ. NO.')
     A                                      DSPATR(HI)
     A N22N70                               DSPATR(UL)
     A  22                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  6  2'Currency'
     A            SUCUCY         3   B  6 11TEXT('CURRENCY')
     A                                      DSPATR(HI)
     A N23N70                               DSPATR(UL)
     A  23                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  6 16'Amount'
     A            SUPAMT        14   B  6 23TEXT('AMOUNT')
     A                                      DSPATR(HI)
     A N24N70                               DSPATR(UL)
     A  24                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  6 38'F/f'
     A            SUFFI          1   B  6 42TEXT('FED. FUNDS IND.')
     A                                      DSPATR(HI)
     A N25N70                               DSPATR(UL)
     A  25                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  6 46'Base currency rate'
     A            SUBCXR        14   B  6 65TEXT('BASE CCY RATE')
     A                                      DSPATR(HI)
     A N26N70                               DSPATR(UL)
     A  26                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  7  2'Book'
     A            SBOKC          2   B  7  7TEXT('BOOK CODE')
     A                                      DSPATR(HI)
     A N27N70                               DSPATR(UL)
     A  27                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A** Start of change CAS001
     A**Start of change CAS005
     A**86******                        8  2'Yield Curve'                                     CAS005
     A**86******  SOYLC          5   B  8 14TEXT('Yield Curve')                               CAS005
     A**********                            DSPATR(HI)                                        CAS005
     A*N56N70***                            DSPATR(UL)                                        CAS005
     A**56******                            DSPATR(PC RI)                                     CAS005
     A**70******                            DSPATR(PR)                                        CAS005
     A**********                            DSPATR(MDT)                                       CAS005
     A**********                            OVRDTA                                            CAS005
     A**End of change CAS005
     A** End of change CAS001
     A*
     A                                  7 11'Taxable'
     A            STAXI          1   B  7 19TEXT('TAXABLE IND.')
     A                                      DSPATR(HI)
     A N28N70                               DSPATR(UL)
     A  28                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                  7 23'Profit Centre'
     A  82                                  DSPATR(ND)
     A                                      OVRATR
     A            SPRFC          4   B  7 37TEXT('PROFIT CENTRE')
     A  82                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N29N12N70                            DSPATR(UL)
     A  29                                  DSPATR(PC RI)
     A  70
     A**Start of change 209167
     A**82******
     AO 82
     A**End of change 209167
     AO 12                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A*                                     Begin /CSW097/
     A*N80N81********                   7 43'Originating Branch'
     A*N80N81*****SORBR          3   B  7 62TEXT('ORIGINATING BRANCH')
     A N80N81                           7 43'Orig. Branch'
     A N80N81     SORBR          3   B  7 56TEXT('ORIGINATING BRANCH')
     A*                                     End   /CSW097/
     A                                      DSPATR(HI)
     A N30N70                               DSPATR(UL)
     A  30                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A*                                     Begin /CSW097/
     A        83                        7 60'Colltrl Agrmnt Ind'
     A        83  SCAIN          1   B  7 80TEXT('COLLATERAL AGREEMENT IND.')
     A        83                            DSPATR(HI)
     A N55N70 83                            DSPATR(UL)
     A  55    83                            DSPATR(PC RI)
     A  70    83                            DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A*                                     End   /CSW097/
      **Start of change CLE025
     A  88                              8  2'Facility Reference'
     A  88        SFCCU         10   B  8 21TEXT('FACILITY CUSTOMER')
     A                                      DSPATR(HI)
     A N60N70                               DSPATR(UL)
     A  60                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A  88        SFACT          3   B  8 32TEXT('FACILITY TYPE')
     A                                      DSPATR(HI)
     A N61N70                               DSPATR(UL)
     A  61                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A  88        SFCNO          2   B  8 36TEXT('FACILITY SEQUENCE')
     A                                      DSPATR(HI)
     A N62N70                               DSPATR(UL)
     A  62                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
      *                                              Begin /CDL038/
     A                                  8 40'Class'
     A  08                                  DSPATR(ND)
     A            SCLAS          4A  B  8 46
     A                                      DSPATR(HI)
     A N09N70                               DSPATR(UL)
     A  09                                  DSPATR(PC RI)
     A  70
     AO 08                                  DSPATR(PR)
     A  08                                  DSPATR(ND)
     A                                      OVRDTA
      *                                              End   /CDL038/
      **End of change CLE025
     A                                  9  1'Our details'
     A                                      DSPATR(UL)
     A                                 10  2'Base rate'
     A            SUBRTT         2   B 10 12TEXT('OUR BASE RATE CODE')
     A                                      DSPATR(HI)
     A N31N70                               DSPATR(UL)
     A  31                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 10 16'Spread/Rate'
     A            SURTSP        12   B 10 28TEXT('OUR SPREAD/RATE')
     A                                      DSPATR(HI)
     A N32N70                               DSPATR(UL)
     A  32                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 10 42'Spread ind'
     A            SUSPIN         1   B 10 54TEXT('OUR SPREAD IND.')
     A                                      DSPATR(HI)
     A N33N70                               DSPATR(UL)
     A  33                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 10 57'Rate fix days'
     A            SUFRFD         2   B 10 71TEXT('OUR RATE FIX DAYS')
     A                                      DSPATR(HI)
     A N34N70                               DSPATR(UL)
     A  34                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 11  2'Rate change  Date'
     A            SUNICD         6   B 11 20TEXT('OUR RATE CHANGE DATE')
     A                                      DSPATR(HI)
     A N35N70                               DSPATR(UL)
     A  35                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 11 28'Freq.'
     A            SUICFR         1   B 11 34TEXT('OUR RATE CHANGE FREQUENCY')
     A                                      DSPATR(HI)
     A N36N70                               DSPATR(UL)
     A  36                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 11 37'Day'
     A            SUICFD         2   B 11 41TEXT('OUR RATE CHANGE DAY IN MTH.')
     A                                      DSPATR(HI)
     A N37N70                               DSPATR(UL)
     A  37                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 11 45'Short term rate'
     A            SUEINR        12   B 11 61TEXT('OUR SHORT TERM RATE')
     A                                      DSPATR(HI)
     A N38N70                               DSPATR(UL)
     A  38                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 12  2'Int. payment Date'
     A            SUNIPD         6   B 12 20TEXT('OUR INT. PAYMENT DATE')
     A                                      DSPATR(HI)
     A N39N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  39                                  DSPATR(PC RI)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 12 28'Freq.'
     A            SUIPFR         1   B 12 34TEXT('OUR INT. PAYMENT FREQUENCY')
     A                                      DSPATR(HI)
     A N40N70                               DSPATR(UL)
     A  40                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 12 37'Day'
     A            SUIPFD         2   B 12 41TEXT('OUR INT. PAYMNT. DAY IN MTH.')
     A                                      DSPATR(HI)
     A N41N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  41                                  DSPATR(PC RI)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 12 45'Calc.meth.'
     A            SUICBS         1   B 12 56TEXT('INTEREST CALC. BASIS')
     A                                      DSPATR(HI)
     A N42N70                               DSPATR(UL)
     A  42                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A**Start of change CAS005
     A  86                             12 59'Yield Curve'
     A  86        SOYLC          5   B 12 72TEXT('Yield Curve')
     A                                      DSPATR(HI)
     A N56N70                               DSPATR(UL)
     A  56                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A  87                             13 02'Fwd Yield Curve'
     A  87        SOFYC          5   B 13 18TEXT('Our Fwd Yield Curve')
     A                                      DSPATR(HI)
     A N58N70                               DSPATR(UL)
     A  58                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A**End of change CAS005
      *
     A                                 14  1'Their details'
     A                                      DSPATR(UL)
     A                                 15  2'Base rate'
     A            STBRTT         2   B 15 12TEXT('THEIR BASE RATE CODE')
     A                                      DSPATR(HI)
     A N43N70                               DSPATR(UL)
     A  43                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 15 16'Spread/Rate'
     A            STRTSP        12   B 15 28TEXT('THEIR SPREAD/RATE')
     A                                      DSPATR(HI)
     A N44N70                               DSPATR(UL)
     A  44                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 15 42'Spread ind'
     A            STSPIN         1   B 15 54TEXT('THEIR SPREAD IND.')
     A                                      DSPATR(HI)
     A N45N70                               DSPATR(UL)
     A  45                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 15 57'Rate fix days'
     A            STFRFD         2   B 15 71TEXT('THEIR RATE FIX DAYS')
     A                                      DSPATR(HI)
     A N46N70                               DSPATR(UL)
     A  46                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 16  2'Rate change  Date'
     A            STNICD         6   B 16 20TEXT('THEIR RATE CHANGE DATE')
     A                                      DSPATR(HI)
     A N47N70                               DSPATR(UL)
     A  47                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 16 28'Freq.'
     A            STICFR         1   B 16 34TEXT('THEIR RATE CHANGE FREQ.')
     A                                      DSPATR(HI)
     A N48N70                               DSPATR(UL)
     A  48                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 16 37'Day'
     A            STICFD         2   B 16 41TEXT('THEIR RATE CHGE DAY IN MTH.')
     A                                      DSPATR(HI)
     A N49N70                               DSPATR(UL)
     A  49                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 16 45'Short term rate'
     A            STEINR        12   B 16 61TEXT('THEIR SHORT TERM RATE')
     A                                      DSPATR(HI)
     A N50N70                               DSPATR(UL)
     A  50                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 17  2'Int. payment Date'
     A            STNIPD         6   B 17 20TEXT('THEIR INT. PAYMENT DATE')
     A                                      DSPATR(HI)
     A N51N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  51                                  DSPATR(PC RI)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 17 28'Freq.'
     A            STIPFR         1   B 17 34TEXT('THEIR INT. PAYMENT FREQUENCY')
     A                                      DSPATR(HI)
     A N52N70                               DSPATR(UL)
     A  52                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 17 37'Day'
     A            STIPFD         2   B 17 41TEXT('THEIR INT. PAY. DAY IN MTH.')
     A                                      DSPATR(HI)
     A N53N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  53                                  DSPATR(PC RI)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A                                 17 45'Calc.meth.'
     A            STICBS         1   B 17 56TEXT('INTEREST CALC. BASIS')
     A                                      DSPATR(HI)
     A N54N70                               DSPATR(UL)
     A  54                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A**Start of change CAS005
     A  87                             17 59'Yield Curve'
     A  87        STYLC          5   B 17 72TEXT('Yield Curve')
     A                                      DSPATR(HI)
     A N57N70                               DSPATR(UL)
     A  57                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A  87                             18  2'Fwd Yield Curve'
     A  87        STFYC          5   B 18 18TEXT('Their Fwd Yield Curve')
     A                                      DSPATR(HI)
     A N59N70                               DSPATR(UL)
     A  59                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                      OVRDTA
     A**End of change CAS005
     A**Start of change 186458
     A**Start of change CAS005
     A**********  SCPTX         17   O 18 02TEXT('Counterparty Narrative')                    CAS005
     A            SCPTX         17   O 19 02TEXT('Counterparty Narrative')
     A**End of change CAS005
     A                                      OVRATR
     A                                      OVRDTA
     A N67                                  DSPATR(ND)
     A**Start of change CAS005
     A**********  SCPST         15   O 18 20TEXT('Counterparty Status')                       CAS005
     A            SCPST         15   O 19 20TEXT('Counterparty Status')
     A**End of change CAS005
     A                                      OVRATR
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N67                                  DSPATR(ND)
     A**Start of change CAS005
     A**********  SBKTX         11   O 18 42TEXT('Broker Narrative')                          CAS005
     A            SBKTX         11   O 19 42TEXT('Broker Narrative')
     A**End of change CAS005
     A                                      OVRATR
     A                                      OVRDTA
     A N67                                  DSPATR(ND)
     A**Start of change CAS005
     A**********  SBKST         15   O 18 54TEXT('Broker Status')                             CAS005
     A            SBKST         15   O 19 54TEXT('Broker Status')
     A**End of change CAS005
     A                                      OVRATR
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N67                                  DSPATR(ND)
     A**End of change 186458
     A*/COPY WNCPYSRC,IRH00002
      *
     A**************************************************************************
     A* -------------------------------------------------------------------*
     A* * MAIN SCREEN FOOTER                                               *
     A* -------------------------------------------------------------------*
     A*
     A          R IRSIRSF1                  TEXT('SCREEN FOOTER')
     A                                      CA03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A*
     A            DDFOTX        40A    23  2OVRDTA
     A                                      DSPATR(HI)
     A            DDSTS         24     23 45OVRDTA
     A  79                                  DSPATR(RI)
     A N79                                  DSPATR(ND)
     A*
     A                                 24  2'F3=Exit F12=Cancel'
     A                                 24 22'F8=Next'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 31'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 40'F14=Settlements'
     A N74                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 57'F15=Select'
     A N75                                  DSPATR(ND)
     A                                      OVRATR
