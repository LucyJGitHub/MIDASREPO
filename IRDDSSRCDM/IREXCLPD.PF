      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas IR Italian Reporting Customer detail file')      *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting - Italy                           *
      *                                                               *
      *  PF/IREXCLPD - Italian Reporting Customer detail file         *
      *                (Anagrafica)                                   *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 21Dec20               *
      *  Prev Amend No. MMI032             Date 02SEP98               *
      *                 UIT021 * amend *   Date 15jul98               *
      *                 UIT021             Date 07feb97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - Upgrade UCI Italian Returns upgrade                 *
      *  MMI032 - Recompile to incorporate FICS BOI data              *
      *           dic 3.0 changes.                                    *
      *  UIT021 - BOI European Reporting data dictionary 2.6.3 changes*
      *  UIT021 - BOI European Reporting                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R IREXCLD0                  TEXT('(C) Midas-Kapiti +
     A                                      International Ltd. 1997')
      *
     A            IDRTYP         3S 0       TEXT('Record Type')
     A                                      COLHDG('Rec.' 'Type')
      *
     A            IDAUTR        20A         TEXT('Audit Trail Data')
     A                                      COLHDG('Audit' 'Trail' 'Data')
      *
     A            IDSQCT         6S 0       TEXT('Sequential Counter')
     A                                      COLHDG('Sequential' 'Counter')
      *
     A            IDUCTR         3S 0       TEXT('UIC Country Cd Counterparty')
     A                                      COLHDG('UIC Ctry Cd' 'Counterparty')
      *
     A            IDECSG         3S 0       TEXT('Economical Subgroup Counterpa-
     A                                      rty')
     A                                      COLHDG('Econ.' 'Sbgrp' 'Ctrpy')
      *
     A            IDECBC         3S 0       TEXT('Economical Branch Class.')
     A                                      COLHDG('Econ.' 'Brch' 'Class.')
      *
     A            IDPRCT         5S 0       TEXT('Province of Counterparty')
     A                                      COLHDG('Prov.' 'Of' 'Ctrpy')
      *
     A            IDICCR         5S 0       TEXT('Internal Code CR Zone')
     A                                      COLHDG('Internal Code' 'CR Zone')
      *
     A*********   IDOCCR         9S 0       TEXT('Official Code CR Zone')
     A            IDOCCR         6A         TEXT('Official Code CR Zone')
     A                                      COLHDG('Official Code' 'CR Zone')
      *
     A            IDCUST        16A         TEXT('Client Internal Number')
     A                                      COLHDG('Client' 'Internal' 'Number')
      *
     A            IDGUAR        16A         TEXT('Guarantor Internal Number')
     A                                      COLHDG('Guarantor' 'Internal' 'Numb-
     A                                      er')
      *
     A            IDCRNM        60A         TEXT('Client CR Name')
     A                                      COLHDG('Client CR Name')
      *
     A            IDFCCR        16A         TEXT('Fiscal cd Ctrpy/Issuer CR')
     A                                      COLHDG('Fiscal Code' 'Counterparty/-
     A                                      ' 'Issues CR')
      *
      ** Begin MMI032
     A************IDHOLC        30A         TEXT('Head Office Client Location')
     A            IDHOLC         6A         TEXT('Head Office Client Location')
      ** End MMI032
     A                                      COLHDG('Head Office' 'Client' 'Loca-
     A                                      tion')
      *
     A            IDHLCD         6S 0       TEXT('CAB Code Head Office Location-
     A                                      ')
     A                                      COLHDG('CAB Code' 'Head Office' 'Lo-
     A                                      cation')
      *
     A            IDCUAD        40A         TEXT('Client Address')
     A                                      COLHDG('Client' 'Address')
      *
     A            IDCUCT        40A         TEXT('Client City')
     A                                      COLHDG('Client' 'City')
      *
     A            IDCCCN         7A         TEXT('CCIAA Registered  Client Nr')
     A                                      COLHDG('CCIAA' 'Registered' 'Clien-
     A                                      t Nr')
      *
     A*********   IDCCCL        15A         TEXT('CCIAA H.O. Client Location')
     A            IDCCCL        30A         TEXT('CCIAA H.O. Client Location')
     A                                      COLHDG('CCIAA' 'Head Office' 'Clie-
     A                                      nt Location')
      *
     A********    IDCUCR         9S 0       TEXT('CR Customer Code')
     A            IDCUCR        13S 0       TEXT('CR Customer Code')
     A                                      COLHDG('CR' 'Customer' 'Code')
      *
     A            IDCNAM        35A         TEXT('Customer Internal Name')
     A                                      COLHDG('Customer' 'Internal' 'Name')
      *
     A            IDCUDB         1S 0       TEXT('Doubful Code of Customer')
     A                                      COLHDG('Doubtful' 'Code of' 'Custo-
     A                                      mer')
      *
      ** Begin MMI032
     A            IDNOMG         7S 0       TEXT('Number of Message')
     A                                      COLHDG('Number of' 'Message')
      *
     A            IDINDM         1A         TEXT('Indicator of Doc Message')
     A                                      COLHDG('Indicator of' 'Doc message')
      *
     A            IDPDOC         2S 0       TEXT('Personal Data Operation Code')
     A                                      COLHDG('Personal Data' 'Oper Code')
      *
     A            IDRONC         1A         TEXT('Request of New Code')
     A                                      COLHDG('Request of' 'New Code')
      *
     A            IDINDC         1A         TEXT('Indicator of Doc CCIAA')
     A                                      COLHDG('Indicator of' 'Doc CCIAA')
      *
     A            IDNMRD         1A         TEXT('Ind of Notice R/O Max Delay')
     A                                      COLHDG('Ind of Notice' 'Max Delay')
      *
     A            IDIDFC         1A         TEXT('Ind of Doc Fiscal Code')
     A                                      COLHDG('Ind of Doc' 'Fiscal Code')
      *
     A            IDRCFN         2A         TEXT('Reqst Code of First Notif')
     A                                      COLHDG('Reqst Code' 'First Notif')
      *
     A            IDRDFN         8S 0       TEXT('Reqst Date of First Notif')
     A                                      COLHDG('Reqst Date' 'First Notif')
      *
     A            IDLVOR         1A         TEXT('Level of Response')
     A                                      COLHDG('Level of' 'Response')
      *
     A            IDPTCG         1A         TEXT('Position toward Credit Group')
     A                                      COLHDG('Pos towards' 'Credit Group')
      *
     A            IDQOII         1A         TEXT('Qualification of Id of Indv')
     A                                      COLHDG('Qual of Id' 'of individual')
      *
     A            IDIOCR         1A         TEXT('Indicator of Ceased Register')
     A                                      COLHDG('Indicator of' 'Ceased Reg')
      *
     A            IDHOLO         5A         TEXT('Head Office Location')
     A                                      COLHDG('Head Office' 'Location')
      *
     A            IDCOMT         2A         TEXT('Code of Mean of Trans')
     A                                      COLHDG('Code of Means' 'of Trans')
      ** End MMI032
      *
     A            IDCRMO         1S 0       TEXT('CR Modification Code')
     A                                      COLHDG('CR' 'Modif.' 'Code')
      *
     A            IDCRTP         1S 0       TEXT('CR Client Type')
     A                                      COLHDG('CR' 'Client' 'Type')
      *
     A            IDRFDT         8S 0       TEXT('Reference Date')
     A                                      COLHDG('Reference' 'Date')
      *
     A            IDBRAC         3S 0       TEXT('Branch Activity Counterparty')
     A                                      COLHDG('Branch' 'Activ.' 'Ctrpy')
      *
      ** Begin MMI032
     A************IDAGOP         4S 0       TEXT('Aggregated UIC Operation Cd')
     A************                          COLHDG('Aggr.' 'UIC' 'Oper. Cd')
      ** End MMI032
      *
     A            IDECSE         3S 0       TEXT('Economical Sector')
     A                                      COLHDG('Econ.' 'Sector')
      *
     A            IDEND          1A         TEXT('End position')
     A                                      COLHDG('End' 'Position')
      *
