      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas IR Invalid records from IRFRAVAL')
      *****************************************************************
      *                                                               *
      *  Midas - IR ILE Module                                        *
      *                                                               *
      *  PF/IRIFRAPD - Invalid FRA Records                            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CAS006             Date 21Jan03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP056             Date 13Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CIR005             Date 21Jan00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSW097             Date 02Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP003  *CREATE    Date 08Jun98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAS006 - Hedge Accounting Phase B                            *
      *  CAP056 - Automatic Authorisation of Interface deals          *
      *  CIR005 - FRA/IRS Business Day Conventions                    *
      *  CAP003 - Conversion of IR inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R IRIFRAD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            SFOTRANID     20A         TEXT('Front Office Transaction Id')
     A            SFOASOCID     20A         TEXT('Front Office Assoc Deal ID')
     A            SRPRLOCN       1A         TEXT('Deal Repair Location')
      ** Action code
     A            SACCD     R               REFFLD(ACTO)
      *
      ** Deal number
     A            SDLNO     R     A         REFFLD(DEAL)
      *
      ** Deal type
     A            SDTYP     R               REFFLD(DTPE)
      *
      ** Deal subtype
     A            SDLST     R               REFFLD(DLST)
      *
      ** Deal date
     A            SDDAT     R     A         REFFLD(CHGD)
     A                                      TEXT('Deal Date')
     A                                      COLHDG('Deal' 'Date')
      ** Branch code
     A            SBRCA     R               REFFLD(BRCA)
     A                                      TEXT('Branch code')
      *
      ** Broker code
     A            SBRKC     R               REFFLD(BKCD)
      *
      ** Brokerage
     A            SBAGE     R   14A         REFFLD(BRKG)
     A                                      TEXT('Brokerage')
      *
      ** Linked reference number
     A            SLNKDN    R     A         REFFLD(LNKDN)
     A                                      TEXT('Linked ref no')
      *
      ** Customer
     A            SCNUM     R               REFFLD(CSNM)
     A                                      TEXT('Customer Shortname or number')
      *
      ** Value date
     A            SVDAT     R     A         REFFLD(CHGD)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value' 'Date')
      *
      ** Maturity date
     A            SMDAT     R     A         REFFLD(CHGD)
     A                                      TEXT('Maturity Date')
     A                                      COLHDG('Maturity' 'Date')
      *
      ** A/C Sequence number
     A            SCDAS     R     A         REFFLD(ACSQ)
      *
      ** Currency
     A            SUCUCY    R               REFFLD(CCY)
      *
      ** Amount
     A            SUPAMT    R               REFFLD(AM14)
     A                                      TEXT('Amount')
      *
      ** Federal funds
     A            SUFFI     R               REFFLD(FEDF)
      *
      ** Base currency rate
     A            SUBCXR        14A
     A                                      TEXT('Base currency rate')
      *
      ** Book code
     A            SBOKC     R               REFFLD(BOKC)
      *
      ** Interest Calculation Basis
     A            SUICBS         1A
     A                                      TEXT('Interest Calculation Basis')
      *
      ** Taxable ind
     A            STAXI     R               REFFLD(MTAX)
      *
      ** Rate Fix Days
     A            SUFRFD         2A
     A                                      TEXT('Rate Fix Days')
      *
      ** Profit centre
     A            SPRFC     R               REFFLD(PRFC)
      *
      ** Originating branch
     A            SORBR     R               REFFLD(ORBR)
      *
      ** Buy/Sell Indicator
     A            SBYSL          1A
     A                                      TEXT('Buy/Sell Indicator')
      *
      ** Settlement Formula
     A            SSETF          1A
     A                                      TEXT('Settlement Formula')
      *
      ** Reference Rate
     A            SRFRTE         2A
     A                                      TEXT('Reference Rate')
      *
      ** Contract Rate
     A            SCRTE         12A
     A                                      TEXT('Contract Rate')
      *
      ** Timestamp
     A            STMESTMP  R               REFFLD(TIMESTMP)
      **
      ** Start of CSW097
      ** Collateral Agreement Ind
     A            SCAIN          1A
     A                                      TEXT('Collateral Agreement Ind')
      ** End of CSW097
      *                                              Begin /CIR005/
     A            SCYR1     R               REFFLD(CCYD)
     A                                      TEXT('Currency 1 for Rate +
     A                                            Fixing Convention')
     A                                      COLHDG('Currency 1 for' +
     A                                             'Rate Fixing' +
     A                                             '-Convention')
     A            SCYR2     R               REFFLD(CCYD)
     A                                      TEXT('Currency 2 for Rate +
     A                                            Fixing Convention')
     A                                      COLHDG('Currency 2 for' +
     A                                             'Rate Fixing' +
     A                                             '-Convention')
     A            SCYR3     R               REFFLD(CCYD)
     A                                      TEXT('Currency 3 for Rate +
     A                                            Fixing Convention')
     A                                      COLHDG('Currency 3 for' +
     A                                             'Rate Fixing' +
     A                                             '-Convention')
     A            SCYR4     R               REFFLD(CCYD)
     A                                      TEXT('Currency 4 for Rate +
     A                                            Fixing Convention')
     A                                      COLHDG('Currency 4 for' +
     A                                             'Rate Fixing' +
     A                                             '-Convention')
     A            SCYR5     R               REFFLD(CCYD)
     A                                      TEXT('Currency 5 for Rate +
     A                                            Fixing Convention')
     A                                      COLHDG('Currency 5 for' +
     A                                             'Rate Fixing' +
     A                                             '-Convention')
     A            SCYR6     R               REFFLD(CCYD)
     A                                      TEXT('Currency 6 for Rate +
     A                                            Fixing Convention')
     A                                      COLHDG('Currency 6 for' +
     A                                             'Rate Fixing' +
     A                                             '-Convention')
     A            SCYR7     R               REFFLD(CCYD)
     A                                      TEXT('Currency 7 for Rate +
     A                                            Fixing Convention')
     A                                      COLHDG('Currency 7 for' +
     A                                             'Rate Fixing' +
     A                                             '-Convention')
     A            SCYR8     R               REFFLD(CCYD)
     A                                      TEXT('Currency 8 for Rate +
     A                                            Fixing Convention')
     A                                      COLHDG('Currency 8 for' +
     A                                             'Rate Fixing' +
     A                                             '-Convention')
     A            SCYR9     R               REFFLD(CCYD)
     A                                      TEXT('Currency 9 for Rate +
     A                                            Fixing Convention')
     A                                      COLHDG('Currency 9 for' +
     A                                             'Rate Fixing' +
     A                                             '-Convention')
     A            SCYR0     R               REFFLD(CCYD)
     A                                      TEXT('Currency 10 for Rate +
     A                                            Fixing Convention')
     A                                      COLHDG('Currency 10 for' +
     A                                             'Rate Fixing' +
     A                                             '-Convention')
      *                                              End   /CIR005/
      **Start of change CAP056
     A            SAUTH          1          TEXT('Automatic +
     A                                      Authorisation')
     A                                      COLHDG('Automatic' +
     A                                      'Authorisation')
      **End of change CAP056
      **Start of change CAS006
     A            SRKVL         12A         TEXT('Risk Value Factor')
     A                                      COLHDG('Risk Value Factor')
      *
     A            SRKSN          1A         TEXT('Risk Value Factor Sign')
     A                                      COLHDG('Risk Value ' 'Factor Sign')
      **End of change CAS006
     A*                                              Begin /CDL038/
     A            SCLAS          4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
