      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas IR Revalued SIRS File')
      *****************************************************************
      *                                                               *
      *  Midas - IR Module                                            *
      *                                                               *
      *  PF/IRNPVAPD - Midas IR Revalued FRAs and IRSs                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 05Dec05               *
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CAS005  *CREATE    Date 16Dec02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *                                                               *
      *****************************************************************
      *
     A          R IRNPVAD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A            IRBRCA         3A         TEXT('Branch Code')
     A                                      COLHDG('Branch' +
     A                                             'Code')
     A            IRBOKC         2A         TEXT('Book Code')
     A                                      COLHDG('Book' +
     A                                             'Code')
     A            IRCYCD         3A         TEXT('Currency Code')
     A                                      COLHDG('Currency' +
     A                                             'Code')
     A            IRDTYP         2A         TEXT('Deal Type')
     A                                      COLHDG('Deal' +
     A                                             'Type')
     A            IRDLST         2A         TEXT('Deal Sub-Type')
     A                                      COLHDG('Deal' +
     A                                             'Sub-Type')
     A            IRDLNO         6S 0       TEXT('Deal Number')
     A                                      COLHDG('Deal' +
     A                                             'Number')
      * Begin /CSD027/
     A**********  IRCNUM         6S 0       TEXT('Customer Number')                           CSD027
     A            IRCNUM         6A         TEXT('Customer Number')
      * End /CSD027/
     A                                      COLHDG('Customer' +
     A                                             'Number')
     A            IRYLDC         5A         TEXT('Yield Curve')
     A                                      COLHDG('Yield' +
     A                                             'Curve')
     A            IRPAMT        13P 0       TEXT('Current Principal')
     A                                      COLHDG('Current' +
     A                                             'Principal')
     A            IRNPVA        15P 0       TEXT('NPV Amount')
     A                                      COLHDG('NPV' +
     A                                             'Amount')
     A            IRAINT        13P 0       TEXT('Accrued Interest')
     A                                      COLHDG('Accrued' +
     A                                             'Interest')
     A            IRURPL        13P 0       TEXT('Unrealized Profit/Loss')
     A                                      COLHDG('Unrealized' +
     A                                             'Profit/Loss')
     A            IRPUPL        13P 0       TEXT('Previous Unrealized +
     A                                            Profit/Loss')
     A                                      COLHDG('Previous Unrealized' +
     A                                             'Profit/Loss')
     A            IRTUPL        13P 0       TEXT('Todays Unrealized  +
     A                                            Profit/Loss Posting Amount')
     A                                      COLHDG('Todays Unrealized' +
     A                                             'P/L Posting Amt')
     A            IRYUPL        13P 0       TEXT('Unrealized Profit/Loss +
     A                                            at Year-End')
     A                                      COLHDG('Unrealized P/L' +
     A                                             'at Year-End')
     A*                                              Begin /CDL038/
     A            IRCLAS         4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
