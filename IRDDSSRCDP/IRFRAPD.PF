      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas IR FRA format definition file')
      *****************************************************************
      *                                                               *
      *  Midas - Application Program Interface Module                 *
      *                                                               *
      *  PF/IRFRAPD - API Incoming Message Definition File for        *
      *             Forward Rate Aggreements                          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CAP084             Date 30Jul03               *
      *                 CAS006             Date 21Jan03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CIR005             Date 21Jan00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSW097             Date 30Mar99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP003  *CREATE    Date 01Jun98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAP084 - Display Confirmation Matching status.  Patterned    *
      *           after fix 186458. This fix originally put into      *
      *           display file and DSP module for API. Needs to go in *
      *           this file for API wrappers.                         *
      *  CAS006 - Hedge Accounting Phase B                            *
      *  CIR005 - FRA/IRS Business Day Conventions                    *
      *  CSW097 - MT34*, MT36* message generation.                    *
      *  CAP003 - Conversion of inoput progarsm into modules for use  *
      *           as APIs.                                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R IRFRAD0                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
      ** Message contents
      *
      ** Action code
     A            SACCD     R               REFFLD(ACTO)
      *
      ** Deal number
     A            SDLNO     R     A         REFFLD(DEAL)
      *
      ** Deal type
     A            SDTYP     R               REFFLD(DTPE)
      *
      ** Deal subtype
     A            SDLST     R               REFFLD(DLST)
      *
      ** Deal date
     A            SDDAT     R     A         REFFLD(CHGD)
     A                                      TEXT('Deal Date')
     A                                      COLHDG('Deal' 'Date')
      ** Branch code
     A            SBRCA     R               REFFLD(BRCA)
     A                                      TEXT('Branch code')
      *
      ** Broker code
     A            SBRKC     R               REFFLD(BKCD)
      *
      ** Brokerage
     A            SBAGE     R   14A         REFFLD(BRKG)
     A                                      TEXT('Brokerage')
      *
      ** Linked reference number
     A            SLNKDN    R     A         REFFLD(LNKDN)
     A                                      TEXT('Linked ref no')
      *
      ** Customer
     A            SCNUM     R               REFFLD(CSNM)
     A                                      TEXT('Customer Shortname or number')
      *
      ** Value date
     A            SVDAT     R     A         REFFLD(CHGD)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value' 'Date')
      *
      ** Maturity date
     A            SMDAT     R     A         REFFLD(CHGD)
     A                                      TEXT('Maturity Date')
     A                                      COLHDG('Maturity' 'Date')
      *
      ** A/C Sequence number
     A            SCDAS     R     A         REFFLD(ACSQ)
      *
      ** Currency
     A            SUCUCY    R               REFFLD(CCY)
      *
      ** Amount
     A            SUPAMT    R               REFFLD(AM14)
     A                                      TEXT('Amount')
      *
      ** Federal funds
     A            SUFFI     R               REFFLD(FEDF)
      *
      ** Base currency rate
     A            SUBCXR        14A
     A                                      TEXT('Base currency rate')
      *
      ** Book code
     A            SBOKC     R               REFFLD(BOKC)
      *
      ** Interest Calculation Basis
     A            SUICBS    R               REFFLD(ICMT)
     A                                      TEXT('Interest Calculation Basis')
      *
      ** Taxable ind
     A            STAXI     R               REFFLD(MTAX)
      *
      ** Rate Fix Days
     A            SUFRFD    R     A         REFFLD(IPDM)
     A                                      TEXT('Rate Fix Days')
      *
      ** Profit centre
     A            SPRFC     R               REFFLD(PRFC)
      *
      ** Originating branch
     A            SORBR     R               REFFLD(ORBR)
      *
      ** Buy/Sell Indicator
     A            SBYSL     R               REFFLD(CPTI)
     A                                      TEXT('Buy/Sell Indicator')
      *
      ** Settlement Formula
     A            SSETF     R               REFFLD(ICMT)
     A                                      TEXT('Settlement Formula')
      *
      ** Reference Rate
     A            SRFRTE    R               REFFLD(MRBC)
     A                                      TEXT('Reference Rate')
      *
      ** Contract Rate
     A            SCRTE     R   +1A         REFFLD(SPRD)
     A                                      TEXT('Contract Rate')
      **
      ** Start of CSW097
      ** Collateral Agreement Ind
     A            SCAIN          1A
     A                                      TEXT('Collateral Agreement Ind')
      ** End of CSW097
      *                                              Begin /CIR005/
     A            SCYR1     R               REFFLD(CCYD)
     A                                      TEXT('Currency 1 for Rate Fixing +
     A                                            Convention')
     A                                      COLHDG('Currency 1 for' +
     A                                             'Rate Fixing' +
     A                                             'Convention')
     A            SCYR2     R               REFFLD(CCYD)
     A                                      TEXT('Currency 2 for Rate Fixing +
     A                                            Convention')
     A                                      COLHDG('Currency 2 for' +
     A                                             'Rate Fixing' +
     A                                             'Convention')
     A            SCYR3     R               REFFLD(CCYD)
     A                                      TEXT('Currency 3 for Rate Fixing +
     A                                            Convention')
     A                                      COLHDG('Currency 3 for' +
     A                                             'Rate Fixing' +
     A                                             'Convention')
     A            SCYR4     R               REFFLD(CCYD)
     A                                      TEXT('Currency 4 for Rate Fixing +
     A                                            Convention')
     A                                      COLHDG('Currency 4 for' +
     A                                             'Rate Fixing' +
     A                                             'Convention')
     A            SCYR5     R               REFFLD(CCYD)
     A                                      TEXT('Currency 5 for Rate Fixing +
     A                                            Convention')
     A                                      COLHDG('Currency 5 for' +
     A                                             'Rate Fixing' +
     A                                             'Convention')
     A            SCYR6     R               REFFLD(CCYD)
     A                                      TEXT('Currency 6 for Rate Fixing +
     A                                            Convention')
     A                                      COLHDG('Currency 6 for' +
     A                                             'Rate Fixing' +
     A                                             'Convention')
     A            SCYR7     R               REFFLD(CCYD)
     A                                      TEXT('Currency 7 for Rate Fixing +
     A                                            Convention')
     A                                      COLHDG('Currency 7 for' +
     A                                             'Rate Fixing' +
     A                                             'Convention')
     A            SCYR8     R               REFFLD(CCYD)
     A                                      TEXT('Currency 8 for Rate Fixing +
     A                                            Convention')
     A                                      COLHDG('Currency 8 for' +
     A                                             'Rate Fixing' +
     A                                             'Convention')
     A            SCYR9     R               REFFLD(CCYD)
     A                                      TEXT('Currency 9 for Rate Fixing +
     A                                            Convention')
     A                                      COLHDG('Currency 9 for' +
     A                                             'Rate Fixing' +
     A                                             'Convention')
     A            SCYR0     R               REFFLD(CCYD)
     A                                      TEXT('Currency 10 for Rate Fixing +
     A                                            Convention')
     A                                      COLHDG('Currency 10 for' +
     A                                             'Rate Fixing' +
     A                                             'Convention')
      *                                              End   /CIR005/
      **Start of change CAS006
     A            SRKVL         12A         TEXT('Risk Value Factor')
     A                                      COLHDG('Risk Value Factor')
      *
     A            SRKSN          1A         TEXT('Risk Value Factor Sign')
     A                                      COLHDG('Risk Value ' ' Factor Sign')
      **End of change CAS006
      *                                              Begin /CAP084/
     A            SCPTX         17          TEXT('Counterparty Narrative')
     A                                      COLHDG('Counterparty Narr.')
     A            SCPST         15          TEXT('Counterparty Status')
     A                                      COLHDG('Counterparty Status')
     A            SBKTX         11          TEXT('Broker Narrative')
     A                                      COLHDG('Broker Narrative')
     A            SBKST         15          TEXT('Broker Status')
     A                                      COLHDG('Broker Status')
      *                                              End   /CAP084/
     A*                                              Begin /CDL038/
     A            SCLAS          4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
