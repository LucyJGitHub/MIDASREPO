      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IR Cross Ccy Reval using All-in-Rate Report')
      *****************************************************************
      *                                                               *
      *  Midas - FRA/IRS Module                                       *
      *                                                               *
      *  IR001957P2 - Cross Currency IRS Reval All-in-Rate Report     *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS008  *CREATE    Date 16Jun04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS008 - IAS 39 Enhancements                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE IR001957P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    27'CROSS CURRENCY IRS REVALUATION +
     A                                      USING ALL-IN-RATE REPORT UP TO +
     A                                      BUT EXCLUDING'
     A            REVCDE         7       103
     A                                   132' '
     A                                      SPACEA(1)
     A                                    27'-------------------------------+
     A                                      -------------------------------+
     A                                      ---------------------'
     A                                   132' '
     A                                      SPACEA(2)
      *
     A                                     3'BRANCH'
     A            RBRCA     R             10REFFLD(BRCA)
     A            RBRNM     R             14REFFLD(BRNM)
     A                                      SPACEA(1)
      *
     A                                     3'BOOK CODE:'
     A            RBKCD          2        14
     A            RBKNM         30        17
     A                                      SPACEA(2)
      *
     A                                     3'DEAL'
     A                                    14'CUST.'
     A                                    22'YIELD'
     A                                    30'VALUE'
     A                                    73'DEALT'
     A                                    94'DISCOUNT'
     A                                   114'NET PRESENT'
     A                                      SPACEA(1)
     A                                     3'NUMBER'
     A                                    14'NUMBER'
     A                                    22'CURVE'
     A                                    30'DATE'
     A                                    40'CCY'
     A                                    53'AMOUNT'
     A                                    71'EQUIVALENT'
     A                                    95'FACTOR'
     A                                   117'VALUE'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETP2
      *
     A            RDLNO          6         3
     A            RCUST          6        14
     A            RYIELD         5        22
     A            RVDAT          7        29
     A            RCCY           3        40
     A            RAMT          21        44
     A            REQUI         21        67
     A            RFACT         11        93
     A            RNPV          21       110
     A                                      SPACEA(1)
      *
      *-----------------------------*
      *** Deal Totals Record Format *
      *-----------------------------*
      *
     A          R DTOT2
      *
     A                                   110'======================'
     A                                      SPACEA(1)
     A            RDLNO          6       100HIGHLIGHT
     A            RNPVT         21       110HIGHLIGHT
     A                                      SPACEA(1)
     A                                   110'======================'
     A                                      SPACEA(1)
     A                                    55'Adjustment for accrued +
     A                                      interest posted this +
     A                                      period'
     A            RDACR         21       110HIGHLIGHT
     A                                      SPACEA(1)
     A                                   110'======================'
     A                                      SPACEA(1)
     A                                     2'--------------------+
     A                                      --------------------+
     A                                      --------------------+
     A                                      --------------------+
     A                                      -------- Adjusted'
     A            RDLNO1         6       100HIGHLIGHT
     A            RDADJ         21       110HIGHLIGHT
     A                                      SPACEA(1)
     A                                   110'======================'
     A                                      SPACEA(1)
      *
     A          R ENDRPT2                   TEXT('END OF REPORT')
      *
     A                                    43'** END OF REPORT **'
      *
