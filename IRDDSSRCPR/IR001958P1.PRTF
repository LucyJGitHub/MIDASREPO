      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IR Cross Ccy Reval using Base Rate Report')
      *****************************************************************
      *                                                               *
      *  Midas - FRA/IRS Module                                       *
      *                                                               *
      *  IR001958P1 - Cross Currency IRS Reval Base Rate Report       *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS008  *CREATE    Date 16Jun04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS008 - IAS 39 Enhancements                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE IR001958P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    27'CROSS CURRENCY IRS REVALUATION +
     A                                      USING BASE RATE REPORT UP TO +
     A                                      BUT EXCLUDING'
     A            REVCDE         7       101
     A                                   132' '
     A                                      SPACEA(1)
     A                                    27'-------------------------------+
     A                                      -----------------------------+
     A                                      ---------------------'
     A                                   132' '
     A                                      SPACEA(2)
      *
     A                                     3'BRANCH'
     A            RBRCA     R             11REFFLD(BRCA)
     A            RBRNM     R             15REFFLD(BRNM)
      *
     A                                   101'CURRENCY'
     A            RRCCY     R            110REFFLD(CCY)
     A            RCYNM     R            114REFFLD(CYNM)
     A                                   132' '
     A                                      SPACEA(2)
      *
     A                                     3'DEAL'
     A                                    10'COUNTERPARTY'
     A                                    36'BOOK'
     A                                    43'VALUE'
     A                                    50'MATURITY'
     A                                    65'CURRENT PRINCIPALS'
     A                                    88'---------------- +
     A                                      PROFIT/LOSS ----------------'
     A                                      SPACEA(1)
      *
     A                                    43'DATE'
     A                                    50'  DATE  '
     A                                    94'NPV AMOUNT'
     A                                   114'ADJUSTED AMOUNT'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETP1
      *
     A            RDLNO          6         2
     A            RCNUM          6        10
     A            BBCRNM    R             17REFFLD(CRNM)
     A            RBOKC          2        37
     A            RVDAT          7        42
     A            RMDAT          7        50
     A            RUCUCY         3        59
     A            RUPAMT        21        63
     A            RDTOT         21        86
     A            RDADJ         21       109
     A                                      SPACEA(1)
     A  31                                 2 '* CURVE MISSING - NOT VALUED *'
     A N31 32                              2 '* RATES MISSING - NOT VALUED *'
     A            RTCUCY         3        59
     A            RTPAMT        21        63
     A                                      SPACEA(1)
      *
      *-----------------------------*
      ***  Book Total Record Format *
      *-----------------------------*
      *
     A          R BKTOTP1
     A                                    60'TOTAL FOR BOOK'
     A                                      SPACEB(1)
     A            RBOKC1         2        75HIGHLIGHT
     A            ZBKTOT        21        86HIGHLIGHT
     A            RBKTOT        21       109HIGHLIGHT
     A                                      SPACEA(2)
      *
      *-------------------------------*
      ***  Branch Total Record Format *
      *-------------------------------*
      *
     A          R BRTOTP1
     A                                    60'TOTAL FOR BRANCH'
     A                                      SPACEB(1)
     A            RBRCA1         3        77HIGHLIGHT
     A            ZBRTOT        21        86HIGHLIGHT
     A            RBRTOT        21       109HIGHLIGHT
     A                                      SPACEA(2)
      *
     A          R ENDRPT1                   TEXT('END OF REPORT')
      *
     A                                    43'** END OF REPORT **'
