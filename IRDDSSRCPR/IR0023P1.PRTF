      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IR Voce/Sottovoce Original and Derived Dec')     *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module - Italy.                   *
      *                                                               *
      *  PRTF/IR0023P1 - Voce/Sottovoce Original and Derived Decision *
      *                  Table - List                                 *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 13Jan21               *
      *  Prev Amend No. UIT021             Date 07may97               *
      *  Prev Amend No.                    Date                       *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  UIT021 - BOI Monthly Reporting                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(C) Midas-Kapiti +
     A                                      International Ltd. 1997')
      *
     A                                      SKIPB(3)
     A                                     3'REFERENCE IR0023P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    24'"BOI" Voce/Sottovoce Original and -
     A                                      "BOI" Voce/Sottovoce Derived Decisi-
     A                                      on Table List'
     A                                      SPACEA(1)
      *
     A                                    24'-----------------------------------
     A                                      ------------------------------------
     A                                      -------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     3'VOCE/SOTTOVOCE'
     A                                    27'ACCOUNT'
     A                                    44'TYPE OF'
     A                                    62'DURATION'
     A                                    80'BASE'
     A                                    94'ADDITION'
     A                                   112'VOCE/SOTTOVOCE'
     A                                      SPACEA(1)
     A                                     6'ORIGINAL'
     A                                    27'NATURE'
     A                                    44'CUSTOMER'
     A                                    62'CODIFIC.'
     A                                    80'CCY'
     A                                    96'CODE'
     A                                   115'DERIVED'
     A                                      SPACEA(1)
      *
     A                                     3'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -------------------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETLP1                    SPACEA(001)
      *
     A            RFTOR1         5A        6
     A            RFTOR2         2A       12
     A            RACNA          3A       29
     A            RTPCU          2A       47
     A            RDUPE          2A       65
     A            RBACY          1A       81
     A            RADCD          1A       97
     A            RFTDE1         5A      115
     A            RFTDE2         2A      121
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
