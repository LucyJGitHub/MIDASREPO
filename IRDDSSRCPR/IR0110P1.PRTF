      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IR UIC Control Postings Extract Report')         *
      *****************************************************************
      *                                                               *
      *  Midas ABS - BOI Returns                                      *
      *                                                               *
      *  IR0110P1 - UIC Control Postings Extract Report               *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 22Dec20               *
      *  Prev Amend No. UIT021             Date 12FEB97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  UIT021 - BOI Monthly Reporting                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *---------------*
      * Report Header *
      *---------------*
      *
     A          R PAGHDR                     SKIPB(2)
      *
     A                                     2'REFERENCE IR0110P1'
     A            BJURPT    R             39REFFLD(URPT)
     A                                   105'DATE'
     A            BJMRDT    R            111REFFLD(MRDT)
     A                                   121'PAGE'
     A                                   127PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
     A                                     2'BRANCH:'
     A            RBRCA          3A       10
     A  10                                41'UIC CONTROL REPORT +
     A                                      DAILY POSTINGS EXTRACT'
     A                                      SPACEA(1)
     A  10                                41'-------------------+
     A                                      ----------------------'
     A                                      SPACEA(2)
     A  11                                40'UIC CONTROL REPORT +
     A                                      MONTHLY POSTINGS EXTRACT'
     A                                      SPACEA(1)
     A  11                                40'-------------------+
     A                                      ------------------------'
     A                                      SPACEA(2)
     A  12                                40'UIC CONTROL REPORT PREVIOUS +
     A                                      MONTH''S POSTINGS EXTRACT'
     A                                      SPACEA(1)
     A  12                                40'----------------------------+
     A                                      ------------------------'
     A                                      SPACEA(2)
      *
      *-------------------*
      * Report Sub-Header *
      *-------------------*
      *
     A          R SUBHDR
      *
     A  20                                 2'LIRA'
     A                                      SPACEA(1)
     A  20                                 2'----'
     A                                      SPACEA(2)
     A N20                                 2'NON-LIRA'
     A                                      SPACEA(1)
     A N20                                 2'--------'
     A                                      SPACEA(2)
      *
     A                                     3'CCY'
     A                                     9'CAUSALE'
     A                                    18'RESID.'
     A  15                                29'RRN'
     A                                    43'DEBIT'
     A                                    69'CREDIT'
     A                                    83'ACCOUNT DETAILS'
     A                                   102'NARRATIVE'
     A                                      SPACEA(1)
     A                                     3'CODE'
     A                                    10'CODE'
     A                                    42'POSTINGS'
     A                                    68'POSTINGS'
     A                                      SPACEA(1)
     A                                     3'----'
     A                                     9'-------'
     A                                    18'------'
     A  15                                29'---'
     A                                    42'--------'
     A                                    68'--------'
     A                                    83'---------------'
     A                                   102'---------'
      *
      *------------------*
      * Detail Line Lira *
      *------------------*
      *
     A          R LIRDTL                    SPACEB(1)
      *
     A            RCCY           3         3
     A            RCAUC          4  0     10
     A            RRENR          3        19
     A  15        RRRN           6  0     26EDTCDE(4)
     A  25        RDAMT2        15  2     33EDTWRD(' ,   ,   ,   , 0 .  ')
     A N25        RCAMT2        15  2     59EDTWRD(' ,   ,   ,   , 0 .  ')
     A            RCNUM          6  0     82
     A            RACOD          4  0     89
     A            RACSQ          2  0     94
     A            RATYP          3        97
     A            RPNAR         30       102
      *
      *----------------------*
      * Detail line Non-Lira *
      *----------------------*
      *
     A          R NLIDTL                    SPACEB(1)
      *
     A            RCCY           3         3
     A            RCAUC          4  0     10
     A            RRENR          3        19
     A  15        RRRN           6  0     26EDTCDE(4)
     A  25 30     RDAMT0        15  0     31EDTWRD('   ,   ,   ,   , 0 ')
     A  25 31     RDAMT1        15  1     32EDTWRD('  ,   ,   ,   , 0 . ')
     A  25 32     RDAMT2        15  2     33EDTWRD(' ,   ,   ,   , 0 .  ')
     A  25 33     RDAMT3        15  3     35EDTWRD('   ,   ,   , 0 .   ')
     A N25 30     RCAMT0        15  0     57EDTWRD('   ,   ,   ,   , 0 ')
     A N25 31     RCAMT1        15  1     58EDTWRD('  ,   ,   ,   , 0 . ')
     A N25 32     RCAMT2        15  2     59EDTWRD(' ,   ,   ,   , 0 .  ')
     A N25 33     RCAMT3        15  3     61EDTWRD('   ,   ,   , 0 .   ')
     A            RCNUM          6  0     82
     A            RACOD          4  0     89
     A            RACSQ          2  0     94
     A            RATYP          3        97
     A            RPNAR         30       102
      *
      *---------------------*
      * Causale Code Totals *
      *----------------------*
      *
     A          R CAUTOT                    SPACEB(2)
      *
     A                                     4'TOTAL CAUSALE CODE'
     A            RCAUC          4  0     24
     A  30        RCADT0        15  0     31EDTWRD('   ,   ,   ,   , 0 ')
     A  31        RCADT1        15  1     32EDTWRD('  ,   ,   ,   , 0 . ')
     A  32        RCADT2        15  2     33EDTWRD(' ,   ,   ,   , 0 .  ')
     A  33        RCADT3        15  3     35EDTWRD('   ,   ,   , 0 .   ')
     A  30        RCACT0        15  0     57EDTWRD('   ,   ,   ,   , 0 ')
     A  31        RCACT1        15  1     58EDTWRD('  ,   ,   ,   , 0 . ')
     A  32        RCACT2        15  2     59EDTWRD(' ,   ,   ,   , 0 .  ')
     A  33        RCACT3        15  3     61EDTWRD('   ,   ,   , 0 .   ')
     A                                    86'NO. DR''S'
     A            RCAND          4  0     95EDTCDE(1)
     A                                   106'NO. CR''S'
     A            RCANC          4  0    115EDTCDE(1)
     A                                      SPACEA(2)
      *
      *----------------------*
      * Currency Code Totals *
      *----------------------*
      *
     A          R CCYTOT
     A                                     4'TOTAL CURRENCY'
     A            RCCY           3        24
     A  30        RCYDT0        15  0     31EDTWRD('   ,   ,   ,   , 0 ')
     A  31        RCYDT1        15  1     32EDTWRD('  ,   ,   ,   , 0 . ')
     A  32        RCYDT2        15  2     33EDTWRD(' ,   ,   ,   , 0 .  ')
     A  33        RCYDT3        15  3     35EDTWRD('   ,   ,   , 0 .   ')
     A  30        RCYCT0        15  0     57EDTWRD('   ,   ,   ,   , 0 ')
     A  31        RCYCT1        15  1     58EDTWRD('  ,   ,   ,   , 0 . ')
     A  32        RCYCT2        15  2     59EDTWRD(' ,   ,   ,   , 0 .  ')
     A  33        RCYCT3        15  3     61EDTWRD('   ,   ,   , 0 .   ')
     A                                    86'NO. DR''S'
     A            RCYND          4  0     95EDTCDE(1)
     A                                   106'NO. CR''S'
     A            RCYNC          4  0    115EDTCDE(1)
     A                                      SPACEA(2)
      *
      *----------------------*
      * Lira/Non-Lira Totals *
      *----------------------*
      *
     A          R LNLTOT
     A                                    31'-----------------------'
     A                                    57'-----------------------'
     A                                    95'-----'
     A                                   115'-----'
     A                                      SPACEA(2)
     A  20                                 4'TOTAL LIRA'
     A N20                                 4'TOTAL NON-LIRA'
     A            RLNDT2        15  2     33EDTWRD(' ,   ,   ,   , 0 .  ')
     A            RLNCT2        15  2     59EDTWRD(' ,   ,   ,   , 0 .  ')
     A                                    86'NO. DR''S'
     A            RLNND          4  0     95EDTCDE(1)
     A                                   106'NO. CR''S'
     A            RLNNC          4  0    115EDTCDE(1)
     A                                      SPACEA(2)
      *
      *---------------*
      * Branch Totals *
      *---------------*
      *
     A          R BRNTOT
     A                                     4'TOTAL BRANCH'
     A            RBRCA          3A       24
     A            RBRDT2        15  2     33EDTWRD(' ,   ,   ,   , 0 .  ')
     A            RBRCT2        15  2     59EDTWRD(' ,   ,   ,   , 0 .  ')
     A                                    86'NO. DR''S'
     A            RBRND          4  0     95EDTCDE(1)
     A                                   106'NO. CR''S'
     A            RBRNC          4  0    115EDTCDE(1)
      *
      *----------------------------*
      * No Lira Details For Branch *
      *----------------------------*
      *
     A          R NOLIRA                    SPACEB(3)
     A                                    49'*** NO LIRA +
     A                                      DETAILS TO REPORT ***'
      *
      *---------------*
      * End of Report *
      *---------------*
      *
     A          R RPTEND                    SPACEB(3)
     A                                    55'*** END OF REPORT ***'
     A
