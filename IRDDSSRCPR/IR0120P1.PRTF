      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IR FX Control Posting Extract Report')           *
      *****************************************************************
      *                                                               *
      *  Midas ABS - BOI Returns                                      *
      *                                                               *
      *  IR0120P1 - FX Control Posting Extract Report by Causale      *
      *             Code with RRN                                     *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 22Dec20               *
      *  Prev Amend No. UIT021             Date 14FEB97               *
      *  Prev Amend No.                    Date                       *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  UIT021 - BOI Monthly Reporting                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *---------------*
      * Report Header *
      *---------------*
      *
     A          R IR120P11                  SKIPB(2)
      *
     A                                     2'REFERENCE IR0120P1'
     A            BJURPT    R             39REFFLD(URPT)
     A                                   105'DATE'
     A            BJMRDT    R            111REFFLD(MRDT)
     A                                   121'PAGE:'
     A                                   127PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
     A                                     2'BRANCH:'
     A            RBRCA          3A       10
     A  25                                36'F.X. CONTROL REPORT - CURRENT +
     A                                      MONTH POSTINGS BY CAUSALE CODE'
     A                                      SPACEA(1)
     A  25                                36'------------------------------+
     A                                      ------------------------------'
     A                                      SPACEA(2)
     A  26                                35'F.X. CONTROL REPORT - PREVIOUS +
     A                                      MONTH POSTINGS BY CAUSALE CODE'
     A                                      SPACEA(1)
     A  26                                35'-------------------------------+
     A                                      ------------------------------'
     A                                      SPACEA(2)
      *
     A                                     4'CAUSALE'
     A                                    15'RRN'
     A                                    21'CCY'
     A                                    27'RESID.'
     A                                    45'DEBIT'
     A                                    68'CREDIT'
     A                                    82'ACCOUNT DETAILS'
     A                                   101'NARRATIVE'
     A                                      SPACEA(1)
     A                                     5'CODE'
     A                                    21'CODE'
     A                                    44'POSTINGS'
     A                                    67'POSTINGS'
     A                                      SPACEA(1)
     A                                     4'-------'
     A                                    15'---'
     A                                    21'----'
     A                                    27'------'
     A                                    44'--------'
     A                                    67'--------'
     A                                    82'---------------'
     A                                   101'---------'
      *
      *-------------*
      * Detail Line *
      *-------------*
      *
     A          R IR120P12                  SPACEB(1)
      *
     A            RCAUC          4         5
     A            RRRN           6  0     12EDTCDE(Z)
     A            RCCY           3        21
     A            RRENR          3        28
     A  40 30     AMTD          15  0     33EDTCDE(J)
     A  40 31     AMTD1         15  1     34EDTCDE(J)
     A  40 32     AMTD2         15  2     35EDTCDE(J)
     A  40 33     AMTD3         15  3     37EDTCDE(J)
     A  41 30     AMTC          15  0     56EDTCDE(J)
     A  41 31     AMTC1         15  1     57EDTCDE(J)
     A  41 32     AMTC2         15  2     58EDTCDE(J)
     A  41 33     AMTC3         15  3     60EDTCDE(J)
     A            RCNUM          6  0     81
     A            RACOD          4  0     88
     A            RACSQ          2  0     93
     A            RATYP          3        96
     A            RPNAR         30       101
      *
      *---------------*
      * End of Report *
      *---------------*
      *
     A          R IR120P13                  SPACEB(3)
      *
     A                                    55'*** END OF REPORT ***'
      *
