      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IR FRA Rev. Report Using N-T-R')
      *****************************************************************
      *                                                               *
      *  Midas - FRA/IRS Module                                       *
      *                                                               *
      *  PRTF/IR1950P1 - Midas FRA Revaluation Report Using Net-      *
      *                  Treasury-Rate                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE IR1950P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A N22
     AO 22N23N24                          44'FRA REVALUATION REPORT +
     A                                      USING NET-TREASURY-RATE'
     A  22 24                             36'FRA REVALUATION REPORT +
     A                                      USING NET-TREASURY-RATE +
     A                                      UP TO'
     A  22 23                             26'FRA REVALUATION REPORT +
     A                                      USING NET-TREASURY-RATE +
     A                                      UP TO BUT EXCLUDING'
     A  22 24     RDATH          7        89
     A  22 23     RDATHE         7        93
     A                                   132' '
     A                                      SPACEA(1)
      *
     A N22
     AO 22N23N24                          44'-----------------------+
     A                                      -----------------------'
     A  22 24                             36'-----------------------+
     A                                      ------------------------+
     A                                      -------------'
     A  22 23                             26'-----------------------+
     A                                      ------------------------+
     A                                      ------------------------+
     A                                      ---'
     A                                   132' '
     A                                      SPACEA(2)
      *
     A  15                                21'***  WARNING : RATES MAY -
     A                                      HAVE CHANGED SINCE DISCOUNT -
     A                                      FACTORS LAST RECALCULATED  ***'
     A                                      SPACEA(2)
      *
     A  11                                 4'BRANCH'
     A  11        A8BRCD    R             11REFFLD(BRCA)
     A  11        A8BRNM    R             15REFFLD(BRNM)
     A                                   101'CURRENCY'
     A            A6CYCD         3       110
     A            A6CYNM        14       114
     A                                      SPACEA(2)
      *
     A                                     5'VALUE   CUSTOMER   DEAL'
     A                                    29'BOUGHT CONTRACT'
     A                                    52'NOMINAL VALUE'
     A                                    70'REVALUATION'
     A                                    90'PROFIT/LOSS'
     A                                   112'PRESENT VALUE'
     A                                      SPACEA(1)
      *
     A                                     5'DATE     NUMBER'
     A                                    29'/SOLD    RATE'
     A                                    73'RATE'
     A                                   113'PROFIT/LOSS'
     A                                      SPACEA(2)
      *
     A          R BKHEAD
     A                                     4'BOOK'
     A            BDBKCD         2         9
     A            BDBKD         30        12
     A                                      SPACEA(2)
      *
     A          R DETAIL
     A            RVDAT          7         4
     A            RCNUM          6        14
     A            RDLNO          6        23
     A            RBYSL          1        31
     A            REINR         12        34
     A            RPAMT         17        49
     A N21        RFRART        13        69
     A  21                                73'*.*'
     A N21        RPL           21        84
     A N20N21     RPLNPV        21       108
     A                                      SPACEA(2)
     A  20                               108'* MM Rates Missing *'
     A                                      SPACEA(2)
     A  21                               108'* FRA Rate Missing *'
     A                                      SPACEA(2)
      *
     A          R TOTALS
     A  16                               108'---------------------'
     A  16                                  SPACEA(1)
     A  16                                91'TOTAL FOR BOOK'
     A  16        RTOTBK        21       108
     A  16                                  SPACEA(1)
     A  16                               108'====================='
     A  16                                  SPACEA(1)
     A  17                                87'TOTAL FOR CURRENCY'
     A  17        RTOTCY        21       108
     A  17                                  SPACEA(1)
     A  17                               108'====================='
     A  17                                  SPACEA(1)
      *
     A  18 11                             70'TOTAL FOR BRANCH (IN BASE +
     A                                      CURRENCY)'
     A  18N11                             81'TOTAL (IN BASE CURRENCY)'
     A  18        RTOTBR        21       108
     A  18                                  SPACEA(1)
     A  18                               108'====================='
     A  18                                  SPACEA(2)
      *
     A  18 11                             46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A  18N11                             51'***  END OF REPORT  ***'
