      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IR IRS Revaluation Using Base Rate Report 1')
      *****************************************************************
      *                                                               *
      *  Midas - FRA/IRS Module                                       *
      *                                                               *
      *  PRTF/IR1956P1 - IRS Revaluation Using Base Rate Report 1     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG9501            Date 06Dec05               *
      *  Prev Amend No. CAS005  *CREATE    Date 16Dec02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG9501 - *IN31 for Curve Missing.                           *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R PAGHD1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE IR1956P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A N10 40N41
     AAN42
     AON10N40                             35'IRS REVALUATION USING BASE RATE +
     A                                      REPORT - SINGLE CURRENCY'
     A N10 40 41                          19'IRS REVALUATION USING BASE RATE +
     A                                      REPORT - SINGLE CURRENCY - +
     A                                      PRELIMINARY REPORT FOR'
     A N10 40 42                          20'IRS REVALUATION USING BASE RATE +
     A                                      REPORT - SINGLE CURRENCY - UP TO +
                                            BUT EXCLUDING'
     A  10 40N41
     AAN42
     AO 10N40                             35'IRS REVALUATION USING BASE RATE +
     A                                      REPORT - CROSS CURRENCY'
     A  10 40 41                          19'IRS REVALUATION USING BASE RATE +
     A                                      REPORT - CROSS CURRENCY - +
     A                                      PRELIMINARY REPORT FOR'
     A  10 40 42                          20'IRS REVALUATION USING BASE RATE +
     A                                      REPORT - CROSS CURRENCY - UP TO +
                                            BUT EXCLUDING'
     A  40 42     REVCD          7        99
     A  40 41     REVCDE         7       101
     A                                   132' '
     A                                      SPACEA(1)
      *
     A N10 40N41
     AAN42
     AON10N40                             35'--------------------------------+
     A                                      ------------------------'
     A N10 40 41                          19'--------------------------------+
     A                                      ---------------------------+
     A                                      ------------------------------'
     A N10 40 42                          20'--------------------------------+
     A                                      ---------------------------------+
     A                                      ---------------------'
     A  10 40N41
     AAN42
     AO 10N40                             35'--------------------------------+
     A                                      -----------------------'
     A  10 40 41                          19'--------------------------------+
     A                                      --------------------------+
     A                                      -------------------------------'
     A  10 40 42                          20'--------------------------------+
     A                                      --------------------------------+
     A                                      ----------------------'
     A                                   132' '
     A                                      SPACEA(2)
      *
     A                                     4'BRANCH'
     A            RBRCA     R             11REFFLD(BRCA)
     A            RBRNM     R             15REFFLD(BRNM)
      *
     A                                   101'CURRENCY'
     A            RRCCY     R            110REFFLD(CCY)
     A            RCYNM     R            114REFFLD(CYNM)
      *
     A                                   132' '
     A                                      SPACEA(2)
      *
     A                                     3'DEAL'
     A                                    10'COUNTERPARTY'
     A                                    40'BOOK'
     A                                    51'VALUE'
     A                                    60'MATURITY'
     A  10                                73'CURRENT PRINCIPALS'
     A N10                                73'CURRENT PRINCIPAL'
     A                                    94'------------- +
     A                                      PROFIT/LOSS -------------'
     A                                      SPACEA(1)
     A                                    51'DATE'
     A                                    60'  DATE  '
     A                                   103'NPV AMOUNT'
     A                                   118'ADJUSTED AMOUNT'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DTOT1
      *
     A            RDLNO          6         2
     A            RCNUM          6        10
     A            BBCRNM    R             17REFFLD(CRNM)
     A            RBOKC          2        41
     A            RVDAT          7        50
     A            RMDAT          7        60
     A            RUCUCY         3        70
     A            RUPAMT        19        74
     A            RDTOT         19        94
     A            RDADJ         19       114
     A                                      SPACEA(1)
      ** Start of Change BUG9501
     A**30******                           2 '* RATES MISSING - NOT VALUED *'
     A  30N31                              2 '* RATES MISSING - NOT VALUED *'
     A  31                                 2 '* CURVE MISSING - NOT VALUED *'
      ** End of Change BUG9501
     A            RTCUCY         3        70
     A            RTPAMT        19        74
     A                                      SPACEA(1)
      *
      *-----------------------------*
      ***  Book Total Record Format *
      *-----------------------------*
      *
     A          R BKTOT1
      *
     A                                    70'TOTAL FOR BOOK'
     A                                      SPACEB(1)
     A            RBOKC1         2        85HIGHLIGHT
     A            ZBKTOT        19        94HIGHLIGHT
     A            RBKTOT        19       114HIGHLIGHT
     A                                      SPACEA(2)
      *
      *---------------------------------*
      ***  Currency Total Record Format *
      *---------------------------------*
      *
     A          R CYTOT1
      *
     A                                    66'TOTAL FOR CURRENCY'
     A                                      SPACEB(1)
     A            RRCCY1         3        85HIGHLIGHT
     A            ZCYTOT        19        94HIGHLIGHT
     A            RCYTOT        19       114HIGHLIGHT
     A                                      SPACEA(2)
      *
      *
      *-------------------------------*
      ***  Branch Total Record Format *
      *-------------------------------*
      *
     A          R BRTOT1
      *
     A                                    68'TOTAL FOR BRANCH'
     A                                      SPACEB(1)
     A            RBRCA1         3        85HIGHLIGHT
     A            ZBRTOT        19        94HIGHLIGHT
     A            RBRTOT        19       114HIGHLIGHT
     A                                      SPACEA(1)
     A                                    68'IN BASE CURRENCY'
     A                                      SPACEA(1)
      *
     A          R ENDBRC1                   TEXT('END OF REPORT +
     A                                      FOR BRANCH')
      *
     A                                    43'** END OF REPORT FOR +
     A                                      BRANCH **'
      *
     A          R ENDRPT1                   TEXT('END OF REPORT')
      *
     A                                    43'** END OF REPORT **'
      *
