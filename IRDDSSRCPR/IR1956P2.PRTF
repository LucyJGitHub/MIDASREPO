      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas IR IRS Revaluation Using Base Rate Report 2')
      *****************************************************************
      *                                                               *
      *  Midas - FRA/IRS Module                                       *
      *                                                               *
      *  PRTF/IR1956P2 - IRS Revaluation Using Base Rate Report 2     *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG13098           Date 22Jan07               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAS005  *CREATE    Date 16Dec02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG13098 - Misleading column heading (currency related)      *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R PAGHD2                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE IR1956P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A N10 40N41
     AAN42
     AON10N40                             35'IRS REVALUATION USING BASE RATE +
     A                                      REPORT - SINGLE CURRENCY'
     A N10 40 41                          19'IRS REVALUATION USING BASE RATE +
     A                                      REPORT - SINGLE CURRENCY - +
     A                                      PRELIMINARY REPORT FOR'
     A N10 40 42                          20'IRS REVALUATION USING BASE RATE +
     A                                      REPORT - SINGLE CURRENCY - UP TO +
                                            BUT EXCLUDING'
     A  10 40N41
     AAN42
     AO 10N40                             35'IRS REVALUATION USING BASE RATE +
     A                                      REPORT - CROSS CURRENCY'
     A  10 40 41                          19'IRS REVALUATION USING BASE RATE +
     A                                      REPORT - CROSS CURRENCY - +
     A                                      PRELIMINARY REPORT FOR'
     A  10 40 42                          20'IRS REVALUATION USING BASE RATE +
     A                                      REPORT - CROSS CURRENCY - UP TO +
                                            BUT EXCLUDING'
     A  40 42     REVCD          7        99
     A  40 41     REVCDE         7       101
     A                                   132' '
     A                                      SPACEA(1)
      *
     A N10 40N41
     AAN42
     AON10N40                             35'--------------------------------+
     A                                      ------------------------'
     A N10 40 41                          19'--------------------------------+
     A                                      ---------------------------+
     A                                      ------------------------------'
     A N10 40 42                          20'--------------------------------+
     A                                      ---------------------------------+
     A                                      ---------------------'
     A  10 40N41
     AAN42
     AO 10N40                             35'--------------------------------+
     A                                      -----------------------'
     A  10 40 41                          19'--------------------------------+
     A                                      ---------------------------+
     A                                      ------------------------------'
     A  10 40 42                          20'--------------------------------+
     A                                      --------------------------------+
     A                                      ----------------------'
     A                                   132' '
     A                                      SPACEA(2)
      *
     A                                     4'BRANCH'
     A            RBRCA     R             11REFFLD(BRCA)
     A            RBRNM     R             15REFFLD(BRNM)
      *
     A                                   101'CURRENCY'
      **Start of change BUG13098
     A**********  RRCCY     R            110REFFLD(CCY)                                     BUG13098
     A**********  RCYNM     R            114REFFLD(CYNM)                                    BUG13098
      **End of change BUG13098
      *
     A                                   132' '
      **Start of change BUG13098
     A**********                            SPACEA(2)                                       BUG13098
     A                                      SPACEA(1)
      **End of change BUG13098
      *
     A                                     3'DEAL'
     A                                    10'OUR/'
     A                                    17'LEG'
     A                                    25'FROM'
     A                                    37'TO'
     A                                    42' VALUE '
     A                                    60'PRINCIPAL'
     A                                    72'DSC/'
     A                                    79'RATE'
     A                                    87'ACT/'
     A                                   100'AMOUNT'
     A                                   117'NPV AMOUNT'
     A                                      SPACEA(1)
     A                                    10'THEIR'
     A                                    42' DATE  '
     A                                    72'YLD'
     A                                    85'FWD/SPR'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DDETL2
      *
     A            RDLNO          6         2
     A            ROTNAR         5        10
     A            RFFNAR         5        16
     A            RSLIP          7        23
     A                                    31'-'
     A            RNIPD          7        33
     A            RPYDT          7        42
     A            RCCY           3        50
     A            RPRIN         19        53
     A            RYLD           1        73
     A            RRATE         12        75
     A            RAFNAR         1        88
     A            RAMNT         19        90
     A            RNPV          19       110
     A                                      SPACEA(1)
      *
      *-----------------------------------*
      ***  Our/Their Totals Record Format *
      *-----------------------------------*
      *
     A          R OTTOT2
      *
      **Start of change BUG13098
     A            ROTCUR         3A  O   106HIGHLIGHT
      **End of change BUG13098
     A            ROTTOT        19       110HIGHLIGHT
     A                                      SPACEA(1)
      *
      *-----------------------------------*
      ***  Deal Totals Record Format *
      *-----------------------------------*
      *
     A          R DTOT2
      *
     A                                   110'==================='
     A                                      SPACEA(1)
      **Start of change BUG13098
     A**********  RDLNO          6       100HIGHLIGHT                                       BUG13098
     A            RDLNO          6        98HIGHLIGHT
     A            RDTCUR         3A  O   106HIGHLIGHT
      **End of change BUG13098
     A            RDTOT         19       110HIGHLIGHT
     A                                      SPACEA(1)
     A                                   110'==================='
     A                                      SPACEA(1)
     A                                    55'Adjustment for accrued +
     A                                      interest posted this +
     A                                      period'
     A            RDACR         19       110HIGHLIGHT
     A                                      SPACEA(1)
     A                                   110'==================='
     A                                      SPACEA(1)
     A                                     2'--------------------+
     A                                      --------------------+
     A                                      --------------------+
     A                                      --------------------+
     A                                      -------- Adjusted'
     A            RDLNO1         6       100HIGHLIGHT
     A            RDADJ         19       110HIGHLIGHT
     A                                      SPACEA(1)
     A                                   110'==================='
     A                                      SPACEA(1)
      *
     A          R ENDBRC2                   TEXT('END OF REPORT +
     A                                      FOR BRANCH')
      *
     A                                    43'** END OF REPORT FOR +
     A                                      BRANCH **'
      *
     A          R ENDRPT2                   TEXT('END OF REPORT')
      *
     A                                    43'** END OF REPORT **'
      *
