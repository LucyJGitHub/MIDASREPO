      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas IR Internal Table Report -List')                 *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module - Italy.                   *
      *                                                               *
      *  PRTF/IR5000P1 - Internal Table Report -List                  *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 14Jan21               *
      *  Prev Amend No. UIT022             Date 16Jan97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  UIT022 - Telegramma Decadale                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(C) Midas-Kapiti +
     A                                      International Ltd. 1997')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE IR5000P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'Internal Table Report'
     A                                      SPACEA(1)
     A                                    40'---------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                    05'TABLE :'
     A            RTABL          3A       14
     A            RTABN         30A       18
     A                                      SPACEA(001)
      *
     A                                    02'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------------------'
     A                                      SPACEA(002)
      *
      *---------------------------*
      ***  Comment line format  ***
      *---------------------------*
      *
     A
     A          R COMMP1                    SPACEA(001)
      *
     A            RCOMM         60        18
      *
      *-------------------*
      ***  Line format  ***
      *-------------------*
      *
     A          R LINEP1                    SPACEA(001)
     A                                    15':---------------------------------+
     A                                      ----------------------------------:'
      *
      *--------------------------------*
      ***  Table Header line Format  ***
      *--------------------------------*
      *
     A          R DETLP1                    SPACEA(001)
     A  70N71                              8'Header'
     A N70 71                              8'Code'
     A                                    15':'
     A            RKEY          18        18
     A                                    38':'
     A            RNARR         40        41
     A                                    83':'
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
