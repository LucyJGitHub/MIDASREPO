     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IR Voce/sottovoce Codes - PUMA 2 List')          *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module - Italy.                   *
      *                                                               *
      *  IR0028 - Voce/sottovoce Codes - PUMA 2 List.                 *
      *                                                               *
      *  Function:  This program will list the codes contained in     *
      *             file ERITCDPD.                                    *
      *                                                               *
      *  (phase(s)) I/C on request.                                   *
      *                                                               *
      *  Called By: CLP/IRC0028 - Voce/Sottovoce Codes - PUMA 2 List. *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 13Jan21               *
      *  Prev Amend No. UIT021             Date 17apr97               *
      *  Prev Amend No.                    Date                       *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  UIT021 - BOI Monthly Reporting.                              *
      *                                                               *
      *****************************************************************
      *
     FERITCDL1IF  E           K        DISK
      *
      ***  Voce/Sottovoce codes - Retrieval Index - Prefix N2
      *
     FIR0028AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
     FIR0028P1O   E                    PRINTER
     F                                              KINFDS SPOOL1
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *  ===========================================                  *
      *                                                               *
      *  89 - End of File ERITCDPD.                                   *
      *                                                               *
      *  U7 - Database Error.                                         *
      *  U8 - Program Error.                                          *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *  ===============================                              *
      *                                                               *
      *  INIT   - Program Initialisation.                             *
      *  PROCES - Detail Processing.                                  *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  WP1REC - Write a Detail Record to the Report.                *
      *  WP1END - Write End of Report.                                *
      *                                                               *
      *  AUDIT  - Produce Audit Report and End Program.               *
      *  *PSSR  - Program Error Processing Subroutine.                *
      *  RCFP1  - RCF Processing for P1 Report.                       *
      *  RCFAU  - RCF Processing for Audit Report.                    *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  E X T E R N A L   P R O G R A M S   I N D E X                *
      *  =============================================                *
      *                                                               *
      *  AOBANKR0 - Access Program to Access Bank Details.            *
      *  ZSFILE   - Midas FC Set-Up Spool File Numbers File.          *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      *
      ***  Array containing Copyright statement.
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ***  Local data area for database error details
      ***  *LOCK IN LDA immediately before and OUT LDA immediately
      ***  after each database error handling.
      *
     IDBERR       DS
     I                                      134 141 WBFILE
     I                                      142 170 WBKEY
     I                                      171 180 WBPGM
     I                                      181 1830WBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ***  Data Area giving Installation Control Details.
      *
     IPSDS       SDS
      *
      ***  Program Status Data Structure.
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      *
      ***  File Information Data Structure for IR0028P1.
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      *
      ***  File Information Data Structure for IR0028AU.
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      ***  Dummy Data Structures used by Access Programs.
      *
     IDSFDY     E DSDSFDY
      *
      *** First DS for Access Pgms, short format.
      *
     ISDBANK    E DSSDBANKPD
      *
      ***  External data structures for Bank Details.
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      ***  Output Audit Report and End Program.
      *
     C                     EXSR AUDIT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  INIT - Program Initialisation.                               *
      *                                                               *
      *  Called By: Main Processing.                                  *
      *                                                               *
      *  Calls: RCFAU    - RCF Processing for Audit Report,           *
      *         *PSSR    - Program Error Processing Subroutine,       *
      *         AOBANKR0 - Access Program to Access Bank Details.     *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ***  Set up copyright parameter.
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           #SEQ    5
     C                     PARM           #RLEV   1
     C                     PARM           #RENT   3
      *
      ***  Initialise LDA field.
      *
     C                     MOVEL'IR0028'  WBPGM
      *
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVE 'SDBANKPD'WBFILE           ***************
     C                     Z-ADD001       WBASE            * DB ERROR 01 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF
      *
      **   Access RUNDAT for multibranching indicator.
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
      ***  RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ***  RCF Processing for P1 File.
      *
     C                     EXSR RCFP1
      *
      ***  Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Detail Processing.                                  *
      *                                                               *
      *  Called By: Main Processing.                                  *
      *                                                               *
      *  Calls: AUDIT  - Produce Audit Report and End Program,        *
      *         REPORT - Process Report Lines,                        *
      *         WP1END - Write End of Report.                         *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR                           *** PROCES ***
      *
      ***  Read first record from File.
      *
     C                     READ ERITCDL1                 89
      *
      ***  If End of Records, (*IN89), Perform: Audit Reporting (No
      ***  Records).
      *
     C           *IN89     IFEQ *ON
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  Do Until End of File.
      *
     C           *IN89     DOWEQ*OFF
      *
      ***  Count records read.
      *
     C                     ADD  1         RCOUNT
      *
      ***  Process Report Lines.
      *
     C                     EXSR REPORT
      *
      ***  Read next record.
      *
     C                     READ ERITCDL1                 89
      *
      ***  End of Do Until End of Valid Records.
     C                     ENDDO
      *
      *
      ***  Write final records and Totals to Report.
      *
     C                     EXSR WP1END
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/REPORT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES - Detail Processing.                       *
      *                                                               *
      *  Calls: WP1REC - Format and Write a Detail Record to the      *
      *                  Report.                                      *
      *                                                               *
      *****************************************************************
      *
     C           REPORT    BEGSR                           *** REPORT ***
      *
     C                     MOVELN2BOIC    R1BOIC           Voce Code
     C                     MOVE N2BOIC    R2BOIC           Sottovoce Code
      *
     C                     MOVE N2NARR    RNARR            Narrative
      *
     C                     MOVE N2FREQ    RFREQ            Frequency
      *
      ***  Write out the record.
      *
     C                     EXSR WP1REC
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *                                                               *
      *  WP1REC - Write a Detail record to the Report.                *
      *                                                               *
      *  Called By: REPORT - Process Report Lines.                    *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           WP1REC    BEGSR                           *** WP1REC ***
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Detail Record.
      *
     C                     WRITEDETLP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Write End of Report.                                *
      *                                                               *
      *  Called By: PROCES - Detail Processing.                       *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Total Format.
      *
     C                     WRITETRAILP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT - Produce Audit report and End Program.                *
      *                                                               *
      *  Called By: Main Processing,                                  *
      *             PROCES - Detail Processing,                       *
      *             *PSSR  - Program Error Processing Subroutine.     *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  Output Report Header.
      *
     C                     WRITEHEADAU
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ELSE
      *
      ***  Or, if records read, Output File Controle.
      *
     C                     WRITECONTROL
     C                     ENDIF
     C                     ENDIF
      *
      ***  End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR - Program Error Processing Subroutine.                 *
      *                                                               *
      *  Called By: INIT - Program Initialisation.                    *
      *                                                               *
      *  Calls: AUDIT - Produce Audit Report and End Program.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVE DBERR     WLDA
     C                     OUT  WLDA
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1 - RCF Processing for P1 Report.                        *
      *                                                               *
      *  Called By: INIT - Program Initialisation.                    *
      *                                                               *
      *  Calls: ZSFILE - Midas FC Set-Up Spool File Numbers File.     *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM #SEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU - RCF Processing for Audit Report.                     *
      *                                                               *
      *  Called By: INIT - Program Initialisation.                    *
      *                                                               *
      *  Calls: ZSFILE - Midas FC Set-Up Spool File Numbers File.     *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM #SEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      *
**  CPY@
(C) Copyright Midas-Kapiti International Ltd. 1997
