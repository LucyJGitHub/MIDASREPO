     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IR Daily Balance Calculation')                   *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting - Italy                           *
      *                                                               *
      *  IR0180 - Daily Balance Calculation                           *
      *                                                               *
      *  Function:  This program read every day (during close of      *
      *  (phase(s)) business) and recalculate each figures which      *
      *             need a daily maintenance.                         *
      *             This run in Daily EOD after daily posting         *
      *                                                               *
      *  Called By: IRC0180 - Daily Balance Calculation               *
      *             EOD run : parameter TMODE is 'D'                  *
      *             EOM run : Parameter TMODE is 'T'
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 13Jan21               *
      *  Prev Amend No. 193574             Date 01Jun01               *
      *                 UIT021             Date 03feb97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  193574 - Do not issue database error anymore if extension    *
      *           record is not found in main file ACCNTAB.  Delete   *
      *           the extension record instead.                       *
      *  UIT021 - BOI Monthly Reporting                               *
      *                                                               *
      *****************************************************************
     FACCNTBXPUF  E           K        DISK
      ***  Details Accnt ext. file                         Prefix 'IA'
      *
     E/COPY ZSRSRC,ZDATE2Z1
      ***  Standard subroutines arrays
      *
     E                    CPY@    1   1 80
      ***  Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      ***  Local data area for database error details
      *
     IPSDS       SDS
      ***  Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ***  Working Data Structures
      *
     IDSFDY     E DSDSFDY
      ***  First Data structure for Access object program
      *
     IDSSDY     E DSDSSDY
      ***  Second Data structure for Access object program
      *
     ISDBANK    E DSSDBANKPD
      ***  Bank details
      *
     ISDACOD    E DSSDACODPD
      ***  Account codes details
      *
     IACCNTD    E DSACCNTAB
      ***  Account details
      *
     IIRSTAT    E DSIRSTAT
      ***  Italian Returns Status data area
      *
      /EJECT
      *
      *****************************************************************
      *                       M A I N L I N E S                       *
      *****************************************************************
      *
      ***  Read All record
      *
     C                     READ ACCNTBDP                 89
      *
     C           *IN89     DOWEQ*OFF
      *
      ***  Access Account details/ Account details
     C                     EXSR #ACCN
      *                                                                   193574
      ***  Continue processing if extension record is in PF/ACCNTAB       193574
     C           *IN07     IFEQ '0'                                       193574
      *
      ***  Perform Average Balance Calculation
     C                     EXSR #AVER
      *
      ***  Perform Numbers Balance Calculation (Numeri)
     C                     EXSR #NUME
      *
      ***  Perform EOM processing if required (IRSTAT/IREMT pos 8)
      *
     C           IREMT     IFNE ' '
     C                     EXSR #EOMP
     C                     ENDIF
      *
     C                     UPDATACCNTBDP
      *                                                                   193574
     C                     ENDIF                                          193574
      *                                                                   193574
     C                     READ ACCNTBDP                 89
     C                     ENDDO
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      *                E N D    O F   M A I N L I N E S               *
      *****************************************************************
      *
      *****************************************************************
      *   #ACCN  - Access account details / Account code details      *
      *   ------                                                      *
      *   Called by : Mainlines                                       *
      *   Calls     :                                                 *
      *****************************************************************
      *
     C           #ACCN     BEGSR
      *
      ***  Access Accound details
      *
     C                     MOVE IACNUM    @CUST
     C                     MOVE IACCY     @CCY
     C                     MOVE IAACOD    @ACOD
     C                     MOVE IAACSQ    @ACSQ
     C                     MOVE IABRCA    @BRCA
     C                     CALL 'AOACCTR0'
     C                     PARM '       ' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM '       ' @FILL  10
     C                     PARM           @CUST   6
     C                     PARM           @CCY    3
     C                     PARM           @ACOD
     C                     PARM           @ACSQ   2
     C                     PARM           @BRCA   3
     C           ACCNTD    PARM ACCNTD    DSSDY
      *
     C                     MOVE '0'       *IN07                           193574
     C           @RTCD     IFNE *BLANKS
     C           @RTCD     IFEQ '*NRF   '                                 193574
     C                     MOVE '1'       *IN07                           193574
     C                     DELETACCNTBDP                                  193574
     C                     ELSE                                           193574
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVEL'ACCNTAB 'DBFILE           * DBERROR 002 *
     C           @CUST     CAT  @CCY:0    DBKEY     P      ***************
     C           DBKEY     CAT  @ACOD:0   DBKEY
     C           DBKEY     CAT  @ACSQ:0   DBKEY
     C           DBKEY     CAT  @BRCA:0   DBKEY
     C                     MOVELPGM       DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF                                          193574
      *
      ***  Access Accound code details
      *
     C                     CALL 'AOACODR0'
     C                     PARM '       ' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @ACOD  10
     C           SDACOD    PARM SDACOD    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE            ***************
     C                     MOVEL'SDACODPD'DBFILE           * DBERROR 003 *
     C                     MOVEL@ACOD     DBKEY     P      ***************
     C                     MOVELPGM       DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      *   #AVER  - Average calculation                                *
      *   ------                                                      *
      *   Called by : Mainlines                                       *
      *   Calls     :                                                 *
      *****************************************************************
      *
     C           #AVER     BEGSR
      *
      ***  Average Book balance in currency
      *
     C                     Z-ADDLDBL      WWBALA 150
     C                     Z-ADDIAAVBK    WWOLDB 150
     C                     EXSR #AVBL
     C                     Z-ADDWWNEWB    IAAVBK
      *
      ***  Assets average book balance in currency
      ***  Assets average cleared balance in currency
      *
     C           A5ACSC    IFEQ 'AS'                       Assets account code
     C                     Z-ADDLDBL      WWBALA
     C                     Z-ADDIAAAVB    WWOLDB
     C                     EXSR #AVBL
     C                     Z-ADDWWNEWB    IAAAVB
      *
     C                     Z-ADDCLBL      WWBALA
     C                     Z-ADDIAAACB    WWOLDB
     C                     EXSR #AVBL
     C                     Z-ADDWWNEWB    IAAACB
      *
     C                     ENDIF
      *
      ***  Liabilities average book balance in currency
      ***  Liabilities average cleared balance in currency
      *
     C           A5ACSC    IFEQ 'LI'                       Liabilities accnt cd
     C                     Z-ADDLDBL      WWBALA
     C                     Z-ADDIALAVB    WWOLDB
     C                     EXSR #AVBL
     C                     Z-ADDWWNEWB    IALAVB
      *
     C                     Z-ADDCLBL      WWBALA
     C                     Z-ADDIALACB    WWOLDB
     C                     EXSR #AVBL
     C                     Z-ADDWWNEWB    IALACB
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      *   #NUME  - Numbers calculation (numeri)                       *
      *   ------                                                      *
      *   Called by : Mainlines                                       *
      *   Calls     :                                                 *
      *****************************************************************
      *
     C           #NUME     BEGSR
      *
      ***  Debit not capitalized
     C                     Z-ADDIADMNC    WWBALA 150
     C                     Z-ADDIADNNC    WWOLDB 150
     C                     EXSR #NUBL
     C                     Z-ADDWWNEWB    IADNNC
      *
      ***  Debit capitalized
     C                     Z-ADDIADMCP    WWBALA 150
     C                     Z-ADDIADNCP    WWOLDB 150
     C                     EXSR #NUBL
     C                     Z-ADDWWNEWB    IADNCP
      *
      ***  Credit not capitalized
     C                     Z-ADDIACMNC    WWBALA 150
     C                     Z-ADDIACNNC    WWOLDB 150
     C                     EXSR #NUBL
     C                     Z-ADDWWNEWB    IACNNC
      *
      ***  Credit capitalized
     C                     Z-ADDIACMCP    WWBALA 150
     C                     Z-ADDIACNCP    WWOLDB 150
     C                     EXSR #NUBL
     C                     Z-ADDWWNEWB    IACNCP
      *
     C                     ENDSR
      *
      *****************************************************************
      *   #AVBL  - Average Balance calculation :                      *
      *   ------                                                      *
      *   input :  WWBALA : A/C Bal to add in current average balance *
      *            WWOLDB : Current average balance                   *
      *            WNBRD1 : Number of days of previous average        *
      *            WNBRD2 : Number of days for new average            *
      *   output : WWNEWB : New average balance                       *
      *   Formule :                                                   *
      *              (WWOLDB * WNBRD1) + (WWBALA * (WNBRD2 - WNBRD1)) *
      *     WWNEWB = ------------------------------------------------ *
      *                               WNBRD2                          *
      *   Called by : #AVER                                           *
      *   Calls     :                                                 *
      *****************************************************************
      *
     C           #AVBL     BEGSR
      *
     C           WWOLDB    MULT WNBRD1    WW0001 200       WWOLD * WNBRD1
      *
     C           WNBRD2    SUB  WNBRD1    WW0002  50       WNBRD2 - WNBRD1
     C           WWBALA    MULT WW0002    WW0003 200       * WWBALA
      *
     C           WW0001    ADD  WW0003    WW0004 200       --- Sum ------
      *
     C           WW0004    DIV  WNBRD2    WWNEWB 150H       --- Div -----
      *
     C                     ENDSR
      *
      *****************************************************************
      *   #NUBL  - Nubers Figures (numeri) calculation :              *
      *   ------                                                      *
      *   input :  WWBALA : Relative balance                          *
      *            WWOLDB : Current numeri amount                     *
      *            WNBRD1 : Number of days of previous average        *
      *            WNBRD2 : Number of days for new average            *
      *   output : WWNEWB : New numeri                                *
      *   Formule :  WWNEWB = WWOLDB + (WWBALA * (WNBRD2 - WNBRD1))   *
      *   Called by : #NUME                                           *
      *   Calls     :                                                 *
      *****************************************************************
      *
     C           #NUBL     BEGSR
      *
     C           WNBRD2    SUB  WNBRD1    WW0002           WNBRD2 - WNBRD1
     C           WWBALA    MULT WW0002    WW0003           * WWBALA
      *
     C           WWOLDB    ADD  WW0003    WWNEWB           --- Sum ------
      *
     C                     ENDSR
      *
      *****************************************************************
      *   #EOMP  - End of month processing                            *
      *   ------                                                      *
      *   Called by : Mainlines when IREMT = M,T,S or Y               *
      *   Calls     :                                                 *
      *****************************************************************
      *
     C           #EOMP     BEGSR
      *
     C           IREMT     IFEQ 'S'
     C           IREMT     OREQ 'T'
     C           IREMT     OREQ 'M'
      *
      ***  Perform Monthly processing
      *
     C                     ADD  IADMNC    IADQNC
     C                     ADD  IACMNC    IACQNC
     C                     ADD  IADMCP    IADQCP
     C                     ADD  IACMCP    IACQCP
     C                     ADD  IADMMV    IADQMV
     C                     ADD  IACMMV    IACQMV
      *
     C                     ENDIF
      *
     C           IREMT     IFEQ 'S'
     C           IREMT     OREQ 'T'
      *
      ***  Perform Quaterly processing
      *
     C                     ADD  IADQNC    IADHNC
     C                     ADD  IACQNC    IACHNC
     C                     ADD  IADQCP    IADHCP
     C                     ADD  IACQCP    IACHCP
     C                     ADD  IADQMV    IADHMV
     C                     ADD  IACQMV    IACHMV
      *
     C                     ENDIF
      *
     C           IREMT     IFEQ 'S'
      *
      ***  Perform Six-monthly processing
      *
     C                     ADD  IADHNC    IADYNC
     C                     ADD  IACHNC    IACYNC
     C                     ADD  IADHCP    IADYCP
     C                     ADD  IACHCP    IACYCP
     C                     ADD  IADHMV    IADYMV
     C                     ADD  IACHMV    IACYMV
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      *   *INZSR - Initial subroutine                                 *
      *   ------                                                      *
      *   Called by : Pgm start                                       *
      *   Calls     :                                                 *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
      ***  Entry Parameter : Tmode : Daily/End of month date
      *
     C           *ENTRY    PLIST
     C                     PARM           TMODE   1
      *
      ***  Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Read in Installation Data
      *
     C           *NAMVAR   DEFN           LDA
      *
      ***  Read Italian Returns Status data area
      *
     C           *NAMVAR   DEFN           IRSTAT
     C                     IN   IRSTAT
      *
      ***  Retrieve Run date and next working date (SDBANKPD)
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           * DBERROR 001 *
     C                     MOVEL'*FIRST'  DBKEY     P      ***************
     C                     MOVELPGM       DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           TMODE     IFNE 'D'
     C                     EXSR #MRUN                      Monthly run
     C                     ELSE
     C                     EXSR #DRUN                      Daily run
     C                     ENDIF
      *
      ***  Evaluate Number days for average balance processing
      ***  WNBRD1 = Prev. calc. proc. date - Last day of previous month
      ***  WNBRD2 = Calculation processing date - Last day of previous month
      ***  thus
      ***  WNBRD1 = WXPCPD - WXLDPM
      ***  WNBRD2 = WXCPDT - WXLDPM
      *
     C                     MOVELBJMRDT    DAY02   20
     C           BJRDNB    SUB  DAY02     WXLDPM  50       Lst day of Prv mnth
      *
     C           WXPCPD    SUB  WXLDPM    WNBRD1  50
     C           WXCPDT    SUB  WXLDPM    WNBRD2  50
      *
     C                     ENDSR
      *
      *****************************************************************
      *   #DRUN  - Daily run - Evaluate EOM calendar = WWEOMC ,       *
      *   ------               Previous calc. proc. date = WXPCPD     *
      *                        and Calc processing date = WXCPDT.     *
      *                        WXPCPD = Rundate - 1                   *
      *                        WXCPDT = Min(WWEOMC; Next wrkg date -1)*
      *                        WWEOMC = 01/next month - 1day          *
      *   Called by :                                                 *
      *   Calls     :                                                 *
      *****************************************************************
      *
     C           #DRUN     BEGSR
      *
      ***  Previous calculation processing date is Rundate - 1
      *
     C           BJRDNB    SUB  1         WXPCPD  50       Prev Calc Proc date
      *
      ***  Calc. proc. date is shorter date between EOM calendar date
      ***                            and Next working date - 1
      *
     C           BJDNWD    SUB  1         WWDNWD  50       Nxt wrkg date - 1
      *
      ***  EOM calendar : 01 next month - 1
      *
     C                     MOVE *OFF      *IN98             DDMMYY
     C                     MOVE BJRDNB    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     WWMMYY  40
     C                     MOVELWWMMYY    WWMONT  20
     C                     ADD  1         WWMONT            month + 1
      *
     C           WWMONT    IFLT 13
     C                     MOVELWWMONT    WWMMYY
     C                     ELSE
     C                     ADD  1         WWMMYY            Year + 1
     C                     MOVEL'01'      WWMMYY
     C                     ENDIF
     C                     MOVE WWMMYY    ZDATE
     C                     MOVEL01        ZDATE
      *
     C                     EXSR ZDATE1
     C           ZDAYNO    SUB  1         WWEOMC  50       end of month Cal
      *
     C           WWDNWD    IFLT WWEOMC
     C                     Z-ADDWWDNWD    WXCPDT  50       Calc proc. date
     C                     ELSE
     C                     Z-ADDWWEOMC    WXCPDT  50
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      *   #MRUN  - Monthly run - Evaluate EOM calendar = WWEOMC ,     *
      *   ------                 Previous calc. proc. date = WXPCPD   *
      *                          and Calc processing date = WXCPDT.   *
      *                          WXPCPD = WWEOMC                      *
      *                          WXCPDT = Next working date - 1day    *
      *                          WWEOMC = 01/next month - 1day        *
      *   Called by :                                                 *
      *   Calls     :                                                 *
      *****************************************************************
      *
     C           #MRUN     BEGSR
      *
      ***  Calculation processing date is Next working date - 1
      *
     C           BJDNWD    SUB  1         WXCPDT  50       Calc Proc date
      *
      ***  Previous calculation processing date is EOMdate
      *
      ***  EOM calendar : 01 next month - 1 day
      *
     C                     MOVE *OFF      *IN98             DDMMYY
     C                     MOVE BJRDNB    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     WWMMYY  40
     C                     MOVELWWMMYY    WWMONT  20
     C                     ADD  1         WWMONT            month + 1
      *
     C           WWMONT    IFLT 13
     C                     MOVELWWMONT    WWMMYY
     C                     ELSE
     C                     ADD  1         WWMMYY            Year + 1
     C                     MOVEL'01'      WWMMYY
     C                     ENDIF
     C                     MOVE WWMMYY    ZDATE
     C                     MOVEL01        ZDATE             01mmyy (next month)
      *
     C                     EXSR ZDATE1
     C           ZDAYNO    SUB  1         WWEOMC  50       end of month Cal
      *
     C                     Z-ADDWWEOMC    WXPCPD  50       Prev. calc. prc. date
      *
     C                     ENDSR
      *
      *****************************************************************
      *   *PSSR  - Program exception error routine                    *
      *   ------  Called automatically if a program error occurs,     *
      *           or directly by the program code using EXSR.         *
      *   Called by :                                                 *
      *   Calls     :                                                 *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *
      ***  Copy in Calculation Specs. for Standard subroutine
      *
     C/COPY ZSRSRC,ZDATE1Z2
     C/COPY ZSRSRC,ZDATE2Z2
      *
      ***  Copy After Output Specs. for Arrays used by standard subroutine
      *
      /COPY ZSRSRC,ZDATE2Z3
**  CPY@
(C) Copyright Midas-Kapiti International Ltd. 1997
