     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IR Customer Details Extract')                    *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting - Italy                           *
      *                                                               *
      *  IR0200 - Customer Details Extract                            *
      *                                                               *
      *  Function:  This program creates output file for Anagrafica   *
      *             report (PF/IREXCLPD) : Customer details           *
      *             input or amended during the day.                  *
      *                                                               *
      *  (phase(s)) EOM run                                           *
      *                                                               *
      *  Called By: IRC0200 - Customer Details Extract                *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 13Jan21               *
      *  Prev Amend No. 143734             Date 15SEP98               *
      *                 MMI032             Date 02SEP98               *
      *                 UIT021 * amend *   Date 15Jul98               *   UIT021
      *                 UIT021             Date 07fev97               *   UIT021
      *                                                               *   UIT021
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  143734 - Obtain Province Code from Fixed Table               *
      *  MMI032 - FICS BOI Interface amendment to incorporate         *
      *           data dic 3.0 changes                                *
      *  UIT021 - BOI Monthly Reporting * amend * changes for dd 2.6.3*   UIT021
      *  UIT021 - BOI Monthly Reporting                               *
      *                                                               *
      *****************************************************************
     F*DCUSTL1IP  E           K        DISK         KINFSR *PSSR          MMI032
     FSDCUSTL5IP  E           K        DISK         KINFSR *PSSR          MMI032
      ***  IR Customer Details
      *
     FSDCUT2L0IF  E           K        DISK         KINFSR *PSSR
      ***  IR Customer Ext Details                         Prefix '  '
      *
     FSDCTT1L0IF  E           K        DISK         KINFSR *PSSR
      ***  IR Country Ext Details                          Prefix 'PE'
      *
     FSDBCT1L0IF  E           K        DISK         KINFSR *PSSR
      ***  IR branch Ext Details                          Prefix 'PA'
     FIRFXTBL1IF  E           K        DISK         KINFSR *PSSR          143734
      *
     FIREXCLPDO   E                    DISK         KINFSR *PSSR
      ***  Extracted Customer details                      Prefix 'ID'
      *
      ***  Array for ZDATE2 subroutine : Day Nr to Date
      *
      /COPY ZSRSRC,ZDATE2Z1
      *
      ***  Array for ZDATE9 subroutine : Day Nr to YYYYMMDD
      *
      /COPY ZSRSRC,ZDATE9Z1
      *
     E                    CPY@    1   1 80
      ***  Array containing Copyright statement
      *
     I*BBREBF**** 01                                                   MMI032
     I@CUSTFT     01
      *
     ILDA       E DSLDA
      ***  Local data area for database error details
      *
     IPSDS       SDS
      ***  Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *------------------------------*
      ***  Working Data Structure  ***
      *------------------------------*
     IIRSTAT    E DSIRSTAT
      ***  Italian Returns Status data area
      *
     IDSFDY     E DSDSFDY
      ***  First data Structure for Access Object programs (Short DS)
      *
     IDSSDY     E DSDSSDY
      ***  Second DS for AO programs (Long DS)
      *
     ISDBANK    E DSSDBANKPD
      ***  Bank details
      *
     ISDCUST    E DSSDCUSTPD
      ***  Customer Details
      *
     ISDCTRY    E DSSDCTRYPD
      ***  Country Details
      *
     I            DS
     I                                        3   4 MONTH
     I                                        1   60ZDATE
      *
      ***  DS for ZDATE9 subroutine : Day Nr to YYYYMMDD
      *
      /COPY ZSRSRC,ZDATE9Z2
      *
     I            DS
     I                                        1   20DATEDD
     I                                        3   40DATEMM
     I                                        5   80DATEYY
     I                                        1   80DATE08
      *
      *****************************************************************
      *                       M A I N L I N E S                       *
      *****************************************************************
      *
     C           *IN01     IFEQ *ON
      *
      ***  Reset record
     C                     CLEARIREXCLD0
      *
      ***  Fill IREXCLPD details
     C                     EXSR #FILL
      *
      ***  Add  the new record
     C                     WRITEIREXCLD0
      *
     C                     ENDIF
      *
     CLR                   Z-ADDWWRRNV    IRSQCT
     CLR                   OUT  IRSTAT
      *
      *****************************************************************
      *                E N D    O F    M A I N L I N E S              *
      *****************************************************************
      *
      /EJECT
      *****************************************************************
      *  #Fill  - Fill extracted customer details file                *
      *  ------                                                       *
      *  Called By : Mainlines                                        *
      *  Calls     :                                                  *
      *****************************************************************
      *
     C           #FILL     BEGSR
      *
      ***  Record type is 002
     C                     Z-ADD002       IDRTYP
      *
      ***  Audit trail data : Customer number + Customer short name
     C                     MOVELBBCUST    IDAUTR
     C                     MOVE BBCSSN    IDAUTR
      *
      ***  Sequential counter
     C                     ADD  1         WWRRNV
     C                     Z-ADDWWRRNV    IDSQCT
      *
      ***  UIC Country Code Counterparty :
      ***     For non-resident : SDCTRYXP  UIC contry code
      ***     For resident     : SDCUSTXP  CAB Provincia
     C           BBCUST    CHAINSDCUT2L0             88
     C           *IN88     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE            ***************
     C                     MOVEL'SDCUT2L0'DBFILE           * DBERROR 004 *
     C                     MOVE BBCUST    DBKEY     P      ***************
     C                     MOVELPGM       DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***   Provincie code is 5.0 and UIC Ctry code is 3.0
      ***   do not use this field to store province code --> field C104
      ***   always filled with UIC country code.
      ***        BBCOLC    IFEQ BJCNCD                     Resident
     C***                  ELSE                            Non-Resident
      *
     C           BBCOLC    CHAINSDCTT1L0             88
     C           *IN88     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD6         DBASE            ***************
     C                     MOVEL'SDCTT1L0'DBFILE           * DBERROR 006 *
     C                     MOVE BBCOLC    DBKEY     P      ***************
     C                     MOVELPGM       DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     Z-ADDPEUCTR    IDUCTR
      *
     C***                  ENDIF
      *
      ***  Economical Subgroup Classification from Cust Ext detail.
     C                     Z-ADDPCECSG    IDECSG
      *
      ***  Economical Branch Classification from Cust Ext. details
     C                     Z-ADDPCECBC    IDECBC
      *
      ***  Province of Counterparty
     C********             Z-ADDPCPRCT    IDPRCT                          143734
      *                                                                   143734
     C           KFXTB     KLIST                                          143734
     C                     KFLD           KINTC   3                       143734
     C                     KFLD           KINTK  18                       143734
      *                                                                   143734
      * Access description of Province (if present)                       143734
      *                                                                   143734
     C                     Z-ADD99999     IDPRCT                          143734
     C           PCPROV    IFNE *BLANKS                                   143734
     C                     MOVE 'BBK'     KINTC                           143734
     C                     MOVE *BLANKS   KINTK                           143734
     C                     MOVELPCPROV    KINTK                           143734
     C           KFXTB     CHAINIRFXTBL1             88                   143734
     C           *IN88     IFEQ '0'                                       143734
     C                     MOVE F0PNAR    WRK05   5                       143734
     C           WRK05     IFNE *BLANKS                                   143734
     C                     MOVE WRK05     IDPRCT                          143734
     C                     END                                            143734
     C                     END                                            143734
     C                     END                                            143734
      *
      ***  Internal code CR zone (code sportello from branch)
     C           BBBRCD    IFNE WWBRCD
      *
     C           BBBRCD    CHAINSDBCT1L0             88
     C           *IN88     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD7         DBASE            ***************
     C                     MOVEL'SDBCT1L0'DBFILE           * DBERROR 007 *
     C                     MOVE BBBRCD    DBKEY     P      ***************
     C                     MOVELPGM       DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE BBBRCD    WWBRCD  3
     C                     ENDIF
      *
     C                     Z-ADDPAORSP    IDICCR
     C*********            Z-ADDPAONSP    IDOCCR                          UIT021
     C                     MOVE PAONSP    IDOCCR                          UIT021
      *
      ***  Client Internal Number = Customer Number
     C                     MOVELBBCUST    IDCUST
      *
      ***  Guarantor Internal Number = Customer Number
     C                     MOVELBBCUST    IDGUAR
      *
      ***  Client CR name from Ext. Details
     C                     MOVELPCCRNM    IDCRNM
      *
      ***  Fiscal Code Counterparty/Issuer CR from Ext. Details
     C                     MOVELPCFCCR    IDFCCR
      *
      ***  H.O. Client Location from Ext. Details
     C                     MOVELPCHOLC    IDHOLC
      *
      ***  CAB code H.O. Location from Ext. Details
     C                     MOVELPCHLCD    IDHLCD
      *
      ***  Client address from Ext details
     C                     MOVELPCCUAD    IDCUAD
      *
      ***  Client City from Ext. Details.
     C                     MOVELPCCUCT    IDCUCT
      *
      ***  CCIAA Registered Nr from Ext. Details
     C                     MOVELPCCCCN    IDCCCN
      *
      ***  CCIAA H.O. Client Location from Ext. Details
     C                     MOVELPCCCCL    IDCCCL
      *
      ***  CR Customer Code from Ext. Details
     C                     MOVELPCCUCR    IDCUCR
      *
      ***  Customer Internal Name from Customer details
     C                     MOVELBBCRNM    IDCNAM
      *
      ***  Doubtfull Code from Ext. Details
     C                     Z-ADDPCDBCD    IDCUDB
      *                                                                   MMI032
      ***  Number of Message                                              MMI032
     C                     Z-ADD*ZEROS    IDNOMG                          MMI032
      *                                                                   MMI032
      ***  Indicator of Documented Message                                MMI032
     C                     MOVE *BLANKS   IDINDM                          MMI032
      *                                                                   MMI032
      ***  Personal Data Operation Code                                   MMI032
     C                     Z-ADD*ZEROS    IDPDOC                          MMI032
      *                                                                   MMI032
      ***  Request of New Code                                            MMI032
     C                     MOVE *BLANKS   IDRONC                          MMI032
      *                                                                   MMI032
      ***  Indicator of Documented CCIAA                                  MMI032
     C                     MOVE *BLANKS   IDINDC                          MMI032
      *                                                                   MMI032
      ***  Indicator of Notice Reported out of Maximum Delay              MMI032
     C                     MOVE *BLANKS   IDNMRD                          MMI032
      *                                                                   MMI032
      ***  Indicator of Documented Fiscal Code                            MMI032
     C                     MOVE *BLANKS   IDIDFC                          MMI032
      *                                                                   MMI032
      ***  Request Code of First Notification                             MMI032
     C                     MOVE *BLANKS   IDRCFN                          MMI032
      *                                                                   MMI032
      ***  Reference Date of First Notification                           MMI032
     C                     Z-ADD*ZEROS    IDRDFN                          MMI032
      *                                                                   MMI032
      ***  Level of Response                                              MMI032
     C                     MOVE *BLANKS   IDLVOR                          MMI032
      *                                                                   MMI032
      ***  Position Towards Credit Group                                  MMI032
     C                     MOVE *BLANKS   IDPTCG                          MMI032
      *                                                                   MMI032
      ***  Qualification of Identification of Individual                  MMI032
     C                     MOVE *BLANKS   IDQOII                          MMI032
      *                                                                   MMI032
      ***  Indicator of Ceased Register                                   MMI032
     C                     MOVE *BLANKS   IDIOCR                          MMI032
      *                                                                   MMI032
      ***  Head Office Location                                           MMI032
     C                     MOVE *BLANKS   IDHOLO                          MMI032
      *                                                                   MMI032
      ***  Code of Mean of Transmission                                   MMI032
     C                     MOVE *BLANKS   IDCOMT                          MMI032
      *
      ***  CR Modifcation Code must be calculated :
      ***  If Original entry month is Rundat month --> New customer  '1'
      ***  else
      ***  If Last chg month is Rundat month --> Amended customer    '2'
      *
     C                     Z-ADDBBLCD     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE MONTH     LCDMNT  2
      *
     C                     Z-ADDBBORED    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE MONTH     OREMNT  2
      *
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE MONTH     RUNMNT  2
      *
     C           OREMNT    IFEQ RUNMNT
     C                     Z-ADD1         IDCRMO
     C                     ELSE
     C           LCDMNT    IFEQ RUNMNT
     C           BBTYLC    ANDEQ'A'
     C                     Z-ADD2         IDCRMO
     C                     ENDIF
     C                     ENDIF
      *
      ***  CR Client Type from Ext Details
     C                     Z-ADDPCCRTP    IDCRTP
      *
      ***  Reference date =  rundate DDMMYYYY
     C                     Z-ADDBJRDNB    @@DAYN  50
     C                     EXSR ZDATE9
     C                     Z-ADD@@YR      DATEYY
     C                     Z-ADD@@Z71M    DATEMM
     C                     Z-ADD@@Z71D    DATEDD
      *
     C                     Z-ADDDATE08    IDRFDT
      *
      ***  End position must be different than zero or blank
     C                     MOVE '*'       IDEND
      *
      ***  other field are zero or Blanks
     C                     ENDSR
      /EJECT
      *****************************************************************
      *  *INZSR - Initial subroutine                                  *
      *  ------                                                       *
      *  Called By : At Program Start                                 *
      *  Calls     :                                                  *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in Installation Data
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           IRSTAT
     C           *LOCK     IN   IRSTAT
      *
     C                     Z-ADDIRSQCT    WWRRNV  60
      *
      ***  Access  SDBANKPD Details
     C                     MOVEL*BLANKS   @RTCD   7
     C                     MOVEL'*FIRST ' @OPTN   7
     C                     CALL 'AOBANKR0'
     C                     PARM           @RTCD
     C                     PARM           @OPTN
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
     C                     Z-ADD5         DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           * DBERROR 005 *
     C                     MOVEL'*FIRST ' DBKEY     P      ***************
     C                     MOVELPGM       DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Default *IN98 for ZDATE2 routine : format DDMMYY
     C                     MOVE *OFF      *IN98
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *   *PSSR  - Program exception error routine                    *
      *   Called by :                                                 *
      *   Calls     :                                                 *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      *  Standard subroutine :                                        *
      *  -------------------                                          *
      *  ZDATE2 : Convert Day number to DDMMYY                        *
      *  ZDATE9 : Convert Day number to YYYYMMDD                      *
      *****************************************************************
      /COPY ZSRSRC,ZDATE2Z2
      *
      /COPY ZSRSRC,ZDATE9Z3
      *
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
** @YD  USED BY SR. ZDATE9 - YEARS, IN DAYS, CUMULATIVE IN 4 YEAR SPAN
00366007310109601461
** @MD  USED BY SR. ZDATE9 - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
00000000310005900090001200015100181002120024300273003040033400365
**  CPY@
(C) Copyright Midas-Kapiti International Ltd. 1997
