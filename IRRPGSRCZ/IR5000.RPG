     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IR Internal Table Report - List')                *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module - Italy.                   *
      *                                                               *
      *  IR5000 - Internal Table Report - List                        *
      *                                                               *
      *  Function:  This program will list the internal table file    *
      *             (IRFXTBPD). Prompt program, send existing table   *
      *             code or 999 for all tables (PROMPT is IR5001)     *
      *  (phase(s)) I/C                                               *
      *                                                               *
      *  Called By: IRC5000 -Internal Table Report                    *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 10Feb21               *
      *  Prev Amend No. UIT022 *Create     Date 16Jan97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  UIT022 - Telegramma Decadale                                 *
      *                                                               *
      *****************************************************************
     FIRFXTBL0IF  E           K        DISK
      ***  Internal tables codes - update
     F            F0PR                              KRENAMEPOSITB
     FIRFXTBL1IF  E           K        DISK
      ***  Internal tables codes - retrieval
     F            F0PR                              KRENAMEREADTB
      *
     FIR5000AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
     FIR5000P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
      *
      /TITLE E-lines
      *
      ***  Copyrigths Array
      *
     E                    CPY@    1   1 80
      *
      ***  Array containing Copyright statement
      *
     E                    COMM        2 60               Comments
     E                    HKEY        5 18   HNAR   40
      *
      /TITLE I-Lines
      *
      ***  Rename READTB fields
      *
     IREADTB
     I              F0INTC                          TBINTC
     I              F0INTK                          TBINTK
     I              F0PNAR                          TBPNAR
     I              F0LCD                           TBLCD
     I              F0TYLC                          TBTYLC
      *
     ILDA       E DSLDA                         256
      ***  Local data area for database error details
      *
     IRUNDAT    E DSRUNDAT
      ***  Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      ***  File Information Data Structure for IR5000P1.
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for IR5000AU.
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *-------------------------------------------------*
      ***  Data Structures used for Access Programs.  ***
      *-------------------------------------------------*
     IDSFDY     E DSDSFDY
      *** First DS for Access Pgms, short format
      *
     ISDBANK    E DSSDBANKPD
      ***  External data structures for Bank Details
      *
      *
      ***  Working data structures :
      *
     IDSPARM      DS                            100
      ***  Selected Table 999 for ALL (*Entry parameters)
      *
     I                                        1   3 PTABL
     IPOSIDS      DS                             18
      ***  Table key pointer
      *
     I                                        1   3 TBKY
     I                                        4  18 TBBLK
     IREADDS      DS                             18
      ***  Table Code Key pointer
      *
     I                                        1   3 CDKY
     I                                        5   5 CDBLK2
     I                                        4  18 CDBLK
      *
     C/TITLE Main Processing
      *****************************************************************
      *                    P R O G R A M   S T A R T                  *
      *****************************************************************
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      ***  End program
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      *                       P R O G R A M   E N D                   *
      *****************************************************************
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine manage table read and list               *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    :                                                   *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR                           *** PROCES ***
      *
     C                     Z-ADD0         RCOUNT
     C                     MOVE '0'       WWINDC  1        first code detail
      *
      ***  Position on header details / Read Header
      *
     C                     MOVE '999'     KHDRT
     C           PTABL     IFEQ '999'                      All table
     C                     MOVE *LOVAL    KHDRC              Header code
     C           KHDR      SETLLPOSITB
      *
      ***  Read until header is Header record of table
      *
     C                     MOVE *ON       *IN89
     C           *IN89     DOWEQ*ON
     C                     READ POSITB                   89*OFF = found
      *
      ***  Check if it is a table header
      *
     C           *IN89     IFEQ *OFF                       I Read a record
     C                     MOVE F0INTK    POSIDS
     C           TBBLK     IFNE *BLANKS
     C                     MOVE *ON       *IN89
     C                     MOVEL*HIVAL    TBBLK
     C                     MOVELPOSIDS    KHDRC
     C           KHDR      SETGTPOSITB
     C                     ENDIF
     C                     ELSE
      *
      ***  End of file reached
      *
     C                     LEAVE                           *IN89 is *ON
     C                     ENDIF
     C                     ENDDO
      *
      ***  Only one table to list
      *
     C                     ELSE
     C                     MOVELPTABL     TBKY             Table code
     C                     MOVE *BLANKS   TBBLK
     C                     MOVELPOSIDS    KHDRC
     C           KHDR      CHAINPOSITB               89    *OFF = found
     C                     ENDIF
      *
      ***  Open printer file if a table header is reached
      *
     C           *IN89     IFEQ *OFF
     C                     OPEN IR5000P1
     C                     EXSR RCFP1
     C                     ELSE
      *
      ***  Else audit report
      *
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  Do for all tables or  for selected table
      *
     C           *IN89     DOWEQ*OFF
      *
      ***  1) fill arrays for page header with Header details
      *
     C                     MOVELF0INTK    RTABL
     C                     MOVELF0PNAR    RTABN
      *
      ***  Read Table header details
      *
     C                     EXSR #RHDR
      *
      ***  Print header of page
      *
     C                     EXSR #PHDR
      *
      ***  2) Do while details exist : print details  (TBKY = current table)
      *
     C                     MOVELTBKY      KTBKY   3
     C           KTBKY     SETLLREADTB
     C           KTBKY     READEREADTB                   88
     C           *IN88     DOWEQ*OFF
      *
     C                     ADD  1         RCOUNT           Detail line
      *
      ***  Perform detail write
      *
     C                     EXSR WP1REC
      *
      ***  Read next details
      *
     C           KTBKY     READEREADTB                   88
     C                     ENDDO
      *
      ***  Read next table if necessary  (PTABL = 999 --> All tables)
      *
     C           PTABL     IFNE '999'
     C                     MOVE *ON       *IN89            Terminate
     C                     ELSE
      *
      ***  Go to next table: 999 TAB*HIVAL, position after current table
      *
     C                     MOVE *HIVAL    TBBLK
     C                     MOVELPOSIDS    KHDRC
     C           KHDR      SETGTPOSITB
      *
      ***  Read until header is Header record of table
      *
     C                     MOVE *ON       *IN89
     C           *IN89     DOWEQ*ON
     C                     READ POSITB                   89*OFF = found
      *
      ***  Check if it is a table header
      *
     C           *IN89     IFEQ *OFF                       I Read a record
     C                     MOVE F0INTK    POSIDS
     C           TBBLK     IFNE *BLANKS
      *
      ***  Go to next record header table
      *
     C                     MOVE *ON       *IN89            next run
     C                     MOVEL*HIVAL    TBBLK
     C                     MOVELPOSIDS    KHDRC
     C           KHDR      SETGTPOSITB
     C                     ENDIF
     C                     ELSE
      *
      ***  End of file
      *
     C                     LEAVE                           *IN89 is *ON
     C                     ENDIF
     C                     ENDDO                           *IN89 = *OFF ?
      *
     C                     ENDIF
      *
      ***  Output Audit Report
      *
     C                     WRITELINEP1                     '-------------'
     C                     EXSR AUDIT
      *
     C                     Z-ADD0         RCOUNT
     C                     MOVE '0'       WWINDC  1        first code detail
      *
      ***  *IN89 if *OFF, next table will be processed
      ***  *IN89 if *ON, Leave program
      *
     C                     ENDDO                           *IN89 *OFF ?
      *
      ***  End of Report
      *
     C                     EXSR WP1END
     C                     CLOSEIR5000P1               99
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/#RHDR
      *****************************************************************
      *                                                               *
      *  #RHDR  - Fill Arrays to keep Table Description from Table    *
      *           header records (KEY = '999' 'TABxx')                *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           #RHDR     BEGSR                           *** #RHDR  ***
      *
      ***  Reset Arrays
      *
     C                     MOVEL*BLANKS   COMM             Comments
     C                     MOVEL*BLANKS   HKEY             Key code header
     C                     MOVEL*BLANKS   HNAR             Key code narr. hdr
      *
      ***  Define Key list
      *
     C           KTABH     KLIST
     C                     KFLD           KHDRT            '999'
     C                     KFLD           READDS           'TABxx........'
      *
      ***  Read Table header KEY 999 TABlk
      *
     C                     MOVELPOSIDS    READDS           '999' 'TAB....'
      *
      ***  Read Header table header : Comments 1 to 4 (2 lines of 60char)
      *
     C                     MOVE *BLANKS   WWLI60 60
     C                     MOVE *BLANKS   WWLI20 20
      *
     C                     MOVEL'C1'      CDBLK     P
     C           KTABH     CHAINREADTB               87
     C           *IN87     IFEQ *OFF
     C           TBTYLC    ANDNE'D'
     C                     MOVELTBPNAR    WWLI60    P
     C                     ENDIF
      *
     C                     MOVEL'C2'      CDBLK     P
     C           KTABH     CHAINREADTB               87
     C           *IN87     IFEQ *OFF
     C           TBTYLC    ANDNE'D'
     C                     MOVELTBPNAR    WWLI20    P
     C                     MOVE WWLI20    WWLI60
     C                     ENDIF
      *
     C                     MOVELWWLI60    COMM,1
      *
      ***  Second line
      *
     C                     MOVE *BLANKS   WWLI60 60
     C                     MOVE *BLANKS   WWLI20 20
      *
     C                     MOVEL'C3'      CDBLK     P
     C           KTABH     CHAINREADTB               87
     C           *IN87     IFEQ *OFF
     C           TBTYLC    ANDNE'D'
     C                     MOVELTBPNAR    WWLI60    P
     C                     ENDIF
      *
     C                     MOVEL'C4'      CDBLK     P
     C           KTABH     CHAINREADTB               87
     C           *IN87     IFEQ *OFF
     C           TBTYLC    ANDNE'D'
     C                     MOVELTBPNAR    WWLI20    P
     C                     MOVE WWLI20    WWLI60
     C                     ENDIF
      *
     C                     MOVELWWLI60    COMM,2
      *
      ***  Read Header table header : Key Code 1 to 5
      *
     C                     MOVEL'K'       CDBLK     P
     C                     Z-ADD1         J       10
     C                     DO   5         J
     C                     MOVE J         CDBLK2           Code sequence
     C           KTABH     CHAINREADTB               87
     C           *IN87     IFEQ *OFF
     C           TBTYLC    ANDNE'D'
     C                     MOVELTBPNAR    HKEY,J
     C                     ENDIF
      *
     C                     ENDDO
      *
      ***  Read Header table header : Line 1 to 5
      *
     C                     MOVEL'L '      CDBLK     P
     C                     Z-ADD1         J
     C                     DO   5         J
     C                     MOVE J         CDBLK2           Code sequence
     C           KTABH     CHAINREADTB               87
     C           *IN87     IFEQ *OFF
     C           TBTYLC    ANDNE'D'
     C                     MOVELTBPNAR    HNAR,J
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/#PHDR
      *****************************************************************
      *                                                               *
      *  #PHDR  - Print Header of report file using Arrays COMM,      *
      *           HKEY and HNAR                                       *
      *                                                               *
      *  Called By:                                                   *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           #PHDR     BEGSR                           *** #PHDR  ***
      *
      ***  Use arrays content to print Header
      *
     C                     WRITEHEADP1
      *
      ***  Print comments if found
      *
     C           COMM,1    IFNE *BLANKS
     C                     MOVE *BLANKS   RCOMM
     C                     MOVEACOMM,1    RCOMM
     C                     WRITECOMMP1
     C                     ENDIF
      *
     C           COMM,2    IFNE *BLANKS
     C                     MOVE *BLANKS   RCOMM
     C                     MOVEACOMM,2    RCOMM
     C                     WRITECOMMP1
     C                     ENDIF
      *
      ***  Print Table Header line if not blanks
      *
     C                     WRITELINEP1
      *                                                    '----------'
      ***  Add 'Header' Title on first line
      *
     C                     MOVE '0'       WWINDH  1        'Header' ?
      *
      ***  Line 1 to 5  Header of table
      *
     C                     Z-ADD1         I       10
     C                     DO   5         I
     C           HKEY,I    IFNE *BLANKS
     C           HNAR,I    ORNE *BLANKS
     C                     MOVE *BLANKS   RKEY
     C                     MOVE *BLANKS   RNARR
     C                     MOVEAHKEY,I    RKEY
     C                     MOVEAHNAR,I    RNARR
      *
     C           WWINDH    IFEQ '0'
     C                     MOVE '1'       WWINDH
     C                     MOVEA'10'      *IN,70           'Header'
     C                     ELSE
     C                     MOVEA'00'      *IN,70
     C                     ENDIF
      *
     C                     WRITEDETLP1
     C                     ENDIF
     C                     ENDDO
      *
     C                     MOVE '0'       WWINDC           'Code' ?
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *                                                               *
      *  WP1REC - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *  Called By:                                                   *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           WP1REC    BEGSR                           *** WP1REC ***
      *
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD2         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITELINEP1
     C                     EXSR #PHDR                      Print header
     C                     ENDIF
      *
      ***  Fill report fields : Key code and narrative
      *
     C                     MOVELTBINTK    RKEY             18Char
     C                     MOVELTBPNAR    RNARR            40Char
      *
      ***  Print detail : First Code on page
      *
     C           WWINDC    IFEQ '0'
     C                     WRITELINEP1                     '-------------'
     C                     MOVE '1'       WWINDC           'Code'
     C                     MOVEA'01'      *IN,70
     C                     ELSE
     C                     MOVEA'00'      *IN,70
     C                     ENDIF
      *
      ***  Write out Detail Record.
      *
     C                     WRITEDETLP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By:                                                   *
      *  Calls    :                                                   *
      *                                                               *
      *****************************************************************
      *
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     EXSR #PHDR                      Page Header
     C                     WRITELINEP1
     C                     ENDIF
      *
      ***  Write out Total Format.
      *
     C                     WRITETRAILP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report                   *
      *                                                               *
      *  Called By:                                                   *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
     C                     WRITESBHDAU
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     MOVE PGM       DBPGM            Program name
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ELSE
     C                     WRITECONTROL
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  *INZSR - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By:                                                   *
      *  Calls    :                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *INZSR    BEGSR                           ***  INIT  ***
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PBRCH   3
     C                     PARM           PPARM 100
      *
     C                     MOVELPPARM     DSPARM
      *
      ***   Define LDA
      *
     C           *NAMVAR   DEFN           LDA
     C                     MOVEL'IR5000'  DBPGM
      *
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'IR5000'  DBPGM
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C                     OUT  LDA                        ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Check System Date Format, DDMMYY (*IN98 off)
      ***                            MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      **   Access RUNDAT for multibranching indicator
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
     C           AGMBIN    IFEQ 'M'
     C                     SETON                     37
     C                     END
      *
      ***  Define Key list For POSITB record format (IRFXTBL0)
      *
     C           KHDR      KLIST
     C                     KFLD           KHDRT   3
     C                     KFLD           KHDRC  18
      *
      ***  Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
      ***  RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
     C                     ENDSR
      *
**  CPY@
(C) Copyright Midas-Kapiti International Ltd. 1997
