     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IR Internal table Report - Prompt')              *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module - Italy.                   *
      *                                                               *
      *  IR5001 - Internal table Report - Prompt                      *
      *                                                               *
      *  Function:  This program will allow the user to select the    *
      *  (phase(s)) internal table he wants (999 for All tables)      *
      *                                                               *
      *  Called By: IRC5001 - Internal Table Report - Prompt          *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 10Feb21               *
      *  Prev Amend No. UIT022  *Create    Date 17Jan97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  UIT022 - Telegramma Decadale                                 *
      *                                                               *
      *****************************************************************
     FIRFXTBL0IF  E           K        DISK
      ***  Internal table file
      *
     FIR5001DFCF  E                    WORKSTN
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 SWSID
     I                                      254 263 SUSER
      *
      ***  Working DS to setup P#PARM
      *
     IP#PARM      DS                            100
     I                                        1   3 WWTABL
      *
      /EJECT
      *****************************************************************
      *                         M A I N L I N E S                     *
      *****************************************************************
      *
      ***  Clear screen message file
      *
     C                     CALL 'CLRERMSG'
     C                     PARM PGM       ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
      ***  Set working indicator
      *
     C                     MOVE *ON       *IN75
     C                     MOVEA'0'       *IN,20
      *
     C           *INKC     DOUEQ*ON
     C           *IN75     OREQ *OFF
      *
      ***  Display messages
      *
     C                     WRITE#MSGCTL
      *
      ***  Display main screen
      *
     C                     EXFMTIR5001F0
      *
      ***  Clear screen message file
      *
     C                     CALL 'CLRERMSG'
     C                     PARM PGM       ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
      ***  Reset Indicator
      *
     C                     MOVE *OFF      *IN75
     C                     MOVEA'0'       *IN,20
      *
      ***  Check dates entered
      *
     C           *INKC     IFEQ *OFF
     C                     EXSR VALID
     C                     ELSE
      *
     C                     MOVE 'E'       WWCMD   1
     C                     ENDIF
      *
     C                     ENDDO
      *
      *** Fill P#PARM with 'From date' and 'to Date' in Midas Day number
      *
     C           WWCMD     IFEQ 'E'
     C                     MOVE *BLANK    P#PARM
     C                     ENDIF
      *
     C                     MOVE WWCMD     P#CMD
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      *                    E N D     P R O G R A M                    *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *   VALID  - Check if Table exist or is 999                     *
      *   ------                                                      *
      * Called by: main line                                          *
      * Calls: None                                                   *
      *****************************************************************
     C           VALID     BEGSR
      *
      ***  Both date must be entered and must be valid
      *
     C           STABL     IFEQ *BLANKS
     C                     MOVE *ON       *IN20
     C                     MOVE *ON       *IN75
      *
      ***  'Must be an existing table code or '999' for All.'
      *
     C                     MOVEL'ITC0078' ZAMSID
     C                     EXSR SRSMSG
     C                     ELSE
      *
      ***  999 means all tables
      *
     C           STABL     IFNE '999'
      *
      ***  Verify it's an existing table
      *
     C                     MOVELSTABL     KINTK     P
     C           KTAB      SETLLIRFXTBL0                 99
      *
     C           *IN99     IFEQ *OFF                        Not Found
     C                     MOVE *ON       *IN20
     C                     MOVE *ON       *IN75
      *
      ***  'Must be an existing table code or '999' for All.'
      *
     C                     MOVEL'ITC0078' ZAMSID
     C                     EXSR SRSMSG
      *
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C           *IN75     IFEQ *OFF
     C                     MOVELSTABL     WWTABL
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *   SRSMSG - Send message in Subfile message                    *
      *   ------                                                      *
      * Called by : VALID                                             *
      * Calls     : SNDERMSG                                          *
      *                                                               *
      *****************************************************************
      *
     C           SRSMSG    BEGSR
      *
     C                     CALL 'SNDERMSG'
     C                     PARM PGM       ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
      *
      * Clear all fields for default mechanism next time.
      *
     C                     MOVEL*BLANK    ZAPGRL           Rel queue
     C                     MOVEL*BLANK    ZAMSID           Message Id.
     C                     MOVEL*BLANK    ZAMSDA           Message data.
     C                     MOVEL*BLANK    ZAMSTP           Message type.
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *   *INZSR - First Routine                                      *
      *   ------   Called automatically at porgram start.             *
      * Called by: main line                                          *
      * Calls: None                                                   *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Entry parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           P#PARM
     C                     PARM           P#CMD   1
      *
     C                     MOVE P#CMD     WWCMD
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           AGDFF     IFEQ 'M'
     C                     SETON                     98
     C                     END
      *
      *** Fill Midas date on screen
      *
     C                     MOVELAGMRDT    SRUNA
      *
      ***  Message file will be Italian features's one.
      *
     C                     MOVEL'ITUSRMSG'ZAMSGF
      *
      *** Define key list for validation
      *
     C           KTAB      KLIST
     C                     KFLD           KCODE   3
     C                     KFLD           KINTK  18
      *
     C                     MOVEL'999'     KCODE
     C                     MOVE *BLANKS   KINTK
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by:                                                    *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
      *
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     MOVE 'E'       P#CMD
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(C) Copyright Midas-Kapiti International Ltd. 1997
