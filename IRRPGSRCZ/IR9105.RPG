     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IR European Reporting I.C.D. print')             *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting                                   *
      *                                                               *
      *  IR9105 - European Reporting I.C.D. print                     *
      *                                                               *
      *  (c) Copyright Midas-Kapiti International 1996.               *
      *                                                               *
      *    Last Amend No. LUC139                Date 22Dec20          *
      *    Prev Amend No. UIT021                Date 13JAN97          *
      *                   USW004                Date 10SEP96          *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  UIT021 - Bank of Italy Monthly Reporting                     *
      *           (Just recompiled)                                   *
      *  USW004 - SNB Upgrade to release 10.                          *
      *            (SPF/RCF)                                          *
      *                                                               *
      *****************************************************************
     F*ER9105P1O   E             01     PRINTER      KINFDS SPOOL1     UC
     FIR9105P1O   E             01     PRINTER      KINFDS SPOOL1     UC
      *                                Printing report.
     F*ER9105AUO   E                    PRINTER      KINFDS SPOOLU
     FIR9105AUO   E                    PRINTER      KINFDS SPOOLU
      *                                Audit report.
     FERICDRL1IF  E           K        DISK
      *                                European reporting I.C.D.
     FSDCTRYL1IF  E           K        DISK
      *                                Country codes.
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *  -------------------------------------------                  *
      *                                                               *
      *                                                               *
      ***01   - Overflow on printer file ER9105P1.                    *
      *  01   - Overflow on printer file IR9105P1.                    *
      *  50   - No details to report.                                 *
      *  81   - End Of File on ERICDRL1.                              *
      *  82   - Record not found in SDCTRYL1.                         *
      *  95   - Database error.                                       *
      *  98   - Date in format MMDDYY.                                *
      *  LR   - End processing.                                       *
      *                                                               *
      *  U8+U8- Database error.                                       *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   - Program Initialisation                              *
      *  PROCES - Detail Processing                                   *
      *  REPORT - Process Report Lines                                *
      *                                                               *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
      *                                                               *
      *****************************************************************
     E/EJECT
      *
      ***  Array containing Copyright statement.
     E                    CPY@    1   1 80
      *
      ***  Array for errors messages.
     E                    MES     1   2 80               DB error msg.
      *
      ***  Array for dates conversion.
      /COPY ZSRSRC,ZDATE2Z1
      *
      /SPACE 3
      *
      *-------------------------------------------------------------------------
      *
      ***  Local data area for database error details
     ILDA       E DSLDA                         256
      ***                                   134 141 DBFILE
      ***       Defines:                    142 170 DBKEY
      ***                                   171 180 DBPGM
      ***                                   181 1830DBASE
      *
      ***  Get program name and exception data from PSDS.
     IPSDS       SDS
     I                                     *PROGRAM #PGM
     I                                       91 170 WWEXCP
      *
      ***  File Information Data Structure - AU report.
     ISPOOLU      DS
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      ***  File Information Data Structure - P1 report.
     ISPOOL1      DS
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680WWLINE
      *
      ***  External data structures for Bank Details.
     ISDBANK    E DSSDBANKPD
      *
      ***  First DS for access programs, short data structure.
     IDSFDY     E DSDSFDY
      *
      /EJECT
      *
      /TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
     C                     EXSR INIT
      *
      ***  Perform Detail Processing.
     C                     EXSR PROCES
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      *
      ***  Close printer file.
     C***                  CLOSEER9105P1
     C                     CLOSEIR9105P1
      *
     C                     SETON                     LR
     C                     RETRN
      *
      /EJECT
      *
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  RCFP1  - RCF Processing for Printing Repor                   *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  AOBANKR0 Midas Bank I.C.D. Access Object program.            *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ***  Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Receive Parameter List.
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PRLEV   1
     C                     PARM           PENT    3
      *
      ***  Initialise LDA field.
     C                     MOVEL#PGM      DBPGM
      *
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Handle Database Error.
     C           WRTCD     IFNE *BLANKS                    *B1
     C                     MOVEL'01'      P2ERR            ***************
     C                     Z-ADD1         X       20       * DB ERROR 01 *
     C                     MOVE *BLANKS   P2EDTA           ***************
     C                     SETON                     95
     C                     EXSR AUDIT
      *
     C                     ENDIF                           *E1
      *
      ***  Check System Date Format, DDMMYY (*IN98 off)
      ***                            MMDDYY (*IN98 on).
     C           BJDFIN    IFEQ 'M'                        *B1
     C                     SETON                     98
      *
     C                     ENDIF                           *E1
      *
      ***  Opening of printer file.
     C***                  OPEN ER9105P1
     C                     OPEN IR9105P1
      *
      ***  RCF Processing for Printing File.
     C                     EXSR RCFP1
      *
      ***  RCF Processing for Audit File.
     C                     EXSR RCFAU
      *
      ***  Set on overflow indicator to print report heading.
     C                     SETON                     01
      *
     C                     ENDSR
      *
      /EJECT
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  REPORT - Process Report Lines                                *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR                           *** PROCES ***
      *
      ***  Access I.C.D. file to retrieve the user country code for print.
     C           *LOVAL    SETLLERICDRL1
     C                     READ ERICDRL1                 81
      *
      ***  If no record found.
     C           *IN81     IFEQ '1'                        *B1
     C                     SETON                     50    No detail to report
     C                     MOVEL'02'      P2ERR            ***************
     C                     Z-ADD2         X                * DB ERROR 02 *
     C                     MOVE *BLANKS   P2EDTA           ***************
     C                     SETON                     95
     C                     EXSR AUDIT
      *
     C                     END                             *E1
      *
      ***  Do until end of file.
     C           *IN81     DOUEQ'1'                        *B1
      *
     C           VDUCCD    IFNE *BLANKS                    *B2
      *
      ***  Process report lines.
     C                     EXSR REPORT
      *
     C                     END                             *E2
      *
      ***  Read next record.
     C                     READ ERICDRL1                 81
      *
     C                     ENDDO                             *B1
      *
      ***  Write final record.
     C                     EXSR WP1END
      *
     C                     ENDSR
      *
      /EJECT
      *
      *****************************************************************
      /TITLE SR/REPORT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *****************************************************************
      *
     C           REPORT    BEGSR                           *** REPORT ***
      *
      ***  Read Standing Data Country Code.
     C           VDUCCD    CHAINSDCTRYL1             82
     C                     MOVE *BLANKS   P1ICNA
     C           *IN82     IFEQ '0'                        *B1
     C                     MOVE A4CNNM    P1ICNA
      *
     C                     END                             *E1
      *
     C                     MOVE VDUCCD    P1UCCD
     C                     MOVE VDBRCD    P1BRCD
     C                     MOVE VDCCYC    P1CCYC
      *
     C                     MOVE VDTYLC    P1TYLC
      *
      ***  Write out the record.
     C                     EXSR WP1REC
      *
     C                     ENDSR
      *
      /EJECT
      *
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *                                                               *
      *  WP1REC - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *  Called By: REPORT                                            *
      *  Calls:                                                       *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMMYY)        *
      *                                                               *
      *****************************************************************
      *
     C           WP1REC    BEGSR                           *** WP1REC ***
      *
     C                     Z-ADDVDLCD     ZDAYNO  50
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    P1LCD
      *
      ***  If overflow, write out report heading.
     C           *IN01     IFEQ '1'                        *B1
     C***                  WRITEER9105F1
     C                     WRITEIR9105F1
      *
     C                     END                             *E1
      *
      ***  Write out detail record.
     C***                  WRITEER9105F2
     C                     WRITEIR9105F2
      *
     C                     ENDSR
      *
      /EJECT
      *
      *
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ***  Write out Total end of Report format.
     C***                  WRITEER9105F3
     C                     WRITEIR9105F3
      *
     C                     ENDSR
      *
      *****************************************************************
      *
      /EJECT
      *
      /EJECT
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, PROCES.                          *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR
      *
      ***  If DB error, setup message and switches
     C           *IN95     IFEQ '1'                        *B1
     C                     MOVEAMES,X     P2MSG
     C                     SETON                     U7U8
      *
     C                     END                             *E1
      *
     C                     WRITEHEADAU
      *
      ***  No details to report.
     C           *IN50     IFEQ *ON                        *B1     No details to
     C                     WRITENODTLS                             report.
      *
     C                     ENDIF                           *E1
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      /EJECT
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM SFNUM1    ZSNUM   60
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
     C           ZSERR     IFEQ 'Y'                        *B1
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     END                             *E1
      *
     C                     ENDSR
      *
      /EJECT
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILEU
     C                     PARM SFNUMU    ZSNUMU  60
     C                     PARM *BLANK    ZSERR   1
     C*
     C***  If an error occurred during ZSFILE processing,
     C***  return to the calling program.
     C           ZSERR     IFEQ 'Y'                        *B1
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     END                             *E1
      *
     C                     ENDSR
      *
      /EJECT
      *
      **************************************************************************
      /TITLE SR/ZDATE2
      **************************************************************************
      /COPY ZSRSRC,ZDATE2Z2
      *
      /EJECT
      *
      **************************************************************************
**  CPY@
(C) Copyright Midas-Kapiti International Ltd. 1996
** MES -  Array to contain error messages for audit report
ICD1 record not found in SDBANKPD
ICD1 record not found in ERICDRPD
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
