      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LB Total Commitment Line Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Lending Module                         *
     A*                                                               *
     A*  DSPF/LB0210DF - Total Commitment Line Maintenance            *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01498             Date 11Aug94               *
      *                 056584             Date 13Jul93               *
     A*                 LB0008             DATE 03JUL90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01498 - Portfolio Lending Upgrade to Release 10.            *
     A*  056584 - R3 HELP SYSTEM                                      *
     A*  LB0008 - REMOVE CHECK(ER) FROM REVIEW FROM SUBFILE           *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*
     A*****************************************************************
     A* File level keywords                                           *
     A*****************************************************************
     A*
     A                                      CA03
     A* >>> 056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<< 056584 <<<
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A*
     A*****************************************************************
     A* Screen title format - common to all screens                   *
     A*****************************************************************
     A*
     A*                                                           Begin /S01498/
     A**********R LB0210F0
     A          R LB0210F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*                                                           End   /S01498/
     A                                      BLINK
     A*                                                           Begin /S01498/
     A**********  DDWSID        10A  O  1 51
     A**********  BJMRDT         7A  O  1 63
     A**********                        1 72TIME
     A**********                        2 24'TOTAL COMMITMENT LIMIT MAINTENANCE'
     A            WWUSER        10A  O  1  2
     A                                  1 13'LB0210'
     A                                  1 24'TOTAL COMMITMENT LINE MAINTENANCE'
     A                                      DSPATR(HI UL)
     A                                  1 67'DATE'
     A            BJMRDT         7A  O  1 72
     A            DDWSID        10A  O  2  2
     A                                  2 67'TIME'
     A                                  2 72TIME
     A*                                                           End   /S01498/
     A*
     A*****************************************************************
     A* Input screen 1 format                                         *
     A*****************************************************************
     A*
     A          R LB0210F1                  OVERLAY
     A                                      BLINK
     A* >>> 056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<< 056584 <<<
     A                                  4  2'Action'
     A            DD1ACT         1A  B    +1
     A                                      DSPATR(HI)
     A N40                                  DSPATR(UL)
     A  40                                  DSPATR(RI PC)
     A                                  5  2'Customer Number / Short Name'
     A            DD1CNS        10A  B    +1
     A                                      DSPATR(HI)
     A N41                                  DSPATR(UL)
     A  41                                  DSPATR(RI PC)
     A                                  5 52'     Review from'
     A            DD1RVF         6A  B    +1
     A                                      DSPATR(HI)
     A N42                                  DSPATR(UL)
     A  42                                  DSPATR(RI PC)
     A            DDULNE        78A  O  6 02DSPATR(UL)
     A                                 22  2'F3=Exit  Help  Action=I,A,D,E '
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A* Input screen 2 subfile record format                          *
     A*****************************************************************
     A*
     A          R LB0210S2                  SFL
     A  32                                  SFLNXTCHG
     A            DD2SEL         1A  B  8  2
     A*                                                               / LB0008 /
     A**********                            CHECK(ER)
     A*                                                               / LB0008 /
     A                                      DSPATR(HI)
     A N81                                  DSPATR(UL)
     A  81                                  DSPATR(RI PC)
     A            DD2CST         6A  O    +2
     A            DD2CSN        10A  O    +2
     A*                                                           Begin /S01498/
     A            DD2BCH         3A  H
     A*                                                           End   /S01498/
     A*
     A*****************************************************************
     A* Input screen 2 header format and subfile control              *
     A*****************************************************************
     A*
     A          R LB0210C2                  SFLCTL(LB0210S2) OVERLAY
     A                                      CF12
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A                                      SFLSIZ(13)
     A                                      SFLPAG(13)
     A                                      BLINK
     A                                      ROLLUP(26)
     A                                      ROLLDOWN(27)
     A* >>> 056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<< 056584 <<<
     A            SFLRR2         4S 0H      SFLRCDNBR
     A*
     A                                  4 52'     Review from'
     A            DD2RVF         6A  B    +1
     A                                      DSPATR(HI)
     A N43                                  DSPATR(UL)
     A  43                                  DSPATR(RI PC)
     A            DDULNE        78A  O  5 02DSPATR(UL)
     A                                  7 05'Customer      '
     A*
     A*****************************************************************
     A* Input screen 2 footer format                                  *
     A*****************************************************************
     A*
     A          R LB0210F2                  OVERLAY
     A                                      BLINK
     A                                 22  2'F3=Exit  F12=Start Screen  Help  R+
     A                                      ollup  Rolldown  Action=A,D,E'
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A* Input screen 4 header format                                  *
     A*****************************************************************
     A*
     A          R LB0210F3                  OVERLAY
     A                                      BLINK
     A                                 04 02'Action'
     A            DD4ACT         1A  O    +1
     A                                 05 02'Customer'
     A            DD4CST         6A  O    +1
     A            DD4CSN        10A  O    +2
     A*                                                           Begin /S01498/
     A                                  6  2'Branch'
     A N88                                  DSPATR(ND)
     A            DD4BCH         3A  O    +1
     A N88                                  DSPATR(ND)
     A*                                                           End   /S01498/
     A                                  7  2'Authorised'
     A                                      DSPATR(HI UL)
     A                                  8  4'Start Date  Ccy'
     A                                      DSPATR(HI)
     A                                  8 26'   Amount  Exp Date  Rev Date'
     A                                      DSPATR(HI)
     A                                  8 57'Frq Day No.  Auth Date'
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A* Input screen 4 subfile 1 record format                        *
     A*****************************************************************
     A*
     A          R LB0210S3                  SFL
     A  52                                  SFLNXTCHG
     A            DD4SEL         1A  B  9  2
     A                                      DSPATR(HI)
     A N82                                  DSPATR(UL)
     A  82                                  DSPATR(RI PC)
     A            DD4STD         6A  O  9  6
     A            DD4CCY         3A  O  9 16
     A            DD4AMT        15A  O  9 21
     A            DD4EXD         6A  O  9 38
     A            DD4RVD         6A  O  9 48
     A            DD4FRQ         1A  O  9 58
     A            DD4DYN         2A  O  9 63
     A            DD4AUD         6A  O  9 72
     A            DD4RRN         4  0H
     A            DD4TNL         5  0H
     A            DD4STN         5  0H
     A            DD4NT1        35A  H
     A            DD4NT2        35A  H
     A            DD4NT3        35A  H
     A            DD4NT4        35A  H
     A*
     A*****************************************************************
     A* Input screen 4 middle format and subfile 1 control            *
     A*****************************************************************
     A*
     A          R LB0210C3                  SFLCTL(LB0210S3) OVERLAY
     A                                      CF12
     A  48                                  SFLDSP
     A  49                                  SFLDSPCTL
     A  50                                  SFLCLR
     A  51                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(4)
     A                                      BLINK
     A N51                                  ROLLUP(53)
     A* >>> 056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<< 056584 <<<
     A            SFLRR3         4S 0H      SFLRCDNBR
     A*
     A                                 14  2'Unauthorised'
     A                                      DSPATR(HI UL)
     A                                 15  4'Start Date  Ccy'
     A                                      DSPATR(HI)
     A                                 15 26'   Amount  Exp Date  Rev Date'
     A                                      DSPATR(HI)
     A                                 15 57'Frq Day No.'
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A* Input screen 4 subfile 2 record format                        *
     A*****************************************************************
     A*
     A          R LB0210S4                  SFL
     A  62                                  SFLNXTCHG
     A            DD5SEL         1A  B 16  2
     A                                      DSPATR(HI)
     A N83                                  DSPATR(UL)
     A  83                                  DSPATR(RI)
     A  83N85                               DSPATR(PC)
     A            DD5STD         6A  O 16  6
     A            DD5CCY         3A  O 16 16
     A            DD5AMT        15A  O 16 21
     A            DD5EXD         6A  O 16 38
     A            DD5RVD         6A  O 16 48
     A            DD5FRQ         1A  O 16 58
     A            DD5DYN         2A  O 16 63
     A            DD5RRN         4  0H
     A            DD5TNL         5  0H
     A            DD5NT1        35A  H
     A            DD5NT2        35A  H
     A            DD5NT3        35A  H
     A            DD5NT4        35A  H
     A*
     A*****************************************************************
     A* Input screen 4 footer format and subfile 2 control            *
     A*****************************************************************
     A*
     A          R LB0210C4                  SFLCTL(LB0210S4) OVERLAY
     A                                      CF12
     A  58                                  SFLDSP
     A  59                                  SFLDSPCTL
     A  60                                  SFLCLR
     A  61                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(4)
     A                                      BLINK
     A N61                                  ROLLUP(53)
     A* >>> 056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<< 056584 <<<
     A            SFLRR4         4S 0H      SFLRCDNBR
     A*
     A                                 22  2'F3=Exit  F12=Start Screen  Help  R+
     A                                      ollup  Rolldown  Action X'
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A* Insert/Amend screens 3 & 5                                    *
     A*****************************************************************
     A*
     A          R LB0210F5                  OVERLAY
     A                                      BLINK
     A                                      CF12
     A* >>> 056584 >>>
     A*-----------
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<< 056584 <<<
     A                                 04 02'Action'
     A  13                             04 09'I'
     A N13                             04 09'A'
     A                                 05 02'Customer'
     A            DDCNUM         6A  O 05 11
     A            DDCNSN        10A  O 05 19
     A*                                                           Begin /S01498/
     A                                 06 02'Branch'
     A N88                                  DSPATR(ND)
     A            DDBRCH         3A  O 06 09
     A N88                                  DSPATR(ND)
     A*                                                           End   /S01498/
     A                                 07 05'Start date'
     A            DDSTTD         6A  B 07 16
     A  13                                  DSPATR(HI)
     A  13N65                               DSPATR(UL)
     A  13 65                               DSPATR(RI PC)
     A N13                                  DSPATR(PR)
     A  12N13                          07 24'AUTHORISED'
     A  12N13                               DSPATR(HI)
     A N12N13                          07 24'UNAUTHORISED'
     A N12N13                               DSPATR(HI)
     A                                 09 05'Currency'
     A            DDCCY          3A  B 09 14
     A                                      DSPATR(HI)
     A N66                                  DSPATR(UL)
     A  66                                  DSPATR(RI PC)
     A                                 09 24'Amount'
     A            DDCMTL        14A  B 09 31
     A                                      DSPATR(HI)
     A N67                                  DSPATR(UL)
     A  67                                  DSPATR(RI PC)
     A                                 09 49'Expiry Date'
     A            DDEXPD         6A  B 09 61
     A                                      DSPATR(HI)
     A N68                                  DSPATR(UL)
     A  68                                  DSPATR(RI PC)
     A                                 11 05'Review Details'
     A                                      DSPATR(UL)
     A                                 13 06'Date'
     A            DDREVD         6A  B 13 17
     A                                      DSPATR(HI)
     A N69                                  DSPATR(UL)
     A  69                                  DSPATR(RI PC)
     A                                 14 06'Frequency'
     A            DDREVF         1A  B 14 17
     A                                      DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(RI PC)
     A                                 15 06'Day Number'
     A            DDREVN         2A  B 15 17
     A                                      DSPATR(HI)
     A N71                                  DSPATR(UL)
     A  71                                  DSPATR(RI PC)
     A                                 17 05'Notes'
     A                                      DSPATR(UL)
     A            DDNOT1        35A  B 18 06
     A                                      DSPATR(HI UL)
     A            DDNOT2        35A  B 18 43
     A                                      DSPATR(HI UL)
     A            DDNOT3        35A  B 19 06
     A                                      DSPATR(HI UL)
     A            DDNOT4        35A  B 19 43
     A                                      DSPATR(HI UL)
     A                                 22 02'F3=Exit  F12=Start Screen  Help'
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A* Delete/Enquiry screens 6 & 7                                  *
     A*****************************************************************
     A*
     A          R LB0210F4                  OVERLAY
     A                                      BLINK
     A                                      CF12
     A  11                                  CF10
     A                                 04 02'Action'
     A  11                             04 09'D'
     A N11                             04 09'E'
     A                                 05 02'Customer'
     A            DDCNUM         6A  O 05 11
     A            DDCNSN        10A  O 05 19
     A*                                                           Begin /S01498/
     A                                 06 02'Branch'
     A N88                                  DSPATR(ND)
     A            DDBRCH         3A  O 06 09
     A N88                                  DSPATR(ND)
     A*                                                           End   /S01498/
     A                                 07 05'Start date'
     A            DDSTTD         6A  O 07 16
     A  12                             07 24'AUTHORISED'
     A  12                                  DSPATR(HI)
     A N12                             07 24'UNAUTHORISED'
     A N12                                  DSPATR(HI)
     A                                 09 05'Currency'
     A            DDCCY          3A  O 09 14
     A                                 09 24'Amount'
     A            DDCMTL        14A  O 09 31
     A                                 09 49'Expiry Date'
     A            DDEXPD         6A  O 09 61
     A                                 11 05'Review Details'
     A                                      DSPATR(UL)
     A                                 13 06'Date'
     A            DDREVD         6A  O 13 17
     A                                 14 06'Frequency'
     A            DDREVF         1A  O 14 17
     A                                 15 06'Day Number'
     A            DDREVN         2A  O 15 17
     A                                 17 05'Notes'
     A                                      DSPATR(UL)
     A            DDNOT1        35A  O 18 06
     A            DDNOT2        35A  O 18 43
     A            DDNOT3        35A  O 19 06
     A            DDNOT4        35A  O 19 43
     A  11                             22 02'F3=Exit  F10=Delete  F12=Start Scr+
     A                                      een  Help'
     A  11                                  DSPATR(HI)
     A N11                             22 02'F3=Exit  F12=Start Screen  Help'
     A N11                                  DSPATR(HI)
     A*
     A*****************************************************************
     A*
     A*****************************************************************
     A* Message subfile record format                                 *
     A*****************************************************************
     A*
     A          R LB0210S1                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*****************************************************************
     A* Message subfile control record format                         *
     A*****************************************************************
     A*
     A          R LB0210C1                  SFLCTL(LB0210S1) OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  25                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      BLINK
     A            DDPGMQ                    SFLPGMQ
