      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LB Total Commitment Line Authorisation')
     A*****************************************************************
     A*                                                               *
     A*  Midas -  Portfolio Lending Module                        *
     A*                                                               *
     A*  DSPF/LB0211DF - Total Commitment Line Authorisation          *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01498             Date 11Aug94               *
      *                 056584             Date 13Jul93               *
     A*                 LB0008             DATE 05JUL90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01498 - Portfolio Lending Upgrade to Release 10.            *
     A*  056584 - R3 HELP SYSTEM                                      *
     A*  LB0008 - REMOVE CHECK(ER) FROM REVIEW FROM SUBFILE           *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*
     A** FILE LEVEL KEYWORDS
     A*
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
     A* >>> 056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<< 056584 <<<
     A*
     A** SCREEN 1
     A*
     A*                                                           Begin /S01498/
     A**********R LB0211F1
     A          R LB0211F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*                                                           End   /S01498/
     A                                      BLINK
     A* >>> 056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<< 056584 <<<
     A*                                                           Begin /S01498/
     A**********  DDWSID        10A  O  1 51
     A**********  BJMRDT         7A  O  1 63
     A**********                        1 72TIME
     A**********                        2 23'TOTAL COMMITMENT LIMIT AUTHORISATI-
     A**********                            ON'
     A            USER          10A  O  1  2
     A                                  1 13'LB0211'
     A                                  1 23'TOTAL COMMITMENT LINE AUTHORISATI-
     A                                      ON'
     A                                      DSPATR(HI UL)
     A                                  1 67'DATE'
     A            BJMRDT         7A  O  1 72
     A            DDWSID        10A  O  2  2
     A                                  2 67'TIME'
     A                                  2 72TIME
     A*                                                           End   /S01498/
     A                                  4  2'Customer Number / Short Name'
     A            DD1CNS        10A  B  4 31
     A  50                                  DSPATR(RI PC)
     A                                  5  2'Start Date'
     A            DD1SD          6A  B  5 13
     A  51                                  DSPATR(RI PC)
     A                                  5 51'Review from'
     A            DD1RNS         6A  B  5 63
     A  52                                  DSPATR(RI PC)
     A            DD1RSD         6A  B  5 70
     A  53                                  DSPATR(RI PC)
     A            DDUNDR        78A  O  6  2DSPATR(UL)
     A                                 22  2'F3=Exit  Help  Review'
     A                                      DSPATR(HI)
     A*
     A** SCREEN 2 - SUBFILE
     A*
     A          R LB0211S2                  SFL
     A  47                                  SFLNXTCHG
     A            DD2SEL         1A  B  8  2
     A*                                                               / LB0008 /
     A***********                           CHECK(ER)
     A*                                                               / LB0008 /
     A  54                                  DSPATR(RI PC)
     A            DD2CN          6A  O  8  5
     A            DD2CSN        10A  O  8 12
     A            DD2SD          7A  O  8 28
     A            DD2CCY         3A  O  8 39
     A            DD2CLA        18A  O  8 45
     A            DD2ED          7A  O  8 67
     A            DD2RD          5  0H
     A            DD2RF          1A  H
     A            DD2RN          2A  H
     A            DD2N1         35A  H
     A            DD2N2         35A  H
     A            DD2N3         35A  H
     A            DD2N4         35A  H
     A            DD2RRN         4  0H
     A            DD2NLU         5  0H
     A            DD2SDN         5  0H
     A*                                                           Begin /S01498/
     A            DD2BCH         3A  H
     A*                                                           End   /S01498/
     A*
     A** SCREEN 2 - SUBFILE CONTROL
     A*
     A          R LB0211C2                  SFLCTL(LB0211S2)
     A                                      CA12
     A                                      BLINK
     A                                      OVERLAY
     A  43                                  SFLDSP
     A  44                                  SFLDSPCTL
     A  45                                  SFLCLR
     A  46                                  SFLEND
     A                                      SFLPAG(13)
     A                                      SFLSIZ(13)
     A                                      ROLLDOWN(41)
     A                                      ROLLUP(42)
     A* >>> 056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<< 056584 <<<
     A            SFLRR2         4  0H      SFLRCDNBR
     A*
     A*                                                           Begin /S01498/
     A**********  DDWSID        10A  O  1 51
     A**********  BJMRDT         7A  O  1 63
     A**********                        1 72TIME
     A**********                        2 23'TOTAL COMMITMENT LIMIT AUTHORISATI-
     A**********                            ON'
     A            USER          10A  O  1  2
     A                                  1 13'LB0211'
     A                                  1 23'TOTAL COMMITMENT LINE AUTHORISATI-
     A                                      ON'
     A                                      DSPATR(HI UL)
     A                                  1 67'DATE'
     A            BJMRDT         7A  O  1 72
     A            DDWSID        10A  O  2  2
     A                                  2 67'TIME'
     A                                  2 72TIME
     A*                                                           End   /S01498/
     A                                  4 51'Review from'
     A            DD2RNS         6A  B  4 63
     A  52                                  DSPATR(RI PC)
     A            DD2RSD         6A  B  4 70
     A  53                                  DSPATR(RI PC)
     A            DDUNDR        78A  O  5  2DSPATR(UL)
     A                                  7  5'Customer'
     A                                  7 26'Start Date'
     A                                  7 39'Ccy'
     A                                  7 45'Commitment Amount'
     A                                  7 65'Expiry Date'
     A*
     A** SCREEN 2 - TRAILER
     A*
     A          R LB0211F2
     A                                 22  2'F3=Exit  F12=Start Screen  Help  R-
     A                                      ollup  Rolldown  Action X'
     A                                      DSPATR(HI)
     A*
     A** SCREEN 3
     A*
     A          R LB0211F3
     A                                      BLINK
     A                                      CA12
     A                                      CA20
     A*                                                           Begin /S01498/
     A**********  DDWSID        10A  O  1 51
     A**********  BJMRDT         7A  O  1 63
     A**********                        1 72TIME
     A**********                        2 23'TOTAL COMMITMENT LIMIT AUTHORISATI-
     A**********                            ON'
     A            USER          10A  O  1  2
     A                                  1 13'LB0211'
     A                                  1 23'TOTAL COMMITMENT LINE AUTHORISATI-
     A                                      ON'
     A                                      DSPATR(HI UL)
     A                                  1 67'DATE'
     A            BJMRDT         7A  O  1 72
     A            DDWSID        10A  O  2  2
     A                                  2 67'TIME'
     A                                  2 72TIME
     A*                                                           End   /S01498/
     A*
     A                                  4  2'Customer'
     A            DD3CN          6A  O  4 12
     A            DD3CSN        10A  O  4 19
     A                                  4 31'Start Date'
     A            DD3SD          7A  O  4 42
     A*                                                           Begin /S01498/
     A                                  5  2'Branch'
     A N88                                  DSPATR(ND)
     A            DD3BCH         3A  O  5  9
     A N88                                  DSPATR(ND)
     A*                                                           End   /S01498/
     A*
     A                                  8  5'Currency'
     A            DD3CCY         3A  O  8 14
     A                                  8 24'Amount'
     A            DD3CLA        18A  O  8 31
     A                                  8 52'Expiry Date'
     A            DD3ED          7A  O  8 64
     A*
     A                                 10  5'Review Details'
     A                                      DSPATR(UL)
     A                                 12  6'Date'
     A            DD3RD          7A  O 12 17
     A                                 13  6'Frequency'
     A            DD3RF          1A  O 13 17
     A                                 14  6'Day number'
     A            DD3RN          2A  O 14 17
     A*
     A                                 16  5'Notes'
     A                                      DSPATR(UL)
     A            DD3N1         35A  O 17  6
     A            DD3N2         35A  O 17 43
     A            DD3N3         35A  O 18  6
     A            DD3N4         35A  O 18 43
     A*
     A                                 22  2'F3=Exit  F12=Previous Screen  F20=-
     A                                      Authorise  Help'
     A                                      DSPATR(HI)
     A*
     A** MESSAGE SUBFILE RECORD FORMAT
     A*
     A          R LB0211S4                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A** MESSAGE SUBFILE CONTROL RECORD FORMAT
     A*
     A          R LB0211C4                  SFLCTL(LB0211S4)
     A                                      CA12
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
