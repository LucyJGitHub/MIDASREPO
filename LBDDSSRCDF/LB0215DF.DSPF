      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LB Borrowing Power Weight Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Lending Module                         *
     A*                                                               *
     A*  DSPF/LB0215DF - Borrowing Power Weight Maintenance           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01498             Date 11Aug94               *
      *                 056584             Date 14Jul93               *
     A*                 LB0009             DATE 09JUL90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01498 - Portfolio Lending Upgrade to Release 10.            *
     A*  056584 - R3 HELP SYSTEM                                      *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CA03
     A  50                                  CA10
     A  51                                  CA12
     A                                      INDARA
     A* >>> 056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<< 056584 <<<
     A                                      MSGLOC(24)
     A                                      PRINT(MPRINT)
     A*****************************************************************
     A* SCREEN HEADER                                                 *
     A*****************************************************************
     A*
     A*                                                           Begin /S01498/
     A**********R LB0215F1                  OVERLAY
     A          R LB0215F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A*                                                           End   /S01498/
     A                                      BLINK
     A*                                                           Begin /S01498/
     A**********  DDJOB         10A  O 01 51
     A**********  BJMRDT         7A  O 01 63
     A**********                     O 01 72TIME
     A**********                        2 22'BORROWING POWER WEIGHTINGS +
     A**********                            MAINTENANCE'
     A            USER          10A  O  1  2
     A                                  1 13'LB0215'
     A                                  1 23'BORROWING POWER WEIGHT +
     A                                      MAINTENANCE'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 67'DATE'
     A            BJMRDT         7A  O  1 72
     A            DDJOB         10A  O  2  2
     A                                  2 67'TIME'
     A                                  2 72TIME
     A*                                                           End   /S01498/
     A*****************************************************************
     A* SCREEN 1                                                      *
     A*****************************************************************
     A*
     A          R LB0215F2                  OVERLAY
     A                                      BLINK
     A* >>> 056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<< 056584 <<<
     A                                  4  2'Action'
     A            DDACTN         1A  B    +1
     A  41                                  DSPATR(RI PC)
     A                                  5  2'Customer Number / Shortname'
     A            DDCSNO        10A  B    +1
     A  42                                  DSPATR(RI PC)
     A                                  6  2'Instrument Type'
     A            DDINST         3A  B    +1
     A  43                                  DSPATR(RI PC)
     A                                    +2'Security'
     A            DDSECS        10A  B    +1
     A  44                                  DSPATR(RI PC)
     A                                  7 02'Country of Issuer'
     A            DDCTRY         2A  B    +1
     A  45                                  DSPATR(RI PC)
     A                                  7 49'Review From'
     A            DDRCST         6A  B    +1
     A  46                                  DSPATR(RI PC)
     A            DDRINS         3A  B  8 61
     A  47                                  DSPATR(RI PC)
     A            DDRSEC        10A  B    +2
     A  48                                  DSPATR(RI PC)
     A            DDRCTY         2A  B    +2
     A  49                                  DSPATR(RI PC)
     A            DDLINE        80A     9 01DSPATR(UL)
     A                                 22  2'F3=Exit  Help  Action=+
     A                                      I,A,D,E  Review'
     A                                      DSPATR(HI)
     A*****************************************************************
     A* MAINTENANCE SCREEN                                            *
     A*****************************************************************
     A*
     A          R LB0215F4                  OVERLAY
     A                                      BLINK
     A* >>> 056584 >>>
     A*--------
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<< 056584 <<<
     A*                                                           Begin /S01498/
     A**********  DDJOB         10A  O 01 51
     A**********  BJMRDT         7A  O 01 63
     A**********                     O 01 72TIME
     A**********                        2 22'BORROWING POWER WEIGHTINGS +
     A**********                            MAINTENANCE'
     A            USER          10A  O  1  2
     A                                  1 13'LB0215'
     A                                  1 23'BORROWING POWER WEIGHT +
     A                                      MAINTENANCE'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 67'DATE'
     A            BJMRDT         7A  O  1 72
     A            DDJOB         10A  O  2  2
     A                                  2 67'TIME'
     A                                  2 72TIME
     A*                                                           End   /S01498/
     A                                  4  2'Action'
     A            DDACTN         1A  O    +1
     A  71                              4 72'DELETED'
     A                                       DSPATR(BL HI)
     A                                  5  2'Customer Number / Shortname'
     A            DDCNUM         6   O  5 31TEXT('CUSTOMER NUMBER')
     A                                       DSPATR(HI)
     A            DDPNP8        10A  O  5 38TEXT('CUSTOMER SHORTNAME')
     A                                       DSPATR(HI)
     A  60                              6  2'Instrument Type'
     A  60        DDINST         3A  O  6 19DSPATR(HI)
     A  60        DDINSN        20A  O  6 24DSPATR(HI)
     A*
     A  61                              6  2'Security'
     A  61        DDSECS        10A  O  6 12DSPATR(HI)
     A  61        DDSEC1        30A  O  6 24DSPATR(HI)
     A  61        DDSEC2        30A  O  7 24DSPATR(HI)
     A*
     A  62                              6  2'Country of Issuer'
     A  62        DDCTRY         2A  O  6 21DSPATR(HI)
     A  62        DDCTRN        30A  O  6 25DSPATR(HI)
     A*                                                           Begin /S01498/
     A                                  8  2'Branch'
     A N78                                  DSPATR(ND)
     A            DDBRCH         3A  O    +2DSPATR(HI)
     A N78                                  DSPATR(ND)
     A*                                                           End   /S01498/
     A*
     A                                  9 10'Weighting'
     A            DDPERC         3   B  9 21TEXT('B.P WEIGHTING')
     A                                      DSPATR(HI)
     A  63                                  DSPATR(PR)
     A N63N64                               DSPATR(UL)
     A  64                                  DSPATR(RI PC)
     A                                  9 25'%'
     A                                 13  9'Review Details'
     A                                      DSPATR(UL)
     A                                 14 10'Date'
     A            DDDATE         6   B 14 21
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A  65                                  DSPATR(RI PC)
     A                                 15 10'Frequency'
     A            DDFREQ         1A  B 15 21
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A  66                                  DSPATR(RI PC)
     A                                 16 10'Day Number'
     A            DDDAYN         2   B 16 21
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A  67                                  DSPATR(RI PC)
     A  50                             22  2'F3=Exit  +
     A                                      F10=Delete  +
     A                                      F12=Start Screen  Help'
     A                                      DSPATR(HI)
     A N50                             22  2'F3=Exit  +
     A                                      F12=Start Screen  Help'
     A                                      DSPATR(HI)
     A*****************************************************************
     A* INSTRUMENT SUBFILE RECORD FORMAT                              *
     A*****************************************************************
     A*
     A          R LB0215S1                  TEXT('INST    FORMAT')
     A                                      SFL
     A  32                                  SFLNXTCHG
     A*                                                               / LB0009 /
     A************DDSELT         1A  B  8  2CHECK(ER)
     A            DDSELT         1A  B  8  2
     A*                                                               / LB0009 /
     A  70                                  DSPATR(RI PC)
     A            DDSTUS         1A       +1DSPATR(BL HI)
     A            DDINST         3        +1
     A            DDINSN        20        +1
     A*
     A            DDPERC         3   H
     A            DDDATE         6   H
     A            DDFREQ         1A  H
     A            DDDAYN         2   H
     A            DDTNLU         5  0H
     A*
     A*
     A*****************************************************************
     A* INSTRUMENT SUBFILE CONTROL RECORD FORMAT                      *
     A*****************************************************************
     A*
     A          R LB0215C1                  OVERLAY
     A                                      SFLCTL(LB0215S1)
     A  35                                  SFLDSPCTL
     A  33                                  SFLDSP
     A  34                                  SFLEND
     A  36                                  SFLCLR
     A                                      SFLSIZ(13)
     A                                      SFLPAG(13)
     A                                      ROLLUP(38)
     A                                      ROLLDOWN(39)
     A                                      BLINK
     A* >>>056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<<056584 <<<
     A            RRN1           3S 0H      SFLRCDNBR
     A                                  4 51'Review From'
     A            DDRINS         3A  B    +1
     A            DDLINE        80A     5 01DSPATR(UL)
     A                                  6 06'Instrument'
     A                                      DSPATR(HI)
     A*****************************************************************
     A* SECURITY   SUBFILE RECORD FORMAT                              *
     A*****************************************************************
     A*
     A          R LB0215S2                  TEXT('INST    FORMAT')
     A                                      SFL
     A  32                                  SFLNXTCHG
     A*                                                               / LB0009 /
     A************DDSELT         1A  B  8  2CHECK(ER)
     A            DDSELT         1A  B  8  2
     A*                                                               / LB0009 /
     A  70                                  DSPATR(RI PC)
     A            DDSTUS         1A       +1DSPATR(BL HI)
     A            DDSECS        10        +1
     A            DDSEC1        30        +1
     A            DDSEC2        30        +1
     A*
     A            DDPERC         3   H
     A            DDDATE         6   H
     A            DDFREQ         1A  H
     A            DDDAYN         2   H
     A            DDTNLU         5  0H
     A*
     A*
     A*****************************************************************
     A* SECURITY   SUBFILE CONTROL RECORD FORMAT                      *
     A*****************************************************************
     A*
     A          R LB0215C2                  OVERLAY
     A                                      SFLCTL(LB0215S2)
     A  35                                  SFLDSPCTL
     A  33                                  SFLDSP
     A  34                                  SFLEND
     A  36                                  SFLCLR
     A                                      SFLSIZ(13)
     A                                      SFLPAG(13)
     A                                      ROLLUP(38)
     A                                      ROLLDOWN(39)
     A                                      BLINK
     A* >>>056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<<056584 <<<
     A            RRN2           3S 0H      SFLRCDNBR
     A                                  4 51'Review From'
     A            DDRSEC        10A  B    +1
     A            DDLINE        80A     5 01DSPATR(UL)
     A                                  6 06'Security'
     A                                      DSPATR(HI)
     A*****************************************************************
     A* COUNTRY SUBFILE RECORD FORMAT                                 *
     A*****************************************************************
     A*
     A          R LB0215S3                  TEXT('COUNTRY FORMAT')
     A                                      SFL
     A  32                                  SFLNXTCHG
     A*                                                               / LB0009 /
     A************DDSELT         1A  B  8  2CHECK(ER)
     A            DDSELT         1A  B  8  2
     A*                                                               / LB0009 /
     A  70                                  DSPATR(RI PC)
     A            DDSTUS         1A       +1DSPATR(BL HI)
     A            DDCTRY         2        +1
     A            DDCTRN        30        +1
     A*
     A            DDPERC         3   H
     A            DDDATE         6   H
     A            DDFREQ         1A  H
     A            DDDAYN         2   H
     A            DDTNLU         5  0H
     A*
     A*****************************************************************
     A* COUNTRY SUBFILE CONTROL RECORD FORMAT                         *
     A*****************************************************************
     A*
     A          R LB0215C3                  OVERLAY
     A                                      SFLCTL(LB0215S3)
     A  35                                  SFLDSPCTL
     A  33                                  SFLDSP
     A  34                                  SFLEND
     A  36                                  SFLCLR
     A                                      SFLSIZ(13)
     A                                      SFLPAG(13)
     A                                      ROLLUP(38)
     A                                      ROLLDOWN(39)
     A                                      BLINK
     A* >>>056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<<056584 <<<
     A            RRN3           3S 0H      SFLRCDNBR
     A                                  4 51'Review From'
     A            DDRCTY         2A  B    +1
     A            DDLINE        80A     5 01DSPATR(UL)
     A                                  6 06'Country of Issuer'
     A                                      DSPATR(HI)
     A*****************************************************************
     A* CUSTOMER/INSTRUMENT SUBFILE RECORD FORMAT                     *
     A*****************************************************************
     A*
     A          R LB0215S4                  TEXT('INST    FORMAT')
     A                                      SFL
     A  32                                  SFLNXTCHG
     A*                                                               / LB0009 /
     A************DDSELT         1A  B  8  2CHECK(ER)
     A            DDSELT         1A  B  8  2
     A*                                                               / LB0009 /
     A  70                                  DSPATR(RI PC)
     A            DDSTUS         1A       +1DSPATR(BL HI)
     A            DDCNUM         6        +1TEXT('CUSTOMER NUMBER')
     A            DDPNP8        10        +1TEXT('CUSTOMER SHORTNAME')
     A            DDINST         3        +2TEXT('INSTRUMENT CODE')
     A            DDINSN        20        +1TEXT('INSTRUMENT NAME')
     A*
     A            DDPERC         3   H
     A            DDDATE         6   H
     A            DDFREQ         1A  H
     A            DDDAYN         2   H
     A            DDTNLU         5  0H
     A*                                                           Begin /S01498/
     A            DDBRCH         3   H
     A*                                                           End   /S01498/
     A*
     A*****************************************************************
     A* CUSTOMER/INSTRUMENT SUBFILE CONTROL RECORD FORMAT             *
     A*****************************************************************
     A*
     A          R LB0215C4                  OVERLAY
     A                                      SFLCTL(LB0215S4)
     A  35                                  SFLDSPCTL
     A  33                                  SFLDSP
     A  34                                  SFLEND
     A  36                                  SFLCLR
     A                                      SFLSIZ(13)
     A                                      SFLPAG(13)
     A                                      ROLLUP(38)
     A                                      ROLLDOWN(39)
     A                                      BLINK
     A* >>>056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<<056584 <<<
     A            RRN4           3S 0H      SFLRCDNBR
     A                                  4 51'Review From'
     A            DDRCST         6A  B    +1
     A            DDRINS         3A  B    +2
     A            DDLINE        80A     5 01DSPATR(UL)
     A                                  6 06'Customer'
     A                                      DSPATR(HI)
     A                                  6 25'Instrument'
     A                                      DSPATR(HI)
     A*****************************************************************
     A* CUSTOMER/SECURITY SUBFILE RECORD FORMAT                       *
     A*****************************************************************
     A*
     A          R LB0215S5                  TEXT('SEC    FORMAT')
     A                                      SFL
     A  32                                  SFLNXTCHG
     A*                                                               / LB0009 /
     A************DDSELT         1A  B  8  2CHECK(ER)
     A            DDSELT         1A  B  8  2
     A*                                                               / LB0009 /
     A  70                                  DSPATR(RI PC)
     A            DDSTUS         1A       +1DSPATR(BL HI)
     A            DDCNUM         6        +1TEXT('CUSTOMER NUMBER')
     A            DDPNP8        10        +1TEXT('CUSTOMER SHORTNAME')
     A            DDSECS        10        +2TEXT('SECURITY SHORTNAME')
     A            DDSEC1        30        +1TEXT('REPORT NAME 1')
     A*
     A            DDPERC         3   H
     A            DDDATE         6   H
     A            DDFREQ         1A  H
     A            DDDAYN         2   H
     A            DDTNLU         5  0H
     A*                                                           Begin /S01498/
     A            DDBRCH         3   H
     A*                                                           End   /S01498/
     A*
     A*****************************************************************
     A* CUSTOMER/SECURITY SUBFILE CONTROL RECORD FORAT                *
     A*****************************************************************
     A*
     A          R LB0215C5                  OVERLAY
     A                                      SFLCTL(LB0215S5)
     A  35                                  SFLDSPCTL
     A  33                                  SFLDSP
     A  34                                  SFLEND
     A  36                                  SFLCLR
     A                                      SFLSIZ(13)
     A                                      SFLPAG(13)
     A                                      ROLLUP(38)
     A                                      ROLLDOWN(39)
     A                                      BLINK
     A* >>>056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<<056584 <<<
     A            RRN5           3S 0H      SFLRCDNBR
     A                                  4 49'Review From'
     A            DDRCST         6A  B    +1
     A            DDRSEC        10A  B    +2
     A            DDLINE        80A     5 01DSPATR(UL)
     A                                  6 06'Customer'
     A                                      DSPATR(HI)
     A                                  6 25'Security'
     A                                      DSPATR(HI)
     A*****************************************************************
     A* CUSTOMER HAS BOTH INSTRUMENT AND SECURITY  RECORDS - INSTS    *
     A*****************************************************************
     A*
     A          R LB0215S6                  TEXT('INST    FORMAT')
     A                                      SFL
     A  32                                  SFLNXTCHG
     A*                                                               / LB0009 /
     A************DDSELT         1A  B  7  2CHECK(ER)
     A            DDSELT         1A  B  7  2
     A*                                                               / LB0009 /
     A  70                                  DSPATR(RI PC)
     A            DDSTUS         1A       +1DSPATR(BL HI)
     A            DDCNUM         6        +1TEXT('CUSTOMER NUMBER')
     A            DDPNP8        10        +1TEXT('CUSTOMER SHORTNAME')
     A            DDINST         3        +2TEXT('INSTRUMENT CODE')
     A            DDINSN        20        +1TEXT('INSTRUMENT NAME')
     A*
     A            DDPERC         3   H
     A            DDDATE         6   H
     A            DDFREQ         1A  H
     A            DDDAYN         2   H
     A            DDTNLU         5  0H
     A*                                                           Begin /S01498/
     A            DDBRCH         3   H
     A*                                                           End   /S01498/
     A*
     A*****************************************************************
     A* INSTRUMENT CONTROL RECORD FORMAT                              *
     A*****************************************************************
     A*
     A          R LB0215C6                  OVERLAY
     A                                      SFLCTL(LB0215S6)
     A  35                                  SFLDSPCTL
     A  31                                  SFLDSP
     A  34                                  SFLEND
     A  36                                  SFLCLR
     A                                      SFLSIZ(6)
     A                                      SFLPAG(6)
     A                                      ROLLUP(38)
     A                                      ROLLDOWN(39)
     A                                      BLINK
     A* >>>056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<<056584 <<<
     A            RRN6           3S 0H      SFLRCDNBR
     A                                  4 51'Review From'
     A            DDRINS         3A  B    +1
     A            DDRSEC        10A  B    +1
     A            DDLINE        80A     5 01DSPATR(UL)
     A*
     A                                  6 06'Customer'
     A                                      DSPATR(HI)
     A                                  6 25'Instrument'
     A                                      DSPATR(HI)
     A*****************************************************************
     A* CUSTOMER HAS BOTH INSTRUMENT AND SECURITY  RECORDS - SECURITY *
     A*****************************************************************
     A*
     A          R LB0215S7                  TEXT('SECURITY FORMAT')
     A                                      SFL
     A  32                                  SFLNXTCHG
     A*                                                               / LB0009 /
     A************DDSELT         1A  B 15  2CHECK(ER)
     A            DDSELT         1A  B 15  2
     A*                                                               / LB0009 /
     A  70                                  DSPATR(RI PC)
     A            DDSTUS         1A       +1DSPATR(BL HI)
     A            DDCNUM         6        +1TEXT('CUSTOMER NUMBER')
     A            DDPNP8        10        +1TEXT('CUSTOMER SHORTNAME')
     A            DDSECS        10        +2TEXT('SECURITY SHORTNAME')
     A            DDSEC1        30        +1TEXT('REPORT NAME 1')
     A*
     A            DDPERC         3   H
     A            DDDATE         6   H
     A            DDFREQ         1A  H
     A            DDDAYN         2   H
     A            DDTNLU         5  0H
     A*                                                           Begin /S01498/
     A            DDBRCH         3   H
     A*                                                           End   /S01498/
     A*
     A*****************************************************************
     A* SECURITY CONTROL RECORD FORMAT                                *
     A*****************************************************************
     A*
     A          R LB0215C7                  OVERLAY
     A                                      SFLCTL(LB0215S7)
     A  35                                  SFLDSPCTL
     A  33                                  SFLDSP
     A  42                                  SFLEND
     A  36                                  SFLCLR
     A                                      SFLSIZ(6)
     A                                      SFLPAG(6)
     A                                      ROLLUP(38)
     A                                      ROLLDOWN(39)
     A                                      BLINK
     A* >>>056584 >>>
     A*                                                           Begin /S01498/
     A*                                                           End   /S01498/
     A* <<<056584 <<<
     A            RRN7           3S 0H      SFLRCDNBR
     A                                 14 06'Customer'
     A                                      DSPATR(HI)
     A                                 14 25'Security'
     A                                      DSPATR(HI)
     A*****************************************************************
     A* REVIEW SCREEN FOOTING                                         *
     A*****************************************************************
     A*
     A          R LB0215F3                  OVERLAY
     A                                      BLINK
     A                                      ROLLUP(38)
     A                                      ROLLDOWN(39)
     A                                 22  2'F3=Exit'
     A                                      DSPATR(HI)
     A                                    +2'F12=Previous Screen'
     A                                      DSPATR(HI)
     A                                    +2'Help'
     A                                      DSPATR(HI)
     A                                    +2'Rollup'
     A                                      DSPATR(HI)
     A                                    +2'Rolldown'
     A                                      DSPATR(HI)
     A                                    +2'Action=I,A,D,E'
     A                                      DSPATR(HI)
     A*****************************************************************
     A* FORMAT TO BLANK OUT SUBFILE                                   *
     A*****************************************************************
     A*
     A          R LB0215F5                  TEXT('BLANK FORMAT')
     A                                      OVERLAY
     A                                      ROLLUP(38)
     A                                      ROLLDOWN(39)
     A                                  8 02'   '
     A*
     A*****************************************************************
     A* FORMAT TO BLANK OUT SUBFILE                                   *
     A*****************************************************************
     A*
     A          R LB0215F6                  TEXT('BLANK FORMAT')
     A                                      OVERLAY
     A                                      ROLLUP(38)
     A                                      ROLLDOWN(39)
     A                                 16 02'   '
     A*
     A*****************************************************************
     A* MESSAGE SUBFILE RECORD FORMAT                                 *
     A*****************************************************************
     A*
     A          R LB0215S8                  TEXT('MESSAGE SUBFILE FORMAT')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*****************************************************************
     A* MESSAGE SUBFILE CONTROL RECORD FORMAT                         *
     A*****************************************************************
     A*
     A          R LB0215C8                  OVERLAY
     A                                      BLINK
     A                                      SFLCTL(LB0215S8)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  30                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
     A*
