      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LB Select Portfolio Lending Groups record')
     A*****************************************************************
     A*                                                               *
     A*  Midas PORTFOLIO LENDING                                  *
     A*                                                               *
     A*  LB0226DF - Display File for RPG LB0226  (Select Rec)         *
     A*             Select Portfolio Lending Group                    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. LB0011             DATE 04JUL94               *
     A*  PREV AMEND NO. S01486             DATE 19MAY94               *
     A*                                                               *
     A*-------------------------------------------------------------- *
     A*                                                               *
     A*  LB0011 - Renamed from LB0225S# to LB0226DF                   *
     A*  S01486 - Created for Private Banking Access Object AOGROUR0  *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*
     A*
     A***********                                    Begin /LB0011/
     A**********R LB0225S0                  TEXT('(c) MKI +
     A          R LB0226S0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(33 'Invalid #1SEL     ')
     A  84                                  SFLNXTCHG
     A*
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1CHTP         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B  8  5
     A                                      VALUES('1')
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            #1LCGR         3      8 13TEXT('Port Lend Grp')
     A                                      DSPATR(HI)
     A            #1LCGN        20      8 21TEXT('Group Name')
     A                                      DSPATR(HI)
     A            #1LCGS         2  0   8 48TEXT('Port Lend Seq No')
     A                                      DSPATR(HI)
     A            #1ATTI         2      8 64TEXT('Attached Limit')
     A                                      DSPATR(HI)
     A*
     A*=========================================================================
     A*
     A***********                                    Begin /LB0011/
     A**********R LB0225C0                  TEXT('Subfile control.')
     A***********                           SFLCTL(LB0225S0)
     A          R LB0226C0                  TEXT('Subfile control.')
     A                                      SFLCTL(LB0226S0)
     A***********                                    End   /LB0011/
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2AGCD')
     A                                      SETOF(32 'Invalid: #2AITX')
     A                                      SETOF(33 'Invalid: #2PRTL')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'LB0226F1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 28'Select Portfolio Lending Groups'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2CHTP         1   H      TEXT('Type of Last Change')
     A*
     A                                  4 10'Portfolio'
     A                                      DSPATR(HI)
     A                                  4 26'Group Name'
     A                                      DSPATR(HI)
     A                                  4 41'Portfolio Lending'
     A                                      DSPATR(HI)
     A                                  4 61'Attached'
     A                                      DSPATR(HI)
     A*
     A                                  5  8'Lending Group'
     A                                      DSPATR(HI)
     A                                  5 41'Sequence Number'
     A                                      DSPATR(HI)
     A                                  5 62'Limit'
     A                                      DSPATR(HI)
     A*
     A            #2LCGR         3   B  6 13TEXT('Port Lend Grp')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2LCGN        20   B  6 21TEXT('Group Name')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2LCGS         2  0B  6 48TEXT('Port Lend Seq No')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2ATTI         2   B  6 64TEXT('Attached Limit')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A***********                                    Begin /LB0011/
     A**********R LB0225F0                  TEXT('Command keys.')
     A          R LB0226F0                  TEXT('Command keys.')
     A***********                                    End   /LB0011/
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select request    F3=Exit       -
     A                                                                         -
     A                                               '
     A*=========================================================================
     A***********                                    Begin /LB0011/
     A**********R LB0225S1                  TEXT('Program messages')
     A          R LB0226S1                  TEXT('Program messages')
     A***********                                    End   /LB0011/
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A***********                                    Begin /LB0011/
     A**********R LB0225C1                  TEXT('Program messages')
     A***********                           SFLCTL(LB0225S1)
     A          R LB0226C1                  TEXT('Program messages')
     A                                      SFLCTL(LB0226S1)
     A***********                                    End   /LB0011/
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
