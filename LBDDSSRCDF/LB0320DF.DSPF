      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LB Borrowing Power Enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Lending Module                         *
     A*                                                               *
     A*  DSPF/LB0320DF - Borrowing Power Enquiry                      *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 25Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 081239             Date 03Feb95               *
     A*                 S01498             Date 11Aug94               *
     A*                 056584             DATE 15JUL93               *
     A*                 S01355             DATE 10JUL92               *
     A*                 R0671              DATE 30OCT90               *
     A*                 LB0000             DATE 01JUN90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Webfacing Changes                                   *
     A*  081239 - R10 Private Banking Rework                          *
     A*  S01498 - Portfolio Lending Upgrade to Release 10             *
     A*  056584 - R3 HELP SYSTEM                                      *
     A*  S01355 - RELEASE 3 INCORPORATION                             *
     A*  R0671  - Rounding problem when you are in Details            *
     A*           Borrowing Power.                                    *
     A*  LB0000 - XH/JG FIX.                                          *
     A*                                                               *
     A*****************************************************************
      *
      * File level keywords
      *
     A*                                              Begin /S01498/
     A                                      REF(MFRFL)
     A*                                              End   /S01498/
     A                                      CA03
     A N42 45                               CF11
     A  45                                  CA12
     A***********                                    Begin /081239/
     A**49*******                           CF23
     A                                      CA23
     A***********                                    End   /081239/
     A  49                                  CF24
     A* >>> 056584 >>>
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A* <<< 056584 <<<
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
      * Input screen 1 format
      *
     A          R LB0320F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      BLINK
     A* >>> 056584 >>>
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A* <<< 056584 <<<
     A*                                              Begin /S01498/
     A*********** DDWSID        10A  O  1 51
     A*********** BJMRDT         7A  O  1 63
     A***********                       1 72TIME
     A***********                       2 29'BORROWING POWER ENQUIRY'
     A***********                           DSPATR(HI UL)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'LB0320'
     A                                  1 29'BORROWING POWER ENQUIRY'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*                                              End   /S01498/
     A                                  4  2'Customer Number / Short Name'
     A            DD1CNS        10A  B    +1
     A*/LB0000/*
     A                                      DSPATR(HI)
     A*/LB0000/*
     A N33                                  DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A*                                              Begin /S01498/
     A***********                      22  2'F3=Exit  Help'
     A                                 22  2'F3=Exit'
     A*                                              End   /S01498/
     A                                      DSPATR(HI)
      * Input screen 2 header format
      *
     A          R LB0320H2
     A                                      BLINK
     A*                                              Begin /S01498/
     A*********** DDWSID        10A  O  1 51
     A*********** BJMRDT         7A  O  1 63
     A***********                       1 72TIME
     A***********                       2 29'BORROWING POWER ENQUIRY'
     A***********                           DSPATR(HI UL)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'LB0320'
     A                                  1 29'BORROWING POWER ENQUIRY'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*                                              End   /S01498/
     A                                  4  2'Customer:'
     A            BBCUST         6A  O    +1
     A*/LB0000/*
     A                                      DSPATR(HI)
     A*/LB0000/*
     A            BBCRNM        20A  O    +1
     A*/LB0000/*
     A                                      DSPATR(HI)
     A*/LB0000/*
     A            BBCSSN        10A  O    +1
     A*/LB0000/*
     A                                      DSPATR(HI)
     A*/LB0000/*
     A*--   R0671   --****************  4 56'Borrowing Power in 000''s'
     A                                  6  2'Credit Group'
     A                                  6 28'Instr <---- Borrowing ----> <----+
     A                                       - % Weighting ----->'
     A                                  7  7'Instrument/Security'
     A                                  7 29'Ccy   Power ('
     A            DDCCY          3A  O  7 43
     A*/LB0000/*
     A                                      DSPATR(HI)
     A*/LB0000/*
     A                                  7 47')     %  Client  Sec/Inst'
     A                                  7 74'Country'
      *
      * Input screen 2 subfile record format
      *
     A          R LB0320S2                  SFL
     A            DDCRGP         3A  O  8  2
     A*/LB0000/*
     A                                      DSPATR(HI)
     A*/LB0000/*
     A            DDINSE        21A  O    +1
     A*/LB0000/*
     A  63                                  DSPATR(HI)
     A*/LB0000/*
     A            DDINCY         3A  O    +2
     A*/LB0000/*
     A            DDPOWR        16A  O    +2
     A*/LB0000/*
     A                                      DSPATR(HI)
     A*/LB0000/*
     A            DDPERC         3  0O    +2EDTWRD('  0')
     A*/LB0000/*
     A            DDCLIN         3  0O    +3EDTWRD('  0')
     A*/LB0000/*
     A            DDSECU         3  0O    +3EDTWRD('  0')
     A*/LB0000/*
     A            DDINST         3  0O    +2EDTWRD('  0')
     A*/LB0000/*
     A            DDCTRY         3  0O    +3EDTWRD('  0')
     A*/LB0000/*
     A            DD2LCU         3A  H
     A            DD2CCU         3A  H
     A            DD2LBP        16A  H
     A            DD2CUP        16A  H
      *
      * Input screen 2 subfile control record format
      *
     A          R LB0320C2                  SFLCTL(LB0320S2) OVERLAY
     A                                      BLINK
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A N31                                  ROLLUP(27)
     A* >>> 056584 >>>
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A* <<< 056584 <<<
     A            SFLRR2         4S 0H      SFLRCDNBR
      *
     A*>>>> S01355>>>>
     A***********                      21  2'Lombard Credit Group'
     A                                 21  2'Portfolio Lending Group'
     A*<<<< S01355<<<<
     A            DD2LCG         3A  B    +1
     A*/LB0000/*
     A                                      DSPATR(HI)
     A*/LB0000/*
     A N47                                  DSPATR(UL)
     A  47                                  DSPATR(RI PC)
     A  42                                  DSPATR(ND PR)
      ** Start of change CAAA03
     A**********                       22  2'F3=Exit  F11=Change Currency  F12='              CAAA03
     A**********                             Previous Screen'                                 CAAA03
     A                                 22  2'F3=Exit'
     A                                      DSPATR(HI)
     A N42 45                          22  9'F11=Change Currency'
     A                                      DSPATR(HI)
     A                                 22 28'F12=Previous Screen'
      ** End of change CAAA03
     A                                      DSPATR(HI)
     A*                                              Begin /S01498/
     A*N41*******                      22 53'Help  Rollup  Rolldown'
     A N41                             22 53'Rollup  Rolldown'
     A*                                              End   /S01498/
     A                                      DSPATR(HI)
     A*                                              Begin /S01498/
     A**41*******                      23  2'F23=Portfolio Details Enquiry  F24+
     A***********                            =Documentation  Help  Rollup  Roll+
     A***********                            down'
     A  41                             23  2'F23=Portfolio Details Enquiry  F24+
     A                                       =Documentation  Rollup  Rolldown'
     A*                                              End   /S01498/
     A                                      DSPATR(HI)
      *
      * Message subfile record format
      *
     A          R LB0320S3                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * Message subfile control record format
      *
     A          R LB0320C3                  SFLCTL(LB0320S3) OVERLAY
     A                                      BLINK
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
