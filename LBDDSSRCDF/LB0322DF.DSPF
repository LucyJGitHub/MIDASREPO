      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LB Portfolio Lending Exposure Enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Lending Module                         *
     A*                                                               *
     A*  DSPF/LB0322DF - Portfolio Lending Exposure Enquiry           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01498             Date 11Aug94               *
      *                 063335             Date 16Nov93               *
     A*                 S01355             DATE 10JUL92               *
     A*                 B5929              DATE 02MAY91               *
     A*                 B5929              DATE 02MAY91               *
     A*                 B5926              DATE 02MAY91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01498 - Portfolio Lending Upgrade to Release 10             *
     A*  063335 - USING CMD 23 PRODUCES SOME TROUBLES IN GROUP JOB    *
     A*           AS PORTFOLIO IS ALREADY ACTIVE                      *
     A*  S01355 - RELEASE 3 INCORPORATION                             *
     A*  B7803  - FIX B5929 INCORRECTLY INSERTED (LINE COPIED BUT NOT *
     A*           REMOVED                                             *
     A*  B5929  - ENHANCEMENTS                                        *
     A*  B5926  - *IN63 NOT USED TO HIGHLGHT GROUP AND AMOUNT NOT     *
     A*           HIGHLIGHTED                                         *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  CREATION PARAMETERS                                          *
     A*                                                               *
      *                                                               *
      *****************************************************************
      *-------------------------------------------------------------------------
      * File level keywords
      *-------------------------------------------------------------------------
     A*                                              Begin /S01498/
     A                                      REF(MFRFL)
     A*                                              End   /S01498/
     A                                      CA03
     A N42 45                               CF11
     A  45                                  CA12
     A  45                                  CF13
     A****BEGIN 063335
     A**49**********************************CF23
     A****END   063335
     A  49                                  CF24
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *-------------------------------------------------------------------------
      * Input screen 1 format
      *-------------------------------------------------------------------------
     A          R LB0322F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      BLINK
     A*                                              Begin /S01498/
     A***********
     A***WORKSTATION ID
     A*********** DDWSID        10A  O  1 51
     A***********
     A***DATE****
     A*********** BJMRDT         7A  O  1 63
     A***********                       1 72TIME
     A***********
     A***********                       2 32'EXPOSURE ENQUIRY'
     A***********                           DSPATR(HI UL)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'LB0322'
     A                                  1 32'EXPOSURE ENQUIRY'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*                                              End   /S01498/
     A*
     A                                  4  2'Customer Number / Short Name'
     A*
     A** CUSTOMER NUMBER/S.NAME INPUT FIELD
     A            DD1CNS        10A  B    +1
     A                                      DSPATR(HI)
     A N33                                  DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A*
     A*                                              Begin /S01498/
     A***********                      22  2'F3=Exit  Help'
     A                                 22  2'F3=Exit'
     A*                                              End   /S01498/
     A                                      DSPATR(HI)
     A*
      *-------------------------------------------------------------------------
      * Input screen 2 header format
      *-------------------------------------------------------------------------
     A          R LB0322H2
     A                                      BLINK
     A*
     A*                                              Begin /S01498/
     A***WORKSTATION ID
     A*********** DDWSID        10A  O  1 51
     A***********
     A***DATE****
     A*********** BJMRDT         7A  O  1 63
     A***********                       1 72TIME
     A***********
     A***********                       2 32'EXPOSURE ENQUIRY'
     A***********                           DSPATR(HI UL)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'LB0322'
     A                                  1 32'EXPOSURE ENQUIRY'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*                                              End   /S01498/
     A*
     A                                  4  2'Customer:'
     A*
     A** CUSTOMER NUMBER SELECTED
     A            BBCUST         6A  O    +1
     A                                      DSPATR(HI)
     A*
     A** CUSTOMER REPORT NAME
     A            BBCRNM        20A  O    +1
     A                                      DSPATR(HI)
     A*
     A** CUSTOMER REPORT TOWN
     A            BBCRTN        10A  O    +1
     A                                      DSPATR(HI)
     A* B7803 START
     A************                      4 63'Exposure in 000''s'
     A* B7803 END
     A* B5929 START
     A************                      4 63'Exposure in 000''s'
     A* B5929 END
     A*
     A                                  6  2'Credit Group'
     A                                  6 28'Instr'
     A                                  6 40'Exposure'
     A*
     A                                  7  7'Instrument/Security'
     A                                  7 29'Ccy'
     A                                  7 41'('
     A*
     A** CURRENCY OF EXPOSURE AMOUNT
     A            DDCCY          3A  O  7 43
     A                                      DSPATR(HI)
     A                                  7 47')  Reference'
     A*
      *-------------------------------------------------------------------------
      * Input screen 2 subfile record format
      *-------------------------------------------------------------------------
     A          R LB0322S2                  SFL
     A*
     A** PORTFOLIO LENDING GROUP
     A            DDCRGP         3A  O  8  2
     A                                      DSPATR(HI)
     A*
     A** INSTRUMENT/SECURITY NAME
     A            DDINSE        21A  O    +1
     A* B5926 START
     A************                          DSPATR(HI)
     A  63                                  DSPATR(HI)
     A* B5926 END
     A*
     A** INSTRUMENT CURRENCY
     A            DDINCY         3A  O    +2
     A*
     A** EXPOSURE AMOUNT
     A            DDEXPO        15A  O    +1
     A* B5926 START
     A                                      DSPATR(HI)
     A* B5926 END
     A*
     A** REFERENCE
     A            DDREFE        28A  O    +2
     A*
     A** EXPOSURE AMOUNT, PORTFOLIO LENDING CURRENCY
     A            DD2EXL        15A  H
     A*
     A** EXPOSURE AMOUNT, CUSTOMER CURRENCY
     A            DD2EXC        15A  H
     A*
     A** REFERENCE A
     A            DD2RFA        28A  H
     A*
     A** REFERENCE B
     A            DD2RFB        28A  H
     A*
      *-------------------------------------------------------------------------
      * Input screen 2 subfile control record format
      *-------------------------------------------------------------------------
      *
     A          R LB0322C2                  SFLCTL(LB0322S2) OVERLAY
     A                                      BLINK
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A N31                                  ROLLUP(27)
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A            SFLRR2         4S 0H      SFLRCDNBR
      *
     A*>>>> S01355>>>>
     A***********                      21  2'Lombard Credit Group'
     A                                 21  2'Portfolio Lending Group'
     A*<<<< S01355<<<<
     A            DD2LCG         3A  B    +1
     A                                      DSPATR(HI)
     A*
     A N47                                  DSPATR(UL)
     A  47                                  DSPATR(RI PC)
     A  42                                  DSPATR(ND PR)
     A                                 22  2'F3=Exit'
     A                                      DSPATR(HI)
     A                                    +2'F11=Change Currency'
     A                                      DSPATR(HI)
     A                                    +2'F12=Previous Screen'
     A                                      DSPATR(HI)
     A                                    +2'F13=Other Details'
     A                                      DSPATR(HI)
     A*
     A****BEGIN 063335
     A**41*****************************23  2'F23=Portfolio Details Enquiry'
     A*********************************     DSPATR(HI)
     A*********************************   +2'F24=Documentation'
     A                                 23  2'F24=Documentation'
     A****END   063335
     A                                      DSPATR(HI)
     A*                                              Begin /S01498/
     A***********                         +2'Help  Rollup  Rolldown'
     A                                    +2'Rollup  Rolldown'
     A*                                              End   /S01498/
     A                                      DSPATR(HI)
     A*
      *-------------------------------------------------------------------------
      * Message subfile record format
      *-------------------------------------------------------------------------
     A          R LB0322S3                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
      *-------------------------------------------------------------------------
      * Message subfile control record format
      *-------------------------------------------------------------------------
     A          R LB0322C3                  SFLCTL(LB0322S3) OVERLAY
     A                                      BLINK
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
