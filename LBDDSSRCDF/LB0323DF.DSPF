      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LB Portfolio Lending Collateral Enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Lending Module                         *
     A*                                                               *
     A*  DSPF/LB0323DF - Portfolio Lending Collateral Enquiry         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 081239             DATE 03FEB95               *
     A*  PREV AMEND NO. S01498             DATE 11AUG94               *
     A*                 S01355             DATE 10JUL92               *
     A*                 B5929              DATE 02MAY91               *
     A*                 B5926              DATE 01MAY91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  081239 - R10 Private Banking Rework                          *
     A*  S01498 - Portfolio Lending Upgrade to Release 10             *
     A*  S01355 - RELEASE 3 INCORPORATION                             *
     A*  B5929  - ENHANCEMENTS                                        *
     A*  B5926  - *IN63 NOT USED TO HIGHLGHT GROUP AND AMOUNT NOT     *
     A*           HIGHLIGHTED                                         *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CREATION PARAMETERS                                          *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*-------------------------------------------------------------------------
     A* File level keywords
     A*-------------------------------------------------------------------------
     A*                                              Begin /S01498/
     A                                      REF(MFRFL)
     A*                                              End   /S01498/
     A                                      CA03
     A N42 45                               CF11
     A  45                                  CA12
     A  45                                  CF13
     A***********                                    Begin /081239/
     A**49********                          CF23
     A                                      CA23
     A***********                                    End   /081239/
     A  49                                  CF24
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *-------------------------------------------------------------------------
      * Input screen 1 format
      *-------------------------------------------------------------------------
     A          R LB0323F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      BLINK
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A*
     A*                                              Begin /S01498/
     A***WORKSTATION ID
     A*********** DDWSID        10A  O  1 51
     A***********
     A***DATE****
     A*********** BJMRDT         7A  O  1 63
     A***********                       1 72TIME
     A***********
     A***********                       2 31'COLLATERAL ENQUIRY'
     A***********                           DSPATR(HI UL)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'LB0323'
     A                                  1 31'COLLATERAL ENQUIRY'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*                                              End   /S01498/
     A*
     A                                  4  2'Customer Number / Short Name'
     A*
     A** CUSTOMER NUMBER/S.NAME INPUT FIELD
     A            DD1CNS        10A  B    +1
     A                                      DSPATR(HI)
     A N33                                  DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A*
     A*                                              Begin /S01498/
     A***********                      22  2'F3=Exit  Help'
     A                                 22  2'F3=Exit'
     A*                                              End   /S01498/
     A                                      DSPATR(HI)
     A*
      *-------------------------------------------------------------------------
      * Input screen 2 header format
      *-------------------------------------------------------------------------
     A          R LB0323H2
     A                                      BLINK
     A*
     A*                                              Begin /S01498/
     A***WORKSTATION ID
     A*********** DDWSID        10A  O  1 51
     A***********
     A***DATE****
     A*********** BJMRDT         7A  O  1 63
     A***********                       1 72TIME
     A***********
     A***********                       2 31'COLLATERAL ENQUIRY'
     A***********                           DSPATR(HI UL)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'LB0323'
     A                                  1 31'COLLATERAL ENQUIRY'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*                                              End   /S01498/
     A*
     A                                  4  2'Customer:'
     A*
     A** CUSTOMER NUMBER SELECTED
     A            BBCUST         6A  O    +1
     A                                      DSPATR(HI)
     A*
     A** CUSTOMER REPORT NAME
     A            BBCRNM        20A  O    +1
     A                                      DSPATR(HI)
     A*
     A** CUSTOMER REPORT TOWN
     A            BBCRTN        10A  O    +1
     A                                      DSPATR(HI)
     A* B5929 START
     A************                      4 61'Collateral in 000''s'
     A* B5929 END
     A*
     A                                  6  2'Credit Group'
     A                                  6 28'Instr'
     A                                  6 38'Collateral'
     A*
     A                                  7  7'Instrument/Security'
     A                                  7 29'Ccy'
     A                                  7 41'('
     A*
     A** CURRENCY OF COLLATERAL AMOUNT
     A            DDCCY          3A  O  7 43
     A                                      DSPATR(HI)
     A                                  7 47')  Reference'
     A*
      *-------------------------------------------------------------------------
      * Input screen 2 subfile record format
      *-------------------------------------------------------------------------
     A          R LB0323S2                  SFL
     A*
     A** PORTFOLIO LENDING GROUP
     A            DDCRGP         3A  O  8  2
     A                                      DSPATR(HI)
     A*
     A** INSTRUMENT/SECURITY NAME
     A            DDINSE        21A  O    +1
     A* B5926 START
     A************                          DSPATR(HI)
     A  63                                  DSPATR(HI)
     A* B5926 END
     A*
     A** INSTRUMENT CURRENCY
     A            DDINCY         3A  O    +2
     A*
     A** COLLATERAL AMOUNT
     A            DDCOLL        15A  O    +1
     A* B5926 START
     A                                      DSPATR(HI)
     A* B5926 END
     A*
     A** REFERENCE
     A            DDREFE        28A  O    +2
     A*
     A** COLLATERAL AMOUNT, PORTFOLIO LENDING CURRENCY
     A            DD2CLL        15A  H
     A*
     A** COLLATERAL AMOUNT, CUSTOMER CURRENCY
     A            DD2CLC        15A  H
     A*
     A** REFERENCE A
     A            DD2RFA        28A  H
     A*
     A** REFERENCE B
     A            DD2RFB        28A  H
     A*
      *-------------------------------------------------------------------------
      * Input screen 2 subfile control record format
      *-------------------------------------------------------------------------
      *
     A          R LB0323C2                  SFLCTL(LB0323S2) OVERLAY
     A                                      BLINK
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A N31                                  ROLLUP(27)
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A            SFLRR2         4S 0H      SFLRCDNBR
      *
     A*>>>> S01355>>>>
     A***********                      21  2'Lombard Credit Group'
     A                                 21  2'Portfolio Lending Group'
     A*<<<< S01355<<<<
     A            DD2LCG         3A  B    +1
     A                                      DSPATR(HI)
     A*
     A N47                                  DSPATR(UL)
     A  47                                  DSPATR(RI PC)
     A  42                                  DSPATR(ND PR)
     A                                 22  2'F3=Exit'
     A                                      DSPATR(HI)
     A                                    +2'F11=Change Currency'
     A                                      DSPATR(HI)
     A                                    +2'F12=Previous Screen'
     A                                      DSPATR(HI)
     A                                    +2'F13=Other Details'
     A                                      DSPATR(HI)
     A*
     A  41                             23  2'F23=Portfolio Details Enquiry'
     A                                      DSPATR(HI)
     A                                    +2'F24=Documentation'
     A                                      DSPATR(HI)
     A*                                              Begin /S01498/
     A***********                         +2'Help  Rollup  Rolldown'
     A                                    +2'Rollup  Rolldown'
     A*                                              End   /S01498/
     A                                      DSPATR(HI)
     A*
      *-------------------------------------------------------------------------
      * Message subfile record format
      *-------------------------------------------------------------------------
     A          R LB0323S3                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
      *-------------------------------------------------------------------------
      * Message subfile control record format
      *-------------------------------------------------------------------------
     A          R LB0323C3                  SFLCTL(LB0323S3) OVERLAY
     A                                      BLINK
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
