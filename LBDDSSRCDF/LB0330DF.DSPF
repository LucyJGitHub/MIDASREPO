      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LB Borrowing Power weightings Enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Lending Module                         *
     A*                                                               *
     A*  DSPF/LB0330DF - Borrowing Power Weightings Enquiry           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01498             DATE 11AUG94               *
     A*  PREV AMEND NO. 056584     GB      DATE 15JUL93               *
     A*                 37774              DATE 01MAY90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01498 - Portfolio Lending Upgrade to Release 10             *
     A*  056584 - R3 HELP SYSTEM                                      *
     A*  37774  - Incorrect number of spaces between Rollup           *
     A*            and Rolldown on prompt line                        *
     A*****************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*
     A*                                              Begin /S01498/
     A                                      REF(MFRFL)
     A*                                              End   /S01498/
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CA03
     A  41                                  CA12
     A  38                                  CA24
     A  73                                  CA23
     A                                      INDARA
     A* >>> 056584 >>>
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A* <<< 056584 <<<
     A                                      MSGLOC(24)
     A                                      PRINT(MPRINT)
     A*****************************************************************
     A* SCREEN HEADER                                                 *
     A*****************************************************************
     A*
     A          R LB0330F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A                                      BLINK
     A*                                              Begin /S01498/
     A*********** DDJOB         10A  O 01 51
     A*********** BJMRDT         7A  O 01 63
     A***********                    O 01 72TIME
     A***********                       2 24'BORROWING POWER WEIGHTINGS ENQU+
     A***********                           IRY'
     A***********                           DSPATR(HI UL)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'LB0330'
     A                                  1 24'BORROWING POWER WEIGHTINGS +
     A                                      ENQUIRY'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*                                              End   /S01498/
     A*****************************************************************
     A* MESSAGE SUBFILE RECORD FORMAT                                 *
     A*****************************************************************
     A*
     A          R LB0330S1                  TEXT('MESSAGE SUBFILE FORMAT')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*****************************************************************
     A* MESSAGE SUBFILE CONTROL RECORD FORMAT                         *
     A*****************************************************************
     A*
     A          R LB0330C1                  OVERLAY
     A                                      BLINK
     A                                      SFLCTL(LB0330S1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
     A*
     A*****************************************************************
     A* SCREEN 1                                                      *
     A*****************************************************************
     A*
     A          R LB0330F2                  OVERLAY
     A                                      BLINK
     A* >>> 056584 >>>
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A* <<< 056584 <<<
     A                                  4  2'Customer Number / Short Name'
     A            DDPNP8        10A  B  4 31
     A N47N22
     AAN21N27N46                            DSPATR(HI UL)
     A  21
     AO 27
     AO 46
     AO 47
     AO 22                                  DSPATR(RI PC)
     A                                  4 43'Country'
     A            DDCTRY         2A  B  4 51
     A N31N23
     AAN21N24N22                            DSPATR(HI UL)
     A  21
     AO 24
     AO 22
     AO 31
     AO 23                                  DSPATR(RI PC)
     A                                  4 55'Instrument Type'
     A            DDINST         3A  B  4 71
     A N29N23
     AAN21N25N46                            DSPATR(HI UL)
     A  21
     AO 25
     AO 46
     AO 29
     AO 23                                  DSPATR(RI PC)
     A                                  5  2'Security'
     A            DDSECR        10A  B  5 11
     A N47N25
     AAN21N24N30                            DSPATR(HI UL)
     A  21
     AO 24
     AO 30
     AO 47
     AO 25                                  DSPATR(RI PC)
     A*                                              Begin /S01498/
     A***********                      22  2'F3=Exit  Help'
     A                                 22  2'F3=Exit'
     A*                                              End   /S01498/
     A                                      DSPATR(HI)
     A*****************************************************************
     A* COUNTRY SUBFILE RECORD FORMAT                                 *
     A*****************************************************************
     A*
     A          R LB0330S2                  TEXT('COUNTRY FORMAT')
     A                                      SFL
     A            DDCTRC         2      5  2
     A                                      DSPATR(HI)
     A            DDCTRN        30      5  5
     A                                      DSPATR(HI)
     A            DDCTRP         3  0   5 40
     A                                      DSPATR(HI)
     A                                      EDTWRD('   %')
     A  45                                  DSPATR(ND)
     A            DDCTRD         7      5 52
     A                                      DSPATR(HI)
     A            DDCTRF         1      5 66
     A                                      DSPATR(HI)
     A            DDCTRO         2  0   5 75
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A* COUNTRY SUBFILE CONTROL RECORD FORMAT                         *
     A*****************************************************************
     A*
     A          R LB0330C2                  OVERLAY
     A                                      SFLCTL(LB0330S2)
     A  35                                  SFLDSPCTL
     A  33                                  SFLDSP
     A  34                                  SFLEND
     A  36                                  SFLCLR
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A N34                                  ROLLUP(39)
     A                                      BLINK
     A            RRN            3S 0H      SFLRCDNBR
     A                                  4  2'Country     '
     A                                  4 36' Percentage '
     A                                  4 49' Review Date '
     A                                  4 64' Freq. '
     A                                  4 72' Day No. '
     A*****************************************************************
     A* INSTRUMENT SUBFILE RECORD FORMAT                              *
     A*****************************************************************
     A*
     A          R LB0330S3                  TEXT('INST    FORMAT')
     A                                      SFL
     A            DDINTT         3      5  2
     A                                      DSPATR(HI)
     A            DDINTN        20      5  6
     A                                      DSPATR(HI)
     A            DDINTP         3  0   5 40
     A                                      DSPATR(HI)
     A                                      EDTWRD('   %')
     A  45                                  DSPATR(ND)
     A            DDINTD         7      5 52
     A                                      DSPATR(HI)
     A            DDINTF         1      5 66
     A                                      DSPATR(HI)
     A            DDINTO         2  0   5 75
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A* INSTRUMENT SUBFILE CONTROL RECORD FORMAT                      *
     A*****************************************************************
     A*
     A          R LB0330C3                  OVERLAY
     A                                      SFLCTL(LB0330S3)
     A  35                                  SFLDSPCTL
     A  33                                  SFLDSP
     A  34                                  SFLEND
     A  36                                  SFLCLR
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A N34                                  ROLLUP(39)
     A                                      BLINK
     A            RRN1           3S 0H      SFLRCDNBR
     A                                  4  2'Instrument  '
     A                                  4 36' Percentage '
     A                                  4 49' Review Date '
     A                                  4 64' Freq. '
     A                                  4 72' Day No. '
     A*****************************************************************
     A* SECURITY   SUBFILE RECORD FORMAT                              *
     A*****************************************************************
     A*
     A          R LB0330S4                  TEXT('INST    FORMAT')
     A                                      SFL
     A            DDSECT        10      5  2
     A                                      DSPATR(HI)
     A            DDSECN        20      5 15
     A                                      DSPATR(HI)
     A            DDSECP         3  0   5 40
     A                                      DSPATR(HI)
     A                                      EDTWRD('   %')
     A  45                                  DSPATR(ND)
     A            DDSECD         7      5 52
     A                                      DSPATR(HI)
     A            DDSECF         1      5 66
     A                                      DSPATR(HI)
     A            DDSECO         2  0   5 75
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A* SECURITY   SUBFILE CONTROL RECORD FORMAT                      *
     A*****************************************************************
     A*
     A          R LB0330C4                  OVERLAY
     A                                      SFLCTL(LB0330S4)
     A  35                                  SFLDSPCTL
     A  33                                  SFLDSP
     A  34                                  SFLEND
     A  36                                  SFLCLR
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A N34                                  ROLLUP(39)
     A                                      BLINK
     A            RRN2           3S 0H      SFLRCDNBR
     A                                  4  2'Security'
     A                                  4 36' Percentage '
     A                                  4 49' Review Date '
     A                                  4 64' Freq. '
     A                                  4 72' Day No. '
     A*****************************************************************
     A* CUSTOMER HAS ONLY INSTRUMENT RECORDS RECORD FORMAT            *
     A*****************************************************************
     A*
     A          R LB0330S5                  TEXT('INST    FORMAT')
     A                                      SFL
     A            DDINTT         3      6  2
     A                                      DSPATR(HI)
     A            DDINTN        20      6  6
     A                                      DSPATR(HI)
     A            DDINTP         3  0   6 40
     A                                      DSPATR(HI)
     A                                      EDTWRD('   %')
     A  45                                  DSPATR(ND)
     A            DDINTD         7      6 52
     A                                      DSPATR(HI)
     A            DDINTF         1      6 66
     A                                      DSPATR(HI)
     A            DDINTO         2  0   6 75
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A* CUSTOMER HAS ONLY INSTRUMENT RECODS CONTROL RECORD FORMAT     *
     A*****************************************************************
     A*
     A          R LB0330C5                  OVERLAY
     A                                      SFLCTL(LB0330S5)
     A  35                                  SFLDSPCTL
     A  33                                  SFLDSP
     A  34                                  SFLEND
     A  36                                  SFLCLR
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A N34                                  ROLLUP(39)
     A                                      BLINK
     A            RRN3           3S 0H      SFLRCDNBR
     A                                  4  2'Customer'
     A            DDCUNO         6   O  4 12
     A                                      DSPATR(HI)
     A            DDRPTN        20A  O  4 19
     A                                      DSPATR(HI)
     A            DDTOWN        10A  O  4 40
     A                                      DSPATR(HI)
     A                                  5  2'Instrument  '
     A                                  5 36' Percentage '
     A                                  5 49' Review Date '
     A                                  5 64' Freq. '
     A                                  5 72' Day No. '
     A*****************************************************************
     A* CUSTOMER HAS ONLY SECURITY RECORDS  RECORD FORMAT             *
     A*****************************************************************
     A*
     A          R LB0330S6                  TEXT('INST    FORMAT')
     A                                      SFL
     A            DDSECT        10      6  2
     A                                      DSPATR(HI)
     A            DDSECN        20      6 15
     A                                      DSPATR(HI)
     A            DDSECP         3  0   6 40
     A                                      DSPATR(HI)
     A                                      EDTWRD('   %')
     A  45                                  DSPATR(ND)
     A            DDSECD         7      6 52
     A                                      DSPATR(HI)
     A            DDSECF         1      6 66
     A                                      DSPATR(HI)
     A            DDSECO         2  0   6 75
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A* CUSTOMER HAS ONLY SECURITY RECORDS RECORD FORMAT              *
     A*****************************************************************
     A*
     A          R LB0330C6                  OVERLAY
     A                                      SFLCTL(LB0330S6)
     A  35                                  SFLDSPCTL
     A  33                                  SFLDSP
     A  34                                  SFLEND
     A  36                                  SFLCLR
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A N34                                  ROLLUP(39)
     A                                      BLINK
     A            RRN4           3S 0H      SFLRCDNBR
     A                                  4  2'Customer'
     A            DDCUNO         6   O  4 12
     A                                      DSPATR(HI)
     A            DDRPTN        20A  O  4 19
     A                                      DSPATR(HI)
     A            DDTOWN        10A  O  4 40
     A                                      DSPATR(HI)
     A                                 05  2'Security'
     A                                      DSPATR(UL)
     A                                 05 36' Percentage '
     A                                 05 49' Review Date '
     A                                 05 64' Freq. '
     A                                 05 72' Day No. '
     A*****************************************************************
     A* CUSTOMER HAS BOTH SECURITY AND INSTRUMENTS RECORDS - INSTS    *
     A*****************************************************************
     A*
     A          R LB0330S8                  TEXT('INST    FORMAT')
     A                                      SFL
     A            DDINTT         3      6  2
     A                                      DSPATR(HI)
     A            DDINTN        20      6  6
     A                                      DSPATR(HI)
     A            DDINTP         3  0   6 40
     A                                      DSPATR(HI)
     A                                      EDTWRD('   %')
     A  45                                  DSPATR(ND)
     A            DDINTD         7      6 52
     A                                      DSPATR(HI)
     A            DDINTF         1      6 66
     A                                      DSPATR(HI)
     A            DDINTO         2  0   6 75
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A* INSTRUMENT CONTROL RECORD FORMAT                              *
     A*****************************************************************
     A*
     A          R LB0330C8                  OVERLAY
     A                                      SFLCTL(LB0330S8)
     A  35                                  SFLDSPCTL
     A  33                                  SFLDSP
     A  34                                  SFLEND
     A  36                                  SFLCLR
     A                                      SFLSIZ(7)
     A                                      SFLPAG(6)
     A N34                                  ROLLUP(39)
     A                                      BLINK
     A            RRN5           3S 0H      SFLRCDNBR
     A                                  4  2'Customer:'
     A            DDCUNO         6   O  4 12
     A                                      DSPATR(HI)
     A            DDRPTN        20A  O  4 19
     A                                      DSPATR(HI)
     A            DDTOWN        10A  O  4 40
     A                                      DSPATR(HI)
     A                                  5  2'Instrument  '
     A                                      DSPATR(UL)
     A                                  5 36' Percentage '
     A                                  5 49' Review Date '
     A                                  5 64' Freq. '
     A                                  5 72' Day No. '
     A*****************************************************************
     A* CUSTOMER HAS BOTH SECURITY AND INSTRUMENTS RECORDS - SECURITY *
     A*****************************************************************
     A*
     A          R LB0330S9                  TEXT('INST    FORMAT')
     A                                      SFL
     A            DDSECT        10     13  2
     A                                      DSPATR(HI)
     A            DDSECN        20     13 15
     A                                      DSPATR(HI)
     A            DDSECP         3  0  13 40
     A                                      DSPATR(HI)
     A                                      EDTWRD('   %')
     A  45                                  DSPATR(ND)
     A            DDSECD         7     13 52
     A                                      DSPATR(HI)
     A            DDSECF         1     13 66
     A                                      DSPATR(HI)
     A            DDSECO         2  0  13 75
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A* SECURITY CONTROL RECORD FORMAT                                *
     A*****************************************************************
     A*
     A          R LB0330C9                  OVERLAY
     A                                      SFLCTL(LB0330S9)
     A  35                                  SFLDSPCTL
     A  33                                  SFLDSP
     A  42                                  SFLEND
     A  36                                  SFLCLR
     A                                      SFLSIZ(7)
     A                                      SFLPAG(6)
     A N42                                  ROLLUP(39)
     A                                      BLINK
     A            RRN6           3S 0H      SFLRCDNBR
     A                                 12  2'Security'
     A                                      DSPATR(UL)
     A                                 12 36' Percentage '
     A                                 12 49' Review Date '
     A                                 12 64' Freq. '
     A                                 12 72' Day No. '
     A*****************************************************************
     A* SUBFILE SELECTION SCREEN                                      *
     A*****************************************************************
     A*
     A          R LB0330F3                  OVERLAY
     A                                      BLINK
     A N34                                  ROLLUP(39)
     A* >>> 056584 >>>
     A*---------
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A* <<< 056584 <<<
     A                                 20  2'Customer Number / Short Name'
     A            DDPNP8        10A  B 20 31
     A                                      DSPATR(HI UL)
     A                                 20 43'Country'
     A            DDCTRY         2A  B 20 51
     A                                      DSPATR(HI UL)
     A                                 20 55'Instrument Type'
     A            DDINST         3A  B 20 71
     A                                      DSPATR(HI UL)
     A                                 21  2'Security'
     A            DDSECR        10A  B 21 11
     A                                      DSPATR(HI UL)
     A                                 22  2'F3=Exit'
     A                                      DSPATR(HI)
     A  41                             22 11'F12=Previous Screen'
     A                                      DSPATR(HI)
     A  73                             22 32'F23=Portfolio Details +
     A                                      Enquiry'
     A                                      DSPATR(HI)
     A  38                             22 63'F24=Documentation'
     A                                      DSPATR(HI)
     A*
     A*                                              Begin /S01498/
     A*N38N73****                      22 32'Help'
     A***********                           DSPATR(HI)
     A*                                              End   /S01498/
     A N38N73                          22 38'Rollup'
     A                                      DSPATR(HI)
     A*                                                    / 37774  /
     A***N38N73                          22 47'Rolldown'
     A N38N73                          22 46'Rolldown'
     A*                                                    / 37774  /
     A                                      DSPATR(HI)
     A*
     A*                                              Begin /S01498/
     A**73*38****                      23  2'Help'
     A***********                           DSPATR(HI)
     A**73*38****                      23  8'Rollup'
     A***********                           DSPATR(HI)
     A**73*38****                      23 16'Rolldown'
     A***********                           DSPATR(HI)
     A  73 38                          23  2'Rollup'
     A                                      DSPATR(HI)
     A  73 38                          23 10'Rolldown'
     A                                      DSPATR(HI)
     A*                                              End   /S01498/
