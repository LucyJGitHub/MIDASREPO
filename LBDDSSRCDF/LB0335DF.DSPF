      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LB Portfolio Lending Instruments/Groups Enq')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Lending Module                         *
     A*                                                               *
     A*  DSPF/LB0335DF - Portfolio Lending Instrument/Group Enquiry   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01498             Date 11Aug94               *
      *                 056584             Date 15Jul93               *
     A*                 S01355             DATE 10JUL92               *
     A*                 SA9018             DATE 17JAN91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01498 - Portfolio Lending Upgrade to Release 10             *
     A*  056584 - R3 HELP SYSTEM                                      *
     A*  S01355 - RELEASE 3 INCORPORATION                             *
     A*  SA9018 - REMOVE ATTACHED LIMIT                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  CREATION PARAMETERS                                          *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
      * File level keywords
      *
     A*                                              Begin /S01498/
     A                                      REF(MFRFL)
     A*                                              End   /S01498/
     A                                      CA03
     A* >>> 056584 >>>
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A* <<< 056584 <<<
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
      * Input screen 1 format
      *
     A          R LB0335F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      BLINK
     A* >>> 056584 >>>
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A* <<< 056584 <<<
     A*                                              Begin /S01498/
     A*********** DDWSID        10A  O  1 51
     A*********** BJMRDT         7A  O  1 63
     A***********                       1 72TIME
     A*                                              End   /S01498/
     A*>>>> S01355>>>>
     A***********                       2 22'LOMBARD CREDIT INSTRUMENT-
     A*                                              Begin /S01498/
     A***********                       2 22'PORTFOLIO LENDING INSTRUMENT-
     A***********                            GROUP ENQUIRY'
     A*                                              End   /S01498/
     A*<<<< S01355<<<<
     A*                                              Begin /S01498/
     A***********                           DSPATR(HI UL)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'LB0335'
     A                                  1 22'PORTFOLIO LENDING INSTRUMENT/-
     A                                      GROUP ENQUIRY'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*                                              End   /S01498/
     A                                  5  2'Group Code'
     A            DDGCDE         3A  B    +1
     A                                      DSPATR(HI)
     A N40                                  DSPATR(UL)
     A  40                                  DSPATR(RI PC)
     A                                    +3'Instrument Code'
     A            DDICDE         3A  B    +1
     A                                      DSPATR(HI)
     A N41                                  DSPATR(UL)
     A  41                                  DSPATR(RI PC)
     A*                                              Begin /S01498/
     A***********                      22  2'F3=Exit Help  '
     A                                 23  3'F3=Exit  '
     A*                                              End   /S01498/
     A                                      DSPATR(HI)
      *
      * Screen 2 format
      *
     A          R LB0335F2
     A*                                              Begin /S01498/
     A*********** DDWSID        10A  O  1 51
     A*********** BJMRDT         7A  O  1 63
     A***********                       1 72TIME
     A*                                              End   /S01498/
     A*>>>> S01355>>>>
     A***********                       2 22'LOMBARD CREDIT INSTRUMENT-
     A*                                              Begin /S01498/
     A***********                       2 22'PORTFOLIO LENDING INSTRUMENT-
     A*                                              End   /S01498/
     A*<<<< S01355<<<<
     A*                                              Begin /S01498/
     A***********                            GROUP ENQUIRY'
     A***********                           DSPATR(HI UL)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'LB0335'
     A                                  1 22'PORTFOLIO LENDING INSTRUMENT/-
     A                                      GROUP ENQUIRY'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*                                              End   /S01498/
     A  24                              4  2'Requested Group'
     A  24        DDGCDE         3A  O    +1
     A                                      DSPATR(HI)
     A  25                              4  2'Requested Instrument'
     A  25        DDICDE         3A  O    +1
     A                                      DSPATR(HI)
     A                                  6  2'Credit Group'
     A                                    +2'Type'
     A                                    +4'Group/Instrument Name'
     A                                    +6'Sequence'
     A**SA9018                            +6'Attached Limit'
      *
      * Screen 2 subfile record format
      *
     A          R LB0335S2                  SFL
     A            DD2OUT        79A  O  7  2
     A                                      DSPATR(HI)
      *
      * Input screen 2 subfile control record format
      *
     A          R LB0335C2                  SFLCTL(LB0335S2) OVERLAY
     A                                      CF12
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A                                      BLINK
     A N31                                  ROLLUP(27)
     A            SFLRR2         4S 0H      SFLRCDNBR
      *
     A*                                              Begin /S01498/
     A***********                      22  2'F3=Exit  F12=Previous Screen  +
     A***********                           Help  Rollup  Rolldown'
     A                                 23  3'F3=Exit  F12=Previous Screen  +
     A                                      Rollup  Rolldown'
     A*                                              End   /S01498/
     A                                      DSPATR(HI)
      *
      * Message subfile record format
      *
     A          R LB0335S1                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * Message subfile control record format
      *
     A          R LB0335C1                  SFLCTL(LB0335S1) OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      BLINK
     A            DDPGMQ                    SFLPGMQ
