      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LB Portfolio Lending Customer Enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Lending Module                         *
     A*                                                               *
     A*  DSPF/LB0340DF - Portfolio Lending Customer Enquiry           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01498             DATE 11AUG94               *
     A*  PREV AMEND NO. 056584             DATE 15JUL93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01498 - Portfolio Lending Upgrade to Release 10             *
     A*  056584 - R3 HELP SYSTEM                                      *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  CREATION PARAMETERS                                          *
     A*                                                               *
      *                                                               *
      *****************************************************************
      *
      * File level keywords
      *
     A*                                              Begin /S01498/
     A                                      REF(MFRFL)
     A*                                              End   /S01498/
     A                                      CA03
     A  51                                  CA12
     A* >>> 056584 >>>
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A* <<< 056584 <<<
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
      * Input screen 1 format
      *
     A          R LB0340F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      BLINK
     A* >>> 056584 >>>
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A* <<< 056584 <<<
     A*                                              Begin /S01498/
     A*********** DDWSID        10A  O  1 51
     A*********** BJMRDT         7A  O  1 63
     A***********                       1 72TIME
     A***********                       2 33'CUSTOMER ENQUIRY'
     A***********                           DSPATR(HI UL)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'LB0340'
     A                                  1 33'CUSTOMER ENQUIRY'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*                                              End   /S01498/
     A                                  4  2'Customer Number / -
     A                                      Short name'
     A            DDFLD1        10A  B    +1
     A                                      DSPATR(HI)
     A N40                                  DSPATR(UL)
     A  40                                  DSPATR(RI PC)
     A*                                              Begin /S01498/
     A***********                      22  2'F3=Exit  Help'
     A                                 22  2'F3=Exit'
     A*                                              End   /S01498/
     A                                      DSPATR(HI)
      *
      * Screen 2 format
      *
     A          R LB0340F2
     A                                      BLINK
     A*                                              Begin /S01498/
     A*********** DDWSID        10A  O  1 51
     A*********** BJMRDT         7A  O  1 63
     A***********                       1 72TIME
     A***********                       2 33'CUSTOMER ENQUIRY'
     A***********                           DSPATR(HI UL)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'LB0340'
     A                                  1 33'CUSTOMER ENQUIRY'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*                                              End   /S01498/
     A                                  4 10'Short name'
     A                                    +5'Cust. No.'
     A                                    +4'Report Name'
     A                                   +15'Report Town'
      *
      * Screen 2 subfile record format
      *
     A          R LB0340S2                  SFL
     A  32                                  SFLNXTCHG
     A            DD2SEL         1   B  5  3
     A  50                                  DSPATR(HI)
     A N50                                  DSPATR(ND)
     A N50                                  DSPATR(PR)
     A N41 50                               DSPATR(UL)
     A  41 50                               DSPATR(RI PC)
     A            DD2SNM        10   O  5 10
     A                                      DSPATR(HI)
     A            DD2CNO         6  0   5 26
     A                                      DSPATR(HI)
     A            DD2RNM        20   O  5 38
     A                                      DSPATR(HI)
     A            DD2RTN        10   O  5 64
     A                                      DSPATR(HI)
      *
      * Input screen 2 subfile control record format
      *
     A          R LB0340C2                  SFLCTL(LB0340S2) OVERLAY
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A                                      SFLSIZ(16)
     A                                      SFLPAG(15)
     A                                      BLINK
     A N31                                  ROLLUP(27)
     A* >>> 056584 >>>
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A* <<< 056584 <<<
     A* >>> 056584 >>>
     A*                                              Begin /S01498/
     A*                                              End   /S01498/
     A* <<< 056584 <<<
     A            SFLRR2         4S 0H      SFLRCDNBR
      *
      * Customer/shortname field
      *
     A                                 21  2'Customer Number / -
     A                                      Short name'
     A            DDFLD2        10A  B    +1
     A                                      DSPATR(HI)
     A N40                                  DSPATR(UL)
     A  40                                  DSPATR(RI)
     A N50                                  DSPATR(PC)
     A*                                              Begin /S01498/
     A***********                      22  2'F3=Exit  F12=Previous -
     A***********                           Screen  Help  Rollup -
     A***********                            Rolldown'
     A                                 22  2'F3=Exit  F12=Previous -
     A                                      Screen  Rollup  Rolldown'
     A*                                              End   /S01498/
     A                                      DSPATR(HI)
      *
      *
      * Message subfile record format
      *
     A          R LB0340S1                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * Message subfile control record format
      *
     A          R LB0340C1                  SFLCTL(LB0340S1) OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      BLINK
     A            DDPGMQ                    SFLPGMQ
