      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Commit. Limit awaiting authorisation')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Lending Module                         *
     A*                                                               *
     A*  PRTF/LB0350P1 - Total Commitment Lines Awaiting Authorisation*
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01498             Date 11Aug94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01498 - Portfolio Lending Upgrade to Release 10             *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*                                              Begin /S01498/
     A                                      REF(MFRFL)
     A*                                              End   /S01498/
     A*
     A* Page Headings
     A* -------------
     A          R HEAD1                     TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*                                              Begin /S01498/
     A***********                       3  7'REFERENCE LB0350P1  '
     A                                  3  6'REFERENCE LB0350P1  '
     A*                                              End   /S01498/
     A            BJURPT        53A     3 40
     A                                  3 97'    DATE'
     A            BJMRDT         7A     3106
     A                                  3115'    PAGE'
     A            PAGE           4  0   3124
     A                                      EDTCDE(Z)
     A                                  5 40'TOTAL COMMITMENT LINES AWAITING-
     A                                       AUTHORISATION REPORT'
     A*                                              Begin /S01498/
     A***********                           UNDERLINE
     A  39                              5 94'BRANCH'
     A  39        RRBRCA    R           5101REFFLD(BRCA)
     A  39                              5104'-'
     A  39        RRBRNM    R           5105REFFLD(BRNM)
     A                                  6 40'-------------------------------+
     A                                      ---------------------'
     A*                                              End   /S01498/
     A*
     A* Page Sub Headings
     A* -----------------
     A          R HEAD2
     A                                  7  6'BRANCH :'
     A            RRSPTA         3      7 15
     A                                  8  6'ACCOUNT OFFICER :'
     A            RRACOC         2      8 24
     A            RRACON        30      8 27
     A*
     A                                 10  4' CUST  '
     A                                 10 41'      COMMITMENT'
     A                                 10 64' START  '
     A                                 10 74' EXPIRY  '
     A                                 10 83'    REVIEW   '
     A                                 10 97'  DAY'
     A                                 10109'  LAST ACTIVITY   '
     A                                 11  4'  NO   '
     A                                 11 12'REPORT NAME         '
     A                                 11 34'CCY  '
     A                                 11 40'      LINE AMOUNT'
     A                                 11 58' AUTH'
     A                                 11 64' DATE   '
     A                                 11 74' DATE    '
     A                                 11 84' DATE '
     A                                 11 91' FREQ '
     A                                 11 97'  NO '
     A                                 11104' ACTION'
     A                                 11113'DATE'
     A                                 11121'USER'
     A*
     A* Transaction file detail line
     A* ----------------------------
     A          R TRNDTL
     A*
     A            RRCNUM         6         4
     A                                      SPACEB(2)
     A            RRCRNM        20        12
     A            RRCTCY         3        34
     A*
     A            RRAMT1        18        40
     A                                    60'N'
     A            RRCTTD         7        64
     A            RRCTEX         7        74
     A            RRCTVD         7        84
     A            RRCTVF         1        94
     A            RRCTVN         2  0     99EDTCDE(4)
     A            RRCTTP         6       105
     A            RRCTLC         7       112
     A            RRCTUS        10       121
     A*
     A  21        RRCTN1        35        34
     A                                      SPACEB(1)
     A  21        RRCTN2        35        75
     A  22        RRCTN3        35        34
     A                                      SPACEB(1)
     A  22        RRCTN4        35        75
     A*
     A*                                              Begin /S01498/
     A**Null*Report****************************************************
     A**-----------****************************************************
     A**********R NODTL
     A***********                         51'*** NO DETAILS TO REPORT ***'
     A***********                           SPACEB(4)
     A*
     A* Database Error
     A* --------------
     A          R DBERROR
     A                                    47'*** REFERENCE LB0350 +
     A                                      DATABASE ERROR ***'
     A                                      SPACEB(2)
     A*                                              End   /S01498/
     A*
     A* Trailer
     A* -------
     A          R ENDREP
     A*                                              Begin /S01498/
     A***********                         55'*** END OF REPORT ***'
     A***********                           SPACEB(4)
     A N39                                55'*** END OF REPORT ***'
     A N39                                  SPACEB(4)
     A  39                                53'*** END OF BRANCH REPORT ***'
     A  39                                  SPACEB(4)
     A*                                              End   /S01498/
