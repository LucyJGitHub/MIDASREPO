      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LB Commitment Limit report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Lending Module                         *
     A*                                                               *
     A*  PRTF/LB0351P1 - Total Commitment Lines Report                *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01498             DATE 11AUG94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01498 - Portfolio Lending Upgrade to Release 10             *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*                                              Begin /S01498/
     A                                      REF(MFRFL)
     A*                                              End   /S01498/
     A*
     A* Page Headings
     A* -------------
     A          R HEAD1                     TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                              Begin /S01498/
     A***********                       3  7'REFERENCE LB0351P1  '
     A                                  3  6'REFERENCE LB0351P1  '
     A*                                              End   /S01498/
     A            BJURPT        53A     3 41
     A                                  3 97'    DATE'
     A            BJMRDT         7A     3106
     A                                  3115'    PAGE'
     A                                  3124PAGNBR
     A                                      EDTCDE(Z)
     A                                  5 51'TOTAL COMMITMENT LINES REPORT'
     A*                                              Begin /S01498/
     A***********                           UNDERLINE
     A  39                              5 94'BRANCH'
     A  39        RRBRCA    R           5101REFFLD(BRCA)
     A  39                              5104'-'
     A  39        RRBRNM    R           5105REFFLD(BRNM)
     A                                  6 51'-----------------------------'
     A*                                              End   /S01498/
     A*
     A* Page Sub Headings
     A* -----------------
     A          R HEAD2
     A                                  7  6'BRANCH :'
     A            RRSPTA         3      7 15
     A                                  8  6'ACCOUNT OFFICER :'
     A            RRACOC         2      8 24
     A            RRACON        30      8 27
     A                                 10  4'PARENT '
     A                                 10 49'       COMMITMENT'
     A                                 10 69' START    '
     A                                 10 79' EXPIRY   '
     A                                 10 88'    REVIEW    '
     A                                 10102' DAY '
     A                                 10109'  LAST ACTIVITY   '
     A                                 11  4'  NO   '
     A                                 11 11' CUST NO '
     A                                 11 21'REPORT NAME         '
     A                                 11 44'CCY  '
     A                                 11 49'      LINE AMOUNT'
     A                                 11 69' DATE     '
     A                                 11 79' DATE     '
     A                                 11 89' DATE  '
     A                                 11 96' FREQ '
     A                                 11102' NO  '
     A                                 11108'ACTION'
     A                                 11116'DATE'
     A                                 11123'USER'
     A*
     A* PARENT LINE
     A* -----------
     A          R PARENT
     A*
     A            RRPNUM         6         4
     A                                      SPACEB(2)
     A            RRPRNM        20        21
     A            RRPACY         3        44
     A            RRAMP         18        49
     A*
     A* Customer Line
     A* -------------
     A          R CUSTR
     A  30                                10' '
     A                                      SPACEB(1)
     A            RRCNUM         6        12
     A                                      SPACEB(2)
     A            RRCRNM        20        21
     A            RRCACY         3        44
     A            RRAMC         18        49
     A            RRCATD         7        69
     A            RRCAEX         7        79
     A            RRCAVD         7        89
     A            RRCAVF         1        98
     A            RRCAVN         2  0    103EDTCDE(4)
     A            RRCACH         6       108
     A            RRCALC         7       115
     A            RRCAUS        10       123
     A  23        RRCAN1        35        34
     A                                      SPACEB(1)
     A  23        RRCAN2        35        75
     A  24        RRCAN3        35        34
     A                                      SPACEB(1)
     A  24        RRCAN4        35        75
     A*
     A* Branch sub heading
     A* ------------------
     A          R BRTOTS
     A                                 15  3'CURRENCY TOTALS FOR -
     A                                      BRANCH      '
     A            RRSPTA         3     15 31
     A*
     A* Branch detail line
     A* ------------------
     A          R BRDETL
     A            RRBCCY         3        44
     A            RRBRC         21        50
     A                                      SPACEA(2)
     A*
     A* Blank line
     A* ----------
     A          R BLKLIN
     A                                    44' '
     A                                      SPACEA(2)
     A*
     A* Last Branch sub heading
     A* -----------------------
     A          R BRFINL
     A                                     3'BRANCH TOTAL        '
     A*
     A*                                              Begin /S01498/
     A**Null*Report****************************************************
     A**-----------****************************************************
     A**********R NODETL
     A***********                         51'*** NO DETAILS TO REPORT ***'
     A***********                           SPACEB(4)
     A*
     A** Database Error
     A** --------------
     A          R DBERROR
     A                                    47'*** REFERENCE LB0351 +
     A                                      DATABASE ERROR ***'
     A                                      SPACEB(2)
     A*                                              End   /S01498/
     A*
     A* End of report
     A* -------------
     A          R ENDREP
     A*                                              Begin /S01498/
     A***********                         55'*** END OF REPORT ***'
     A***********                           SPACEB(4)
     A N39                                55'*** END OF REPORT ***'
     A N39                                  SPACEB(4)
     A  39                                53'*** END OF BRANCH REPORT ***'
     A  39                                  SPACEB(4)
     A*                                              End   /S01498/
