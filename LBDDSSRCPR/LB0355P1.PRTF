      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Borrowing Power weighting report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Lending Module                         *
     A*                                                               *
     A*  PRTF/LB0355P1 - Borrowing Power Weighting Report             *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01498             Date 11Aug94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  S01498 - Portfolio Lending Upgrade to Release 10             *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*
     A** Page Headings
     A** -------------
     A          R HEADER1                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*                                              Begin /S01498/
     A***********                       3  1'      REFERENCE LB0355P1      '
     A                                  3  6'REFERENCE LB0355P1'
     A*                                              End   /S01498/
     A            BJURPT        53A     3 41
     A                                  3 97'    DATE'
     A            BJMRDT         7A     3106
     A                                  3115'    PAGE'
     A            PAGE           4  0   3124EDTCDE(Z)
     A                                  5 51'BORROWING POWER WEIGHTING REPORT'
     A*
     A*                                              Begin /S01498/
     A***********                           UNDERLINE
     A                                  6 51'--------------------------------'
     A                                  7 89'CONSOLIDATED'
     A*                                              End   /S01498/
     A          R HEADER2
     A                                     1' '
     A                                      SPACEB(2)
     A*
     A  21                                 4'COUNTRY OF ISSUER'
     A  21                                  UNDERLINE
     A*
     A  22                                 4'INSTRUMENT TYPE'
     A  22                                  UNDERLINE
     A*
     A  23                                 4'SECURITY'
     A  23                                  UNDERLINE
     A*
     A  24N22N23                           4'CLIENT'
     A  24N22N23                            UNDERLINE
     A          R HEADER3
     A  24                                 1'   CUSTOMER NO.'
     A                                      SPACEB(2)
      * Begin /CSD027/
     A**24******  RRCNUM         6  0     18                                                  CSD027
     A  24        RRCNUM         6A       18
      * End /CSD027/
     A  24        RRCRNM        20        27
     A N24                                63' REVIEW   '
     A                                      SPACEB(1)
     A  24                                63' REVIEW   '
     A                                      SPACEB(3)
     A                                    76' REVIEW   '
     A                                    89' REVIEW   '
     A                                   106'  LAST ACTIVITY   '
     A                                    47'WEIGHTING   '
     A                                      SPACEB(1)
     A                                    63' DATE   '
     A                                    76' FREQ   '
     A                                    89' DAY NO   '
     A                                   100'  ACTION  '
     A                                   110'  TIME   '
     A                                   119'  DATE   '
     A                                     1' '
     A                                      SPACEA(1)
     A          R DETAIL
     A  21        RRCTYI         2         8
     A                                      SPACEB(1)
     A  21        RRCNAM        30        14
     A  22        RRINST         3        12
     A                                      SPACEB(1)
     A  22        RRISTN        20        23
     A  23        RRSESN        10         9
     A                                      SPACEB(1)
     A  23        RRSRPN        20        23
     A            RRWGHT         3  0     49EDTWRD(' 0 ')
     A            RRREVD         7        64
     A            RRREVF         1        78
     A            RRREVN         2  0     92
     A            RRACTN         6       102
     A            RRTME          6  0    110EDTWRD('  :  :  ')
     A            RRLCD          7       120
     A*
     A** Null Report
     A** -----------
     A          R NODETLS
     A                                    51'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(4)
     A*                                              Begin /S01498/
     A** Database Error
     A** --------------
     A          R DBERROR                   SPACEB(2)
     A                                    47'*** REFERENCE LB0355 +
     A                                      DATABASE ERROR ***'
     A*                                              End   /S01498/
     A*
     A** Trailer
     A** -------
     A          R ENDRPT
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(4)
