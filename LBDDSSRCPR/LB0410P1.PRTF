      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Guarantees & Pledges Maturing/Review Rpt')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Lending Module                         *
     A*                                                               *
     A*  PRTF/LB0410P1  Guarantees and Pledges Maturing/Review Report *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 080669             DATE 22DEC94               *
     A*  PREV AMEND NO. S01498             DATE 11AUG94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  080669 - Space missing between Last Activity Action & Date   *
     A*  S01498 - Portfolio Lending Upgrade to Release 10             *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*                                              Begin /S01498/
     A                                               REF(MFRFL)
     A*                                              End   /S01498/
     A*
     A* Page Headings
     A* -------------
     A          R HEADER1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                              Begin /S01498/
     A***********                       3  7'REFERENCE LB0410P1'
     A                                  3  6'REFERENCE LB0410P1'
     A*                                              End   /S01498/
     A            BJURPT        53A     3 41
     A                                  3101'DATE'
     A            BJMRDT         7A     3106
     A                                  3119'PAGE'
     A            PAGE           4  0   3124EDTCDE(Z)
     A                                  5 43'GUARANTEES AND PLEDGES +
     A                                       MATURING/REVIEW REPORT'
     A*                                              Begin /S01498/
     A***********                           UNDERLINE
     A                                  6 43'-----------------------+
     A                                       ----------------------'
     A  40                              6 89'BRANCH -'
     A  40        RRBRCA    R           6 98REFFLD(BRCA)
     A  40        RRBRNM    R           6102REFFLD(BRNM)
     A*                                              End   /S01498/
     A          R HEADER2
     A*                                              Begin /S01498/
     A***********                       7  6'BRANCH :'
     A*********** RRSPTA         3A     7 15
     A  40                              7  6'BRANCH :'
     A  40        RRSPTA         3A     7 15
     A*                                              End   /S01498/
     A                                  7 24'ACCOUNT OFFICER :'
     A            RRACOF         2A     7 42
     A            RRAREP        30A     7 46
     A                                  9  6'CUSTOMER NO.'
     A            RRPNP8         6A     9 19
     A            RRCREP        20A     9 27
     A          R HEADER3
     A                                 13  1'COL'
     A                                 13  6'SEQ'
     A                                 13 56'START'
     A                                 13 63'MATURITY'
     A                                 13 74'REVIEW'
     A                                 13 84'DAY'
     A                                 13 89'AGREEMENT'
     A                                 13100'LAST ACTIVITY'
     A                                 13115'SPECIFIC'
     A                                 14  1'NO.'
     A                                 14  6'NO.'
     A                                 14 11'TYPE'
     A                                 14 16'CODE'
     A                                 14 21'CCY'
     A                                 14 56'DATE'
     A                                 14 63'DATE'
     A                                 14 72'DATE'
     A                                 14 78'FREQ'
     A                                 14 84'NO'
     A                                 14 90'SIGNED'
     A                                 14100'ACTION'
     A                                 14107'DATE'
     A                                 14115'UTILISATION'
     A          R DETAIL
     A  22                                 6'RECEIVED FROM'
     A                                      SPACEB(2)
     A  22        RRRECV         6        26
     A  23                                 6'ISSUED TO'
     A                                      SPACEB(2)
     A  23        RRISS          6        26
     A            RRCRNM        20        34
     A            RRBPSN         3         1
     A                                      SPACEB(1)
     A            RRSEQN         3         6
     A            RRTYPE         1        13
     A            RRCODE         3        16
     A            RRCCY          3        21
     A  24                                26'COLLATERAL'
     A  24        RRCOLS        18        37
     A  25                                26'GUARANTEE '
     A  25        RRGUAR        18        37
     A  26                                26'AMOUNT/LMT'
     A  26        RRPLEG        18        37
     A  27                                26'SECURITY  '
     A  27        RRSECR        18        37
     A            RRSTTD         7        55
     A            RRMATD         7        63
     A            RRREVD         7        72
     A            RRREVF         1        81
     A            RRREVN         2  0     84EDTCDE(4)
     A            RRAGRS         1        89
     A            RRAGRD         7        91
     A            RRCHTP         6       100
     A*                                              Begin /080669/
     A*********** RRLCD          7       106
     A            RRLCD          7       107
     A*                                              End   /080669/
     A  28                               115'FOR LOAN'
     A  28        RRUTSL         6       124
     A  29                               115'FOR FACILITY'
     A  29        RRUTSF         5       127
     A  30                               115'FOR PORTFOLIO'
     A  30        RRUTSP         4       129
     A*
     A  24                                26'B.POWER   '
     A  24                                  SPACEB(1)
     A  24        RRBPOW        18        37
     A  25        RRNARG        35        26
     A  25                                  SPACEB(1)
     A  26                                26'PERCENTAGE'
     A  26                                  SPACEB(1)
     A  26        RRPERC         3  0     51EDTCDE(3)
     A  27                                26'P''FOLIO/SEC REF'
     A  27                                  SPACEB(1)
     A  27        RRPTFR         4        50
     A*
     A  24        RRNARC        35        26
     A  24                                  SPACEB(1)
     A  26                                26'PORTFOLIO PLEDGED'
     A  26                                  SPACEB(1)
     A  26        RRPORT         4        50
     A  27                                26'REFERENCE '
     A  27                                  SPACEB(1)
     A  27        RRSREF        10        44
     A  27N35     RRSFN1        30        55
     A  27N35     RRSFN2        30        84
     A  27 35                             55'*** NOT FOUND ***'
     A  27 35                             90'*** NOT FOUND ***'
     A*
     A  26        RRNARP        35        26
     A  26                                  SPACEB(1)
     A  27                                26'B.POWER   '
     A  27                                  SPACEB(1)
     A  27        RRSBPW        18        37
     A*
     A  27        RRNARS        35        26
     A  27                                  SPACEB(1)
     A*
     A*                                              Begin /S01498/
     A**Null*Report****************************************************
     A**-----------****************************************************
     A**********R NODETLS
     A***********                         51'*** NO DETAILS TO REPORT ***'
     A***********                           SPACEB(4)
     A*                                              End   /S01498/
     A*
     A* Trailer
     A* -------
     A          R ENDRPT
     A*                                              Begin /S01498/
     A***********                         55'*** END OF REPORT ***'
     A***********                           SPACEB(4)
     A N40                                51'*** END OF REPORT ***'
     A N40                                  SPACEB(3)
     A  40                                46'*** END OF REPORT FOR BRANCH ***'
     A  40                                  SPACEB(3)
     A*
     A* Database Error
     A* --------------
     A          R DBERROR                   SPACEB(3)
     A                                    32'*** REFERENCE LB0410'
     A                                    53'DATABASE ERROR ***'
     A*                                              End   /S01498/
