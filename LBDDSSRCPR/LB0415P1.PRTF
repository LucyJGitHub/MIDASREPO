      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LB Availability Below Zero Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Lending Module                         *
     A*                                                               *
     A*  PRTF/LB0415P1 - Availability Below Zero Report (Full Report) *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01498             DATE 11AUG94               *
     A*  PREV AMEND NO. B8233              DATE 13SEP91               *
     A*                 R0719              DATE 16JAN91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01498 - Portfolio Lending Upgrade to Release 10             *
     A*  B8233  - LINK BETWEEN PARENT AND CHILDREN NOT IN ALL CASES   *
     A*           CORRECT.                                            *
     A*  R0719  - BORROWING POWER INSTEAD OF LENDING POWER            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*                                              Begin /S01498/
     A                                               REF(MFRFL)
     A*                                              End   /S01498/
      *
      **---------------------------------------------------------------
      **   Header Format
      **---------------------------------------------------------------
      *
     A          R HEALIN                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A*                                              Begin /S01498/
     A***********                          5'Reference'
     A                                     6'REFERENCE'
     A***********                         15'LB0415P1'
     A                                    16'LB0415P1'
     A*                                              End   /S01498/
     A            RRURPT        53        38
     A*                                              Begin /S01498/
     A***********                        101'Date'
     A                                   101'DATE'
     A*                                              End   /S01498/
     A            RRMRDT         7       106
     A*                                              Begin /S01498/
     A***********                        119'Page'
     A                                   119'PAGE'
     A*                                              End   /S01498/
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    45'AVAILABILITY BELOW ZERO - FULL REP-
     A                                      ORT'
     A*                                              Begin /S01498/
     A                                      SPACEA(1)
     A***********                           UNDERLINE
     A                                    45'----------------------------------+
     A                                      ---'
     A  39                                95'BRANCH -'
     A  39        RRBRCD    R            104REFFLD(BRCA)
     A  39        RRBRNM    R            108REFFLD(BRNM)
     A***********                           SPACEA(2)
     A***********                          6'Branch :'
     A*********** RRBRCD         3        15
     A***********                         18'-'
     A*********** RRBRNM        30        19
     A***********                           SPACEA(1)
     A                                      SPACEA(2)
     A*                                              End   /S01498/
      *
      **---------------------------------------------------------------
      **   A/c Officer Format
      **---------------------------------------------------------------
      *
     A          R ACOLIN
     A                                      SPACEB(1)
     A                                     6'Account Officer :'
     A            RRACOC         2        26
     A            RRACON        30        29
     A                                      SPACEA(2)
     A                                     3'Parent'
     A                                    86'Commitment'
     A                                   103'Review'
     A                                      SPACEA(1)
     A                                     5'No'
     A                                    11'Cust No'
     A                                    19'Report Name'
     A*R0719/START
     A*****                               45'Lending Power'
     A*****                               69'Exposure'
     A                                    50'Exposure'
     A                                    62'Borrowing Power'
     A*R0719/END
     A                                    85'Line Amount'
     A                                   100'Date    Freq'
     A                                   119'Availability'
     A                                      SPACEA(2)
      *
      **---------------------------------------------------------------
      **   Detail Format
      **---------------------------------------------------------------
      *
     A          R DETLIN
      *
      *--   B8233   --*
     A  30 35                               SPACEB(1)
      *--   B8233   --*
     A    N30     RRPCNB         6         3
     A                                      SPACEB(1)
     A     30     RRCUST         6        11
     A            RRCRNM        20        19
     A*R0719/START
     A*****       RRLPWR        18        41
     A*****       RREXPO        18        60
     A            RREXPO        18        41
     A            RRBORR        18        60
     A*R0719/END
     A            RRCOMA        18        79
     A            RRDATE         7       100
     A            RRREVF         1       108
     A            RRREVN         2       110
     A            RRAVAI        18       113
     A                                      SPACEA(1)
      *
      **---------------------------------------------------------------
      **   End of Report Format
      **---------------------------------------------------------------
      *
     A          R ENDLIN
      *
     A*                                              Begin /S01498/
     A*****55****                         51'** No Details to Report **'
     A***********                           SPACEB(2)
     A***********                         54'** End of Report **'
     A***********                           SPACEB(2)
     A N39                                51'*** END OF REPORT ***'
     A N39                                  SPACEB(3)
     A  39                                46'*** END OF REPORT FOR BRANCH ***'
     A  39                                  SPACEB(3)
      *
      **---------------------------------------------------------------
      ** Database Error Format
      **---------------------------------------------------------------
      *
     A          R DBERROR                   SPACEB(3)
     A                                    32'*** REFERENCE LB0415'
     A                                    53'DATABASE ERROR ***'
     A*                                              End   /S01498/
