      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas LB credit audit list (run controls)')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Lending Module                         *
     A*                                                               *
     A*  PRTF/LB0520AU - Portfolio Lending Group Audit List           *
     A*                  run controls                                 *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01498             Date 11Jul94               *
      *                 S01355             Date 10Jul92               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01498 - Portfolio Lending Upgrade to Release 10.            *
     A*  S01355 - RELEASE 3 INCORPORATION                             *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A**                                                                 *
     A*                                              Begin /S01498/
     A                                      REF(MFRFL)
     A*                                              End   /S01498/
     A*
     A** Page Headings Format
     A*
     A          R FCHEADER                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*                                              Begin /S01498/
     A***********                       3  7'REFERENCE LB0520AU    '
     A                                  3  6'REFERENCE LB0520AU'
     A*                                              End   /S01498/
     A            BJURPT        53A     3 41
     A                                  3 97'    DATE'
     A            BJMRDT         7A     3106
     A                                  3115'    PAGE'
     A            PAGE1          4  0   3124EDTCDE(Z)
     A*>>>> S01355>>>>
     A***********                       5 43'LOMBARD CREDIT GROUP AUDIT LIS+
     A***********                           T - RUN CONTROLS'
     A                                  5 43'PORTFOLIO LENDING GROUP AUDIT +
     A                                      LIST - RUN CONTROLS'
     A*<<<< S01355<<<<
     A*                                              Begin /S01498/
     A***********                           UNDERLINE
     A                                  6 43'------------------------------+
     A                                      -------------------'
     A*                                              End   /S01498/
     A*
     A** File Controls Format
     A*
     A          R FCDETAIL
     A                                 17 53'---------- PROGRAM ----------'
     A                                 17 93'---------- FILE ----------'
     A                                 18  7'FILE - LF/LBGROUL2'
     A                                 20  7'NUMBER OF INSERTIONS TODAY'
     A            RRNOIN         5  0  20 65EDTWRD('  , 0 ')
     A            TRINS          5  0  20103EDTWRD('  , 0 ')
     A  55                             20112'*** DIFFERENCE ***'
     A                                 22  7'NUMBER OF AMENDMENTS TODAY'
     A            RRNOAM         5  0  22 65EDTWRD('  , 0 ')
     A            TRAMD          5  0  22103EDTWRD('  , 0 ')
     A  56                             22112'*** DIFFERENCE ***'
     A                                 24  7'NUMBER OF DELETIONS TODAY'
     A            RRNODL         5  0  24 65EDTWRD('  , 0 ')
     A            TRDEL          5  0  24103EDTWRD('  , 0 ')
     A  57                             24112'*** DIFFERENCE ***'
     A                                 26  7'TOTAL NUMBER OF LIVE RECORDS'
     A            TRLIVE         5  0  26103EDTWRD('  , 0 ')
     A*
     A** Database Error Format
     A*
     A          R FCDBERR
     A                                  9 25'**** DATABASE ERROR ****'
     A                                 11 11'**********************************-
     A                                      ********************'
     A                                 12 11'*                                 -
     A                                                         *'
     A                                 13 11'*    FILE NAME    '''
     A            DBFILE        10A    13 30
     A*                                              Begin /S01498/     *'
     A***********                      13 38'''                         *'
     A                                 13 40'''                       *'
     A*                                              End   /S01498/     *'
     A                                 14 11'*                                 -
     A                                                         *'
     A                                 15 11'*    KEY          '''
     A            DBKEY         29A    15 30
     A                                 15 59'''    *'
     A                                 16 11'*                                 -
     A                                                         *'
     A                                 17 11'*    PROGRAM NAME '''
     A            DBPGM         10A    17 30
     A                                 17 40'''                       *'
     A                                 18 11'*                                 -
     A                                                         *'
     A                                 19 11'*    AT           '''
     A            DBASE          3  0  19 30
     A                                 19 33'''                              *'
     A                                 20 11'*                                 -
     A                                                         *'
     A                                 21 11'**********************************-
     A                                      ********************'
     A*
     A** Trailer Format
     A*
      ***********                                    Begin /S01498/
     A**********R FCTRAILR
     A***********                           SPACEB(4)
     A***********                         55'*** END OF REPORT ***'
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      ***********                                    End   /S01498/
