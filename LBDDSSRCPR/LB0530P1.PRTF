      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Guarantees & pledges signed/unsigned Rpt')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portflio Lending Module.                         *
     A*                                                               *
     A*  PRTF/LB0530P1 - Guarantees & Pledges Signed/Unsigned Report. *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01498             DATE 11AUG94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01498 - Portfolio Lending Upgrade to Release 10.            *
     A*           Since two reports are created within 'P1' then      *
     A*           'No Details to Report' will still be used.          *
     A*                                                               *
     A********************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*                                              Begin /S01498/
     A                                      REF(MFRFL)
     A*                                              End   /S01498/
     A*
     A* Page Headings
     A* -------------
     A          R HEADER1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                              Begin /S01498/
     A***********                       3  7'REFERENCE LB0530P1'
     A                                  3  6'REFERENCE LB0530P1'
     A*                                              End   /S01498/
     A            BJURPT        53A     3 41
     A                                  3101'DATE'
     A            BJMRDT         7A     3106
     A                                  3119'PAGE'
     A            PAGE           4  0   3124EDTCDE(Z)
     A                                  5 46'GUARANTEES AND PLEDGES SIGNED / UN-
     A                                      SIGNED'
     A*                                              Begin /S01498/
     A***********                           UNDERLINE
     A                                  6 46'----------------------------------+
     A                                      ------'
     A  37                              6 89'BRANCH -'
     A  37        RRBRCA    R           6 98REFFLD(BRCA)
     A  37        RRBRNM    R           6102REFFLD(BRNM)
     A*                                              End   /S01498/
     A N50                              7  4'UNSIGNED AGREEMENTS'
     A*                                              Begin /S01498/
     A*N50*******                           UNDERLINE
     A N50                              8  4'-------------------'
     A*                                              End   /S01498/
     A  50                              7  3'SIGNED AGREEMENTS BETWEEN '
     A*                                              Begin /S01498/
     A**50*******                           UNDERLINE
     A  50                              8  3'--------------------------'
     A*                                              End   /S01498/
     A  50        RRFDAT         7      7 29
     A*                                              Begin /S01498/
     A**50*******                           UNDERLINE
     A  50                              8 29'-------'
     A*                                              End   /S01498/
     A  50                              7 36' AND '
     A*                                              Begin /S01498/
     A**50*******                           UNDERLINE
     A  50                              8 36'-----'
     A*                                              End   /S01498/
     A  50        RRTDAT         7      7 41
     A*                                              Begin /S01498/
     A**50*******                           UNDERLINE
     A  50                              8 41'-------'
     A***********                       9  6'BRANCH'
     A*********** RRBRCD         3      9 15
     A  37                              9  6'BRANCH'
     A  37        RRBRCD         3      9 15
     A*                                              End   /S01498/
     A                                  9 24'ACCOUNT OFFICER'
     A            RRACOC         2      9 42
     A            RRACON        30      9 46
     A                                 11  1'CUSTOMER'
     A                                 11 27'SEQ'
     A                                 11 75'START'
     A                                 11 83'MATURITY'
     A                                 11 92'REVIEW'
     A                                 11104'DAY'
     A N50                             11109'LAST CHANGE'
     A  50                             11109'AGREEMENT'
     A  50                             11119'LAST CHANGE'
     A                                 12  1'NUMBER'
     A                                 12 10'ISSUED BY'
     A                                 12 27'NO'
     A                                 12 31'TYPE'
     A                                 12 36'CODE'
     A                                 12 41'CCY'
     A                                 12 75'DATE'
     A                                 12 83'DATE'
     A                                 12 92'DATE'
     A                                 12 99'FREQ'
     A                                 12104'NO.'
     A N50                             12109'ACTION  DATE'
     A  50                             12109'DATE'
     A  50                             12119'ACTION  DATE'
     A*
     A          R DETAIL1
     A            RRCUST         6         1
     A                                      SPACEB(2)
     A            RRCRNM        20         8
     A*
     A          R DETAIL2
     A            RRISSB         6         9
     A                                      SPACEB(1)
     A            RRSEQN         3        27
     A            RRTYPE         1        33
     A            RRCODE         3        36
     A            RRCCY          3        41
     A  51                                45'COLLATERAL'
     A  52                                45'GUARANTEE'
     A  53                                45'PLEDGE'
     A  54                                45'SECURITY'
     A            RRVALU        18        56
     A            RRSTTD         7        75
     A            RRMATD         7        83
     A            RRREVD         7        92
     A            RRREVF         1       101
     A            RRREVN         2  0    104EDTCDE(4)
     A N50        RRCHTP         6       109
     A N50        RRLCD          7       116
     A  50        RRAGRD         7       109
     A  50        RRSCHT         6       119
     A  50        RRSLCD         7       126
     A
     A            RRCRNM        20        12
     A                                      SPACEB(1)
     A            RRNARR        35        45
     A*
     A**Null Report
     A**-----------
     A          R NULL1
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(4)
     A***********                                    Begin /S01498/
     A*
     A* Database Error
     A* --------------
     A          R DBERROR
     A                                      SPACEB(3)
     A                                    32'*** REFERENCE LB0530'
     A                                    53'DATABASE ERROR ***'
     A***********                                    End   /S01498/
     A*
     A* Footing
     A* -------
     A          R FOOTING1
     A***********                                    Begin /S01498/
     A***********                         55'*** END OF REPORT ***'
     A N37                                51'*** END OF REPORT ***'
     A                                      SPACEB(3)
     A  37                                46'*** END OF REPORT FOR BRANCH ***'
     A                                      SPACEB(3)
     A***********                                    End   /S01498/
