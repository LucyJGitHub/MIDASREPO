     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LB Select Portfolio Lending Groups')
     F*****************************************************************
     F*                                                               *
     F*  Midas - PORTFOLIO LENDING                                *
     F*                                                               *
     F*  LB0226 - PORTFOLIO LENDING GROUP                             *
     F*                                                               *
     F*  Function: Select record for Portfolio Lending Groups.        *
     F*            Displays the records from DBF as a subfile.        *
     F*            Loads the SFL from the DBF a page at a time.       *
     F*            The DBF record at which the SFL display starts     *
     F*            may be controlled by positioning fields            *
     F*            on the SFL header. Any key fields on the SFL       *
     F*            header will be used as positioning values.         *
     F*            Any non-key fields on the SFL header will be used  *
     F*            as selection values: a maximum of ##RCMX records   *
     F*            records will be read from the DBF before giving up.*
     F*            A record may be selected from those                *
     F*            displayed in the subfile. The master key data      *
     F*            will be returned to the calling program            *
     F*                                                               *
     F*  Called by: AOGROUR0                                          *
     F*                                                               *
     F*  Calls:     NONE                                              *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. PM0027             Date 17AUG94               *
     F*                 LB0011             Date 04JUL94               *
     F*                                    DATE 19MAY94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  PM0027 - Should ignore deleted records                       *
     F*  LB0011 - Rename to LB0226 from LB0225S                       *
     F*  S01486 - Create Selector program for Access Object for       *
     F*           Private Banking                                     *
     F*                                                               *
     F*****************************************************************
     F*           C R E A T I O N     P A R A M E T E R S             *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     F***LB0225S#CF**E                    WORKSTN                         LB0011
     F***********                             ##RR  KSFILE LB0225S0       LB0011
     FLB0226DFCF  E                    WORKSTN                            LB0011
     F                                        ##RR  KSFILE LB0226S0       LB0011
     F                                              KINFDS INFDS#
     F*
     F* RTV: Port Lend Groups          Retrieval index
     FLBGROUL0IF  E           K        DISK
     F                                              KINFDS INFDS1
     F*
     E                    CPY@    1   1 80
     E* Description     : Copyright notice for inclusion in all programs
     E*
     E/EJECT
     I* Data structures:-
     IPGMDS     ESDSY2PGDSP
     I* Program data structure.
     IJBDTTM      DS
     I* Job date/time.
     I                                        1   60##JDT
     I                                        1   20##JYY
     I                                        3   40##JMM
     I                                        5   60##JDD
     I                                        7  120##JTM
     I                                        7   80##JHH
     I                                        9  100##JNN
     I                                       11  120##JSS
     IINFDS#    E DSY2I#DSP
     I* Display file information data structure.
     I*
     IINFDS1    E DSY2I1DSP
     I* File information data structure.
     I*
     IRUNDAT      DS
     I                                        1   7 MRDT
     I                                    P   8  100RDNB
     I                                       11  11 SUC
     I                                       12  12 DFF
     I                                       13  13 MBIN
     IA@CPY       DS
     I* Copyright array
     I                                        1  80 CPY@
      /EJECT
      * Parameter declarations.
     IP1PARM      DS
      * FLD: Port Lend Grp
     I                                        1   3 P1LCGR
     C/EJECT
     C*****************************************************************
     C           *ENTRY    PLIST                           Entry parameter
     C                     PARM           P0RTN   7        Return code
     C           P1LCGR    PARM P1LCGR    WP0001  3        Port Lend Grp
     C*****************************************************************
     C* Initialise
     C                     EXSR ZZINIT
     C*
     C                     DO   *HIVAL                     DO
     C* Initialise & load subfile page.
     C                     EXSR BAIZSF
     C                     MOVEL'N'       W0RSF   1
     C* Display screen until reload requested:
     C           W0RSF     DOWEQ'N'                        DOW
     C* Display screen.
     C                     EXSR CAEXFM
     C*   Process response:
     C* Cancel & exit program.
     C   03                CAS            ZXEXPG
     C* HOME: Request subfile reload.
     C   05                CAS            FBRQRL
     C*   Display next SFL page.
     C   27                CAS            BBLDSF
     C*   Process screen input.
     C                     CAS            DAPR##
     C                     END
     C*
     C                     END                             OD W0RSF
     C                     END                             OD *HIVAL
     C*****************************************************************
     C/EJECT
     CSR         BAIZSF    BEGSR
     C*================================================================
     C* Initialise & Load subfile page.
     C*================================================================
     C* Clear subfile.
     C                     SETON                     80
     C***********          WRITELB0225C0                                  LB0011
     C                     WRITELB0226C0                                  LB0011
     C                     SETOF                     80
     C* Reset no of records in subfile.
     C                     Z-ADD*ZERO     ##RRMX  50 81     SETOF 81
     C*................................................................
     C* Position DBF file:
     C           KPOS      KLIST
     C                     KFLD           GPLCGR           Port Lend Grp
     C* Setup key.
     C                     MOVEL#2LCGR    GPLCGR           Port Lend Grp
     C           KPOS      SETLLLBGROUD0
     C           GPRECI    DOUNE'*'                                       PM0027
     C           *IN82     OREQ '1'                                       PM0027
     C                     READ LBGROUD0               8782*82=EOF
     C                     END                                            PM0027
     C* Save previous selector values.
     C           *LIKE     DEFN #2LCGR    WZLCGR
     C                     MOVEL#2LCGR    WZLCGR           Port Lend Grp
     C           *LIKE     DEFN #2LCGN    WZLCGN
     C                     MOVEL#2LCGN    WZLCGN           Group Name
     C           *LIKE     DEFN #2LCGS    WZLCGS
     C                     MOVEL#2LCGS    WZLCGS           Port Lend Seq No
     C           *LIKE     DEFN #2ATTI    WZATTI
     C                     MOVEL#2ATTI    WZATTI           Attached Limit
     C*
     C           *LIKE     DEFN #2LCD     WZLCD
     C                     MOVEL#2LCD     WZLCD            Last Chg Date
     C           *LIKE     DEFN #2CHTP    WZCHTP
     C                     MOVEL#2CHTP    WZCHTP           Last Chg Type
     C*
     C* Translate search mask for text field.
     C*
     C                     MOVEL'QSYST'   WQB10X 10
     C                     MOVE 'RNTBL'   WQB10X
     C           *LIKE     DEFN #2LCGN    WQLCGN           Group Name
     C                     CALL 'QDCXLATE'
     C                     PARM 30        WQA5N   50       Len
     C                     PARM #2LCGN    WQLCGN           Group Name
     C                     PARM           WQB10X           QSYSTRNTBL
     C                     PARM 'QSYS'    WQC10X 10
     C*
     C* Load SFL page.
     C*
     C                     Z-ADD*ZERO     ##RROK  50
     C                     EXSR BBLDSF
     C*
     C*================================================================
     CSR         BAEXIT    ENDSR
     C/EJECT
     CSR         BBLDSF    BEGSR
     C*================================================================
     C* Load subfile page.
     C*================================================================
     C* Re-establish fields in read-ahead record.
     C   27                DO
     C           GPRECI    DOUNE'*'                                       PM0027
     C           *IN90     OREQ '1'                                       PM0027
     C  N82                READPLBGROUD0                 90*
     C                     END                                            PM0027
     C  N82                READ LBGROUD0                 90*
     C                     END
     C*
     C* Setof record error indicators.
     C                     MOVE *ALL'0'   WKINDS  1
     C                     MOVEAWKINDS    *IN,33
     C* Start at previous highest record in SFL.
     C                     Z-ADD##RRMX    ##RR    50
     C* Reset count of DBF records read.
     C                     Z-ADD*ZERO     ##RRRD  50
     C*................................................................
     C* Load next SFL page until SFL page full,
     C* or scan limit reached,
     C* or end of file reached.
     C           *IN82     DOWEQ'0'                        DO
     C           ##RROK    ANDLT##SFPG
     C           ##RRRD    ANDLTW0SLM
     C*
     C* Check selection fields - if fail, read next record.
     C*
     C           #2LCD     IFNE *ZERO                      Last Change Dat
     C           GPLCD     CABNE#2LCD     CD020            Last Change Dat
     C                     END
     C*
     C           #2CHTP    IFNE *BLANK                     Type of Last Ch
     C           GPCHTP    CABNE#2CHTP    CD020            Type of Last Ch
     C                     END
     C*
     C           #2LCGR    IFNE *BLANKS                    Port Lend Grp
     C           GPLCGR    CABNE#2LCGR    CD020            Port Lend Grp
     C                     END
     C*
     C           #2LCGN    IFNE *BLANKS                    Group Name
     C           GPLCGN    CABNE#2LCGN    CD020            Group Name
     C                     END
     C*
     C           #2LCGS    IFNE *ZEROS                     Port Lend Seq No
     C           GPLCGS    CABNE#2LCGS    CD020            Port Lend Seq No
     C                     END
     C*
     C           #2ATTI    IFNE *BLANKS                    Attached Limit
     C           GPATTI    CABNE#2ATTI    CD020            Attached Limit
     C                     END
     C*
     C* Scan for search string.
     C*
     C           #2LCGN    IFNE *BLANK                     Group Name
     C                     CALL 'QCLSCAN'
     C                     PARM           GPLCGN           Group Name
     C                     PARM 30        WQA3N   30       Length
     C                     PARM 1         WQB3N   30       Start
     C                     PARM           WQLCGN           Mask
     C                     PARM 30        WQC3N   30       Length
     C                     PARM '1'       WQD1    1        Translate
     C                     PARM '1'       WQE1    1        Trim
     C                     PARM '?'       WQF1    1        Wild
     C                     PARM           WQG3N   30       Result
     C           WQG3N     CABLT1         CD020
     C                     END
     C*
     C* Load SFL fields.
     C*
     C                     EXSR MBFL#1
     C* Output to subfile.
     C                     ADD  1         ##RR       81    *
     C                     ADD  1         ##RROK
     C***********          WRITELB0225S0                                  LB0011
     C                     WRITELB0226S0                                  LB0011
     C*
     C           CD020     TAG
     C* Increment scan check count.
     C                     ADD  1         ##RRRD
     C* Read next record.
     C           GPRECI    DOUNE'*'                                       PM0027
     C           *IN82     OREQ '1'                                       PM0027
     C                     READ LBGROUD0               8782*82=EOF
     C                     END                                            PM0027
     C                     END                             WOD
     C*................................................................
     C* If no DBF records found, display error message.
     C           ##RR      IFEQ *ZERO
     C           *IN82     ANDEQ'1'
     C* Send message '*No data to display'
     C                     MOVEL'Y2U0008' ZAMSID           Message id.
     C                     MOVEL'Y2USRMSG'ZAMSGF           Message file.
     C                     EXSR ZASNMS                     Send message
     C                     END
     C*
     C* Save highest SFL record load can continue at end point.
     C           ##RR      IFGT ##RRMX
     C           ##RRMX    ADD  1         ##SFRC
     C                     Z-ADD##RR      ##RRMX
     C                     END
     C*
     C* If scan limit reached, display error message.
     C           ##RRRD    IFGE W0SLM
     C* Send message '*Scan limit reached'
     C                     MOVEL'Y2U0017' ZAMSID           Message id.
     C                     MOVEL'Y2USRMSG'ZAMSGF           Message file.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE
     C                     Z-ADD*ZERO     ##RROK  50
     C                     END
     C*================================================================
     CSR         BBEXIT    ENDSR
     C/EJECT
     CSR         CAEXFM    BEGSR
     C*================================================================
     C* Display screen.
     C*================================================================
     C* Update screen time.
     C                     TIME           ##TME            Screen time.
     C           W0HLP     DOUEQ'N'
     C* PUTOVR unless conditioned fields change.
     C                     SETON                     86
     C           *IN81     IFNE CAIN81
     C                     SETOF                     86
     C                     END
     C                     MOVE *IN81     CAIN81  1
     C***********          WRITELB0225C1                                  LB0011
     C***********          WRITELB0225F0                                  LB0011
     C***********          EXFMTLB0225C0                                  LB0011
     C                     WRITELB0226C1                                  LB0011
     C                     WRITELB0226F0                                  LB0011
     C                     EXFMTLB0226C0                                  LB0011
     C                     MOVEL'N'       W0HLP   1        Reset help requ
     C* If help requested, display help text.
     C   25                EXSR ZHHPKY                     Display help te
     C                     END
     C* Update job time.
     C                     TIME           ##JTM            Job time.
     C* Clear messages from program message queue.
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C* Reset first message only flag.
     C                     MOVEL'Y'       ZAFSMS  1      99*
     C                     SETOF                       8883*
     C*================================================================
     CSR         CAEXIT    ENDSR
     C/EJECT
     CSR         DAPR##    BEGSR
     C*================================================================
     C* Process screen input.
     C*================================================================
     C* Maintain subfile position where possible.
     C           @#SFRC    IFGT *ZERO
     C                     Z-ADD@#SFRC    ##SFRC
     C                     END
     C*
     C           *IN81     IFEQ '1'
     C* Process subfile records.
     C                     EXSR DBPRSF
     C* If error, exit subroutine:
     C           *IN99     CABEQ'1'       DAEXIT
     C                     END
     C* Change of position specified.
     C           WZLCD     CASNE#2LCD     FBRQRL           Last Chg Date
     C           WZCHTP    CASNE#2CHTP    FBRQRL           Type Last Chg
     C*
     C           WZLCGR    CASNE#2LCGR    FBRQRL           Port Lend Grp
     C           WZLCGN    CASNE#2LCGN    FBRQRL           Group Name
     C           WZLCGS    CASNE#2LCGS    FBRQRL           Port Lend Seq No
     C           WZATTI    CASNE#2ATTI    FBRQRL           Attached Limit
     C                     END
     C*================================================================
     CSR         DAEXIT    ENDSR
     C/EJECT
     CSR         DBPRSF    BEGSR
     C*================================================================
     C* Process modified subfile records.
     C*================================================================
     C* Read first changed record (if any).
     C***********          READCLB0225S0                 88*              LB0011
     C                     READCLB0226S0                 88*              LB0011
     C           *IN88     DOWEQ'0'
     C                     EXSR DCSFRC
     C                     MOVEL*BLANK    #1SEL
     C***********          UPDATLB0225S0                                  LB0011
     C                     UPDATLB0226S0                                  LB0011
     C* Read next record.
     C***********          READCLB0225S0                 88*              LB0011
     C                     READCLB0226S0                 88*              LB0011
     C                     END
     C*================================================================
     CSR         DBEXIT    ENDSR
     C/EJECT
     CSR         DCSFRC    BEGSR
     C*================================================================
     C* Process subfile record.
     C*================================================================
     C* Setof error indicators and SFLNXTCHG.
     C                     MOVEAWKINDS    *IN,33
     C                     SETOF                     84    *
     C* Check selection option
     C           #1SEL     CASEQ'1'       DESLRC
     C                     END
     C*================================================================
     CSR         DCEXIT    ENDSR
     C/EJECT
     CSR         DESLRC    BEGSR
     C*================================================================
     C* Select record & return to calling program
     C*================================================================
     C* Move record values to parameters.
     C                     MOVEL#1LCGR    P1LCGR           Port Lend Grp
     C                     MOVEL*BLANK    P0RTN
     C                     EXSR ZYEXPG
     C*================================================================
     CSR         DEEXIT    ENDSR
     C/EJECT
     CSR         FBRQRL    BEGSR
     C*================================================================
     C* Request subfile reload.
     C*================================================================
     C                     MOVEL'Y'       W0RSF
     C*================================================================
     CSR         FBEXIT    ENDSR
     C/EJECT
     CSR         MBFL#1    BEGSR
     C*================================================================
     C* Move LBGROUD0 fields to #SFLRCD
     C*================================================================
     C                     MOVEL*BLANK    #1SEL            *SFLSEL
     C                     MOVELGPLCD     #1LCD            Last Chg Date
     C                     MOVELGPCHTP    #1CHTP           Last Chg Type
     C                     MOVELGPLCGR    #1LCGR           Port Lend Grp
     C                     MOVELGPLCGN    #1LCGN           Group Name
     C                     MOVELGPLCGS    #1LCGS           Port Lend Seq No
     C                     MOVELGPATTI    #1ATTI           Attached Limit
     C*================================================================
     CSR         MBEXIT    ENDSR
     C/EJECT
     CSR         MEIZ#2    BEGSR
     C*================================================================
     C* Initialise subfile control.
     C*================================================================
     C                     MOVELP1LCGR    #2LCGR           Port Lend Grp
     C                     MOVEL*BLANKS   #2LCGN           Group Name
     C                     MOVEL*BLANKS   #2LCGS           Port Lend Seq No
     C                     MOVEL*BLANKS   #2ATTI           Attached Limit
     C*
     C                     MOVEL*BLANKS   #2LCD            Last Chg Date
     C                     MOVEL*BLANKS   #2CHTP           Last Chg Type
     C*
     C* Drop ? from positioning key fields.
     C*                                                                e
     C                     MOVEL#2LCGR    W0X1    1
     C           W0X1      IFEQ '?'
     C           *LIKE     DEFN #2LCGR    W0LCGR- 1
     C                     MOVE #2LCGR    W0LCGR
     C                     MOVELW0LCGR    #2LCGR           Port Lend Grp
     C                     MOVE ' '       #2LCGR
     C                     END
     C*
     C*================================================================
     CSR         MEEXIT    ENDSR
     C/EJECT
     CSR         ZASNMS    BEGSR
     C*================================================================
     C* Send message to program's message queue.
     C*================================================================
     C           ZAPGM     IFEQ *BLANK
     C                     MOVEL##PGM     ZAPGM
     C                     END
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
     C* Clear all fields for default mechanism next time.
     C                     MOVEL*BLANK    ZAPGM            Program queue
     C                     MOVEL*BLANK    ZAPGRL           Rel queue
     C                     MOVEL*BLANK    ZAMSID           Message Id.
     C                     MOVEL*BLANK    ZAMSGF           Message file.
     C                     MOVEL*BLANK    ZAMSDA           Message data.
     C                     MOVEL*BLANK    ZAMSTP           Message type.
     C*================================================================
     CSR         ZAEXIT    ENDSR
     C/EJECT
     CSR         ZHHPKY    BEGSR
     C*================================================================
     C* Display HELP text.
     C*================================================================
     C                     MOVEL'Y'       W0HLP   1        Signal help req
     C                     CALL 'YDDSHPR'
     C                     PARM ##PGM     W0HPMB 10        Member name.
     C                     PARM *BLANK    YYHPFL 10        *DFT text file.
     C                     PARM *BLANK    YYHPLB 10        Help text lib.
     C                     PARM           W0RTN   7        Return Code.
     C*================================================================
     CSR         ZHEXIT    ENDSR
     C/EJECT
     CSR         ZXEXPG    BEGSR
     C*================================================================
     C* Cancel & exit program.
     C*================================================================
     C* Send message '*No value selected'
     C                     MOVEL'Y2U0016' ZAMSID           Message id.
     C                     MOVEL'Y2USRMSG'ZAMSGF           Message file.
     C                     MOVEL'*PRV '   ZAPGRL           Pgmq rel.
     C                     EXSR ZASNMS                     Send message
     C                     MOVEL'Y2U0016' P0RTN
     C                     EXSR ZYEXPG
     C*================================================================
     CSR         ZXEXIT    ENDSR
     C/EJECT
     CSR         ZYEXPG    BEGSR
     C*================================================================
     C* Exit program: Direct.
     C*================================================================
     C* Terminate program.
     C                     SETON                     LR
     C*
     C* Exit program.
     C                     RETRN
     C*
     C*================================================================
     CSR         ZYEXIT    ENDSR
     C/EJECT
     CSR         ZZINIT    BEGSR
     C*================================================================
     C* Initialisation.
     C*================================================================
     C                     MOVE *BLANK    P0RTN
     C                     MOVE *BLANK    W0RTN   7
     C* Setup job date/time.
     C                     Z-ADDUDATE     ##JDT            Job date.
     C                     TIME           ##JTM            Job time.
     C                     TIME           ##TME   60       Screen time.
     C* Define work field Midas Run Date
     C                     MOVEL*BLANK    WUMRDT  7
     C* Define work field Run day number
     C                     Z-ADD*ZERO     WURDNB  50
     C* Define work field Set Up Complete
     C                     MOVEL*BLANK    WUSUC   1
     C* Define work field Date format flag
     C                     MOVEL*BLANK    WUDFF   1
     C* Define work field Multi-branching Indicator
     C                     MOVEL*BLANK    WUMBIN  1
     C                     MOVEL'SEL'     W0PMD   3        Program Mode
     C*
     C                     Z-ADD14        ##SFPG  30       SFLPAG
     C                     Z-ADD1         ##SFRC
     C                     Z-ADD*ZERO     ##RRMX           MAX RECNO
     C                     Z-ADD500       W0SLM   50       SCAN LIMIT
     C* USER: Initialise program
     C* Standard RPG Creation PAR - Standard Functions  *
     C* Get Rundate - Rundate  *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C                     MOVE MRDT      ##MRDT           RUN DATE
     C                     MOVE MRDT      WUMRDT
     C                     MOVE RDNB      WURDNB           RUNDAY NO.
     C                     MOVE SUC       WUSUC            SET UP COMPLT
     C                     MOVE DFF       WUDFF            DTE FMT FLAG
     C                     MOVE MBIN      WUMBIN           MULTI BR. IND
     C* Copyright Statement - Standard Functions  * copyright
     C* Clear subfile control fields.
     C                     EXSR MEIZ#2
     C*================================================================
     CSR         ZZEXIT    ENDSR
** CPY@ - OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
