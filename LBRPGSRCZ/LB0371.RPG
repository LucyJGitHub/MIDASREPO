     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LB Commitment Limit exceeded amt/select.')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Lending Module                         *
     F*                                                               *
     F*  LB0371 - Total Commitment Lines Exceeded by Amount           *
     F*             Selection                                         *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01498             Date 11Aug94               *
      *                 R00300             Date 20Aug90               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
     F*  S01498 - Portfolio Lending Upgrade to Release 10             *
     F*  R00300 - Message file changes                                *
     F*                                                               *
     F*****************************************************************
     F**                                                              *
     F** C R E A T I O N     P A R A M E T E R S                      *
     F**                                                              *
     F**                                                              *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F**
     F**      FILE DEFINITIONS
     F**      ----------------
     F*
     F**   SCREEN FORMAT
     FLB0371DFCF  E                    WORKSTN      KINFSR *PSSR
     F*
     F*****BANK*DETAILS*FILE*-*PREFIX*BJ*******************************   S01498
     F***SDBANKPDIF**E                    DISK         KINFSR *PSSR       S01498
     F*
     F*****PORTFOLIO*LENDING*ICD*FILE-*PREFIX*LB***********************   S01498
     F***SDLOMBPDIF**E                    DISK         KINFSR *PSSR       S01498
     F*
     F*****CURRENCY*CODES*FILE-*PREFIX*A6******************************   S01498
     F***SDCURRL1IF**E           K        DISK         KINFSR *PSSR       S01498
     F*
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F**                                                              *
     F** ID F  C  H  L    FUNCTION OF INDICATORS                      *
     F**                  ----------------------                      *
     F*********20*********ACCESS*TO*BANK*DETAILS*FILE*-*SDBANKPD*******   S01498
     F*********21*********ACCESS*TO*LB*CREDIT*ICD*FILE*-*SDLOMBPD******   S01498
     F*********22*********ACCESS*TO*CCY*CODES*FILE*-*SDCURRL1**********   S01498
     F**       40         SCREEN VALIDATION FAILED                    *
     F**       41         VALID VALUE NOT YET ENTERED                 *
     F**       50         READ INDICATOR ON LB0371DF                  *
     F**       51         SFLEND                                      *
     F**                                                              *
     F*****************************************************************
     E/EJECT
     E********************************************************************
     E*
     E** ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
     E*****************************************************************
     E/EJECT
     E*****************************************************************
     E/COPY ZSRSRC,ZALIGNZ1
     E*****************************************************************
     I/EJECT
     I*****************************************************************
     I*
     I** DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
     I*                                                                   S01498
     I**  RCF parameter data structure                                    S01498
     I@RPARM      DS                            100                       S01498
     I                                        1  130WWP2                  S01498
     I*
     I**   PROGRAM STATUS DATA STRUCTURE
     IPSDS       SDS
     I***********                           244 253 DDWSID                S01498
     I***********                           254 263 USER                  S01498
     I                                      244 253 SWSID                 S01498
     I                                      254 263 SUSER                 S01498
     I*
     I**   LOCAL DATA AREA
     I***WWLDA******UDS                            256                    S01498
     ILDA         DS                            256                       S01498
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*                                                                   S01498
     ISDBANK    E DSSDBANKPD                                              S01498
     I**  Data structure for bank details                                 S01498
     I*                                                                   S01498
     ISDLMB     E DSSDLOMBPD                                              S01498
     I**  Data structure for Portfolio Lending ICD                        S01498
     I*                                                                   S01498
     ISDCURR    E DSSDCURRPD                                              S01498
     I**  Data structure for currency details                             S01498
     I*                                                                   S01498
     IDSFDY     E DSDSFDY                                                 S01498
     I**  Short data structure for access programs                        S01498
     I*                                                                   S01498
     IDSSDY     E DSDSSDY                                                 S01498
     I**  Long data structure for access programs                         S01498
     I*
     I*****************************************************************
     C/EJECT
     C*****************************************************************   S01498
     C*                                                                   S01498
     C**  Parameter list declarations                                     S01498
     C*                                                                   S01498
     C           *ENTRY    PLIST                                          S01498
     C                     PARM           @SEQ    5                       S01498
     C                     PARM           @LVL    1                       S01498
     C                     PARM           @RENT   1                       S01498
     C                     PARM           @RPARM100                       S01498
     C                     PARM           RACT    1                       S01498
     C                     PARM           RCMD    1                       S01498
     C*                                                                   S01498
     C*****************************************************************
     C*
     C**            START MAINLINE
     C**           ----------------
     C*
     C*
     C**   INITIAL PROCESSING
     C*
     C                     EXSR #A
     C*
     C**   MAIN PROCESSING
     C*
     C                     EXSR #B
     C*
     C**   END PROGRAM
     C*
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**        INDEX TO SUBROUTINES                                  *
     C**       ----------------------                                 *
     C**                                                              *
     C**   ZALIGN  - VALID & RIGHT ALIGN NUMERIC FIELDS               *
     C**                                                              *
     C**   #A      - INITIAL PROCESSING                               *
     C**   #B      - MAIN PROCESSING                                  *
     C**   #BA     - SCREEN VALIDATION                                *
     C**                                                              *
     C**   *PSSR   - PROGRAM ERROR HANDLING ROUTINE                   *
     C**                                                              *
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C/COPY ZSRSRC,ZALIGNZ2
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #A        - INITIAL PROCESSING                             *
     C**                                                              *
     C**   CALLED BY - MAINLINE                                       *
     C**                                                              *
     C**   CALLS     - NONE                                           *
     C**                                                              *
     C*****************************************************************
     C*
     C           #A        BEGSR
     C*                                                                   S01498
     C**  Copyright statement                                             S01498
     C*                                                                   S01498
     C                     MOVEACPY@      ACT@   80                       S01498
     C*
     C************NAMVAR   DEFN LDA       WWLDA                           S01498
     C           *NAMVAR   DEFN           LDA                             S01498
     C           *LOCK     IN   LDA                                       S01498
     C*
     C                     MOVE '1'       *IN51
     C                     MOVEL'LB0371'  DBPGM
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD*ZEROS    DBASE
     C                     OUT  LDA                                       S01498
     C*
     C**   Fill in fields for subroutine ZASNMS                           R00300
     C                     MOVEL*BLANK    ZAPGM  10        Message queue  R00300
     C                     MOVE *BLANKS   ZAMSGF
     C*********************MOVEL'LXGBMSGF'ZAMSGF
     C                     MOVEL'LBUSRMSG'ZAMSGF 10        Message file   R00300
     C                     MOVEL*BLANK    ZAPGRL  5        Rel queue      R00300
     C                     MOVEL*BLANK    ZAMSID  7        Message Id.    R00300
     C                     MOVEL*BLANK    ZAMSDA132        Message data.  R00300
     C                     MOVEL*BLANK    ZAMSTP  7        Message type.  R00300
     C*
     C*****ACCESS*SDBANKPD*TP*GET*RUN*DATE*****************************   S01498
     C*
     C***********1         SETLLSDBANKPD                                  S01498
     C***********          READ SDBANKPD                 20               S01498
     C*
B1   C************IN20     IFEQ '1'                                       S01498
     C*                                                                   S01498
     C**  Use access object to get run date                               S01498
     C*                                                                   S01498
     C                     CALL 'AOBANKR0'                                S01498
     C                     PARM '*DBERR'  P@RTCD  7                       S01498
     C                     PARM '*FIRST'  P@OPTN  7                       S01498
     C           SDBANK    PARM SDBANK    DSFDY                           S01498
     C*                                                                   S01498
     C**  Check if error occurred                                         S01498
     C*                                                                   S01498
     C           P@RTCD    IFNE *BLANK                                    S01498
     C           *LOCK     IN   LDA                                       S01498
     C                     Z-ADD1         DBASE            ****************
     C                     MOVE *BLANKS   DBFILE           *              *
     C***********          MOVEL'SDBANKPD'DBFILE           * DBERROR  001 S01498
     C                     MOVE 'SDBANKPD'DBFILE           * DBERROR 001 *S01498
     C                     MOVE *BLANKS   DBKEY            *              *
     C                     OUT  LDA                        ***************S01498
     C                     MOVE '1'       *INU7            ****************
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     EXSR *PSSR                                     S01498
     C                     RETRN
E1   C                     END
     C*                                                                   S01498
     C**  Rundate in DDMMMYY format                                       S01498
     C*                                                                   S01498
     C                     MOVE BJMRDT    SRUNA                           S01498
     C*
     C*****ACCESS*PORTFOLIO*ICD*TO*GET*PORTFOLIO*LENDING*CURRENCY******   S01498
     C*
     C***********1         SETLLSDLOMBPD                                  S01498
     C***********          READ SDLOMBPD                 21               S01498
     C*
B1   C************IN21     IFEQ '1'                                       S01498
     C*                                                                   S01498
     C**  Get portfolio lending currency using access object              S01498
     C*                                                                   S01498
     C                     CALL 'AOLOMBR0'                                S01498
     C                     PARM *BLANKS   P@RTCD                          S01498
     C                     PARM '*FIRST ' P@OPTN                          S01498
     C           SDLMB     PARM SDLMB     DSFDY                           S01498
     C*                                                                   S01498
     C**  Check if error occurred                                         S01498
     C*                                                                   S01498
     C           P@RTCD    IFNE *BLANKS                                   S01498
     C           *LOCK     IN   LDA                                       S01498
     C                     Z-ADD2         DBASE            ****************
     C                     MOVE *BLANKS   DBFILE           *              *
     C***********          MOVEL'SDLOMBPD'DBFILE           * DBERROR  002 S01498
     C                     MOVE 'SDLOMBPD'DBFILE           * DBERROR 002 *S01498
     C                     MOVE *BLANKS   DBKEY            *              *
     C                     OUT  LDA                        ***************S01498
     C                     MOVE '1'       *INU7            ****************
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     EXSR *PSSR                                     S01498
     C                     RETRN
E1   C                     END
     C*
     C**   ACCESS CCY CODES FILE TO GET NO. OF DEC. PLACES
     C**   FOR PORTFOLIO LENDING CCY
     C*
     C***********LBCCY     CHAINSDCURRL1             22                   S01498
     C*
B1   C************IN22     IFEQ '1'                                       S01498
     C                     CALL 'AOCURRR0'                                S01498
     C                     PARM *BLANKS   P@RTCD                          S01498
     C                     PARM '*KEY   ' P@OPTN                          S01498
     C                     PARM LBCYCD    P@CCY   3                       S01498
     C           SDCURR    PARM SDCURR    DSSDY                           S01498
     C*                                                                   S01498
     C** Check if error occurred                                          S01498
     C*                                                                   S01498
     C           P@RTCD    IFNE *BLANKS                                   S01498
     C           *LOCK     IN   LDA                                       S01498
     C                     Z-ADD3         DBASE            ****************
     C                     MOVE *BLANKS   DBFILE           *              *
     C***********          MOVEL'SDCURRL1'DBFILE           * DBERROR  003 S01498
     C                     MOVE 'SDCURRPD'DBFILE           * DBERROR 003 *S01498
     C                     MOVE *BLANKS   DBKEY            *              *
     C***********          MOVE LBCCY     DBKEY            ***************S01498
     C                     MOVE LBCYCD    DBKEY            ***************S01498
     C                     OUT  LDA                                       S01498
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     EXSR *PSSR                                     S01498
     C                     RETRN
E1   C                     END
     C*
     C**   SET UP CONSTANTS FOR ZALIGN
     C*
     C                     Z-ADDA6NBDP    ZADEC
     C           13        SUB  A6NBDP    ZADIG
     C*
     C                     MOVE '0'       *IN40
     C                     MOVE '1'       *IN41
     C                     MOVE '0'       *IN99
     C*
     C**   CLEAR SCREEN FIELD
     C*
     C                     MOVE *BLANKS   DDSELA
     C*
     C**   INITIALISE SCREEN MESSAGE QUEUE
     C*
     C                     MOVEL'*'       DDPGMQ
     C*
     C           #A0       ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #B        - MAIN PROCESSING                                *
     C**                                                              *
     C**   CALLS     - #BA                                            *
     C**                                                              *
     C**   CALLED BY - MAIN CONTROL                                   *
     C**                                                              *
     C*****************************************************************
     C           #B        BEGSR
     C*
     C**   DOWHILE NOT CMD-3 AND SCREEN NOT VALID
     C*
B1   C           *INKC     DOWEQ'0'
     C           *IN41     ANDEQ'1'
     C*                                                                   S01498
     C**  Screen time                                                     S01498
     C                     TIME           STIME                           S01498
     C*
     C**   DISPLAY SELECTION SCREEN
     C*
     C                     WRITELB0371F1
     C*
     C**   IF ERROR - WRITE MESSAGE SUBFILE
     C*
B2   C           *IN40     IFEQ '1'
     C                     WRITELB0371C2
E2   C                     END
     C*
     C**   READ SELECTION SCREEN
     C*
     C                     READ LB0371F1                 50
     C*
     C**   IF NOT CMD-3
     C*
B2   C           *INKC     IFEQ '0'
     C*
     C**   PERFORM VALIDATION
     C*
     C                     EXSR #BA
     C*
     C**   IF VALID
     C*
B3   C           *IN40     IFEQ '0'
     C*
     C*****CALL*PROGRAM*'LB0370'***************************************   S01498
     C*
     C                     MOVE '0'       *IN41
     C***********          MOVE '1'       WWP1    1                       S01498
     C                     MOVE ZFIELD    WWP2   130
     C*
     C***********          UNLCKWWLDA                                     S01498
     C***********          CALL 'LB0370'                                  S01498
     C***********          PARM           WWP1                            S01498
     C***********          PARM           WWP2                            S01498
     C*
     C************LOCK     IN   WWLDA                                     S01498
B4   C           DBASE     IFNE 0
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
E4   C                     END
     C*
E3   C                     END
     C*
E2   C                     END
     C*
E1   C                     END
     C*                                                                   S01498
     C**  If F3 was pressed send termination code of 'E' back to RCF      S01498
     C*                                                                   S01498
     C           *INKC     IFEQ '1'                                       S01498
     C                     MOVE *BLANKS   @RPARM                          S01498
     C                     MOVE 'E'       RCMD                            S01498
     C                     END                                            S01498
     C*
     C           #B0       ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #BA       - SCREEN VALIDATION                              *
     C**                                                              *
     C**   CALLED BY - #B                                             *
     C**                                                              *
     C**   CALLS     - NONE                                           *
     C**                                                              *
     C*****************************************************************
     C*
     C           #BA       BEGSR
     C*
     C**   CLEAR SCREEN MESSAGE QUEUE
     C*
     C                     MOVE *BLANKS   WWTPGQ
     C                     MOVEL'*'       WWTPGQ
     C                     MOVE *BLANKS   WWPGQR
     C                     MOVEL'*PRV'    WWPGQR
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM           WWTPGQ 10
     C                     PARM           WWPGQR  5
     C*
     C**   SET OFF INVALID INDICATOR
     C*
     C                     MOVE '0'       *IN40
     C*
     C**   IF AN AMOUNT HAS BEEN ENTERED
     C*
B1   C           DDSELA    IFNE *BLANKS
     C*
     C**   EXECUTE ZALIGN TO VALIDATE AMOUNT
     C*
     C                     MOVE '0'       *IN99
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE DDSELA    ZFIELD
     C                     EXSR ZALIGN
     C*
X1   C                     ELSE
     C*
     C                     MOVE '1'       *IN40
     C*
E1   C                     END
     C*
     C**   IF ERROR - SET ON INVALID INDICATOR & LOAD MESSAGE TO
     C**              TO MESSAGE SUBFILE
     C*
B1   C           *IN99     IFEQ '1'
     C           *IN40     OREQ '1'
     C                     MOVE '1'       *IN40
     C                     MOVEL'LBM0061' ZAMSID
     C                     EXSR ZASNMS
E1   C                     END
     C*
     C           #BA0      ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   ZASNMS    - SEND MESSAGE TO PROGRAM'S MESSAGE QUEUE        *
     C**                                                              *
     C**   CALLED BY - MAINLINE                                       *
     C**                                                              *
     C**   CALLS     - Y2SNMGC                                        *
     C**                                                              *
     C*****************************************************************
     C*
     C           ZASNMS    BEGSR
     C*
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZAMSID  7
     C                     PARM           ZAMSGF 10
     C                     PARM           ZAMSDA132
     C                     PARM           ZAMSTP  7
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   *PSSR     - PROGRAM ERROR HANDLING ROUTINE                 *
     C**                                                              *
     C**   CALLS     -                                                *
     C**                                                              *
     C**   CALLED BY -                                                *
     C**                                                              *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C                     DUMP
     C*
     C                     ENDSR
     C*****************************************************************
** CPY@
(c) Finastra International Limited 2001
