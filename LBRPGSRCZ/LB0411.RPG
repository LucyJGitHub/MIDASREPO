     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LB Guarantees and Pledges maturing/sel.')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Lending Module                         *
     F*                                                               *
     F*  LB0411 - Guarantees and Pledges Maturing/Review Selection    *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01498             Date 11Aug94               *
      *                 R00300             Date 21Aug90               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     F*  S01498 - Portfolio Lending Upgrade to Release 10             *
     F*  R00300 - Message file changes                                *
     F*                                                               *
     F*****************************************************************
     F**                                                              *
     F** C R E A T I O N     P A R A M E T E R S                      *
     F**                                                              *
     F**                                                              *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F**
     F**      FILE DEFINITIONS
     F**      ----------------
     F*
     F**   SCREEN FORMAT
     FLB0411DFCF  E                    WORKSTN      KINFSR *PSSR
     F*
     F*****BANK*DETAILS*FILE*-*PREFIX*BJ*******************************   S01498
     F***SDBANKPDIF  E                    DISK         KINFSR *PSSR       S01498
     F*
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F**                                                              *
     F** ID F  C  H  L    FUNCTION OF INDICATORS                      *
     F**                  ----------------------                      *
     F*********20*********ACCESS*TO*BANK*DETAILS*FILE*-*SDBANKPD*******   S01498
     F**       40         SCREEN VALIDATION FAILED                    *
     F**       41         VALID VALUE NOT YET ENTERED                 *
     F**       50         READ INDICATOR ON LB0411DF                  *
     F**       51         SFLEND                                      *
     F**       99         INDICATOR USED BY ZALIGN                    *
     F**                                                              *
     F*****************************************************************
     E/EJECT
     E*****************************************************************
     E*
     E** ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
     E*****************************************************************
     E/EJECT
     E*****************************************************************
     E/COPY ZSRSRC,ZALIGNZ1
     E*****************************************************************
     I/EJECT
     I*****************************************************************
     I*
     I** DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
     I*                                                                   S01498
     I@RPARM      DS                            100                       S01498
     I**  RCF parameter data structure                                    S01498
     I                                        1   30WWP2                  S01498
     I*
     I**   PROGRAM STATUS DATA STRUCTURE
     IPSDS       SDS
     I**************************************244 253 DDWSID                S01498
     I**************************************254 263 USER                  S01498
     I                                      244 253 SWSID                 S01498
     I                                      254 263 SUSER                 S01498
     I*
     I**   LOCAL DATA AREA
     I***WWLDA      UDS                            256                    S01498
     ILDA         DS                            256                       S01498
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*                                                                   S01498
     ISDBANK    E DSSDBANKPD                                              S01498
     I**  Data structure for bank details                                 S01498
     I*                                                                   S01498
     IDSFDY     E DSDSFDY                                                 S01498
     I**  Short data structure for access programs                        S01498
     I*
     I*****************************************************************
     C/EJECT
     C*****************************************************************   S01498
     C*                                                                   S01498
     C**  Parameter List Declarations                                     S01498
     C*                                                                   S01498
     C           *ENTRY    PLIST                                          S01498
     C                     PARM           @SEQ    5                       S01498
     C                     PARM           @LVL    1                       S01498
     C                     PARM           @RENT   3                       S01498
     C                     PARM           @RPARM100                       S01498
     C                     PARM           RACT    1                       S01498
     C                     PARM           RCMD    1                       S01498
     C*
     C*****************************************************************
     C*
     C**            START MAINLINE
     C**           ----------------
     C*
     C*
     C**   INITIAL PROCESSING
     C*
     C                     EXSR #A
     C*
     C**   MAIN PROCESSING
     C*
     C                     EXSR #B
     C*
     C**   END PROGRAM
     C*
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**        INDEX TO SUBROUTINES                                  *
     C**       ----------------------                                 *
     C**                                                              *
     C**   ZALIGN  - VALIDATE & RIGHT ALIGN NUMERIC FIELDS            *
     C**                                                              *
     C**   #A      - INITIAL PROCESSING                               *
     C**   #B      - MAIN PROCESSING                                  *
     C**   #BA     - SCREEN VALIDATION                                *
     C**                                                              *
     C**   *PSSR   - PROGRAM ERROR HANDLING ROUTINE                   *
     C**                                                              *
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C/COPY ZSRSRC,ZALIGNZ2
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #A        - INITIAL PROCESSING                             *
     C**                                                              *
     C**   CALLED BY - MAINLINE                                       *
     C**                                                              *
     C**   CALLS     - NONE                                           *
     C**                                                              *
     C*****************************************************************
     C*
     C           #A        BEGSR
     C*                                                                   S01498
     C**  Copyright statement                                             S01498
     C*                                                                   S01498
     C                     MOVEACPY@      ACT@   80                       S01498
     C*
     C************NAMVAR   DEFN LDA       WWLDA                           S01498
     C           *NAMVAR   DEFN           LDA                             S01498
     C           *LOCK     IN   LDA                                       S01498
     C*
     C                     MOVE '1'       *IN51
     C                     MOVEL'LB0411'  DBPGM
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD*ZEROS    DBASE
     C                     OUT  LDA                                       S01498
     C*                                                                   R00300
     C**   Fill in fields for subroutine ZASNMS                           R00300
     C                     MOVEL*BLANK    ZAPGM  10        Message queue  R00300
     C                     MOVE *BLANKS   ZAMSGF                          R00300
     C*********************MOVEL'LXGBMSGF'ZAMSGF                          R00300
     C                     MOVEL'LBUSRMSG'ZAMSGF 10        Message file   R00300
     C                     MOVEL*BLANK    ZAPGRL  5        Rel queue      R00300
     C                     MOVEL*BLANK    ZAMSID  7        Message Id.    R00300
     C                     MOVEL*BLANK    ZAMSDA132        Message data.  R00300
     C                     MOVEL*BLANK    ZAMSTP  7        Message type.  R00300
     C*
     C*****ACCESS*SDBANKPD*TP*GET*RUN*DATE*****************************   S01498
     C*
     C***********1         SETLLSDBANKPD                                  S01498
     C***********          READ SDBANKPD                 20               S01498
     C*
B1   C************IN20     IFEQ '1'                                       S01498
     C*                                                                   S01498
     C**  Use access object to get run date                               S01498
     C*                                                                   S01498
     C                     CALL 'AOBANKR0'                                S01498
     C                     PARM '*DBERR'  P@RTCD  7                       S01498
     C                     PARM '*FIRST'  P@OPTN  7                       S01498
     C           SDBANK    PARM SDBANK    DSFDY                           S01498
     C*                                                                   S01498
     C**  Check if error occurred                                         S01498
     C*                                                                   S01498
     C           P@RTCD    IFNE *BLANK                                    S01498
     C           *LOCK     IN   LDA                                       S01498
     C                     Z-ADD1         DBASE            ****************
     C                     MOVE *BLANKS   DBFILE           *              *
     C***********          MOVEL'SDBANKPD'DBFILE           * DBERROR  001 S01498
     C                     MOVE 'SDBANKPD'DBFILE           * DBERROR 001 *S01498
     C                     MOVE *BLANKS   DBKEY            *              *
     C                     OUT  LDA                        ***************S01498
     C                     MOVE '1'       *INU7            ****************
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     EXSR *PSSR                                     S01498
     C                     RETRN
E1   C                     END
     C*                                                                   S01498
     C**  Rundate in DDMMMYY format                                       S01498
     C*                                                                   S01498
     C                     MOVE BJMRDT    SRUNA                           S01498
     C*
     C                     MOVE '0'       *IN40
     C                     MOVE '1'       *IN41
     C                     MOVE '0'       *IN99
     C*
     C**   CLEAR SCREEN FIELD
     C*
     C                     MOVE *BLANKS   DDSELP
     C*
     C**   INITIALISE SCREEN MESSAGE QUEUE
     C*
     C                     MOVEL'*'       DDPGMQ
     C*
     C           #A0       ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #B        - MAIN PROCESSING                                *
     C**                                                              *
     C**   CALLS     - #BA                                            *
     C**                                                              *
     C**   CALLED BY - MAIN CONTROL                                   *
     C**                                                              *
     C*****************************************************************
     C           #B        BEGSR
     C*
     C**   DOWHILE NOT CMD-3 AND SCREEN NOT VALID
     C*
B1   C           *INKC     DOWEQ'0'
     C           *IN41     ANDEQ'1'
     C*                                                                   S01498
     C**  Screen time                                                     S01498
     C                     TIME           STIME                           S01498
     C*
     C**   DISPLAY SELECTION SCREEN
     C*
     C                     WRITELB0411F1
     C*
     C**   IF ERROR - WRITE MESSAGE SUBFILE
     C*
B2   C           *IN40     IFEQ '1'
     C                     WRITELB0411C2
E2   C                     END
     C*
     C**   READ SELECTION SCREEN
     C*
     C                     READ LB0411F1                 50
     C*
     C**   IF NOT CMD-3
     C*
B2   C           *INKC     IFEQ '0'
     C*
     C**   PERFORM VALIDATION
     C*
     C                     EXSR #BA
     C*
     C**   IF VALID
     C*
B3   C           *IN40     IFEQ '0'
     C*
     C*****CALL*PROGRAM*'LB0410'***************************************   S01498
     C*
     C                     MOVE '0'       *IN41
     C***********          MOVE '1'       WWP1    1                       S01498
     C                     MOVE DDSELP    WWP2    30
     C*
     C***********          UNLCKWWLDA                                     S01498
     C***********          CALL 'LB0410'                                  S01498
     C***********          PARM           WWP1                            S01498
     C***********          PARM           WWP2                            S01498
     C*
     C************LOCK     IN   WWLDA                                     S01498
     C           DBASE     IFNE 0
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     END
     C*
E3   C                     END
     C*
E2   C                     END
     C*
E1   C                     END
     C*                                                                   S01498
     C**  If F3 was pressed send termination code of 'E' back to RCF      S01498
     C*                                                                   S01498
     C           *INKC     IFEQ '1'                                       S01498
     C                     MOVE *BLANKS   @RPARM                          S01498
     C                     MOVE 'E'       RCMD                            S01498
     C                     END                                            S01498
     C*
     C           #B0       ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #BA       - SCREEN VALIDATION                              *
     C**                                                              *
     C**   CALLED BY - #B                                             *
     C**                                                              *
     C**   CALLS     - NONE                                           *
     C**                                                              *
     C*****************************************************************
     C*
     C           #BA       BEGSR
     C*
     C**   CLEAR SCREEN MESSAGE QUEUE
     C*
     C                     MOVE *BLANKS   WWTPGQ
     C                     MOVEL'*'       WWTPGQ
     C                     MOVE *BLANKS   WWPGQR
     C                     MOVEL'*PRV'    WWPGQR
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM           WWTPGQ 10
     C                     PARM           WWPGQR  5
     C*
     C**   SET OFF INVALID INDICATOR
     C*
     C                     MOVE '0'       *IN40
     C*
     C**   IF A PERIOD HAS BEEN ENTERED
     C*
B1   C           DDSELP    IFNE *BLANKS
     C*
     C**   EXECUTE ZALIGN TO VALIDATE PERIOD
     C*
     C                     Z-ADD*ZEROS    ZADEC
     C                     Z-ADD3         ZADIG
     C                     MOVE '0'       *IN99
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE DDSELP    ZFIELD
     C                     EXSR ZALIGN
     C*
B2   C           *IN99     IFEQ '0'
     C*
     C**   IF NUMERIC , CHECK VALUE IS IN CORRECT RANGE
     C*
     C                     MOVE ZFIELD    DDSELP
     C*
B3   C           DDSELP    IFLT '0'
     C*
     C                     MOVE '1'       *IN40
     C*
E3   C                     END
     C*
     C**   NOT NUMERIC
     C*
X2   C                     ELSE
     C*
     C                     MOVE '1'       *IN40
     C*
E2   C                     END
     C*
     C**   NO PERIOD ENTERED
     C*
X1   C                     ELSE
     C*
     C                     MOVE '1'       *IN40
     C*
E1   C                     END
     C*
     C**   IF ERROR - SET ON INVALID INDICATOR & LOAD MESSAGE TO
     C**              TO MESSAGE SUBFILE
     C*
B1   C           *IN40     IFEQ '1'
     C*
     C                     MOVEL'LBM0060' ZAMSID
     C                     EXSR ZASNMS
     C*
E1   C                     END
     C*
     C           #BA0      ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   ZASNMS    - SEND MESSAGE TO PROGRAM'S MESSAGE QUEUE        *
     C**                                                              *
     C**   CALLED BY - MAINLINE                                       *
     C**                                                              *
     C**   CALLS     - Y2SNMGC                                        *
     C**                                                              *
     C*****************************************************************
     C*
     C           ZASNMS    BEGSR
     C*
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZAMSID  7
     C                     PARM           ZAMSGF 10
     C                     PARM           ZAMSDA132
     C                     PARM           ZAMSTP  7
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   *PSSR     - PROGRAM ERROR HANDLING ROUTINE                 *
     C**                                                              *
     C**   CALLS     -                                                *
     C**                                                              *
     C**   CALLED BY -                                                *
     C**                                                              *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C                     DUMP
     C*
     C                     ENDSR
     C*****************************************************************
** CPY@
(c) Finastra International Limited 2001
