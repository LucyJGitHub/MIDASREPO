     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LB Pledges Agreement report.')                   *
     F********************************************************************
     F*                                                               *
     F*  Midas - Portfolio Lending Module                         *
     F*                                                               *
     F*  LB0530 - Portfolio Lending Pledges Agreement Report.         *
     F*                                                               *
     F*  Function:  This program produces a Guarantees and Pledges    *
     F*  (COB)      signed/unsigned Report.                           *
     F*             Branch Level Available.                           *
     F*                                                               *
     F*  Called By: LBC0530 - LB Portfolio Lending Pledges Agreement  *
     F*                       Report.                                 *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 12Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 093962             Date 29Sep95               *
      *                 087628             Date 01Sep95               *
     F*                 S01498             DATE 11AUG94               *
     F*                 001280PA           DATE 05AUG91               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *  *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*  093962 - ZSFILE need only be called once for each spool file *
     F*  087628 - Recompiled over LBPLEGPD                            *
     F*  S01498 - Portfolio Lending Upgrade to Rel 10.                *
     F*  001280 - If type is collateral, guarantee or security,       *  *
     F*           the Issued may be blank.                            *  *
     F*                                                               *
     F*****************************************************************
     F** C R E A T I O N     P A R A M E T E R S                      *
     F**                                                              *
     F**                                                              *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F**
     F**      FILE DEFINITIONS
     F**     ------------------
     F*
     F**   RUN CONTROLS REPORT
     FLB0530AUO   E             21     PRINTER      KINFDS SPOOLU         S01498
1    F***LB0530AUO***E             21     PRINTER      KINFDS ERF2DS      S01498
     F*
     F**   TOT. COMMITMENT LINES EXCEEDED BY PERCENTAGE REPORT
     FLB0530P1O   E             22     PRINTER      KINFDS SPOOL      UC  S01498
2    F***LB0530P1O***E             22     PRINTER      KINFDS ERF1DS      S01498
     F*
     F**   TRAILER FILE. PREFIX TR.
3    FLBTRAIPDIF  E           K        DISK         KINFSR *PSSR
     F*
     F*****BANK*DETAILS*FILE.*PREFIX*BJ.*******************************   S01498
4    F***SDBANKPDIF**E                    DISK         KINFSR *PSSR       S01498
     F*
     F*****CURRENCY*FILE.*PREFIX*A6.***********************************   S01498
5    F***SDCURRL1IF**E           K        DISK         KINFSR *PSSR       S01498
     F*
     F*****ACCOUNT*OFFICERS*FILE.*PREFIX*AZ.***************************   S01498
6    F***SDACOFL0IF**E           K        DISK         KINFSR *PSSR       S01498
     F*
     F*****REPORT*SELECTION*FILE.*NO*PREFIX.***************************   S01498
7    F***QINLINE*IF**F      96            DISK         KINFSR *PSSR       S01498
     F*
     F*****CLIENT*MASTER*FILE.*PREFIX*BB.******************************   S01498
8    F***SDCUSTL1IF**E           K        DISK         KINFSR *PSSR       S01498
     F*
     F**   GUARANTEES AND PLEDGES FILE. PREFIX PG , BB.
9    FLBPLEGLKIF  E           K        DISK         KINFSR *PSSR
     F*
     F**   GUARANTEES AND PLEDGES FILE. PREFIX PG , BB.
10   FLBPLEGLLIF  E           K        DISK         KINFSR *PSSR
     F            LBPLEGJ0                          KRENAMELBPLEGJL
     F*
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F**                                                              *
     F** ID F  C  H  L    FUNCTION OF INDICATORS                      *
     F**                 ------------------------                     *
     F**                                                              *
     F**    21         - OVERFLOW INDICATOR ON LB0530AU               *
     F**    22         - OVERFLOW INDICATOR ON LB0530P1               *
     F**       23      - ACCESS TO TRAILER FILE - LBTRAIPD            *
     F**       24      - ACCESS TO BANK DETAILS FILE - SDBANKPD       *
     F**       25      - ACCESS TO CURRENCY FILE - SDCURRL1           *
     F**       26      - ACCESS TO ACCOUNT OFFICERS FILE - SDACOFL0   *
     F**       27      - ACCESS TO REPORT SELECTION FILE - QINLINE    *
     F**       28      - ACCESS TO CUSTOMER FILE - SDCUSTL1           *
     F**       29      - ACCESS TO GUAR. AND PLEDGES FILE - LBPLEGLK  *
     F**       30      - ACCESS TO GUAR. AND PLEDGES FILE - LBPLEGLL  *
     F**       37      - Multibranching indicator                         S01498
     F**       40      - RECORDS SELECTED INDICATOR                   *
     F**       50      - CONDITION AGREEMENT DATE ON MAIN REPORT      *
     F**       51      - CONDITION TEXT ON MAIN REPORT                *
     F**       52      - CONDITION TEXT ON MAIN REPORT                *
     F**       53      - CONDITION TEXT ON MAIN REPORT                *
     F**       54      - CONDITION TEXT ON MAIN REPORT                *
     F**       55      - CONDITION SPACING ON MAIN REPORT             *
     F**       60      - *** DIFFERENCE *** ON RUN CONTROLS REPORT    *
     F**       61      - OPEN LB0530P1 printer file.                  *   S01498
     F**       62      - Report no records found.                     *   S01498
     F**       65      - Actual code entered for Entity.              *   S01498
     F**                                                              *
     F*****************************************************************
     F/EJECT
     E*****************************************************************
     E*
     E**   ARRAY FOR COPYRIGHT
     E*
     E                    CPY@    1   1 80
     E*****************************************************************
     E/EJECT
     E*****************************************************************
     E/COPY ZSRSRC,ZSEDITZ1
     E*****************************************************************
     E/EJECT
     E*****************************************************************
     E/COPY ZSRSRC,ZDATE2Z1
     E*****************************************************************
     E/EJECT
     I********************************************************************
     I*
     I**   RENAME FIELDS ON SDCUSTL1
     I***@BBREBF***********                                               S01498
     I***********   BBCUST                          SDCUST                S01498
     I***********   BBCRNM                          SDCRNM                S01498
     I***********   BBACOC                          SDACOC                S01498
     I***********   BBBRCD                          SDBRCD                S01498
     I********************************************************************
     I*
     I**   INTERNALLY DEFINE QINLINE
     I***QINLINE*NS                                                       S01498
      *                                                                   S01498
      **  Report parameters.                                              S01498
      *                                                                   S01498
     I            DS                                                      S01498
     I                                        1 100 @RPRM                 S01498
     I                                        1   50WWFDAT
     I                                        6  100WWTDAT
     I*
     I**   DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
     I*****************************************************************
     I/EJECT
     I*****************************************************************
     I/COPY ZSRSRC,ZSEDITZ2
     I*****************************************************************
     I/EJECT
     I*
     ISDBANK    E DSSDBANKPD                                              S01498
      **  Data structure for bank file                                    S01498
      *                                                                   S01498
     ISDBRCH    E DSSDBRCHPD                                              S01498
      **  Data structure for branch file.                                 S01498
      *                                                                   S01498
     ISDCUST    E DSSDCUSTPD                                              S01498
     I              BBCUST                          SDCUSN                S01498
     I              BBCRNM                          SDCRNM                S01498
     I              BBACOC                          SDACOC                S01498
     I              BBBRCD                          SDBRCD                S01498
     I              QQDFAC                          DFACQQ                                    CGL029
      **  Data structure for customer file                                S01498
      *                                                                   S01498
     ISDCURR    E DSSDCURRPD                                              S01498
      ** Access Object for Account Officer File.                          S01194
      *                                                                   S01194
     ISDACOF    E DSSDACOFPD                                              S01498
      ** Access Object for Account Officer File.                          S01498
      *                                                                   S01498
     IDSFDY     E DSDSFDY                                                 S01498
      **  Short data structure for access programs                        S01498
      *                                                                   S01498
     IDSSDY     E DSDSSDY                                                 S01498
      **  Long data structure for access programs                         S01498
      *                                                                   S01498
     I**   PROGRAM STATUS DATA STRUCTURE
     IPSDS       SDS
     I                                      244 253 JOB
     I                                      254 263 USER
     I*
     I**   FILE INFORMATION DATA STRUCTURE
     I***ERF1DS******DS                                                   S01498
     I*
     ISPOOL       DS                                                      S01498
     I                                       83  92 SFILE                 S01498
     I                                    B 123 1240SFNUM                 S01498
     I**   FOR OVERFLOW
     I                                    B 367 3680LINE
     I*
     I***ERF2DS******DS                                                   S01498
     I*
     ISPOOLU      DS                                                      S01498
     I                                       83  92 SFILEU                S01498
     I                                    B 123 1240SFNUMU                S01498
     I**   FOR OVERFLOW
     I                                    B 367 3680LINE1
     I*
     IRUNDAT    E DS                                                      S01498
     I**  Standard Data Area Layout for System flags and Run Date         S01498
     I*                                                                   S01498
     I**   LOCAL DATA AREA
     I***LDA********UDS                            256                    S01498
     ILDA         DS                            256                       S01498
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *                                                                   S01498
      ** KLIST fields                                                     S01498
      *                                                                   S01498
     IKEYLB       DS                                                      S01498
     I                                        1   3 FBRCD                 S01498
     I                                        4   5 FACOC                 S01498
     I************                            6  110FCUST                 S01498              CSD027
     I                                        6  11 FCUST                                     CSD027
     I            DS                                                      S01498
     I                                        1   3 @ETTY                 S01498
     I                                        1   2 @AC                   S01498
     C*****************************************************************
     C*
     C**   PARAMETERS INPUT TO PROGRAM
     C**   ---------------------------
     C*
     C           *ENTRY    PLIST                                          S01498
     C                     PARM           SEQ     5                       S01498
     C                     PARM           @LVL    1                       S01498
     C                     PARM           @RENT   3                       S01498
     C                     PARM           @RPARM100                       S01498
     C*                                                                   E36542
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*
     C**   KEYS USED BY PROGRAM
     C**   --------------------
      *                                                                   S01498
      ** Key for Branch report                                            S01498
      *                                                                   S01498
     C           KBRCD     KLIST                                          S01498
     C                     KFLD           FBRCD                           S01498
     C                     KFLD           FACOC                           S01498
     C                     KFLD           FCUST                           S01498
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*
     C**              START MAINLINE
     C**             ----------------
     C*
     C**   INITIAL PROCESSING
     C*
     C                     EXSR #A
     C*
     C**   MAIN PROCESSING
     C*
     C                     EXSR #B
     C*
     C**   END PROGRAM
     C*
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**    INDEX TO SUBROUTINES                                      *
     C**   ----------------------                                     *
     C**                                                              *
     C**   ZDATE2  - CONVERT A 5-FIGURE DAY NUMBER INTO DATE FORMAT   *
     C**   ZSEDIT  - CONVERT AMOUNT FOR OUTPUT                        *
     C**                                                              *
     C**   #A      - INITIAL PROCESSING                               *
     C**   #B      - MAIN PROCESSING                                  *
     C**   #BA     - PROCESS UNSIGNED AGREEMENTS                      *
     C**   #BB     - PROCESS SIGNED AGREEMENTS                        *
     C**   #BBA    - SET UP SELECTION FIELDS FOR SIGNED AGREEMENTS    *
     C**   #BC     - TERMINATION PROCESSING                           *
     C**   #YA     - WRITE HEADING FORMAT                             *
     C**   #YB     - WRITE DETAIL FORMAT                              *
     C**   #ZA     - PROCESS DATABASE ERRORS                          *
     C**                                                              *
     C**   *PSSR   - PROGRAM ERROR HANDLING ROUTINE                   *
     C**                                                              *
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C/COPY ZSRSRC,ZSEDITZ3
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #A        - INITIAL PROCESSING                             *
     C**                                                              *
     C**   CALLS     - #ZA                                            *
     C**                                                              *
     C**   CALLED BY - MAINLINE                                       *
     C**                                                              *
     C*****************************************************************
     C*
     C           #A        BEGSR
      *                                                                   S01498
      ***  Copyright Statement.                                           S01498
     C                     MOVEACPY@      ACT@   80                       S01498
      *                                                                   S01498
      ** Define LDA                                                       S01498
     C           *NAMVAR   DEFN           LDA                             S01498
     C           *LOCK     IN   LDA                                       S01498
     C*
     C**   INITIALISE DB ERROR FIELDS AND STORE PROGRAM NAME FOR
     C**   DB ERROR FIELD.
     C*
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD*ZEROS    DBASE
     C                     MOVEL'LB0530'  DBPGM
     C                     OUT  LDA                                       S01498
     C*
     C**   DEFINE WORK FIELDS
     C*
     C           *LIKE     DEFN PGCOLS    WWVALU
     C           *LIKE     DEFN BBCUST    WWCUST
     C           *LIKE     DEFN BBACOC    WWACOC
     C           *LIKE     DEFN BBBRCD    WWBRCD
     C*
     C**   INITIALISE WORK FIELDS
     C*
     C                     Z-ADD*ZEROS    WWNRIP  50
     C                     Z-ADD*ZEROS    WWVALU
     C*************        Z-ADD*ZEROS    WWCUST                                              CSD027
     C                     MOVE *BLANKS   WWCUST                                              CSD027
     C                     MOVE *BLANKS   WWACOC
     C                     MOVE *BLANKS   WWBRCD
     C*
     C**   SET OFF ALL INDICATORS
     C*
     C                     MOVEA'00000000'*IN,21           OFF 21-28
     C                     MOVEA'00'      *IN,29           OFF 29-30
     C                     MOVE '0'       *IN40            OFF 40
     C                     MOVEA'000000'  *IN,50           OFF 50-55
     C                     MOVE '0'       *IN60            OFF 60
     C*                                                                   S01498
     C**  Read in DTAARA/RUNDAT to get multibranching indicator           S01498
     C*                                                                   S01498
     C           *NAMVAR   DEFN           RUNDAT                          S01498
     C                     IN   RUNDAT                                    S01498
     C           AGMBIN    IFEQ 'Y'                                       S01498
     C                     MOVE '1'       *IN37                           S01498
     C                     END                                            S01498
     C*
     C**   ACCESS ICD RECORD 1 TO GET RUN DATE & TITLE
     C*
     C***********1         SETLLSDBANKPD                                  S01498
     C***********          READ SDBANKPD                 24               S01498
     C*
     C**   IF RECORD NOT FOUND OR LAST-CHANGE-TYPE = 'D' (DELETED)
     C*
B1   C************IN24     IFEQ '1'                                       S01498
     C***********BJTYLC    OREQ 'D'                                       S01498
      *                                                                   S01498
      **  Access SDBANKPD for report title                                S01498
      *                                                                   S01498
     C                     CALL 'AOBANKR0'                                S01498
     C                     PARM '*MSG   ' @RTCD   7                       S01498
     C                     PARM '*FIRST ' @OPTN   7                       S01498
     C           SDBANK    PARM SDBANK    DSFDY                           S01498
      *                                                                   S01498
      ** Error                                                            S01498
     C           @RTCD     IFNE *BLANKS                                   S01498
     C           *LOCK     IN   LDA                                       S01498
     C                     Z-ADD1         DBASE            ****************
     C                     MOVE *BLANKS   DBFILE           *              *
     C***********          MOVEL'SDBANKPD'DBFILE           * DBERROR  001 S01498
     C                     MOVE 'SDBANKPD'DBFILE           *              S01498
     C                     MOVE *BLANKS   DBKEY            *              *
     C                     OUT  LDA                                       S01498
     C                     EXSR #ZA                        ****************
E1   C                     END
     C*
     C**   SET UP DATE FORMAT INDICATOR
     C*
B1   C           BJDFIN    IFEQ 'M'
     C                     MOVE '1'       *IN98
X1   C                     ELSE
     C                     MOVE '0'       *IN98
E1   C                     END
      *                                                                   S01498
      **  Branch Level Selection.                                         S01498
      *                                                                   S01498
     C                     MOVE *BLANKS   KEYLB                           S01498
     C                     MOVE @RENT     FBRCD                           S01498
      *                                                                   S01498
      **  Determine if an actual Branch Code has been selected.           S01498
      *                                                                   S01498
     C           @RENT     IFNE *BLANKS                                   S01498
     C           @RENT     ANDNE'ALL'                                     S01498
     C                     MOVE *ON       *IN65                           S01498
     C                     END                                            S01498
      *                                                                   S01498
      **  Manipulate input parameter data.                                S01498
     C                     MOVEL@RPARM    @RPRM                           S01498
     C                     MOVEL@RENT     @ETTY                           S01498
     C*
     C           #A0       ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #B        - MAIN PROCESSING                                *
     C**                                                              *
     C**   CALLS     - #BA , #BB , #BC                                *
     C**                                                              *
     C**   CALLED BY - MAINLINE                                       *
     C**                                                              *
     C*****************************************************************
     C*
     C           #B        BEGSR
     C*
     C*****READ*RECORD*ON*'QINLINE'************************************   S01498
     C*
     C***********1         SETLLQINLINE                                   S01498
     C***********          READ QINLINE                  27               S01498
     C*
B1   C************IN27     DOWEQ'0'                                       S01498
      *                                                                   S01498
      ** Determine if any details to report.                              S01498
     C           *IN65     IFEQ *ON                                       S01498
     C           KBRCD     SETLLLBPLEGLK                                  S01498
     C                     READ LBPLEGLK                 29               S01498
      *                                                                   S01498
      ** Check Branch Code record found.                                  S01498
     C           @RENT     IFNE BBBRCD
     C                     MOVE *ON       *IN29
     C                     ENDIF
      *                                                                   S01498
     C                     ELSE                                           S01498
     C           *LOVAL    SETLLLBPLEGLK
     C                     READ LBPLEGLK                 29
     C                     ENDIF                                          S01498
      *                                                                   S01498
     C           *IN65     IFEQ *ON                                       S01498
     C           KBRCD     SETLLLBPLEGLL                                  S01498
     C                     READ LBPLEGLL                 30               S01498
      *                                                                   S01498
      ** Check Branch Code record found.                                  S01498
     C           @RENT     IFNE BBBRCD
     C                     MOVE *ON       *IN30
     C                     ENDIF
      *                                                                   S01498
     C                     ELSE                                           S01498
     C           *LOVAL    SETLLLBPLEGLL
     C                     READ LBPLEGLL                 30
     C                     ENDIF                                          S01498
     C*
      ** Details have been found                                          S01498
      *                                                                   S01498
     C           *IN29     IFEQ *OFF
     C           *IN30     OREQ *OFF
      *                                                                   S01498
     C**   PROCESS UNSIGNED AGREEMENTS
     C*
     C                     EXSR #BA
     C*
     C**   PROCESS SIGNED AGREEMENTS
     C*
     C                     EXSR #BB
      *                                                                   S01498
      ** No details to report                                             S01498
     C                     ELSE                                           S01498
     C                     MOVE *ON       *IN62                           S01498
     C                     ENDIF                                          S01498
     C*
     C**   PERFORM TERMINATION PROCESSING
     C*
     C                     EXSR #BC
     C*
     C*****READ*NEXT*RECORD*ON*'QINLINE'*******************************   S01498
     C*
     C***********          READ QINLINE                  27               S01498
     C*
E1   C***********          END                                            S01498
     C*
     C           #B0       ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #BA       - PROCESS UNSIGNED AGREEMENTS                    *
     C**                                                              *
     C**   CALLS     - #YA , #YB                                      *
     C**                                                              *
     C**   CALLED BY - #B                                             *
     C**                                                              *
     C*****************************************************************
     C*
     C           #BA       BEGSR
     C*
     C**   READ FIRST LIVE RECORD ON 'LBPLEGLK'
     C*
      ** Check for an actual Branch Code                                  S01498
      *                                                                   S01498
     C           *IN65     IFEQ *ON                                       S01498
     C           KBRCD     SETLLLBPLEGLK                                  S01498
     C                     READ LBPLEGLK                 29               S01498
      *                                                                   S01498
      ** Check Branch Code record found.                                  S01498
     C           @RENT     IFNE BBBRCD
     C                     MOVE *ON       *IN29
     C                     ENDIF
      *                                                                   S01498
     C                     ELSE                                           S01498
     C           *LOVAL    SETLLLBPLEGLK
     C                     READ LBPLEGLK                 29
     C                     ENDIF                                          S01498
      *                                                                   S01498
     C*
     C**   IF NO DETAILS SELECTED OUTPUT NULL REPORT
     C*
B1   C           *IN29     IFEQ '1'
     C***********          WRITEHEADER1                                   S01498
     C***********          WRITENULL1                                     S01498
     C                     MOVE *ON       *IN62                           S01498
     C                     ELSE                                           S01498
     C                     MOVE BBBRCD    WWBRCD
     C                     MOVE BBACOC    WWACOC
E1   C                     END
     C*
     C**   DO WHILE NOT EOF
     C*
B1   C           *IN29     DOWEQ'0'
     C*
     C**   OUTPUT HEADER
     C*
     C                     EXSR #YA
     C*
     C**   DO WHILE BRANCH CODE & ACCOUNT OFFICER HAVE NOT CHANGED
     C**   AND NOT EOF
     C*
B2   C           BBACOC    DOWEQWWACOC
     C           BBBRCD    ANDEQWWBRCD
     C           *IN29     ANDEQ'0'
     C*
     C**   ADD 1 TO RECORDS READ COUNT
     C*
     C                     ADD  1         WWNRIP
     C*
     C**   OUTPUT REPORT
     C*
     C                     EXSR #YB
     C*
     C**   READ NEXT LIVE RECORD ON 'LBPLEGLK'
     C*
     C                     READ LBPLEGLK                 29
     C*
E2   C                     END
     C*
      ** Check for change of actual entity value.                         S01498
      *                                                                   S01498
     C           *IN65     IFEQ '0'                                       S01498
     C           BBBRCD    ANDNE@RENT                                     S01498
     C                     MOVE *ON       *IN29                           S01498
     C                     END                                            S01498
E1   C                     END
     C*
     C**   IF OVERFLOW TAKE NEW PAGE
     C*
B1   C           LINE      IFGT 56
     C           *IN61     ANDEQ*ON                                       S01498
     C*
     C***********          EXSR #YA                                       S01498
     C                     WRITEHEADER1                                   S01498
     C*
E1   C                     END
     C*
     C**   WRITE REPORT FOOTING
     C*
     C           *IN61     IFEQ *ON                                       S01498
     C           @RENT     ANDNE'   '                                     S01498
     C                     WRITEFOOTING1
     C                     CLOSELB0530P1                                  S01498
     C                     MOVE *OFF      *IN61                           S01498
     C                     END                                            S01498
     C*
     C           #BA0      ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #BB       - PROCESS SIGNED AGREEMENTS                      *
     C**                                                              *
     C**   CALLS     - #YA , #YB , #ZB                                *
     C**                                                              *
     C**   CALLED BY - #B                                             *
     C**                                                              *
     C*****************************************************************
     C*
     C           #BB       BEGSR
     C*
     C**   SET UP SELECTION FIELDS
     C*
     C                     EXSR #BBA
     C                     MOVE '1'       *IN50
     C                     MOVE '0'       *IN40
     C                     Z-ADD*ZEROS    PAGE
     C*
     C**   READ FIRST LIVE RECORD ON 'LBPLEGLL'
     C*
      ** Check for an actual Branch Code                                  S01498
      *                                                                   S01498
     C           *IN65     IFEQ *ON                                       S01498
     C           KBRCD     SETLLLBPLEGLL                                  S01498
     C                     READ LBPLEGLL                 30               S01498
      *                                                                   S01498
      ** Check Branch Code record found.                                  S01498
     C           @RENT     IFNE BBBRCD
     C                     MOVE *ON       *IN30
     C                     ENDIF
      *                                                                   S01498
     C                     ELSE                                           S01498
     C           *LOVAL    SETLLLBPLEGLL
     C                     READ LBPLEGLL                 30
     C                     ENDIF                                          S01498
      *                                                                   S01498
     C*
     C**   IF NO DETAILS SELECTED WRITE HEADER
     C*
B1   C           *IN30     IFEQ '1'
     C                     MOVE *BLANKS   RRBRCD
     C                     MOVE *BLANKS   RRBRCA                          S01498
     C                     MOVE *BLANKS   RRBRNM                          S01498
     C                     MOVE *BLANKS   RRACOC
     C                     MOVE *BLANKS   RRACON
     C                     ELSE                                           S01498
     C                     MOVE BBBRCD    WWBRCD
     C                     MOVE BBACOC    WWACOC
     C                     EXSR #YA                                       S01498
E1   C                     END
     C*
     C**   DO WHILE NOT EOF
     C*
B1   C           *IN30     DOWEQ'0'
     C*
     C**   ADD 1 TO RECORDS READ COUNT
     C*
     C                     ADD  1         WWNRIP
     C*
     C**   FROM-DATE <= AGREEMENT-DATE <= TO-DATE
     C*
B2   C           PGAGRD    IFGE WWFDAT
     C           PGAGRD    ANDLEWWTDAT
     C*
     C**   IF BRANCH CODE OR ACCOUNT OFFICER HAVE CHANGED
     C**   - OUTPUT HEADER
     C*
B3   C           BBACOC    IFNE WWACOC
     C           BBBRCD    ORNE WWBRCD
     C*
     C                     MOVE *IN30     *IN29                           S01498
     C                     EXSR #YA
     C*
E3   C                     END
     C*
     C**   OUTPUT REPORT
     C*
     C                     EXSR #YB
     C*
     C**   SET ON RECORDS SELECTED INDICATOR
     C*
     C                     MOVE '1'       *IN40
     C*
E2   C                     END
     C*
     C**   READ NEXT LIVE RECORD ON 'LBPLEGLK'
     C*
      ** Check for change of actual entity value.                         S01498
      *                                                                   S01498
     C           *IN65     IFEQ *ON                                       S01498
     C           BBBRCD    ANDNE@RENT                                     S01498
     C                     MOVE *ON       *IN30                           S01498
E1   C                     ELSE                                           S01498
     C                     READ LBPLEGLL                 30
     C                     END                                            S01498
     C*
E1   C                     END
     C*
     C**   IF OVERFLOW TAKE NEW PAGE
     C*
B1   C           LINE      IFGT 56
     C           *IN61     ANDEQ*ON                                       S01498
     C*
     C***********          EXSR #YA                                       S01498
     C                     WRITEHEADER1                                   S01498
     C*
E1   C                     END
     C*
     C**   IF NO RECORDS SELECTED - OUTPUT NULL REPORT
     C*
B1   C           *IN40     IFEQ '0'
     C*
     C***********          WRITEHEADER1                                   S01498
     C***********          WRITENULL1                                     S01498
     C                     MOVE *ON       *IN62                           S01498
     C*
E1   C                     END
     C*
     C**   WRITE REPORT FOOTING
     C*
     C           *IN61     IFEQ *ON                                       S01498
     C                     WRITEFOOTING1
     C                     ENDIF                                          S01498
     C*
     C           #BB0      ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #BBA      - SET UP SELECTION FIELDS - SIGNED AGREEMENTS    *
     C**                                                              *
     C**   CALLS     - ZDATE2                                         *
     C**                                                              *
     C**   CALLED BY - #BB                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           #BBA      BEGSR
     C*
     C**   SET UP FROM-DATE ON REPORT
     C*
B1   C           WWFDAT    IFEQ 00000
     C*
     C**   SET FIELD TO ALL '0'S
     C*
     C                     MOVE *ALL'0'   RRFDAT
     C*
X1   C                     ELSE
     C*
     C**   CONVERT THE FROM-DATE TO DDMMMYY FORMAT USING ZDATE2
     C*
     C                     Z-ADDWWFDAT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RRFDAT
     C*
E1   C                     END
     C*
     C**   SET UP TO-DATE ON REPORT
     C*
B1   C           WWTDAT    IFEQ 99999
     C*
     C**   SET FIELD TO ALL '9'S
     C*
     C                     MOVE *ALL'9'   RRTDAT
     C*
X1   C                     ELSE
     C*
     C**   CONVERT THE TO-DATE TO DDMMMYY FORMAT USING ZDATE2
     C*
     C                     Z-ADDWWTDAT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RRTDAT
     C*
E1   C                     END
     C*
     C           #BBA0     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #BC       - TERMINATION PROCESSING                         *
     C**                                                              *
     C**   CALLS     - #ZA                                            *
     C**                                                              *
     C**   CALLED BY - B                                              *
     C**                                                              *
     C*****************************************************************
     C*
     C           #BC       BEGSR
     C*
     C**   ACCESS TRAILER FILE TO GET NUMBER OF LIVE RECORDS
     C*
     C                     MOVE 'LBPLEGPD'WWFILE  8
     C           WWFILE    CHAINLBTRAIPD             23
     C*
     C**   IF RECORD NOT FOUND OR RECORD-ID NOT = 'T'
     C*
B1   C           *IN23     IFEQ '1'
     C           TRRECI    ORNE 'T'
     C           *LOCK     IN   LDA                                       S01498
     C                     Z-ADD2         DBASE            ****************
     C                     MOVE *BLANKS   DBFILE           *              *
     C***********          MOVEL'LBTRAIPD'DBFILE           * DBERROR  002 S01498
     C                     MOVE 'LBTRAIPD'DBFILE           *              S01498
     C                     MOVE *BLANKS   DBKEY            *              *
     C                     MOVELWWFILE    DBKEY            ****************
     C                     OUT  LDA                                       S01498
     C                     EXSR #ZA
E1   C                     END
     C*
     C**   NO OF LIVE RECORDS ON TRAILER FILE
     C*
     C                     Z-ADDTRLIVE    RRNROF
     C*
     C**   NO OF LIVE RECORDS FROM PROGRAM
     C*
     C                     Z-ADDWWNRIP    RRNRIP
     C*
     C**   CHECK DIFFENCE
     C*
B1   C           WWNRIP    IFNE TRLIVE
     C*
     C                     MOVE '1'       *IN60
     C                     MOVE '1'       *INU8
     C*
E1   C                     END
     C*
     C**   WRITE RUN CONTROL HEADER
     C*
     C                     WRITEHDRCTRL1
     C*
     C**   WRITE RUN CONTROL DETAIL
     C*
     C                     WRITEDTRCTRL1
     C*
     C**   WRITE RUN CONTROL FOOTING
     C*
     C***********          WRITEFTRCTRL1                                  S01498
     C*
      **  Report if no details found.                                     S01498
     C           *IN62     IFEQ *ON                                       S01498
     C                     WRITENODTLS                                    S01498
     C                     MOVE *OFF      *IN62                           S01498
     C                     END                                            S01498
      *                                                                   S01498
      ** Ensure Audit Spool File recorded by RCF                          S01498
      *                                                                   S01498
     C                     Z-ADDSFNUMU    ZSNUM2  60                      S01498
     C                     CALL 'ZSFILE'                                  S01498
     C                     PARM           SEQ     5                       S01498
     C                     PARM *BLANK    @PARM   3                       S01498
     C                     PARM           SFILEU                          S01498
     C                     PARM           ZSNUM2                          S01498
     C                     PARM           ZSERR                           S01498
     C           *LOCK     IN   LDA                                       S01498
      *                                                                   S01498
      ** If an error occurred during ZSFILE processing,                   S01498
      ** Return to the calling program.                                   S01498
      *                                                                   S01498
     C           ZSERR     IFEQ 'Y'                                       S01498
     C                     SETON                     U7U8LR               S01498
     C                     RETRN                                          S01498
     C                     END                                            S01498
      *                                                                   S01498
     C                     Z-ADD*ZEROS    WWNRIP
     C*
     C           #BC0      ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #YA       - WRITE HEADING FORMAT                           *
     C**                                                              *
     C**   CALLS     - NONE                                           *
     C**                                                              *
     C**   CALLED BY - #BA , #BB , #YB                                *
     C**                                                              *
     C*****************************************************************
     C*
     C           #YA       BEGSR
      *                                                                   S01498
      ** Check for change of Report Level.                                S01498
      *                                                                   S01498
     C           @RENT     IFNE '   '                                     S01498
      *                                                                   S01498
     C           BBBRCD    IFNE WWBRCD                                    S01498
     C           *IN65     ANDEQ*OFF                                      S01498
     C                     WRITEFOOTING1                                  S01498
     C                     CLOSELB0530P1                                  S01498
     C                     Z-ADD*ZEROS    PAGE                            S01498
     C                     MOVE *OFF      *IN61                           S01498
     C                     END                                            S01498
     C                     END                                            S01498
     C*
     C**   SAVE ACCOUNT OFFICER AND BRANCH CODE
     C*
      ** Setup header information                                         S01498
      *                                                                   S01498
      **  Access SDBRCHPD using branch code                               S01498
     C**********           CALL 'AOBRCHR0'                                S01498              CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG   ' @RTCD                           S01498
     C                     PARM '*KEY   ' @OPTN                           S01498
     C                     PARM BBBRCD    @BRCD   3                       S01498
     C********** SDBRCH    PARM SDBRCH    DSFDY                           S01498              CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *                                                                   S01498
      ** Branch record not found - Error                                  S01498
     C           @RTCD     IFNE *BLANKS                                   S01498
     C           *LOCK     IN   LDA                                       S01498
     C                     Z-ADD6         DBASE            ***************S01498
     C                     MOVE *BLANKS   DBFILE           *              S01498
     C                     MOVE 'SDBRCHPD'DBFILE           * DBERROR 006 *S01498
     C                     MOVE *BLANKS   DBKEY            *              S01498
     C                     MOVELBBBRCD    DBKEY            ***************S01498
     C                     OUT  LDA                                       S01498
     C                     EXSR #ZA                                       S01498
     C                     END                                            S01498
     C                     MOVE BBBRCD    WWBRCD
     C                     MOVE BBACOC    WWACOC
     C*
     C**   SET UP REPORT HEADER
     C*
     C                     MOVE BBBRCD    RRBRCD
     C                     MOVE BBBRCD    RRBRCA                          S01498
     C                     MOVE A8BRNM    RRBRNM                          S01498
     C*
     C                     MOVE *BLANKS   RRACOC
     C                     MOVE *BLANKS   RRACON
     C*
     C**   IF NO ACCOUNT OFFICER - DONT GET NAME
     C*
B1   C           BBACOC    IFNE *BLANKS
     C*
     C                     MOVE BBACOC    RRACOC
     C*
     C**   ACCESS THE ACCOUNT OFFICERS FILE TO GET ACCOUNT OFFICER NAME
     C*
     C***********BBACOC    CHAINSDACOFL0             26                   S01498
     C*
     C**   IF RECORD NOT FOUND OR TYPE-OF-LAST-CHANGE - 'D' (DELETED)
     C*
B2   C************IN26     IFEQ '1'                                       S01498
     C***********AZTYLC    OREQ 'D'                                       S01498
      *                                                                   S01498
      ** Access Account Officer File.                                     S01498
      *                                                                   S01498
     C                     CALL 'AOACOFR0'                                S01498
     C                     PARM *BLANK    @RTCD                           S01498
     C                     PARM '*KEY    '@OPTN                           S01498
     C                     PARM           BBACOC                          S01498
     C           SDACOF    PARM SDACOF    DSFDY                           S01498
     C*                                                                   S01498
     C           @RTCD     IFNE *BLANKS                                   S01498
     C           *LOCK     IN   LDA                                       S01498
     C                     Z-ADD3         DBASE            ****************
     C                     MOVE *BLANKS   DBFILE           *              *
     C***********          MOVEL'SDACOFL0'DBFILE           * DBERROR  003 S01498
     C                     MOVE 'SDACOFL0'DBFILE           *              S01498
     C                     MOVE *BLANKS   DBKEY            *              *
     C                     MOVELBBACOC    DBKEY            ****************
     C                     OUT  LDA                                       S01498
     C                     EXSR #ZA
E2   C                     END
     C*
     C                     MOVE AZACON    RRACON
     C*
E1   C                     END
     C*
      **  OPEN printer file LB0530P1.                                     S01498
      *                                                                   S01498
     C           *IN61     IFEQ *OFF                                      S01498
     C                     OPEN LB0530P1                                  S01498
     C                     MOVE *ON       *IN61                           S01498
     C***********          END                                      S01498093962
      *                                                                   S01498
      **  Ensure correct Entity passed to ZSFILE.                         S01498
      *                                                                   S01498
     C           @RENT     IFNE '   '                                     S01498
     C                     MOVE BBBRCD    SENTY                           S01498
     C                     ELSE                                           S01498
     C                     MOVE *BLANKS   SENTY                           S01498
     C                     END                                            S01498
      *                                                                   S01498
      ** Ensure Report Spool File recorded by RCF                         S01498
      *                                                                   S01498
     C                     Z-ADDSFNUM     ZSNUM1  60                      S01498
     C                     CALL 'ZSFILE'                                  S01498
     C                     PARM           SEQ                             S01498
     C                     PARM           SENTY   3                       S01498
     C                     PARM           SFILE                           S01498
     C                     PARM           ZSNUM1                          S01498
     C                     PARM           ZSERR   1                       S01498
     C           *LOCK     IN   LDA                                       S01498
      *                                                                   S01498
      ** If an error occurred during ZSFILE processing,                   S01498
      ** Return to the calling program.                                   S01498
      *                                                                   S01498
     C           ZSERR     IFEQ 'Y'                                       S01498
     C                     SETON                     U7U8LR               S01498
     C                     RETRN                                          S01498
     C                     END                                            S01498
     C                     WRITEHEADER1
     C                     END                                            093962
     C*
     C           #YA0      ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #YB       - WRITE DETAIL FORMAT                            *
     C**                                                              *
     C**   CALLS     - #YA , #ZA , ZDATE2 , ZSEDIT                    *
     C**                                                              *
     C**   CALLED BY - #BA , #BB                                      *
     C**                                                              *
     C*****************************************************************
     C*
     C           #YB       BEGSR
     C*
     C**   IF OVERFLOW TAKE NEW PAGE & WRITE CUSTOMER FORMAT
     C*
B1   C           LINE      IFGT 56
     C*
     C***********          EXSR #YA                                       S01498
     C                     WRITEHEADER1                                   S01498
     C*
     C***********          Z-ADDBBCUST    WWCUST                                              CSD027
     C                     MOVE BBCUST    WWCUST                                              CSD027
     C*
     C                     MOVE BBCUST    RRCUST
     C                     MOVE BBCRNM    RRCRNM
     C*
     C                     WRITEDETAIL1
     C*
E1   C                     END
     C*
     C**   IF CUSTOMER HAS CHANGED - WRITE CUSTOMER FORMAT
     C*
B1   C           BBCUST    IFNE WWCUST
     C*
     C***********          Z-ADDBBCUST    WWCUST                                              CSD027
     C                     MOVE BBCUST    WWCUST                                              CSD027
     C*
     C                     MOVE BBCUST    RRCUST
     C                     MOVE BBCRNM    RRCRNM
     C*
     C                     WRITEDETAIL1
     C*
E1   C                     END
     C*
     C                     MOVE PGISSB    RRISSB
     C*
     C** If no Issued, move *blank in Issued report name                  001280
     C*
     C           PGTYPE    IFEQ 'G'                                       001280
     C********** PGISSB    ANDEQ0                                                      001280 CSD027
     C           PGISSB    ANDEQ*BLANKS                                                       CSD027
     C           PGTYPE    OREQ 'C'                                       001280
     C********** PGISSB    ANDEQ0                                                      001280 CSD027
     C           PGISSB    ANDEQ*BLANKS                                                       CSD027
     C           PGTYPE    OREQ 'S'                                       001280
     C********** PGISSB    ANDEQ0                                                      001280 CSD027
     C           PGISSB    ANDEQ*BLANKS                                                       CSD027
     C           PGTYPE    OREQ 'T'                                       001280
     C********** PGISSB    ANDEQ0                                                      001280 CSD027
     C           PGISSB    ANDEQ*BLANKS                                                       CSD027
     C*
     C                     MOVE *BLANKS   RRCRNM                          001280
     C*
     C                     ELSE                                           001280
     C*
     C**   ACCESS THE CLIENT MASTER FILE TO GET REPORT NAME
     C*
     C***********          MOVE PGISSB    WWALPH  6                       093962
     C                     MOVELPGISSB    WWALPH 10                       093962
     C***********WWALPH    CHAINSDCUSTL1             28                   S01498
     C*
B1   C************IN28     IFEQ '1'                                       S01498
     C***********BBTYLC    OREQ 'D'                                       S01498
      *                                                                   S01498
      ** Access Customer Details.                                         S01498
      *                                                                   S01498
     C                     CALL 'AOCUSTR0'                                S01498
     C                     PARM '*MSG    '@RTCD                           S01498
     C                     PARM '*KEY    '@OPTN                           S01498
     C                     PARM WWALPH    @AENB  10                       S01498
     C                     PARM *BLANKS   @KYST   7                       S01498
     C           SDCUST    PARM SDCUST    DSSDY                           S01498
      *                                                                   S01498
     C           @RTCD     IFNE *BLANK                                    S01498
      *                                                                   S01498
     C           *LOCK     IN   LDA                                       S01498
     C                     Z-ADD4         DBASE            ****************
     C                     MOVE *BLANKS   DBFILE           *              *
     C***********          MOVEL'SDCUSTL1'DBFILE           * DBERROR  004 S01498
     C                     MOVE 'SDCUSTL1'DBFILE           *              S01498
     C                     MOVE *BLANKS   DBKEY            *              *
     C                     MOVELWWALPH    DBKEY            ****************
     C                     OUT  LDA                                       S01498
     C                     EXSR #ZA
E1   C                     END
     C*
     C                     MOVE SDCRNM    RRCRNM
     C*
      *                                                                   S01498
     C                     END                                            001280
     C*
     C                     MOVE PGSEQN    RRSEQN
     C                     MOVE PGTYPE    RRTYPE
     C                     MOVE PGCODE    RRCODE
     C                     MOVE PGCCY     RRCCY
     C*
     C**   SET UP TEXT DEPENDING ON TYPE
     C*
B1   C           PGTYPE    IFEQ 'C'
     C*
     C                     MOVEA'1000'    *IN,51           ON 51
     C                     Z-ADDPGCOLS    WWVALU
     C*
E1   C                     END
     C*
B1   C           PGTYPE    IFEQ 'G'
     C*
     C                     MOVEA'0100'    *IN,51           ON 52
     C                     Z-ADDPGGUAA    WWVALU
     C*
E1   C                     END
     C*
B1   C           PGTYPE    IFEQ 'P'
     C*
     C                     MOVEA'0010'    *IN,51           ON 53
     C                     Z-ADDPGPLED    WWVALU
     C*
E1   C                     END
     C*
B1   C           PGTYPE    IFEQ 'S'
     C*
     C                     MOVEA'0001'    *IN,51           ON 54
     C                     Z-ADDPGCOLS    WWVALU
     C*
E1   C                     END
     C*
     C**   ACCESS CURRENCY FILE TO GET NUMBER OF DECIMAL PLACES
     C*
     C***********PGCCY     CHAINSDCURRL1             25                   S01498
     C*
     C**   IF RECORD NOT FOUND OR TYPE-OF-LAST-CHANGE - 'D' (DELETED)
     C*
B1   C************IN25     IFEQ '1'                                       S01498
     C***********A6TYLC    OREQ 'D'                                       S01498
      *                                                                   S01498
      **  Access Currency File.                                           S01498
      *                                                                   S01498
     C                     CALL 'AOCURRR0'                                S01498
     C                     PARM '*MSG    '@RTCD                           S01498
     C                     PARM '*KEY    '@OPTN                           S01498
     C                     PARM PGCCY     @AJCD   3                       S01498
     C           SDCURR    PARM SDCURR    DSSDY                           S01498
      *                                                                   S01498
     C           @RTCD     IFNE *BLANK                                    S01498
     C           *LOCK     IN   LDA                                       S01498
     C                     Z-ADD5         DBASE            ****************
     C                     MOVE *BLANKS   DBFILE           *              *
     C***********          MOVEL'SDCURRL1'DBFILE           * DBERROR  005 S01498
     C                     MOVE 'SDCURRL1'DBFILE           *              S01498
     C                     MOVE *BLANKS   DBKEY            *              *
     C                     MOVELPGCCY     DBKEY            ****************
     C                     OUT  LDA                                       S01498
     C                     EXSR #ZA
E1   C                     END
     C*
     C**   CONVERT AMOUNT FOR OUTPUT USING ZSEDIT
     C*
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     Z-ADDWWVALU    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRVALU
     C*
     C**   CONVERT THE START DATE TO DDMMMYY FORMAT
     C*
     C                     Z-ADDPGSTTD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RRSTTD
     C*
     C**   CONVERT THE MATURITY DATE TO DDMMMYY FORMAT
     C*
     C                     Z-ADDPGMATD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RRMATD
     C*
     C**   CONVERT THE REVIEW DATE TO DDMMMYY FORMAT
     C*
     C                     Z-ADDPGREVD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RRREVD
     C*
     C                     MOVE PGREVF    RRREVF
     C                     Z-ADDPGREVN    RRREVN
     C*
     C**   IF SIGNED AGREEMENT
     C*
B1   C           *IN50     IFEQ '1'
     C*
     C**   CONVERT THE AGREEMENT DATE TO DDMMMYY FORMAT
     C*
     C                     Z-ADDPGAGRD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RRAGRD
     C*
     C**   CONVERT THE LAST CHANGE DATE TO DDMMMYY FORMAT
     C*
     C                     Z-ADDPGLCD     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RRSLCD
     C*
     C           PGCHTP    IFEQ 'I'
     C                     MOVE 'INSERT'  RRSCHT
     C                     END
     C           PGCHTP    IFEQ 'A'
     C                     MOVE 'AMEND '  RRSCHT
     C                     END
     C           PGCHTP    IFEQ 'D'
     C                     MOVE 'DELETE'  RRSCHT
     C                     END
     C*
X1   C                     ELSE
     C*
     C**   CONVERT THE LAST CHANGE DATE TO DDMMMYY FORMAT
     C*
     C                     Z-ADDPGLCD     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RRLCD
     C*
     C           PGCHTP    IFEQ 'I'
     C                     MOVE 'INSERT'  RRCHTP
     C                     END
     C           PGCHTP    IFEQ 'A'
     C                     MOVE 'AMEND '  RRCHTP
     C                     END
     C           PGCHTP    IFEQ 'D'
     C                     MOVE 'DELETE'  RRCHTP
     C                     END
     C*
E1   C                     END
     C*
     C                     MOVE PGNARR    RRNARR
     C*
     C                     WRITEDETAIL2
     C*
     C                     MOVE '0'       *IN55
     C*
     C           #YB0      ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #ZA       - PROCESS DATABASE ERRORS                        *
     C**                                                              *
     C**   CALLS     - NONE                                           *
     C**                                                              *
     C**   CALLED BY - #A , #BC , #YA , #YB                           *
     C**                                                              *
     C*****************************************************************
     C*
     C           #ZA       BEGSR
     C*
     C**   WRITE RUN CONTROL HEADER
     C*
     C                     WRITEHDRCTRL1
     C*
     C**   WRITE RUN CONTROL DBERROR FORMAT
     C*
     C                     WRITEDBRCTRL1
     C           *IN61     IFEQ *ON                                       S01498
     C                     WRITEDBERROR                                   S01498
     C                     ENDIF                                          S01498
     C                     EXSR *PSSR
     C*
     C**   WRITE RUN CONTROL FOOTING
     C*
     C***********          WRITEFTRCTRL1                                  S01498
      **  Report if no details found.                                     S01498
     C           *IN62     IFEQ '1'                                       S01498
     C                     WRITENODTLS                                    S01498
     C                     MOVE *OFF      *IN62                           S01498
     C                     END                                            S01498
      *                                                                   S01498
      ** Ensure Audit Spool File recorded by RCF                          S01498
      *                                                                   S01498
     C                     Z-ADDSFNUMU    ZSNUM2  60                      S01498
     C                     CALL 'ZSFILE'                                  S01498
     C                     PARM           SEQ     5                       S01498
     C                     PARM *BLANK    @PARM   3                       S01498
     C                     PARM           SFILEU                          S01498
     C                     PARM           ZSNUM2                          S01498
     C                     PARM           ZSERR                           S01498
     C           *LOCK     IN   LDA                                       S01498
     C                     OUT  LDA                                       S01498
      *                                                                   S01498
      ** If an error occurred during ZSFILE processing,                   S01498
      ** Return to the calling program.                                   S01498
      *                                                                   S01498
     C           ZSERR     IFEQ 'Y'                                       S01498
     C                     SETON                     U7U8LR               S01498
     C                     RETRN                                          S01498
     C                     END                                            S01498
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C           #ZA0      ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   *PSSR     - PROGRAM ERROR HANDLING ROUTINE                 *
     C**                                                              *
     C**   CALLS     -                                                *
     C**                                                              *
     C**   CALLED BY -                                                *
     C**                                                              *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C                     DUMP
     C*
     C           #PSSR0    ENDSR
     C*****************************************************************
     C/EJECT
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
