/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas LE Facility Hist Update for Retail Int.Cap.')   */
/*********************************************************************/
/*                                                                   */
/*       Midas - Customer Lending Module                             */
/*                                                                   */
/*       LEC000030A - Midas LE Facility History Update for Retail    */
/*                    Interest Capitalisation                        */
/*                                                                   */
/*       (c) Finastra International Limited 2019                     */
/*                                                                   */
/*       Last Amend No. MD052994  *CREATE    Date 25Nov19            */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD052994 - CL Facility Event not Generated for Retail       */
/*                  Account Debit Capitalised Interests              */
/*                                                                   */
/*********************************************************************/
             PGM

             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)

             COPYRIGHT  TEXT('(c) Finastra International Limited +
                          2019')

/** Global monitor message */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

             SNDPGMMSG  MSG('LEC000030A - Credit Lines Facility +
                          History Update for Retail Account Interest +
                          Capitalisation') TOMSGQ(MRUNQ)

             CHGJOB     SWS(XXXXXX00)

/** Create data area LDA */

             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)

             RTVJOBA    TYPE(&JOBTYPE)

/** Start commitment control */

             STRCMTCTL  LCKLVL(*CHG) CMTSCOPE(*JOB)
             MONMSG     MSGID(CPF8351)

/** Clear Action Files */

             CLRPFM     FILE(FACACT)
             CLRPFM     FILE(HISACT)

/** Call program to create Facility History Action Movement */

             CALL       PGM(LE000030A)

/** Database error processing */

             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                ROLLBACK
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) +
                             MSGDTA(&MEM) TOPGMQ(*EXT) +
                             TOMSGQ(MOPERQ)
                GOTO       CMDLBL(ABNOR)
             ENDDO

             GOTO       CMDLBL(END)

ABNOR:
             CHGJOB     SWS(XXXXXX11)

/** Abnormal termination - batch job */

             IF         COND(&JOBTYPE = '0') THEN(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) +
                             MSGDTA('Program LEC000030A ended +
                             abnormally - see job log') +
                             TOMSGQ(MOPERQ MRUNQ)
                MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO

END:
             ENDCMTCTL
             MONMSG     MSGID(CPF0000)

             ENDPGM
