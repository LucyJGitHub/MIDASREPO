/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas LE Fac. Exposure Upd for todays RE postings')   */
/*********************************************************************/
/*                                                                   */
/*       Midas - Customer Lending Module                             */
/*                                                                   */
/*       LEC000037 - Midas LE Facility Exposure Update for todays    */
/*                   Retail postings                                 */
/*                                                                   */
/*       (c) Finastra International Limited 2023                     */
/*                                                                   */
/*       Last Amend No. MD061267  *CREATE    Date 26Jul23            */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD061267 - Credit Lines (CLE025) fixes.                     */
/*                                                                   */
/*********************************************************************/
             PGM

             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)

             COPYRIGHT  TEXT('(c) Finastra International Limited 2023')

/** Global monitor message */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

             SNDPGMMSG  MSG('LEC000037 - Midas LE Facility Exposure Update based +
                          on RESEODPD') TOMSGQ(MRUNQ)

             CHGJOB     SWS(XXXXXX00)

/** Create data area LDA */

             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)

             RTVJOBA    TYPE(&JOBTYPE)

/** Start commitment control */

             STRCMTCTL  LCKLVL(*CHG) CMTSCOPE(*JOB)
             MONMSG     MSGID(CPF8351)

/** Call LE Facility Exposure Update based on RESEODPD */

             CALL       PGM(LE000037)

/** Database error processing */

             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                ROLLBACK
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) TOPGMQ(*EXT) +
                             TOMSGQ(MOPERQ)
                GOTO       CMDLBL(ABNOR)
             ENDDO

             GOTO       CMDLBL(END)

/** Abnormal termination */

 ABNOR:

             CHGJOB     SWS(XXXXXX11)

             IF         COND(&JOBTYPE = '0') THEN(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program LEC000037 +
                             ended abnormally - see job log') TOMSGQ(MOPERQ MRUNQ)
                MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO

/** End program */

 END:
             ENDCMTCTL
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)

             ENDPGM
