/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas LE Aggregated Facility Processing')             */
/*********************************************************************/
/*                                                                   */
/*       Midas - Customer Lending Module                             */
/*                                                                   */
/*       LEC000058 - Aggregated Facility Processing                  */
/*                                                                   */
/*       (c) Finastra International Limited 2004                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CLE134             Date 01Aug12              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CLE041  *CREATE    Date 11Oct04              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CLE134 - Past Due Call Loan Processing (Recompile)          */
/*       CLE041 - Collateralised Lending Phase 2B                    */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ)
 
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5)
             DCL        VAR(&CENT) TYPE(*CHAR) LEN(3) VALUE('ALL')
             DCL        VAR(&STAT) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(7) VALUE('  DPLIB')
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2004')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('LEC000058 - Midas LE Aggregated Facility +
                            Processing') TOMSGQ(MRUNQ)
 
             CHGJOB     SWS(00000000)
 
/* Create data area LDA */
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
 
/* Setup system prefix variable from SDSTAT data area. */
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(%SUBSTRING(&DPLIB 1 2)) VALUE(&SYSID)
 
/* Call program CB0160 to get value of restart indicator. */
 
             CALL       PGM(CB0160) PARM(&CNAM &CSEQ &STAT)
 
/* If it is NOT a restart. */
 
             IF         COND(&STAT *NE 'Y') THEN(DO)
                DLTF       FILE(XLEAGNHPD)
                MONMSG     MSGID(CPF2105)
                DLTF       FILE(XLEAGNMPD)
                MONMSG     MSGID(CPF2105)
                DLTF       FILE(XLEAGNNPD)
                MONMSG     MSGID(CPF2105)
                DLTF       FILE(XLEAGNZPD)
                MONMSG     MSGID(CPF2105)
 
                CPYF       FROMFILE(LEAGNHPD) TOFILE(&DPLIB/XLEAGNHPD) +
                             MBROPT(*REPLACE) CRTFILE(*YES)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                             EXEC(CLRPFM FILE(XLEAGNHPD))
                CPYF       FROMFILE(LEAGNMPD) TOFILE(&DPLIB/XLEAGNMPD) +
                             MBROPT(*REPLACE) CRTFILE(*YES)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                             EXEC(CLRPFM FILE(XLEAGNMPD))
                CPYF       FROMFILE(LEAGNNPD) TOFILE(&DPLIB/XLEAGNNPD) +
                             MBROPT(*REPLACE) CRTFILE(*YES)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                             EXEC(CLRPFM FILE(XLEAGNNPD))
                CPYF       FROMFILE(LEAGNZPD) TOFILE(&DPLIB/XLEAGNZPD) +
                             MBROPT(*REPLACE) CRTFILE(*YES)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                             EXEC(CLRPFM FILE(XLEAGNZPD))
             ENDDO
 
/* If it is a restart. */
 
             IF         COND(&STAT *EQ 'Y') THEN(DO)
                CPYF       FROMFILE(XLEAGNHPD) TOFILE(LEAGNHPD) +
                             MBROPT(*REPLACE) CRTFILE(*YES)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                             EXEC(CLRPFM FILE(LEAGNHPD))
                CPYF       FROMFILE(XLEAGNMPD) TOFILE(LEAGNMPD) +
                             MBROPT(*REPLACE) CRTFILE(*YES)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                             EXEC(CLRPFM FILE(LEAGNMPD))
                CPYF       FROMFILE(XLEAGNNPD) TOFILE(LEAGNNPD) +
                             MBROPT(*REPLACE) CRTFILE(*YES)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                             EXEC(CLRPFM FILE(LEAGNNPD))
                CPYF       FROMFILE(XLEAGNZPD) TOFILE(LEAGNZPD) +
                             MBROPT(*REPLACE) CRTFILE(*YES)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                             EXEC(CLRPFM FILE(LEAGNZPD))
             ENDDO
 
             CHGVAR     VAR(&STAT) VALUE('Y')
 
/* Call CB0150 to update restart indicator */
 
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)
 
/* Call Aggregated Facility Processing Program */
 
             CLRPFM     FILE(LEAGNHPD)
             CLRPFM     FILE(LEAGNMPD)
             CLRPFM     FILE(LEAGNNPD)
             CLRPFM     FILE(LEAGNZPD)
             CALL       PGM(LE000058)
 
/* If no error occured in the program */
 
             IF         COND(%SWITCH(XXXXXX00)) THEN(DO)
 
/* Call CB0150 to update restart indicator. */
 
                CHGVAR     VAR(&STAT) VALUE('N')
                CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)
 
/* Delete back up files */
                DLTF       FILE(XLEAGNHPD)
                MONMSG     MSGID(CPF2105)
                DLTF       FILE(XLEAGNMPD)
                MONMSG     MSGID(CPF2105)
                DLTF       FILE(XLEAGNNPD)
                MONMSG     MSGID(CPF2105)
                DLTF       FILE(XLEAGNZPD)
                MONMSG     MSGID(CPF2105)
                DLTF       FILE(XLEAGNZPD)
                MONMSG     MSGID(CPF2105)
 
/* Reorganize files */
                RGZPFM     FILE(LEAGFTQD)
                MONMSG     MSGID(CPF2995 CPF2981)
 
/* Replace records in Old files with record in New Files */
                CPYF       FROMFILE(LEAGNHPD) TOFILE(LEAGHHPD) +
                             MBROPT(*REPLACE) CRTFILE(*YES)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                             EXEC(CLRPFM FILE(LEAGHHPD))
                CPYF       FROMFILE(LEAGNMPD) TOFILE(LEAGFMPD) +
                             MBROPT(*REPLACE) CRTFILE(*YES)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                             EXEC(CLRPFM FILE(LEAGFMPD))
                CPYF       FROMFILE(LEAGNNPD) TOFILE(LEAGFNPD) +
                             MBROPT(*REPLACE) CRTFILE(*YES)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                             EXEC(CLRPFM FILE(LEAGFNPD))
                CPYF       FROMFILE(LEAGNZPD) TOFILE(LEAGFZPD) +
                             MBROPT(*REPLACE) CRTFILE(*YES)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                             EXEC(CLRPFM FILE(LEAGFZPD))
 
/* Clear temporary files */
                CLRPFM     FILE(LEAGNHPD)
                CLRPFM     FILE(LEAGNMPD)
                CLRPFM     FILE(LEAGNNPD)
                CLRPFM     FILE(LEAGNZPD)
 
             ENDDO
 
/* Database error processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ MRUNQ)
             ENDDO
 
             GOTO       CMDLBL(END)
 
ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSG('LEC000058 Midas LE Aggregated Facility +
                          Processing Ended +
                          Abnormally') TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
END:
             ENDPGM
