/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI    TEXT('Midas LE Loans Amortisation A/C Key Generation')      */
/*********************************************************************/
/*                                                                   */
/*       Midas - Customer Lending Module                             */
/*                                                                   */
/*       LEC004600 - Midas LE Effective Interest Rate Period Split   */
/*                                                                   */
/*       (c) Finastra International Limited 2004                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CLE134             Date 01Aug12              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*                      CAS019             Date 24Mar07              */
/*                      CAS016             Date 28Feb06              */
/*                      232970             Date 08Apr05              */
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/* Midas Release 4.04 -----------------------------------------------*/
/*                      CAS009  *CREATE    Date 04May04              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CLE134 - Past Due Call Loan Processing (Recompile)          */
/*       CAS019 - Upgrade of CAS016 to Midas Plus (Recompile)        */
/*       CAS016 - IAS18 EIR Recalculation (Fee Amortisation Over     */
/*                Whole Period)                                      */
/*       232970 - RGZPFM has changed at R5V3                         */
/*       CAS009 - Effective Interest Rate/Amortised Cost Accounting  */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ)
 
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5)
             DCL        VAR(&STAT) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(7) VALUE('  DPLIB')
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RTCD) TYPE(*CHAR) LEN(7)                                     /*CAS016*/
             DCL        VAR(&OPTN) TYPE(*CHAR) LEN(7)                                     /*CAS016*/
             DCL        VAR(&SARD) TYPE(*CHAR) LEN(6)                                     /*CAS016*/
             DCL        VAR(&SCSARD) TYPE(*CHAR) LEN(200)                                 /*CAS016*/
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited +
                          2004')
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2004')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('LEC004600 - Midas LE Customer Lending +
                          Effective Interest Rate Period Split') +
                          TOMSGQ(MRUNQ)
 
             CHGJOB     SWS(00000000)
 
/* Create data area LDA */
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
 
/**/                                                                                      /*CAS016*/
/* Check if CAS016 is enabled. */                                                         /*CAS016*/
/**/                                                                                      /*CAS016*/
             CHGVAR     VAR(&RTCD) VALUE('       ')                                       /*CAS016*/
             CHGVAR     VAR(&SARD) VALUE('CAS016')                                        /*CAS016*/
             CHGVAR     VAR(&OPTN) VALUE('*VERIFY')                                       /*CAS016*/
                                                                                          /*CAS016*/
             CALL       PGM(AOSARDR0) PARM(&RTCD &OPTN &SARD &SCSARD)                     /*CAS016*/
                                                                                          /*CAS016*/
             IF         COND(&RTCD *EQ '       ') THEN(GOTO +
                          CMDLBL(END))                                                    /*CAS016*/
 
/* Setup system prefix variable from SDSTAT data area. */
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(%SUBSTRING(&DPLIB 1 2)) VALUE(&SYSID)
 
/* Call program CB0160 to get value of restart indicator. */
 
             CALL       PGM(CB0160) PARM(&CNAM &CSEQ &STAT)
 
/* If it is NOT a restart. */
 
             IF         COND(&STAT *NE 'Y') THEN(DO)
 
               DLTF       FILE(XLEEIRHPD)
               MONMSG     MSGID(CPF2105)
               CPYF       FROMFILE(CLOANCL) TOFILE(&DPLIB/XCLOANCL) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
               MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
               CPYF       FROMFILE(LEEIRDPD) TOFILE(XLEEIRDPD) +
                          MBROPT(*REPLACE)
               MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(XLEEIRDPD))
               CPYF       FROMFILE(LEEIRAPD) TOFILE(XLEEIRAPD) +
                          MBROPT(*REPLACE)
               MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(XLEEIRAPD))
               CPYF       FROMFILE(LEEIRHPD) TOFILE(&DPLIB/XLEEIRHPD) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
               MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
               CPYF       FROMFILE(LEFEED) TOFILE(&DPLIB/XLEFEED) +
                          MBROPT(*REPLACE)
               MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
             ENDDO
 
/* If it is a restart. */
 
             IF         COND(&STAT *EQ 'Y') THEN(DO)
               CPYF       FROMFILE(XCLOANCL) TOFILE(CLOANCL) +
                          MBROPT(*REPLACE)
               MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
               CPYF       FROMFILE(XLEEIRDPD) TOFILE(LEEIRDPD) +
                          MBROPT(*REPLACE)
               MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(LEEIRDPD))
               CPYF       FROMFILE(XLEEIRAPD) TOFILE(LEEIRAPD) +
                          MBROPT(*REPLACE)
               MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(LEEIRAPD))
               CPYF       FROMFILE(XLEEIRHPD) TOFILE(LEEIRHPD) +
                          MBROPT(*REPLACE)
               MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(LEEIRHPD))
               CPYF       FROMFILE(XLEFEED) TOFILE(LEFEED) +
                          MBROPT(*REPLACE)
               MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
             ENDDO
 
             CHGVAR     VAR(&STAT) VALUE('Y')
 
/* Call CB0150 to update restart indicator */
 
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)
 
/* Call Lending EIR Period Split Program */
 
             CALL       PGM(LE004600)
 
/* If no error occured in the program */
 
             IF         COND(%SWITCH(XXXXXX00)) THEN(DO)
 
/* Call CB0150 to update restart indicator. */
 
               CHGVAR     VAR(&STAT) VALUE('N')
               CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)
 
               DLTF       FILE(XLEEIRHPD)
               MONMSG     MSGID(CPF2105)
/**********    RGZPFM     FILE(LEEIRDPD)                                                  /*232970*/
/**********    MONMSG     MSGID(CPF2995 CPF2981)                                          /*232970*/
               CALL       PGM(SCC000060) PARM('LEEIRDPD' '*FIRST')                        /*232970*/
               IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))                                                  /*232970*/
                                                                                          /*232970*/
/**********    RGZPFM     FILE(LEEIRAPD)                                                  /*232970*/
/**********    MONMSG     MSGID(CPF2995 CPF2981)                                          /*232970*/
               CALL       PGM(SCC000060) PARM('LEEIRAPD' '*FIRST')                        /*232970*/
               IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))                                                  /*232970*/
                                                                                          /*232970*/
/**********    RGZPFM     FILE(LEEIRHPD)                                                  /*232970*/
/**********    MONMSG     MSGID(CPF2995 CPF2981)                                          /*232970*/
               CALL       PGM(SCC000060) PARM('LEEIRHPD' '*FIRST')                        /*232970*/
               IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))                                                  /*232970*/
                                                                                          /*232970*/
             ENDDO
 
/* Database error processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOMSGQ(MOPERQ MRUNQ)
             ENDDO
 
             GOTO       CMDLBL(END)
 
ABNOR:
             CHGJOB     SWS(XXXXXX11)
             DMPCLPGM
             SNDPGMMSG  MSG('LEC004600 Midas LE Effective +
                          Interest Rate Period Split +
                          ended abnormally - see job log') +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
END:
             ENDPGM
