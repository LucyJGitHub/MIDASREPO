/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI    TEXT('Midas LE Dropped Participant Contribution History')   */
/*********************************************************************/
/*                                                                   */
/*     Midas Standing Data Module                                    */
/*                                                                   */
/*     LEC0464   - Drop Participant's Contribution History Records   */
/*                                                                   */
/*       (c) Finastra International Limited 2005                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CLE134             Date 01Aug12              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*                      BUG15159           Date 26Oct07              */
/*                      BUG13152           Date 25Jan07              */
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CLE106  *CREATE    Date 01Aug04              */
/*                                                                   */
/*********************************************************************/
/*       MD046248 - Finastra Rebranding                              */
/*       CLE134 - Past Due Call Loan Processing (Recompile)          */
/*       BUG15159 - Attempt to issue CLRPFM to XLEPCHSPD which is    */
/*                  not found.  Applied fix BUG1407                  */
/*       BUG13152 - Pointer not set for location referenced          */
/*       CLE106 - Syndications Manager                               */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ)
 
/* Parameters declaration */
 
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5 0)
 
/* Variables Declaration */
 
             DCL        VAR(&BUSY) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&TOLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited +
                          2005')
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2005')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('LEC0464 - Drop Participants History  +
                          Records ') TOMSGQ(MRUNQ MOPERQ) +
                          MSGTYPE(*INFO)
 
             RTVJOBA    TYPE(&JOBTYPE)
             CHGJOB     SWS(XXXXXX00)
 
             CALL       PGM(CB0160) PARM(&CNAM &CSEQ &BUSY)
 
/*   If Busy is 'Y', which is equal to the value of the restart  */
/*   indicator.                                                  */
 
             IF         COND(&BUSY *EQ 'Y') THEN(DO)
 
 
             CPYF       FROMFILE(XLEPCHSPD) TOFILE(LEPCHSPD) +
                          MBROPT(*REPLACE)
/**********  MONMSG     MSGID(CPF2817)                      */                          /*BUG15159*/
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                                  /*BUG15159*/
 
             ENDDO
 
             ELSE       CMD(DO)
 
/*        Create Security copies of Updated files */
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&PRE)
             CHGVAR     VAR(&TOLIB) VALUE(&PRE *TCAT 'DPLIB')
 
             CPYF       FROMFILE(LEPCHSPD) TOFILE(&TOLIB/XLEPCHSPD) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
/**********  MONMSG     MSGID(CPF2817)                      */                          /*BUG15159*/
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                                  /*BUG15159*/
 
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ 'Y')
 
             ENDDO
 
/**********  CALL       PGM(LEC0465) */                                                 /*BUG13152*/
             CALL       PGM(LE0464)                                                     /*BUG13152*/
 
/*   If Switches 7 and 8 are on,                    */
/*   Retrieve Error Narrative from LDA and send a   */
/*   MEM0001 message from the message File MIDAS to */
/*   Message Queues MOPERQ and MRUNQ.               */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
 
             RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
 
             SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                          TOMSGQ(MOPERQ MRUNQ) MSGTYPE(*INFO)
             MONMSG     MSGID(CPF2469)
 
             ENDDO
 
/*   If Switches 7 and 8 are off */
 
             ELSE       CMD(DO)
 
/*   Call PGM CB0150 with parameters 'Component Name',   */
/*   'Component Seq. No.' and Busy = 'N'.                */
/*   Clear Security copies XLEPCHSPD                     */
 
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ 'N')
 
/* Clear security copies */
 
/**********  CLRPFM     FILE(XLEPCHSPD)                     */                          /*BUG15159*/
             DLTF       FILE(XLEPCHSPD)                                                 /*BUG15159*/
             MONMSG     MSGID(CPF2105)                                                  /*BUG15159*/
 
             ENDDO
 
             GOTO       CMDLBL(END)
ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
 
/* Abnormal termination - batch job */
 
             IF         COND(&JOBTYPE = '0') THEN(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                             LEC0464 ended abnormally - see job log') +
                             TOMSGQ(MOPERQ)
                MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO
 
END:
 
             ENDPGM
