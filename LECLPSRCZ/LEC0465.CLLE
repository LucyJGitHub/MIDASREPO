/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas LE Lending Trans. Ref. File Housekeeping')      */
/*********************************************************************/
/*                                                                   */
/*     Midas Standing Data Module                                    */
/*                                                                   */
/*     LEC0464   - Drop Lending Transaction Reference Records        */
/*                                                                   */
/*       (c) Finastra International Limited 2005                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CLE106  *CREATE    Date 01Aug04              */
/*                                                                   */
/*********************************************************************/
/*       MD046248 - Finastra Rebranding                              */
/*       CLE106 - Syndications Manager                               */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ)
 
/* Parameters declaration */
 
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5 0)
 
/* Variables Declaration */
 
             DCL        VAR(&BUSY) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&TOLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited +
                          2005')
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2005')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('LEC0465 - Drop Lending Transaction +
                          Reference   Records ') TOMSGQ(MRUNQ +
                          MOPERQ) MSGTYPE(*INFO)
 
             RTVJOBA    TYPE(&JOBTYPE)
             CHGJOB     SWS(XXXXXX00)
 
             CALL       PGM(CB0160) PARM(&CNAM &CSEQ &BUSY)
 
/*   If Busy is 'Y', which is equal to the value of the restart  */
/*   indicator.                                                  */
 
             IF         COND(&BUSY *EQ 'Y') THEN(DO)
 
 
             CPYF       FROMFILE(XLETREFPD) TOFILE(LETREFPD) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817)
 
 
             ENDDO
 
             ELSE       CMD(DO)
 
/*        Create Security copies of Updated files */
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&PRE)
             CHGVAR     VAR(&TOLIB) VALUE(&PRE *TCAT 'DPLIB')
 
             CPYF       FROMFILE(LETREFPD) TOFILE(&TOLIB/XLETREFPD) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF2817)
 
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ 'Y')
 
             ENDDO
 
             CALL       PGM(LE0465)
 
/*   If Switches 7 and 8 are on,                    */
/*   Retrieve Error Narrative from LDA and send a   */
/*   MEM0001 message from the message File MIDAS to */
/*   Message Queues MOPERQ and MRUNQ.               */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
 
             RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
 
             SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                          TOMSGQ(MOPERQ MRUNQ) MSGTYPE(*INFO)
             MONMSG     MSGID(CPF2469)
 
             ENDDO
 
/*   If Switches 7 and 8 are off */
 
             ELSE       CMD(DO)
 
/*   Call PGM CB0150 with parameters 'Component Name',   */
/*   'Component Seq. No.' and Busy = 'N'.                */
/*   Clear Security copies XLETREFPD                     */
 
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ 'N')
 
/* Clear security copies */
 
             CLRPFM     FILE(XLETREFPD)
 
             ENDDO
 
             GOTO       CMDLBL(END)
ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
 
/* Abnormal termination - batch job */
 
             IF         COND(&JOBTYPE = '0') THEN(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                             LEC0465 ended abnormally - see job log') +
                             TOMSGQ(MOPERQ)
                MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO
 
END:
 
             ENDPGM
