/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas LE Monthly Hist Proc/Rprtg-start Task Split')   */
/*********************************************************************/
/*                                                                   */
/*       Midas - Customer Lending Module                             */
/*                                                                   */
/*       LEC1001T1  - LE Monthly History Processing and Reporting    */
/*                  - Start Task Split                               */
/*                                                                   */
/*       (c) Finastra International Limited 2012                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CLE137 *CREATE     Date 06Aug12              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CLE137 - LE Monthly History processing                      */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ)
 
/* Declare file and variables */
 
             DCLF       FILE(CBCMPNLC)
 
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&JLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTAQLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NAME1) TYPE(*CHAR) LEN(5) VALUE('LEHIS')
             DCL        VAR(&NAME2_C) TYPE(*CHAR) LEN(5)
             DCL        VAR(&NAME2_N) TYPE(*DEC) LEN(5 0) VALUE(01000)
             DCL        VAR(&WKDHSEQ) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&TSK#) TYPE(*DEC) LEN(3 0) VALUE(1)
             DCL        VAR(&OWNER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CNAME) TYPE(*CHAR) LEN(10) +
                          VALUE('LEC1001')
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&CSEQA) TYPE(*CHAR) LEN(5)
 
             COPYRIGHT  TEXT('(c) Finastra International Limited +
                          2012')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Send message to MRUNQ */
 
             SNDPGMMSG  MSG('LEC1001T1 - LE Monthly Hist Proc - +
                          Start Task Split') TOMSGQ(MRUNQ)
 
/* Determine full JLIB and DTAQLIB library name */
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(&JLIB) VALUE(&SYSID *CAT 'JLIB')
             CHGVAR     VAR(&DTAQLIB) VALUE(&SYSID *CAT 'DPLIB')
 
/* Clear index file */
 
             CLRPFM     FILE(LEHISIPD)
 
/* Remove all members from driving file */
 
             RMVM       FILE(LEHISUPD) MBR(*ALL)
             MONMSG     MSGID(CPF7301 CPF7303)
 
             RMVM       FILE(LEHISTPD) MBR(*ALL)
             MONMSG     MSGID(CPF7301 CPF7303)
             RMVM       FILE(LEHIDTPD) MBR(*ALL)
             MONMSG     MSGID(CPF7301 CPF7303)
 
             CALL       PGM(QCLRDTAQ) PARM(LEHISSERVE '*LIBL')
             MONMSG     MSGID(CPF9801)
 
/* Retrieve number of sub-tasks */
 
 CHAIN:      RCVF
             CHGVAR     VAR(&WKDHSEQ) VALUE(&DHCSEQ)
             IF         COND((&DHCOTT *NE &CNAME) *OR (&WKDHSEQ *NE +
                          &CSEQ)) THEN(DO)
             GOTO       CMDLBL(CHAIN)
             ENDDO
 
/* Create member and data queue(clear if needed) for 1st task   */
 
             CHGVAR     VAR(&CSEQA) VALUE(&CSEQ)
             CHGVAR     VAR(&MEMBER) VALUE(&NAME1 *CAT &CSEQA)
             ADDPFM     FILE(LEHISUPD) MBR(&MEMBER)
             ADDPFM     FILE(LEHISTPD) MBR(&MEMBER)
             ADDPFM     FILE(LEHIDTPD) MBR(&MEMBER)
             CHKOBJ     OBJ(&DTAQLIB/&MEMBER) OBJTYPE(*DTAQ)
             MONMSG     MSGID(CPF9801) EXEC(DO)
                CRTDTAQ    DTAQ(&DTAQLIB/&MEMBER) MAXLEN(50)
                RTVOBJD    OBJ(&DTAQLIB) OBJTYPE(*LIB) OWNER(&OWNER)
                CHGOBJOWN  OBJ(&DTAQLIB/&MEMBER) OBJTYPE(*DTAQ) +
                             NEWOWN(&OWNER)
             ENDDO
             CALL       PGM(QCLRDTAQ) PARM(&MEMBER &DTAQLIB)
 
/* Create member and data queue(clear if needed) for 2nd */
/* and following tasks */
 
             CHGVAR     VAR(&TSK#) VALUE(2)
 LOOP:       IF         COND(&TSK# *GT &DHTSNO) THEN(GOTO +
                          CMDLBL(START))
 
/* Create member name based on Seq # and Task # */
             CHGVAR     VAR(&WKDHSEQ) VALUE(&DHCSEQ)
             CHGVAR     VAR(&NAME2_N) VALUE(01000)
             CHGVAR     VAR(&NAME2_N) VALUE((&NAME2_N * &WKDHSEQ) + +
                          &WKDHSEQ + &TSK# - 2)
             CHGVAR     VAR(&NAME2_C) VALUE(&NAME2_N)
             CHGVAR     VAR(&MEMBER) VALUE(&NAME1 *CAT &NAME2_C)
 
/* Create data queue if it doesn't already exist */
/* and set authorities. */
 
             ADDPFM     FILE(LEHISUPD) MBR(&MEMBER)
             ADDPFM     FILE(LEHISTPD) MBR(&MEMBER)
             ADDPFM     FILE(LEHIDTPD) MBR(&MEMBER)
             CHKOBJ     OBJ(&DTAQLIB/&MEMBER) OBJTYPE(*DTAQ)
             MONMSG     MSGID(CPF9801) EXEC(DO)
                CRTDTAQ    DTAQ(&DTAQLIB/&MEMBER) MAXLEN(50)
                RTVOBJD    OBJ(&DTAQLIB) OBJTYPE(*LIB) OWNER(&OWNER)
                CHGOBJOWN  OBJ(&DTAQLIB/&MEMBER) OBJTYPE(*DTAQ) +
                             NEWOWN(&OWNER)
             ENDDO
             CALL       PGM(QCLRDTAQ) PARM(&MEMBER &DTAQLIB)
 
             CHGVAR     VAR(&TSK#) VALUE(&TSK# + 1)
             GOTO       CMDLBL(LOOP)
 
/* Start journalling driving/update files */
 
 START:
 
             CLRPFM     FILE(MNHSTHH)
             CLRPFM     FILE(MNHSTMN)
             CLRPFM     FILE(MNHSTT2)
             CLRPFM     FILE(HIUPDHH)
             CLRPFM     FILE(HIUPDHM)
             CLRPFM     FILE(HIUPDHN)
             CLRPFM     FILE(HIUPDHP)
             CLRPFM     FILE(HIUPDHQ)
             CLRPFM     FILE(HIUPDZX)
 
/* Write Headers & Trailers */
 
             CALL       LE09001
 
/* Goto End                              */
 
             GOTO       CMDLBL(END)
 
 ABNOR:      SNDPGMMSG  MSG('Program LEC1001T1 ended abnormally') +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)
             CHGJOB     SWS(XXXXXX11)
 
 END:        ENDPGM
