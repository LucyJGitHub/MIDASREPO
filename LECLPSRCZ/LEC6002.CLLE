/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas LE Ensure GLAPRTPD exists in QTEMP')            */
/*********************************************************************/
/*                                                                   */
/*       Midas - Customer Lending Module                             */
/*                                                                   */
/*       LEC6002 - Ensure a clear PF/GLAPRCTD exists in QTEMP        */
/*                 before calling LE6002 with QTEMP at the top       */
/*                 of the library list.                              */
/*                                                                   */
/*       (c) Finastra International Limited 2016                     */
/*                                                                   */
/*       Last Amend No. MD054062           Date 03Sep19              */
/*       Prev Amend No. CER050  *Create    Date 16Jun19              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD054062 - Initialization program encountered issues during */
/*                  switch on of CER050                              */
/*       CER050 - Annualised Percentage Rate                         */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
             PGM        PARM(&ERR &LOAN)

             DCL        VAR(&MKICPY) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited +
                          2016')
             DCL        VAR(&ERR) TYPE(*CHAR) LEN(7)
             DCL        VAR(&LOAN) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(7) VALUE('  DPLIB')

             CHGJOB     SWS(XXXXXX00)
/* Ensure QTEMP is at the top of the library list                    */
             RMVLIBLE   LIB(QTEMP)
             MONMSG     MSGID(CPF0000)

             ADDLIBLE   LIB(QTEMP) POSITION(*FIRST)
             MONMSG     MSGID(CPF0000)

               RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
               CHGVAR     VAR(%SUBSTRING(&DPLIB 1 2)) VALUE(&SYSID)

/* Ensure PF/GLAPRCTD is in QTEMP and empty                          */
             CHKOBJ     OBJ(QTEMP/GLAPRCTD) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
               CPYF       FROMFILE(&DPLIB/GLAPRCTD) +
                          TOFILE(QTEMP/GLAPRCTD) CRTFILE(*YES)
               ENDDO

             CLRPFM     FILE(QTEMP/GLAPRCTD)

             OVRDBF     FILE(CLOAN) SHARE(*NO)
             OVRDBF     FILE(LOAMS) SHARE(*NO)
             OVRDBF     FILE(LEFEEDL2) SHARE(*NO)

/* Call the LE APR Calculation program LE6002                        */
             CALL       LE6002 PARM(&ERR &LOAN)

             DLTOVR     FILE(CLOAN)

/* Ensure QTEMP is at the bottom of the library list                 */
             RMVLIBLE   LIB(QTEMP)
             MONMSG     MSGID(CPF0000)

             ADDLIBLE   LIB(QTEMP) POSITION(*LAST)
             MONMSG     MSGID(CPF0000)

             CPYF       FROMFILE(QTEMP/GLAPRCTD) +
                          TOFILE(&DPLIB/GLAPRCTD) MBROPT(*REPLACE) +
                          CRTFILE(*NO)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)

/* Free resources that were allocated by the program                 */                 /*MD054062*/
/* that have finished running                                        */                 /*MD054062*/
                                                                                        /*MD054062*/
             RCLRSC                                                                     /*MD054062*/
                                                                                        /*MD054062*/
             ENDPGM
