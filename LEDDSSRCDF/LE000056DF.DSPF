      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Aggregated Facility Hist. Rpt - Prmpt Dsp')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LE000056DF - Aggregated Facility History Report -       *
      *                    Prompt Display                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE041  *CREATE   Date 11Oct04                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE041 - Collateralised Lending Phase 2B                     *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      CF03
     A                                      CF12
     A  51                                  CSRINPONLY
      *
      ** Detail Screen
      *
     A          R LE000056D0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A                                  1 13'LE000056P'
     A                                  1 67'DATE '
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWSID    R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A                                  2 26'Aggregated Facility History +
     A                                      Report'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5  2'Aggregated Facility:'
     A            DDCUST         6A  B  5 23TEXT('Facility Customer')
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI PC)
     A N25N50                               DSPATR(UL)
     A  50                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            DDFACT         3A  B  5 30TEXT('Facility Type')
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI PC)
     A N25N50                               DSPATR(UL)
     A  50                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            DDFCNO         2A  B  5 34TEXT('Facility Number')
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI PC)
     A N25N50                               DSPATR(UL)
     A  50                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
      *
      ** Screen Footer
      *
     A          R LE000056F0                TEXT('Screen Footer')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRATR
     A                                 23  2'F3=Exit'
     A                                 23 14'F12=Cancel'
      *
      ** Message Subfile
      *
     A          R LE000056S0                SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R LE000056C0                SFLCTL(LE000056S0)
     A                                      SFLSIZ(03)
     A                                      SFLPAG(1)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A                                      OVERLAY
     A  95
     AON95                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
