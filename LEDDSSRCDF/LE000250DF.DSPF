      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Commitment Fees Enquiry Display')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LE000250DF - Commitment Fess Enquiry Display            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE037  *CREATE    Date 10Dec03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE037 - Commitment Fees                                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
     A                                      CF03
      *
     A          R LE000250SM                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R LE000250CM                TEXT('Message Subfile Control')
     A                                      CF12
     A                                      SFLCTL(LE000250SM)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  84                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A          R LE000250S0                TEXT('Select Screen Subfile')
     A                                      SFL
     A  82                                  SFLNXTCHG
      *
     A            #0SEL          1   B  8 12
     A N13N14                               DSPATR(UL HI)
     A  13N14                               DSPATR(RI PC)
     A  14                                  DSPATR(ND PR)
     A            #0CNUM         6   O  8 18TEXT('Customer')
     A N14                                  DSPATR(HI)
     A  14                                  DSPATR(ND)
     A            #0FACT         3   O  8 29TEXT('Facility Type')
     A N14                                  DSPATR(HI)
     A  14                                  DSPATR(ND)
     A            #0FACN         2   O  8 34TEXT('Facility No.')
     A N14                                  DSPATR(HI)
     A  14                                  DSPATR(ND)
     A            #0FSEQ         2   O  8 39TEXT('Sequence No.')
     A N14                                  DSPATR(HI)
     A  14                                  DSPATR(ND)
     A            #0FCCY         3   O  8 45TEXT('Facility Currency')
     A N14                                  DSPATR(HI)
     A  14                                  DSPATR(ND)
     A            #0LPDT         7      8 52TEXT('Last Pay Date')
     A N14                                  DSPATR(HI)
     A  14                                  DSPATR(ND)
     A            #0NPYD         7      8 63TEXT('Next Pay Date')
     A N14                                  DSPATR(HI)
     A  14                                  DSPATR(ND)
     A            #0BRCA         3   H      TEXT('Branch')
     A            #0CALC         1  0H      TEXT('Calculation Basis')
     A            #0CALT         2   H      TEXT('Calculation Type')
     A            #0PSTD         5   H      TEXT('Fee Period Start')
     A            #0PEND         5   H      TEXT('Fee Period End')
      *
     A          R LE000250C0                TEXT('Select Screen Control')
     A                                      SFLCTL(LE000250S0)
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A                                      CF05
     A N87                                  SFLDSP
     A N87                                  SFLDSPCTL
     A  86N87                               SFLEND(*MORE)
     A  87                                  SFLCLR
     A N86                                  ROLLUP(88)
     A                                      OVERLAY
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'LE000250DF'
     A                                  1 28'COMMITMENT FEES ENQUIRY'
     A                                      DSPATR(HI UL)
     A
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 72TIME
      *
     A                                  4 11'Sel'
     A                                  4 17'Customer'
     A                                  4 28'Facility'
     A                                  4 39'Seq'
     A                                  4 45'Ccy'
     A                                  4 51'Last Pay'
     A                                  4 62'Next Pay'
      *
     A                                  5 29'Type/No.'
     A                                  5 53'Date'
     A                                  5 64'Date'
     A            #0SCNM         6   B  6 18DSPATR(HI UL)
     A            #0SFCT         3   B  6 29DSPATR(HI UL)
     A            #0SFCN         2   B  6 34DSPATR(HI UL)
      *
     A          R LE000250H0                TEXT('Detail Header Screen')
     A                                      OVERLAY
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'LE000250DF'
     A                                  1 28'COMMITMENT FEES ENQUIRY'
     A                                      DSPATR(HI UL)
     A
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 72TIME
      *
     A                                  4  2'Customer'
     A            #1CNUM         6      4 11TEXT('Customer No.')
     A                                      DSPATR(HI)
     A                                  4 20'Facility'
     A            #1FCLT         6      4 29TEXT('Facility Type')
     A                                      DSPATR(HI)
     A                                  4 38'Fee Sequence'
     A            #1FSEQ         2      4 51TEXT('Fee Seq.')
     A                                      DSPATR(HI)
     A                                  4 56'Fee Ccy'
     A            #1FCCY         3      4 64TEXT('Fee Ccy')
     A                                      DSPATR(HI)
      *
     A                                  5  2'Fee Type'
     A            #1CALT         2      5 11TEXT('Calculation Type')
     A                                      DSPATR(HI)
     A            #1NARR        43      5 20TEXT('Narrative')
     A                                      DSPATR(HI)
      *
     A                                  6  2'Fee Period  Start'
     A            #1PSTD         7      6 20TEXT('Fee Period Start')
     A                                      DSPATR(HI)
     A                                  6 29'End'
     A            #1PEND         7      6 33TEXT('Fee Period End')
     A                                      DSPATR(HI)
      *
     A                                  7  2'Last Payment Date'
     A            #1LPDT         7      7 20TEXT('Last Payment Date')
     A                                      DSPATR(HI)
     A                                  7 29'Next Payment Date'
     A            #1NPYD         7      7 47TEXT('Next Payment Date')
     A                                      DSPATR(HI)
      *
     A                                  8  2'Total Fee Amount'
     A            #1TFAM        17      8 20
     A                                      DSPATR(HI)
     A                                  8 38'Total No. of Days'
     A            #1TNUM         8      8 57
     A                                      DSPATR(HI)
      *
     A          R LE000250S1                TEXT('Detail Screen')
     A                                      SFL
      *
     A            #1PRD         15   O 11  2TEXT('Period')
     A                                      DSPATR(HI)
     A            #1BALA        15   O 11 20TEXT('Amount')
     A                                      DSPATR(HI)
     A            #1PCT          7   O 11 42TEXT('Percentage')
     A                                      DSPATR(HI)
     A            #1RATE         7   O 11 52TEXT('Rate')
     A                                      DSPATR(HI)
     A            #1NUMD         3   O 11 61TEXT('No. of Days')
     A                                      DSPATR(HI)
     A            #1FAMT        15   O 11 65TEXT('Fee Amount')
     A                                      DSPATR(HI)
      *
     A          R LE000250C1                TEXT('Detail Screen Control')
     A                                      CF12
     A                                      SFLCTL(LE000250S1)
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(11)
     A  21                                  SFLDSP
     A  22                                  SFLDSPCTL
     A  23                                  SFLEND(*MORE)
     A N21N22                               SFLCLR
      *
     A                                 10  6'Period'
     A  15                             10 18'Available Amount'
     A  16                             10 16'Outstanding Amount'
     A  17                             10 19'Facility Amount'
     A                                 10 40'Percentage'
     A                                 10 54'Rate'
     A                                 10 60'Day/s'
     A                                 10 69'Fee Amount'
      *
     A          R LE000250S2                TEXT('Detail Screen')
     A                                      SFL
      *
     A            #2PRD         15   O 11  2TEXT('Period')
     A N14
     AON19                                  DSPATR(HI)
     A  14 19                               DSPATR(ND)
     A            #2BALA        15   O 11 20TEXT('Amount')
     A                                      DSPATR(HI)
     A            #2TAMT        15   O 11 36TEXT('Tiered Amount')
     A                                      DSPATR(HI)
     A            #2RATE         8   O 11 52TEXT('Rate')
     A                                      DSPATR(HI)
     A            #2NUMD         3   O 11 61TEXT('No. of Days')
     A N14                                  DSPATR(HI)
     A  14                                  DSPATR(ND)
     A            #2FAMT        15   O 11 65TEXT('Fee Amount')
     A                                      DSPATR(HI)
      *
     A          R LE000250C2                TEXT('Detail Screen Control')
     A                                      CF12
     A                                      SFLCTL(LE000250S2)
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(11)
     A  21                                  SFLDSP
     A  22                                  SFLDSPCTL
     A  23                                  SFLEND(*MORE)
     A N21N22                               SFLCLR
      *
     A                                 10  6'Period'
     A  15                             10 18'Available Amount'
     A  16                             10 16'Outstanding Amount'
     A  17                             10 19'Facility Amount'
     A  18                             10 37'Tiered Amount'
     A N18                             10 40'Percent'
     A                                 10 54'Rate'
     A                                 10 60'Day/s'
     A                                 10 69'Fee Amount'
      *
     A          R LE000250F1
     A                                      OVERLAY
     A N11                             23  2'E=Enquire   F3=End   F5=Refresh'
     A  11                             23  2'F3=End   F12=Previous'
