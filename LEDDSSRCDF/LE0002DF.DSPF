      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE MQSeries object checking utility (LM)')
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  DSPF/LE0002DF - MQSeries Object Checking Utility for Loan    *
      *                  Manager.                                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Last Amend No. 210893  *CREATE    Date 30May01               *
      *  Prev Amend No. Xnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  210893 - MQSeries Object Checking Utility                    *
      *                                                               *
      *****************************************************************
      *
     A                                      CF03(03)
      *
      ** Input screen
      *
     A          R MQPARM                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            USER          10A  O  1  2TEXT('*USER')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DATE           6Y 0O  1 72TEXT('*DATE')
     A                                      EDTWRD('  /  /  ')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  1 16'MQUTIL'
     A                                      DSPATR(HI)
      *
     A            SYSID         10A  O  2  2TEXT('SYSTEM ID')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            TIME           6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  3 21'Check MQSeries Objects for Loan M-
     A                                      anager'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
      *
     A                                  9 10'Midas System Prefix'
     A                                  9 52':'
     A            PREFIX         2A  I  9 54
      *
     A                                 11 10'Midas Branch Code for Loan Manager-
     A                                       Branch'
     A                                 11 52':'
     A            BRCH           3A  I 11 54
      *
     A                                 13 10'AS/400 Name'
     A                                 13 52':'
     A            ASNAME        10A  I 13 54
      *
     A                                 15 10'Loan Manager Server Name'
     A                                 15 52':'
     A            SRVNAM        15A  I 15 54
      *
     A                                 17  3'Generate Audit Report'
     A                                 17 52':'
     A            GENAUD         1A  I 17 54VALUES('N' 'Y')
     A                                 17 57'(Y/N)'
      *
     A                                 23  2'F3 = Exit'
      *
     A            MESS          78A  O 24  2
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
