      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Facility History Rework Request')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LE002170DF - Facility History Rework Display            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CLE023 *CREATE     Date 19Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE023 - Lending Facility History Improvements.              *
      *                                                               *
      *================================================================
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *================================================================
      *
      ** Header format
      *
     A          R LE002170F0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'LE002170'
     A                                  1 67'DATE '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A                                  2 28'FACILITY HISTORY REWORK'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
      *
      ** Detail Format
      *
     A          R LE002170F1
     A                                      CF03
     A                                      CF05
     A                                      OVERLAY
      *
     A                                  7 19'Facility Customer    :'
     A            #1CUST        10A  B  7 45
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A                                  9 19'Facility Type        :'
     A            #1FACT         3A  B  9 45
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A                                 11 19'Facility Sequence    :'
     A            #1FCNO         2A  B 11 45
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A                                 13 19'Rework from Date     :'
     A            #1DATE         6A  B 13 45
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
      *
     A                                 22  2'F3=Exit'
     A                                 22 14'F5=Refresh'
      *
      ** Subfile details for program messages
      *
     A          R LE002170S0                TEXT('Program messages subfile')
     A                                      SFL SFLMSGRCD(23)
     A            #0SFKY                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
      ** Subfile control for messages
      *
     A          R LE002170C0                TEXT('Program messages control')
     A                                      INDTXT(89 'End of subfile')
     A                                      SFLCTL(LE002170S0)
     A                                      SFLPAG(01) SFLSIZ(02)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  89
     AON89                                  SFLEND
     A            #0PGMQ                    SFLPGMQ
