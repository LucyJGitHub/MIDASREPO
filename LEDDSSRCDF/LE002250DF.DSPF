      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Adjustment to Amortised Amount')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE002250DF -  Adjustments to Amortisation Input              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *           for Lending                                         *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CA03(03)
     A                                      CA05(05)
     A                                      MSGLOC(24)
      *
     A          R LE002250F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      OVERLAY
     A            #1USER        10A  O 01 02
     A                                  1 13'LE002250'
     A                                 01 28'ADJUSTMENTS TO AMORTISED AMOUNT'
     A                                      DSPATR(UL HI)
     A            #1MRDT         7A  O  1 72
     A            #1JOB         10A  O  2  2
     A                                  2 71TIME
      *
     A                                 05 16'Loan no.'
     A            #1LNUM         6A  B 05 31DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(UL)
     A                                 07 16'Customer'
     A            #1CNUM        10A  B 07 31DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(UL)
     A                                 08 16'Adjustment'
     A            #1AJD         14A  B 08 31DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27                                  DSPATR(UL)
     A                                 09 16'Sign (+ or -)'
     A            #1SIGN         1A  B 09 31DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(UL)
     A                                 10 16'Currency'
     A            #1CCY          3A  B 10 31DSPATR(HI)
     A  29                                  DSPATR(RI)
     A                                      CHECK(FE)
     A N29                                  DSPATR(UL)
      *
     A                                 23  2'F3=Exit  F5=Clear'
      *
     A          R LE002250F1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R LE002250F2                TEXT('Program messages')
     A                                      SFLCTL(LE002250F1)
     A                                      SFLPAG(01) SFLSIZ(05)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  30
     A N30                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
