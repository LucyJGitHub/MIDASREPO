      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Guarantees detail input')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LE0080DF - Guarantees Detail Input                      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 26Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 126274             Date 24Nov97               *
      *                 123857             Date 28Oct97               *
      *                 121019             Date 22JUL97               *
      *                 120056             Date 11JUL97               *
      *                 CLE002 *Create     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Webfacing Changes.                                  *
      *  126274 - Subfile should show Shortname                       *
      *  123857 - Condition F11 for authorisation on ICD value        *
      *  121019 - Change screen text.                                 *
      *  120056 - Include F9 in subfile screen for insert             *
      *  CLE002 - Customer Lending  Enhancements - Authorisation      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CF03
      *
      ** Header format
      *
     A          R LE0080F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 14'LE0080'
     A                                  1 30'GUARANTEES, SECURITY,'
     A                                      DSPATR(HI)
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 30'AND COLLATERALS INPUT'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
      ** Initial Screen format
      *
     A          R LE0080F1
     A                                      OVERLAY
     A                                  7  2'Customer'
     A  41                                  DSPATR(ND)
     A            GCUSA         10A  B  7 11
     A  48                                  DSPATR(RI PC)
     A N48N41                               DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                  7 46'Review from ...'
     A  41                                  DSPATR(ND)
     A            SRCUS          6A  B  7 62
     A  82                                  DSPATR(RI PC)
     A N82N41                               DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            SRFOL          6A  B  7 69
     A  83                                  DSPATR(RI PC)
     A N83N41                               DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            SRSEQ          3A  B  7 76
     A  84                                  DSPATR(RI PC)
     A N84N41                               DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                  8  2'Facility'
     A  41                                  DSPATR(ND)
     A            FACILA         5   B  8 11
     A  49                                  DSPATR(RI PC)
     A N49N41                               DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                  8 17'or'
     A  41                                  DSPATR(ND)
     A                                  8 20'Loan'
     A  41                                  DSPATR(ND)
     A            LOANA          6   B  8 25
     A  45                                  DSPATR(RI PC)
     A N45N41                               DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                  8 38'OR'
     A  41                                  DSPATR(ND)
     A*                                      Begin /121019/
     A********************************* 8 46'Unauthorised Only'
     A                                  8 46'Unauthorised Only?'
     A*                                      End   /121019/
     A  41
     AON14                                  DSPATR(ND)
     A*                                      Begin /121019/
     A*********** SRAUT          1A  B  8 64
     A            SRAUT          1A  B  8 65
     A*                                      End   /121019/
     A  85                                  DSPATR(RI PC)
     A N85N41                               DSPATR(UL)
     A  41
     AON14                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
     A                                  9  2'Sequence No'
     A  41                                  DSPATR(ND)
     A            SEQRA          3   B  9 14
     A  46                                  DSPATR(RI PC)
     A N46N41                               DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                  9 18'Action'
     A  41                                  DSPATR(ND)
     A            ACTN           1A  B  9 25
     A  52                                  DSPATR(RI PC)
     A N52N41                               DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            ERCDA         76A  O 23 02
     A  42                                  DSPATR(ND)
      ** Start of Changes CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A N42                                  DSPATR(RI)
      ** Start of Changes CAAA03
     A                                      DSPATR(HI)
     A            ERRMSG        76A  O 24 02
     A  43                                  DSPATR(ND)
      ** Start of Changes CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A N43                                  DSPATR(RI)
      ** Start of Changes CAAA03
     A                                      DSPATR(HI)
     A                                 22 02'F3=Exit'
      *
      ** Detail Screen format
      *
     A          R LE0080F2
     A                                      OVERLAY
     A  12                                  CF10
      *                                              Begin /123857/
     A**13*******                           CF11
     A  13N26                               CF11
      *                                              End   /123857/
     A                                      CF12
     A*/COPY WNCPYSRC,LE0080DF03
     A                                 04 05'Action'
     A  40                                  DSPATR(ND)
     A            ACTION         1A  O 04 22
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 05 05'Customer'
     A  40                                  DSPATR(ND)
     A            GCUS           6A  O 05 22
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 05 32'Loan no'
     A  40                                  DSPATR(ND)
     A            LOAN           6A  O 05 41
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 05 49'or Facility'
     A  40                                  DSPATR(ND)
     A            FACIL          5A  O 05 62
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 06 05'Seq. No'
     A  40                                  DSPATR(ND)
     A            SEQR           3A  O 06 22
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 07 05'Start date'
     A  40                                  DSPATR(ND)
     A            STRDA          6   B 07 22
     A  53                                  DSPATR(RI PC)
     A N53N66                               DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 08 05'End date'
     A  40                                  DSPATR(ND)
     A            ENDATA         6   B 08 22
     A  54                                  DSPATR(RI PC)
     A N54N66                               DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 09 05'Review date'
     A  40                                  DSPATR(ND)
     A            RVDTA          6   B 09 22
     A  55                                  DSPATR(RI PC)
     A N55N66                               DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 10 05'Review freq'
     A  40                                  DSPATR(ND)
     A            RVFQ           1A  B 10 22
     A  56                                  DSPATR(RI PC)
     A N56N66                               DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 10 32'Day no'
     A  40                                  DSPATR(ND)
     A            DAYA           2   B 10 40
     A  35                                  DSPATR(RI PC)
     A N35N66                               DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 11 05'Currency'
     A  40                                  DSPATR(ND)
     A            GCCY           3A  B 11 22
     A  57                                  DSPATR(RI PC)
     A N57N66                               DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,LE0080DF05
     A                                 12 05'Unit amount'
     A*/COPY WNCPYSRC,LE0080DF06
     A  40                                  DSPATR(ND)
     A            UNAMT         14   B 12 22
     A  58                                  DSPATR(RI PC)
     A N58N66                               DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 12 42'No of items'
     A  40                                  DSPATR(ND)
     A            NITMA          5   B 12 54
     A  50                                  DSPATR(RI PC)
     A N50N66                               DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,LE0080DF01
     A                                 13 42'Collateral code'
     A  40                                  DSPATR(ND)
     A            COLL           3A  B 13 58
     A N66                                  DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,LE0080DF02
     A                                 14 05'Narrative'
     A  40                                  DSPATR(ND)
     A            NARRS         35A  B 14 22
     A N66                                  DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 15 05'Given by'
     A*/COPY WNCPYSRC,LE0080DF07
     A  40                                  DSPATR(ND)
     A*/COPY WNCPYSRC,LE0080DF08
     A            GIVEA         10A  B 15 22
     A  59                                  DSPATR(RI PC)
     A N59N67                               DSPATR(UL)
     A  67                                  DSPATR(PR)
     A*/COPY WNCPYSRC,LE0080DF09
     A  40                                  DSPATR(ND)
     A*/COPY WNCPYSRC,LE0080DF10
     A                                      DSPATR(HI)
     A                                 16 05'Type'
     A*/COPY WNCPYSRC,LE0080DF11
     A  40                                  DSPATR(ND)
     A*/COPY WNCPYSRC,LE0080DF12
     A            TYPE           1A  B 16 22
     A  60                                  DSPATR(RI PC)
     A N60N67                               DSPATR(UL)
     A  67                                  DSPATR(PR)
     A*/COPY WNCPYSRC,LE0080DF13
     A  40                                  DSPATR(ND)
     A*/COPY WNCPYSRC,LE0080DF14
     A                                      DSPATR(HI)
     A                                      CHECK(FE)
     A            STATUS        16A  O 21 61
     A                                      DSPATR(HI)
     A            ERCDA         76A  O 23 02
     A  42                                  DSPATR(ND)
      ** Start of Changes CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A N42                                  DSPATR(RI)
      ** End of Changes CAAA03
     A                                      DSPATR(HI)
     A            ERRMSG        76A  O 24 02
     A  43                                  DSPATR(ND)
      ** Start of Changes CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A N43                                  DSPATR(RI)
      ** End of Changes CAAA03
     A                                      DSPATR(HI)
     A                                 22 02'F3=Exit'
     A  12                             22 13'F10=Delete'
      *                                              Begin /123857/
     A**13*******                      22 27'F11=Authorise'
     A  13N26                          22 27'F11=Authorise'
      *                                              End   /123857/
     A                                 22 44'F12=Previous'
     A*/COPY WNCPYSRC,LE0080DF04
      *
      ** Subfile Footer
      *
     A          R LE0080F3
     A                                      OVERLAY
     A*                                      Begin /120056/
     A*********************************22 02'F3=Exit     F5=Refresh     +
     A*********************************     F12=Previous'
     A                                 22 02'F3=Exit     F5=Refresh     +
     A                                      F9=Insert     F12=Previous'
     A*                                      End   /120056/
     A                                 23 71'Page Keys'
     A            ERRMSG        76A  O 24 02
     A  43                                  DSPATR(ND)
      ** Start of Changes CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A N43                                  DSPATR(RI)
      ** Start of Changes CAAA03
     A                                      DSPATR(HI)
      *
      ** Subfile records
      *
     A          R LE0080S0
     A                                      SFL
     A  20                                  SFLNXTCHG
     A*                                              Begin /126274/
     A*********** SACTN          1   B 10  6
     A            SACTN          1   B 10  2
     A*                                              End   /126274/
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI UL)
     A*                                              Begin /126274/
     A*********** SCUST          6   O 10 12DSPATR(HI)
     A            SCUST          6   O 10  5DSPATR(HI)
     A            SCSNM         10   O 10 12DSPATR(HI)
     A*                                              End   /126274/
     A            SFCLN          1   O 10 23DSPATR(HI)
     A            SFCNO          5   O 10 31DSPATR(HI)
     A            SLONO          6   O 10 41DSPATR(HI)
     A            SSEQN          3   O 10 52DSPATR(HI)
     A            SSTAT         16   O 10 62DSPATR(HI)
     A            SBRCA          3   H
     A            SGSTS          1   H
     A            SIUSR         10   H
     A            SAUSR         10   H
      *
      ** Subfile control
      *
     A          R LE0080C0
     A                                      CF05
     A*                                      Begin /120056/
     A                                      CF09
     A*                                      End   /120056/
     A                                      CF12
     A                                      SFLCTL(LE0080S0)
     A N06N07                               SFLDSP
     A N06                                  SFLDSPCTL
     A  06                                  SFLCLR
     A  08                                  SFLEND(*MORE)
     A N08                                  ROLLUP(09)
     A                                      SFLPAG(10)
     A                                      SFLSIZ(11)
     A                                      OVERLAY
     A            SRECN          4S 0H      SFLRCDNBR
     A                                  4 47'Review from ...'
     A            SRCUS          6A  B  4 63
     A  82                                  DSPATR(RI PC)
     A N82                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            SRFOL          6   B  4 70
     A  83                                  DSPATR(RI PC)
     A N83                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            SRSEQ          3A  B  4 77
     A  84                                  DSPATR(RI PC)
     A N84                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5 47'Unauthorised Only'
     A N14                                  DSPATR(ND)
     A            SRAUT          1   B  5 65
     A  85                                  DSPATR(RI PC)
     A N85                                  DSPATR(UL)
     A N14                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
     A                                  5  2'Actions,'
     A                                  6  3'E=Enquire'
     A                                  6 16'A=Amend'
     A                                  6 27'D=Delete'
     A  14                              6 39'X=Authorise'
     A*                                              Begin /126274/
     A********************************* 8  5'Actn'
     A********************************* 8 11'Customer'
     A                                  8  1'Actn'
     A                                  8  8'Customer'
     A*                                              End   /126274/
     A                                  8 22'Fac./'
     A                                  8 30'Facility'
     A                                  8 42'Loan'
     A                                  8 50'Sequence'
     A                                  8 62'Status'
     A                                  9 22'Loan'
     A                                  9 31'Number'
     A                                  9 41'Number'
     A                                  9 51'Number'
