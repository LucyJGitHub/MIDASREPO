      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Guarantees Detail Input')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Customer Lending Module
     A*                                                                  *
     A*       LE0080FM  - GUARANTEES DETAIL INPUT                        *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 112563             DATE 27JAN97               *
     A*       LAST AMEND NO. S01408          DATE  08SEP96               *
     A*       PREV AMEND NO. BH3422          DATE  17SEP92               *S01199
     A*                      BHF332          DATE  08AUG90               *S01199
     A*                      BHF229          DATE  07JUL90               *S01199
     A*                      S01199          DATE  12DEC89               *S01199
0009 A*            AMEND NO. S01117          DATE  12DEC89               *S01199
     A*            AMEND NO. E16857          DATE  30/01/89              *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
     A*  112563  -  Header and footer changes required for GUI        *
     A*             Multilanguage support.                            *
     A*       S01408 - Addition of core hook LE0080FM04                  *
     A*              - Addition of core hook LE0080FM03                  *
     A*              - Addition of core hook LE0080FM02                  *
     A*              - Addition of core hook LE0080FM01                  *
     A*  BH3422  -  Help text amendments for Lending Enhancements.       *
     A*       BHF332 - AMEND HELP DOCUMENT TO RELEASE 10 LEVEL           *S01199
     A*       BHF229 - INDARA MISMATCH. REMOVE INDARA.                   *
     A*       S01199 - HELP SYSTEM.                                      *S01199
     A*       S01117 - MULTIBRANCHING                                    *S01199
     A*       E16857 - F10 SHOULD BE USED FOR DELETE,NOT F7.             *
     A*                                                                  *
     A********************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*****************************************************************
     A                                      CHGINPDFT             PRINT(MPRINT)
     A**                                                   /S01117/
     A**                                                   /BHF229/
     A**********                            INDARA
     A**                                                   /BHF229/
      *                                              /* BEGIN S01408 */
     A*/COPY WNCPYSRC,LE0080FM01
      *                                              /* END S01408 */
     A**                                                   /S01117/
     A*                                                    /S01199/
     A*                                                    /S01199/
     A*                                                    /S01199/
     A*                                                        /BH3422/G/DC10)
     A*                                                        /BH3422/G/DC10)
     A*                                                    /S01199/
     A          R LE0080F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A**********                            CF07                     /*E16857*/
     A                                      CF10
     A                                      CF12
     A                                      CF03
      *                                              /* BEGIN S01408 */
     A*/COPY WNCPYSRC,LE0080FM02
      *                                              /* END S01408 */
     A*                                                    /S01199/
     A*                                                        /BH3422/G/DC10)
     A*                                                        /BH3422/G/DC10)
     A*                                                    /S01199/
     A            SCID           2A  B 01 02
     A  68                                  DSPATR(PC)
     A                                      DSPATR(PR ND)
     A**                                             Begin /112563/
     A                                  1  6'LE0080'
     A**                                             End   /112563/
     A                                 01 31'GUARANTEES, SECURITY,'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            TIME           8A  O 01 65
     A                                 01 74'WID'
     A            WSID           3A  O 01 78
     A                                 02 31'AND COLLATERAL INPUT'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 04 05'Action'
     A  40                                  DSPATR(ND)
     A            ACTION         1A  O 04 22
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 05 05'Customer'
     A  40                                  DSPATR(ND)
     A            GCUS           6A  O 05 22
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 05 32'Loan no'
     A  40                                  DSPATR(ND)
     A            LOAN           6A  O 05 41
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 05 49'or Facility'
     A  40                                  DSPATR(ND)
     A            FACIL          5A  O 05 62
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 06 05'Seq no'
     A  40                                  DSPATR(ND)
     A            SEQR           3A  O 06 22
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 07 05'Start date'
     A  40                                  DSPATR(ND)
     A            STRD           6   B 07 22
     A  53                                  DSPATR(RI)
     A N53N66                               DSPATR(UL)
     A  41                                  DSPATR(PC)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 08 05'End date'
     A  40                                  DSPATR(ND)
     A            ENDAT          6   B 08 22
     A  54                                  DSPATR(RI)
     A N54N66                               DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 09 05'Review date'
     A  40                                  DSPATR(ND)
     A            RVDT           6   B 09 22
     A  55                                  DSPATR(RI)
     A N55N66                               DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 10 05'Review freq'
     A  40                                  DSPATR(ND)
     A            RVFQ           1A  B 10 22
     A  56                                  DSPATR(RI)
     A N56N66                               DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 10 32'Day no'
     A  40                                  DSPATR(ND)
     A            DAY            2   B 10 40
     A  35                                  DSPATR(RI)
     A N35N66                               DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 11 05'Currency'
     A  40                                  DSPATR(ND)
     A            GCCY           3A  B 11 22
     A  57                                  DSPATR(RI)
     A N57N66                               DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 12 05'Unit amount'
     A  40                                  DSPATR(ND)
     A            UAMT          14   B 12 22
     A  58                                  DSPATR(RI)
     A N58N66                               DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 12 42'No of items'
     A  40                                  DSPATR(ND)
     A            NITM           5   B 12 54
     A  50                                  DSPATR(RI)
     A N50N66                               DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,LE0080FM05
     A                                 13 42'Collateral code'
     A  40                                  DSPATR(ND)
     A            COLL           3A  B 13 58
     A N66                                  DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,LE0080FM06
     A                                 14 05'Narrative'
     A  40                                  DSPATR(ND)
     A            NARR          35A  B 14 22
     A N66                                  DSPATR(UL)
     A  66                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 15 05'Given by'
     A  40                                  DSPATR(ND)
     A            GIVER         10A  B 15 22
     A  59                                  DSPATR(RI)
     A N59N67                               DSPATR(UL)
     A  67                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 16 05'Type'
     A  40                                  DSPATR(ND)
     A            TYPE           1A  B 16 22
     A  60                                  DSPATR(RI)
     A N60N67                               DSPATR(UL)
     A  67                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      CHECK(FE)
     A            ERCD          76A  O 19 02
     A  42                                  DSPATR(ND)
     A                                      DSPATR(RI)
     A                                      DSPATR(HI)
     A            ERRMSG        76A  O 20 02
     A  43                                  DSPATR(ND)
     A                                      DSPATR(RI)
     A                                      DSPATR(HI)
     A**                                             Begin /112563/
     A***********                      22 05'Press F3 to end,'
     A                                 22 05'F3=Exit'
     A**                                             End   /112563/
     A*/*E16857*/
     A*********                        22 25'F7 to delete, F12 to return
     A**                                             Begin /112563/
     A***********                      22 25'F10 to delete, F12 to return -
     A***********                           to initial screen,'
     A                                 22 14'F10=Delete  F12=Initial screen'
     A**                                             End   /112563/
     A*/*E16857*/
     A  40                                  DSPATR(ND)
     A                                 23 05'Or enter next'
      *                                              /* BEGIN S01408 */
     A*/COPY WNCPYSRC,LE0080FM03
      *                                              /* END S01408 */
     A  41                                  DSPATR(ND)
     A                                 23 19'Customer'
     A  41                                  DSPATR(ND)
     A            GCUS2         10A  B 23 28
     A  48                                  DSPATR(RI)
     A N48N41                               DSPATR(UL)
     A  66                                  DSPATR(PC)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 23 39','
     A  41                                  DSPATR(ND)
     A                                 24 05'Facility'
     A  41                                  DSPATR(ND)
     A            FACL2          5   B 24 14
     A  49                                  DSPATR(RI)
     A N49N41                               DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 24 20'or'
     A  41                                  DSPATR(ND)
     A                                 24 23'Loan'
     A  41                                  DSPATR(ND)
     A            LOAN2          6   B 24 28
     A  45                                  DSPATR(RI)
     A N45N41                               DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 24 35',Seq no'
     A  41                                  DSPATR(ND)
     A            SEQR2          3   B 24 43
     A  46                                  DSPATR(RI)
     A N46N41                               DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 24 47',Action'
     A  41                                  DSPATR(ND)
     A            ACTN2          1A  B 24 55
     A  52                                  DSPATR(RI)
     A N52N41                               DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                      DSPATR(HI)
      *                                              /* BEGIN S01408 */
     A*/COPY WNCPYSRC,LE0080FM04
      *                                              /* END S01408 */
