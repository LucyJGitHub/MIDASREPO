      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Midas Italy Guarantees Puma Details')         *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE0081DF - Midas Italy Guarantees Puma Details               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. LUC139             Date 08Jan21               *
      *  Prev Amend No. CER009  *CREATE    Date 16Aug06               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  CER009 - Puma II Upgrade to MidasPlus                        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 'Return to main menu')
     A  31N30                               CF05(05 'Refresh')
     A                                      CF12(12 'Previous Screen')
     A          R LE0081S0
     A                                      OVERLAY
      *
     A                                  4  3'Codice Aziendale:'
     A            LICOLC         3A  B  4 26
     A N31                                  DSPATR(PR)
     A N32 31                               DSPATR(UL)
     A  32 31                               DSPATR(RI)
     A  32 31                               DSPATR(PC)
     A N32                                  DSPATR(HI)
     A                                  5  3'Stato Rapporto:'
     A            LIX563         3A  B  5 26
     A N31                                  DSPATR(PR)
     A N33 31                               DSPATR(UL)
     A  33 31                               DSPATR(RI)
     A  33 31                               DSPATR(PC)
     A N33                                  DSPATR(HI)
     A  31N30                          23  1'F3=Main menu  F5=Refresh  F12=Retu-
     A                                      rn'
     A                                      COLOR(BLU)
     A N30N31                          23  1'F3=Main  F10=Confirm  F12=Return'
     A                                      COLOR(BLU)
     A  30                             23  1'F3=Main menu  F12=Return'
     A                                      COLOR(BLU)
     A                                  2 35'PUMA details'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            #0USER        10A  O  1  2
     A            #0JOB         10   O  2  2
     A            #0PGM         10A  O  1 13
     A                                  2 73TIME
     A                                  1 67'Date:'
     A                                  2 67'Time:'
     A            BJMRDT         7   O  1 73
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #0PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(07)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25                                  SFLEND
     A            #0PGM                     SFLPGMQ(10)
