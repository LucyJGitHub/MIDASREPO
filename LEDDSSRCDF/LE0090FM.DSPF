      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Accrued Interest Adjustment')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*  LE0090FM - SCREEN FORMAT FOR ACCR.INT.ADJS. INPUT            *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 26Apr04               *
      *                 CAS005             Date 16Dec02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 112563             Date 27Jan97               *
      *                 CAC001             Date 01Feb96               *
     A*                 056299             DATE 25OCT93               *
     A*                 BH3422             DATE 17SEP92               *
     A*                 BHF332             DATE 08AUG90               *
     A*                 S01199             DATE 12DEC89               *
     A*                 S01117             DATE 12DEC89               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Webfacing Changes.                                  *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  CAC001 - Profit Centre Accounting Development:               *
     A*           Two new fields, Adjustment to Accrued Cost of Funds *
     A*           and Sign, will be displayed to make it possible to  *
     A*           manually adjust any accrual cost of funds on        *
     A*           directly funded loans.                              *
     A*  056299 - Wrong help text are displayed for input flds.       *
     A*  BH3422 - Help text amendments for Lending Enhancements.      *
     A*  BHF332 - AMEND HELP DOCUMENT TO RELEASE 10 LEVEL             *
     A*  S01199 - HELP SYSTEM.                                    *
     A*  S01117 - MULTIBRANCHING                                      *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*****************************************************************
     A*                                                    /S01199/
     A*                                                    /S01199/
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A*                                                    /S01199/
     A*                                                        /BH3422/G/DC10)
     A*                                                        /BH3422/G/DC10)
     A*                                                    /S01199/
     A          R LE0090F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CF12
     A                                      CF03
     A*                                                    /S01199/
     A*                                                        /BH3422/G/DC10)
     A*********              Start of Change               /056299/
     A*                                                    Begin  /CAC001/
     A*                                                    End    /CAC001/
     A*                      End of Change                 /056299/
     A*                                                        /BH3422/G/DC10)
     A*                                                    /S01199/
     A            SCID           2A  B 01 02DSPATR(PR ND)
     A**                                             Begin /112563/
     A                                  1  6'LE0090'
     A**                                             End   /112563/
     A                                 01 17'ADJUSTMENTS TO ACCRUED INTEREST-C-
     A                                      UST. LENDING'
     A                                      DSPATR(UL HI)
     A            TIME           8A  O 01 65
     A                                 01 74'WID'
     A            WSID           3A  O 01 78
     A                                 05 16'Loan no.'
     A            LNUM           6A  B 05 31DSPATR(HI)
     A  41                                  DSPATR(RI)
     A N41                                  DSPATR(UL)
     A                                 07 16'Customer'
     A            CNUM          10A  B 07 31DSPATR(HI)
     A  51                                  DSPATR(RI)
     A N51                                  DSPATR(UL)
     A                                 08 16'Adjustment'
     A            AJD           14A  B 08 31DSPATR(HI)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(UL)
     A                                 09 16'Sign (+ or -)'
     A            SIGN           1A  B 09 31DSPATR(HI)
     A  63                                  DSPATR(RI)
     A N63                                  DSPATR(UL)
     A                                 10 16'Currency'
     A            CCY            3A  B 10 31DSPATR(HI)
     A  67                                  DSPATR(RI)
     A                                      CHECK(FE)
     A N67                                  DSPATR(UL)
     A*                                                    Begin  /CAC001/
     A  55                             12 16'Adjustment to accrued +
     A                                      cost of funds'
     A            ACJ           14A  B 12 53DSPATR(HI)
     A N55                                  DSPATR(ND PR)
     A  56                                  DSPATR(RI)
     A N56                                  DSPATR(UL)
     A  55                             13 16'Sign (+ or -)'
     A            SGN            1A  B 13 53DSPATR(HI)
     A N55                                  DSPATR(ND PR)
     A  57                                  DSPATR(RI)
     A N57                                  DSPATR(UL)
     A*                                                    End    /CAC001/
      **Start of change CAS005
     A  35                             15 16'Adjustment using Net +
     A                                      Treasury Rate'
     A  35        #0ANTR        14A  B 15 53DSPATR(HI)
     A N35                                  DSPATR(ND PR)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(UL)
      *
     A  35                             16 16'Sign (+ or -)'
     A  35        #0SIGN         1A  B 16 53DSPATR(HI)
     A N35                                  DSPATR(ND PR)
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(UL)
      **End of change CAS005
     A            ERCODE        76A  O 21 03
     A  32                                  DSPATR(ND)
      ** Start of change CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A N32                                  DSPATR(RI)
      ** End of change CAAA03
     A                                      DSPATR(HI)
     A            ERMESS        76A  O 22 03
     A  33                                  DSPATR(ND)
      ** Start of change CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A N33                                  DSPATR(RI)
      ** End of change CAAA03
     A                                      DSPATR(HI)
     A**                                             Begin /112563/
     A***********                      23 06'Press F3 to end, F12 to clea-
     A***********                           r screen'
     A                                 23  2'F3=Exit  F12=Clear'
     A**                                             End   /112563/
