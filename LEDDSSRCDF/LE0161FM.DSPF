      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE O/s facilities by customer')                  *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module                              *
     A*                                                               *
     A*  LE0161FM  OUTSTANDING FACILITIES BY CUSTOMER ENQUIRY         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  Last Amend No. MD025572           Date 19Aug14               *
      *  Prev Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 260453             Date 03Jun09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG8293            Date 23Aug05               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CLE023             Date 19Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 170100             Date 08Nov99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CLE004             Date 02Dec96               *
     A*                 BH3422             Date 21SEP92               *
     A*                 S01311             Date 29JAN91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD025572 - Incorrect position of F9 label on screen.         *
      *             Recompile                                         *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,LEH00405                                 *
      *             WNCPYSRC,LEH00406                                 *
      *             WNCPYSRC,LEH00407                                 *
      *             WNCPYSRC,LEH00408                                 *
      *             WNCPYSRC,LEH00409                                 *
      *             WNCPYSRC,LEH00410                                 *
      *             WNCPYSRC,LEH00411                                 *
      *             WNCPYSRC,LEH00412                                 *
      *  260453 - Discrepancy in exposure. Make use of system value   *
      *           'CLE023OnUseSpot' to enable selection between CEXP  *
      *           or CAMD for display of exposure. When value is 'N', *
      *           show facility exchange rate else use spot rates     *
      *           computation.                                        *
     A*  BUG8293- Webfacing: Red bar appears on the bottom of the     *
     A*           screen evenif no error message is displayed         *
      *  CLE023 - Lending Facility History Improvements.              *
     A*  170100 - If CLE006 (*IN52) is off, use an alternative column *
     A*           heading -  Display 'Credit Agr.' not 'Syndicated'.  *
     A*  CLE004 - Customer Lending Enhancements - Syndications.       *
     A*  BH3422 - Help text amendments for Lending Enhancements.      *
     A*  S01311 - EXPOSURE MANAGEMENT ENQUIRIES                       *
     A*           New program created to replace LE0160               *
     A*                                                               *
     A*****************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A*                                                        /BH3422/
     A*                                                        /BH3422/
     A                                      CF03
     A          R LE0161F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            USER          10A  O 01 02
     A                                 01 13'LE0161'
     A                                 01 23'OUTSTANDING FACILITIES BY CUSTOMER-
     A                                       ENQUIRY'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O 01 72
     A            WSID          10A  O 02 02
     A                                 02 71TIME
     A            BRTTL         40A  O 03 21
     A N20
     AO 21                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 05 06'Customer'
     A  21                                  DSPATR(ND)
     A            BBCRNM        20A  O 05 16
     A  21                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            BBCRTN        10A  O 05 38
     A  21                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            BBCUST         6A  O 06 16
     A  21                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A*
      *                                              Begin /CLE004/
     A***********                      07 03'Facility'
     A**21*******                           DSPATR(ND)
     A***********                      07 44'Current'
     A**21*******                           DSPATR(ND)
     A***********                      07 65'Current'
     A**21*******                           DSPATR(ND)
     A***********                      08 06'Ref'
     A**21*******                           DSPATR(ND)
     A***********                      08 12'Ccy'
     A**21*******                           DSPATR(ND)
     A***********                      08 24'Amount'
     A**21*******                           DSPATR(ND)
     A***********                      08 44'Exposure'
     A**21*******                           DSPATR(ND)
     A***********                      08 64'Available'
     A**21*******                           DSPATR(ND)
      *
     A*/COPY WNCPYSRC,LEH00405
     A                                 07 04'Fclty'
     A  21                                  DSPATR(ND)
      *                                              Begin /170100/
     A*******                          07 14'Syndicated'
     A  52                             07 14'Syndicated'
     A*/COPY WNCPYSRC,LEH00406
     A**21***
     A*******ON50                           DSPATR(ND)
     A  21
     AON52
     AON50                                  DSPATR(ND)
      *
     A*/COPY WNCPYSRC,LEH00407
     A N52                             07 14'Credit Agr.'
     A*/COPY WNCPYSRC,LEH00408
     A  21
     AO 52
     AON50                                  DSPATR(ND)
      *                                              End   /170100/
      * Start of Change CLE023
     A**********                       07 46'Current'                                         CLE023
     A N71                             07 46'Current'
     A  21                                  DSPATR(ND)
      ** Start of change 260453
     A**71******                       07 43'Current Exposure'                                260453
     A  71 72                          07 43'Current Exposure'
     A  21                                  DSPATR(ND)
     A  71N72                          07 43'Current Utilised'
      ** End of change 260453
      * End of Change CLE023
     A  21                                  DSPATR(ND)
      * Start of Change CLE023
     A**********                       07 68'Current'                                         CLE023
     A N71                             07 68'Current'
     A  21                                  DSPATR(ND)
     A  71                             07 62'Current Available'
      * End of Change CLE023
     A  21                                  DSPATR(ND)
     A*/COPY WNCPYSRC,LEH00409
     A                                 08 05'Ref'
     A  21                                  DSPATR(ND)
     A                                 08 11'Ccy'
     A  21                                  DSPATR(ND)
     A                                 08 15'fclty'
     A  21
     AON50                                  DSPATR(ND)
     A                                 08 28'Amount'
     A*/COPY WNCPYSRC,LEH00410
     A  21                                  DSPATR(ND)
      * Start of Change CLE023
     A**********                       08 45'Exposure'                                        CLE023
     A N71                             08 45'Exposure'
     A  21                                  DSPATR(ND)
      ** Start of change 260453
     A**71******                       08 45'at spot rate'                                    260453
     A  71 72                          08 45'at spot rate'
     A  21                                  DSPATR(ND)
     A  71N72                          08 43'at fac exch rate'
      ** End of change 260453
      * End of Change CLE023
     A  21                                  DSPATR(ND)
      * Start of Change CLE023
     A**********                       08 67'Available'                                       CLE023
     A N71                             08 67'Available'
     A  21                                  DSPATR(ND)
     A  71                             08 63'at fac exch rate'
      * End of Change CLE023
     A  21                                  DSPATR(ND)
      *                                              End   /CLE004/
     A*
     A          R LE0161S1                  SFL
     A            OPTN           1A  B 09 02DSPATR(UL HI PC)
     A            SFAC           6A  O 09 04DSPATR(HI)
      *                                              Begin /CLE004/
     A*********** FCCY           3A  O 09 12DSPATR(HI)
     A*********** SFAMT         19A  O 09 17DSPATR(HI)
     A*********** SCAMD         19A  O 09 38DSPATR(HI)
     A*********** SCAVL         19A  O 09 59DSPATR(HI)
     A*/COPY WNCPYSRC,LEH00411
     A            FCCY           3A  O 09 11DSPATR(HI)
     A            SPRTR          5A  O 09 15DSPATR(HI)
     A            SFAMT         19A  O 09 21DSPATR(HI)
     A            SCAMD         19A  O 09 41DSPATR(HI)
     A            SCAVL         19A  O 09 61DSPATR(HI)
     A*/COPY WNCPYSRC,LEH00412
      *                                              End   /CLE004/
     A            HCUST          6A  H
     A            HBRCD          3A  H
     A*
     A          R LE0161C1                  SFLCTL(LE0161S1)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0013)
     A                                      BLINK
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A N41N42                               SFLCLR
     A  60                                  SFLEND
     A  45                                  ROLLUP(18)
     A  46                                  ROLLDOWN(19)
     A  06                                  CF01
     A  06                                  CF12
     A                                      OVERLAY
      **Start of change BUG8293
     A**********  MSG           76A  O 22  2DSPATR(RI)                                       BUG8293
     A            MSG           76A  O 22  2
     A  30                                  DSPATR(RI)
      **End of change BUG8293
     A                                      DSPATR(HI)
     A N30                                  DSPATR(ND)
     A N06                             23 02'F3=Exit'
     A N06 45N46                       23 12'Rollup'
     A N06 45 46                       23 12'Rollup   Rolldown'
     A N06N45 46                       23 12'Rolldown'
     A  06                             23 02'F1=Initial Enquiry'
     A  06                             23 23'F3=Exit'
     A  06                             23 33'F12=Previous'
     A  06 45N46                       23 48'Rollup'
     A  06 45 46                       23 48'Rollup   Rolldown'
     A  06N45 46                       23 48'Rolldown'
     A                                 24 02'Customer Number or Shortname'
     A            SCUST         10A  B 24 33
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A                                 24 47'Branch'
     A N20                                  DSPATR(ND)
     A            SBRCD          3A  B 24 55
     A  20N32                               DSPATR(UL)
     A  32                                  DSPATR(RI)
     A N20                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
     A                                 24 62'Company'
     A N20                                  DSPATR(ND)
     A            SCOMP          3A  B 24 71
     A  20N33                               DSPATR(UL)
     A  33                                  DSPATR(RI)
     A N20                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
