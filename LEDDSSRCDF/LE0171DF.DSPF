      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('MIDAS/ABS ER Facilities maintenance - LU window')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LE0171DF - LE Facilities - Display File                 *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *
      *  The naming standards for the record formats should be the
      *  file name but changing # to F, i.e.
      *
      *  LE0171DF  ===>  LE0171F6
      *
      *  Field names should always start with # followed by the format
      *  number (i.e. 0) and the file field name cut of module id, i.e.
      *
      *  IBPLEX ===> #0PLEX
      *
      *-------------------------------------------------------------------------
      *
      *  The code in this display file that can be changed is within
      *  the record format(s).
      *  None of the following keywords can be changed : ASSUME
      *                                                  OVERLAY
      *                                                  PUTOVR
      *                                                  OVRATR
      *                                                  OVRDTA
      *
      *-------------------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CF12
      *-------------------------------------------------------------------------
     A          R LE0171F6                  TEXT('(c) Finastra International +
     A                                      Limited +                          -
     A                                      2005')
      *
     A                                      PUTOVR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'LE0171F6'
     A                                  1 25'European Reporting - Luxembourg'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            AGMRDT    R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  5  2'Subordinated......................-
     A                                       :'
     A                                      OVRATR
     A            #0SUBR         1A  B  5 40OVRDTA
     A                                      OVRATR
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  23N15                               DSPATR(PC)
     A                                  5 42'        '
     A                                      OVRATR
     A                                  6  2'Mobilisation Customer.............-
     A                                       :'
     A                                      OVRATR
     A            #0MOBL        10A  B  6 40OVRDTA
     A                                      OVRATR
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  25N15                               DSPATR(PC)
     A                                  7  2'Degree of Risk................... -
     A                                       : '
     A                                      OVRATR
     A            #0DGRI         1Y 0B  7 40OVRDTA
     A                                      OVRATR
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  22N15                               DSPATR(PC)
     A                                  7 42'        '
     A                                      OVRATR
     A                                 22  3'F12=Previous'
     A                                      COLOR(BLU)
     A                                      OVRATR
      *-------------------------------------------------------------------------
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            WWPGM                     SFLPGMQ
      *-------------------------------------------------------------------------
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  69
     AON69                                  SFLEND
     A            WWPGM                     SFLPGMQ
