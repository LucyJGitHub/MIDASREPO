      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Facilities Extension Details Display')        *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE0172DF - Facilities Extension Details Display              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. LUC139             Date 08Jan21               *
      *  Prev Amend No. CER009  *CREATE    Date 16Aug06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  CER009 - Puma II Upgrade to MidasPlus                        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CF03
     A                                      CF12
      *
     A          R LE0172F0
     A                                      TEXT('(c) Misys International -
     A                                      Banking Systems Ltd. 2006')
     A                                      OVERLAY
      *
     A            #0USER        10A  O  1  2
     A            #0JOB         10A  O  2  2
     A            #0PGM         10A  O  1 13
     A                                  1 67'Date'
     A            BJMRDT         7A  O  1 73
     A                                  2 67'Time'
     A                                  2 72TIME
      *
     A                                  1 27'European Reporting - Italy'
     A                                      DSPATR(HI)
      *
     A                                  4  2'312 - Operativita'' . . . . .:'
     A            #0SBCL         1A  O  4 32DSPATR(HI)
      *
     A                                  5  2'109 - Operazione in pool . .:'
     A            #0OPPL         1A  O  5 32DSPATR(HI)
      *
     A                                  6  2'6006- Accordato altrui . . .:'
     A            #0EXPO        14A  O  6 32DSPATR(HI)
      *
     A                                  7  2'225 - Organo Deliberante . .:'
     A            #0DECD         3A  O  7 32DSPATR(HI)
      *
     A                                  8  2'367 - Copertura SACE . . . .:'
     A            #0SACO         1A  O  8 32DSPATR(HI)
      *
     A                                  9  2'104 - Percentuale SACE . . .:'
     A            #0SAPE         6A  O  9 32DSPATR(HI)
      *
     A                                 23  2'F3=Exit   F12=Previous'
     A                                      COLOR(BLU)
      *
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #0PGM                     SFLPGMQ(10)
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0001)
     A            #0PGM                     SFLPGMQ(10)
