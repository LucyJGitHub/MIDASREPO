      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Participations sold enquiry screen')          *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LE0191DF - Participations Sold Enquiry Screen           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. FCM101             Date 11Feb19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CER059             Date 19Jul10               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 245251             Date 05Mar07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CLE042             Date 06Sep05               *
      *                 CLE036             Date 22Sep03               *
      *                 CGL029             Date 01Sep03               *
      *                 CLE034             Date 05May03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001             Date 23Nov01               *
      *                 200044             Date 09Jan02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 187261             Date 16Jan01               *
      *                 CLE011             Date 07Nov00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 157508             Date 10Feb99               *
      *                 147830             Date 10Dec98               *
      *                 CEU004             Date 02Jan98               *
      *                 CLE005             Date 22May97               *
      *                 CLE004 *Create     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  FCM101 - Floor Rates on LE Module                            *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus                                          *
      *  245251 - Enquiry title is for wrong type of loan             *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CLE036 - LE Enh for Nordea Phase 1 Pty 2- Specific Base Rate *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE034 - Lending Enhancements for Phase 1 Priority 1A.       *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *  200044 - Amended heading PARTICIPATIONS SOLD/PURCHASED IN A  *
      *           RISK LOAN to RISK PARTICIPATIONS SOLD/PURCHASED.    *
      *  187261 - Do not display fields linked to Syndications unless *
      *           CLE006 is switched on.                              *
      *  CLE011 - Rate Fixing Days for Lending Rollover Processing    *
      *  157508 - Participations Sold in Guarantee Loans not          *
      *           identified correctly                                *
      *  147830 - On Participations sold SFL identify Risk parts sold *
      *           as 'Risk'. Also show original borrower and report   *
      *           name on details enquiry screen.                     *
      *  CEU004 - Customer Lending  Enhancements                      *
      *  CLE005 - Customer Lending  Enhancements - Others             *
      *  CLE004 - Customer Lending  Enhancements - Syndications       *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      CF08
     A                                      CA12
     A  27                                  CF14
     A  32                                  CF19
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      * Header screen
      *
     A          R LE0191F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            SUSER     R           1  2REFFLD(USER)
     A                                  1 14'LE0191'
     A                                  1 32'PARTICIPATIONS SOLD FOR'
     A                                      DSPATR(HI)
     A  23                              2 29'PARTICIPATION PURCHASED'
     A                                      DSPATR(HI)
     A  23        SOLNO          6      2 53DSPATR(HI)
     A  22                              2 37'LOAN'
     A                                      DSPATR(HI)
     A  22        SLOAN          6      2 42DSPATR(HI)
      *                                              Begin /157508/
     A** Start of change 245251
     A**36******                        2 32'GUARANTEE ISSUED'                                245251
     A  36                              2 36'RISK LOAN'
     A** End of change 245251
     A                                      DSPATR(HI)
     A** Start of change 245251
     A**36******  SGUAR          6      2 49DSPATR(HI)                                        245251
     A  36        SGUAR          6      2 46DSPATR(HI)
     A** End of change 245251
      *                                              End   /157508/
     A            SRUNA     R           1 73REFFLD(RUNA)
     A            SWSID     R           2  2REFFLD(JOJOB)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
      * First detail screen
      *
     A          R LE0191F1
     A                                      ROLLUP(69)
      *
     A  24 26                           1  5'REPAY'
     A                                      DSPATR(RI)
      *                                              Begin /CLE005/
     A***********                       1 23'PARTICIPATIONS SOLD DETAILS -
     A***********                           ENQUIRY'
     A N19N20                           1 23'PARTICIPATIONS SOLD DETAILS -
     A                                      ENQUIRY'
      *                                              End   /CLE005/
     A                                      DSPATR(UL HI)
      *                                              Begin /CLE005/
     A  19                             01 26'PARTICIPATIONS SOLD IN A-
     A                                       CALL LOAN'
     A                                      DSPATR(UL HI)
     A** Start of change 200044
     A**20******                       01 26'PARTICIPATIONS SOLD IN A-                        200044
     A**********                             RISK LOAN'                                       200044
     A  20                             01 31'RISK PARTICIPATIONS SOLD'
     A** End of change 200044
     A                                      DSPATR(UL HI)
     A  19
     AO 20                             02 36'DETAILS ENQUIRY'
     A                                      DSPATR(UL HI)
      *                                              End   /CLE005/
     A            SRUNA     R           1 73REFFLD(RUNA)
     A            SLSTS         20A     2  2DSPATR(HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'Customer'
     A            SCRNM         20A     4 11DSPATR(HI)
     A            SCNUM          6A     4 32DSPATR(HI)
     A                                  4 41'Original Loan No.'
     A            SOLNO          6A     4 60DSPATR(HI)
     A  31                              4 69'Branch'
     A  31        SBRCA          3A     4 76DSPATR(HI)
      *                                              Begin /147830/
     A                                  5  1'Original Borrower'
     A            SORGB          6A     5 19DSPATR(HI)
     A            SRPTN         20A     5 27DSPATR(HI)
      *                                              End   /147830/
     A                                  5 66'Book Code'
     A            SBOKC          2A     5 76DSPATR(HI)
     A                                  6  1'Loan no'
     A            SLNRF          6A     6 11DSPATR(HI)
     A                                  6 21'Type'
     A            SLNTD         30A     6 26DSPATR(HI)
     A                                  6 66'Proc.type '
     A            SPTYP          2A     6 78DSPATR(HI)
     A                                  7  1'Deal date'
     A            SDDAT          7A     7 11DSPATR(HI)
     A                                  7 20'Start date'
     A            SVDAT          7A     7 32DSPATR(HI)
     A                                  7 40'Maturity date'
     A            SMDAT          7A     7 55DSPATR(HI)
     A                                  7 63'Recourse ind'
     A            SRCSI          1A     7 76DSPATR(HI)
     A                                  8  1'Currency'
     A            SCCY           3A     8 11DSPATR(HI)
      **Start of change CLE036
     A**********                        8 26'Amount'                                          CLE036
     A**********  SCPAM         20A     8 39DSPATR(HI)                                        CLE036
     A**********                        9 26'Amount sold'                                     CLE036
     A**********  SPSAM         20A     9 39DSPATR(HI)                                        CLE036
     A**********                       10  1'Interest rate'                                   CLE036
     A**********  SINTR         13A    10 16DSPATR(HI)                                        CLE036
     A**********                       10 31'Base rate'                                       CLE036
     A**********  SBRTT          2A    10 42DSPATR(HI)                                        CLE036
      **Start of change CER020
     A**********                        8 31'Amount'                                          CER020
     A**********  SCPAM         20A     8 44DSPATR(HI)                                        CER020
     A                                  8 15'Amount'
     A            SCPAM         20A     8 22DSPATR(HI)
      **End of change CER020
     A                                  9  1'Interest rate'
     A            SINTR         13A     9 16DSPATR(HI)
     A                                  9 31'Amount sold'
     A            SPSAM         20A     9 44DSPATR(HI)
     A  66                             10  1'Base rate'
     A            SBASR         13A    10 16DSPATR(HI)
     A N66                                  DSPATR(ND)
     A N66                             10 31'Base rate'
     A  66                             10 31'Base rate code'
     A            SBRTT          2A    10 46DSPATR(HI)
      **End of change CLE036
     A                                 10 54'Change ind'
     A            SCHIN          1A    10 66DSPATR(HI)
     A                                 10 69'Basis'
     A            SICBS          1A    10 76DSPATR(HI)
     A                                 11  1'Int capitalized'
     A            SINTC          3A    11 21DSPATR(HI)
     A                                 11 31'Next int date'
     A            SNIDT          7A    11 46DSPATR(HI)
     A                                 11 54'Freq'
     A            SIPFR         15A    11 59DSPATR(HI)
     A            SIFDY          4A    11 76DSPATR(HI)
     A                                 12  1'Outstanding interest +
     A                                      to date'
     A            SOINT         20A    12 31DSPATR(HI)
     A                                 12 54'Accrue'
     A            SADDI          1A    12 61DSPATR(HI)
     A                                 12 64'Round-down'
     A            SRDFC          1A    12 75DSPATR(HI)
     A                                 13  1'Interest paid to date'
     A            SIPRD         20A    13 31DSPATR(HI)
     A                                 14  1'W/holding tax rate'
     A            SWTRT         12A    14 21DSPATR(HI)
     A                                 14 39'Type'
     A            SWTIN          1A    14 45DSPATR(HI)
     A                                 15  1'Margin Rate'
     A            SMARG          9A    15 21DSPATR(HI)
     A  28                             15 32'Portfolio Reference'
     A  28        SPTFR          4A    15 52DSPATR(HI)
     A                                 16  1'Facility cust'
     A            SFCUS          6A    16 17DSPATR(HI)
     A                                 16 25'No'
     A            SFACT          6A    16 28DSPATR(HI)
     A N33                             16 41'Discount amount'
     A N33        STOTI         20A    16 57DSPATR(HI)
     A                                 17  1'Account officer'
     A            SACOF          2A    17 17DSPATR(HI)
     A            SACON         30A    17 21DSPATR(HI)
     A                                 17 54'Alternate n/a ref'
     A            SNACD          1A    17 73DSPATR(HI)
     A                                 18  1'Country of risk'
     A            SCRSK          2A    18 17DSPATR(HI)
     A            SCNNM         30A    18 21DSPATR(HI)
     A                                 18 54'Billing ind'
     A            SBTIN          1A    18 73DSPATR(HI)
     A                                 19  1'Industry'
     A            SLIND          3A    19 17DSPATR(HI)
     A            SLINA         30A    19 21DSPATR(HI)
     A                                 19 54'Billing days'
     A            SBLDY          2A    19 73DSPATR(HI)
     A  29                             20  1'Prof.Cent.'
     A  29        SPRFC          4A    20 12DSPATR(HI)
     A  29        SPCDS         30A    20 17DSPATR(HI)
     A  30                             20 48'Orig.Branch'
     A  30        SORBR          3A    20 60DSPATR(HI)
     A                                 20 64'Frb code'
     A            SFRBC          5A    20 73DSPATR(HI)
     A** Start of change CAS001
     A                                 21 01'Yield Curve'
     A  81                                  DSPATR(ND)
     A            #1YLDC         5   O 21 17TEXT('Yield Curve')
     A  81                                  DSPATR(ND)
     A N81                                  DSPATR(HI)
     A                                 21 31'Forward Yield Curve'
     A  81                                  DSPATR(ND)
     A            #1FYLD         5   O 21 52TEXT('Forward Yield Curve')
     A  81                                  DSPATR(ND)
     A N81                                  DSPATR(HI)
     A** End of change CAS001
     A                                 22  2'F3=Exit'
     A                                 22 11'F8=Fees summary'
     A                                 22 28'F12=Previous'
     A  27                             22 42'F14=Settlements'
     A  32                             22 59'F19=Funding info'
     A                                 23  2'Or rollup for next screen'
      **Start of Change CER020
     A                                  8 43'Ins Str Dt'
     A N92                                  DSPATR(ND)
     A            SINSDT         7A  O  8 54
     A N92                                  DSPATR(ND)
     A                                      DSPATR(HI)
      *
     A                                  8 62'Fin Mat Dt'
     A N92                                  DSPATR(ND)
     A            SFNMDT         7A  O  8 73
     A N92                                  DSPATR(ND)
     A                                      DSPATR(HI)
      **End of Change CER020
      ** Start of change FCM101
     A N10
     AA 12                             14 48'Base Rate Floor'
     A N10
     AA 11                             14 48'All-in Rate Floor'
     A            SFLOOR        13A    14 66DSPATR(HI)
     A  10                                  DSPATR(ND)
      ** End of change FCM101
      *
      * Second detail screen
      *
     A          R LE0191F2
     A                                      ROLLUP(70)
     A                                      ROLLDOWN(68)
     A  24 26                           1  5'REPAY'
     A                                      DSPATR(RI)
      *                                              Begin /CLE005/
     A***********                       1 23'PARTICIPATIONS SOLD DETAILS -
     A***********                           ENQUIRY'
     A***********                           DSPATR(UL HI)
     A N19N20                           1 23'PARTICIPATIONS SOLD DETAILS -
     A                                      ENQUIRY'
     A                                      DSPATR(UL HI)
     A  19                             01 26'PARTICIPATIONS SOLD IN A-
     A                                       CALL LOAN'
     A                                      DSPATR(UL HI)
     A** Start of change 200044
     A**20******                       01 26'PARTICIPATIONS SOLD IN A-                        200044
     A**********                             RISK LOAN'                                       200044
     A  20                             01 31'RISK PARTICIPATIONS SOLD'
     A** End of change 200044
     A                                      DSPATR(UL HI)
     A  19
     AO 20                             02 36'DETAILS ENQUIRY'
     A                                      DSPATR(UL HI)
      *                                              End   /CLE005/
     A            SRUNA     R           1 73REFFLD(RUNA)
     A                                  2  2'Rollover details-'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 11'Multi-currency allowed'
     A            SMCRA          3A     3 35DSPATR(HI)
     A                                  4 11'Rollover date'
     A            SRLDT          7A     4 25DSPATR(HI)
     A                                  4 38'Freq'
     A            SRLFQ         15A     4 43DSPATR(HI)
     A            SRLDY          4A     4 60DSPATR(HI)
     A                                  5 11'New int rate'
     A            SNINT         13A     5 24DSPATR(HI)
      **Start of change CLE036
     A**********                        5 38'Base rate'                                       CLE036
     A**********  SNBRA          2A     5 48DSPATR(HI)                                        CLE036
     A**********                        5 55'Basis'                                           CLE036
     A**********  SNCAS          1A     5 62DSPATR(HI)                                        CLE036
     A N66                              5 38'Base rate'
     A  66                              5 38'Base rate code'
     A            SNBRA          2A     5 53DSPATR(HI)
     A  66                              5 56'Base rate'
     A            SNBSR         13A     5 66DSPATR(HI)
     A N66                                  DSPATR(ND)
      **End of change CLE036
     A                                  6 11'Next Rollover Date'
     A            SNROD          7A     6 31DSPATR(HI)
     A                                  6 39'New Principal'
     A            SNPAM         20A     6 53DSPATR(HI)
     A                                  7 11'New Discount'
     A            SNDAM         20A     7 24DSPATR(HI)
      **Start of change CLE036
     A                                  7 55'Basis'
     A            SNCAS          1A     7 62DSPATR(HI)
      **End of change CLE036
     A                                  8  2'Syndication-'
      *                                              Begin /187261/
     A N15                                  DSPATR(ND)
      *                                              End   /187261/
     A                                  8 16'Default Parts Loan type/+
     A                                       subtype'
      *                                              Begin /187261/
     A N15                                  DSPATR(ND)
      *                                              End   /187261/
      *                                              Start /CLE042/
     A************SDLTS**********5A*****8 48DSPATR(HI)
     A            SDLTS          9A     8 48DSPATR(HI)
      *                                              End   /CLE042/
      *                                              Begin /187261/
     A N15                                  DSPATR(ND)
      *                                              End   /187261/
      *                                              Start /CLE042/
     A**********************************8 54'Linked to syndicate'
     A                                  8 58'Linked to syndicate'
      *                                              End   /CLE042/
      *                                              Begin /187261/
     A N15                                  DSPATR(ND)
      *                                              End   /187261/
      *                                              Start /CLE042/
     A************SLNKS**********3A*****8 74DSPATR(HI)
     A            SLNKS          3A     8 78DSPATR(HI)
      *                                              End   /CLE042/
      *                                              Begin /187261/
     A N15                                  DSPATR(ND)
      *                                              End   /187261/
     A                                  9  2'Repayment details-'
     A                                 10 11'Repayment date'
     A            SNRPD          7A    10 26DSPATR(HI)
     A                                 10 38'Freq'
     A            SRFRQ         15A    10 43DSPATR(HI)
     A            SRDYN          4A    10 60DSPATR(HI)
     A                                 11 11'Loan ccy repayment amount'
     A            SRAMT         17A    11 38DSPATR(HI)
     A                                 12 11'Fclty ccy repayment amount'
     A            SFCRA         17A    12 38DSPATR(HI)
     A                                 13 11'Schedule'
     A            SRSCH          3A    13 20DSPATR(HI)
     A                                 13 31'Repayment type'
     A            SREPT          1A    13 46DSPATR(HI)
     A                                 13 56'Auto Settle'
     A            SAUTO          3A    13 68DSPATR(HI)
     A                                 14 11'Total repayment past due'
     A            SRPPD         17A    14 38DSPATR(HI)
     A                                 16  1'Base currency equiv'
     A            SBCXR         14A    16 21DSPATR(HI)
     A                                 16 38'Facility currency equiv'
     A            SFCXR         14A    16 63DSPATR(HI)
     A  24                             17  2'Cust Conf'
     A  24        SCOCU         10A    17 12DSPATR(HI)
     A  24                             17 24'Auto Pay'
     A  24        SAPMI          3A    17 33DSPATR(HI)
     A  25                             17 39'Receiver Name'
     A  25        SRECE         10A    17 53DSPATR(HI)
     A  25                             17 64'Town'
     A  25        SCRTN         10A    17 69DSPATR(HI)
     A  24                             18  2'Original Maturity Date'
     A  24        SOMDT          7A    18 26DSPATR(HI)
     A                                 18 39'Generation'
     A            SMNSG          6A    18 50DSPATR(HI)
     A            SGAPT         21A    18 58DSPATR(HI)
     A  34                             19  2'Payment due day'
     A  34        SPDDI          8A    19 19DSPATR(HI)
     A  34                             19 29'Payment taken day'
     A  34        SPTDI          8A    19 48DSPATR(HI)
     A  34                             20  2'Grace days for'
     A  34                             20 19'Principal repayment'
     A  34        SGDPR          2A    20 39DSPATR(HI)
     A  34                             20 43'Interest repayment'
     A  34        SGDIN          2A    20 62DSPATR(HI)
      **Start of change CLE034
     A                                 21 43'Settlement Allocation'
     A N78                                  DSPATR(ND)
     A            SSTAL          3A  O 21 65DSPATR(HI)
     A N78                                  DSPATR(ND)
      **End of change CLE034
     A                                 22  2'F3=Exit'
     A                                 22 11'F8=Fees summary'
     A                                 22 28'F12=Previous'
     A  27                             22 42'F14=Settlements'
     A  32                             22 59'F19=Funding info'
     A                                 23  2'Or rollup for next screen, +
     A                                      rolldown for previous screen'
     A**Start of change CLE011
     A                                 03 59'Rate Fixing Days'
     A N40                                  DSPATR(ND)
     A            SRFDY          2A  O 03 77DSPATR(HI)
     A N40                                  DSPATR(ND)
      *
     A                                 04 66'Rate Fixed'
     A N40                                  DSPATR(ND)
     A            SRFRI          1A    04 78DSPATR(HI)
     A N40                                  DSPATR(ND)
     A**End of change CLE011
      *
      * Third detail screen
      *
     A          R LE0191F3
     A                                      ROLLDOWN(69)
      *
     A  24 26                           1  5'REPAY'
     A                                      DSPATR(RI)
      *                                              Begin /CLE005/
     A***********                       1 23'PARTICIPATIONS SOLD DETAILS -
     A***********                           ENQUIRY'
     A***********                           DSPATR(UL HI)
     A N19N20                           1 23'PARTICIPATIONS SOLD DETAILS -
     A                                      ENQUIRY'
     A                                      DSPATR(UL HI)
     A  19                             01 26'PARTICIPATIONS SOLD IN A-
     A                                       CALL LOAN'
     A                                      DSPATR(UL HI)
     A** Start of change 200044
     A**20******                       01 26'PARTICIPATIONS SOLD IN A-                        200044
     A**********                             RISK LOAN'                                       200044
     A  20                             01 31'RISK PARTICIPATIONS SOLD'
     A** End of change 200044
     A                                      DSPATR(UL HI)
     A  19
     AO 20                             02 36'DETAILS ENQUIRY'
     A                                      DSPATR(UL HI)
      *                                              End   /CLE005/
     A            SRUNA     R           1 73REFFLD(RUNA)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Customer'
     A            SCRNM         20A     4 11DSPATR(HI)
     A                                  4 34'No'
     A            SCNUM          6A     4 37DSPATR(HI)
     A                                  4 51'Loan no'
     A            SLNRF          6A     4 59DSPATR(HI)
     A                                  6  2'Loan narr'
     A            SNAR1         35A     6 13DSPATR(HI)
     A            SNAR2         35A     7 13DSPATR(HI)
     A            SNAR3         35A     8 13DSPATR(HI)
     A            SNAR4         35A     9 13DSPATR(HI)
     A N27                             11  2'Settlement instructions'
     A N27                             12  6'Start - type'
     A N27        SSDST          2A    12 19DSPATR(HI)
     A N27                             12 23'Ours'
     A**Start of change CGL029
     A*N27******  SOSDA         12A    12 28DSPATR(HI)                                        CGL029
     A*N27*31***                       12 41'Br'                                              CGL029
     A*N27*31***  SOSDB          3A    12 44DSPATR(HI)                                        CGL029
     A*N27******                       12 49'Theirs'                                          CGL029
     A*N27******  STSTN         10A    12 56DSPATR(HI)                                        CGL029
     A N27        SOSDA         18A    12 28DSPATR(HI)
     A N27 31                          12 47'Br'
     A N27 31     SOSDB          3A    12 50DSPATR(HI)
     A N27                             12 55'Theirs'
     A N27        STSTN         10A    12 62DSPATR(HI)
     A**End of change CGL029
     A N27                             13  3'Maturity - type'
     A N27        SMDST          2A    13 19DSPATR(HI)
     A N27                             13 23'Ours'
     A**Start of change CGL029
     A*N27******  SOMDA         12A    13 28DSPATR(HI)                                        CGL029
     A*N27*31***                       13 41'Br'                                              CGL029
     A*N27*31***  SOMDB          3A    13 44DSPATR(HI)                                        CGL029
     A*N27******                       13 49'Theirs'                                          CGL029
     A*N27******  STMAN         10A    13 56DSPATR(HI)                                        CGL029
     A N27        SOMDA         18A    13 28DSPATR(HI)
     A N27 31                          13 47'Br'
     A N27 31     SOMDB          3A    13 50DSPATR(HI)
     A N27                             13 55'Theirs'
     A N27        STMAN         10A    13 62DSPATR(HI)
     A**End of change CGL029
     A N27                             14 17'For account of'
     A N27        SFACO         10A    14 32DSPATR(HI)
     A N27                             15 11'Special instructions'
     A N27        SSPI1         30A    15 32DSPATR(HI)
     A N27        SSPI2         30A    16 32DSPATR(HI)
     A N27        SSPI3         30A    17 32DSPATR(HI)
      *                                              Begin /CEU004/
     A  34 35                          18 11'Settlement Currency'
     A  34 35     SSCCY          3A    18 35DSPATR(HI)
     A  34 35                          19 11'''In'' Ccy for Fields 72'
     A  34 35     SICCY          3A    19 35DSPATR(HI)
      *                                              End   /CEU004/
     A                                 22  2'F3=Exit'
     A                                 22 11'F8=Fees summary'
     A                                 22 28'F12=Previous'
     A  27                             22 42'F14=Settlements'
     A  32                             22 59'F19=Funding info'
     A                                 23  2'Or rolldown for previous screen'
      *
      * Footer for subfile screen
      *
     A          R LE0191F4
     A                                      OVERLAY
     A                                 22  2'E-Enquire'
     A                                 23  2'F3=Exit'
     A                                 23 12'F12=Previous screen'
      *
      * No subfile record to display
      *
     A          R LE0191F5                  TEXT('Program messages')
     A                                      OVERLAY
     A  71                             13 22'** No Participations +
     A                                      Sold exist **'
     A                                      DSPATR(HI)
      *
      * Subfile of loan records
      *
     A          R LE0191S0
     A                                      SFL
     A  76                                  SFLNXTCHG
     A            SSBRC          3   H
      *
     A            SLSEL          1   B  8  4
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI UL)
     A            SLNRF          6      8 13DSPATR(HI)
      *                                              Begin /147830/
     A            SSRISK         4      8 21DSPATR(HI)
      *                                              End   /147830/
     A            SSMDT          7      8 29DSPATR(HI)
     A            SSAMT         15      8 41DSPATR(HI)
     A            SSRCI          3      8 61DSPATR(HI)
     A            SSLNK          3      8 71DSPATR(HI)
      *
      * Subfile control
      *
     A          R LE0191C0
     A                                      SFLCTL(LE0191S0)
     A N72N71                               SFLDSP
     A N72                                  SFLDSPCTL
     A  72                                  SFLCLR
     A                                      SFLPAG(13)
     A                                      SFLSIZ(14)
     A N72 77                               SFLEND(*MORE)
     A                                      OVERLAY
     A            SRNBR          4S 0H      SFLRCDNBR
      *
     A                                  6  3'Sel'
     A                                  6  8'Participation Sold'
     A                                  6 29'Maturity'
     A                                  6 49'Amount'
     A                                  6 58'Recourse'
     A                                  6 68'Linked to'
      *                                              Begin /187261/
     A N15                                  DSPATR(ND)
      *                                              End   /187261/
      *
     A                                  7 12'Reference'
     A                                  7 31'Date'
     A                                  7 68'Syndicate'
      *                                              Begin /187261/
     A N15                                  DSPATR(ND)
      *                                              End   /187261/
      *
      * Message subfile record
      *
     A          R LE0191S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * Message subfile control
      *
     A          R LE0191C1                  TEXT('Program messages')
     A                                      SFLCTL(LE0191S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  75
     AON75                                  SFLEND
     A            ##PGM                     SFLPGMQ