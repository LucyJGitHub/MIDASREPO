      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Loans/Parts Purc/ Parts Sold - PUMA')         *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE0193DF - Loans/Parts Purchased/Parts Sold                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. LUC139             Date 08Jan21               *
      *  Prev Amend No. CER009  *CREATE    Date 16Aug06               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  LUC139 - UCI Italian features upgrade to FM2.1               *
      *  CER009 - Puma II Upgrade to MidasPlus                        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CF12
      *
     A          R LE0193F0
     A                                      TEXT('(c) Misys International -
     A                                      Banking Systems Ltd. 2006')
     A                                      OVERLAY
      *
     A            #0USER        10A  O  1  2
     A            #0JOB         10A  O  2  2
     A            #0PGM         10A  O  1 13
     A                                  1 67'Date'
     A            BJMRDT         7A  O  1 73
     A                                  2 67'Time'
     A                                  2 72TIME
      *
     A                                  2 29'Customer Loans - Italian'
     A                                      DSPATR(HI)
      *
     A                                  4  2'Motivation of Operation . . . :'
     A            #0MOOP         1A  O  4 34DSPATR(HI)
      *
     A                                  4 44'Agricul. Credit Class.  :'
     A            #0AGCT         3A  O  4 70DSPATR(HI)
      *
     A                                  5  2'Agricultural Laws Class.  . . :'
     A            #0AGCL         3A  O  5 34DSPATR(HI)
      *
     A                                  5 44'Econom. Branch of Dest. :'
     A            #0ECBD         2A  O  5 70DSPATR(HI)
      *
     A                                  6  2'Subsidized Credit Indicator . :'
     A            #0SBCR         1A  O  6 34DSPATR(HI)
      *
     A                                  6 44'Type of Product . . . . :'
     A            #0TPPR         6A  O  6 70DSPATR(HI)
      *
     A                                  7  2'Subsidized Int. for Export  . :'
     A            #0INSB         1A  O  7 34DSPATR(HI)
      *
     A                                  7 44'Personalisation of Oper.:'
     A            #0PEPR         6A  O  7 70DSPATR(HI)
      *
     A                                  8  2'Capofila (valutaria)  . . . . :'
     A            #0LEAD         6A  O  8 34DSPATR(HI)
      *
     A                                  8 44'Global/Partial Operation:'
     A            #0GLPA         1A  O  8 70DSPATR(HI)
      *
     A                                  9  2'Law for Subsidized Credit Cd  :'
     A            #0SBLW         3A  O  9 34DSPATR(HI)
      *
     A                                  9 44'Oper. with Third Party. :'
     A            #0TPFD         1A  O  9 70DSPATR(HI)
      *
     A                                 10  2'Destination Prov. Investment  :'
     A            #0DPIV         5A  O 10 34DSPATR(HI)
      *
     A                                 10 44'556 - Tipo attivit{ CR  :'
     A            #0TPAC         3A  O 10 70DSPATR(HI)
      *
     A                                 11  2'Economic. Dest. of Investment :'
     A            #0EDIV         3A  O 11 34DSPATR(HI)
      *
     A                                 11 44'557 - Ruolo affidato CR :'
     A            #0CLRL         3A  O 11 70DSPATR(HI)
      *
     A                                 12  2'564 - Tipo garanzia CR  . . . :'
     A            #0LNIT         7A  O 12 34DSPATR(HI)
      *
     A                                 12 44'563 - Stato rapporto CR :'
     A            #0ACST         3A  O 12 70DSPATR(HI)
      *
     A                                 13  2'206 - Categoria CR  . . . . :'
     A            #0CATC        11A  O 13 32DSPATR(HI)
      *
     A                                 13 44'5008- Scaduto dal . . . :'
     A            #0UNDT         6A  O 13 70DSPATR(HI)
      *
     A                                 14  2'Effective Global Rate . . . . :'
     A            #0TEGL         9A  O 14 34DSPATR(HI)
      *
     A                                 14 44'Quota agg.altrui:'
     A            #0PSAM        14A  O 14 62DSPATR(HI)
      *
      ** Start of LUC139
     A                                 15  2'Priority Risk Code Product  . :'
     A            #0PTRP         3A  O 15 34DSPATR(HI)
      *
     A                                 16  2'Com. Rate Max. Uncovered Bal. :'
     A            #0CMPC         9A  O 16 34DSPATR(HI)
      ** End of LUC139
      *
     A                                 23  2'F3=Exit   F12=Previous'
     A                                      COLOR(BLU)
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #0PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            #0PGM                     SFLPGMQ(10)
      *
