      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Customer portfolio')                          *
     A*****************************************************************
     A*  Midas - Customer Lending Module                              *
     A*                                                               *
     A*  LE0201FM - CUSTOMER PORTFOLIO ENQUIRY                        *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 29Nov05               *
      *                 CLE042             Date 06Sep05               *
      *                 BUG2620            Date 12May04               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CLE023             Date 19Nov01               *
      *                 199124             Date 11Dec01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 190009             Date 27Feb01               *
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 147830             Date 17Dec98               *
      *                 088131             Date 20Jun95               *
     A*                 072584                  DATE 20JUL94          *
     A*                 BH3422                  DATE 21SEP92          *
     A*                 S01311                  DATE 23JAN91          *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *  BUG2620- Condition reverse image attribute.                  *
      *  CLE023 - Lending Facility History Improvements.              *
      *  199124 - 'Synd Loan' column still being displayed eventhough *
      *           CLE006 (Syndication) is off.                        *
      *  190009 - MUI Customer Portfolio Enquiry Manual ID error.     *
      *           Amended to show to the screen the current record    *
      *           format instead of the program name.                 *
      *  147830 - New narrative on sfl display. Loans identified as   *
      *           Prime,Intnl,Part or Risk.                           *
     A*  088131 - For Guarantees Issued, screen should say 'Interest' *
     A*           instead of 'Fee' as this enquiry shows only the     *
     A*           interest rate and interest accrued, and does not    *
     A*           show details of any fees for the guarantee.         *
     A*  072584 - Position the cursor correctly when a field is       *
     A*           incorrect.                                          *
     A*  BH3422 - Help text amendments for Lending enhancements       *
     A*  S01311 - EXPOSURE MANAGEMENT ENQUIRIES                       *
     A*           - New program created to replace LE0200             *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CF03
     A*                                                        /BH3422/
     A*                                                        /BH3422/
     A*
     A* FIRST SCREEN
     A*
     A          R LE0201F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A  06                                  CF01(11)
     A  06                                  CF12(12)
     A            USER          10A  O 01 02
     A**Start of change 190009
     A**********                       01 13'LE0201'                      190009
     A                                 01 13'LE0201F0'
     A**End of change 190009
     A                                  1 29'CUSTOMER PORTFOLIO ENQUIRY'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O 01 72
     A            WSID          10A  O 02 02
     A                                 02 71TIME
     A  06                              4 11'Customer'
     A  06        SCNUM          6A  O  4 21
     A                                      DSPATR(HI)
     A  06        SCRNM         20A  O 04 30
     A                                      DSPATR(HI)
     A  06        SCRTN         10A  O 04 53
     A                                      DSPATR(HI)
     A                                  7 21'Loans'
     A            SMNULN         1A  B  7 60
     A N31                                  DSPATR(UL)
     A*********              Start of Change               /072584/
     A**31*****                             DSPATR(RI)
     A*********                             DSPATR(HI PC)
     A  31                                  DSPATR(RI PC)
     A                                      DSPATR(HI)
     A*                      End of change                 /072584/
     A                                  9 21'Participations Purchased'
     A            SMNUPP         1A  B  9 60
     A N32                                  DSPATR(UL)
     A*********              Start of Change               /072584/
     A**32*****                             DSPATR(RI)
     A  32                                  DSPATR(RI PC)
     A*                      End of change                 /072584/
     A                                      DSPATR(HI)
     A                                 11 21'Participations sold to'
     A            SMNUPS         1A  B 11 60
     A N33                                  DSPATR(UL)
     A*********              Start of Change               /072584/
     A**33*****                             DSPATR(RI)
     A  33                                  DSPATR(RI PC)
     A*                      End of change                 /072584/
     A                                      DSPATR(HI)
     A                                 13 21'Guarantees by'
     A            SMNUGB         1A  B 13 60
     A N34                                  DSPATR(UL)
     A*********              Start of Change               /072584/
     A**34*****                             DSPATR(RI)
     A  34                                  DSPATR(RI PC)
     A*                      End of change                 /072584/
     A                                      DSPATR(HI)
     A                                 15 21'Guarantees issued'
     A            SMNUGI         1A  B 15 60
     A N35                                  DSPATR(UL)
     A*********              Start of Change               /072584/
     A**35*****                             DSPATR(RI)
     A  35                                  DSPATR(RI PC)
     A*                      End of change                 /072584/
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,LE0201FM03
     A                                 17 21'Facilities'
     A            SMNUFC         1A  B 17 60
     A N36                                  DSPATR(UL)
     A*********              Start of Change               /072584/
     A**36*****                             DSPATR(RI)
     A  36                                  DSPATR(RI PC)
     A*                      End of change                 /072584/
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,LE0201FM04
     A                                 19 21'Branch Code'
     A N20                                  DSPATR(ND)
     A            SBRCD          3A  B 19 60
     A N37                                  DSPATR(UL)
     A  37                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A*********              Start of Change               /072584/
     A  37N39                               DSPATR(PC)
     A*                      End of change                 /072584/
     A N20                                  DSPATR(ND PR)
     A                                 20 21'Company Code'
     A N20                                  DSPATR(ND)
     A            SCOMP          3A  B 20 60
     A N38                                  DSPATR(UL)
     A  38                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A*********              Start of Change               /072584/
     A  38N39                               DSPATR(PC)
     A*                      End of change                 /072584/
     A N20                                  DSPATR(ND PR)
     A            MSG           79A  O 22  2
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(HI)
     A  06                             23 02'F1=Initial Enquiry   F3=Exit   -
     A                                      F12=Previous'
     A N06                             23 02'F3=Exit'
     A                                 24 02'Customer Number or Shortname'
     A            SCUST         10A  B 24 33
     A N39                                  DSPATR(UL)
     A*********              Start of Change               /072584/
     A**39*****                             DSPATR(RI)
     A  39                                  DSPATR(RI PC)
     A*                      End of change                 /072584/
     A                                      DSPATR(HI)
     A*
     A* SECOND SCREENS
     A*
     A* LOANS HEADINGS
     A*
     A          R LE0201F1
     A            USER          10A  O 01 02
     A**Start of change 190009
     A**********                       01 13'LE0201'                      190009
     A                                 01 13'LE0201F1'
     A**End of change 190009
     A                                  1 29'CUSTOMER PORTFOLIO ENQUIRY'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O 01 72
     A            WSID          10A  O 02 02
     A                                 02 39'LOANS'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 02 71TIME
     A            BRTTL         40A  O 04 21
     A N20                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 06 06'Customer'
     A            BBCRNM        20A  O 06 16
     A                                      DSPATR(HI)
     A            BBCRTN        10A  O 06 38
     A                                      DSPATR(HI)
     A            BBCUST         6A  O 07 16
     A                                      DSPATR(HI)
     A                                 08 15'Loan Type'
      *                                              Begin /147830/
     A**Start of change 199124
     A**********                       08 25'Synd'                                            199124
     A  26                             08 25'Synd'
     A**End of change 199124
     A***********                      08 26'Current'
     A                                 08 34'Current'
     A***********                      08 42'Daily'
     A                                 08 49'Daily'
     A***********                      08 58'Accrued'
     A                                 08 60'Accrued'
      *                                              End   /147830/
     A                                 08 71'Part'
     A                                 08 76'Mat.'
     A                                 09 05'Loan'
     A                                 09 11'Ccy'
      *                                              Begin /147830/
     A***********                      09 15'/Sub Type'
     A                                 09 15'Sub Type'
     A**Start of change 199124
     A**********                       09 25'Loan'                                            199124
     A  26                             09 25'Loan'
     A**End of change 199124
     A***********                      09 25'Principal'
     A                                 09 33'Principal'
     A***********                      09 38'Interest Amount'
     A                                 09 47'Int. Amt.'
     A***********                      09 53'Interest to Date'
     A                                 09 58'Int. to Date'
      *                                              End   /147830/
     A                                 09 71'Sold'
     A                                 09 76'Date'
     A*
     A* LOANS SUBFILE
     A*
     A          R LE0201S1                  SFL
     A*/COPY WNCPYSRC,LE0201FM01
     A            OPTN           1A  B 10 02DSPATR(UL HI PC)
      **Start of change CLE148
     A**********  LNNO           6S 0O 10 04DSPATR(HI)                                        CLE148
     A            LNNO           6A  O 10 04DSPATR(HI)
      **End of change CLE148
     A            CCY            3A  O 10 11DSPATR(HI)
      *                                              Begin /147830/
     A************SLTST          5A  O 10 15DSPATR(HI)
     A************SCPAM         16A  O 10 21DSPATR(HI)
     A************SDINT         15A  O 10 38DSPATR(HI)
     A************SAINT         16A  O 10 55DSPATR(HI)
      *                                              Begin /CLE042/
     A************SLTST**********5A  O 10 17DSPATR(HI)
     A************SLNAR**********5A  O 10 24DSPATR(HI)
     A            SLTST          9A  O 10 15DSPATR(HI)
     A            SLNAR          4A  O 10 25DSPATR(HI)
      *                                              End   /CLE042/
     A N23        SCPAM         16A  O 10 30DSPATR(HI)
     A  23                           O 10 30'***************'
     A N24        SDINT         10A  O 10 47DSPATR(HI)
     A  24                           O 10 47'*********'
     A N25        SAINT         13A  O 10 58DSPATR(HI)
     A  25                           O 10 58'************'
      *                                              End   /147830/
     A            SPART          1A  O 10 72DSPATR(HI)
     A            MDATE          7A  O 10 74DSPATR(HI)
     A*/COPY WNCPYSRC,LE0201FM02
     A*
     A* LOANS SUBFILE CONTROL
     A*
     A          R LE0201C1                  SFLCTL(LE0201S1)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A                                      BLINK
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A N41N42                               SFLCLR
     A  60                                  SFLEND
     A  45                                  ROLLUP(18)
     A  46                                  ROLLDOWN(19)
     A                                      CF01(21)
     A                                      CF12(22)
     A                                      OVERLAY
      **Start of change BUG2620
     A**********  MSG           79A  O 22  2DSPATR(RI)                                       BUG2620
     A            MSG           79A  O 22  2
     A  30                                  DSPATR(RI)
      **End of change BUG2620
     A                                      DSPATR(HI)
     A N30                                  DSPATR(ND)
     A                                 23 02'F1=Initial Enquiry   F3=Exit   -
     A                                      F12=Previous'
     A  45N46                          23 48'Rollup'
     A  45 46                          23 48'Rollup   Rolldown'
     A N45 46                          23 48'Rolldown'
     A                                 24 02'Customer Number or Shortname'
     A            SCUST         10A  B 24 33
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A                                 24 47'Branch'
     A N20                                  DSPATR(ND)
     A            SBRCD          3A  B 24 55
     A  20N37                               DSPATR(UL)
     A  37                                  DSPATR(RI)
     A N20                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
     A                                 24 62'Company'
     A N20                                  DSPATR(ND)
     A            SCOMP          3A  B 24 71
     A  20N38                               DSPATR(UL)
     A  38                                  DSPATR(RI)
     A N20                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
     A*
     A* PARTICIPATIONS PURCHASED HEADINGS
     A*
     A          R LE0201F2
     A            USER          10A  O 01 02
     A**Start of change 190009
     A**********                       01 13'LE0201'                      190009
     A                                 01 13'LE0201F2'
     A**End of change 190009
     A                                  1 29'CUSTOMER PORTFOLIO ENQUIRY'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O 01 72
     A            WSID          10A  O 02 02
     A                                 02 30'PARTICIPATIONS PURCHASED'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 02 71TIME
     A            BRTTL         40A  O 04 21
     A N20                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 06 06'Customer'
     A            BBCRNM        20A  O 06 16
     A                                      DSPATR(HI)
     A            BBCRTN        10A  O 06 38
     A                                      DSPATR(HI)
     A            BBCUST         6A  O 07 16
     A                                      DSPATR(HI)
      *                                              Begin /147830/
     A**Start of change 199124
     A**********                       08 25'Synd'                                            199124
     A  26                             08 25'Synd'
     A**End of change 199124
     A***********                      08 28'Current'
     A                                 08 34'Current'
     A***********                      08 42'Daily'
     A                                 08 49'Daily'
     A***********                      08 57'Accrued'
     A                                 08 60'Accrued'
     A***********                      08 70'Part'
     A                                 08 71'Part'
     A***********                      08 75'Mat.'
     A                                 08 76'Mat.'
      *                                              End   /147830/
     A                                 09 03'Customer'
     A                                 09 12'Loan'
     A                                 09 18'Ccy'
      *                                              Begin /147830/
     A**Start of change 199124
     A**********                       09 25'Loan'                                            199124
     A  26                             09 25'Loan'
     A**End of change 199124
     A***********                      09 27'Principal'
     A                                 09 33'Principal'
     A***********                      09 37'Interest Amount'
     A                                 09 47'Int. Amt.'
     A***********                      09 53'Interest to Date'
     A                                 09 58'Int. to Date'
     A***********                      09 70'Sold'
     A                                 09 71'Sold'
     A***********                      09 75'Date'
     A                                 09 76'Date'
      *                                              End   /147830/
     A*
     A* PARTICIPATIONS PURCHASED SUBFILE
     A*
     A          R LE0201S2                  SFL
     A            OPTN           1A  B 10 02DSPATR(UL HI PC)
      * Begin /CSD027/
     A**********  OLNO           6S 0O 10 04DSPATR(HI)                                        CSD027
     A            OLNO           6A  O 10 04DSPATR(HI)
      * End /CSD027/
      **Start of change CLE148
     A**********  LNNO           6S 0O 10 11DSPATR(HI)                                        CLE148
     A            LNNO           6A  O 10 11DSPATR(HI)
      **End of change CLE148
     A            CCY            3A  O 10 18DSPATR(HI)
      *                                              Begin /147830/
      *                                              Begin /CLE042/
     A************SLNAR**********5A**O 10 24DSPATR(HI)
     A            SLNAR          4A  O 10 24DSPATR(HI)
      *                                              End   /CLE042/
     A************SCPAM         16A  O 10 22DSPATR(HI)
     A N23        SCPAM         16A  O 10 30DSPATR(HI)
     A  23                           O 10 30'***************'
     A************SDINT         15A  O 10 39DSPATR(HI)
     A N24        SDINT         10A  O 10 47DSPATR(HI)
     A  24                           O 10 47'*********'
     A************SAINT         16A  O 10 55DSPATR(HI)
     A N25        SAINT         13A  O 10 58DSPATR(HI)
     A  25                           O 10 58'************'
      *                                              End   /147830/
     A            SPART          1A  O 10 72DSPATR(HI)
     A            MDATE          7A  O 10 74DSPATR(HI)
     A*
     A* PARTICIPATIONS PURCHASED SUBFILE CONTROL
     A*
     A          R LE0201C2                  SFLCTL(LE0201S2)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A                                      BLINK
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A N41N42                               SFLCLR
     A  60                                  SFLEND
     A  45                                  ROLLUP(18)
     A  46                                  ROLLDOWN(19)
     A                                      CF01(21)
     A                                      CF12(22)
     A                                      OVERLAY
     A            MSG           79A  O 22  2DSPATR(RI)
     A                                      DSPATR(HI)
     A N30                                  DSPATR(ND)
     A                                 23 02'F1=Initial Enquiry   F3=Exit   -
     A                                      F12=Previous'
     A  45N46                          23 48'Rollup'
     A  45 46                          23 48'Rollup   Rolldown'
     A N45 46                          23 48'Rolldown'
     A                                 24 02'Customer Number or Shortname'
     A            SCUST         10A  B 24 33
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A                                 24 47'Branch'
     A N20                                  DSPATR(ND)
     A            SBRCD          3A  B 24 55
     A  20N37                               DSPATR(UL)
     A  37                                  DSPATR(RI)
     A N20                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
     A                                 24 62'Company'
     A N20                                  DSPATR(ND)
     A            SCOMP          3A  B 24 71
     A  20N38                               DSPATR(UL)
     A  38                                  DSPATR(RI)
     A N20                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
     A*
     A* PARTICIPATIONS SOLD HEADINGS
     A*
     A          R LE0201F3
     A            USER          10A  O 01 02
     A**Start of change 190009
     A**********                       01 13'LE0201'                      190009
     A                                 01 13'LE0201F3'
     A**End of change 190009
     A                                  1 29'CUSTOMER PORTFOLIO ENQUIRY'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O 01 72
     A            WSID          10A  O 02 02
     A                                 02 32'PARTICIPATIONS SOLD'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 02 71TIME
     A            BRTTL         40A  O 04 21
     A N20                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 06 06'Customer'
     A            BBCRNM        20A  O 06 16
     A                                      DSPATR(HI)
     A            BBCRTN        10A  O 06 38
     A                                      DSPATR(HI)
     A            BBCUST         6A  O 07 16
     A                                      DSPATR(HI)
     A                                 08 10'Sale of'
      *                                              Begin /147830/
     A**Start of change 199124
     A**********                       08 25'Synd'                                            199124
     A  26                             08 25'Synd'
     A**End of change 199124
     A***********                      08 27'Current'
     A                                 08 34'Current'
     A***********                      08 45'Daily'
     A                                 08 49'Daily'
     A***********                      08 61'Accrued'
     A                                 08 60'Accrued'
      *                                              End   /147830/
     A                                 08 73'Maturity'
     A                                 09 05'Ref'
     A                                 09 12'Loan'
     A                                 09 18'Ccy'
      *                                              Begin /147830/
     A**Start of change 199124
     A**********                       09 25'Loan'                                            199124
     A  26                             09 25'Loan'
     A**End of change 199124
     A***********                      09 26'Principal'
     A                                 09 33'Principal'
     A***********                      09 40'Interest Amount'
     A                                 09 47'Int. Amt.'
     A***********                      09 57'Interest to Date'
     A                                 09 58'Int. to date'
      *                                              End   /147830/
     A                                 09 75'Date'
     A*
     A* PARTICIPATIONS SOLD SUBFILE
     A*
     A          R LE0201S3                  SFL
     A            OPTN           1A  B 10 02DSPATR(UL HI PC)
      **Start of change CLE148
     A**********  LNNO           6S 0O 10 04DSPATR(HI)                                        CLE148
     A            LNNO           6A  O 10 04DSPATR(HI)
      **End of change CLE148
      * Begin /CSD027/
     A**********  OLNO           6S 0O 10 11DSPATR(HI)                                        CSD027
     A            OLNO           6   O 10 11DSPATR(HI)
      * End /CSD027/
     A            CCY            3A  O 10 18DSPATR(HI)
      *                                              Begin /147830/
      *                                              Begin /CLE042/
     A************SLNAR**********5A**O*10 24DSPATR(HI)
     A            SLNAR          4A  O 10 24DSPATR(HI)
      *                                              End   /CLE042/
     A************SCPAM         16A  O 10 22DSPATR(HI)
     A N23        SCPAM         16A  O 10 30DSPATR(HI)
     A  23                           O 10 30'***************'
     A************SDINT         15A  O 10 40DSPATR(HI)
     A N24        SDINT         10A  O 10 47DSPATR(HI)
     A  24                           O 10 47'*********'
     A************SAINT         16A  O 10 56DSPATR(HI)
     A N25        SAINT         13A  O 10 58DSPATR(HI)
     A  25                           O 10 58'************'
      *                                              End   /147830/
     A            MDATE          7A  O 10 74DSPATR(HI)
     A*
     A* PARTICIPATIONS SOLD SUBFILE CONTROL
     A*
     A          R LE0201C3                  SFLCTL(LE0201S3)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A                                      BLINK
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A N41N42                               SFLCLR
     A  60                                  SFLEND
     A  45                                  ROLLUP(18)
     A  46                                  ROLLDOWN(19)
     A                                      CF01(21)
     A                                      CF12(22)
     A                                      OVERLAY
     A            MSG           79A  O 22  2DSPATR(RI)
     A                                      DSPATR(HI)
     A N30                                  DSPATR(ND)
     A                                 23 02'F1=Initial Enquiry   F3=Exit   -
     A                                      F12=Previous'
     A  45N46                          23 48'Rollup'
     A  45 46                          23 48'Rollup   Rolldown'
     A N45 46                          23 48'Rolldown'
     A                                 24 02'Customer Number or Shortname'
     A            SCUST         10A  B 24 33
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A                                 24 47'Branch'
     A N20                                  DSPATR(ND)
     A            SBRCD          3A  B 24 55
     A  20N37                               DSPATR(UL)
     A  37                                  DSPATR(RI)
     A N20                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
     A                                 24 62'Company'
     A N20                                  DSPATR(ND)
     A            SCOMP          3A  B 24 71
     A  20N38                               DSPATR(UL)
     A  38                                  DSPATR(RI)
     A N20                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
     A*
     A* GUARANTEES HEADINGS
     A*
     A          R LE0201F4
     A            USER          10A  O 01 02
     A**Start of change 190009
     A**********                       01 13'LE0201'                      190009
     A                                 01 13'LE0201F4'
     A**End of change 190009
     A                                  1 29'CUSTOMER PORTFOLIO ENQUIRY'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O 01 72
     A            WSID          10A  O 02 02
     A                                 02 37'GUARANTEES'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 02 71TIME
     A            BRTTL         40A  O 04 21
     A N20                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 06 06'Customer'
     A            BBCRNM        20A  O 06 16
     A                                      DSPATR(HI)
     A            BBCRTN        10A  O 06 38
     A                                      DSPATR(HI)
     A            BBCUST         6A  O 07 16
     A                                      DSPATR(HI)
     A                                 08 33'Customer'
     A                                 08 43'Loan/Flct'
     A**Start of change CLE023
     A  57                             08 56'Current Exposure'
     A**End of change CLE023
     A                                 09 04'Ref'
     A                                 09 13'G''tee amount'
     A                                 09 28'Ccy'
     A                                 09 34'G''teed'
     A                                 09 45'G''teed'
     A**Start of change CLE023
     A**********                       09 58'Outstanding'                                     CLE023
     A N57                             09 58'Outstanding'
     A  57                             09 58'at Spot Rate'
     A**End of change CLE023
     A*
     A* GUARANTEES SUBFILE
     A*
     A          R LE0201S4                  SFL
     A            GUSN           3S 0O 10 04DSPATR(HI)
     A            STAMT         17A  O 10 10DSPATR(HI)
     A            CCYG           3A  O 10 28DSPATR(HI)
      * Begin /CSD027/
     A**********  CNUM           6S 0O 10 34DSPATR(HI)                                        CSD027
     A            CNUM           6A  O 10 34DSPATR(HI)
      * End /CSD027/
     A            SFCLN          6A  O 10 44DSPATR(HI)
     A            SOSAMT        18A  O 10 54DSPATR(HI)
     A*
     A* GUARANTEES SUBFILE CONTROL
     A*
     A          R LE0201C4                  SFLCTL(LE0201S4)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A                                      BLINK
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A N41N42                               SFLCLR
     A  60                                  SFLEND
     A  45                                  ROLLUP(18)
     A  46                                  ROLLDOWN(19)
     A                                      CF01(21)
     A                                      CF12(22)
     A                                      OVERLAY
     A            MSG           79A  O 22  2DSPATR(RI)
     A                                      DSPATR(HI)
     A N30                                  DSPATR(ND)
     A                                 23 02'F1=Initial Enquiry   F3=Exit   -
     A                                      F12=Previous'
     A  45N46                          23 48'Rollup'
     A  45 46                          23 48'Rollup   Rolldown'
     A N45 46                          23 48'Rolldown'
     A                                 24 02'Customer Number or Shortname'
     A            SCUST         10A  B 24 33DSPATR(PC)
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A                                 24 47'Branch'
     A N20                                  DSPATR(ND)
     A            SBRCD          3A  B 24 55
     A  20N37                               DSPATR(UL)
     A  37                                  DSPATR(RI)
     A N20                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
     A                                 24 62'Company'
     A N20                                  DSPATR(ND)
     A            SCOMP          3A  B 24 71
     A  20N38                               DSPATR(UL)
     A  38                                  DSPATR(RI)
     A N20                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
     A*
     A* GUARANTEES ISSUED HEADINGS
     A*
     A          R LE0201F5
     A            USER          10A  O 01 02
     A**Start of change 190009
     A**********                       01 13'LE0201'                      190009
     A                                 01 13'LE0201F5'
     A**End of change 190009
     A                                  1 29'CUSTOMER PORTFOLIO ENQUIRY'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O 01 72
     A            WSID          10A  O 02 02
     A                                 02 33'GUARANTEES ISSUED'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 02 71TIME
     A            BRTTL         40A  O 04 21
     A N20                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 06 06'Customer'
     A            BBCRNM        20A  O 06 16
     A                                      DSPATR(HI)
     A            BBCRTN        10A  O 06 38
     A                                      DSPATR(HI)
     A            BBCUST         6A  O 07 16
     A                                      DSPATR(HI)
     A                                 08 15'Loan type'
     A                                 08 30'G''teed'
     A*                                              Begin 088131
     A************                     08 46'Fee'
     A************                     08 61'Fees'
     A                                 08 44'Interest'
     A                                 08 59'Interest'
     A*                                              End   088131
     A                                 08 73'Maturity'
     A                                 09 05'Loan'
     A                                 09 11'Ccy'
     A                                 09 15'/Sub type'
     A                                 09 30'Amount'
     A                                 09 46'Rate'
     A                                 09 60'Accrued'
     A                                 09 75'Date'
     A*
     A* GUARANTEES ISSUED SUBFILE
     A*
     A          R LE0201S5                  SFL
     A            OPTN           1A  B 10 02DSPATR(UL HI PC)
      **Start of change CLE148
     A**********  LNNO           6S 0O 10 04DSPATR(HI)                                        CLE148
     A            LNNO           6A  O 10 04DSPATR(HI)
      **End of change CLE148
     A            CCY            3A  O 10 11DSPATR(HI)
      *                                              Begin /CLE042/
     A************SLTST**********5A  O 10 17DSPATR(HI)
     A************SCPAM*********16A  O 10 24DSPATR(HI)
     A            SLTST          9A  O 10 15DSPATR(HI)
     A            SCPAM         16A  O 10 25DSPATR(HI)
      *                                              End   /CLE042/
     A            SFRATE         9A  O 10 43DSPATR(HI)
      *                                              Begin /147830/
     A************SAINT         16A  O 10 54DSPATR(HI)
     A            SAINT         13A  O 10 57DSPATR(HI)
      *                                              End   /147830/
     A            MDATE          7A  O 10 74DSPATR(HI)
     A*
     A* GUARANTEES ISSUED SUBFILE CONTROL
     A*
     A          R LE0201C5                  SFLCTL(LE0201S5)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A                                      BLINK
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A N41N42                               SFLCLR
     A  60                                  SFLEND
     A  45                                  ROLLUP(18)
     A  46                                  ROLLDOWN(19)
     A                                      CF01(21)
     A                                      CF12(22)
     A                                      OVERLAY
     A            MSG           79A  O 22  2DSPATR(RI)
     A                                      DSPATR(HI)
     A N30                                  DSPATR(ND)
     A                                 23 02'F1=Initial Enquiry   F3=Exit   -
     A                                      F12=Previous'
     A  45N46                          23 48'Rollup'
     A  45 46                          23 48'Rollup   Rolldown'
     A N45 46                          23 48'Rolldown'
     A                                 24 02'Customer Number or Shortname'
     A            SCUST         10A  B 24 33
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A                                 24 47'Branch'
     A N20                                  DSPATR(ND)
     A            SBRCD          3A  B 24 55
     A  20N37                               DSPATR(UL)
     A  37                                  DSPATR(RI)
     A N20                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
     A                                 24 62'Company'
     A N20                                  DSPATR(ND)
     A            SCOMP          3A  B 24 71
     A  20N38                               DSPATR(UL)
     A  38                                  DSPATR(RI)
     A N20                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
