      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Facility activity enquiry')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LE0211DF - Facility Activity Enquiry                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. BUG22126           Date 23Jan09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG8261            Date 01Sep05               *
      * Midas Release 4.01.03 ----------------------------------------*
      *                 214838             Date 12Feb03               *
      *                 201127             Date 20Jan03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 189570             Date 16Oct01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CLE014             Date 20Mar01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 187261             Date 16Jan01               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 176776             Date 18Apr00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 161412             Date 28May99               *
      *                 127658             Date 24Nov97               *
      *                 CLE005             Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,LEH00078                                 *
      *             WNCPYSRC,LEH00079                                 *
      *             WNCPYSRC,LEH00034                                 *
      *             WNCPYSRC,LEH00035                                 *
      *  BUG22126 - Include aggregate facilities in the Facility      *
      *             Activity Enquiry                                  *
      *  BUG8261 - Events without narrative but have a corresponding  *
      *            amount.                                            *
      *  214838 - Change text on loan maturity where frequency        *
      *           repayments have been taken into account.            *
      *           Amendment to CLE014.                                *
      *  201127 - Should show 'Start Date' rather than 'Date Approved'*
      *           when enquiring on the facility activity.            *
      *  189570 - Remove currency column as all amounts should be in  *
      *           facility currency.                                  *
      *  CLE014 - Customer Driven Lending Enhancements                *
      *  187261 - Do not display fields linked to Syndications unless *
      *           CLE006 is switched on.                              *
      *  176776 - Increase the field size of facility amount.         *
      *  161412 - Incorrect help tag and accessing incorrect document *
      *  127658 - Misleading/Incorrect narrative to display           *
      *  CLE005 - Customer Lending Enhancements - Others (T3)         *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *                                              Begin /161412/
      *                                              End   /161412/
      *
      ** Header record
      *
     A          R LE0211F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'LE0211'
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A                                  2 30'FACILITY ACTIVITY ENQUIRY'
     A                                      DSPATR(UL HI)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
      ** Detail record
      *
     A          R LE0211F1                  OVERLAY
      *
     A                                  3  2'Customer'
     A            SCNUM          6   O  3 18DSPATR(HI)
     A            SCUST         10   O  3 25DSPATR(HI)
     A                                  3 37'Facility'
     A*/COPY WNCPYSRC,LEH00078
     A            SFCTY          6   O  3 54DSPATR(HI)
     A*/COPY WNCPYSRC,LEH00079
     A                                  3 63'Status'
     A            SASTS          7   O  3 70DSPATR(HI)
     A**Start of change 201127
     A**********                        4  2'Date Approved'                                   201127
     A                                  4  2'Start Date'
     A**End of change 201127
     A            SDTAP          7   O  4 18DSPATR(HI)
     A                                  4 37'Date Expires'
     A            SDTEX          7   O  4 54DSPATR(HI)
     A                                  4 63'Type'
     A            SFCTI          9   O  4 70DSPATR(HI)
     A                                  5  2'Currency'
     A            SFCCY          3   O  5 18DSPATR(HI)
     A                                  5 37'Facility Amount'
      *                                              Begin /176776/
     A*********** SFAMT         17   O  5 54DSPATR(HI)
     A            SFAMT         20   O  5 54DSPATR(HI)
      *                                              End   /176776/
     A                                  6  2'Over-line %'
     A            SOVPC          6   O  6 18DSPATR(HI)
     A                                  6 37'Revolving Credit'
     A            SRVCR          3   O  6 54DSPATR(HI)
     A                                  6 63'Branch'
     A            SBRCA          3   O  6 73DSPATR(HI)
     A                                  7  2'Multi-Customer'
     A            SMCSI          3   O  7 18DSPATR(HI)
     A                                  7 37'Multi-Branch'
     A            SMBFC          3   O  7 54DSPATR(HI)
     A                                  7 63'Multi-CCY'
     A            SMCCY          3   O  7 73DSPATR(HI)
      *                                              Begin /127658/
     A**21******                        8  2'CREDIT AGREEMENT/TRANCHE +
     A**********                            FACILITY'
     A  31                              8  2'CREDIT AGREEMENT'
     A                                      DSPATR(HI)
      ** Start of change of BUG22126
     A  51                              8  2'AGGREGATE FACILITY'
     A                                      DSPATR(HI)
      ** End of change of BUG22126
     A  21                              8  2'TRANCHE FACILITY'
      *                                              End   /127658/
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,LEH00034
     A                                  8 57'Syndicated'
      *                                              Begin /187261/
     A N14                                  DSPATR(ND)
      *                                              End   /187261/
     A            SSYIN          3   O  8 70DSPATR(HI)
      *                                              Begin /187261/
     A N14                                  DSPATR(ND)
      *                                              End   /187261/
     A*/COPY WNCPYSRC,LEH00035
     A  21                              9  2'CA Customer'
     A  21        SCANM          6   O  9 14DSPATR(HI)
     A  21        SCACS         10   O  9 21DSPATR(HI)
     A  21                              9 33'Facility'
     A  21        SCAFC          6   O  9 42DSPATR(HI)
     A                                 10  5'Due Date'
     A                                 10 20'Narrative'
      *                                              Begin /189570/
     A************                     10 50'Ccy'
     A************                     10 56'Amount'
     A                                 10 55'Amount (Fclty Ccy)'
      *                                              End   /189570/
     A                                 10 75'Code'
      *
      ** Subfile record
      *
     A          R LE0211S0                  SFL
      *
     A            SVLDT          7   O 11  5DSPATR(HI)
     A**Start of Change CLE014
     A**********  SFATP         33   O 11 15DSPATR(HI)                                        CLE014
     A**Start of Change 214838
     A**********  SFATP         34   O 11 15DSPATR(HI)
     A**Start of Change BUG8261
     A**********  SFATP         35   O 11 15DSPATR(HI)                                       BUG8261
     A            SFATP         41   O 11 14DSPATR(HI)
     A**End of Change BUG8261
     A**End of Change 214838
     A**End of Change CLE014
      *                                              Begin /189570/
     A************SACCY          3   O 11 50DSPATR(HI)
      *                                              End   /189570/
      *                                              Begin /176776/
     A*********** SAAMT         17   O 11 56DSPATR(HI)
     A            SAAMT         20   O 11 56DSPATR(HI)
      *                                              End   /176776/
     A            SCODE          1   O 11 77DSPATR(HI)
      *
      ** Subfile control
      *
     A          R LE0211C0                  SFLCTL(LE0211S0)
     A                                      OVERLAY
     A  23                                  SFLDSPCTL
     A  24                                  SFLDSP
     A N23N24                               SFLCLR
     A  25                                  SFLEND(*MORE)
     A                                      SFLPAG(10) SFLSIZ(11)
     A N20                                  CA05
     A N25                                  ROLLUP(22)
     A            SSREC          4S 0H      SFLRCDNBR
      *
     A                                 22  3'F3=Exit'
     A  20                             22 12'Roll_Keys '
     A N20                             22 12'F5=Refresh'
     A N20                             22 24'Roll_Keys'
     A                                 23  3'Branch'
     A            SSBRCA         3   B 23 10DSPATR(HI)
     A N26                                  DSPATR(UL)
     A  26                                  DSPATR(RI PC)
     A                                 23 15'Customer'
     A            SSCUST        10   B 23 24DSPATR(HI)
     A N27                                  DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A                                 23 36'Facility'
     A            SSFACT         3   B 23 45DSPATR(HI)
     A N28                                  DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A            SSFCNO         2   B 23 49DSPATR(HI)
     A N28                                  DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A                                 23 53'From Date'
     A            SSVLDT         6   B 23 63DSPATR(HI)
     A N29                                  DSPATR(UL)
     A  29                                  DSPATR(RI PC)
     A                                 23 71'Code'
     A            SSCODE         1   B 23 76DSPATR(HI)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(RI PC)
      *
      ** Message subfile record
      *
     A          R LE0211S1                  SFL SFLMSGRCD(24)
     A            SMSG                      SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
      ** Message subfile control
      *
     A          R LE0211C1                  SFLCTL(LE0211S1)
     A                                      OVERLAY
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
