      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Rollover Confirmation Request Screen')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Customer Lending Module
     A*                                                                  *
     A*        LE0425FM - SCREEN FORMAT FOR ROLLOVER CONFIRMATIONS       *
     A*                   REQUEST                                        *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 112563             Date 27Jan97               *
      *                 XXXXXX             Date 0000000               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*                                                               *
     A*****************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03
     A                                      PRINT(*LIBL/MPRINT)
     A          R LE0425F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            SCID           2A  B  1  2DSPATR(PR)
     A                                      DSPATR(ND)
     A                                      TEXT('SCREEN ID')
     A                                  1  5'WID  :'
     A            WSID           3A  O  1 12DSPATR(HI)
     A                                      TEXT('WORKSTATION ID')
     A**                                             Begin /112563/
     A                                  1 17'LE0425'
     A**                                             End   /112563/
     A                                  1 25'ROLLOVER CONFIRMATION REQUEST'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 60'Time :'
     A                                  1 67TIME
     A                                      DSPATR(HI)
     A                                  2  5'User :'
     A            USRID         10A  O  2 12DSPATR(HI)
     A                                      TEXT('USER ID')
     A                                  2 60'Date :'
     A            RUNA      R        O  2 68DSPATR(HI)
     A                                      TEXT('DATE')
     A*
     A          R SFRCD425                  SFL
     A                                      TEXT('SUBFILE RECORD')
     A  50                                  SFLNXTCHG
     A            LOAN           6A  B 09 08DSPATR(UL)
     A                                      TEXT('LOAN NUMBER')
     A  11                                  DSPATR(RI PC)
     A  60                                  DSPATR(PR)
     A            NARR          53A  O 09 20
     A                                      TEXT('NARRATIVE')
     A            DUP            1A  B 09 75DSPATR(UL)
     A                                      TEXT('DUPLICATE')
     A N15
     AO 60
     AO 61                                  DSPATR(PR)
     A  12                                  DSPATR(RI PC)
     A N15                                  DSPATR(ND)
     A*
     A          R SFCTL425                  SFLCTL(SFRCD425)
     A                                      TEXT('SUBFILE CONTROL RECORD')
     A  13                                  SFLDSP
     A  14                                  SFLDSPCTL
     A N13N14                               SFLINZ
     A                                      SFLSIZ(10)
     A                                      SFLPAG(10)
     A                                      OVERLAY
     A  62                                  CF12
     A  62                                  CF06
     A            RECNO          2  0H      SFLRCDNBR
     A                                      TEXT('SUBFILE RECORD NUMBER')
     A                                 04 08'Enter up to 10 Loan Numbers to req+
     A                                      uest Rollover Confirmations'
     A                                 06 08'Loan Number'
     A                                 06 35'Details'
     A*
     A          R LE0425F3                  TEXT('FOOTER FORMAT')
     A                                      OVERLAY
     A**                                             Begin /112563/
     A***********                      22 04'F3' DSPATR(HI)
     A***********                      22 07'to terminate.'
     A                                 22  2'F3=Exit' DSPATR(HI)
     A**                                             End   /112563/
     A*
     A          R LE0425F2                  TEXT('VALIDATION COMPLETE +
     A                                      FORMAT')
     A                                      OVERLAY
     A                                 20 30'* VALIDATION COMPLETE *' DSPATR(RI)
     A**                                             Begin /112563/
     A***********                      22 04'F3' DSPATR(HI)
     A***********                      22 07'to terminate,'
     A***********                      22 21'F12' DSPATR(HI)
     A***********                      22 25'to re-amend details,'
     A***********                      23 04'F6' DSPATR(HI)
     A***********                      23 07'to request Confirmations.'
     A                                 22  2'F3=Exit' DSPATR(HI)
     A                                 22 11'F12=Re-amend details' DSPATR(HI)
     A                                 22 33'F6=Request Confirmations'
     A                                      DSPATR(HI)
     A**                                             End   /112563/
