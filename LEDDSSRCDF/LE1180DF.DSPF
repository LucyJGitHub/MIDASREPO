      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Consolidated Confirmation Prompt Program')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LE1180DF - Consolidated Confirmation Prompt Program     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 133261             Date 10Jun98               *
      *                 CLE004 *C *CREATE  Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  133261 - The Help tags do not have a help tag no.            *
      *  CLE004 - Customer Lending  Enhancements - Syndications       *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
      *
      * Header format
      *
     A          R LE1180F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 14'LE1180P'
     A                                  1 30'CONSOLIDATED CONFIRMATION'
     A                                      DSPATR(HI)
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 35'PROMPT PROGRAM'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
      * Initial screen
      *
     A          R LE1180F1
     A                                      OVERLAY
     A N19                                  CA05
     A N19                                  CA12
      *
     A                                  7  6'Customer No.'
     A  19                                  DSPATR(ND)
     A            SCUSN          6   B  7 20
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI UL)
     A  19                                  DSPATR(ND PR)
     A                                  7 32'BLANKS for selecting ALL'
     A  19                                  DSPATR(ND)
     A                                  8  6'From Loan No.'
     A  19                                  DSPATR(ND)
     A            SFLON          6   B  8 20
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI UL)
     A  19                                  DSPATR(ND PR)
     A                                  9  6'To Loan No.'
     A  19                                  DSPATR(ND)
     A            STLON          6   B  9 20
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI UL)
     A  19                                  DSPATR(ND PR)
     A                                  9 32'BLANKS for selecting ALL'
     A  19                                  DSPATR(ND)
      *
     A                                 10 19'Consolidated Confirmation +
     A                                      is Switched Off'
     A N19                                  DSPATR(ND)
     A                                 12 31'Option not Allowed'
     A N19                                  DSPATR(ND)
      *
     A                                 22  2'F3=Exit'
     A N19                             22 12'F5=Refresh'
     A N19                             22 25'F12=Previous Screen'
      *
      ** Message subfile record
      *
     A          R LE1180S0                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      ** Message subfile control
      *
     A          R LE1180C0                  TEXT('Program messages')
     A                                      SFLCTL(LE1180S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  21
     AON21                                  SFLEND
     A            ##PGM                     SFLPGMQ
