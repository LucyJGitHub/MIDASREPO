      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Risk participant details')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LE2022DF - Participant Maintenance                      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 29Nov05               *
      *                 CGL029             Date 01Sep03               *
      *                 CLE031             Date 03Feb03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 153650             Date 02Fev99               *
      *                 145908             Date 26Oct98               *
      *                 CEU004             Date 02Feb98               *
      *                 CLE005 *CREATE     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  153650 - Client names should be shown                        *
      *  145908 - Midas standard Processing errors                    *
      *  CEU004 - LE Settlement Ccy Conversion Upgrade to DBA R2.0    *
      *  CLE005 - Customer Lending Enhancements - Tranche 3           *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      CA03(03 'Exit')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      **  Header format
      *
     A          R LE2022F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      KEEP
     A            S#USER    R        O  1  2REFFLD(USER)
     A                                  1 14'LE2022'
     A                                  1 34'RISK PARTICIPANT DETAILS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            S#RUNA    R        O  1 73REFFLD(RUNA)
     A            S#WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
      **  Subfile Format
      *
     A          R LE2022S1                  SFL
     A  35                                  SFLNXTCHG
     A            S#SEL1         1A  B 12  3
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N40                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A            S#CNUM         6A  O 12  8DSPATR(HI)
     A            S#CUSD        10A  O 12 16DSPATR(HI)
     A            S#JDTE         8A  O 12 30DSPATR(HI)
     A            S#SHTP         1A  O 12 43DSPATR(HI)
     A            S#SHAR        16A  O 12 48DSPATR(HI)
     A            H#OSDB         3A  H
     A            H#OMDB         3A  H
     A            H#RCSI         1A  H
     A            H#SKIR        11S 7H
     A            H#SKIS         1A  H
     A            H#SKFR        11S 7H
     A            H#SKFS         1A  H
     A            H#RSTM         2S 0H
     A**Start of change CGL029
     A**********  H#RONS        12A  H                                                        CGL029
     A            H#RONS        18A  H
     A**End of change CGL029
     A            H#RIBN         8A  H
     A            H#RIBA        35A  H
      * Begin /CSD027/
     A**********  H#ROBN         6S 0H                                                        CSD027
     A**********  H#ROCN         6S 0H                                                        CSD027
     A            H#ROBN         6A  H
     A            H#ROCN         6A  H
      * End /CSD027/
     A            H#PSTM         2S 0H
     A**Start of change CGL029
     A**********  H#PONS        12A  H                                                        CGL029
     A            H#PONS        18A  H
     A**End of change CGL029
     A            H#PIBN         8A  H
     A            H#PIBA        35A  H
      * Begin /CSD027/
     A**********  H#POBN         6S 0H                                                        CSD027
     A**********  H#POCN         6S 0H                                                        CSD027
     A            H#POBN         6A  H
     A            H#POCN         6A  H
      * End /CSD027/
     A            H#CVMR         1A  H
     A            H#RCRN         8A  H
     A            H#RCRA        35A  H
     A            H#RVNO         8A  H
     A            H#AWBN         8A  H
     A            H#AWBA        35A  H
     A            H#BENN         8A  H
     A            H#BENA        35A  H
     A            H#DTP1        35A  H
     A            H#DTP2        35A  H
     A            H#DTP3        35A  H
     A            H#DTP4        35A  H
     A            H#DCHG         3A  H
     A            H#BTB1        35A  H
     A            H#BTB2        35A  H
     A            H#BTB3        35A  H
     A            H#BTB4        35A  H
     A            H#BTB5        35A  H
     A            H#BTB6        35A  H
     A            H#POBR         3A  H
     A            H#ROBR         3A  H
     A            H#PCRF        15A  H
     A            H#PCOB         3A  H
      *                                              Begin /CEU004/
     A            H#SCCY         3A  H
     A            H#ICCY         3A  H
      *                                              End   /CEU004/
      **Start of Change CLE031
     A            H#REXR        14A  H
     A            H#REXI         1A  H
     A            H#PSCY         3A  H
     A            H#PEXR        14A  H
     A            H#PEXI         1A  H
      **End of Change CLE031
      *
      **  Subfile Control
      *
     A          R LE2022C1                  SFLCTL(LE2022S1)
     A                                      SFLSIZ(0045)
     A                                      SFLPAG(0009)
     A                                      CF05(05 'Refresh')
      *                                              Begin /145908/
     A                                      CA12(12 'Previous')
      *                                              End   /145908/
     A N07                                  CF09(09 'Add')
     A                                      OVERLAY
     A  30                                  SFLDSP
     A  31                                  SFLDSPCTL
     A N31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A            ##SFRC         4S 0H      SFLRCDNBR(CURSOR)
     A                                  3  8'                  '
     A                                  4  2'Customer'
     A            S#ANUM         6A  O  4 11DSPATR(HI)
     A            S#CRNM        20A  O  4 19DSPATR(HI)
     A                                  4 43'Branch'
     A            S#BRCA         3A  O  4 50DSPATR(HI)
     A                                  4 56'Amount'
     A            S#FAMT        16A  O  4 63DSPATR(HI)
     A                                  5  2'Facility'
     A            S#PMFC         6A  O  5 11DSPATR(HI)
     A                                  5 18'-'
     A            S#PMFT         3A  O  5 20DSPATR(HI)
     A                                  5 24'-'
     A            S#PMFN         2A  O  5 26DSPATR(HI)
     A                                  5 30'Currency'
     A            S#FCCY         3A  O  5 39DSPATR(HI)
     A                                  5 47'Percentage Sold'
     A            S#PCSD        15A  O  5 63DSPATR(HI)
     A                                  7  2'Actions,'
     A                                  7 51'Review from...'
     A            S#REV1         6A  B  7 66
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A  51                                  DSPATR(PC)
     A                                  8  3'E=Enquire'
     A                                  8 16'A=Amend    D=Delete'
     A  07                                  DSPATR(ND)
     A                                 10  2'Sel   -----Customer-----    Joinin-
     A                                      g     ---------Share---------'
     A                                 11  8'Number  Shortname       Date      -
     A                                      Type  Amount/Percentage'
      *
      **  Detail Screen
      *
     A          R LE2022F2
     A  08
     AO 09                                  CF05(05 'Refresh')
     A                                      CA12(12 'Previous')
     A  56                                  CF10(10 'Delete')
     A                                      CF14(14 'Settlement')
     A                                      OVERLAY
     A                                      PROTECT
     A                                  3  9'           '
     A                                  4  2'Customer'
     A            S#ANUM         6A  O  4 11DSPATR(HI)
     A            S#CRNM        20A  O  4 19DSPATR(HI)
     A                                  4 55'Branch'
     A            S#BRCA         3A  O  4 62DSPATR(HI)
     A                                  5  2'Facility'
     A            S#PMFC         6A  O  5 11DSPATR(HI)
     A                                  5 18'-'
     A            S#PMFT         3A  O  5 20DSPATR(HI)
     A                                  5 24'-'
     A            S#PMFN         2A  O  5 26DSPATR(HI)
     A                                  5 31'Currency'
     A            S#FCCY         3A  O  5 40DSPATR(HI)
      *                                              Begin /153650/
     A***********                       7 21'Member Name . . . :'
     A*********** S#CSSN        10A  B  7 42
     A                                  7  5'Member Name   :'
     A            S#CSSN        10A  B  7 21
      *                                              End   /153650/
     A  55
     AON09                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N55 09                               DSPATR(UL)
     A N41                                  DSPATR(HI)
      *                                              Begin /153650/
     A***********                       9 21'Introduced by . . :'
     A*********** S#INUM        10A  B  9 42
     A                                  9  5'Introduced by :'
     A            S#INUM        10A  B  9 21
      *                                              End   /153650/
     A  55                                  DSPATR(PR)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N55                                  DSPATR(UL)
     A N42                                  DSPATR(HI)
      *                                              Begin /153650/
     A***********                      11 21'Joining Date. . . :'
     A*********** S#JDAT         6A  B 11 42
     A                                 11  5'Joining Date  :'
     A            S#JDAT         6A  B 11 21
      *                                              End   /153650/
     A  55                                  DSPATR(PR)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N55                                  DSPATR(UL)
     A N43                                  DSPATR(HI)
     A                                 13  5'Share Type (Amount or Percentage) -
     A                                      :'
     A            S#SHTP         1A  B 13 42
     A  55                                  DSPATR(PR)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A N55                                  DSPATR(UL)
     A N44                                  DSPATR(HI)
     A                                 14 21'Share . . . . . . :'
     A            S#SHAR        16A  B 14 42
     A  55                                  DSPATR(PR)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A N55                                  DSPATR(UL)
     A N45                                  DSPATR(HI)
     A                                 16 17'Fee Base Rate . . . . :'
     A            S#LPBR         2A  B 16 42
     A  55                                  DSPATR(PR)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A N55                                  DSPATR(UL)
     A N46                                  DSPATR(HI)
     A                                 17 21'Rate/Spread . . . :'
     A            S#LPAM        12A  B 17 42
     A  55                                  DSPATR(PR)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A N55                                  DSPATR(UL)
     A N47                                  DSPATR(HI)
     A                                 18 21'Sign. . . . . . . :'
     A            S#LPPS         1A  B 18 42
     A  55                                  DSPATR(PR)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A N55                                  DSPATR(UL)
     A N48                                  DSPATR(HI)
     A                                 20 17'Risk Participation'
     A                                 21 20'Loan Type/Sub-type :'
     A            S#DFTP         2A  B 21 42
     A  55                                  DSPATR(PR)
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A N55                                  DSPATR(UL)
     A N49                                  DSPATR(HI)
     A                                 21 45'-'
     A            S#DFST         2A  B 21 47
     A  55                                  DSPATR(PR)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A N55                                  DSPATR(UL)
     A N50                                  DSPATR(HI)
     A                                 22  9'         '
      *                                              Begin /153650/
     A            S#SRNM        20   O  7 34DSPATR(HI)
     A            S#SRTN        10   O  7 56DSPATR(HI)
     A            S#IRNM        20   O  9 34DSPATR(HI)
     A            S#IRTN        10   O  9 56DSPATR(HI)
      *                                              End   /153650/
      *
      **  Footer1
      *
     A          R LE2022F3
     A                                      OVERLAY
     A                                 22  2'F3=Exit    F5=Refresh'
     A                                 22 27'F9=Insert'
     A  07                                  DSPATR(ND)
      *                                              Begin /145908/
     A                                 22 40'F12=Previous Screen'
      *                                              End   /145908/
     A  34                             23 71'Page keys'
      *
      **  No records found
      *
     A          R LE2022F4
     A                                      OVERLAY
     A                                 13 34'NO RECORDS FOUND'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *
      **  Footer2
      *
     A          R LE2022F5
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A  55
     AO 56                                  DSPATR(ND)
     A                                 23 23'F10=Delete'
     A N56                                  DSPATR(ND)
     A                                 23 35'F12=Previous Screen'
     A                                 23 56'F14=Settlements'
      *
      **  Messge Format
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      **  Messge Control
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
