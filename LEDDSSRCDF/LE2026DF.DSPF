     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Risk & Sub-Participant Details')              *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module                              *
     A*                                                               *
     A*  DSPF/LE2026DF - Risk & Sub Participant Details Display       *
     A*                                                               *
      *  (c) Finastra International Limited 2005                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. 246090             Date 01Mar07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CLE106 *C *CREATE  Date 01Aug04               *
      *                                    Date                       *
      * Midas Release 4 --------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  246090 - Value date zeros on participant screen              *
      *  CLE106 - Syndication Manager                                 *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*=========================================================================
     A*
     A* Header format
     A*
     A          R LE2026F2
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      OVERLAY
     A                                      KEEP
     A            S#USER    R        O  1  2REFFLD(USER)
     A            S#WSID    R        O  2  2REFFLD(JOJOB)
     A                                  1 14'LE2026'
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A            S#RUNA    R        O  1 72REFFLD(RUNA)
     A  26N27N28                        2 29'FUNDING PARTICIPANT DETAILS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  27N26N28                        2 25'SUB PARTICIPANT DETAILS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  28N26N27                        2 26'RISK PARTICIPANT DETAILS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*=========================================================================
     A          R LE2026S1                  SFL
     A  35                                  SFLNXTCHG
     A            S#SEL          1A  B 12  3
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N40                                  DSPATR(UL)
     A N40                                  DSPATR(HI)
     A*/COPY WNCPYSRC,LEH00482
     A            S#CSSN        10A  O 12 16DSPATR(HI)
     A            S#CNUM         6A  O 12  8DSPATR(HI)
     A            S#FACT         3A  O 12 28DSPATR(HI)
     A            S#FCNO         2A  O 12 33DSPATR(HI)
     A*/COPY WNCPYSRC,LEH00483
     A            S#SHTP         1A  O 12 59DSPATR(HI)
     A            S#SHAR        16A  O 12 63DSPATR(HI)
     A            S#JDTE         6A  O 12 39DSPATR(HI)
     A**Start of change 246090
     A**********  S#VDTC         6A  O 12 48DSPATR(HI)                                        246090
     A**End of change 246090
     A            H#TNLU         5S 0H
     A            H#INUM         6A  H
     A            H#FAMT        13S 0H
     A            H#SHTP         1A  H
     A            H#RCSI         1A  H
     A            H#PASI         1A  H
     A            H#SKIR        11S 7H
     A            H#SKIS         1A  H
     A            H#SKFR        11S 7H
     A            H#SKFS         1A  H
     A            H#SHPC        15S 0H
     A            H#RSTM         2S 0H
     A            H#RONS        18A  H
     A            H#RIBN         8A  H
     A            H#RIBA        35A  H
     A            H#ROBN         6S 0H
     A            H#ROCN         6S 0H
     A            H#PSTM         2S 0H
     A            H#PONS        18A  H
     A            H#PIBN         8A  H
     A            H#PIBA        35A  H
     A            H#POBN         6S 0H
     A            H#POCN         6S 0H
     A            H#CVMR         1A  H
     A            H#RCRN         8A  H
     A            H#RCRA        35A  H
     A            H#RVNO         8A  H
     A            H#AWBN         8A  H
     A            H#AWBA        35A  H
     A            H#BENN         8A  H
     A            H#BENA        35A  H
     A            H#DTP1        35A  H
     A            H#DTP2        35A  H
     A            H#DTP3        35A  H
     A            H#DTP4        35A  H
     A            H#DCHG         3A  H
     A            H#BTB1        35A  H
     A            H#BTB2        35A  H
     A            H#BTB3        35A  H
     A            H#BTB4        35A  H
     A            H#BTB5        35A  H
     A            H#BTB6        35A  H
     A            H#POBR         3A  H
     A            H#ROBR         3A  H
     A            H#NACD         1A  H
     A            H#SCCY         3A  H
     A            H#ICCY         3A  H
     A            H#REXR        14A  H
     A            H#REXI         1A  H
     A            H#PSCY         3A  H
     A            H#PEXR        14A  H
     A            H#PEXI         3A  H
     A            H#COAG         1A  H
     A            H#FCAG         1A  H
     A            H#PHAG         1A  H
     A            H#SCAG         1A  H
     A            H#LTST         7A  H
     A*=========================================================================
     A          R LE2026C1                  SFLCTL(LE2026S1)
     A                                      SFLSIZ(0036)
     A                                      SFLPAG(0009)
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A  30                                  SFLDSP
     A  31                                  SFLDSPCTL
     A N31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A            ##SFRC         4S 0H      SFLRCDNBR(CURSOR)
     A                                 10  2'Sel'
     A                                  4  2'Agent Customer    '
     A            S#ANUM         6A  O  4 22DSPATR(HI)
     A            S#CRNM        20A  O  4 30DSPATR(HI)
     A                                  4 68'Branch'
     A            S#BRCA         3A  O  4 76DSPATR(HI)
     A                                  5  2'Primary Facility'
     A            S#PMFC         6A  O  5 22DSPATR(HI)
     A*/COPY WNCPYSRC,LEH00484
     A                                  5 29'-'
     A            S#PMFT         3A  O  5 31DSPATR(HI)
     A                                  5 35'-'
     A            S#PMFN         2A  O  5 37DSPATR(HI)
     A*/COPY WNCPYSRC,LEH00485
     A                                  5 41'Amount:'
     A            S#FAMT        14   O  5 50DSPATR(HI)
     A                                  5 66'Currency'
     A            S#FCCY         3A  O  5 76DSPATR(HI)
     A            S#PRNM        20   O  6 22DSPATR(HI)
     A            S#PRTN        10   O  6 44DSPATR(HI)
     A***********                       7  2'Actions'
     A                                  7 46'Percentage Sold'
     A  60                                  DSPATR(ND)
     A            S#SOLD        16A  O  7 63DSPATR(HI)
     A  60                                  DSPATR(ND)
     A                                  8  3'E=Enquire'
     A*/COPY WNCPYSRC,LEH00486
     A                                 10  7'---Participant Facility----'
     A*/COPY WNCPYSRC,LEH00487
     A                                 10 38'Joining'
     A**Start of change 246090
     A**********                       10 48'Value'                                           246090
     A**End of change 246090
     A                                 10 57'---------Share---------'
     A*/COPY WNCPYSRC,LEH00488
     A                                 11  7'Customer Shortname'
     A                                 11 28'Type No'
     A*/COPY WNCPYSRC,LEH00489
     A                                 11 40'Date'
     A**Start of change 246090
     A**********                       11 49'Date'                                            246090
     A**End of change 246090
     A                                 11 57'Type'
     A                                 11 63'Amount/Percentage'
     A*=========================================================================
     A          R LE2026F1
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 14'F5=Refresh'
     A                                 23 29'F12=Previous'
     A                                 23 46'F14=Settlements'
     A*=========================================================================
     A          R LE2026F3
     A                                      CF12(12 'Previous')
     A                                      CF14(14 'Settlement')
     A                                      OVERLAY
     A                                      PROTECT
     A*=========================================================================
     A                                  4  2'Agent Customer'
     A            S#CRNM        20A  O  4 30DSPATR(HI)
     A            S#ANUM         6A  O  4 22DSPATR(HI)
     A                                  4 68'Branch'
     A            S#BRCA         3A  O  4 76DSPATR(HI)
     A                                  5  2'Primary Facility'
     A            S#PMFC         6A  O  5 22DSPATR(HI)
     A*/COPY WNCPYSRC,LEH00490
     A                                  5 29'-'
     A            S#PMFT         3A  O  5 31DSPATR(HI)
     A                                  5 35'-'
     A            S#PMFN         2A  O  5 37DSPATR(HI)
     A*/COPY WNCPYSRC,LEH00491
     A                                  5 41'Amount:'
     A            S#FAMT        14   O  5 50DSPATR(HI)
     A                                  5 66'Currency'
     A            S#FCCY         3A  O  5 76DSPATR(HI)
     A            S#PRNM        20   O  6 22DSPATR(HI)
     A            S#PRTN        10   O  6 44DSPATR(HI)
     A                                  6 67'Alt n/a'
     A            S#NACD         1   O  6 76
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A                                  8  3'Member           :'
     A            S#CSSN        10A  O  8 22
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N41                                  DSPATR(HI)
     A            S#SRNM        20   O  8 34DSPATR(HI)
     A            S#SRTN        10   O  8 56DSPATR(HI)
     A                                  9  3'Facility Type/No.:'
     A            S#FACT         3A  O  9 22
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A            S#FCNO         2A  O  9 26DSPATR(HI)
     A*/COPY WNCPYSRC,LEH00492
     A                                 10  3'Introduced By    :'
     A            S#INUM        10A  O 10 22
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N42                                  DSPATR(HI)
     A            S#IRNM        20   O 10 34DSPATR(HI)
     A            S#IRTN        10   O 10 56DSPATR(HI)
     A                                 11 22'Joining Date ....... :'
     A            S#JDTE         6A  O 11 46
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N43                                  DSPATR(HI)
     A**Start of change 246090
     A**********                       12 22'Value Date ......... :'                          246090
     A**********  S#VDTC         6A  O 12 46                                                  246090
     A*N51******                            DSPATR(HI)                                        246090
     A**51******                            DSPATR(RI)                                        246090
     A**51******                            DSPATR(PC)                                        246090
     A**End of change 246090
     A                                 13  3'Risk Participant Loan Type/Subtype-
     A                                       .... :'
     A N28                                  DSPATR(ND)
     A            S#LTST         7A  O 13 46
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A N44                                  DSPATR(HI)
     A N28                                  DSPATR(ND)
     A                                 14  3'Participant Share: Amount/Percent-
     A                                       ..... :'
     A            S#SHTP         1A  O 14 46
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A N44                                  DSPATR(HI)
     A                                 15 22'Share .............. :'
     A                                 16 22'Recourse ........... :'
     A                                 17 22'Assign/Participation :'
     A            S#PASI         1A  O 17 46
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A N53                                  DSPATR(HI)
     A                                 18  3'Interest Skim    : Rate :'
     A                                 18 46'Fees Skim Rate . :'
     A            S#SHAR        16A  O 15 46
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A N45                                  DSPATR(HI)
     A            S#RCSI         1A  O 16 46
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A            S#SKIR        12A  O 18 30
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A N46                                  DSPATR(HI)
     A            S#SKFR        12A  O 18 65
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A N48                                  DSPATR(HI)
     A            S#SKIS         1A  O 18 43
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A N47                                  DSPATR(HI)
     A            S#SKFS         1A  O 18 78
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A N49                                  DSPATR(HI)
     A                                 20  3'Agents:'
     A                                 20 22'Co-Agent ......... :'
     A            S#COAG         1A  O 20 43
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A N66                                  DSPATR(ND)
     A                                 20 46'Facility ........ :'
     A            S#FCAG         1A  O 20 69
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
     A N66                                  DSPATR(ND)
     A                                 21 22'Paying/Handling .. :'
     A            S#PHAG         1A  O 21 43
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A N64                                  DSPATR(HI)
     A N66                                  DSPATR(ND)
     A                                 21 46'Security ........ :'
     A            S#SCAG         1A  O 21 69
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A N65                                  DSPATR(HI)
     A N66                                  DSPATR(ND)
     A*=========================================================================
     A          R LE2026F4
     A                                      OVERLAY
     A                                 13 34'NO RECORDS FOUND'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A  25                                  SFLDSP
     A  25                                  SFLDSPCTL
     A  25                                  SFLINZ
     A  25                                  SFLCLR
     A  25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
      *
     A          R LE2026F5
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 14'F12=Previous'
