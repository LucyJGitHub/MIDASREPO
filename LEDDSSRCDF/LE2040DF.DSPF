      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Loans amendment input display')               *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LE2040DF - Loans Amendment Input Display                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. AR847588           Date 09Mar15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE036             Date 22Sep03               *
      *                 CLE034             Date 05May03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CAP061             Date  8Jun01               *
      *                 186706             Date 09Mar01               *
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 126383             Date 25NOV97               *
      *                 126274             Date 20NOV97               *
      *                 123857             Date 27OCT97               *
      *                 121019             Date 22JUL97               *
      *                 120023             Date 10JUL97               *
      *                 CLE004 *Create     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR847588 - Enable system to accept negative spread rate.     *
      *           - Applied for MD033488                              *
      *  CLE036 - Lending Enhancements for Nordea Phase 1 Priority 2  *
      *  CLE034 - Lending Enhancements for Phase 1 Priority 1A.       *
      *  CAP061 - Lending API enhancements - Loans Amendment          *
      *  186706 - Upon reversal, F10 is not displayed until settle-   *
      *           ment details have been shown.                       *
      *  126383 - In enquiry mode cannot tell if a loan is reversed.  *
      *  126274 - Subfile should show Shortname                       *
      *  123857 - Condition the output of F11 for authorisation       *
      *  121019 - Change screen text.                                 *
      *  120023 - F10 Delete is not shown in ascending order. It is   *
      *           shown after F12 on screen. When value date is in    *
      *            error highlight field.                              *
      *  CLE004 - Customer Lending  Enhancements - Syndications       *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CF03
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
      *
      ** Header format
      *
     A          R LE2040F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 14'LE2040'
      **Start of change CAP061
     A**********                        1 31'LOAN AMENDMENTS INPUT'             CAP061
     A**********                            DSPATR(HI)                          CAP061
     A N60                              1 31'LOAN AMENDMENTS INPUT'
     A N60                                  DSPATR(HI)
     A  60                              1 31'LOAN AMENDMENT REPAIR'
     A  60                                  DSPATR(HI)
      **End of change CAP061
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
      ** Initial screen
      *
     A          R LE2040F1
     A                                      OVERLAY
     A*/COPY WNCPYSRC,LE2040DF01
     A  25                                  CF12
      *
     A                                  9 21'Action'
     A            SACTN          1   B  9 31
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI UL)
     A  25                                  DSPATR(PR)
      *
     A                                  9 41'Loan no '
     A            SLOAN          6   B  9 57
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI UL)
      *
     A                                 10 41'Amendment type'
     A            SATYP          2   B 10 57
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI UL)
      *
     A                                 11 41'Value date'
     A            SVDAT          6   B 11 57
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI UL)
      *
     A                                 12 41'Sequence no'
     A            SSEQN          3   B 12 57
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI UL)
      *
     A*                                      Begin /121019/
     A******************************** 15  7'or'
     A                                 14 35'OR'
     A*                                      End   /121019/
     A  25                                  DSPATR(ND)
      *
     A*                                      Begin /121019/
     A******************************** 15 21'Review from         Loan +
     A********************************       no'
     A                                 16 07'Review from   Loan no'
     A*                                      End   /121019/
     A  25                                  DSPATR(ND)
     A*                                      Begin /121019/
     A*********** SRLON          6   B 15 50
     A            SRLON          6   B 16 31
     A*                                      End   /121019/
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI UL)
     A  25                                  DSPATR(ND PR)
      *
     A*                                      Begin /121019/
     A******************************** 17 25'Requiring Authorisation'
     A                                 16 41'Unauthorised Only?'
     A*                                      End   /121019/
     A  25
     AON29                                  DSPATR(ND)
     A*                                      Begin /121019/
     A*********** SRAUT          1   B 17 50
     A            SRAUT          1   B 16 61
     A*                                      End   /121019/
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI UL)
     A  25
     AON29                                  DSPATR(ND PR)
      *
     A                                 20 16'Sequence no. allocated to last lo-
     A                                      an amendment'
     A  24                                  DSPATR(ND)
     A            WLSNA          3A  O 20 62
     A  24                                  DSPATR(ND)
     A                                      DSPATR(HI)
      *
     A                                 22  2'F3=Exit'
     A  25                             22 12'F12=Previous screen'
     A*/COPY WNCPYSRC,LE2040DF02
      *
      ** Subfile footer
      *
     A          R LE2040F2
     A                                      OVERLAY
     A                                 22  2'F3=Exit'
     A                                 22 12'F5=Refresh'
      **Start of change CAP061
     A**********                       22 25'F9=Insert'                         CAP061
     A**********                       22 37'F12=Previous screen'               CAP061
     A N60                             22 25'F9=Insert'
     A N60                             22 37'F12=Previous screen'
      **End of change CAP061
     A  13                             23 71'Page keys'
      *
      ** Detail footer
      *
     A          R LE2040F3
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A*                                      Begin /120023/
     A******************************** 23 10'F12=Previous screen'
     A**27**************************** 23 30'F10=Delete'
     A**Start of change 186706
     A**27******                       23 10'F10=Delete'
     A  27 15                          23 10'F10=Delete'
     A**End of change 186706
     A                                 23 21'F12=Previous screen'
     A*                                      End   /120023/
     A*                                      Begin /123857/
     A**26**************************** 23 41'F11=Authorise'
     A  26N81                          23 41'F11=Authorise'
     A*                                      End   /123857/
     A  28                             23 55'F14=Settlements'
      *
      ** Details screen
      *
     A          R LE2040F4
     A                                      OVERLAY
     A**Start of change 186706
     A**27******                            CF10
     A  27 15                               CF10
     A**End of change 186706
     A*                                      Begin /123857/
     A**26****************************      CF11
     A  26N81                               CF11
     A*                                      End   /123857/
     A                                      CF12
     A  28                                  CF14
      *
      *
     A                                  4  2'Loan no'
     A            SLOAN          6   O  4 25DSPATR(HI)
     A                                  4 43'Action'
     A            SACTN          1   O  4 52DSPATR(HI)
     A                                  4 57'Status'
     A            SSTAT         16   O  4 64DSPATR(HI)
      **Start of change CAP061
     A**********                        6  2'Amendment type'
     A**********  SATYP          2   O  6 25DSPATR(HI)
     A                                  6  2'Amendment type'
     A            SATYP          2   B  6 25DSPATR(HI)
     A  60N61                               DSPATR(UL)
     A N60                                  DSPATR(PR)
     A  61                                  DSPATR(RI PC)
      **End of change CAP061
     A*                                      Begin /121019/
     A********************************* 7  2'Value'
     A                                  7  2'Value Date'
     A*                                      End   /121019/
      **Start of change CAP061
     A**********  SVDAT          6   O  7 25DSPATR(HI)
     A            SVDAT          6   B  7 25DSPATR(HI)
     A  60N48                               DSPATR(UL)
     A N60                                  DSPATR(PR)
     A*                                      Begin /120023/
     A**48******                            DSPATR(RI)
     A  48                                  DSPATR(RI PC)
      **End of change CAP061
     A*                                      End   /120023/
     A                                  7 33'Sequence'
     A            SSEQN          3   O  7 43DSPATR(HI)
     A                                  9  2'Customer'
     A            SCNUM         10   B  9 25
     A  37                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N37                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  23                              9 43'Type/sub-type'
     A  23        SLTYP          2   O  9 58
     A                                      DSPATR(HI)
     A  23        SSTYP          2   B  9 61
     A  46                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N46                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  23                             10 43'Maturity date'
     A  23        SMDAT          6   B 10 58
     A  47                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N47                               DSPATR(UL)
     A                                      DSPATR(HI)
     A**Start of change CLE036
     A**********                       11  2'Base Rate'                                       CLE036
     A N05                             11  2'Base Rate'
     A  05                             11  2'Base Rate Code'
     A**End of change CLE036
     A            SBRAT          2   B 11 25
     A  38                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N38                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 12  2'Rate/Spread'
     A            SRTSP         12   B 12 25
     A  39                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N39                               DSPATR(UL)
     A                                      DSPATR(HI)
      **Start of change AR847588
     A            SRSGN          1   B 12 38
     A  14                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N14                               DSPATR(UL)
     A                                      DSPATR(HI)
      **End of change AR847588
     A                                 13  2'Currency'
     A            SCURR          3   B 13 25
     A  40                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N40                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 14  2'Amount'
     A            SAMNT         14   B 14 25
     A  41                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N41                               DSPATR(UL)
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,LEH01092
     A                                 15  2'Stop Accruals'
     A            SSTPA          1   B 15 25
     A  42                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N42                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  51                             15 43'Funding Spread/Rate'
     A  51        SFRAT         12   B 15 64
     A  52                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N52                               DSPATR(UL)
     A                                      DSPATR(HI)
      **Start of Change CLE034
     A  76                             16 43'Settlement Allocations'
     A  76        SSTAL          1A  B 16 66
     A  56                                  DSPATR(RI PC)
     A  18                                  DSPATR(PR)
     A N18N56                               DSPATR(UL)
     A                                      DSPATR(HI)
      *
      **End of Change CLE034
     A                                 17  2'Special Instructions'
     A  50                             17 40'Default Participation'
     A  50        SDFTP          2   B 17 64
     A  43                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N43                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  50                             18 40'Loan Type'
     A            SSPI1         30   B 19  2
     A  45                                  DSPATR(RI PC)
     A  18                                  DSPATR(PR)
     A N18N45                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            SSPI2         30   B 20  2
     A  45                                  DSPATR(RI PC)
     A  18                                  DSPATR(PR)
     A N18N45                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  50                             20 40'Default Participation'
     A  50        SDFST          2   B 20 64
     A  44                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N44                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            SSPI3         30   B 21  2
     A  45                                  DSPATR(RI PC)
     A  18                                  DSPATR(PR)
     A N18N45                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  50                             21 40'Loan Subtype'
     A*                                      Begin /126383/
     A  75                             22 02'RECORD REVERSED'
     A  75                                  DSPATR(HI)
     A  75                                  DSPATR(RI)
     A*                                      End   /126383/
      *
      ** Subfile records
      *
     A          R LE2040S0
     A                                      SFL
     A  20                                  SFLNXTCHG
     A*                                      Begin /126274/
     A*********** SLSEL          1   B 10  4
     A**49*******                           DSPATR(RI PC)
     A*N49*******                           DSPATR(HI UL)
     A*********** SLNRF          6   O 10  8DSPATR(HI)
     A*********** SCUST          6   O 10 16DSPATR(HI)
     A*********** SAMTP         21   O 10 24DSPATR(HI)
     A*********** SSQNO          3   O 10 47DSPATR(HI)
     A*********** SVLDT          7   O 10 53DSPATR(HI)
     A*
      **Start of change CAP061
     A            XREPN          5S 0H
     A            XMSID          7A  H
 
      **End of change CAP061
     A            SLSEL          1   B 10  2
     A  49                                  DSPATR(RI PC)
     A N49                                  DSPATR(HI UL)
     A            SLNRF          6   O 10  5DSPATR(HI)
     A            SCUST          6   O 10 12DSPATR(HI)
     A            SCSNM         10   O 10 19DSPATR(HI)
     A            SAMTP         21   O 10 30DSPATR(HI)
     A            SSQNO          3   O 10 52DSPATR(HI)
     A            SVLDT          7   O 10 56DSPATR(HI)
     A*                                      End   /126274/
     A            SLSTA         16   O 10 64DSPATR(HI)
     A            SLMTP          2   H
     A            SLDTE          5  0H
     A            SIUSR         10   H
     A            SAUSR         10   H
     A            SBRCA          3   H
     A            SASTS          1   H
      *
      ** Subfile control
      *
     A          R LE2040C0
     A                                      CF05
      **Start of change CAP061
     A**********                            CF09                                CAP061
     A**********                            CF12                                CAP061
     A N60                                  CF09
     A N60                                  CF12
      **End of change CAP061
     A                                      SFLCTL(LE2040S0)
     A N11N12                               SFLDSP
     A N11                                  SFLDSPCTL
     A  11                                  SFLCLR
     A  19                                  SFLEND(*MORE)
     A N19                                  ROLLUP(21)
     A                                      SFLPAG(10)
     A                                      SFLSIZ(11)
     A                                      OVERLAY
     A            SRECN          4S 0H      SFLRCDNBR
     A                                  4 54'Review from'
     A            SRLON          6   B  4 66
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI UL)
     A                                  5 54'Req. Authorisation'
     A N29                                  DSPATR(ND)
     A            SRAUT          1   B  5 73
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI UL)
     A N29                                  DSPATR(ND PR)
     A                                  5  2'Actions,'
      **Start of change CAP061
     A**********                        6  3'E=Enquire'                         CAP061
     A**********                        6 16'A=Amend'                           CAP061
     A**********                        6 27'R=Reverse'                         CAP061
     A**29******                        6 39'X=Authorise'                       CAP061
     A N60                              6  3'E=Enquire'
     A N60                              6 16'A=Amend'
     A N60                              6 27'R=Reverse'
     A  29N60                           6 39'X=Authorise'
     A  60                              6  3'U=Amend'
     A  60                              6 13'Q=Delete'
      **End of change CAP061
     A*                                      Begin /126274/
     A**********                        8  3'Sel'
     A**********                        8  9'Loan'
     A**********                        8 15'Customer'
     A**********                        8 29'Amendment'
     A**********                        8 47'Seq'
     A**********                        8 52'Value Date'
     A*                                      End   /126274/
     A                                  8 68'Status'
     A*                                      Begin /126274/
     A**********                        9 31'Type'
     A                                  8  1'Sel'
     A                                  8  6'Loan'
     A                                  8 13'Customer'
     A                                  8 30'Amendment'
     A                                  8 52'Seq'
     A                                  8 57'Value'
     A                                  9 31'Type'
     A*                                      End   /126274/
      *
      ** Message subfile record
      *
     A          R LE2040S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      ** Message subfile control
      *
     A          R LE2040C1                  TEXT('Program messages')
     A                                      SFLCTL(LE2040S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  19
     AON19                                  SFLEND
     A            ##PGM                     SFLPGMQ
