      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Transaction Income Splits Display')           *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LE2080DF - Transaction Income Splits Display            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE035  *CREATE    Date 05May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CLE035 - Lending Enhancements for Nordea Phase 1 Priority 2: *
      *           Income Splits                                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      ** Header
      *
     A          R LE2080F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'LE2080DF'
     A  61                              1 28'LOAN INCOME SPLITS MAINTENANCE'
     A                                      DSPATR(HI)
     A N61                              1 28'FEE INCOME SPLITS MAINTENANCE'
     A                                      DSPATR(HI)
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 72TIME
     A            #0TIME    R        O  2 73REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
      *
     A          R LE2080S0
     A                                      SFL
     A  84                                  SFLNXTCHG
      *
     A            #0RECI#        1   H      TEXT('Record ID')
      **Start of change CLE148
     A**********  #0LNRF#        6  0H      TEXT('Loan Reference')                            CLE148
     A            #0LNRF#        6   H      TEXT('Loan Reference')
      **End of change CLE148
     A            #0CNUM#        6  0H      TEXT('Facility Customer')
     A            #0FACL#        5  0H      TEXT('Facility')
     A            #0FESQ#        2  0H      TEXT('Fee Sequence')
     A            #0ISCN#        6  0H      TEXT('Split Customer')
     A            #0IPAL#       12  9H      TEXT('Split Customer')
     A            #0BRCA#        3   H      TEXT('Loan/Fee Branch')
     A            #0LCD#         5  0H      TEXT('Last Change Date')
     A            #0CHTP#        1   H      TEXT('Type of Last Change')
     A            #0PCRF#       15   H      TEXT('PC Reference')
     A            #0PPCR#       15   H      TEXT('Parent PC Reference')
      *
     A            #0SELE         1   B  8 16TEXT('Select')
     A  32                                  DSPATR(RI PC)
     A N32N51                               DSPATR(HI UL)
     A  51                                  DSPATR(PR ND)
      *
     A            #0ISCN        10   B  8 23TEXT('Split Customer')
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A N33N52                               DSPATR(UL)
     A  52                                  DSPATR(PR)
      *
     A            #0IPAL        13   B  8 43TEXT('Income Percentage Allocation')
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A N34N53                               DSPATR(UL)
     A  53                                  DSPATR(PR)
      *
     A          R LE2080C0
     A                                      SFLCTL(LE2080S0)
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A  81                                  SFLDSP
     A                                      SFLDSPCTL
     A N80 81 82                            SFLEND(*MORE)
     A  80                                  SFLCLR
     A N82                                  ROLLUP(85)
     A                                      OVERLAY
     A                                      CF03
     A  56                                  CF05
     A  57                                  CF09
     A  58                                  CF10
     A  59                                  CF12
     A N56N57N59                            CF15
      *
     A            #0RLRN         4  0H      SFLRCDNBR
      *
     A                                  4  2'Action:'
     A            #0ACTN         1   B  4 10
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A N26N54                               DSPATR(UL)
     A  54                                  DSPATR(PR)
      *
     A  61                              4 15'Loan:'
     A            #0LNRF         6   B  4 21
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A N27N54                               DSPATR(UL)
     A N61
     AO 54                                  DSPATR(PR)
     A N61                                  DSPATR(ND)
      *
     A  62                              4 70'DELETED'
     A                                      DSPATR(RI)
      *
     A N61                              5  2'Customer:'
     A            #0CNUM        10   B  5 12
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A N28N54                               DSPATR(UL)
     A  61
     AO 54                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
      *
     A N61                              5 25'Facility:'
     A            #0FACL         5   B  5 35
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI)
     A N29N54                               DSPATR(UL)
     A  61
     AO 54                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
      *
     A N61                              5 43'Loan:'
     A            #0LOAN         6   B  5 49
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A N30N54                               DSPATR(UL)
     A  61
     AO 54                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
      *
     A N61                              5 58'Fee Sequence:'
     A            #0FESQ         2   B  5 72
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A N31N54                               DSPATR(UL)
     A  61
     AO 54                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
      *
     A N51 81                           7 15'Sel'
     A  81                              7 23'Customer'
     A  81                              7 37'Income Percentage Allocation'
      *
     A          R LE2080F1                  TEXT('Command Keys')
     A                                      OVERLAY
     A                                 23  2'F3=End'
     A N56N57N59                       23 10'F15=Browse All'
     A  56                             23 10'F5=Refresh'
     A  55 57                          23 22'F9=Insert'
     A N55 57                          23 22'F9=Amend'
     A  58                             23 33'F10=Confirm Delete'
     A  59                             23 53'F12=Cancel'
     A  60                             23 65'D=Delete'
      *
     A          R LE2080S1                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R LE2080C1                  TEXT('Program messages')
     A                                      SFLCTL(LE2080S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
