      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Transaction Income Splits Repair Display')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LE2081DF - Transaction Income Splits Repair             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE035  *CREATE    Date 22Sep03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE035 - Lending Enhancements for Nordea Phase 1 Priority 2: *
      *           Income Splits                                       *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CF03
 
      *--------------------------------------------------------------------*
      ** Screen Header                                                     *
      *--------------------------------------------------------------------*
 
     A          R LE2081F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'LE2081DF'
     A  21                              1 27'LOAN INCOME SPLITS REPAIR'
     A  21                                  DSPATR(HI UL)
     A N21                              1 28'FEE INCOME SPLITS REPAIR'
     A N21                                  DSPATR(HI UL)
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
 
      *--------------------------------------------------------------------*
      ** Transaction Income Splits Subfile Format                          *
      *--------------------------------------------------------------------*
 
     A          R LE2081S1                  TEXT('Transact Inc Splits Subfile')
     A                                      SFL
     A  95                                  SFLNXTCHG
 
      ** Hidden fields
 
     A            #1REPN         5   H      TEXT('Repair Number')
     A            #1MSID         7   H      TEXT('Message ID ')
     A            #1TMST        26   H      TEXT('Timestamp')
     A            #1PCRF        15   H      TEXT('PC Reference')
     A            #1PPCR        15   H      TEXT('Parent PC Reference')
 
      ** Select field
 
     A            #1SEL          1   B  8  4TEXT('Select')
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI UL)
     A  30N31                               DSPATR(ND PR)
 
      ** Loan/Fees Income Split detail line
 
     A            #1DETL        65   O  8  9TEXT('Subfile detail line')
     A  30                                  DSPATR(ND)
     A                                      DSPATR(HI)
 
      *--------------------------------------------------------------------*
      ** Transaction Income Splits Subfile Control                         *
      *--------------------------------------------------------------------*
 
     A          R LE2081C1                  TEXT('(Subfile Control')
     A                                      SFLCTL(LE2081S1)
     A                                      SFLSIZ(15) SFLPAG(14)
     A                                      OVERLAY BLINK
     A                                      PUTOVR
     A  92                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
 
     A                                      CF05
     A            #1RLRN         4  0H      SFLRCDNBR
 
      ** Subfile field labels
 
     A                                  5  3'Sel'
     A  21                              5  9'Loan'
     A  21                              5 18'Act'
     A  21                              5 24'Customer'
     A  21                              5 36'Income Percentage'
     A  21                              5 54'Allocation'
 
     A N21                              4  9'Loan/Fclty'
     A N21                              4 22'Loan/'
     A N21                              4 32'Fee'
     A N21                              4 39'Act'
     A N21                              4 46'Customer'
     A N21                              4 59'Income Percentage'
     A N21                              5  9'Customer'
     A N21                              5 22'Fclty'
     A N21                              5 32'Seq'
     A N21                              5 59'Allocation'
 
      ** Search string input fields to filter subfile
 
     A            #1KEY1         6   B  6  9
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A*                                     OVRATR
     A            #1KEY2         6   B  6 22
     A                                      DSPATR(HI UL)
     A  21                                  DSPATR(ND PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            #1KEY3         2   B  6 32
     A                                      DSPATR(HI UL)
     A  21                                  DSPATR(ND PR)
     A                                      OVRDTA
     A                                      OVRATR
 
      *--------------------------------------------------------------------*
      ** Message Subfile Format                                            *
      *--------------------------------------------------------------------*
 
     A          R LE2081S2                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #2MSGK                    SFLMSGKEY
     A            #2PGMQ                    SFLPGMQ
 
      *--------------------------------------------------------------------*
      ** Message Subfile Control                                           *
      *--------------------------------------------------------------------*
 
     A          R LE2081C2                  TEXT('Program messages')
     A                                      SFLCTL(LE2081S2)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR BLINK
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  94                                  SFLEND
     A            #2PGMQ                    SFLPGMQ
 
      *--------------------------------------------------------------------*
      ** Selection Details Screen                                          *
      *--------------------------------------------------------------------*
 
     A          R LE2081F3                  TEXT('Selection Details')
     A                                      OVERLAY
     A                                      CF12
 
      ** Common Income Split Details
 
     A                                  8  2'Action:'
     A            #3ACTN         1   O  8 13
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A                                  9  2'Loan:'
     A            #3LNRF         6   O  9 13
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A                                 10  2'Customer:'
     A            #3CUST         6   B 10 13
     A N34                                  DSPATR(HI)
     A  21
     A N22                                  DSPATR(UL)
     A  34                                  DSPATR(RI)
     A  34 21                               DSPATR(PC)
     A N21
     AO 22                                  DSPATR(PR)
 
      ** Loan Income Split Details
 
     A  21                             12  2'Income Percentage:'
     A            #3LIPA        13   B 12 22
     A N35                                  DSPATR(HI UL)
     A  35                                  DSPATR(RI PC)
     A N21                                  DSPATR(ND PR)
 
      ** Fees Income Split Details
 
     A N21                             12  2'Facility:'
     A            #3FFAC         5   O 12 13
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  21                                  DSPATR(ND)
 
     A N21                             13  2'Fee Sequence:'
     A            #3FFES         2   O 13 17
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  21                                  DSPATR(ND)
 
     A N21                             14  2'Split Customer:'
     A            #3FISC         6   B 14 19
     A N38                                  DSPATR(HI)
     A N38N22                               DSPATR(UL)
     A  38                                  DSPATR(RI PC)
     A  21                                  DSPATR(ND)
     A  21
     AO 22                                  DSPATR(PR)
 
     A N21                             15  2'Percent Allocation:'
     A            #3FIPA        13   B 15 23
     A N39                                  DSPATR(HI UL)
     A  39                                  DSPATR(RI PC)
     A  21                                  DSPATR(ND PR)
 
      *--------------------------------------------------------------------*
      ** Screen Footer                                                     *
      *--------------------------------------------------------------------*
 
     A          R LE2081F4                  TEXT('Command Keys')
     A                                      OVERLAY
     A  81                             23  2'F3=End  F5=Refresh  U=Amend'
     A  81                             23 31'Q=Delete'
     A N81                             23  2'F3=End  F12=Previous'
