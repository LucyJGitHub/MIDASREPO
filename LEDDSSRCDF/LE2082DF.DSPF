      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Transaction Income Splits Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LE2082DF - Transaction Income Splits Enquiry Display    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE035  *CREATE    Date 22Sep03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE035 - Lending Enhancement for Nordea Phase 1 Priority 2   *
      *           Income Splits                                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
     A                                      CF03
     A                                      CF05
     A  62                                  CF12
      *
     A          R LE2082F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'LE2082DF'
     A  61                              1 28'LOAN INCOME SPLITS ENQUIRY'
     A                                      DSPATR(HI)
     A N61                              1 27'FEE INCOME SPLITS ENQUIRY'
     A                                      DSPATR(HI)
     A
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 72TIME
      *
     A  61                              6 13'Loan'
     A  61                              6 23'Customer'
     A  61                              6 37'Income Percentage Allocation'
      *
     A N61                              5  8'Loan/Fclty'
     A N61                              6  8'Customer'
     A N61                              5 21'Loan/'
     A N61                              6 21'Facility'
     A N61                              5 35'Fee'
     A N61                              6 35'Seq'
     A N61                              5 42'Customer'
     A N61                              5 55'Income Percentage'
     A N61                              6 55'Allocation'
      *
     A          R LE2082S0
     A                                      SFL
     A                                      SFLNXTCHG
      *
     A            #0LNRF         6   O  9 13TEXT('Loan Reference')
     A                                      DSPATR(HI)
     A            #0ISCM         6   O  9 24TEXT('Split Customer')
     A                                      DSPATR(HI)
     A            #0IPAL        13   O  9 44TEXT('Income Percentage Allocation')
     A                                      DSPATR(HI)
      *
     A          R LE2082C0
     A                                      SFLCTL(LE2082S0)
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A  81                                  SFLDSP
     A  81                                  SFLEND(*MORE)
     A                                      SFLDSPCTL
     A N81                                  SFLCLR
     A                                      OVERLAY
      *
     A            #0LREC         4  0H      SFLRCDNBR
      *
     A            #0LOAN         6   B  7 13DSPATR(HI UL)
      *
     A          R LE2082S1
     A                                      SFL
     A                                      SFLNXTCHG
      *
     A            #1CNUM         6   O  9 10TEXT('Customer Number')
     A                                      DSPATR(HI)
     A            #1FACL         6   O  9 21TEXT('Facility')
     A                                      DSPATR(HI)
     A            #1FESQ         2   O  9 35TEXT('Fee Sequence')
     A                                      DSPATR(HI)
     A            #1ISCN         6   O  9 43TEXT('Split Customer')
     A                                      DSPATR(HI)
     A            #1IPAL        13   O  9 55TEXT('Income Percentage Allocation')
     A                                      DSPATR(HI)
      *
     A          R LE2082C1
     A                                      SFLCTL(LE2082S1)
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A  81                                  SFLDSP
     A  81                                  SFLEND(*MORE)
     A                                      SFLDSPCTL
     A N81                                  SFLCLR
     A                                      OVERLAY
      *
     A            #1FREC         4  0H      SFLRCDNBR
      *
     A            #1CUST         6   B  7 10DSPATR(HI UL)
     A            #1FACS         6   B  7 21DSPATR(HI UL)
     A            #1FEES         2   B  7 35DSPATR(HI UL)
      *
     A          R LE2082F1
     A                                      OVERLAY
     A                                 24  2'No Records To Display'
     A                                      DSPATR(HI)
      *
     A          R LE2082F2                  TEXT('Command Keys')
     A                                      OVERLAY
     A                                 23  2'F3=End'
     A                                 23 10'F5=Refresh'
     A  62                             23 22'F12=Return'
