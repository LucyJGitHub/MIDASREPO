     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Manual repayment input initial screen')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module                              *
     A*                                                               *
     A*  DSPF/LE2110DF - Initial screen for Manual Repayment Input    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE036             Date 23Oct03               *
      *                 CLE034             Date 05May03               *
      *                 CLE028             Date 27Jun02               *
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *                 CAP063             Date 06Jun01               *
      * Midas DBA 3.05 -----------------------------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 161072             Date 25May99               *
      *                 158802             Date 19Apr99               *
      *                 156873             Date 23Mar98               *
      *                 130737             Date 17Feb98               *
      *                 126274             Date 21Nov97               *
     A*                 123857             Date 27OCT97               *
     A*                 121123             Date 29JUL97               *
     A*                 121019             Date 22JUL97               *
     A*                 120026             Date 03JUL97               *
     A*                 CLE004 *Create     Date 02Dec96               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE036 - Lending Enhancements for Nordea Phase 1 Priority 2: *
      *           Allow repayments in facility currency.              *
      *  CLE034 - Lending Enhancements for Phase 1 Priority 1A.       *
      *  CLE028 - Flat Rate Personal Loans (Rule of 78s)              *
      *  CAP063 - Lending API enhancements - Manual Repayment         *
      *  161072 - Manual Repayment of interest expected against       *
      *           LN is rejected                                      *
      *  158802 - Condition display of F10=Delete on *IN26 as well    *
      *  156873 - Manual repayments input, subfile shows strange      *
      *           currency codes.                                     *
     A*  130737 - Status not shown on subfile.                        *
     A*  126274 - Subfile should show Shortname                       *
     A*  123857 - Condition the output of F11 for authorisation       *
     A*  121123 - Special Instructions not required.                  *
     A*  121019 - Change screen text.                                 *
     A*  120026 - F10 and F12 not in ascending order                  *
     A*  CLE004 - Customer Lending  Enhancements - Syndications.      *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      * Header format
      *
     A          R LE2110F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 14'LE2110'
      *                                              Begin /CAP063/
     A**********************************1 31'MANUAL REPAYMENT INPUT'
     A**********************************    DSPATR(HI)
     A N65                              1 31'MANUAL REPAYMENT INPUT'
     A                                      DSPATR(HI)
     A  65                              1 30'MANUAL REPAYMENT REPAIR'
     A                                      DSPATR(HI)
      *                                              End   /CAP063/
      *
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 72TIME
      *
      * Initial screen
      *
     A          R LE2110F1
      *
     A                                      OVERLAY
     A  19                                  CF12
     A*/COPY WNCPYSRC,LE2110DF01
     A                                  9 11'Action'
     A            SACTN          1A  B  9 19
     A  19                                  DSPATR(PR)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A N67                                  DSPATR(UL)
     A N67                                  DSPATR(HI)
     A                                  9 27'Loan no'
     A            SLOAN          6A  B  9 39
      *                                              Begin /CAP063/
     A  65                                  DSPATR(PR)
      *                                              End   /CAP063/
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
      *                                              Begin /CAP063/
     A*N68********                          DSPATR(UL)
     A N68N65                               DSPATR(UL)
      *                                              End   /CAP063/
     A N68                                  DSPATR(HI)
     A                                 10 27'Value date'
     A            SVALD          6A  B 10 39
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A N69                                  DSPATR(UL)
     A N69                                  DSPATR(HI)
     A                                 10 50'Expected Value date'
     A            SEVAL          6A  B 10 71
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A N70                                  DSPATR(HI)
     A                                 11 27'Sequence no'
     A            SSEQN          3A  B 11 39
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A N71                                  DSPATR(UL)
     A N71                                  DSPATR(HI)
      **Start of change CLE036
     A  14                             12 27'Currency'
     A  14        SRCCY          3A  B 12 39
     A  15                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
     A N15                                  DSPATR(UL)
     A N15                                  DSPATR(HI)
      **End of change CLE036
     A                                 11 50'Expected Sequence no'
     A            SESEQ          3A  B 11 71
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A N72                                  DSPATR(UL)
     A N72                                  DSPATR(HI)
     A*                                      Begin /121019/
     A******************************** 15  2'or       Review from Loan no     '
      **Start of change CLE036
     A**********                       13 39'OR'                                              CLE036
     A                                 13 44'OR'
      **End of change CLE036
     A                                 15 11'Review from     Loan no'
     A*                                      End   /121019/
     A  19                                  DSPATR(ND)
     A            SRFLN          6A  B 15 39
     A  19                                  DSPATR(ND)
     A  19                                  DSPATR(PR)
     A  73                                  DSPATR(RI)
     A  73                                  DSPATR(PC)
     A N73                                  DSPATR(UL)
     A N73                                  DSPATR(HI)
     A*                                      Begin /121019/
     A**46**************************** 17 14'Requiring authorisation'
     A  46                             15 50'Unauthorised Only?'
     A*                                      End   /121019/
     A  19                                  DSPATR(ND)
     A*                                      Begin /121019/
     A**46******* SRAUT          1A  B 17 39
     A  46        SRAUT          1A  B 15 71
     A*                                      End   /121019/
     A  19                                  DSPATR(ND)
     A  19                                  DSPATR(PR)
     A  74                                  DSPATR(RI)
     A  74                                  DSPATR(PC)
     A N74                                  DSPATR(UL)
     A N74                                  DSPATR(HI)
     A                                 20 11'Sequence no allocated to last manu-
     A                                      al repayment'
     A N58                                  DSPATR(ND)
     A            SLASN1         3S 0O 20 59DSPATR(HI)
     A N58                                  DSPATR(ND)
     A                                 23  3'F3=Exit'
     A  19                             23 60'F12=Previous screen'
     A*/COPY WNCPYSRC,LE2110DF02
      *
      * Detail Screen
      *
     A          R LE2110D1                  OVERLAY
     A*                                      Begin /158802/
     A**34******                            CF10
     A  34 26                               CF10
     A*                                      End   /158802/
     A*                                      Begin /123857/
     A**35******                            CF11
     A  35N45                               CF11
     A*                                      End   /123857/
     A                                      CF12
     A  28                                  CF14
      *
      *
     A                                  4  2'Loan no'
     A            SLOAN          6A  O  4 24DSPATR(HI)
      *
     A                                  4 38'Action'
     A            SACTN          1A  O  4 46DSPATR(HI)
      *
     A                                  4 54'Status'
     A            SSTAT         16A  O  4 63DSPATR(HI)
      *
     A                                  5  2'Customer'
     A            SCUST         10A  B  5 24DSPATR(HI)
     A  75                                  DSPATR(RI PC)
     A N75N31N32                            DSPATR(UL)
     A  31
     AO 32                                  DSPATR(PR)
      *
      **Start fo change CLE036
     A**********                        7  2'Currency'                                        CLE036
     A                                  7  2'Repayment Currency'
      **End of change CLE036
      *                                              Begin /156873/
     A*********** SCCY           3A  O  7 24DSPATR(HI)
     A            SSCCY          3A  O  7 24DSPATR(HI)
      **Start of change CLE036
     A N54 25                           7 44'Expected Currency'
     A N54 25     SEXCY          3A  O  7 63DSPATR(HI)
      ** End of change CLE036
      *                                              End   /156873/
      *
     A                                  8  2'Current principal'
     A            SCURP         19A  O  8 24DSPATR(HI)
      *
     A                                  9  2'O/S interest to date'
      **Start of change CLE028
     A  07                                  DSPATR(ND)
      **End of change CLE028
     A            SINTD         18A  O  9 24DSPATR(HI)
      **Start of change CLE028
     A  07                                  DSPATR(ND)
      **End of change CLE028
      *
      **Start of change CLE036
     A**********                        9 43'W/hold tax type'                                 CLE036
     A                                  9 44'W/hold tax type'
      **End of change CLE036
     A            SWTTP          1A  O  9 60DSPATR(HI)
      *
     A  89                              9 63'Past due Y/N'
     A  17                                  DSPATR(ND)
     A  89        SPASD          1A  B  9 77DSPATR(HI)
     A  76                                  DSPATR(RI PC)
     A N76N32                               DSPATR(UL)
     A  17
     AO 32                                  DSPATR(PR)
     A  17                                  DSPATR(ND)
      **Start of change CLE036
     A  25                             10  2'Fac. Cur Principal'
     A  25        SFURP         19A  O 10 24DSPATR(HI)
     A  25                             10 44'Fac. O/S Interest'
     A  25        SFIND         18A  O 10 63DSPATR(HI)
      **End of change CLE036
      *
      **Start of change CLE028
     A                                 10  2'Interest Refunded'
     A  08                                  DSPATR(ND)
     A            #1INRF        18A  O 10 24DSPATR(HI)
     A  08                                  DSPATR(ND)
     A                                 10 43'Charges Refunded'
     A  09                                  DSPATR(ND)
     A            #1CGRF        17A  O 10 63DSPATR(HI)
     A  09                                  DSPATR(ND)
      **End of change CLE028
     A                                 11  2'Received Date'
     A            SVALD          6A  O 11 24DSPATR(HI)
     A  18                                  DSPATR(RI)
      *
     A                                 11 31'Seq'
     A  24                                  DSPATR(ND)
     A            SSEQN          3A  O 11 35DSPATR(HI)
     A  24                                  DSPATR(ND)
      *
     A                                 11 43'Expected Date'
     A            SEVAL          6A  O 11 63DSPATR(HI)
      *                                              Begin /161072/
     A  70                                  DSPATR(RI)
      *                                              End   /161072/
      *
     A                                 11 72'Seq'
     A            SESEQ          3A  O 11 77DSPATR(HI)
      *
     A                                 12 11'Principal'
     A            SRPRI         14A  B 12 24DSPATR(HI)
     A  77                                  DSPATR(RI PC)
     A N77N32                               DSPATR(UL)
     A  32                                  DSPATR(PR)
      *
     A            SPSIG          1A  B 12 39DSPATR(HI)
     A  78                                  DSPATR(RI PC)
     A N78N32N63                            DSPATR(UL)
     A  32
     AO 63                                  DSPATR(PR)
      *
     A N54                             12 52'Principal'
     A N54        SEPRI         17A  O 12 63DSPATR(HI)
      *
     A                                 13 11'Interest'
      **Start of change CLE028
     A  07                                  DSPATR(ND)
      **End of change CLE028
     A            SRINT         14A  B 13 24DSPATR(HI)
     A  79                                  DSPATR(RI PC)
     A N79N32                               DSPATR(UL)
      **Start of change CLE028
     A**32******                            DSPATR(PR)
     A  07
     AO 32                                  DSPATR(PR)
     A  07                                  DSPATR(ND)
      **End of change CLE028
      *
     A            SISIG          1A  B 13 39DSPATR(HI)
     A  29                                  DSPATR(RI PC)
     A N29N32                               DSPATR(UL)
      **Start of change CLE028
     A**32******                            DSPATR(PR)
     A  07
     AO 32                                  DSPATR(PR)
     A  07                                  DSPATR(ND)
      **End of change CLE028
      *
     A N55                             13 52'Interest'
     A N55        SEINT         17A  O 13 63DSPATR(HI)
      *
      **Start of change CLE036
     A**87******                       17 11'Penalty'                                         CLE036
     A**87******  SRPEN         14A  B 17 24DSPATR(HI)                                        CLE036
     A**81******                            DSPATR(RI PC)                                     CLE036
     A*N81*87N32                            DSPATR(UL)                                        CLE036
     A**32******                            DSPATR(PR)                                        CLE036
      **********                                                                              CLE036
     A**********  SRPIG          1A  B 17 39DSPATR(HI)                                        CLE036
     A**82******                            DSPATR(RI PC)                                     CLE036
     A*N82*87N32                            DSPATR(UL)                                        CLE036
     A**32******                                                                              CLE036
     A*ON87*****                             DSPATR(PR ND)                                    CLE036
      **********                                                                              CLE036
     A**87N59***                       17 52'Penalty'                                         CLE036
     A**87N59***  SEPEN         17A  O 17 63DSPATR(HI)                                        CLE036
      **End of change CLE036
     A*
     A N55                             15 52'W/hold tax'
     A N55        SWHTX         17A  O 15 63DSPATR(HI)
      *
     A                                 14 11'Total'
     A            SRTOT         14A  B 14 24DSPATR(HI)
     A  83                                  DSPATR(RI PC)
     A N83N32                               DSPATR(UL)
     A  32                                  DSPATR(PR)
      *
     A            SRSIG          1A  B 14 39DSPATR(HI)
     A  84                                  DSPATR(RI PC)
     A N84N32                               DSPATR(UL)
     A  32                                  DSPATR(PR)
      *
     A N56                             14 52'Total'
     A N56        SETOT         17A  O 14 63DSPATR(HI)
      *
      **Start of change CLE036
     A  87                             16 11'Penalty'
     A  87        SRPEN         14A  B 16 24DSPATR(HI)
     A  81                                  DSPATR(RI PC)
     A N81 87N32                            DSPATR(UL)
     A  32                                  DSPATR(PR)
      *
     A            SRPIG          1A  B 16 39DSPATR(HI)
     A  82                                  DSPATR(RI PC)
     A N82 87N32                            DSPATR(UL)
     A  32
     AON87                                  DSPATR(PR ND)
      *
     A  87N59                          16 52'Penalty'
     A  87N59     SEPEN         17A  O 16 63DSPATR(HI)
     A*
     A N57                             17 11'Write-off'
     A N57        SWOFF          1A  B 17 24DSPATR(HI)
     A  85                                  DSPATR(RI PC)
     A N85N32                               DSPATR(UL)
     A  32
     AO 27                                  DSPATR(PR)
     A*N57******                       18 11'Write-off'                                       CLE036
     A*N57******  SWOFF          1A  B 18 24DSPATR(HI)                                        CLE036
     A**85******                            DSPATR(RI PC)                                     CLE036
     A*N85N32***                            DSPATR(UL)                                        CLE036
     A**32******                                                                              CLE036
     A*O*27*****                            DSPATR(PR)                                        CLE036
      **Start of Change CLE034
     A**90******                       20 11'Settlement Allocation'                           CLE036
     A**90******  SSTAL          1A  B 20 36DSPATR(HI)                                        CLE036
     A*N90******                            DSPATR(ND)                                        CLE036
     A**91******                            DSPATR(RI PC)                                     CLE036
     A*N91N92***                            DSPATR(UL)                                        CLE036
     A**92******                            DSPATR(PR)                                        CLE036
      **End of Change CLE034
     A  90                             17 28'Sett. Alloc.'
     A  90        SSTAL          1A  B 17 43DSPATR(HI)
     A N90                                  DSPATR(ND)
     A  91                                  DSPATR(RI PC)
     A N91N92                               DSPATR(UL)
     A  92                                  DSPATR(PR)
      *
     A  25 32                          18  2'Loan Ccy Principal'
     A  25 32                          19 11'Interest'
     A  25 32                          20 11'Total'
     A  25 32                          21 11'Penalty'
     A  25 32     SLPRI         14A  O 18 24DSPATR(HI)
     A  25 32     SLINT         14A  O 19 24DSPATR(HI)
     A  25 32     SLTOT         14A  O 20 24DSPATR(HI)
     A  25 32     SLPEN         14A  O 21 24DSPATR(HI)
     A  25 32     SLCCY          3A  O 18 41DSPATR(HI)
      **End of change CLE036
      *
     A************                     20  2'Special instr.'            /121123/
     A************SSIN1         30A  B 20 17DSPATR(HI)                  /121123/
     A**33********                          DSPATR(UL)                  /121123/
     A**32N33*****                          DSPATR(PR)                  /121123/
      ************                                                      /121123/
     A************SSIN2         30A  B 20 48DSPATR(HI)                  /121123/
     A**33********                          DSPATR(UL)                  /121123/
     A**32N33*****                          DSPATR(PR)                  /121123/
      ************                                                      /121123/
     A************SSIN3         30A  B 21 17DSPATR(HI)                  /121123/
     A**33********                          DSPATR(UL)                  /121123/
     A**32N33*****                          DSPATR(PR)                  /121123/
      *
      * Detail screen footer
      *
     A          R LE2110F2                  OVERLAY
     A                                 23  2'F3=Exit'
     A*                                      Begin /120026/
     A******************************** 23 10'F12=Previous screen'
     A**34**************************** 23 30'F10=Delete'
     A*                                      Begin /158802/
     A**34******                       23 10'F10=Delete'
     A  34 26                          23 10'F10=Delete'
     A*                                      End   /158802/
     A                                 23 21'F12=Previous screen'
     A*                                      End   /120026/
     A*                                      Begin /123857/
     A**35******                       23 41'F11=Authorise'
     A  35N45                          23 41'F11=Authorise'
     A*                                      End   /123857/
     A N24                             23 55'F14=Settlements'
      *
     A          R LE2110S0                  SFL
     A  21                                  SFLNXTCHG
     A            SVALD          6   H
     A            SSBRC          3   H
      *                                              Begin /CAP063/
     A            XREPN          5S 0H
     A            SEVAL          6   H
     A            SESEQ          3   H
     A            ASTS           1   H
      *                                              End   /CAP063/
      **Start of change CLE036
     A            SRCCY          3   H
      **End of change CLE036
     A*/COPY WNCPYSRC,LE2110DF03
     A*                                              Begin /126274/
     A*********** SACTN          1   B 10  4
     A*N49*******                           DSPATR(UL HI)
     A**49*******                           DSPATR(RI PC)
     A*********** SLOAN          6   O 10  7DSPATR(HI)
     A*********** SCNUM          6   O 10 16DSPATR(HI)
     A*********** SVDAT          7   O 10 25DSPATR(HI)
     A*********** SSEQN          3   O 10 35DSPATR(HI)
     A*********** SCURP         19   O 10 39DSPATR(HI)
     A*********** SCCY           3   O 10 59DSPATR(HI)
     A            SACTN          1   B 10  2
     A N49                                  DSPATR(UL HI)
     A  49                                  DSPATR(RI PC)
     A            SLOAN          6   O 10  4DSPATR(HI)
     A            SCNUM          6   O 10 11DSPATR(HI)
     A            SCSNM         10   O 10 18DSPATR(HI)
     A            SVDAT          7   O 10 29DSPATR(HI)
     A            SSEQN          3   O 10 37DSPATR(HI)
     A            SCURP         19   O 10 41DSPATR(HI)
      *                                              Begin /156873/
     A*********** SCCY           3   O 10 61DSPATR(HI)
     A            SSCCY          3   O 10 61DSPATR(HI)
      *                                              End   /156873/
     A*                                              End   /126274/
     A*                                              Begin /130737/
     A************SSTAT         16   O 10 64DSPATR(HI)
     A            SSTAT         16   O 10 65DSPATR(HI)
     A*                                              End   /130737/
      *
      * Subfile control
      *
     A          R LE2110C0                  SFLCTL(LE2110S0)
     A                                      SFLPAG(10)
     A                                      SFLSIZ(11)
     A  23                                  SFLDSP
     A  22                                  SFLDSPCTL
     A N22N23                               SFLCLR
     A N37                                  ROLLUP(64)
     A  37                                  SFLEND(*MORE)
     A                                      CF05
      *                                              Begin /CAP063/
     A**************************************CF09
     A**************************************CF12
     A N65                                  CF09
     A N65                                  CF12
      *                                              End   /CAP063/
     A                                      OVERLAY
      *
      *
      *
     A            SRCNR          4S 0H      SFLRCDNBR
      *
     A                                  4 54'Review from'
     A            SRFLN          6A  B  4 66
     A  73                                  DSPATR(RI PC)
     A N73                                  DSPATR(UL HI)
      *
     A                                  5  2'Actions,'
      *
     A  46                              5 54'Req. Authorisation'
     A  46        SRAUT          1A  B  5 73
     A  74                                  DSPATR(RI PC)
     A N74                                  DSPATR(UL HI)
      *
      *                                              Begin /CAP063/
     A**********************************6  3'E=Enquire'
     A N65                              6  3'E=Enquire'
     A  65                              6  3'U=Amend  '
      *                                              End   /CAP063/
      *
      *                                              Begin /CAP063/
     A**********************************6 16'A=Amend'
     A N65                              6 16'A=Amend'
     A  65                              6 14'Q=Delete '
      *                                              End   /CAP063/
      *
     A                                  6 27'R=Reverse'
      *                                              Begin /CAP063/
     A  65                                  DSPATR(ND)
      *                                              End   /CAP063/
      *
     A  86 46                           6 40'X=Authorise'
      *
      *                                              Begin /126274/
     A***********                       8  3'Sel'
      *
     A***********                       8  8'Loan'
      *
     A                                  8  1'Sel'
      *
     A                                  8  5'Loan'
      *
      *                                              End   /126274/
     A                                  8 15'Customer'
      *
      *                                              Begin /126274/
     A***********                       8 26'Value'
      *
     A***********                       8 35'Seq'
      *
     A***********                       8 44'Current'
      *
     A***********                       8 59'Ccy'
      *
     A                                  8 30'Value'
      *
     A                                  8 37'Seq'
      *
     A                                  8 46'Current'
      *
     A                                  8 61'Ccy'
      *                                              End   /126274/
      *
     A                                  8 68'Status'
     A*                                              Begin /126274/
      *
     A***********                       9 27'date'
      *
     A***********                       9 43'Principal'
      *
     A                                  9 31'date'
      *
     A                                  9 45'Principal'
      *
     A*                                              End   /126274/
      *
      * Subfile footer
      *
     A          R LE2110F3
      *
     A                                      OVERLAY
     A                                 22  2'F3=Exit'
     A                                 22 12'F5=Refresh'
     A                                 22 25'F9=Insert'
      *                                              Begin /CAP063/
     A  65                                  DSPATR(ND)
      *                                              End   /CAP063/
     A                                 22 37'F12=Previous screen'
      *                                              Begin /CAP063/
     A  65                                  DSPATR(ND)
      *                                              End   /CAP063/
     A                                 23 71'Page Keys'
     A  16                                  DSPATR(ND)
     A  38                             21  2'*** No Records to Display ***'
     A                                      DSPATR(HI)
      *
     A          R LE2110S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R LE2110C1                  TEXT('Program messages')
     A                                      SFLCTL(LE2110S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
