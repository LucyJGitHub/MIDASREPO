     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Repayment schedule enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module                              *
     A*                                                               *
     A*  DSPF/LE2120DF - Initial screen for REPAYMENT SCHED INPUT     *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE036             Date 25Sep03               *
      *                 CLE034             Date 05May03               *
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 126274             Date 24NOV97               *
     A*                 123857             Date 20OCT97               *
     A*                 121019             Date 22JUL97               *
     A*                 120027             Date 03JUL97               *
     A*                 CLE004 *Create     Date 02Dec96               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE036 - Lending Enhancements for Nordea Phase 1 Priority 2: *
      *           Allow repayments in facility currency.              *
      *  CLE034 - Lending Enhancements for Phase 1 Priority 1A.       *
     A*  126274 - Subfile should show Shortname                       *
      *  123857 - Condition the output of F11 for authorisation       *
     A*  121019 - Change screen text.                                 *
     A*  120027 - Screen text for delete is wrong should say 'R' for  *
     A*           Reverse.                                            *
     A*  CLE004 - Customer Lending  Enhancements - Syndications.      *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CA03
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
      *
      * Header format
      *
     A          R LE2120F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 14'LE2120'
     A                                  1 29'REPAYMENT SCHEDULE INPUT'
     A                                      DSPATR(HI)
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
      * Initial screen
      *
     A          R LE2120F1
     A*/COPY WNCPYSRC,LE2120DF01
     A  27                                  CF12
     A                                      OVERLAY
     A                                  8 11'Loan no'
     A            SLOAN          6A  B  8 19
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(HI)
     A N31                                  DSPATR(UL)
     A                                  8 27'Action'
     A            SACTN          1A  B  8 34
     A  27                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(HI)
     A N32N27                               DSPATR(UL)
     A                                  8 37'Value Date'
     A            SVDAT          6A  B  8 48
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(HI)
     A N33                                  DSPATR(UL)
     A                                  8 56'Sequence'
     A            SSEQN          3A  B  8 65
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(HI)
     A N34                                  DSPATR(UL)
     A*                                      Begin /121019/
     A*N27**************************** 10 10'or'
     A N27                             10 37'OR'
     A*                                      End   /121019/
     A N27                             12 15'Review from Loan no'
     A            SLONR          6A  B 12 35
     A  27                                  DSPATR(ND PR)
     A  35N27                               DSPATR(RI)
     A  35N27                               DSPATR(PC)
     A N35N27                               DSPATR(HI)
     A N35N27                               DSPATR(UL)
     A N27                             12 43'Date'
     A            SDATR          6A  B 12 48
     A  27                                  DSPATR(ND PR)
     A  36N27                               DSPATR(RI)
     A  36N27                               DSPATR(PC)
     A N36N27                               DSPATR(HI)
     A N36N27                               DSPATR(UL)
     A N27                             12 56'Seq no'
     A            SSEQR          3A  B 12 63
     A  27                                  DSPATR(ND PR)
     A  37N27                               DSPATR(RI)
     A  37N27                               DSPATR(PC)
     A N37N27                               DSPATR(HI)
     A N37N27                               DSPATR(UL)
     A*                                      Begin /121019/
     A**24N27************************* 14 28'Unauthorised Only'
     A  24N27                          14 27'Unauthorised Only?'
     A*                                      End   /121019/
     A            SAUTR          1A  B 14 46
     A  24 38N27                            DSPATR(RI)
     A  24 38N27                            DSPATR(PC)
     A  24N38N27                            DSPATR(HI)
     A  24N38N27                            DSPATR(UL)
     A  27
     AON24                                  DSPATR(PR ND)
     A                                 22  2'F3=Exit'
     A  27                             22 12'F12=Previous screen'
     A*/COPY WNCPYSRC,LE2120DF02
     A                                 23  2' '
     A  26                             20 11'Sequence no. allocated to last rep-
     A                                      ayment input'
     A  26        SLASN          3   O 20 59DSPATR(HI)
      *
      * Subfile
      *
     A          R LE2120S0                  SFL
     A  83                                  SFLNXTCHG
     A*                                              Begin /126274/
     A*********** SACTN          1A  B 11  4
     A**30*******                           DSPATR(RI PC)
     A*N30*******                           DSPATR(HI UL)
     A*********** SLOAN          6A  O 11  9
     A***********                           DSPATR(HI)
     A*********** SVALD          8A  O 11 17
     A***********                           DSPATR(HI)
     A*********** SSEQN          3A  O 11 27
     A***********                           DSPATR(HI)
     A*********** SCNUM          6A  O 11 34
     A***********                           DSPATR(HI)
     A            SACTN          1A  B 11  2
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI UL)
     A            SLOAN          6A  O 11  5
     A                                      DSPATR(HI)
     A            SVALD          8A  O 11 13
     A                                      DSPATR(HI)
     A            SSEQN          3A  O 11 22
     A                                      DSPATR(HI)
     A            SCNUM          6A  O 11 26
     A                                      DSPATR(HI)
     A            SCSNM         10A  O 11 33
     A                                      DSPATR(HI)
     A*                                              End   /126274/
     A            SCURR          3A  O 11 44
     A                                      DSPATR(HI)
     A            SAMTO         18A  O 11 49
     A                                      DSPATR(HI)
     A            SSTAT         10A  O 11 70
     A                                      DSPATR(HI)
     A            SVDTH          5  0H
     A            STATH          1   H
     A            SBRCH          3   H
     A            SORBH          3   H
     A*/COPY WNCPYSRC,LE2120DF03
      *
      * Subfile control record
      *
     A          R LE2120C0                  SFLCTL(LE2120S0)
     A                                      SFLSIZ(11)
     A                                      SFLPAG(10)
     A                                      CF09
     A                                      CF12
     A                                      CF05
     A                                      OVERLAY
     A  80                                  ROLLUP(81)
     A N80                                  SFLEND(*MORE)
     A N86                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  84                                  SFLCLR
     A            SSREC          4S 0H      SFLRCDNBR
     A                                  5  2'Actions,'
     A                                  4 51'Review from'
     A            SLONR          6A  B  4 63
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI UL)
     A            SDATR          6A  B  4 70
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI UL)
     A            SSEQR          3A  B  4 77
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI UL)
     A  24                              5 60'Unauthorised only'
     A            SAUTR          1A  B  5 78
     A  24 38                               DSPATR(RI PC)
     A  24N38                               DSPATR(HI UL)
     A N24                                  DSPATR(PR ND)
     A*                                      Begin /120027/
     A********************************* 6  2' E=Enquire    A=Amend    D=Delete'
     A                                  6  2' E=Enquire    A=Amend    R=Reverse'
     A*                                      End   /120027/
     A*                                              Begin /126274/
     A********************************* 8  2'Actn    Loan    Value    Seq   Cus-
     A*********************************     tomer   Curr           Amount     S-
     A*********************************     tatus'
     A********************************* 9 19'Date     No    Number'
     A                                  8  1'Sel  Loan     Value  Seq   Cus-
     A                                      tomer'
                                        8 44'Curr           Amount     Status'
     A                                  9 15'Date   No'
     A*                                              End   /126274/
     A  24                              6 39'X=Authorise'
      *
      * Footer for Subfile screen
      *
     A          R LE2120F2
     A                                 22  2'F3=Exit'
     A                                 22 12'F5=Refresh'
     A                                 22 25'F9=Insert'
     A                                 22 37'F12=Previous'
     A  29                             23 71'Page keys'
     A  86                             23  2'*** No Records to display ***'
     A                                      DSPATR(HI)
      *
     A          R LE2120F3
     A                                      OVERLAY
     A  50                                  CF10
      *                                              Begin /123857'
     A**51*******                           CF11
     A  51N87                               CF11
      *                                              End   /123857'
     A                                      CF12
     A                                      CF14
      *
     A                                  4 14'Loan no'
     A            SLOAN          6   O  4 28
     A                                      DSPATR(HI)
     A                                  4 39'Action'
     A            SACTN          1A  O  4 46
     A                                      DSPATR(HI)
     A                                  4 52'Sequence'
     A            SSEQN          3A  O  4 61
     A                                      DSPATR(HI)
      *
     A                                  6 28'Customer'
     A            SCUST         10A  B  6 46
     A  41                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N41                               DSPATR(UL)
     A                                      DSPATR(HI)
      *
     A                                  7 28'Value Date'
     A            SVDAT          6A  B  7 46
     A  42                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N42                               DSPATR(UL)
     A                                      DSPATR(HI)
      *
     A                                  8 28'Currency'
     A            SCURR          3A  B  8 46
     A  43                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N43                               DSPATR(UL)
     A                                      DSPATR(HI)
      *
     A                                  9 28'Amount'
     A            SAMNT         14A  B  9 46
     A  44                                  DSPATR(RI PC)
     A  22                                  DSPATR(PR)
     A N22N44                               DSPATR(UL)
     A                                      DSPATR(HI)
      *
     A                                 10 28'Auto-settlement'
     A            SAUTO          1A  B 10 46
     A  45                                  DSPATR(RI PC)
     A    N25                               DSPATR(PR)
     A  25N45                               DSPATR(UL)
     A                                      DSPATR(HI)
      **Start of change CLE034
     A  56                             11 28'Settlement Allocations'
     A  56        SSTAL          1   B 11 52
     A  46                                  DSPATR(RI PC)
     A  22N25                               DSPATR(PR)
     A N22N46
     AO 25                                  DSPATR(UL)
     A                                      DSPATR(HI)
      **End of change CLE034
      *
     A                                 12 12'Special Instructions'
     A            SSPI1         30A  B 12 38
     A                                      DSPATR(HI)
     A  22N25                               DSPATR(PR)
     A N22
     AO 25                                  DSPATR(UL)
     A            SSPI2         30A  B 13 38
     A                                      DSPATR(HI)
     A  22N25                               DSPATR(PR)
     A N22
     AO 25                                  DSPATR(UL)
     A            SSPI3         30A  B 14 38
     A                                      DSPATR(HI)
     A  22N25                               DSPATR(PR)
     A N22
     AO 25                                  DSPATR(UL)
      **Start of change CLE036
      *
     A  11                             16 12'Loan Currency Repayment Amount:'
     A  11        SLRAM         14A  O 16 46
     A                                      DSPATR(HI)
      *
     A  11        SLCCY          3A  O 16 61
     A                                      DSPATR(HI)
      *
      **End of change CLE036
     A            SSTAT         10A  O 22 60
     A                                      DSPATR(HI)
      *
      * Footer for Detail screen
      *
     A          R LE2120F4
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 45'F12=Previous'
     A  50                             23 13'F10=Delete'
      *                                              Begin /123857'
     A**51*******                      23 28'F11=Authorise'
     A  51N87                          23 28'F11=Authorise'
      *                                              End   /123857'
     A                                 23 61'F14=Settlements'
      *
      * Message subfile record
      *
     A          R LE2120S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * Message subfile control
      *
     A          R LE2120C1                  TEXT('Program messages')
     A                                      SFLCTL(LE2120S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  85
     AON85                                  SFLEND
     A            ##PGM                     SFLPGMQ
