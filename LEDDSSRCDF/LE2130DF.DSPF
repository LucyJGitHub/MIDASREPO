      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Transfer of Principal Input Display')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LE2130DF - Transfer of Principal Input Display          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 138865             Date 21Sep98               *
      *  Prev Amend No. 123857             Date 24Oct97               *
      *                 121019             Date 22Jul97               *
      *                 CLE004 *Create     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  138865 - Cannot change original loan when transfer principal *
      *           on parts sold                                       *
      *  123857 - Condition F11 for authorisation on ICD value.       *
      *  121019 - Change screen text.                                 *
      *  CLE004 - Customer Lending  Enhancements - Syndications       *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
      *
      * Header format
      *
     A          R LE2130F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 14'LE2130'
     A                                  1 31'TRANSFER OF PRINCIPAL +
     A                                       INPUT'
     A                                      DSPATR(HI)
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
      * Initial screen
      *
     A          R LE2130F1
     A                                      OVERLAY
     A  38                                  CA12
      *
     A                                  8 11'Action'
     A            SACTN          1   B  8 19
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A N21N38                               DSPATR(UL)
     A  38                                  DSPATR(PR)
     A                                  8 27'Originating Loan no'
     A            SLNRF          6   B  8 51
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI UL)
     A                                  9 27'Value date'
     A            SVDAT          6   B  9 51
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI UL)
     A                                 10 27'Transfer Sequence no'
     A            SLASN          3   B 10 51
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI UL)
      *
     A*                                      Begin /121019/
     A******************************** 14  4'OR'
     A                                 12 34'OR'
     A*                                      End   /121019/
     A  38                                  DSPATR(ND)
     A                                 14 11'Review from'
     A  38                                  DSPATR(ND)
     A                                 14 27'Originating Loan no'
     A  38                                  DSPATR(ND)
     A            SRLON          6   B 14 51
     A  38                                  DSPATR(PR ND)
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI UL)
     A                                 15 27'Value date'
     A  38                                  DSPATR(ND)
     A            SRVDT          6   B 15 51
     A  38                                  DSPATR(PR ND)
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI UL)
     A                                 16 27'Transfer Sequence no'
     A  38                                  DSPATR(ND)
     A            SRSEQ          3   B 16 51
     A  38                                  DSPATR(PR ND)
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI UL)
     A*                                      Begin /121019/
     A******************************** 17 27'Unauthorised'
     A                                 17 27'Unauthorised Only?'
     A*                                      End   /121019/
     A  38
     AON37                                  DSPATR(ND)
     A            SRAUT          1   B 17 51
     A  38
     AON37                                  DSPATR(PR ND)
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI UL)
     A                                 20 24'Receiving loan inserted'
     A N39                                  DSPATR(ND)
     A            SLALC          6   O 20 48DSPATR(HI)
     A N39                                  DSPATR(ND)
     A                                 21 13'Sequence no. allocated +
     A                                      to ''Transfer Principal +
     A                                      From'''
     A N40                                  DSPATR(ND)
     A            SFRSQ          3   O 21 65DSPATR(HI)
     A N40                                  DSPATR(ND)
     A                                 22 39'''Transfer Principal +
     A                                      To'''
     A N41                                  DSPATR(ND)
     A            STOSQ          3   O 22 65DSPATR(HI)
     A N41                                  DSPATR(ND)
      *
     A                                 23  2'F3=Exit'
     A  38                             23 11'F12=Previous Screen'
      *
      * Detail screen
      *
     A          R LE2130F2
     A                                      OVERLAY
     A  34                                  CA10
      *                                              Begin /123857/
     A**33*******                           CA11
     A  33N43                               CA11
      *                                              End   /123857/
     A                                      CA12
      *
      *                                              Begin /138865/
      *                                              End   /138865/
      *
     A                                  5  2'Originating loan'
     A            SLNRF          6   O  5 24DSPATR(HI)
     A                                  5 38'Action'
     A            SACTN          1   O  5 46DSPATR(HI)
     A                                  5 54'Status'
     A            SSTAT         16   O  5 62DSPATR(HI)
     A                                  7  2'Customer'
     A            SCNUM         10   O  7 24DSPATR(HI)
     A                                  8  2'Outstanding amount'
     A            SOAMT         15   O  8 24DSPATR(HI)
     A                                  8 41'Ccy'
     A            SCURR          3   O  8 46DSPATR(HI)
      *
     A                                 12  2'Receiving loan'
     A            SROLN          6   B 12 24
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI)
     A  42
     AO 35                                  DSPATR(PR)
     A N29N35N42                            DSPATR(UL)
     A                                 12 36'New loan'
     A            SNRLI          1   B 12 46
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A  42
     AO 35                                  DSPATR(PR)
     A N30N35N42                            DSPATR(UL)
     A                                 12 54'Branch'
     A            SBRCA          3   B 12 62
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A  42
     AO 35                                  DSPATR(PR)
     A N36N35N42                            DSPATR(UL)
     A                                 14  2'Receiving customer'
     A            SRORC         10   B 14 24
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A  42
     AO 35                                  DSPATR(PR)
     A N31N35N42                            DSPATR(UL)
      *                                              Begin /138865/
     A                                 14 39'Original Loan'
     A N45                                  DSPATR(ND)
     A            SOLNO          6   B 14 56
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(HI)
     A  42
     AON45
     AO 35                                  DSPATR(PR)
     AAN44N35N42                            DSPATR(UL)
     A N45                                  DSPATR(ND)
      *                                              End   /138865/
     A                                 16  2'Transfer amount'
     A            SPRAM         14   B 16 24
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A  35                                  DSPATR(PR)
     A N32N35                               DSPATR(UL)
      *
     A                                 23  2'F3=Exit'
     A  34                             23 11'F10=Delete'
      *                                              Begin /123857/
     A**33*******                      23 23'F11=Authorise'
     A  33N43                          23 23'F11=Authorise'
      *                                              End   /123857/
     A                                 23 38'F12=Previous screen'
      *
      * Footer for subfile screen
      *
     A          R LE2130F3
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
     A                                 23 25'F9=Insert'
     A                                 23 37'F12=Previous screen'
      *
      * Subfile of loan records
      *
     A          R LE2130S0
     A                                      SFL
     A  76                                  SFLNXTCHG
      *
     A            SHVDT          5  0H
     A            SADAT          6   H
     A            SBRCH          3   H
     A            SORED          5  0H
     A            SIUSR         10   H
     A            SAUSR         10   H
     A            SASTS          1   H
      *
     A            SLSEL          1   B 11  4
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI UL)
     A            SSLON          6   O 11  7DSPATR(HI)
     A            SSVDT          7   O 11 15DSPATR(HI)
     A            SSSEQ          3   O 11 25DSPATR(HI)
     A            SSRLN          6   O 11 34DSPATR(HI)
     A            SSNEW          1   O 11 45DSPATR(HI)
     A            SSAMT         14   O 11 49DSPATR(HI)
     A            SSSTS         16   O 11 64DSPATR(HI)
      *
      * Subfile control
      *
     A          R LE2130C0
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      SFLCTL(LE2130S0)
     A N78N77                               SFLDSP
     A N78                                  SFLDSPCTL
     A  78                                  SFLCLR
     A                                      SFLPAG(10)
     A                                      SFLSIZ(11)
     A N78 79                               SFLEND(*MORE)
     A                                      OVERLAY
     A N79                                  ROLLUP(74)
     A            SRNBR          4S 0H      SFLRCDNBR
     A                                  4 47'Review from'
     A            SRLON          6   B  4 60
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI UL)
     A            SRVDT          6   B  4 67
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI UL)
     A            SRSEQ          3   B  4 74
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI UL)
     A                                  5 47'Unauthorised'
     A N37                                  DSPATR(ND)
     A            SRAUT          1   B  5 60
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI UL)
     A N37                                  DSPATR(PR ND)
     A                                  6  3'Actions,'
     A                                  7  3'E=Enquire'
     A                                  7 16'A=Amend'
     A                                  7 27'R=Reverse'
     A  37                              7 40'X=Authorise'
      *
     A                                  9  3'Sel'
     A                                  9  8'Orig.'
     A                                  9 16'Value'
     A                                  9 23'Transfer'
     A                                  9 33'Receiving'
     A                                  9 44'New'
     A                                  9 54'Transfer'
     A                                  9 64'Status'
      *
     A                                 10  8'Loan'
     A                                 10 16'Date'
     A                                 10 24'Seq No'
     A                                 10 35'Loan'
     A                                 10 44'Loan'
     A                                 10 56'Amount'
      *
      * Message subfile record
      *
     A          R LE2130S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * Message subfile control
      *
     A          R LE2130C1                  TEXT('Program messages')
     A                                      SFLCTL(LE2130S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  75
     AON75                                  SFLEND
     A            ##PGM                     SFLPGMQ
