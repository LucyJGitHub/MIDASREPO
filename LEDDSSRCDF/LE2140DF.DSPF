      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Manual Facility Utilisation')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LE2140DF - Manual Facility Utilisation Display          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 150215             Date 11Dec98               *
      *                 148360             Date 25Nov98               *
      *                 138870             Date 15Jul98               *
      *                 133261             Date 20May98               *
      *                 CLE005 *Create     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  150215 - Include drilldown for Manual Facility Utilisation.  *
      *  148360 - Could not enter a negative utilisation amount.      *
      *  138870 - Could not amend narrative for facility utilisation  *
      *  133261 - The Help tags do not have a help tag no.            *
      *  CLE005 - Customer Lending  Enhancements - Syndications       *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CF03
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
      *
      ** Header format
      *
     A          R LE2140F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 14'LE2140'
     A                                  1 28'MANUAL FACILITY UTILISATION'
     A                                      DSPATR(HI)
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
      ** Initial screen
      *
     A          R LE2140F1
     A                                      OVERLAY
     A  25                                  CF12
      *
     A                                  7 19'Action'
     A            SACTN          1   B  7 29
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI UL)
     A  25                                  DSPATR(PR)
      *
     A                                  7 36'Customer'
     A            SCNUM         10   B  7 54
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI UL)
      *
     A                                  8 36'Facility Type/No'
     A            SFTYP          3   B  8 54
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI UL)
      *
     A            SFNUM          2   B  8 58
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI UL)
      *
     A                                  9 36'Sequence No.'
     A            SFSEQ          3   B  9 54
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI UL)
      *
     A                                 11 40'OR'
     A  25                                  DSPATR(ND)
      *
     A                                 13 14'Review from   Customer'
     A  25                                  DSPATR(ND)
     A            SRCUS          6   B 13 38
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI UL)
     A  25                                  DSPATR(ND PR)
      *
     A                                 13 47'Facility Type/No'
     A  25                                  DSPATR(ND)
     A            SRTYP          3   B 13 65
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI UL)
     A  25                                  DSPATR(ND PR)
      *
     A            SRNUM          2   B 13 69
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI UL)
     A  25                                  DSPATR(ND PR)
      *
     A                                 15 28'Unauthorised Only?'
     A  25
     AON29                                  DSPATR(ND)
     A            SRAUT          1   B 15 48
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI UL)
     A  25
     AON29                                  DSPATR(ND PR)
      *
     A                                 20 18'Sequence no. allocated to last -
     A                                      utilisation'
     A N24                                  DSPATR(ND)
     A            SGSQN          3A  O 20 61
     A N24                                  DSPATR(ND)
     A                                      DSPATR(HI)
      *
     A                                 23  2'F3=Exit'
     A  25                             23 12'F12=Previous screen'
      *
      ** Subfile footer
      *
     A          R LE2140F2
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
      *                                              Begin /150215/
     A***********                      23 25'F9=Insert'
     A N05                             23 25'F9=Insert'
      *                                              End   /150215/
     A                                 23 37'F12=Previous Screen'
     A  13                             23 71'Page keys'
      *
      ** Detail footer
      *
     A          R LE2140F3
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A  26                             23 25'F11=Authorise'
     A  27                             23 12'F10=Delete'
     A                                 23 41'F12=Previous screen'
      *
      ** Details screen
      *
     A          R LE2140F4
     A                                      OVERLAY
     A  26                                  CF11
     A  27                                  CF10
     A                                      CF12
      *
     A                                  4  2'Customer'
     A            SDCNO          6   O  4 11DSPATR(HI)
     A            SDCNM         10   O  4 18DSPATR(HI)
     A                                  4 43'Action'
     A            SDACT          1   O  4 51DSPATR(HI)
     A                                  4 57'Status'
     A            SDSTS         16   O  4 64DSPATR(HI)
     A                                  5  2'Facility Type/No'
     A            SDFTP          3   O  5 20DSPATR(HI)
     A            SDFNO          2   O  5 24DSPATR(HI)
     A                                  6  2'Sequence No'
     A            SDFSQ          3   O  6 20DSPATR(HI)
      *
     A                                  9  2'Currency'
     A                                 10  2'Utilisation Amount'
     A                                 11  2'Start Date'
     A                                 12  2'Expiry Date'
     A                                 13  2'Exchange Rate'
     A                                 13 37'Exchange Rate Indicator'
     A                                 15  2'Narrative'
     A            SUCCY          3   B  9 21
     A
     A  43                                  DSPATR(RI PC)
     A  40                                  DSPATR(PR)
     A N40N43                               DSPATR(UL)
     A                                      DSPATR(HI)
      *
     A            SUAMT         14   B 10 21
     A  44                                  DSPATR(RI PC)
     A  55                                  DSPATR(PR)
     A N44N55                               DSPATR(UL)
     A                                      DSPATR(HI)
      *                                              Begin /148360/
     A            SAMTS          1   B 10 36
     A  51                                  DSPATR(RI PC)
     A  55                                  DSPATR(PR)
     A N51N55                               DSPATR(UL)
     A                                      DSPATR(HI)
      *                                              End   /148360/
      *
     A            SSTDT          6   B 11 21
     A  45                                  DSPATR(RI PC)
     A  40                                  DSPATR(PR)
     A N40N45                               DSPATR(UL)
     A                                      DSPATR(HI)
      *
     A            SEXDT          6   B 12 21
     A  46                                  DSPATR(RI PC)
     A  55                                  DSPATR(PR)
     A N55N46                               DSPATR(UL)
     A                                      DSPATR(HI)
      *
     A            SUXRT         12   B 13 21
     A  47                                  DSPATR(RI PC)
     A  40                                  DSPATR(PR)
     A N40N47                               DSPATR(UL)
     A                                      DSPATR(HI)
      *
     A            SUXID          1   B 13 61
     A  48                                  DSPATR(RI PC)
     A  40                                  DSPATR(PR)
     A N40N48                               DSPATR(UL)
     A                                      DSPATR(HI)
      *
     A            SUNAR         30   B 15 21
      *                                              Begin /138870/
     A**40*******                           DSPATR(PR)
     A*N40*******                           DSPATR(UL)
     A  41                                  DSPATR(PR)
     A N41                                  DSPATR(UL)
      *                                              End   /138870/
     A                                      DSPATR(HI)
      *
      ** Subfile records
      *
     A          R LE2140S0
     A                                      SFL
     A  20                                  SFLNXTCHG
     A            SLSEL          1   B 10  8
     A  49                                  DSPATR(RI PC)
     A N49                                  DSPATR(HI UL)
     A            SSCUS          6   O 10 14DSPATR(HI)
     A            SSTYP          3   O 10 25DSPATR(HI)
     A            SSNUM          2   O 10 33DSPATR(HI)
     A            SSUSQ          3   O 10 45DSPATR(HI)
     A            SSCCY          3   O 10 57DSPATR(HI)
     A            SSSTS         10   O 10 66DSPATR(HI)
      *
      ** Subfile control
      *
     A          R LE2140C0
     A                                      CF05
      *                                              Begin /150215/
     A***********                           CF09
     A N05                                  CF09
      *                                              End   /150215/
     A                                      CF12
     A                                      SFLCTL(LE2140S0)
     A N11N12                               SFLDSP
     A N11                                  SFLDSPCTL
     A  11                                  SFLCLR
     A  19                                  SFLEND(*MORE)
     A N19                                  ROLLUP(21)
     A                                      SFLPAG(10)
     A                                      SFLSIZ(11)
     A                                      OVERLAY
     A            SRECN          4S 0H      SFLRCDNBR
      *
     A                                  4 54'Review from'
     A            SRCUS          6   B  4 66
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI UL)
      *                                              Begin /150215/
     A  05                                  DSPATR(PR)
      *                                              End   /150215/
     A            SRTYP          3   B  4 73
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI UL)
      *                                              Begin /150215/
     A  05                                  DSPATR(PR)
      *                                              End   /150215/
     A            SRNUM          2   B  4 77
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI UL)
      *                                              Begin /150215/
     A  05                                  DSPATR(PR)
      *                                              End   /150215/
      *
     A                                  5 54'Unauthorised Only?'
      *                                              Begin /150215/
     A*N29*******                           DSPATR(ND)
     A  05
     AON29                                  DSPATR(ND)
      *                                              End   /150215/
     A            SRAUT          1   B  5 73
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI UL)
      *                                              Begin /150215/
     A*N29*******                           DSPATR(ND PR)
     A  05
     AON29                                  DSPATR(ND PR)
      *                                              End   /150215/
     A                                  5  2'Actions,'
     A                                  6  3'E=Enquire'
      *                                              Begin /150215/
     A***********                       6 16'A=Amend'
     A***********                       6 27'D=Delete'
     A**29*******                       6 39'X=Authorise'
     A N05                              6 16'A=Amend'
     A N05                              6 27'D=Delete'
     A  29N05                           6 39'X=Authorise'
      *                                              End   /150215/
     A                                  8  7'Sel'
     A                                  8 14'Customer'
     A                                  8 25'--Facility--'
     A                                  8 41'Utilisation'
     A                                  8 55'Currency'
     A                                  8 66'Status'
     A                                  9 25'Type'
     A                                  9 31'Number'
     A                                  9 43'Seq. No'
      *
      ** Message subfile record
      *
     A          R LE2140S1                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      ** Message subfile control
      *
     A          R LE2140C1                  TEXT('Program messages')
     A                                      SFLCTL(LE2140S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  19
     AON19                                  SFLEND
     A            ##PGM                     SFLPGMQ
