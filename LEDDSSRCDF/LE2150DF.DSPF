     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Facility amendments input')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module                              *
     A*                                                               *
     A*  DSPF/LE2150DF - Facility Amendments Input                    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. 165649             Date 17Jul01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 157580             Date 03May99               *
      *                 133261             Date 20May98               *
     A*                 CLE005 *Create     Date 22May97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  165649 - Add a new negative sign field for reactivation amt. *
      *  157580 - Various help tag problems.                          *
      *  133261 - The Help tags do not have a help tag no.            *
     A*  CLE005 - Customer Lending Enhancements - Others (T3)         *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
      ** Header format
      *
     A          R LE2150F0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 14'LE2150'
     A                                  1 29'FACILITY AMENDMENTS INPUT'
     A                                      DSPATR(HI)
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 72TIME
      *
      ** Initial screen
      *
     A          R LE2150F1
     A                                      CA03
     A  20                                  CA12
     A                                      OVERLAY
      *
     A                                  7 19'Action'
     A            F1ACTI         1A  B  7 29DSPATR(HI)
     A  20                                  DSPATR(PR)
     A N11N20                               DSPATR(UL)
     A  11                                  DSPATR(RI PC)
     A                                  7 36'Customer'
     A            F1CUST        10A  B  7 54DSPATR(HI)
     A N12                                  DSPATR(UL)
     A  12                                  DSPATR(RI PC)
     A                                  8 36'Facility Type/No'
     A            F1FACT         3A  B  8 54DSPATR(HI)
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(RI PC)
     A            F1FCNO         2A  B  8 58DSPATR(HI)
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(RI PC)
     A                                  9 36'Amendment Type'
     A            F1FATP         2A  B  9 54DSPATR(HI)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(RI PC)
     A                                 10 36'Value Date'
     A            F1VLDT         6A  B 10 54DSPATR(HI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(RI PC)
     A                                 11 36'Sequence No.'
     A            F1SQNO         3A  B 11 54DSPATR(HI)
     A N16                                  DSPATR(UL)
     A  16                                  DSPATR(RI PC)
     A N20                             13 40'OR'
     A N20                             15 14'Review from   Customer'
     A N20        F1CNUM         6A  B 15 38DSPATR(HI)
     A N17                                  DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A N20                             15 47'Facility Type/No'
     A N20        F1FAC2         3A  B 15 65DSPATR(HI)
     A N18                                  DSPATR(UL)
     A  18                                  DSPATR(RI PC)
     A N20        F1FCN2         2A  B 15 69DSPATR(HI)
     A N18                                  DSPATR(UL)
     A  18                                  DSPATR(RI PC)
     A N20                             17 30'Unauthorised Only?'
     A N20        F1FAAM         1A  B 17 50DSPATR(HI)
     A N19                                  DSPATR(UL)
     A  19                                  DSPATR(RI PC)
     A  39                             20 14'Sequence no. allocated to last fa-
     A                                      cility amendment'
     A  39        F1SQN2         3A  O 20 64DSPATR(HI)
     A                                 23  2'F3=Exit'
     A  20                             23 12'F12=Previous Screen'
      *
      ** Detail screen
      *
     A          R LE2150F2
     A                                      CA03
     A  25                                  CA10
     A  26                                  CA11
     A                                      CA12
     A                                      OVERLAY
      *
     A                                  4  2'Customer'
     A            F2CNUM         6A  O  4 11DSPATR(HI)
     A            F2CUST        10A  O  4 18DSPATR(HI)
     A                                  4 43'Action'
     A            F2ACTI         1A  O  4 51DSPATR(HI)
     A                                  4 57'Status'
     A            F2ASTS        16A  O  4 64DSPATR(HI)
     A                                  5  2'Facility Type/No'
     A            F2FACT         3A  O  5 20DSPATR(HI)
     A            F2FCNO         2A  O  5 24DSPATR(HI)
     A                                  7  2'Amendment Type'
     A            F2FATP        24A  O  7 18DSPATR(HI)
     A                                  8  2'Value Date'
     A            F2VLDT         6A  O  8 18DSPATR(HI)
     A                                  8 29'Sequence'
     A            F2SQNO         3A  O  8 39DSPATR(HI)
     A                                 11  2'Currency'
     A            F2ACCY         3A  B 11 20DSPATR(HI)
     A N21N30                               DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A  30                                  DSPATR(PR)
     A                                 12  2'Amount'
     A            F2AAMT        14A  B 12 20DSPATR(HI)
     A N22N30                               DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A  30                                  DSPATR(PR)
     A** Start of Change 165649
     A  43        F2SIGN         1A  B 12 35DSPATR(HI)
     A  43N30N44                            DSPATR(UL)
     A  43 44                               DSPATR(RI PC)
     A  43 30                               DSPATR(PR)
     A** End of Change 165649
     A                                 13  2'Revolving Credit'
     A            F2RVCR         1A  B 13 20DSPATR(HI)
     A N23N30                               DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A  30                                  DSPATR(PR)
     A                                 14  2'New Expiry Date'
     A            F2NDEX         6A  B 14 20DSPATR(HI)
     A N24N30                               DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A  30                                  DSPATR(PR)
     A                                 23  2'F3=Exit'
     A  25                             23 12'F10=Delete'
     A  26                             23 25'F11=Authorise'
     A                                 23 41'F12=Previous Screen'
      *
      ** Footer format
      *
     A          R LE2150F3
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
     A                                 23 25'F9=Insert'
     A                                 23 37'F12=Previous Screen'
     A  32                             23 71'Page keys'
      *
      ** Subfile records
      *
     A          R LE2150S0                  SFL
     A  37                                  SFLNXTCHG
     A            S0AAMT        13  0H
     A            S0ACCY         3   H
     A            S0AUSR        10   H
     A            S0BRCA         3   H
     A            S0FAT2         2   H
     A            S0IUSR        10   H
     A            S0NDEX         5  0H
     A            S0ORBR         3   H
     A            S0RVCR         1   H
     A            S0VLD2         6   H
     A*/COPY WNCPYSRC,LE2150DF01
     A            S0SEL          1A  B 10  3DSPATR(HI)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(RI PC)
     A  38                                  DSPATR(ND PR)
     A            S0CNUM         6A  O 10  7DSPATR(HI)
     A            S0FACT         3A  O 10 17DSPATR(HI)
     A            S0FCNO         2A  O 10 23DSPATR(HI)
     A            S0VLDT         7A  O 10 27DSPATR(HI)
     A            S0SQNO         3A  O 10 37DSPATR(HI)
     A            S0FATP        24A  O 10 42DSPATR(HI)
     A            S0ASTS        10A  O 10 69DSPATR(HI)
      *
      ** Subfile control
      *
     A          R LE2150C0                  SFLCTL(LE2150S0)
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      OVERLAY
     A N33                                  SFLDSP SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A N34                                  ROLLUP(35)
     A                                      SFLSIZ(11) SFLPAG(10)
      *
     A            SSREC          4S 0H      SFLRCDNBR
     A                                  4 54'Review from'
     A            C0CNUM         6A  B  4 66DSPATR(HI)
     A N27                                  DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A            C0FACT         3A  B  4 73DSPATR(HI)
     A N28                                  DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A            C0FCNO         2A  B  4 77DSPATR(HI)
     A N28                                  DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A                                  5  2'Actions,'
     A                                  5 54'Unauthorised Only?'
     A            C0FAAM         1A  B  5 73DSPATR(HI)
     A N29                                  DSPATR(UL)
     A  29                                  DSPATR(RI PC)
     A                                  6  3'E=Enquire'
     A                                  6 16'A=Amend'
     A                                  6 27'D=Delete'
     A  31                              6 39'X=Authorise'
     A                                  8  2'Sel'
     A                                  8  7'Customer'
     A                                  8 17'Facility'
     A                                  8 28'Value'
     A                                  8 37'Seq'
     A                                  8 47'Amendment'
     A                                  8 69'Status'
     A                                  9 17'Type'
     A                                  9 23'No'
     A                                  9 28'Date'
     A                                  9 49'Type'
      *
      ** Message subfile record
      *
     A          R LE2150S1                  SFL
     A                                      SFLMSGRCD(24)
     A            SMSG                      SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
      ** Message subfile control
      *
     A          R LE2150C1                  SFLCTL(LE2150S1)
     A                                      OVERLAY
     A                                      SFLDSP SFLDSPCTL SFLINZ
     A  34
     AON34                                  SFLEND
     A                                      SFLSIZ(0002) SFLPAG(0001)
     A            SPGM                      SFLPGMQ
