      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Fee settlements/adjustments display')         *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      * DSPF/LE2220DF - Fee Settlements/Adustments Display           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE034             Date 05May03               *
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. 202826             Date 25Apr02               *
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 157580             Date 03May99               *
      *                 155671             Date 18Feb99               *
      *                 140815             Date 19Aug98               *
      *                 CLE005 *Create     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE034 - Lending Enhancements for Phase 1 Priority 1A.       *
      *  202826 - Protect Settlement type field for 'A'mend.          *
      *  157580 - Various help tag problems.                          *
      *  155671 - Change 'Amount Past Due' to 'Amount Due/Past Due'.  *
      *  140815 - User should be able to use Customer Shortname on    *
      *           input of Fee Adjustment                             *
      *  CLE005 - Customer Lending  Enhancements - Others (T3)        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
      * Header format
      *
     A          R LE2220F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 14'LE2220'
     A  21                              1 31'FEE ADJUSTMENT INPUT'
     A                                      DSPATR(HI)
     A  22                              1 31'FEE SETTLEMENT INPUT'
     A                                      DSPATR(HI)
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
      * Initial screen
      *
     A          R LE2220F1
     A                                      OVERLAY
     A  24                                  CA12
      *
     A                                  7 11'Action'
     A            SACTN          1   B  7 21
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A N30N24                               DSPATR(UL)
     A  24                                  DSPATR(PR)
     A                                  7 31'Customer'
      *                                              Begin /140815/
     A*********** SCNUM          6   B  7 50
     A            SCNUM         10   B  7 50
      *                                              End   /140815/
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI UL)
     A                                  8 31'Facility'
     A            SFACT          3   B  8 50
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI UL)
     A            SFCNO          2   B  8 54
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI UL)
     A                                  9 31'Fee Sequence'
     A            SFSEQ          2   B  9 50
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI UL)
     A                                 10 31'Value Date'
     A N22                                  DSPATR(ND)
     A            SVDAT          6   B 10 50
     A N22                                  DSPATR(ND PR)
     A  34 22                               DSPATR(RI PC)
     A N34 22                               DSPATR(HI UL)
     A*/COPY WNCPYSRC,LEH00625
      *
     A                                 12 36'OR'
     A  24                                  DSPATR(ND)
     A                                 14 11'Review from'
     A  24                                  DSPATR(ND)
     A                                 14 31'Customer'
     A  24                                  DSPATR(ND)
     A            SRCUS          6   B 14 50
     A  24                                  DSPATR(PR ND)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI UL)
     A                                 15 31'Facility'
     A  24                                  DSPATR(ND)
     A            SRFAC          3   B 15 50
     A  24                                  DSPATR(PR ND)
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI UL)
     A            SRFNO          2   B 15 54
     A  24                                  DSPATR(PR ND)
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI UL)
     A                                 16 31'Fee Sequence'
     A  24                                  DSPATR(ND)
     A            SRFSQ          2   B 16 50
     A  24                                  DSPATR(PR ND)
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI UL)
      *
     A                                 17 31'Unauthorised Only?'
     A  24
     AON23                                  DSPATR(ND)
     A            SRAUT          1   B 17 50
     A  24
     AON23                                  DSPATR(ND PR)
     A  38 23N24                            DSPATR(RI PC)
     A N38 23N24                            DSPATR(HI UL)
     A*/COPY WNCPYSRC,LEH00626
      *
     A                                 23  2'F3=Exit'
     A  24                             23 11'F12=Previous Screen'
      *
      * Detail screen
      *
     A          R LE2220F2
     A                                      OVERLAY
     A  25                                  CA10
     A  26                                  CA11
     A  27                                  CF14
     A                                      CA12
      *
     A                                  4 43'Action'
     A            SACTN          1   O  4 52DSPATR(HI)
     A                                  4 57'Status'
     A            SFSTS         16   O  4 64DSPATR(HI)
     A                                  6 10'Customer'
      *                                              Begin /140815/
     A*********** SCNUM          6   O  6 21DSPATR(HI)
     A***********                       6 29'Facility'
     A            SCNUM         10   O  6 19DSPATR(HI)
     A                                  6 30'Facility'
      *                                              End   /140815/
     A            SFACT          3   O  6 39DSPATR(HI)
     A            SFCNO          2   O  6 43DSPATR(HI)
     A                                  6 48'Fee Sequence'
     A            SFSEQ          2   O  6 62DSPATR(HI)
     A            SFTYP         10   O  6 66DSPATR(HI)
     A                                  7 10'Fee Currency'
     A            SFCCY          3   O  7 24DSPATR(HI)
     A                                  7 29'Fee Code'
     A            SFCOD          2   O  7 39DSPATR(HI)
     A            SFENV         20   O  7 43DSPATR(HI)
     A*/COPY WNCPYSRC,LEH00627
     A  21                              8 10'Total Accrued to Date'
      *                                              Begin /155671/
     A**22********                      8 10'Fee Amount Past Due'
     A  22                              8 10'Fee Amount Due/Past Due'
      *                                              End   /155671/
     A            SAMTP         18   O  8 34DSPATR(HI)
     A  22                              9 10'Value Date'
     A  22        SVDAT          6   O  9 24DSPATR(HI)
     A  22 34                               DSPATR(RI)
      *
     A                                 11 16'Adjustment Amount'
     A N21                                  DSPATR(ND)
     A            SFADJ         16   B 11 35
     A N21                                  DSPATR(ND)
     A N21
     AO 28 21                               DSPATR(PR)
     A  39 21                               DSPATR(RI PC)
     A N39 21                               DSPATR(HI)
     A N39 21N28                            DSPATR(UL)
     A                                 12 16'Sign (+ or -)'
     A N21                                  DSPATR(ND)
     A            SSIGN          1   B 12 50
     A N21                                  DSPATR(ND)
     A N21
     AO 28 21                               DSPATR(PR)
     A  40 21                               DSPATR(RI PC)
     A N40 21                               DSPATR(HI)
     A N40 21N28                            DSPATR(UL)
      *
     A                                 13 16'Settlement Amount'
     A N22                                  DSPATR(ND)
     A            SFSET         16   B 13 35
     A N22                                  DSPATR(ND)
     A N22
     AO 28 22                               DSPATR(PR)
     A  41 22                               DSPATR(RI PC)
     A N41 22                               DSPATR(HI)
     A N41 22N28                            DSPATR(UL)
     A                                 14 16'Write Off/Full/Partial'
     A N22                                  DSPATR(ND)
     A            STYPE          1   B 14 50
     A N22                                  DSPATR(ND)
     A N22
     A**Start of change 202826
     AO 16
     A**End Of change 202826
     AO 28 22                               DSPATR(PR)
     A  42 22                               DSPATR(RI PC)
     A N42 22                               DSPATR(HI)
     A**Start of change 202826
     A*N42*22N28***                         DSPATR(UL)                                        202826
     A N16
     AAN42 22N28                            DSPATR(UL)
     A**End of change 202826
      **Start of Change CLE034
     A  11 22                          15 16'Settlement Allocation'
     A  11 22     SFSTAL         1A  B 15 50
     A  12                                  DSPATR(RI PC)
     A  22 28                               DSPATR(PR)
     A N12 22                               DSPATR(HI)
     A N12 22N28                            DSPATR(UL)
      **End of Change CLE034
      *
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Previous Screen'
     A  25                             23 32'F10=Delete'
     A  26                             23 44'F11=Authorise'
     A  27                             23 59'F14=Settlements'
      *
      * Footer for subfile screen
      *
     A          R LE2220F3
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
     A                                 23 25'F9=Insert'
     A                                 23 37'F12=Previous screen'
     A  29                             23 71'Page keys'
      *
      * Subfile of loan records
      *
     A          R LE2220S0
     A                                      SFL
     A  76                                  SFLNXTCHG
      *
     A            SBRCH          3   H
     A            SIUSR         10   H
     A            SAUSR         10   H
     A            SSTAT          1   H
     A            SSVDT          5  0H
     A            SSADJ         13  0H
     A*/COPY WNCPYSRC,LE2220DF01
      *
     A            SLSEL          1   B 10  4
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI UL)
     A            SSCNO          6   O 10  9DSPATR(HI)
     A            SSFAC          3   O 10 19DSPATR(HI)
     A            SSFNO          2   O 10 23DSPATR(HI)
     A            SSFEE          2   O 10 29DSPATR(HI)
     A            SSDUE          7   O 10 34DSPATR(HI)
     A            SSAMT         19   O 10 43DSPATR(HI)
     A            SSCCY          3   O 10 64DSPATR(HI)
     A            SSSTS         10   O 10 71DSPATR(HI)
      *
      * Subfile control
      *
     A          R LE2220C0
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      SFLCTL(LE2220S0)
     A N78N77                               SFLDSP
     A N78                                  SFLDSPCTL
     A  78                                  SFLCLR
     A                                      SFLPAG(10)
     A                                      SFLSIZ(11)
     A N78 79                               SFLEND(*MORE)
     A                                      OVERLAY
      *
     A            SRNBR          4S 0H      SFLRCDNBR
     A                                  4 48'Review from'
     A            SRCUS          6   B  4 60
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI UL)
     A            SRFAC          3   B  4 67
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI UL)
     A            SRFNO          2   B  4 71
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI UL)
     A            SRFSQ          2   B  4 74
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI UL)
     A                                  5 48'Unathorised Only?'
     A N23                                  DSPATR(ND)
     A            SRAUT          1   B  5 67
     A N23                                  DSPATR(ND PR)
     A  38 23                               DSPATR(RI PC)
     A N38 23                               DSPATR(HI UL)
     A                                  5  3'Actions,'
     A                                  6  3'E=Enquire'
     A                                  6 16'A=Amend'
     A                                  6 27'D=Delete'
     A  23                              6 39'X=Authorise'
      *
     A                                  8  3'Sel'
     A                                  8  8'Customer'
     A                                  8 18'Facility'
     A                                  8 28'Fee'
     A  22                              8 33'Value date'
     A  21                              8 51'Adjustment'
     A  22                              8 51'Settlement'
     A                                  8 64'Ccy'
     A                                  8 71'Status'
      *
     A                                  9  9'Number'
     A                                  9 28'Seq'
     A                                  9 55'Amount'
      *
      * Message subfile record
      *
     A          R LE2220S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * Message subfile control
      *
     A          R LE2220C1                  TEXT('Program messages')
     A                                      SFLCTL(LE2220S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  75
     AON75                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*/COPY WNCPYSRC,LEH00628
