     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Tranche Facilities by Credit Agreement')      *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module                              *
     A*                                                               *
     A*  DSPF/LE3145DF - Tranche Facilities by Credit Agreement       *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 29Nov05               *
      * Midas Release 4.01.03 ----------------------------------------*
      *                 201127             Date 28Jan02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 147221 *Create     Date 16Nov98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR787620 - Hooks Incorporation                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  201127 - Enquiry should show 'Start Date' instead of 'Date   *
      *           Approved'.                                          *
     A*  147221 - Credit Agreement extra processing                   *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A*=========================================================================
     A          R LE3145F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'LE3145F1'
     A                                  1 25'TRANCHE FACILITIES BY +
     A                                       CREDIT AGREEMENT'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A  29                              3 18'Branch -'
     A  29        FBRCA          3   O  3 27DSPATR(HI)
     A  29        FBRNM         20A  O  3 31DSPATR(HI)
     A  29                              5  6'Customer'
     A  29        FCRNM         20A  O  5 16DSPATR(HI)
     A  29        FCSSN         10A  O  5 38DSPATR(HI)
     A  29                              5 51'Facility'
     A  29        FFACT          6A  O  5 62DSPATR(HI)
     A*/COPY WNCPYSRC,LEH00179
      * Begin /CSD027/
     A**29******  FCNUM          6  0O  6 16DSPATR(HI)                                        CSD027
     A  29        FCNUM          6A  O  6 16DSPATR(HI)
      * End /CSD027/
     A  29                              6 38'Rev.Credit'
     A  29        FRVCR          1A  O  6 49DSPATR(HI)
     A**Start of change 201127
     A**29******                        6 51'Date Approved'                                   201127
     A  29                              6 51'Start Date'
     A**End of change 201127
     A  29        FDTAP          7A  O  6 65DSPATR(HI)
     A  29                              7  6'Type'
     A  29        FFCTI          9A  O  7 16DSPATR(HI)
     A  29                              7 51'Expiry Date'
     A  29        FDTEX          7A  O  7 65DSPATR(HI)
     A  29 26                          10  5'Customer'
     A  29 26                          10 23'Branch'
     A  29 26                          10 30'Facility'
     A  29 26                          10 44'Facility Amount'
     A  29 26                          10 68'Currency'
      *
     A*=========================================================================
     A          R LE3145S0                  SFL
     A  55                                  SFLNXTCHG
     A            SOPTN          1A  B 11  2DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A N51                                  DSPATR(UL)
      * Begin /CSD027/
     A**********  SCUST          6  0O 11  5DSPATR(HI)                                        CSD027
     A            SCUST          6A  O 11  5DSPATR(HI)
      * End /CSD027/
     A            SCSSN         10A  O 11 12DSPATR(HI)
     A            SBRCA          3A  O 11 24DSPATR(HI)
     A            SFACT          6A  O 11 31DSPATR(HI)
     A            SFAMT         23A  O 11 40DSPATR(HI)
     A            SFCCY          3A  O 11 70DSPATR(HI)
      *
     A*=========================================================================
     A          R LE3145C0                  SFLCTL(LE3145S0)
     A                                      CF12
     A                                      OVERLAY
     A  26                                  SFLDSP
     A  27                                  SFLDSPCTL
     A N26N27                               SFLCLR
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                 22  2'F3=Exit'
     A                                 22 10'F12=Previous'
     A                                 22 23'1-Tranche Facility Details'
     A                                 22 51'2-Loans by Tranche Facility'
     A                                 23  2'Customer Number or Shortname'
     A            CCNUM         10A  B 23 33DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL)
     A                                 23 47'Branch'
     A            CBRCA          3A  B 23 55DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL)
     A                                 23 62'Facility'
     A            CFACT          3A  B 23 72DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(UL)
     A            CFCNO          2A  B 23 76DSPATR(HI)
     A  43                                  DSPATR(RI)
     A N43                                  DSPATR(UL)
      *
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGM                       SFLPGMQ
      *
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            PGM                       SFLPGMQ
