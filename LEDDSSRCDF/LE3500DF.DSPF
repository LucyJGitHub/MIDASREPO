      *================================================================
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Facility Currency Conversion Disp. File')
      *================================================================
      *                                                               *
      *  Midas     -  Lending Module                                  *
      *                                                               *
      *  LE3500DF  -  Facility Currency Conversion Maintenance.       *
      *                                                               *
      *  Function:  This display enables the maintenance of criteria  *
      *             for selection of facilities which are converted   *
      *             into Euro during the next COB.                    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. 190014             Date 16May01               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 153979             Date 11Oct99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 148063             Date 19Nov98               *
      *                 143509             Date 27Sep98               *
      *                 CEU021  *CREATE    Date 30Apr98               *
      *                                    Date                       *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  190014 - Allow 'ALL' to be entered in Branch and customer    *
      *           fields.                                             *
      *  153979 - Only display 'Generate Account Key' and 'Start Date *
      *           of Generate A/C Key' if CLE007 is switched ON.      *
      *  148063 - Not permit to input 'ALL' for branch or customer      *
      *  143509 - F5/Refresh not working properly                     *
      *  CEU021 - EMU LE Facility Currency Conversion                 *
      *                                                               *
      *================================================================
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      INDARA
     A*---------------------------------------------------------------------
     A          R LE3500S1                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A  11                                  SFLNXTCHG
     A            S1OPTN         2A  B  9  2TEXT('OPTION')
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A N70                                  DSPATR(HI)
     A            S1REFR        10A  O  9  5TEXT('Reference')
     A            S1BRCA         3A  O  9 16TEXT('Branch')
     A            S1CNUM         6A  O  9 23TEXT('Customer')
     A            S1CCY          3A  O  9 32TEXT('Currency')
     A            S1DESC        30A  O  9 41TEXT('Description')
     A*---------------------------------------------------------------------
     A          R LE3500C1                  SFLCTL(LE3500S1)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      CA03
     A                                      CA05
     A                                      OVERLAY
     A N82                                  SFLDSP
     A  81                                  SFLDSPCTL
     A  82                                  SFLCLR
     A N83                                  SFLEND(*MORE)
     A N82 83                               ROLLUP(01)
     A  81                                  ROLLDOWN(02)
     A            SCRRN1         4S 0H      TEXT('SFLRCDNBR field')
     A                                      SFLRCDNBR(CURSOR)
     A            SCUSER        10A  O  1  2
     A                                  1 13'LE3500S1'
     A                                  1 22'Work With Facility Ccy Conversion -
     A                                      Criteria'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SCDATE         7A  O  1 73
      *
     A            SCWSID        10A  O  2  2
     A                                  2 72TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A                                  4  1'Select option and press Enter'
     A                                      COLOR(BLU)
     A                                  5  2'A=Amend'
     A                                      COLOR(BLU)
     A                                  5 10'D=Delete'
     A                                      COLOR(BLU)
     A                                  5 19'E=Enquire'
     A                                      COLOR(BLU)
     A                                  5 29'I=Insert'
     A                                      COLOR(BLU)
     A                                  5 38'DF=Display Facilities'
     A                                      COLOR(BLU)
     A                                  7  5'Reference'
     A                                      DSPATR(HI)
     A                                  7 16'Branch'
     A                                      DSPATR(HI)
     A                                  7 23'Customer'
     A                                      DSPATR(HI)
     A                                  7 32'Currency'
     A                                      DSPATR(HI)
     A                                  7 41'Description'
     A                                      DSPATR(HI)
     A            SCOPTN         2A  B  8  2TEXT('OPTION')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A N71                                  DSPATR(HI)
     A            SCPOS1        10A  B  8  5TEXT('Reference')
     A                                      CHECK(LC)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A N54                                  DSPATR(HI)
     A*---------------------------------------------------------------------
     A          R LE3500D1
     A                                      CA03
     A                                      CA05
     A  75                                  CA10
     A                                      CA12
     A                                      OVERLAY
     A            SCUSER        10A  O  1  2
     A  76 77                               DSPATR(PC)
     A                                  1 13'LE3500D1'
     A                                  1 22'Facility Ccy Conversion Maintenanc-
     A                                      e'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SCDATE         7A  O  1 73
     A            SCWSID        10A  O  2  2
     A                                  2 72TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A  75                              4  2'Press F10 to Confirm Deletion'
     A                                      COLOR(BLU)
     A  77                              4  2'Press ENTER to Continue'
     A                                      COLOR(BLU)
     A N75N77N69                        4  2'Change details,'
     A                                      COLOR(BLU)
     A N75N77N69                        4 18'Press ENTER to Continue'
     A                                      COLOR(BLU)
     A                                  6  1'Reference  . . . . . . . . . .:'
     A            S2REFR        10   B  6 37TEXT('Reference')
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A N72                                  DSPATR(HI)
     A  76                                  DSPATR(PR)
     A N76                                  DSPATR(UL)
     A            S2DESC        30A  B  6 48TEXT('Description')
     A                                      CHECK(LC)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A N66                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
      *                                     Start of change /148063/
     A**********                        7  1'Branch Code (ALL). . . . . . .:'
      **Start of change 190014
     A**********                        7  1'Branch Code  . . . . . . . . .:'                 190014
     A                                  7  1'Branch Code (ALL). . . . . . .:'
      **End of change 190014
      *                                       End of change /148063/
     A            S2BRCA         3A  B  7 37TEXT('Branch Code')
     A  73
     AO 40                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A N73                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A            S2BRNM        30A  O  7 48TEXT('Branch Name')
      *                                     Start of change /148063/
     A**********                        8  1'Customer Number (ALL). . . . .:'
      **Start of change 190014
     A**********                        8  1'Customer Number  . . . . . . .:'                 190014
     A                                  8  1'Customer Number (ALL). . . . .:'
      **End of change 190014
      *                                       End of change /148063/
     A            S2CNUM        10A  B  8 37TEXT('Customer Number')
     A  74
     AO 41                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A N74                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A            S2CNAM        30A  O  8 48TEXT('Customer Name')
     A                                  9  1'Currency . . . . . . . . . . .:'
     A            S2CCY          3A  B  9 37TEXT('Currency')
     A  53
     AO 43                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A N53                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A            S2CYNM        14A  O  9 48TEXT('Currency Description')
     A                                 10  1'Facility Type  . . . . . . . .:'
     A            S2FACT         3A  B 10 37TEXT('Facility Type')
     A  78
     AO 44                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
     A N78                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
     A            S2FCNM        30A  O 10 48TEXT('Facility Type Description')
     A                                 11  1'Facility Number  . . . . . . .:'
     A            S2FCNO         2A  B 11 37TEXT('Facility Number')
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A N79                                  DSPATR(HI)
     A  77                                  DSPATR(PR)
     A N77                                  DSPATR(UL)
      *                                              Begin /153979/
     A***********                      12  1'Generate Account Key . . . . .:'
     A*********** S2GEAK         1A  B 12 37TEXT('Generate Account Key')
     A**57*******                           DSPATR(PC)
     A**57*******                           DSPATR(RI)
     A*N57*******                           DSPATR(HI)
     A**77*******                           DSPATR(PR)
     A*N77*******                           DSPATR(UL)
     A*********** S2GEAT         3A  O 12 48TEXT('Generate A/C Key Description')
     A***********                      13  1'Start Date of Generate A/C Key:'
     A*********** S2GADT         6A  B 13 37TEXT('Generate A/C Key Start Date')
     A**59*******
     A*O*46*******                           DSPATR(PC)
     A**59*******                           DSPATR(RI)
     A*N59*******                           DSPATR(HI)
     A**77*******                           DSPATR(PR)
     A*N77*******                           DSPATR(UL)
     A  84                             12  1'Generate Account Key . . . . .:'
     A  84        S2GEAK         1A  B 12 37TEXT('Generate Account Key')
     A  84 57                               DSPATR(PC)
     A  84 57                               DSPATR(RI)
     A  84N57                               DSPATR(HI)
     A  84 77                               DSPATR(PR)
     A  84N77                               DSPATR(UL)
     A  84        S2GEAT         3A  O 12 48TEXT('Generate A/C Key Description')
     A  84                             13  1'Start Date of Generate A/C Key:'
     A  84        S2GADT         6A  B 13 37TEXT('Generate A/C Key Start Date')
     A  84 59
     AO 84 46                               DSPATR(PC)
     A  84 59                               DSPATR(RI)
     A  84N59                               DSPATR(HI)
     A  84 77                               DSPATR(PR)
     A  84N77                               DSPATR(UL)
      *                                              End   /153979/
     A            S2GADN         6  0H
     A            S2CUST         6   H
      *---------------------------------------------------------------------
     A          R LE3500F1
     A                                      OVERLAY
      *                                              Begin /143509/
     A***********                      22  2'F03=Exit'
     A                                 22  2'F3=Exit'
      *                                              End   /143509/
     A                                      COLOR(BLU)
      *                                              Begin /143509/
     A***********                      22 22'F05=Refresh'
     A                                 22 22'F5=Refresh'
      *                                              End   /143509/
     A                                      COLOR(BLU)
     A          R LE3500F2
     A                                      OVERLAY
      *                                              Begin /143509/
     A***********                      22  2'F03=Exit'
     A                                 22  2'F3=Exit'
      *                                              End   /143509/
     A                                      COLOR(BLU)
      *                                              Begin /143509/
     A***********                      22 22'F05=Refresh'
     A                                 22 22'F5=Refresh'
      *                                              End   /143509/
     A                                      COLOR(BLU)
     A                                 22 42'F12=Previous'
     A                                      COLOR(BLU)
     A  75                             22 62'F10=Delete'
     A                                      COLOR(BLU)
      *---------------------------------------------------------------------
     A          R MSGSFL                    TEXT('Message Subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SCMGKY                    SFLMSGKEY
     A            D@PGMN                    SFLPGMQ
      *---------------------------------------------------------------------
     A          R MSGCTL                    TEXT('Control Record')
     A                                      SFLCTL(MSGSFL)
     A                                      SFLSIZ(20)
     A                                      SFLPAG(1)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            D@PGMN                    SFLPGMQ
