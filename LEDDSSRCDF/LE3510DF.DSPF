      *================================================================
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Facilities selected for conversion')
      *================================================================
      *                                                               *
      *  Midas     -  Lending Module                                  *
      *                                                               *
      *  LE3510DF  -  Facilities Selected for Conversion              *
      *                                                               *
      *  Function:  This display is called from the selection         *
      *             criteria maintenance function and displays all    *
      *             facilities affected by a selection criterion.     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 143509             Date 27Sep98               *
      *  Prev Amend No. CEU021  *CREATE    Date 07May98               *
      *                                    Date                       *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  143509 - F5/Refresh not working properly                     *
      *  CEU021 - EMU LE Facility Currency Conversion                 *
      *                                                               *
      *================================================================
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      INDARA
     A*---------------------------------------------------------------------
     A          R LE3510S1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A  11                                  SFLNXTCHG
     A            S1OPTN         2A  B 10  2TEXT('OPTION')
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A N70                                  DSPATR(HI)
     A            S1CNUM         6A  O 10  5TEXT('Customer Number')
     A            S1FACT         3A  O 10 14TEXT('Facility Type')
     A            S1FCNO         2A  O 10 28TEXT('Facility Sequence')
     A            S1BRCA         3A  O 10 44TEXT('Branch')
     A            S1OCCY         3A  O 10 51TEXT('Currency')
     A            S1STAT         9A  O 10 60TEXT('Status')
     A*---------------------------------------------------------------------
     A          R LE3510C1                  SFLCTL(LE3510S1)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A N82                                  SFLDSP
     A  81                                  SFLDSPCTL
     A  82                                  SFLCLR
     A N83                                  SFLEND(*MORE)
     A N82 83                               ROLLUP(01)
     A  81                                  ROLLDOWN(02)
     A            SCRRN1         4S 0H      TEXT('SFLRCDNBR field')
     A                                      SFLRCDNBR(CURSOR)
     A            SCUSER        10A  O  1  2
     A                                  1 13'LE3510S1'
     A                                  1 21'Work With Selected Facilities'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SCDATE         7A  O  1 73
      *
     A            SCWSID        10A  O  2  2
     A                                  2 72TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A                                  4  1'Select option and press Enter'
     A                                      COLOR(BLU)
     A                                  5  1'E=Enquire'
     A                                      COLOR(BLU)
     A                                  6  1'Reference:'
     A                                      DSPATR(HI)
     A            SCREFR        10A  O  6 12TEXT('Reference')
     A                                      DSPATR(HI)
     A            SCDESC        30A  O  6 23TEXT('Reference Desc')
     A                                      DSPATR(HI)
     A                                  8  5'Customer'
     A                                      DSPATR(HI)
     A                                  8 14'Facility Type'
     A                                      DSPATR(HI)
     A                                  8 28'Facility Number'
     A                                      DSPATR(HI)
     A                                  8 44'Branch'
     A                                      DSPATR(HI)
     A                                  8 51'Currency'
     A                                      DSPATR(HI)
     A                                  8 60'Status'
     A                                      DSPATR(HI)
     A            SCOPTN         2A  B  9  2TEXT('OPTION')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A N71                                  DSPATR(HI)
     A            SCCNUM         6A  B  9  5TEXT('Customer Number')
     A                                      CHECK(LC)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A N54                                  DSPATR(HI)
     A            SCFACT         3A  B  9 14TEXT('Facility Type')
     A                                      CHECK(LC)
     A  54                                  DSPATR(RI)
     A N54                                  DSPATR(HI)
     A            SCFCNO         2A  B  9 28TEXT('Facility Number')
     A                                      CHECK(LC)
     A  54                                  DSPATR(RI)
     A N54                                  DSPATR(HI)
     A            SCOCCY         3A  B  9 51TEXT('Currency')
     A  54                                  DSPATR(RI)
     A N54                                  DSPATR(HI)
     A*---------------------------------------------------------------------
     A          R LE3510D1
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A            SCUSER        10A  O  1  2
     A  76 77                               DSPATR(PC)
     A                                  1 13'LE3510D1'
     A                                  1 21'Selected Facilities Enquiry'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SCDATE         7A  O  1 73
     A            SCWSID        10A  O  2  2
     A                                  2 72TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A            S2DUMY         1A  B  3  1
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A                                  4  1'Press ENTER to Continue'
     A                                      COLOR(BLU)
     A                                  6  1'Reference  . . . . . . . . . .:'
     A            S2REFR        10A  O  6 37TEXT('Reference')
     A            S2DESC        30A  O  6 48TEXT('Description')
     A                                  7  1'Status . . . . . . . . . . . .:'
     A            S2STAT         9A  O  7 37TEXT('Status')
     A                                  8  1'Branch Code  . . . . . . . . .:'
     A            S2BRCA         3A  O  8 37TEXT('Branch Code')
     A            S2BRNM        30A  O  8 48TEXT('Branch Name')
     A                                  9  1'Customer Number  . . . . . . .:'
     A            S2CNUM         6A  O  9 37TEXT('Customer Number')
     A            S2CNAM        30A  O  9 48TEXT('Customer Name')
     A                                 10  1'Facility Type  . . . . . . . .:'
     A            S2FACT         3A  O 10 37TEXT('Facility Type')
     A            S2FCNM        30A  O 10 48TEXT('Facility Type Description')
     A                                 11  1'Facility Number  . . . . . . .:'
     A            S2FCNO         2A  O 11 37TEXT('Facility Number')
     A                                 12  1'Original Currency  . . . . . .:'
     A            S2OCCY         3A  O 12 37TEXT('Currency')
     A            S2CYNM        14A  O 12 48TEXT('Currency Description')
     A                                 13  1'Original Amount  . . . . . . .:'
     A            S2OAMT        20A  O 13 37TEXT('Original Amount')
     A                                 14  1'Euro Amount  . . . . . . . . .:'
     A            S2EAMT        20A  O 14 37TEXT('Original Amount')
     A                                 15  1'Generate Account Key . . . . .:'
     A            S2GEAK         1A  O 15 37TEXT('Generate Account Key')
     A            S2GEAT         3A  O 15 48TEXT('Generate A/C Key Description')
     A                                 16  1'Start Date of Generate A/C Key:'
     A            S2GADT         6A  O 16 37TEXT('Generate A/C Key Start Date')
      *---------------------------------------------------------------------
     A          R LE3510F1
     A  97                                  OVERLAY
      *                                              Begin /143509/
     A***********                      22  2'F03=Exit'
     A                                 22  2'F3=Exit'
      *                                              End   /143509/
     A                                      COLOR(BLU)
      *                                              Begin /143509/
     A***********                      22 22'F05=Refresh'
     A                                 22 22'F5=Refresh'
      *                                              Begin /143509/
     A                                      COLOR(BLU)
     A                                 22 42'F12=Previous'
     A                                      COLOR(BLU)
      *---------------------------------------------------------------------
     A          R MSGSFL                    TEXT('Message Subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SCMGKY                    SFLMSGKEY
     A            D@PGMN                    SFLPGMQ
      *---------------------------------------------------------------------
     A          R MSGCTL                    TEXT('Control Record')
     A                                      SFLCTL(MSGSFL)
     A                                      SFLSIZ(20)
     A                                      SFLPAG(1)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            D@PGMN                    SFLPGMQ
      *===================================================================
