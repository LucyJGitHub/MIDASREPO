      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Assignment Display')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LEASGNDDF - Assignment Display                          *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CAP070  *CREATE    Date 27Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP070 - Conversion of LE inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A*=========================================================================
     A          R LEASGNF2                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      OVERLAY
     A                                      KEEP
     A            S#USER    R        O  1  2REFFLD(USER)
     A            S#WSID    R        O  2  2REFFLD(JOJOB)
     A                                  1 14'LEASGND'
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A            S#RUNA    R        O  1 72REFFLD(RUNA)
     A                                  2 29'ASSIGNMENT DETAILS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*=========================================================================
     A          R LEASGNS1                  SFL
     A  35                                  SFLNXTCHG
     A            S#SEL          1A  B 12  3
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A            S#HIST         1A  O 12 08DSPATR(HI)
     A            S#ASTN         6A  O 12 13DSPATR(HI)
     A            S#ASTX        10A  O 12 21DSPATR(HI)
     A            S#ADTE         6A  O 12 39DSPATR(HI)
     A            S#SHTP         1A  O 12 52DSPATR(HI)
     A            S#SHAR        16A  O 12 60DSPATR(HI)
     A            H#ASFS        10A  H
     A            H#VDTC         5S 0H
     A*=========================================================================
     A          R LEASGNC1                  SFLCTL(LEASGNS1)
     A                                      SFLSIZ(0045)
     A                                      SFLPAG(0009)
     A N66                                  CF09(09 'Add')
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A  30                                  SFLDSP
     A  31                                  SFLDSPCTL
     A N31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A            ##SFRC         4S 0H      SFLRCDNBR(CURSOR)
     A                                 10  2'Sel'
     A                                  4  6'Facility'
     A            S#PMFC         6A  O  4 16DSPATR(HI)
     A                                  4 23'-'
     A            S#PMFT         3A  O  4 25DSPATR(HI)
     A                                  4 29'-'
     A            S#PMFN         2A  O  4 31DSPATR(HI)
     A                                  5 61'Currency'
     A            S#FCCY         3A  O  5 71DSPATR(HI)
     A                                  4 63'Branch'
     A            S#BRCA         3A  O  4 71DSPATR(HI)
     A                                  7  2'Actions'
     A                                  8  3'E=Enquire'
     A                                 10 07'HIST'
     A                                 10 13'---Assigned  To---'
     A                                 11 13'Number  Shortname'
     A                                 10 37'Assignment'
     A                                 11 40'Date'
     A                                 11 50'Type'
     A                                 10 50'----Assignment    Share----'
     A                                 11 60'Amount/Percentage'
     A                                  8 17'A=Amend     R=Reverse'
     A  66                                  DSPATR(ND)
     A                                  4 37'Amount:'
     A            S#FAMT        14A  O  4 46DSPATR(HI)
     A*=========================================================================
     A          R LEASGNF1
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 14'F5=Refresh'
     A                                 23 29'F10=Reverse' DSPATR(HI)
     A N68                                  DSPATR(ND)
     A                                 23 44'F12=Previous'
     A                                 23 61'F14=Settlements'
     A N69                                  DSPATR(ND)
     A*=========================================================================
     A          R LEASGNF3
     A                                      CF12(12 'Previous')
     A  68                                  CF10(10 'Reverse')
     A  69                                  CF14(14 'Settlement')
     A                                      OVERLAY
     A                                      PROTECT
     A                                  4  6'Facility'
     A            S#PMFC         6A  O  4 16
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A                                  4 23'-'
     A            S#PMFT         3A  O  4 25
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI)
     A                                  4 29'-'
     A            S#PMFN         2A  O  4 31
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A                                  5 61'Currency'
     A            S#FCCY         3A  O  5 71DSPATR(HI)
     A                                  4 63'Branch'
     A            S#BRCA         3A  O  4 71DSPATR(HI)
     A                                  4 37'Amount:'
     A            S#FAMT        14A  O  4 46DSPATR(HI)
     A                                  8 23'Assigned To ........ :'
     A                                 11 23'Assigned From ...... :'
     A                                 14 23'Assignment Date .... :'
     A                                 17  2'Assigned Share Type: Amount/Percen-
     A                                      tage... :'
     A                                 18 23'Share .............. :'
     A            S#ASTS        10A  B  8 47
     A  67
     AON09                                  DSPATR(PR)
     A  44                                  DSPATR(RI PC)
     A N67 09                               DSPATR(UL)
     A N44                                  DSPATR(HI)
     A            S#ATNM        20A  O  8 59DSPATR(HI)
     A            S#ATTN        10A  O  9 47DSPATR(HI)
     A            S#ASFS        10A  B 11 47
     A  67
     AON09                                  DSPATR(PR)
     A  45                                  DSPATR(RI PC)
     A N67 09                               DSPATR(UL)
     A N45                                  DSPATR(HI)
     A            S#AFNM        20A  O 11 59DSPATR(HI)
     A            S#AFTN        10A  O 12 47DSPATR(HI)
     A            S#ADTE         6A  B 14 47
     A  67                                  DSPATR(PR)
     A  46                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N46                                  DSPATR(HI)
     A            S#SHTP         1A  B 17 47
     A  67                                  DSPATR(PR)
     A  47                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N47                                  DSPATR(HI)
     A            S#SHAR        16A  B 18 47
     A  67                                  DSPATR(PR)
     A  48                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N48                                  DSPATR(HI)
     A*=========================================================================
     A          R LEASGNF4
     A                                      OVERLAY
     A                                 13 34'NO RECORDS FOUND'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A*=========================================================================
     A          R LEASGNF5
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 14'F5=Refresh'
     A                                 23 29'F9=Add'
     A  66                                  DSPATR(ND)
     A                                 23 36'F12=Previous'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A  25                                  SFLDSP
     A  25                                  SFLDSPCTL
     A  25                                  SFLINZ
     A  25                                  SFLCLR
     A  25                                  SFLEND
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
