      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Customer Loans - Details screen 1')           *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LECLIP1DDF - Customer Loans - Screen 1 Display file          *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CLE042             Date 06Sep05               *
      *                 CAP074  *CREATE    Date 25Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus                                          *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CAP074 - Lending API enhancements - Loans input.             *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03
     A                                      CF12
     A  72                                  CF14
     A  73                                  CF15
     A  74                                  CF07
     A  74                                  CF08
     A  75                                  CF09
     A  76                                  CF10
      *
      ***  Details 1
      *
     A          R LECLIPD1
     A                                      OVERLAY
     A                                      ROLLUP(79)
     A                                      PUTOVR
      *
     A            DDUSER        10A  O  1  2
     A                                  1 13'LECLIP1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A N82N83                           2 31'Customer Loan Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  82                              2 27'Participation Purchased Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  83                              2 30'Participation Sold Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
     A            DDNARR        15A  O  4  2
     A  85                                  DSPATR(HI)
     A  85                                  DSPATR(RI)
     A N85                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                  4 43'Action'
     A            DDACTN         1   B  4 53
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  68
     AO 10N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                  4 57'Status'
     A            DDLSTS        15   O  4 65DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  5  2'Loan reference'
     A                                      DSPATR(UL)
      *
     A                                  6  2'Loan no'
     A            DDLNRF         6   B  6 22
     A  11                                  DSPATR(RI)
     A N11                                  DSPATR(HI)
     A  11N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A N82                              6 43'Customer'
     A  82                              6 43'Orig borrower'
     A            DDCUST        10   B  6 58
     A  70                                  DSPATR(PR)
     A N70 82
     AON70                                  DSPATR(UL)
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                  7  2'Facility reference'
     A  83                                  DSPATR(ND)
     A                                      OVRATR
     A            DDFCUS         6   B  7 22
     A  83
     AO 70                                  DSPATR(PR)
     A N70N83                               DSPATR(UL)
     A  83                                  DSPATR(ND)
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            DDFTYP         3   B  7 29
     A  83
     AO 70                                  DSPATR(PR)
     A N70N83                               DSPATR(UL)
     A  83                                  DSPATR(ND)
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            DDFSEQ         2   B  7 33
     A  83
     AO 70                                  DSPATR(PR)
     A N70N83                               DSPATR(UL)
     A  83                                  DSPATR(ND)
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                  7 43'Branch'
     A N81                                  DSPATR(ND)
     A                                      OVRATR
     A            DDBRCA         3   B  7 58
     A N81
     AO 70                                  DSPATR(PR)
     A N70 81                               DSPATR(UL)
     A N81                                  DSPATR(ND)
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                  8  2'Loan type/Sub type'
     A            DDLTYP         2   B  8 22
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDSUTP         2   B  8 25
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *                                              Start /CLE042/
     A            DDCLAS         4   B  8 28
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *                                              End   /CLE042/
      *
     A  82                              8 43'Purchased from'
     A  83                              8 43'Sale of loan'
     A  83        DDOLNO         6   B  8 58
     A N83
     AO 70                                  DSPATR(PR)
     A  83N70                               DSPATR(UL)
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A  82        DDPFRM        10   B  8 58
     A N82
     AO 70                                  DSPATR(PR)
     A  82N70                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A  82                              9 43'Mirror loan no'
     A N84                                  DSPATR(ND)
     A                                      OVRATR
     A  82        DDMLNR         6   B  9 58
     A N82
     AO 70
     AON84                                  DSPATR(PR)
     A  82N70                               DSPATR(UL)
     A N82
     AON84                                  DSPATR(ND)
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                 10  2'Loan Details'
     A                                      DSPATR(UL)
      *
     A                                 11  2'Currency'
     A            DDCURR         3   B 11 22
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                 11 43'Amount'
     A            DDCPAM        14   B 11 53
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                 12  2'Deal date'
     A            DDDDAT         6   B 12 22
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDAUTO         1   B 12 43
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                 12 45'Auto settlements'
      *
      **Start of change CER020
     A**********                       13 02'Start date'                                      CER020
     A N60                             13 02'Start date'
     A  60                             13 02'Start/Int Str Date'
      **End of change CER020
     A            DDVDAT         6   B 13 22
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
      **Start of change CER020
     A  60        DDISDT         6   B 13 30
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      **End of change CER020
     A            DDRCSI         1   B 13 43
     A N82N83
     AO 70                                  DSPATR(PR)
     A N70 82
     AON70 83                               DSPATR(UL)
     A N82N83                               DSPATR(ND)
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 13 45'Recourse indicator'
     A N82N83                               DSPATR(ND)
     A                                      OVRATR
      *
      **Start of change CER020
     A**********                       14  2'Maturity date'                                   CER020
     A N60                             14  2'Maturity date'
     A  60                             14  2'Mat/Final Mat Date'
      **End of change CER020
     A            DDMDAT         6   B 14 22
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
      **Start of change CER020
     A  60        DDFMDT         6   B 14 30
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      **End of change CER020
     A            DDAPMI         1   B 14 43
     A N80
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N80                                  DSPATR(ND)
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 14 45'Auto payment at maturity'
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A            DDMCRA         1   B 15 43
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 15 45'Multi ccy rollovers +
     A                                       allowed'
      *
     A                                 16  2'Rollover Details'
     A                                      DSPATR(UL)
     A                                 17  2'Rollover date'
     A            DDRLDT         6   B 17 22
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 17 35'Frequency/Day number'
     A            DDRLFQ         1   B 17 57
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDRLDY         2   B 17 59
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                 18 35'Rate Fixing Days'
     A N86                                  DSPATR(ND)
     A                                      OVRATR
     A            DDRFDY         2A  B 18 57
     A  86N34                               DSPATR(HI)
     A  86N34N70                            DSPATR(UL)
     A N86
     AO 86 70                               DSPATR(PR)
     A  86 34                               DSPATR(RI PC)
     A N86                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                 19  2'Syndicated Loans'
     A                                      DSPATR(UL)
     A N84                                  DSPATR(ND)
     A                                      OVRATR
     A                                 20  2'Default participation +
     A                                       loan type/sub type'
     A N84
     AO 83                                  DSPATR(ND)
     A                                      OVRATR
     A            DDDFTP         2   B 20 43
     A N84
     AO 83
     AO 70                                  DSPATR(PR)
     A N70N83                               DSPATR(UL)
     A N84
     AO 83                                  DSPATR(ND)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDDFST         2   B 20 46
     A N84
     AO 83
     AO 70                                  DSPATR(PR)
     A N70N83                               DSPATR(UL)
     A N84
     AO 83                                  DSPATR(ND)
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *                                              Start /CLE042/
     A            DDDFCL         4   B 20 49
     A N84
     AO 83
     AO 70                                  DSPATR(PR)
     A N70N83                               DSPATR(UL)
     A N84
     AO 83                                  DSPATR(ND)
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *                                              End   /CLE042/
      *
      *                                              Start /CLE042/
     A*********************************20 52'Linked to syndicate'
     A                                 20 54'Linked to syndicate'
      *                                              End   /CLE042/
     A N82N83
     AON84                                  DSPATR(ND)
     A                                      OVRATR
      *                                              Start /CLE042/
     A************DDLNKS*********1***B*20 73
     A            DDLNKS         1   B 20 74
      *                                              End   /CLE042/
     A  87
     AON82N83
     AO 70
     AON84                                  DSPATR(PR)
     A N70 82
     AON70 83                               DSPATR(UL)
     A N82N83
     AON84                                  DSPATR(ND)
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A  83 84                          21  2'Participant facility'
     A  83 84     DDPTFC         6   O 21 23DSPATR(HI)
     A  83 84     DDPTFN         2   O 21 34DSPATR(HI)
     A  83 84     DDPTFT         3   O 21 30DSPATR(HI)
      *
     A                                 21 52'Assignment'
     A N82N83
     AON84                                  DSPATR(ND)
     A                                      OVRATR
     A            DDASSN         1   B 21 64
     A  87
     AON82
     AO 70
     AON84                                  DSPATR(PR)
     A N70 82                               DSPATR(UL)
     A N82N83
     AON84                                  DSPATR(ND)
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
      ***  Screen footer
      *
     A          R LECLIPF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 10'F12=Prev'
     A  72                             23 19'F14=Settl Inst'
     A  73                             23 34'F15=Select'
     A  74                             23 45'F7=Prev F8=Next'
     A  75                             23 61'F9=Amend'
     A  76                             23 19'F10=Delete'
     A                                 23 72'Page keys'
     A            DDFOTX        40A  O 24  2
     A                                      DSPATR(HI)
      *
      ***  Message subfile record
      *
     A          R LECLIPS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ***  Message subfile control
      *
     A          R LECLIPC1                  TEXT('Program messages')
     A                                      SFLCTL(LECLIPS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
