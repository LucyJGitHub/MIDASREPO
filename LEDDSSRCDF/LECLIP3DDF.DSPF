      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Customer Loans - Details screen 3')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending  API Module                         *
      *                                                               *
      *  LECLIP3DDF - Customer Loans - Screen 3 Display file          *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 225959             Date 23Mar04               *
      *                 224261             Date 09Jan04               *
      *                 CAP074  *CREATE    Date 25Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  225959 - Missing CAS00x changes                             *
      *  224261 - Funding spread/rate still being shown in certain    *
      *           conditions                                          *
      *  CAP074 - Lending API enhancements - Loans input.             *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03
     A                                      CF12
     A  72                                  CF14
     A  75                                  CF09
     A  76                                  CF10
      *
      ***  Details 3
      *
     A          R LECLIPD3
     A                                      OVERLAY
     A                                      ROLLUP(79)
     A                                      ROLLDOWN(78)
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2
     A                                  1 13'LECLIP3'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A N82N83                           2 31'Customer Loan Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  82                              2 27'Participation Purchased Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  83                              2 30'Participation Sold Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4 22'Loan no'
     A            DDLNR3         6A  O  4 30DSPATR(HI)
     A                                      OVRDTA
     A                                  4 43'Action'
     A            DDACT3         1A  O  4 53DSPATR(HI)
     A                                      OVRDTA
     A                                  4 57'Status'
     A            DDLST3        15A  O  4 65DSPATR(HI)
     A                                      OVRDTA
     A                                  6  2'Management Information'
     A                                      DSPATR(UL)
     A                                  7  2'A/C officer'
     A            DDACOF         2A  B  7 24
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N10                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  7 29'Industry'
     A  83                                  DSPATR(ND)
     A                                      OVRATR
     A            DDLIND         3A  B  7 46
     A  83
     AO 70                                  DSPATR(PR)
     A N70N83                               DSPATR(UL)
     A  11                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A  83                                  DSPATR(ND)
     A N11                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  7 52'Fees'
     A            DDFEEI         1A  B  7 63
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  12                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A N12                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  8  2'Originating branch'
     A  83
     AON62
     AON81                                  DSPATR(ND)
     A                                      OVRATR
     A            DDORBR         3A  B  8 24
     A  70
     AO 83
     AON62
     AON81                                  DSPATR(PR)
     A  81 62N70
     A N83                                  DSPATR(UL)
     A  83
     AON62
     AON81                                  DSPATR(ND)
     A  13                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A N13                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  8 29'Country of risk'
     A  83                                  DSPATR(ND)
     A                                      OVRATR
     A            DDCRSK         2A  B  8 46
     A  83
     AO 70                                  DSPATR(PR)
     A N70N83                               DSPATR(UL)
     A  14                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A  83                                  DSPATR(ND)
     A N14                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  8 52'Documents'
     A            DDDOCI         1A  B  8 63
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  15                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
     A N15                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  9  2'Book code'
     A            DDBOKC         2A  B  9 24
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  16                                  DSPATR(RI)
     A  16                                  DSPATR(PC)
     A N16                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  9 29'Frb code'
     A            DDFRBC         5A  B  9 46
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  17                                  DSPATR(RI)
     A  17                                  DSPATR(PC)
     A N17                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  9 52'Secured'
     A            DDSECI         1A  B  9 63
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  18                                  DSPATR(RI)
     A  18                                  DSPATR(PC)
     A N18                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 10  2'A/C seq no'
     A            DDACSQ         2A  B 10 24
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  19                                  DSPATR(RI)
     A  19                                  DSPATR(PC)
     A N19                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 10 29'Portfolio'
     A  82
     AO 83
     AON87                                  DSPATR(ND)
     A                                      OVRATR
     A            DDPTFR         4A  B 10 46
     A  89
     AO 70
     AON87                                  DSPATR(PR)
     A  87N70N89                            DSPATR(UL)
     A N87                                  DSPATR(ND)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A N20                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 10 52'Alter N/A'
     A            DDNACD         1A  B 10 63
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A N21                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 11  2'Booking profit centre'
     A  83
     AO 63                                  DSPATR(ND)
     A                                      OVRATR
     A            DDPRFC         4A  B 11 24
     A  83
     AO 63                                  DSPATR(ND)
     A  70
     AO 83
     AO 63
     AO 86                                  DSPATR(PR)
     A N83N86N63
     A N70                                  DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A N22                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      * Start of change 225959
     A                                 11 29'Yield Curve'
     A  93                                  DSPATR(ND)
     A                                      OVRATR
     A            DDYLDC         5   B 11 46
     A  93                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A  70
     AO 93                                  DSPATR(PR)
     A  23                                  DSPATR(RI PC)
     A N93N23                               DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 11 52'Forward Yield Curve'
     A  94                                  DSPATR(ND)
     A                                      OVRATR
     A            DDFYLD         5   B 11 72
     A  94                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A  70
     AO 94                                  DSPATR(PR)
     A  24                                  DSPATR(RI PC)
     A N94N24                               DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      * End of change 225959
     A                                 13  2'Loan Description'
     A                                 12 46'Rates'
     A                                      DSPATR(UL)
     A                                      OVRATR
     A            DDNAR1        35A  B 14  2
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 13 46'Base ccy'
     A            DDBCXR        14A  B 13 56
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A N25                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 13 72'Ind.'
     A            DDBMIN         1A  B 13 77
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
     A N26                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDNAR2        35A  B 15  2
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 14 46'equi. rate'
     A            DDNAR3        35A  B 16  2
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 15 46'Fclty ex.'
     A  83                                  DSPATR(ND)
     A                                      OVRATR
     A            DDFCXR        14A  B 15 56
     A  83
     AO 70                                  DSPATR(PR)
     A N70N83                               DSPATR(UL)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A  83                                  DSPATR(ND)
     A N27                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 15 72'Ind.'
     A  83                                  DSPATR(ND)
     A                                      OVRATR
     A            DDFMIN         1A  B 15 77
     A  83
     AO 70                                  DSPATR(PR)
     A N70N83                               DSPATR(UL)
     A  28                                  DSPATR(RI)
     A  28                                  DSPATR(PC)
     A  83                                  DSPATR(ND)
     A N28                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDNAR4        35A  B 17  2
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 16 46'rate'
     A  83                                  DSPATR(ND)
     A                                      OVRATR
     A                                 19  2'Guarantee receiver'
     A N90                                  DSPATR(ND)
     A                                      OVRATR
     A            DDRECE        10A  B 19 26
     A  70
     AON90                                  DSPATR(PR)
     A  90N70                               DSPATR(UL)
     A N90                                  DSPATR(ND)
     A  29                                  DSPATR(RI)
     A  29                                  DSPATR(PC)
     A N29                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 18 46'Funding'
     A N64                                  DSPATR(ND)
     A  64                                  DSPATR(UL)
     A                                      OVRATR
     A                                 20  2'Customer confirmation'
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A            DDCOCU         6A  B 20 26
     A  70
     AON80                                  DSPATR(PR)
     A  80N70                               DSPATR(UL)
     A N80                                  DSPATR(ND)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A N30                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 19 46'Spread/Rate'
     A N64                                  DSPATR(ND)
     A                                      OVRATR
     A            DDFRSP        12A  B 19 60
      * Start of change 224261
     A**91
     A***O 70
     A***ON64                                  DSPATR(PR)
     A****N70N91                               DSPATR(UL)
     A  91
     AO 70
     AO 71
     AON64                                  DSPATR(PR)
     A N70N91N71                            DSPATR(UL)
      * End of change 224261
     A N64                                  DSPATR(ND)
     A  31 64                               DSPATR(RI)
     A  31 64                               DSPATR(PC)
     A N31 64                               DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 21  2'Previous maturity date'
     A N70
     AON80                                  DSPATR(ND)
     A                                      OVRATR
     A            DDOMDT         6A  O 21 26DSPATR(HI)
     A N70
     AON80                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 20 46'Negative'
     A N64                                  DSPATR(ND)
     A                                      OVRATR
     A            DDFSGN         1A  B 20 60
      * Start of change 224261
     A**91
     A***O 70
     A***ON64                                  DSPATR(PR)
     A****N70N91                               DSPATR(UL)
     A  91
     AO 70
     AO 71
     AON64                                  DSPATR(PR)
     A N70N91N71                            DSPATR(UL)
      * End of change 224261
     A N64                                  DSPATR(ND)
     A  32 64                               DSPATR(RI)
     A  32 64                               DSPATR(PC)
     A N32 64                               DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 21 46'Profit centre'
     A N64                                  DSPATR(ND)
     A                                      OVRATR
     A            DDFPRC         4A  B 21 60
      * Start of change 224261
     A**91
     A***O 70
     A***ON64                                  DSPATR(PR)
     A****N70N91                               DSPATR(UL)
     A  91
     AO 70
     AO 71
     AON64                                  DSPATR(PR)
     A N70N91N71                            DSPATR(UL)
      * End of change 224261
     A N64                                  DSPATR(ND)
     A  33 64                               DSPATR(RI)
     A  33 64                               DSPATR(PC)
     A N33 64                               DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
      ***  Screen footer
      *
     A          R LECLIPF3                  TEXT('SELECT SCREEN FOOTER')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 10'F12=Prev'
     A  76                             23 19'F10=Delete'
     A  72                             23 19'F14=Settl Inst'
     A  75                             23 61'F9=Amend'
     A                                 23 71'Page keys'
      *
      ***  Message subfile record
      *
     A          R LECLIPS3                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ***  Message subfile control
      *
     A          R LECLIPC3                  TEXT('Program messages')
     A                                      SFLCTL(LECLIPS3)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
