      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Customer Loans - Details screen 4')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending  API Module                         *
      *                                                               *
      *  LECLIP4DDF - Customer Loans - Screen 4 Display file          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAP074  *CREATE    Date 25Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP074 - Lending API enhancements - Loans input.             *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03
     A                                      CF12
     A  72                                  CF14
     A  75                                  CF09
     A  76                                  CF10
      *
      ***  Details 4
      *
     A          R LECLIPD4
     A                                      OVERLAY
     A                                      ROLLDOWN(78)
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2
     A                                  1 13'LECLIP4'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A N82N83                           2 31'Customer Loan Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  82                              2 27'Participation Purchased Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  83                              2 30'Participation Sold Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4 22'Loan no'
     A            DDLNR4         6   O  4 30DSPATR(HI)
     A                                      OVRDTA
     A                                  4 43'Action'
     A            DDACT4         1   O  4 53DSPATR(HI)
     A                                      OVRDTA
     A                                  4 57'Status'
     A            DDLST4        15   O  4 65DSPATR(HI)
     A                                      OVRDTA
     A                                  6  2'Special Instructions'
     A            DDSPI1        30   B  8  2
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                      OVRATR
     A            DDSPI2        30   B  9  2
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                      OVRATR
     A            DDSPI3        30   B 10  2
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                      OVRATR
      *
      ***  Screen footer
      *
     A          R LECLIPF4                  TEXT('SELECT SCREEN FOOTER')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 10'F12=Prev'
     A  76                             23 19'F10=Delete'
     A  72                             23 19'F14=Settl Inst'
     A  75                             23 61'F9=Amend'
     A                                 23 71'Page keys'
      *
      ***  Message subfile record
      *
     A          R LECLIPS4                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ***  Message subfile control
      *
     A          R LECLIPC4                  TEXT('Program messages')
     A                                      SFLCTL(LECLIPS4)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
