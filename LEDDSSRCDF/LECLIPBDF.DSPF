      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Customer Loans - Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LECLIPBDF - Customer Loans browse                            *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAP074  *CREATE    Date 25Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP074 - Lending API enhancements - Loans input.             *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN SUB-FILE 1                                         *
      * -------------------------------------------------------------------*
      *
     A          R LECLIPS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDLOAN         6A  O  8  6DSPATR(HI)
     A            DDCNUM         6A  O  8 14DSPATR(HI)
     A            DDCSNM        10A  O  8 21DSPATR(HI)
     A            DDVDTE         7A  O  8 33DSPATR(HI)
     A            DDMDTE         7A  O  8 42DSPATR(HI)
     A            DDAMNT        14A  O  8 51DSPATR(HI)
     A            DDLCCY         3A  O  8 67DSPATR(HI)
     A            DDSTAT         9A  O  8 72
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(RI)
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN CONTROL 1                                          *
      * -------------------------------------------------------------------*
      *
     A          R LECLIPC1                  SFLCTL(LECLIPS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2
     A                                  1 13'LECLIPB1'
     A                                  1 26'            Midas            '
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A N82N83                           2 31'Customer Loan Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  82                              2 27'Participation Purchased Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  83                              2 30'Participation Sold Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  2'Sel'
     A                                  5  6'Loan'
     A N82                              5 14'Cust.'
     A  82                              5 14'Original Borrower'
     A                                  5 33'Start'
     A                                  5 42'Maturity'
     A                                  5 59'Amount'
     A                                  5 67'Ccy'
     A                                  5 72'Status'
     A                                  6 33'Date'
     A                                  6 42'Date'
     A                                  6  6'Number'
     A N82                              6 14'Number'
     A N82                              6 21'Shortname'
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN SUB-FILE 2                                         *
      * -------------------------------------------------------------------*
      *
     A          R LECLIPS2                  TEXT('SELECT SCREEN SUBFILE')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFOTRANID    20A  O  8  6DSPATR(HI)
     A            DDLOAN         6A  O  8 28DSPATR(HI)
     A            DDCNUM         6A  O  8 36DSPATR(HI)
     A            DDVDTE         7A  O  8 44DSPATR(HI)
     A            DDMDTE         7A  O  8 53DSPATR(HI)
     A            DDLCCY         3A  O  8 62DSPATR(HI)
     A            DDAMNT        14A  O  8 67DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN CONTROL 2                                          *
      * -------------------------------------------------------------------*
      *
     A          R LECLIPC2                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLCTL(LECLIPS2)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2
     A                                  1 13'LECLIPB2'
     A                                  1 26'            Midas            '
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A N82N83                           2 31'Customer Loan Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  82                              2 27'Participation Purchased Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  83                              2 30'Participation Sold Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  2'Sel'
     A                                  5  6'Front Office Id.'
     A N82                              5 36'Cust.'
     A  82                              5 36'Orig.'
     A                                  6 28'Number'
     A N82                              6 36'Number'
     A  82                              6 36'Borr.'
     A                                  5 28'Loan'
     A                                  5 44'Start'
     A                                  6 44'Date'
     A                                  5 62'Curr.'
     A                                  6 62'Code'
     A                                  5 75'Amount'
     A                                  5 53'Maturity'
     A                                  6 53'Date'
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R LECLIPF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
