      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE CLIP Display - Extension')
      *****************************************************************
      *                                                               *
      *  Finastra  - Customer Lending Module                          *
      *                                                               *
      *  LECLIPD5DP - Midas LE Clip Display - Extension               *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *
      *  The naming standards for the record formats should be the
      *  file name but changing # to F, i.e.
      *
      *  LECLIPD5#6  ===>  LECLIPD5F6
      *
      *  Field names should always start with # followed by the format
      *  number (i.e. 0) and the file field name cut of module id, i.e.
      *
      *  IBPLEX ===> #0PLEX
      *
      *-------------------------------------------------------------------------
      *
      *  The code in this display file that can be changed is within
      *  the record format(s).
      *-------------------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT
     A                                      INDARA
     A                                      CA12(12 'Previous')
     A                                      CA03(03 'Exit')
     A*-------------------------------------------------------------------------
     A          R LECLIPD5F6
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A                                      OVERLAY
     A                                      PUTOVR
     A            SUSER         10A  O  1  2
     A            SJOB          10A  O  2  2
     A            SPGM          10A  O  1 13
     A                                  1 65'Date:'
     A            BJMRDT         7A  O  1 72
     A                                  2 65'Time:'
     A                                  2 71TIME
     A                                  2 26'European Reporting - Luxembourg'
     A                                      DSPATR(HI)
     A                                  4  2'Lump-Sum code.....................-
     A                                       :'
     A                                      OVRATR
     A            LLUSU          2A  B  4 39OVRDTA
     A                                      OVRATR
     A N59                                  DSPATR(HI)
     A  59                                  DSPATR(RI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A  59N20                               DSPATR(PC)
     A                                  5  2'Degree of risk....................-
     A                                       :'
     A                                      OVRATR
     A  37                                  DSPATR(ND)
     A            LDGRI          1A  B  5 39OVRDTA
     A                                      OVRATR
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(RI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A  60N20                               DSPATR(PC)
     A  37                                  DSPATR(ND)
     A                                  6  2'LMI Circular 96 / 127.............-
     A                                       :'
     A                                      OVRATR
     A            LIM93          1A  B  6 39OVRDTA
     A                                      OVRATR
     A N57                                  DSPATR(HI)
     A  57                                  DSPATR(RI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A  57N20                               DSPATR(PC)
     A                                  7  2'Claims Type.......................-
     A                                       :'
     A                                      OVRATR
     A            LCLAT          1A  B  7 39OVRDTA
     A                                      OVRATR
     A N58                                  DSPATR(HI)
     A  58                                  DSPATR(RI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A  58N20                               DSPATR(PC)
     A                                  8  2'Correction Amount.................-
     A                                      .:'
     A                                      OVRATR
     A            LCOAM         15A  B  8 39OVRDTA
     A                                      OVRATR
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A  54N20                               DSPATR(PC)
     A                                  9  2'Subordinate.......................-
     A                                       :'
     A                                      OVRATR
     A            LSUBR          1A  B  9 39OVRDTA
     A                                      OVRATR
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A  50N20                               DSPATR(PC)
     A                                 10  2'Mobilisable Customer..............-
     A                                       :'
     A                                      OVRATR
     A            LMOBL         10A  B 10 39OVRDTA
     A                                      OVRATR
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A  51N20                               DSPATR(PC)
     A                                 11  2'Risk Code.........................-
     A                                       :'
     A                                      OVRATR
     A            LRISC          2A  B 11 39OVRDTA
     A                                      OVRATR
     A N56                                  DSPATR(HI)
     A  56                                  DSPATR(RI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A  56N20                               DSPATR(PC)
     A                                 12  2'Consumer credit code..............-
     A                                       :'
     A                                      OVRATR
     A            LGUAC          1A  B 12 39OVRDTA
     A                                      OVRATR
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(RI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A  61N20                               DSPATR(PC)
     A                                 13  2'Import / Export Code..............-
     A                                       :'
     A                                      OVRATR
     A            LIMEX          1A  B 13 39OVRDTA
     A                                      OVRATR
     A N55                                  DSPATR(HI)
     A  55                                  DSPATR(RI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A  55N20                               DSPATR(PC)
     A                                 14  2'Property Sector Code..............-
     A                                       :'
     A                                      OVRATR
     A            LPPSC          6A  B 14 39OVRDTA
     A                                      OVRATR
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A  53N20                               DSPATR(PC)
     A                                 23  9'F12=Previous'
     A                                      COLOR(BLU)
     A                                      OVRATR
     A                                 23  1'F3=Exit'
     A                                      COLOR(BLU)
      *-------------------------------------------------------------------------
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *-------------------------------------------------------------------------
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  69
     AON69                                  SFLEND
     A            SPGM                      SFLPGMQ
