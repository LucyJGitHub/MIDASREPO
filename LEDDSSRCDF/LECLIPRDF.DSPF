      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Invalid Customer Loans - Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LECLIPRDF - Invalid Customer Loans details browse            *
      *              ("REPAIR FUNCTION")                              *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAP074  *CREATE    Date 25Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP074 - Lending API enhancements - Loans input.             *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03
     A                                      CF12
      *
     A          R LECLIPS1                  SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2002')
     A            DDOPT          1A  B  7  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFOTRANID    20A  O  7  4DSPATR(HI)
     A            DDLNRF         6A  O  7 26DSPATR(HI)
     A            DDCUST        10A  O  7 34DSPATR(HI)
     A            DDVDAT         6A  O  7 46DSPATR(HI)
     A            DDMDAT         6A  O  7 54DSPATR(HI)
     A            DDCURR         3A  O  7 62DSPATR(HI)
     A            DDCPAM        14A  O  7 67DSPATR(HI)
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
     A            DDBRCA    R        H      REFFLD(BRSN)
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN CONTROL                                            *
      * -------------------------------------------------------------------*
      *
     A          R LECLIPS0                  SFLCTL(LECLIPS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'LECLIPR'
     A                                  1 26'Invalid Customer Loans Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Sel'
     A                                  4  6'Front Office Id.'
     A                                  4 34'Cust.'
     A                                  5 26'Number'
     A                                  5 34'Number'
     A                                  4 26'Loan'
     A                                  4 46'Start'
     A                                  5 46'Date'
     A                                  4 62'Curr.'
     A                                  5 62'Code'
     A                                  4 75'Amount'
     A                                  5 54'Date'
     A                                  4 54'Matu.'
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R LECLIPF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      *
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * END MESSAGE                                                      *
      * -------------------------------------------------------------------*
      *
     A          R LECLIPEND                 TEXT('END MESSAGE')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'LECLIPR'
     A                                  1 26'Invalid Customer +
     A                                      Loans Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
     A            DDFOTX        40A    24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
