      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Facility input First screen Display')         *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LEFCIP1DDF - Facility Input Primary Screen Display      *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAP073  *CREATE    Date 18Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus                                          *
      *  CAP073 - Conversion of MN inputs into modular structure to   *
      *           use as APIs. Facility input Transaction Details     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03
     A                                      CF12
     A  73                                  CF15
     A  74                                  CF07
     A  74                                  CF08
     A  76                                  CF10
     A  77                                  CF21
     A  71                                  CF22
     A  72                                  CF23
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R LEFCIPD1
     A                                      ROLLUP(79)
     A                                      PUTOVR
      *
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'LEFCIP1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 18'Facility input Detai-
     A                                      ls Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                      TEXT('CURRENT TIME')
     A                                  3 69'Page 1 of 2'
     A                                  4  2'Action'
     A            DDACTN         1A  B  4  9TEXT('Action')
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  68
     A  10N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4 11'Customer'
     A            DDCSSN        10A  B  4 20TEXT('Customer No.')
     A  12                                  DSPATR(RI)
     A N12                                  DSPATR(HI)
     A  12N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDNAME        10A  O  4 32TEXT('Customer NA.')
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4 43'Facility Type/No'
     A            DDFACT         3A  B  4 61TEXT('Facility No.')
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(HI)
     A  13N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDFCNO         2A  B  4 65TEXT('Facility No.')
     A  14                                  DSPATR(RI)
     A N14                                  DSPATR(HI)
     A  14                                  DSPATR(PC)
     A  69N14                               DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDFSTS        10A  O  4 71DSPATR(HI)
     A                                      OVRDTA
     A*
     A                                  6  2'Branch'
     A N81                                  DSPATR(ND)
     A                                      OVRATR
     A            DDBRCA         3A  B  6  9
     A N81
     AO 70                                  DSPATR(PR)
     A N70 81                               DSPATR(UL)
     A N81                                  DSPATR(ND)
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  7  2'Currency'
     A            DDFCCY         3A  B  7 11
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  7 17'Amount'
     A            DDFAMT        15A  B  7 24
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  7 41'Over-line %'
     A            DDOVPC         6A  B  7 53
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  7 63'Fcty/Cmmt Ind'
     A            DDFCTI         1A  B  7 77
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  8  2'Start Date'
     A            DDDTAP         6A  B  8 16
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  8 25'Revolving Credit'
     A            DDRVCR         1A  B  8 42
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  8 46'Multi Currency'
     A            DDMCCY         1A  B  8 61
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  8 65'Secured'
     A            DDSECI         1A  B  8 73
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  9  2'Date Expires'
     A            DDDTEX         6A  B  9 16
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  9 25'Country of Risk'
     A            DDCRSK         2A  B  9 41
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  9 46'Multi Customer'
     A            DDMCSI         1A  B  9 61
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  9 65'Alt n/a'
     A            DDNACD         1A  B  9 73
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 10  2'Multi Branch'
     A N81                                  DSPATR(ND)
     A            DDMBFC         1A  B 10 16
     A  70
     AON81                                  DSPATR(PR)
     A N81                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      **Start of change CER020
     A**********                       10 25'Rev. Cr. Date Changed'                           CER020
     A                                 10 18'Rev. Cr. Date Changed'
      **End of change CER020
     A  59                                  DSPATR(ND)
      **Start of change CER020
     A**********  DDRVCD         6A  O 10 48DSPATR(HI)                                        CER020
     A            DDRVCD         6A  O 10 40DSPATR(HI)
      **End of change CER020
     A  59                                  DSPATR(ND)
      **Start of change CER020
     A                                 10 47'Previous Expiry Date'
     A N42                                  DSPATR(ND)
     A            DDPEXD         6A  O 10 68
     A                                      DSPATR(HI)
     A N42                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
      **End of change CER020
     A                                 11  2'Date of Last Fin. Stmt.'
     A            DDDLFS         6A  B 11 26
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  30                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      **Start of change CER020
     A**********                       11 48'Nominal Rate'                                    CER020
     A**********  DDNOMR        12A  B 11 61                                                  CER020
     A                                 11 33'Nomn Rate'
     A            DDNOMR        12A  B 11 43
      **End of change CER020
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      **Start of change CER020
     A                                 11 56'Penultimate Exp Dt'
     A N42                                  DSPATR(ND)
     A            DDPNXD         6A  O 11 75
     A                                      DSPATR(HI)
     A N42                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
      **End of change CER020
     A                                 12  2'Review Date'
     A            DDRVDT         6A  B 12 14
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 12 22'Frequency'
     A            DDRVFR         1A  B 12 32
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 12 35'Day No'
     A            DDRVDY         2A  B 12 42
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 12 48'Account Officer'
     A            DDACOF         2A  B 12 64
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 13  2'Profit Centre'
     A  58                                  DSPATR(ND)
     A            DDPRFC         4A  B 13 16
     A  70
     AO 58
     AO 84                                  DSPATR(PR)
     A N70N58
     AON70N84                               DSPATR(UL)
     A  58                                  DSPATR(ND)
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 13 48'Originating Branch'
     A N81                                  DSPATR(ND)
     A            DDORBR         3A  B 13 67
     A  70
     AON81                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N81                                  DSPATR(ND)
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 14  2'Tranche/Credit Agreement Ind'
     A N82                                  DSPATR(ND)
     A            DDTRCA         2A  B 14 32
     A  70
     AON82                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N82                                  DSPATR(ND)
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 15  2'Credit/Agreement Customer'
     A N82                                  DSPATR(ND)
     A            DDCANM        10A  B 15 28
     A  70
     AON82                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N82                                  DSPATR(ND)
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 15 41'Credit/Agreement Type/No.'
     A N82                                  DSPATR(ND)
     A            DDCAFT         3A  B 15 69
     A  70
     AON82                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N82                                  DSPATR(ND)
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDCAFN         2A  B 15 73
     A  70
     AON82                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N82                                  DSPATR(ND)
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 16  2'Narrative'
     A            DDNAR1        35A  B 16 21
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDNAR2        35A  B 17 21
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDNAR3        35A  B 18 21
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDNAR4        35A  B 19 21
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 20  2'Bill/Telex Ind'
     A            DDBTIN         1A  B 20 17
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 20 21'Billing Days'
     A            DDBLDY         2A  B 20 34
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 20 48'Account Sequence Number'
     A N82
     AON88                                  DSPATR(ND)
     A            DDACSQ         2A  B 20 72
     A N82
     AO 70
     AON88                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N82
     AON88                                  DSPATR(ND)
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R LEFCIPS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R LEFCIPC1                  TEXT('MSG SUBFILE CONTROL')
     A                                      SFLCTL(LEFCIPS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A*
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R LEFCIPF1                  TEXT('SCREEN FOOTER')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 10'F12=Prv'
     A  76                             23 18'F10=Rev/Close'
     A  73                             23 32'F15=Sel'
     A  74                             23 40'F7=Prv F8=Nxt'
     A                                 23 54'F21=Fund'
     A N85
     AO 09                                  DSPATR(ND)
     A                                 23 63'F22=Risk'
     A N85
     AON82
     AO 09                                  DSPATR(ND)
     A                                 23 72'F23=Assi'
     A N85
     AO 09                                  DSPATR(ND)
     A            DDFOTX        48A  O 24  2
     A                                      DSPATR(HI)
