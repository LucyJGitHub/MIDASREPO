      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Facility input Second screen Display')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LEFCIP2DDF - Facility Input Secondary Screen Display    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE042             Date 06Sep05               *
      *  Prev Amend No. BUG3723            Date 19Jul04               *
      *                 CLE029             Date 23Aug02               *
      *                 CAP073 *CREATE     Date 18Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE042 - Extended Loan Sub Type                              *
      *  BUG3723- Missing Rule of 78s (CLE028) change in the Lending  *
      *           APIs as description in the amendment history.       *
      *  CLE029 - Addition of Severity code field                     *
      *  CAP073 - Conversion of MN inputs into modular structure to   *
      *           use as APIs. Facility input Transaction Details     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03
     A                                      CF12
     A  73                                  CF15
     A  74                                  CF07
     A  74                                  CF08
     A  76                                  CF10
     A  77                                  CF21
     A  71                                  CF22
     A  72                                  CF23
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R LEFCIPD2
     A                                      ROLLDOWN(78)
     A                                      PUTOVR
      *
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'LEFCIP2'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 18'Facility input Detai-
     A                                      ls Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                      TEXT('CURRENT TIME')
     A                                  3 69'Page 2 of 2'
     A                                  4  2'Action'
     A            DDACTN         1A  O  4  9TEXT('Action')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 11'Customer'
     A            DDCSSN        10A  O  4 21TEXT('Customer No.')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A            DDNAME        10A  O  4 32TEXT('Customer No.')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 43'Facility Type/No'
     A            DDFACT         3A  O  4 61TEXT('Facility No.')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A            DDFCNO         2A  O  4 65TEXT('Facility No.')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A            DDFST2        10A  O  4 71DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  6  2'Primary Facility Customer'
     A N70
     AON86                                  DSPATR(ND)
     A                                      OVRATR
     A            DDPMFC         6A  O  6 28DSPATR(HI)
     A N70
     AON86                                  DSPATR(ND)
     A                                      OVRATR
     A                                  6 41'Primary Facility Type/No.'
     A N70
     AON86                                  DSPATR(ND)
     A                                      OVRATR
     A            DDPMFT         3A  O  6 69DSPATR(HI)
     A N70
     AON86                                  DSPATR(ND)
     A                                      OVRATR
     A            DDPMFN         2A  O  6 73DSPATR(HI)
     A N70
     AON86                                  DSPATR(ND)
     A                                      OVRATR
     A                                  8  2'Syndication Indicator'
     A N85
     AO 86                                  DSPATR(ND)
     A                                      OVRATR
     A            DDSYIN         1A  B  8 28
     A                                      OVRATR
     A                                      OVRDTA
     A N70                                  DSPATR(UL)
     A  70
     AO 86                                  DSPATR(PR)
     A N85
     AO 86                                  DSPATR(ND)
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI)
     A                                  8 41'Syndication Complete'
     A N85
     AON70
     AO 86                                  DSPATR(ND)
     A                                      OVRATR
     A            DDSYCP         1A  B  8 69
     A                                      OVRATR
     A                                      OVRDTA
     A N70
     AON85
     AO 86                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A  70
     AON70
     AO 86                                  DSPATR(PR)
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A                                  9  2'Agent Customer No.'
     A N85
     AO 86                                  DSPATR(ND)
     A                                      OVRATR
     A            DDANUM        10A  B  9 28
     A                                      OVRATR
     A                                      OVRDTA
     A N85
     AO 86                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A  86
     AO 70                                  DSPATR(PR)
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A                                 10  2'Drawing Types'
     A            DDDTS1         1A  B 10 28
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N85
     AO 13                                  DSPATR(PC)
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(HI)
     A            DDDTS2         1A  B 10 30
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(HI)
     A            DDDTS3         1A  B 10 32
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(HI)
     A            DDDTS4         1A  B 10 34
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(HI)
     A            DDDTS5         1A  B 10 36
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(HI)
      ** Start of change BUG3723
     A            DDDTS6         1A  B 10 38
     A                                      OVRATR
     A                                      OVRDTA
     A N81
     AO 70                                  DSPATR(PR)
     A N70 81                               DSPATR(UL)
     A  13 81                               DSPATR(RI)
     A N13 81                               DSPATR(HI)
     A  13 81                               DSPATR(PC)
     A N81                                  DSPATR(ND)
      ** End of change BUG3723
     A                                 10 41'Default Interest Basis'
     A            DDDICB         1A  B 10 69
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A                                 11  2'Rollover Types'
     A            DDRTS1         1A  B 11 28
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A            DDRTS2         1A  B 11 30
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  19                                  DSPATR(RI)
     A N19                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDRTS3         1A  B 11 32
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  19                                  DSPATR(RI)
     A N19                                  DSPATR(HI)
     A            DDRTS4         1A  B 11 34
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  19                                  DSPATR(RI)
     A N19                                  DSPATR(HI)
     A            DDRTS5         1A  B 11 36
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  19                                  DSPATR(RI)
     A N19                                  DSPATR(HI)
     A            DDRTS6         1A  B 11 38
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  19                                  DSPATR(RI)
     A N19                                  DSPATR(HI)
     A                                 11 41'Irregular Rollover Period'
     A            DDRLPD         2A  B 11 69
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A                                 12  2'Loan Administrator Id'
     A N69                                  DSPATR(ND)
     A            DDLAID         2A  B 12 28
     A                                      OVRATR
     A                                      OVRDTA
     A  70
     AON69
     AO 77                                  DSPATR(PR)
     A N70
     AAN77                                  DSPATR(UL)
     A N69                                  DSPATR(ND)
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A                                 13  2'Currency Selection'
     A N87                                  DSPATR(ND)
     A            DDCSEL         1A  B 13 22
     A                                      OVRATR
     A                                      OVRDTA
     A  70
     AON87                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N87                                  DSPATR(ND)
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A                                 13 28'Allowable Currencies'
     A N87                                  DSPATR(ND)
     A            DDCY01         3A  B 13 49
     A                                      OVRATR
     A                                      OVRDTA
     A  70
     AON87                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N87                                  DSPATR(ND)
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A            DDCY02         3A  B 13 53
     A                                      OVRATR
     A                                      OVRDTA
     A  70
     AON87                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N87                                  DSPATR(ND)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(HI)
     A            DDCY03         3A  B 13 57
     A                                      OVRATR
     A                                      OVRDTA
     A  70
     AON87                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N87                                  DSPATR(ND)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(HI)
     A            DDCY04         3A  B 13 61
     A                                      OVRATR
     A                                      OVRDTA
     A  70
     AON87                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N87                                  DSPATR(ND)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(HI)
     A            DDCY05         3A  B 13 65
     A                                      OVRATR
     A                                      OVRDTA
     A  70
     AON87                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N87                                  DSPATR(ND)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(HI)
     A            DDCY06         3A  B 13 69
     A                                      OVRATR
     A                                      OVRDTA
     A  70
     AON87                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N87                                  DSPATR(ND)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(HI)
     A            DDCY07         3A  B 14 49
     A                                      OVRATR
     A                                      OVRDTA
     A  70
     AON87                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N87                                  DSPATR(ND)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(HI)
     A            DDCY08         3A  B 14 53
     A                                      OVRATR
     A                                      OVRDTA
     A  70
     AON87                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N87                                  DSPATR(ND)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(HI)
     A            DDCY09         3A  B 14 57
     A                                      OVRATR
     A                                      OVRDTA
     A  70
     AON87                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N87                                  DSPATR(ND)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(HI)
     A            DDCY10         3A  B 14 61
     A                                      OVRATR
     A                                      OVRDTA
     A  70
     AON87                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N87                                  DSPATR(ND)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(HI)
     A            DDCY11         3A  B 14 65
     A                                      OVRATR
     A                                      OVRDTA
     A  70
     AON87                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N87                                  DSPATR(ND)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(HI)
     A            DDCY12         3A  B 14 69
     A                                      OVRATR
     A                                      OVRDTA
     A  70
     AON87                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N87                                  DSPATR(ND)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(HI)
     A                                 15  2'Approval Date'
     A            DDCOMD         6A  B 15 17
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(HI)
     A                                 15 27'Closing Date'
     A N85                                  DSPATR(ND)
     A            DDCDTE         6A  B 15 40
     A                                      OVRATR
     A                                      OVRDTA
     A  70
     AON85                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N85                                  DSPATR(ND)
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A                                 15 49'Last Availability Date'
     A            DDAVLD         6A  B 15 72
     A                                      OVRATR
     A                                      OVRDTA
     A  70N84                               DSPATR(PR)
     A  70 84
     AON70                                  DSPATR(UL)
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A                                 16  2'Late Payment Penalty Type'
     A            DDLPTP         1A  B 16 29
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(HI)
     A                                 16 34'Base Rate'
     A            DDLPBR         2A  B 16 44
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(HI)
     A                                 16 49'Amount'
     A            DDLPAM        14A  B 16 56
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(HI)
     A                                 16 72'Sign'
     A            DDLPPS         1A  B 16 77
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(HI)
     A                                 17  2'Default Participation Loan Type/Su-
     A                                      btype'
     A N85
     AO 86                                  DSPATR(ND)
     A                                      OVRATR
     A            DDDFTP         2A  B 17 44
     A                                      OVRATR
     A                                      OVRDTA
     A N85
     AO 86                                  DSPATR(ND)
     A  86
     AON85
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(HI)
     A            DDDFST         2A  B 17 47
     A                                      OVRATR
     A                                      OVRDTA
     A N85
     AO 86                                  DSPATR(ND)
     A  86
     AON85
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(HI)
      *                                              Start /CLE042/
     A            DDDFCL         4A  B 17 50
     A                                      OVRATR
     A                                      OVRDTA
     A N85
     AO 86                                  DSPATR(ND)
     A  86
     AON85
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(HI)
      *                                              End   /CLE042/
     A                                 18  2'Credit/Agreement Exchange Rate'
     A N82                                  DSPATR(ND)
     A                                      OVRATR
     A            DDCAXR        13A  B 18 33TEXT('C/A Exchange Rate')
     A                                      OVRATR
     A                                      OVRDTA
     A N82                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A  49                                  DSPATR(RI PC)
     A N49                                  DSPATR(HI)
     A                                 18 50'Exchange Rate Indicator'
     A N82                                  DSPATR(ND)
     A            DDCMDI         1A  B 18 74TEXT('Exchange Rate Indicator')
     A                                      OVRATR
     A                                      OVRDTA
     A N82                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A N82
     AO 70                                  DSPATR(PR)
     A  50                                  DSPATR(RI PC)
     A N50                                  DSPATR(HI)
      *                                              Start /CLE029/
     A                                 19  2'Severity Code'
     A            DDSVCD         1A  B 19 16TEXT('Severity Code')
     A                                      OVRATR
     A                                      OVRDTA
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  51                                  DSPATR(RI PC)
     A N51                                  DSPATR(HI)
      *                                              End   /CLE029/
     A
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R LEFCIPS2                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R LEFCIPC2                  TEXT('MSG SUBFILE CONTROL')
     A                                      SFLCTL(LEFCIPS2)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R LEFCIPF2                  TEXT('SCREEN FOOTER')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 10'F12=Prv'
     A  76                             23 18'F10=Rev/Close'
     A  73                             23 32'F15=Sel'
     A  74                             23 40'F7=Prv F8=Nxt'
     A                                 23 54'F21=Fund'
     A N85
     AON70                                  DSPATR(ND)
     A                                 23 63'F22=Risk'
     A N85
     AON82
     AON70                                  DSPATR(ND)
     A                                 23 72'F23=Assi'
     A N85
     AON70                                  DSPATR(ND)
