      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Facility input details - browse')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LEFCIPBDF - Facility input browse                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAP073  *CREATE    Date 18Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP073 - Conversion of MN inputs into modular structure to   *
      *           use as APIs. Facility input Transaction Details     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN SUB-FILE 1                                         *
      * -------------------------------------------------------------------*
      *
     A          R LEFCIPS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDCNUM         6A  O  8  6DSPATR(HI)
     A            DDFACT         3A  O  8 13DSPATR(HI)
     A            DDFCNO         2A  O  8 17DSPATR(HI)
     A            DDNAME        10A  O  8 20DSPATR(HI)
     A            DDDTAP         6A  O  8 33DSPATR(HI)
     A            DDDTEX         6A  O  8 42DSPATR(HI)
     A            DDFAMT        14A  O  8 51DSPATR(HI)
     A            DDFCCY         3A  O  8 67DSPATR(HI)
     A            DDSTAT         6A  O  8 72DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN CONTROL 1                                          *
      * -------------------------------------------------------------------*
      *
     A          R LEFCIPC1                  SFLCTL(LEFCIPS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2
     A                                  1 13'LEFCIPB1'
     A                                  1 26'            Midas            '
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 23'Facility Input and Authorisa+
     A                                      tion'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  2'Sel'
     A                                  5  6'Facility ref.'
     A                                  5 20'Shortname'
     A                                  5 33'Start'
     A                                  5 42'Expiry'
     A                                  5 59'Amount'
     A                                  5 67'Ccy'
     A                                  5 72'Status'
     A                                  6 33'Date'
     A                                  6 42'Date'
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN SUB-FILE 2                                         *
      * -------------------------------------------------------------------*
      *
     A          R LEFCIPS2                  TEXT('SELECT SCREEN SUBFILE')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFOTRANID    20A  O  8  6DSPATR(HI)
     A            DDCNUM         6A  O  8 28DSPATR(HI)
     A            DDFACT         3A  O  8 35DSPATR(HI)
     A            DDFCNO         2A  O  8 39DSPATR(HI)
     A            DDDTAP         6A  O  8 42DSPATR(HI)
     A            DDDTEX         6A  O  8 49DSPATR(HI)
     A            DDFAMT        14A  O  8 56DSPATR(HI)
     A            DDFCCY         3A  O  8 71DSPATR(HI)
     A            DDSTAT         6A  O  8 75DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN CONTROL 2                                          *
      * -------------------------------------------------------------------*
      *
     A          R LEFCIPC2                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLCTL(LEFCIPS2)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2
     A                                  1 13'LEFCIPB2'
     A                                  1 26'            Midas            '
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 23'Facility Input and Authorisa+
     A                                      tion'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  2'Sel'
     A                                  5  6'Front Office Id.'
     A                                  5 28'Facility ref.'
     A                                  5 42'Start'
     A                                  6 42'Date'
     A                                  5 49'Expiry'
     A                                  6 49'Date'
     A                                  5 56'Amount'
     A                                  5 71'Cur'
     A                                  5 75'Status'
      *
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R LEFCIPF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
