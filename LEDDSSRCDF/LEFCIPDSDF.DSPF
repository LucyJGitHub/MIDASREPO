      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas BOE Facilities Maintenance Window Disp File')    *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LEFCIPDSDF - LE Facility Maintenance Window             *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Midas Plus 1.4 Base ------------------------------------------*
      *  Prev Amend No. LLN022  *CREATE    Date 24Jan05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN022 - BOE Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A                                      CF12(12 'Previous')
     A          R WINDOWF
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      WINDOW(&SWROW &SWCOL 7 45)
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A                                  3  1'Euro Facility ..........:'
     A            ULEURO         1A  B  3 40OVRDTA
     A N50N20                               DSPATR(HI)
     A  50N20                               DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                  5  1'F3=Main Menu'
     A                                  5 15'F12=Previous'
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(06)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A                                      WINDOW(WINDOWF)
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
