      *****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('LE Facilities LUX Display')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LEFCIPLDF - LUX LE Facilities Display File                   *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *-------------------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
      *-------------------------------------------------------------------------
     A          R LEFCIPDSP
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A                                      KEEP
     A                                      OVERLAY
     A                                      CF03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A  40                                  CF10(10 'Delete')
     A                                      CF12(12 'Previous')
     A                                      PUTOVR
     A            SUSER         10A  O  1  2
     A            SJOB          10A  O  2  2
     A            SPGM          10A  O  1 13
     A                                  1 65'Date:'
     A            BJMRDT         7A  O  1 72
     A                                  2 65'Time:'
     A                                  2 71TIME
     A                                  2 24'European Reporting - Luxembourg'
     A                                      DSPATR(HI)
      *
     A                                  5  1'Subordinated......................-
     A                                       :'
     A            LSUBR          1A  B  5 38OVRDTA
     A                                      DSPATR(HI)
     A N50N20                               DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                  6  1'Mobilisable Customer..............-
     A                                       :'
     A            LMOBL         10A  B  6 38OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N51N20                               DSPATR(UL)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                  7  1'Degree of Risk................... -
     A                                       :'
     A            LDGRI          1A  B  7 38OVRDTA
     A                                      DSPATR(HI)
     A N52N20                               DSPATR(UL)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                 23 40'F12=Previous'
     A                                      COLOR(BLU)
     A                                 23  1'F3=Main Menu'
     A                                      COLOR(BLU)
     A  35                             23 15'F5=Refresh'
     A                                      COLOR(BLU)
     A  40                             23 27'F10=Delete'
     A                                      COLOR(BLU)
     A                                 23  1'F12=Previous'
     A                                      COLOR(BLU)
     A                                      OVRATR
      *-------------------------------------------------------------------------
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *-------------------------------------------------------------------------
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  69
     AON69                                  SFLEND
     A            ##PGM                     SFLPGMQ
