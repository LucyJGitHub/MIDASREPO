      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Fee Adjustment Display')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LEFEADDDF - Fee Adjustment Display                      *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 12Apr04               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP072 *C *CREATE  Date 27Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Webfacing changes.                                  *
      *  CAP072 - Conversion of LE inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*=========================================================================
     A          R LEFEADF2
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      KEEP
     A            S#USER    R        O  1  2REFFLD(USER)
     A            S#WSID    R        O  2  2REFFLD(JOJOB)
     A                                  1 14'LEFEADSIN'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A            S#RUNA    R        O  1 72REFFLD(RUNA)
     A                                  2 29'FEE ADJUSTMENT INPUT'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*=========================================================================
     A          R LEFEADS1                  SFL
     A  35                                  SFLNXTCHG
     A            S#SEL          1A  B 10  4
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI UL)
     A            S#CSSN        10A  O 10  8DSPATR(HI)
     A            S#FACT         3A  O 10 19DSPATR(HI)
     A            S#FCNO         2A  O 10 23DSPATR(HI)
     A            S#FSEQ         2A  O 10 29DSPATR(HI)
     A            S#SADE        19A  O 10 43DSPATR(HI)
     A            S#FCCY         3A  O 10 64DSPATR(HI)
     A            S#STAT        10A  O 10 71DSPATR(HI)
     A*=========================================================================
     A          R LEFEADC1                  SFLCTL(LEFEADS1)
     A                                      SFLSIZ(11)
     A                                      SFLPAG(10)
     A                                      CF09(09 'Add')
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A  30                                  SFLDSP
     A  31                                  SFLDSPCTL
     A N31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A N32                                  ROLLUP(33)
     A            ##SFRC         4S 0H      SFLRCDNBR
     A                                  4 48'Review from'
     A            R#CSSN        10A  B  4 60DSPATR(UL)
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A            R#FACT         3A  B  4 71DSPATR(UL)
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI)
     A            R#FCNO         2A  B  4 75DSPATR(UL)
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A                                  5  2'Actions,'
     A                                  5 48'Unauthorised Only?'
     A            R#UNAU         1A  B  5 67DSPATR(HI UL)
     A                                  6  3'E=Enquire'
     A                                  6 16'A=Amend'
     A                                  6 27'D=Delete'
     A  64                              6 39'X=Authorise'
     A                                  8  3'Sel'
     A                                  8  8'Customer'
     A                                  8 18'Facility'
     A                                  8 28'Fee'
     A                                  8 51'Adjustment'
     A                                  8 64'Ccy'
     A                                  8 71'Status'
      *
     A                                  9  9'Number'
     A                                  9 28'Seq'
     A                                  9 55'Amount'
     A*=========================================================================
     A          R LEFEADF1
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      ** Start of change CAAA03
     A N62                                  DSPATR(ND)
      ** End of change CAAA03
     A                                 23 23'F10=Delete' DSPATR(HI)
     A N68                                  DSPATR(ND)
     A                                 23 35'F11=Authorise' DSPATR(HI)
     A N66                                  DSPATR(ND)
     A                                 23 50'F12=Previous Screen'
     A*=========================================================================
     A          R LEFEADF3
     A                                      CF12(12 'Previous')
     A  68                                  CF10(10 'Reverse')
     A  66                                  CF11(11 'Authorize')
     A                                      OVERLAY
     A                                      PROTECT
     A                                  4  2'Action'
     A            S#ACTN         1A  B  4 20
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(HI)
     A                                      DSPATR(PR)
     A                                  4 37'Status'
     A            S#STAT        10A  O  4 44DSPATR(HI)
     A                                  6  2'Customer'
     A            S#CSSN        10A  B  6 20
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                  7  2'Facility Type/No'
     A            S#FACT         3A  B  7 20
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A            S#FCNO         2A  B  7 24
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                  8  2'Fee sequence'
     A            S#FSEQ         2A  B  8 20
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A            S#FTYP        10A  O  8 23DSPATR(HI)
     A                                 10  2'Fee Currency'
     A            S#FCCY         3A  O 10 20DSPATR(HI)
     A                                 11  2'Fee Code'
     A            S#FCOD         2A  O 11 20DSPATR(HI)
     A            S#FENV        20A  O 11 23DSPATR(HI)
     A                                 12  2'Total Accrued to Date'
     A            S#ACCD        18A  O 12 24DSPATR(HI)
     A                                 14  2'Adjustment Amount'
     A            S#SADJ        14A  B 14 20
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(HI)
     A  67                                  DSPATR(PR)
     A N67                                  DSPATR(UL)
     A                                 15  2'Sign (+ or -)'
     A            S#SIGN         1A  B 15 20
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(HI)
     A  67                                  DSPATR(PR)
     A N67                                  DSPATR(UL)
     A*=========================================================================
     A          R LEFEADF4
     A                                      OVERLAY
     A                                 13 32'NO RECORDS TO DISPLAY'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A*=========================================================================
     A          R LEFEADF5
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 14'F5=Refresh'
     A                                 23 29'F9=Add'
     A                                 23 36'F12=Previous'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A  25                                  SFLDSP
     A  25                                  SFLDSPCTL
     A  25                                  SFLINZ
     A  25                                  SFLCLR
     A  25                                  SFLEND
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
