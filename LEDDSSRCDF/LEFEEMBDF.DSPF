      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Fee input details - browse')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending  Module                             *
      *                                                               *
      *  LEFEEMBDF - Fee input browse                                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAP078  *CREATE    Date 18Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP078 - Lending API enhancements - Fee input.               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN SUB-FILE 1                                         *
      * -------------------------------------------------------------------*
      *
     A          R LEFEEMS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDCNUM         6A  O  8  6DSPATR(HI)
     A            DDFACT         3A  O  8 13DSPATR(HI)
     A            DDFCNO         2A  O  8 17DSPATR(HI)
     A            DDLNRF         6A  O  8 20DSPATR(HI)
     A            DDFSEQ         2A  O  8 27DSPATR(HI)
     A            DDECOD         2A  O  8 30DSPATR(HI)
     A            DDECUR         3A  O  8 33DSPATR(HI)
     A            DDEAMT        14A  O  8 37DSPATR(HI)
     A            DDEPSD         6A  O  8 52DSPATR(HI)
     A            DDEPED         6A  O  8 59DSPATR(HI)
     A            DDSTAT         8A  O  8 66DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN CONTROL 1                                          *
      * -------------------------------------------------------------------*
      *
     A          R LEFEEMC1                  SFLCTL(LEFEEMS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous')
     A                                      CF11(11 'Toggle Front Office ID')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2
     A                                  1 13'LEFEEMB1'
     A                                  1 26'            Midas            '
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 23'Fee Input and Authorisa+
     A                                      tion'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  3'E=Enquire'
     A                                  4 16'A=Amend'
     A                                  4 27'D=Delete'
     A                                  4 39'X=Authorise'
     A                                  5  2'Sel'
     A                                  5  6'Cust   Fclty  Loan'
     A                                  5 28'Fee'
     A                                  5 33'Ccy'
     A                                  5 40'Amount'
     A                                  5 53'Fee Period'
     A                                  5 66'Status'
     A*
     A                                  6 26'Seq Code'
     A                                  6 52'Start'
     A                                  6 60'End'
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN SUB-FILE 2                                         *
      * -------------------------------------------------------------------*
      *
     A          R LEFEEMS2                  TEXT('SELECT SCREEN SUBFILE')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFOTRANID    20A  O  8  6DSPATR(HI)
     A            DDCNUM         6A  O  8 27DSPATR(HI)
     A            DDFACT         3A  O  8 34DSPATR(HI)
     A            DDFCNO         2A  O  8 38DSPATR(HI)
     A            DDLNRF         6A  O  8 41DSPATR(HI)
     A            DDFSEQ         2A  O  8 48DSPATR(HI)
     A            DDECOD         2A  O  8 51DSPATR(HI)
     A            DDECUR         3A  O  8 54DSPATR(HI)
     A            DDEPSD         6A  O  8 58DSPATR(HI)
     A            DDEPED         6A  O  8 65DSPATR(HI)
     A            DDSTAT         8A  O  8 72DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN CONTROL 2                                          *
      * -------------------------------------------------------------------*
      *
     A          R LEFEEMC2                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous')
     A                                      CF11(11 'Toggle Front Office ID')
     A                                      SFLCTL(LEFEEMS2)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2
     A                                  1 13'LEFEEMB2'
     A                                  1 26'            Midas            '
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 23'Fee Input and Authorisa+
     A                                      tion'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  2'Sel'
     A                                  5  6'Front Office Id.'
     A                                  5 28'Cust  Fclty   Loan'
     A                                  5 49'Fee'
     A                                  5 54'Ccy'
     A                                  5 59'Fee Period'
     A                                  5 72'Status'
     A*
     A                                  6 47'Seq Code'
     A                                  6 58'Start'
     A                                  6 65'End'
      *
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R LEFEEMF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
