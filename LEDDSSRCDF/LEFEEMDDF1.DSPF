      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Fee input Display')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LEFEEMDDF - Fee Display                                 *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAP078  *CREATE    Date 21Oct02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP078 - Conversion of MN inputs into modular structure to   *
      *           use as APIs. Fee input Transaction Details          *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
     A          R LEFEEMF2
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      OVERLAY
     A                                      KEEP
     A            DDUSER    R        O  1  2REFFLD(USER)
     A            DDWSID    R        O  2  2REFFLD(JOJOB)
     A                                  1 14'LEFEEMSIN'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A            DDRUNA    R        O  1 72REFFLD(RUNA)
     A                                  2 29'FEE INPUT'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A          R LEFEEMS1                  SFL
     A  53                                  SFLNXTCHG
     A            DDSEL          1A  B 10  3
     A  54                                  DSPATR(RI PC)
     A N54                                  DSPATR(HI UL)
     A            DDCSSN        10A  O 10  6DSPATR(HI)
     A            DDFACT         3A  O 10 17DSPATR(HI)
     A            DDFCNO         2A  O 10 21DSPATR(HI)
     A            DDLNRF         6A  O 10 23DSPATR(HI)
     A            DDFSEQ         2A  O 10 30DSPATR(HI)
     A            DDPSTD         6A  O 10 33DSPATR(HI)
     A            DDPEND         6A  O 10 40DSPATR(HI)
     A            DDSTAT        10A  O 10 47DSPATR(HI)
      *
     A          R LEFEEMC1                  SFLCTL(LEFEEMS1)
     A                                      SFLSIZ(11)
     A                                      SFLPAG(10)
     A N66                                  CF09(09 'Add')
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A N51                                  SFLCLR
     A  52                                  SFLEND(*MORE)
     A N52                                  ROLLUP(33)
     A            ##SFRC         4S 0H      SFLRCDNBR
     A                                  4 50'Review from'
     A            R#CSSN        10A  B  4 62DSPATR(UL)
     A  55                                  DSPATR(RI PC)
     A N55                                  DSPATR(HI)
     A            R#FACT         3A  B  4 73DSPATR(UL)
     A  56                                  DSPATR(RI PC)
     A N56                                  DSPATR(HI)
     A            R#FCNO         2A  B  4 77DSPATR(UL)
     A  57                                  DSPATR(RI PC)
     A N57                                  DSPATR(HI)
     A            R#LNRF         6A  B  4 77DSPATR(UL)
     A  58                                  DSPATR(RI PC)
     A N58                                  DSPATR(HI)
     A            R#FSEQ         2A  B  4 77DSPATR(UL)
     A  59                                  DSPATR(RI PC)
     A N59                                  DSPATR(HI)
     A                                  5  2'Actions,'
     A                                  5 50'Unauthorised Only?'
     A            R#UNAU         1A  B  5 69DSPATR(HI UL)
     A                                  6  3'E=Enquire'
     A                                  6 16'A=Amend'
     A  66                                  DSPATR(ND)
     A                                  6 27'D=Delete'
     A  66                                  DSPATR(ND)
     A  64                              6 39'X=Authorise'
     A                                  8  2'Sel'
     A                                  8  6'Customer'
     A                                  8 17'Facility'
     A                                  8 23'Loan'
     A                                  8 30'Seq'
     A                                  8 33'Start'
     A                                  8 40'End'
     A                                  8 47'Status'
      *
     A          R LEFEEMF1
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F10=Delete' DSPATR(HI)
     A N68                                  DSPATR(ND)
     A                                 23 35'F11=Authorise' DSPATR(HI)
     A N66                                  DSPATR(ND)
     A                                 23 50'F12=Previous Screen'
     A                                 23 70'F14=Settl.'
     A N71                                  DSPATR(ND)
      *
     A          R LEFEEMF3
     A                                      CF12(12 'Previous')
     A  68                                  CF10(10 'Reverse')
     A  66                                  CF11(11 'Authorise')
     A  71                                  CF14(14 'Settlement')
     A                                      OVERLAY
     A                                      PROTECT
     A                                  4 43'Action'
     A            DDACTN         1A  B  4 52
     A  10N69                               DSPATR(RI PC)
     A N10                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                  4 57'Status'
     A            DDSTAT        10A  O  4 64DSPATR(HI)
     A            DDFETP        14   O  5  3DSPATR(HI)
     A                                  6  3'Customer'
     A            DDCSSN        10A  B  6 12
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                  6 23'Facility'
     A  62                                  DSPATR(ND)
     A            DDFACT         3A  B  7 20
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A  62                                  DSPATR(ND)
     A            DDFCNO         2A  B  7 24
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A  62                                  DSPATR(ND)
     A                                  6 41'Loan no'
     A  73                                  DSPATR(ND)
     A            DDLNRF         6A  B  6 50
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A  73                                  DSPATR(ND)
     A                                  6 59'Fee Sequence'
     A            DDFSEQ         2A  O  6 73
     A                                      DSPATR(HI)
      *
     A                                  8  3'Fee Code'
     A            DDECOD         2   B  8 15
     A  14                                  DSPATR(RI PC)
     A  84
     AO 85
     AO 86
     AO 69                                  DSPATR(PR)
     A N84N85N86
     A N69N14                               DSPATR(UL)
     A                                      DSPATR(HI)
      *
     A                                  8 20'Currency'
     A            DDECUR         3   B  8 30
     A  15                                  DSPATR(RI PC)
     A  84
     AO 85
     AO 86
     AO 69                                  DSPATR(PR)
     A N84N85N86
     A N69N15                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 39'Amount'
     A            DDEAMT        14   B  8 47
     A  16                                  DSPATR(RI)
     A  85N86
     AO 16                                  DSPATR(PC)
     A  84
     AO 86
     AO 69                                  DSPATR(PR)
     A N84N86
     A N69N16                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDESGN         1   B  8 62
     A  17                                  DSPATR(RI PC)
     A  84
     AO 86
     AO 69                                  DSPATR(PR)
     A N84N86
     A N69N17                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9  3'Fee Period'
     A                                  9 20'Start'
     A            DDEPSD         6   B  9 30
     A  18                                  DSPATR(RI PC)
     A  84
     AO 85
     AO 86
     AO 69                                  DSPATR(PR)
     A N84N85N86
     A N69N18                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 39'End'
     A            DDEPED         6   B  9 47
     A  19                                  DSPATR(RI)
     A  84
     AO 86
     AO 19                                  DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69N19                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  3'Payment On'
     A            DDEPON         1   B 10 15
     A  20                                  DSPATR(RI PC)
     A  84
     AO 85
     AO 86
     AO 69                                  DSPATR(PR)
     A N84N85N86
     A N69N20                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 20'Advice/Confirmed Ind'
     A            DDEADV         1   B 10 42
     A  21                                  DSPATR(RI PC)
     A  87N86
     AO 69                                  DSPATR(PR)
     A  87 86
     A N69N21
     AON87 86
     A N69N21
     AON87N86
     A N69N21                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 47'Shared Fee'
     A N72                                  DSPATR(ND)
     A            DDESHR         1   B 10 59
     A N72                                  DSPATR(ND)
     A  22                                  DSPATR(RI PC)
     A  84
     AO 85
     AO 86
     AO 69
     AON72                                  DSPATR(PR)
     A N84N85N86
     A N69N22                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 12  3'Calculation Type'
     A            DDCALT         2   B 12 21
     A  23                                  DSPATR(RI PC)
     A  84
     AO 85
     AO 86
     AO 69                                  DSPATR(PR)
     A N84N85N86
     A N69N23                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 13  3'Rates'
     A            DDRTE1         8   B 13 10
     A  24                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N24                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 13 21'Amounts'
     A            DDAMT1        11   B 13 30
     A  25                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N25                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDRTE2         8   B 14 10
     A  26                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N26                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDAMT2        11   B 14 30
     A  27                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N27                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDRTE3         8   B 15 10
     A  28                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N28                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDAMT3        11   B 15 30
     A  29                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N29                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDRTE4         8   B 16 10
     A  30                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N30                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDAMT4        11   B 16 30
     A  31                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N31                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDRTE5         8   B 17 10
     A  32                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N32                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDAMT5        11   B 17 30
     A  33                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N33                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 19  3'Percent Ind'
     A            DDPIND         1   B 19 16
     A  34                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N34                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 19 19'Calculation Basis'
     A            DDCALC         1   B 19 38
     A  35                                  DSPATR(RI PC)
     A  86
     AO 69                                  DSPATR(PR)
     A N86
     A N69N35                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 19 42'Exchange Rate'
     A            DDEXRT        14   B 19 57
     A  36                                  DSPATR(RI PC)
     A  86
     AO 69                                  DSPATR(PR)
     A N86
     A N69N36                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 19 73'Ind'
     A            DDMDIN         1   B 19 78
     A  37                                  DSPATR(RI PC)
     A  86
     AO 69                                  DSPATR(PR)
     A N86
     A N69N37                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 20  3'Next Payment Date'
     A            DDNPYD         6   B 20 22
     A  38                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N38                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 20 31'Frequency'
     A            DDFREQ         1   B 20 42
     A  39                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N39                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 20 46'Day in Month'
     A            DDDYIM         2   B 20 60
     A  40                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N40                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  63                             21  4'Profit Centre'
     A  63        DDPRFC         4A  B 21 18
     A  41                                  DSPATR(RI PC)
     A  69
     AON61                                  DSPATR(PR)
     A N69N41 61                            DSPATR(UL)
     A                                      DSPATR(HI)
      *
      *
     A          R LEFEEMF4
     A                                      OVERLAY
     A                                 13 32'NO RECORDS TO DISPLAY'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A          R LEFEEMF5
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 14'F5=Refresh'
     A                                 23 29'F9=Add'
     A  66                                  DSPATR(ND)
     A                                 23 36'F12=Previous'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A  60                                  SFLDSP
     A  60                                  SFLDSPCTL
     A  60                                  SFLINZ
     A  60                                  SFLCLR
     A  60                                  SFLEND
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
