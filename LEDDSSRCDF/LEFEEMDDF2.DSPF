      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Fee input - Details screen')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending  API Module                         *
      *                                                               *
      *  LEFEEMDDF - Fee input - Detail Screen                        *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAP078  *CREATE    Date 25Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP078 - Lending API enhancements - Fee input.               *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03
     A                                      CF12
     A  52                                  CF14
     A  53                                  CF15
     A  54                                  CF07
     A  54                                  CF08
     A  56                                  CF10
     A  51                                  CF11
      *
      ***  Details
      *
     A          R LEFEEMD1
     A                                      OVERLAY
     A                                      ROLLUP(79)
     A                                      PUTOVR
      *
     A            DDUSER        10A  O  1  2
     A                                  1 13'LEFEEM1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 30'Fee Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
     A            DDFETP        14   O  3  3DSPATR(HI)
     A                                  3 57'Status'
     A            DDSTAT        16A  O  3 64DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  4  3'Action'
     A            DDACTN         1A  B  4 10TEXT('Action')
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  68
     A  10N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4 12'Customer'
     A            DDCSSN        10A  B  4 21TEXT('Customer No.')
     A  11                                  DSPATR(RI)
     A N11                                  DSPATR(HI)
     A  11N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4 33'Facility'
     A            DDFACT         3A  B  4 42TEXT('Facility No.')
     A  12                                  DSPATR(RI)
     A N12                                  DSPATR(HI)
     A  12N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDFCNO         2A  B  4 46TEXT('Facility No.')
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(HI)
     A  13                                  DSPATR(PC)
     A  69N13                               DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4 49'Loan no'
     A            DDLNRF         6A  B  4 57TEXT('Loan No.')
     A  14                                  DSPATR(RI)
     A N14                                  DSPATR(HI)
     A  14N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4 64'Fee Sequence'
     A            DDFSEQ         2   O  4 77DSPATR(HI)
     A*
     A                                  8  3'Fee Code'
     A            DDECOD         2   B  8 15
     A  15                                  DSPATR(RI PC)
     A  84
     AO 85
     AO 86
     AO 69                                  DSPATR(PR)
     A N84N85N86
     A N69N15                               DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A                                  8 20'Currency'
     A            DDECUR         3   B  8 30
     A  16                                  DSPATR(RI PC)
     A  84
     AO 85
     AO 86
     AO 69                                  DSPATR(PR)
     A N84N85N86
     A N69N16                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 39'Amount'
     A            DDEAMT        14   B  8 47
     A  17                                  DSPATR(RI)
     A  85N86
     AO 17                                  DSPATR(PC)
     A  84
     AO 86
     AO 69                                  DSPATR(PR)
     A N84N86
     A N69N17                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDESGN         1   B  8 62
     A  18                                  DSPATR(RI PC)
     A  84
     AO 86
     AO 69                                  DSPATR(PR)
     A N84N86
     A N69N18                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9  3'Fee Period'
     A                                  9 20'Start'
     A            DDEPSD         6   B  9 30
     A  19                                  DSPATR(RI PC)
     A  84
     AO 85
     AO 86
     AO 69                                  DSPATR(PR)
     A N84N85N86
     A N69N19                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 39'End'
     A            DDEPED         6   B  9 47
     A  20                                  DSPATR(RI)
     A  84
     AO 86
     AO 20                                  DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69N20                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  3'Payment On'
     A            DDEPON         1   B 10 15
     A  21                                  DSPATR(RI PC)
     A  84
     AO 85
     AO 86
     AO 69                                  DSPATR(PR)
     A N84N85N86
     A N69N21                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 20'Advice/Confirmed Ind'
     A            DDEADV         1   B 10 42
     A  22                                  DSPATR(RI PC)
     A  87N86
     AO 69                                  DSPATR(PR)
     A  87 86
     A N69N22
     AON87 86
     A N69N22
     AON87N86
     A N69N22                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 47'Shared Fee'
     A N72                                  DSPATR(ND)
     A            DDESHR         1   B 10 59
     A N72                                  DSPATR(ND)
     A  23                                  DSPATR(RI PC)
     A  84
     AO 85
     AO 86
     AO 69
     AON72                                  DSPATR(PR)
     A N84N85N86
     A N69N23                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 12  3'Calculation Type'
     A            DDCALT         2   B 12 21
     A  24                                  DSPATR(RI PC)
     A  84
     AO 85
     AO 86
     AO 69                                  DSPATR(PR)
     A N84N85N86
     A N69N24                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 13  3'Rates'
     A            DDRTE1         8   B 13 10
     A  25                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N25                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 13 21'Amounts'
     A            DDAMT1        11   B 13 30
     A  26                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N26                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDRTE2         8   B 14 10
     A  27                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N27                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDAMT2        11   B 14 30
     A  28                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N28                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDRTE3         8   B 15 10
     A  29                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N29                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDAMT3        11   B 15 30
     A  30                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N30                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDRTE4         8   B 16 10
     A  31                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N31                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDAMT4        11   B 16 30
     A  32                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N32                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDRTE5         8   B 17 10
     A  33                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N33                               DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDAMT5        11   B 17 30
     A  34                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N34                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 19  3'Percent Ind'
     A            DDPIND         1   B 19 16
     A  35                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N35                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 19 19'Calculation Basis'
     A            DDCALC         1   B 19 38
     A  36                                  DSPATR(RI PC)
     A  86
     AO 69                                  DSPATR(PR)
     A N86
     A N69N36                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 19 42'Exchange Rate'
     A            DDEXRT        14   B 19 57
     A  37                                  DSPATR(RI PC)
     A  86
     AO 69                                  DSPATR(PR)
     A N86
     A N69N37                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 19 73'Ind'
     A            DDMDIN         1   B 19 78
     A  38                                  DSPATR(RI PC)
     A  86
     AO 69                                  DSPATR(PR)
     A N86
     A N69N38                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 20  3'Next Payment Date'
     A            DDNPYD         6   B 20 22
     A  39                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N39                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 20 31'Frequency'
     A            DDFREQ         1   B 20 42
     A  40                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N40                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 20 46'Day in Month'
     A            DDDYIM         2   B 20 60
     A  41                                  DSPATR(RI PC)
     A  69                                  DSPATR(PR)
     A N69N41                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  63                             21  4'Profit Centre'
     A  63        DDPRFC         4A  B 21 18
     A  42                                  DSPATR(RI PC)
     A  69
     AON61                                  DSPATR(PR)
     A N69N42 61                            DSPATR(UL)
     A                                      DSPATR(HI)
      *
      ***  Screen footer
      *
     A          R LEFEEMF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 10'F12=Prev'
     A  52                             23 19'F14=Settl Inst'
     A  53                             23 34'F15=Select'
     A  54                             23 45'F7=Prev F8=Next'
     A  56                             23 19'F10=Delete'
     A  51                             23 61'F11=Auth.'
     A                                 23 71'Page keys'
     A            DDFOTX        40A  O 24  2
     A                                      DSPATR(HI)
      *
      ***  Message subfile record
      *
     A          R LEFEEMS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ***  Message subfile control
      *
     A          R LEFEEMC1                  TEXT('Program messages')
     A                                      SFLCTL(LEFEEMS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
