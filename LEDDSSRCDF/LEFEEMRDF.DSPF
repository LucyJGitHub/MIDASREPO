      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Fee input Repair Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LEFEEMRDF - Fee Repair Browse                                *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAP078  *CREATE    Date 18Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP078 - Lending API enhancements - Fee input.               *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R LEIFEEMS1                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A            DDOPT          1A  B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFOTRANID    20A  O  8  4DSPATR(HI)
     A            DDCNUM         6A  O  8 26DSPATR(HI)
     A            DDFACT         3A  O  8 35DSPATR(HI)
     A            DDFCNO         2A  O  8 39DSPATR(HI)
     A            DDLNRF         6A  O  8 44DSPATR(HI)
     A            DDFSEQ         2A  O  8 51DSPATR(HI)
     A            DDECOD         2A  O  8 54DSPATR(HI)
     A            DDECUR         3A  O  8 57DSPATR(HI)
     A            DDEAMT        14A  O  8 61DSPATR(HI)
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R LEIFEEMS0                 TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(LEIFEEMS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A*
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 14'LEFEEMR'
     A                                  1 26'INVALID FEE MAIN-
     A                                      TENANCE'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Sel'
     A                                  4  6'Front Office Id.'
     A                                  4 27'Cust   Fclty      Loan'
     A                                  4 52'Fee'
     A                                  4 57'Ccy'
     A                                  4 64'Amount'
     A*
     A                                  5 50'Seq Code'
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R LEIFEEMF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A*
     A* -------------------------------------------------------------------*
     A* * END MESSAGE                                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R LEIFEEMEND                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
     A                                      PUTOVR
     A*
     A            DDUSER    R        O  1  2REFFLD(USER)
     A            DDWSID    R        O  2  2REFFLD(JOJOB)
     A                                  1 14'LEFEEMRPB'
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  1 26'INVALID FEE MAIN-
     A                                      TENANCE'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            DDRUNA    R        O  1 73REFFLD(RUNA) OVRDTA
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
     A            DDFOTX        54A    02 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
