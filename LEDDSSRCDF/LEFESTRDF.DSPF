/*STDA*  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas LE Fee Settlement Repair Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module.                             *
      *                                                               *
      *  LEFESTRDF - Fee Settlement Repair Browse                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CAP072  *CREATE    Date 27Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP072 - Conversion of LE inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R LEIFESTS1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A            S#OPT          1A  B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            S#ACTN         1A  O  8  6DSPATR(HI)
     A            S#CSSN        10A  O  8 11DSPATR(HI)
     A                               O  8 22'-'
     A            S#FACT         3A  O  8 24DSPATR(HI)
     A                               O  8 28'-'
     A            S#FCNO         2A  O  8 31DSPATR(HI)
     A            S#FSEQ         2A  O  8 46
     A            S#VDAT         6A  O  8 56
     A            S#SADJ        14A  O  8 67
     A            @XREPN         5S 0H
     A            @XTMST          Z  H
     A            S#TYPE         1A  H
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R LEIFESTS0                 TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(LEIFESTS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            S#SFRN         4S 0H      SFLRCDNBR
     A*
     A            S#USER    R        O  1  2REFFLD(USER)
     A            S#WSID    R        O  2  2REFFLD(JOJOB)
     A                                  1 14'LEFESTRPB'
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            S#RUNA    R        O  1 72REFFLD(RUNA)
     A                                  3 24'INVALID FEE SETTLEMENT MAIN-
     A                                      TENANCE'
     A                                  4 37'ENQUIRY'
     A                                  6  1'Sel Act Customer  Facility Type/N-
     A                                      umber  Fee Sequence  Value Date    -
     A                                      Amount'
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R LEIFESTF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A*
     A* -------------------------------------------------------------------*
     A* * END MESSAGE                                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R LEIFESTEND                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
     A                                      PUTOVR
     A*
     A            S#USER    R        O  1  2REFFLD(USER)
     A            S#WSID    R        O  2  2REFFLD(JOJOB)
     A                                  1 14'LEFESTRPB'
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            S#RUNA    R        O  1 72REFFLD(RUNA)
     A                                  3 24'INVALID FEE SETTLEMENT MAIN-
     A                                      TENANCE'
     A                                  4 37'ENQUIRY'
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
     A            S#FOTX        40A    24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
