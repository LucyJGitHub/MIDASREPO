      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Funding Participant Display')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LEFPRTDDF - Funding Participant Display                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CAP068  *CREATE    Date 20Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP068 - Conversion of LE inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A*
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A*=========================================================================
     A          R LEFPRTF2                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      OVERLAY
     A                                      KEEP
     A            S#USER    R        O  1  2REFFLD(USER)
     A            S#WSID    R        O  2  2REFFLD(JOJOB)
     A                                  1 14'LEFPRTD'
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A            S#RUNA    R        O  1 72REFFLD(RUNA)
     A                                  2 29'FUNDING PARTICIPANT DETAILS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*=========================================================================
     A          R LEFPRTS1                  SFL
     A  35                                  SFLNXTCHG
     A            S#SEL          1A  B 12  3
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A            S#CNUM         6A  O 12  8DSPATR(HI)
     A            S#CSNM        10A  O 12 16DSPATR(HI)
     A            S#FACT         3A  O 12 28DSPATR(HI)
     A            S#FCNO         2A  O 12 33DSPATR(HI)
     A            S#JDTE         6A  O 12 39DSPATR(HI)
     A            S#VDTC         6A  O 12 48DSPATR(HI)
     A            S#SHTP         1A  O 12 59DSPATR(HI)
     A            S#SHAR        16A  O 12 63DSPATR(HI)
     A*=========================================================================
     A          R LEFPRTC1                  SFLCTL(LEFPRTS1)
     A                                      SFLSIZ(0045)
     A                                      SFLPAG(0009)
     A N66                                  CF09(09 'Add')
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A  30                                  SFLDSP
     A  31                                  SFLDSPCTL
     A N31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A            ##SFRC         4S 0H      SFLRCDNBR(CURSOR)
     A                                 10  2'Sel'
     A                                  4  2'Agent Customer    '
     A                                  5  2'Primary Facility'
     A            S#PMFC         6A  O  5 22DSPATR(HI)
     A                                  5 29'-'
     A            S#PMFT         3A  O  5 31DSPATR(HI)
     A                                  5 35'-'
     A            S#PMFN         2A  O  5 37DSPATR(HI)
     A                                  5 66'Currency'
     A            S#FCCY         3A  O  5 76DSPATR(HI)
     A                                  4 68'Branch'
     A            S#BRCA         3A  O  4 76DSPATR(HI)
     A                                  7 46'Percentage Sold'
     A  65                                  DSPATR(ND)
     A                                  7  2'Actions'
     A                                  8  3'E=Enquire'
     A                                 10  7'---Participant Facility----'
     A                                 11  7'Customer Shortname'
     A                                 11 28'Type No'
     A                                 10 38'Joining'
     A                                 11 40'Date'
     A                                 10 48'Value'
     A                                 11 49'Date'
     A                                 11 57'Type'
     A                                 10 57'---------Share---------'
     A                                 11 63'Amount/Percentage'
     A            S#ANUM         6A  O  4 22DSPATR(HI)
     A            S#CRNM        20A  O  4 30DSPATR(HI)
     A            S#SOLD        16A  O  7 63DSPATR(HI)
     A  65                                  DSPATR(ND)
     A                                  8 17'A=Amend     R=Reverse'
     A  66                                  DSPATR(ND)
     A                                  5 41'Amount:'
     A            S#FAMT        14   O  5 50DSPATR(HI)
     A            S#PRNM        20   O  6 22DSPATR(HI)
     A            S#PRTN        10   O  6 44DSPATR(HI)
     A*=========================================================================
     A          R LEFPRTF1
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 14'F5=Refresh'
     A                                 23 29'F10=Reverse' DSPATR(HI)
     A N68                                  DSPATR(ND)
     A                                 23 44'F12=Previous'
     A                                 23 61'F14=Settlements'
     A N69                                  DSPATR(ND)
     A*=========================================================================
     A          R LEFPRTF3
     A                                      CF12(12 'Previous')
     A  68                                  CF10(10 'Reverse')
     A  69                                  CF14(14 'Settlement')
     A                                      OVERLAY
     A                                      PROTECT
     A                                  4  2'Agent Customer'
     A            S#CRNM        20A  O  4 30DSPATR(HI)
     A            S#ANUM         6A  O  4 22DSPATR(HI)
     A                                  4 68'Branch'
     A            S#BRCA         3A  O  4 76DSPATR(HI)
     A                                  5  2'Primary Facility'
     A            S#PMFC         6A  O  5 22
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A                                  5 29'-'
     A            S#PMFT         3A  O  5 31
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI)
     A                                  5 35'-'
     A            S#PMFN         2A  O  5 37
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A                                  5 41'Amount:'
     A            S#FAMT        14   O  5 50DSPATR(HI)
     A                                  5 66'Currency'
     A            S#FCCY         3A  O  5 76DSPATR(HI)
     A            S#PRNM        20   O  6 22DSPATR(HI)
     A            S#PRTN        10   O  6 44DSPATR(HI)
     A                                  8  3'Member           :'
     A*
     A            S#CSSN        10A  B  8 22
     A  67
     AON09                                  DSPATR(PR)
     A  44                                  DSPATR(RI PC)
     A N67 09                               DSPATR(UL)
     A N44                                  DSPATR(HI)
     A            S#SRNM        20   O  8 34DSPATR(HI)
     A            S#SRTN        10   O  8 56DSPATR(HI)
     A                                  9  3'Facility Type/No.:'
     A            S#FACT         3A  B  9 22
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A N67 09                               DSPATR(UL)
     A  67
     AON09                                  DSPATR(PR)
     A            S#FCNO         2A  O  9 26
     A N46                                  DSPATR(HI)
     A  09                                  DSPATR(ND)
     A  46                                  DSPATR(RI)
     A                                 10  3'Introduced By    :'
     A            S#INUM        10A  B 10 22
     A  67                                  DSPATR(PR)
     A  47                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N47                                  DSPATR(HI)
     A            S#IRNM        20   O 10 34DSPATR(HI)
     A            S#IRTN        10   O 10 56DSPATR(HI)
     A                                 11 22'Joining Date ....... :'
     A            S#JDTE         6A  B 11 46
     A  67
     AON09                                  DSPATR(PR)
     A  48                                  DSPATR(RI PC)
     A N67 09                               DSPATR(UL)
     A N48                                  DSPATR(HI)
     A                                 12 22'Value Date ......... :'
     A            S#VDTC         6A  B 12 46
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(RI PC)
     A N67N09                               DSPATR(UL)
     A  67
     AO 09                                  DSPATR(PR)
     A  09                                  DSPATR(ND)
     A                                 14  3'Participant Share: Amount/Percent-
     A                                       ..... :'
     A            S#SHTP         1A  B 14 46
     A  67                                  DSPATR(PR)
     A  50                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N50                                  DSPATR(HI)
     A                                 15 22'Share .............. :'
     A            S#SHAR        16A  B 15 46
     A  67                                  DSPATR(PR)
     A  51                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N51                                  DSPATR(HI)
     A                                 16 22'Recourse ........... :'
     A            S#RCSI         1A  B 16 46
     A  67                                  DSPATR(PR)
     A  52                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N52                                  DSPATR(HI)
     A                                 17 22'Assign/Participation :'
     A            S#PASI         1A  B 17 46
     A  67                                  DSPATR(PR)
     A  53                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N53                                  DSPATR(HI)
     A                                 18  3'Interest Skim    : Rate ..........-
     A                                      ..... :'
     A            S#SKIR        12A  B 18 46
     A  67                                  DSPATR(PR)
     A  54                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N54                                  DSPATR(HI)
     A                                 19 22'Sign ............... :'
     A            S#SKIS         1A  B 19 46
     A  67                                  DSPATR(PR)
     A  55                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N55                                  DSPATR(HI)
     A                                 20  3'Fees Skim        : Rate ..........-
     A                                      ..... :'
     A            S#SKFR        12A  B 20 46
     A  67                                  DSPATR(PR)
     A  56                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N56                                  DSPATR(HI)
     A                                 21 22'Sign ............... :'
     A            S#SKFS         1A  B 21 46
     A  67                                  DSPATR(PR)
     A  57                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N57                                  DSPATR(HI)
     A                                  6 67'Alt n/a'
     A            S#NACD         1   B  6 76
     A  67                                  DSPATR(PR)
     A N67                                  DSPATR(UL)
     A  58                                  DSPATR(RI PC)
     A N58                                  DSPATR(HI)
     A*=========================================================================
     A          R LEFPRTF4
     A                                      OVERLAY
     A                                 13 34'NO RECORDS FOUND'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A*=========================================================================
     A          R LEFPRTF5
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 14'F5=Refresh'
     A                                 23 29'F9=Add'
     A  66                                  DSPATR(ND)
     A                                 23 36'F12=Previous'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A  25                                  SFLDSP
     A  25                                  SFLDSPCTL
     A  25                                  SFLINZ
     A  25                                  SFLCLR
     A  25                                  SFLEND
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
