      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Rollover Instructions Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending API Module                          *
      *                                                               *
      *  LELERIBDF - Rollover Instructions Browse                     *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAP076  *CREATE    Date 05Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP076 - Lending API enhancements - Rollover details         *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
     A* TRANSACTION NUMBER SUBFILE
      * -------------------------------------------------------------------*
      *
     A          R LELERIS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A            DDOPT          1   B  8  3
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDLOAN         6A  O  8  6
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCNUM         6A  O  8 13
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCSNM        10A  O  8 20
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSRDT         7A  O  8 31
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSNRD         7A  O  8 39
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSRFQ         1A  O  8 48
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSRDY         2A  O  8 51
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSRST        16A  O  8 60
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
      *
      * -------------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                               *
      * -------------------------------------------------------------------*
      *
     A          R LELERIC1                  SFLCTL(LELERIS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'LELERIB'
     A                                  1 26'            Midas            '
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 25'Rollover Instructions Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  2'Sel'
     A                                  5  7'Loan'
     A                                  5 17'Customer'
     A                                  5 32'Roll'
     A                                  6 32'Date'
     A                                  5 40'Next'
     A                                  6 40'Roll'
     A                                  5 46'Freq/Dayno'
     A                                  5 61'Status'
      *
      * -------------------------------------------------------------------*
     A* FRONT OFFICE ID SUBFILE
      * -------------------------------------------------------------------*
      *
     A          R LELERIS2                  TEXT('SELECT SCREEN SUBFILE')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDFOTRANID    20A  O  8  5
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDLOAN         6A  O  8 26
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCNUM         6A  O  8 33
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSRDT         7A  O  8 41
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSNRD         7A  O  8 49
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSRFQ         1A  O  8 58
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSRDY         2A  O  8 61
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSRST        16A  O  8 64
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
      *
      * -------------------------------------------------------------------*
     A* * FRONT OFFICE ID SUBFILE CONTROL                                  *
      * -------------------------------------------------------------------*
      *
     A          R LELERIC2                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLCTL(LELERIS2)
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'LELERIB'
     A                                  1 26'            Midas            '
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 25'Rollover Instructions Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  2'Sel'
     A                                  5  6'Front Office Id.'
     A                                  5 26'Loan'
     A                                  5 32'Customer'
     A                                  5 42'Roll'
     A                                  6 42'Date'
     A                                  5 50'Next'
     A                                  6 50'Roll'
     A                                  5 56'Freq/Dayno'
     A                                  5 69'Status'
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R LELERIF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
