      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Rollover Instructions - Detail screen')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LELERIDDF - Rollover Instructions - Detail Screen            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG2579            Date 07Jun04               *
      *  Prev Amend No. BUG827             Date 06May04               *
      *                 CAP084             Date 12Jan04               *
      *                 CAP076  *CREATE    Date 10Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG2579 - Missing CLE023 changes from LE2070.                *
      *            Lending Facility History Improvements.             *
      *  BUG827 - Missing CASxxx changes from LE2070.                 *
      *           CAS001 - Net Present Value (NPV) Accounting.        *
      *  CAP084 - API wrapper project.                                *
      *           Changed error indicators due to change in LEELERIPD *
      *  CAP076 - Lending API enhancements - Rollover details         *
      *                                                               *
      *****************************************************************
      *
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
     A          R LELERIS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R LELERIC1                  TEXT('Program messages')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  74                                  CF14
     A  75                                  CF15
     A  77                                  CF16
     A                                      SFLCTL(LELERIS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R LELERID1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  74                                  CF14
     A  75                                  CF15
     A  77                                  CF16
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'LELERI1'
      *                                                                                       CAP084
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                  2 30'Rollover Instructions Input'
     A                                      DSPATR(HI UL)
      *                                                                                       CAP084
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  3  2'Action'
     A            DDACTN         1   B  3 10TEXT('ACTION CODE')
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  68
     AO 10N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                  3 15'Loan no'
     A            DDLNRF         6   B  3 24
     A  11                                  DSPATR(RI)
     A N11                                  DSPATR(HI)
     A  11N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                  3 54'Status'
     A            DDRSTS        16   O  3 62DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  4 15'Customer'
     A            DDCUST        10   B  4 24
     A**Start of change CAP084
     A**13******                            DSPATR(RI PC)                                     CAP084
     A*N13******                            DSPATR(HI)                                        CAP084
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A**End of change CAP084
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                  6  2'Rollover date'
     A            DDRLDT         6   O  6 24
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A**70*14***                            DSPATR(RI PC)                                     CAP084
     A  78                                  DSPATR(HI)
     A  78 13                               DSPATR(RI)
     A**End of change CAP084
     A                                      OVRDTA
     A            DDRLDTB        6   B  6 31
     A**Start of change CAP084
     A**14******                            DSPATR(RI PC)                                     CAP084
     A*N14******                            DSPATR(HI)                                        CAP084
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A**End of change CAP084
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  6 39'Fix Days'
     A N63                                  DSPATR(ND)
     A                                      OVRATR
     A            DDRFDY         2   O  6 48DSPATR(HI)
     A N63                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                  7 39'Rate fixed at'
     A N63
     AON64                                  DSPATR(ND)
     A                                      OVRATR
     A            DDRFRT        12   O  7 54DSPATR(HI)
     A N63
     AON64                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                  6 54'Freq/day no'
     A            DDRLFQ         1   O  6 67
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A                                      OVRDTA
     A                                  6 69'/'
     A            DDRLDY         2   O  6 71
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A                                      OVRDTA
     A            DDRLFQB        1   B  6 75
     A**Start of change CAP084
     A**15******                            DSPATR(RI PC)                                     CAP084
     A*N15******                            DSPATR(HI)                                        CAP084
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A**End of change CAP084
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  6 77'/'
     A  70                                  DSPATR(ND)
     A                                      OVRATR
     A            DDRLDYB        2   B  6 79
     A**Start of change CAP084
     A**16******                            DSPATR(RI PC)                                     CAP084
     A*N16******                            DSPATR(HI)                                        CAP084
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A**End of change CAP084
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                  7  2'New base rate'
     A            DDNBRA         2   O  7 24
     A                                      OVRDTA
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A**70*17***                            DSPATR(RI PC)                                     CAP084
     A  78                                  DSPATR(HI)
     A  78 16                               DSPATR(RI)
     A**********  DDNBRAB        2   B  7 27                                                  CAP084
     A**17******                            DSPATR(RI PC)                                     CAP084
     A*N17******                            DSPATR(HI)                                        CAP084
     A            DDNBRAB        2   B  7 27
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A**End of change CAP084
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                  8  2'New spread/rate'
     A            DDNRTS        12   O  8 24
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A                                      OVRDTA
     A            DDNRTSB       12   B  8 39
     A**Start of change CAP084
     A**18******                            DSPATR(RI PC)                                     CAP084
     A*N18******                            DSPATR(HI)                                        CAP084
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A**End of change CAP084
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                  8 54'New spread ind'
     A            DDNSIN         1   O  8 69
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A                                      OVRDTA
     A            DDNSINB        1   B  8 71
     A**Start of change CAP084
     A**19******                            DSPATR(RI PC)                                     CAP084
     A*N19******                            DSPATR(HI)                                        CAP084
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A**End of change CAP084
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                  9  2'New int calc basis'
     A            DDNCAS         1   O  9 24
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A                                      OVRDTA
     A            DDNCASB        1   B  9 26
     A**Start of change CAP084
     A**20******                            DSPATR(RI PC)                                     CAP084
     A*N20******                            DSPATR(HI)                                        CAP084
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A**End of change CAP084
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                  9 39'New change ind'
     A            DDNCHI         1   O  9 54
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A                                      OVRDTA
     A            DDNCHIB        1   B  9 56
     A**Start of change CAP084
     A**21******                            DSPATR(RI PC)                                     CAP084
     A*N21******                            DSPATR(HI)                                        CAP084
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A**End of change CAP084
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                 10  2'Next Rollover date'
     A            DDNROD         6   O 10 24
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A**70*22***                            DSPATR(RI PC)                                     CAP084
     A  78                                  DSPATR(HI)
     A  78 21                               DSPATR(RI)
     A**End of change CAP084
     A                                      OVRDTA
     A            DDNRODB        6   B 10 31
     A**Start of change CAP084
     A**22******                            DSPATR(RI PC)                                     CAP084
     A*N22******                            DSPATR(HI)                                        CAP084
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A**End of change CAP084
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *                                                                                       CAP084
     A**Start of change BUG827
     A                                 10 39'New Fwd Yield Curve'
     A N65                                  DSPATR(ND)
     A                                      OVRATR
     A            DDFYLD         5   O 10 60
     A  78                                  DSPATR(HI)
     A N65                                  DSPATR(ND)
     A                                      OVRDTA
     A            DDFYLDB        5   B 10 66
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N65                                  DSPATR(ND)
     A                                      OVRDTA
     A**End of change BUG827
     A                                 11  2'New principal'
     A            DDNPRAM       14   O 11 24
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A                                      OVRDTA
     A            DDNPRAB       14   B 11 39
     A**Start of change CAP084
     A**23******                            DSPATR(RI PC)                                     CAP084
     A*N23******                            DSPATR(HI)                                        CAP084
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A**End of change CAP084
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                 12  2'New discount amount'
     A            DDNDAM        14   O 12 24
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A                                      OVRDTA
     A            DDNDAMB       14   B 12 39
     A**Start of change CAP084
     A**24******                            DSPATR(RI PC)                                     CAP084
     A*N24******                            DSPATR(HI)                                        CAP084
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A**End of change CAP084
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                 13  2'New interest date'
     A N60                                  DSPATR(ND)
     A                                      OVRATR
     A            DDNIPD         6   O 13 24
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A N60                                  DSPATR(ND)
     A                                      OVRDTA
     A            DDNIPDB        6   B 13 31
     A**Start of change CAP084
     A**25******                            DSPATR(RI PC)                                     CAP084
     A*N25******                            DSPATR(HI)                                        CAP084
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A**End of change CAP084
     A N60
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N60                                  DSPATR(ND)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                 13 39'New Maturity date'
     A N60                                  DSPATR(ND)
     A                                      OVRATR
     A            DDNMDT         6   O 13 57
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A N60                                  DSPATR(ND)
     A                                      OVRDTA
     A            DDNMDTB        6   B 13 64
     A**Start of change CAP084
     A**26******                            DSPATR(RI PC)                                     CAP084
     A*N26******                            DSPATR(HI)                                        CAP084
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A**End of change CAP084
     A N60
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N60                                  DSPATR(ND)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                 14  2'New currency'
     A N61                                  DSPATR(ND)
     A                                      OVRATR
     A            DDNCCY         3   O 14 24
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A N61                                  DSPATR(ND)
     A                                      OVRDTA
     A            DDNCCYB        3   B 14 29
     A**Start of change CAP084
     A**27******                            DSPATR(RI PC)                                     CAP084
     A*N27******                            DSPATR(HI)                                        CAP084
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A**End of change CAP084
     A N61
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N61                                  DSPATR(ND)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                 14 34'New ccy amount'
     A N61                                  DSPATR(ND)
     A                                      OVRATR
     A            DDNCPA        14   O 14 49
     A N61                                  DSPATR(ND)
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A                                      OVRDTA
     A            DDNCPAB       14   B 14 64
     A**Start of change CAP084
     A**28******                            DSPATR(RI PC)                                     CAP084
     A*N28******                            DSPATR(HI)                                        CAP084
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A**End of change CAP084
     A N61
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N61                                  DSPATR(ND)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                 15  2'Fac ccy new exch rate'
     A**Start of change BUG2579
     A*N61******                            DSPATR(ND)                                       BUG2579
     A N61N67                               DSPATR(ND)
     A**End of change BUG2579
     A                                      OVRATR
     A            DDNFCE        14   O 15 24
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A**Start of change BUG2579
     A*N61******                            DSPATR(ND)                                       BUG2579
     A N61N67                               DSPATR(ND)
     A**End of change BUG2579
     A                                      OVRDTA
     A            DDNFCEB       14   B 15 39
     A**Start of change CAP084
     A**29******                            DSPATR(RI PC)                                     CAP084
     A*N29******                            DSPATR(HI)                                        CAP084
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A**End of change CAP084
     A**Start of change BUG2579
     A*N61******                                                                             BUG2579
     A N61N67
     A**End of change BUG2579
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A**Start of change BUG2579
     A*N61******                            DSPATR(ND)                                       BUG2579
     A N61N67                               DSPATR(ND)
     A**End of change BUG2579
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                 15 55'Fac exch rate ind'
     A**Start of change BUG2579
     A*N61******                            DSPATR(ND)                                       BUG2579
     A N61N67                               DSPATR(ND)
     A**End of change BUG2579
     A                                      OVRATR
     A            DDNFMD         1   O 15 74
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A**Start of change BUG2579
     A*N61******                            DSPATR(ND)                                       BUG2579
     A N61N67                               DSPATR(ND)
     A**End of change BUG2579
     A                                      OVRDTA
     A            DDNFMDB        1   B 15 76
     A**Start of change CAP084
     A**30******                            DSPATR(RI PC)                                     CAP084
     A*N30******                            DSPATR(HI)                                        CAP084
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI)
     A**End of change CAP084
     A**Start of change BUG2579
     A*N61******                                                                             BUG2579
     A N61N67
     A**End of change BUG2579
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A**Start of change BUG2579
     A*N61******                            DSPATR(ND)                                       BUG2579
     A N61N67                               DSPATR(ND)
     A**End of change BUG2579
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                 16  2'Bas ccy new exch rate'
     A N61                                  DSPATR(ND)
     A                                      OVRATR
     A            DDNBCE        14   O 16 24
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A N61                                  DSPATR(ND)
     A                                      OVRDTA
     A            DDNBCEB       14   B 16 39
     A**Start of change CAP084
     A**31******                            DSPATR(RI PC)                                     CAP084
     A*N31******                            DSPATR(HI)                                        CAP084
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A**End of change CAP084
     A N61
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N61                                  DSPATR(ND)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                 16 55'Base exch rate ind'
     A N61                                  DSPATR(ND)
     A                                      OVRATR
     A            DDNBMD         1   O 16 74
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A N61                                  DSPATR(ND)
     A                                      OVRDTA
     A            DDNBMDB        1   B 16 76
     A**Start of change CAP084
     A**32******                            DSPATR(RI PC)                                     CAP084
     A*N32******                            DSPATR(HI)                                        CAP084
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A**End of change CAP084
     A N61
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N61                                  DSPATR(ND)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                 17  2'Next loan currency +
     A                                      Repayment amount'
     A N61                                  DSPATR(ND)
     A                                      OVRATR
     A            DDNLRA        14   O 17 39
     A**Start of change CAP084
     A**70******                            DSPATR(HI)                                        CAP084
     A  78                                  DSPATR(HI)
     A**End of change CAP084
     A N61                                  DSPATR(ND)
     A                                      OVRDTA
     A            DDNLRAB       14   B 17 54
     A**Start of change CAP084
     A**33******                            DSPATR(RI PC)                                     CAP084
     A*N33******                            DSPATR(HI)                                        CAP084
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A**End of change CAP084
     A N61
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N61                                  DSPATR(ND)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                 18  2'Special instructions'
     A            DDSPI1        30   B 19  2
     A**Start of change CAP084
     A**34******                            DSPATR(RI PC)                                     CAP084
     A*N34******                            DSPATR(HI)                                        CAP084
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A**End of change CAP084
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDSPI2        30   B 20  2
     A**Start of change CAP084
     A**35******                            DSPATR(RI PC)                                     CAP084
     A*N35******                            DSPATR(HI)                                        CAP084
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A**End of change CAP084
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDSPI3        30   B 21  2
     A**Start of change CAP084
     A**36******                            DSPATR(RI PC)                                     CAP084
     A*N36******                            DSPATR(HI)                                        CAP084
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A**End of change CAP084
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 18 39'New Funding Details'
     A N62                                  DSPATR(ND)
     A                                      OVRATR
     A                                 19 39'Spread/rate'
     A N62                                  DSPATR(ND)
     A                                      OVRATR
     A**Start of change CAP084
     A**********  DDNFRSB       12   B 19 53                                                  CAP084
     A**37******                            DSPATR(RI PC)                                     CAP084
     A*N37******                            DSPATR(HI)                                        CAP084
     A            DDNFRS        12   O 19 53
     A  78                                  DSPATR(HI)
     A N62                                  DSPATR(ND)
     A                                      OVRDTA
     A            DDNFRSB       12   B 19 66
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A**End of change CAP084
     A N62
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N62                                  DSPATR(ND)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                 20 39'Spread sign'
     A N62                                  DSPATR(ND)
     A                                      OVRATR
     A**Start of change CAP084
     A**********  DDNFSNB        1   B 20 53                                                  CAP084
     A**38******                            DSPATR(RI PC)                                     CAP084
     A*N38******                            DSPATR(HI)                                        CAP084
     A            DDNFSN         1   O 20 53
     A  78                                  DSPATR(HI)
     A N62                                  DSPATR(ND)
     A                                      OVRDTA
     A            DDNFSNB        1   B 20 55
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A**End of change CAP084
     A N62
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N62                                  DSPATR(ND)
     A                                      OVRDTA
      *                                                                                       CAP084
     A                                 21 39'Profit centre'
     A N62                                  DSPATR(ND)
     A                                      OVRATR
     A**Start of change CAP084
     A**********  DDNFPCB        4   O 21 53                                                  CAP084
     A            DDNFPC         4   O 21 53
     A**End of change CAP084
     A                                      DSPATR(HI)
     A N62                                  DSPATR(ND)
     A                                      OVRDTA
      *
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R LELERIF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  74                                  CF14
     A  75                                  CF15
     A  77                                  CF16
     A                                      OVERLAY
     A                                      BLINK
     A            DDFOTX        40A    23  2
     A                                      DSPATR(HI)
     A                                 24  2'F3=Exit F12=Cancel'
     A                                 24 21'F8=Next'
     A N72                                  DSPATR(ND)
     A                                 24 29'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                 24 38'F14=Settlements'
     A N74                                  DSPATR(ND)
     A                                 24 54'F15=Select'
     A N75                                  DSPATR(ND)
     A                                 24 65'F16=Authorise'
     A N77                                  DSPATR(ND)
