      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Loan amendments subfile browse')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LELOAMBDF - Invalid Loan Amendment details browse            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAP075  *CREATE    Date 30Aug02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP075 - Lending API enhancements - Loan Amendment           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(22)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * Message subfile                                                  *
      * -------------------------------------------------------------------*
      *
     A          R LELOAMM1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            S#SFKY                    SFLMSGKEY
     A            S#PGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   Message subfile control                                          *
      * -------------------------------------------------------------------*
      *
     A          R LELOAMM0                  TEXT('Message subfile CTL')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(LELOAMM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A            S#PGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * Loan amendments subfile
      * -------------------------------------------------------------------*
     A          R LELOAMS1                  SFL
     A                                      TEXT('Select screen subfile')
     A  40                                  SFLNXTCHG
     A            S#OPT          1A  B  8  2
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
     A            S#LOAN         6A  O  8  5DSPATR(HI)
     A            S#CUST         6   O  8 12DSPATR(HI)
     A            S#CSNM        10   O  8 19DSPATR(HI)
     A            S#ATXT        19   O  8 30DSPATR(HI)
     A            S#SEQN         3   O  8 50DSPATR(HI)
     A            S#VDTA         7   O  8 54DSPATR(HI)
     A            S#STAT        16   O  8 62DSPATR(HI)
     A            S#VDAT         6   H
     A            S#ATYP         2   H
     A            S#RECN         4S 0H
      *
      * -------------------------------------------------------------------*
      * Loan amendments subfile control
      * -------------------------------------------------------------------*
     A          R LELOAMS0                  SFLCTL(LELOAMS1)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      TEXT('Select screen control')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND(*MORE)
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            S#SFRN         4S 0H      SFLRCDNBR
     A            S#USER        10A  O  1  2OVRDTA
     A                                  1 13'LELOAMB'
     A                                  1 66'Date:'
     A            S#RUNA         7A  O  1 73OVRDTA
     A            S#WID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 31'Select Loan Amendments'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A            S#TIME         6  0   2 72
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
 
     A                                  4 59'Unauthorised Only?'
     A N29                                  DSPATR(ND)
     A            S#RAUT         1   B  4 78OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI UL)
     A N29                                  DSPATR(ND PR)
     A N60                              4  1'E=Enquire'
     A N60                              4 12'A=Amend'
     A N60                              4 21'R=Reverse'
     A  29N60                           4 32'X=Authorise'
     A  28N60                           4 45'Q=Release'
 
     A                                  6  1'Sel'
     A                                  6  5'Loan'
     A                                  6 12'Customer'
     A                                  6 30'Amendment type'
     A                                  6 50'Seq'
     A                                  6 54'Value'
     A                                  6 62'Status'
     A            SLLOAN         6A  B  7  5OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A                                      OVRATR
      *
      * -------------------------------------------------------------------*
      * Front office Id subfile
      * -------------------------------------------------------------------*
      *
     A          R LELOAMS3                  SFL
     A                                      TEXT('Select screen subfile')
     A  40                                  SFLNXTCHG
     A            S#OPT          1A  B  8  2
     A N40                                  DSPATR(UL HI)
     A  40                                  DSPATR(RI PC)
     A            S#FRNT        20A  O  8  5DSPATR(HI)
     A            S#LOAN         6A  O  8 26DSPATR(HI)
     A            S#VDTA         7A  O  8 33DSPATR(HI)
     A            S#SEQN         3A  O  8 41DSPATR(HI)
     A            S#ATXT        19A  O  8 45DSPATR(HI)
     A            S#STAT        16A  O  8 65DSPATR(HI)
     A            S#VDAT         6   H
     A            S#ATYP         2   H
     A            S#RECN         4S 0H
      *
      * -------------------------------------------------------------------*
      * Front office Id control
      * -------------------------------------------------------------------*
     A          R LELOAMS2                  SFLCTL(LELOAMS3)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      TEXT('Select screen control')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND(*MORE)
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            S#SFRN         4S 0H      SFLRCDNBR
     A            S#USER        10A  O  1  2OVRDTA
     A                                  1 13'LELOAMB'
     A                                  1 66'Date:'
     A            S#RUNA         7A  O  1 73OVRDTA
     A            S#WID         10A  O  2  2OVRDTA
     A                                  2 31'Select Loan Amendments'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A            S#TIME         6  0   2 72
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
 
     A                                  4 59'Unauthorised Only?'
     A N29                                  DSPATR(ND)
     A            S#RAUT         1   B  4 78OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI UL)
     A N29                                  DSPATR(ND PR)
     A N60                              4  1'E=Enquire'
     A N60                              4 12'A=Amend'
     A N60                              4 21'R=Reverse'
     A  29N60                           4 32'X=Authorise'
     A  28N60                           4 45'Q=Release'
 
     A                                  6  1'Sel'
     A                                  6  5'Front Office ID'
     A                                  6 26'Loan'
     A                                  6 33'Value'
     A                                  6 41'Seq'
     A                                  6 45'Amendment type'
     A                                  6 65'Status'
     A            SLFRNT        20   B  7  5OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A                                      OVRATR
      *
      * -------------------------------------------------------------------*
      * No records format - Loan key
      * -------------------------------------------------------------------*
     A          R LELOAMS4
     A                                      TEXT('No records screen')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A            S#USER        10A  O  1  2
     A                                  1 13'LELOAMB'
     A                                  1 66'Date:'
     A            S#RUNA         7A  O  1 73
     A            S#WID         10A  O  2  2
     A                                  2 31'Select Loan Amendments'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A            S#TIME         6  0   2 72
     A                                      EDTWRD('  :  :  ')
 
     A                                  4 59'Unauthorised Only?'
     A N29                                  DSPATR(ND)
     A            S#RAUT         1   B  4 78
     A                                      DSPATR(HI UL)
     A N29                                  DSPATR(ND PR)
     A N60                              4  1'E=Enquire'
     A N60                              4 12'A=Amend'
     A N60                              4 21'R=Reverse'
     A  29N60                           4 32'X=Authorise'
     A  28N60                           4 45'Q=Release'
 
     A                                  6  1'Sel'
     A                                  6  5'Loan'
     A                                  6 12'Customer'
     A                                  6 30'Amendment type'
     A                                  6 50'Seq'
     A                                  6 54'Value'
     A                                  6 62'Status'
     A            SLLOAN         6A  B  7  5DSPATR(UL HI)
      *
      * -------------------------------------------------------------------*
      * No records format - FO ID
      * -------------------------------------------------------------------*
     A          R LELOAMS5
     A                                      TEXT('No records screen')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A            S#USER        10A  O  1  2
     A                                  1 13'LELOAMB'
     A                                  1 66'Date:'
     A            S#RUNA         7A  O  1 73
     A            S#WID         10A  O  2  2
     A                                  2 31'Select Loan Amendments'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A            S#TIME         6  0   2 72
     A                                      EDTWRD('  :  :  ')
 
     A                                  4 59'Unauthorised Only?'
     A N29                                  DSPATR(ND)
     A            S#RAUT         1   B  4 78
     A                                      DSPATR(HI UL)
     A N29                                  DSPATR(ND PR)
 
     A N60                              4  1'E=Enquire'
     A N60                              4 12'A=Amend'
     A N60                              4 21'R=Reverse'
     A  29N60                           4 32'X=Authorise'
     A  28N60                           4 45'Q=Release'
 
     A                                  6  1'Sel'
     A                                  6  5'Front Office ID'
     A                                  6 27'Loan'
     A                                  6 35'Value'
     A                                  6 44'Seq'
     A                                  6 49'Amendment type'
     A            SLFRNT        20   B  7  5DSPATR(UL HI)
      *
      * -------------------------------------------------------------------*
      * Select screen footer                                               *
      * -------------------------------------------------------------------*
     A          R LELOAMF1                  TEXT('Select screen footer')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CLRL(23)
     A     81                          23  2'Roll up/down for further records'
     A     81                               DSPATR(HI) OVRATR
     A     80                               DSPATR(ND)
     A     80                          23  2'No records to display'
     A     80                               DSPATR(HI) OVRATR
     A     81                               DSPATR(ND)
     A                                 24  2'F3=Exit  F5=Refresh  F9=Insert'
     A            TGLTXT        30   O 24 34OVRDTA
     A                                 24 66'F12=Previous'
