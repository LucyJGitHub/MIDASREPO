      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Loan amendments detail screen')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LELOAMDDF - Loan amendments detail Screen                    *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE042             Date 06Sep05               *
      *                 CAP075  *CREATE    Date 28Aug02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CAP075 - Lending API enhancements - Loan amendments          *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      * *------------------------------------------------------------------*
      * * Message subfile format                                           *
      * *------------------------------------------------------------------*
      *
     A          R LELOAMS1                  SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2002')
      *
     A                                      SFLMSGRCD(22)
      *
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ(10)
      *
      * *------------------------------------------------------------------*
      * * Message subfile control                                          *
      * *------------------------------------------------------------------*
      *
                R LELOAMC1                  SFLCTL(LELOAMS1)
     A                                      TEXT('Program messages')
      *
     A                                      BLINK
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      OVERLAY PUTOVR
     A  97
     AON97                                  SFLEND
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
      *
     A            DDPGMQ                    SFLPGMQ(10)
      *
      * *------------------------------------------------------------------*
      * * Main display screen                                              *
      * *------------------------------------------------------------------*
      *
     A          R LELOAMD1
      *
     A                                      CF03
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A                                      CF12
     A  74                                  CF14
     A  75                                  CF15
     A  77                                  CF16
      *
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'LELOAMD1'
     A                                  1 66'Date:'
     A            S#RUNA         7A  O  1 73OVRDTA
      *
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 31'Loan Amendment Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A            S#TIME         6  0   2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  4  2'Action'
     A            DDACTN         1A  B  4  9OVRDTA
     A  10                                  DSPATR(RI)
     A  10
     AO 68N69                               DSPATR(PC)
     A N10                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                  4 12'Loan No'
     A            DDLOAN         6A  B  4 23OVRDTA
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                  4 33'Amendment Type'
     A            DDATYP         2A  B  4 48OVRDTA
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                  4 54'Status'
     A            DDSTAT        16A  O  4 61OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  5 12'Value Date'
     A            DDVDAT         6A  B  5 23OVRDTA
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                  5 61'**REVERSED LOAN**' OVRATR
     A  63                                  DSPATR(HI RI)
     A N63                                  DSPATR(ND)
      *
     A                                  6 12'Sequence'
     A            DDSEQN         3A  B  6 23OVRDTA
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
      *
     A                                  9  2'Customer'
     A            DDCNUM        10A  B  9 23OVRDTA
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *
     A                                  9 42'Type/Subtype' OVRATR
     A N60                                  DSPATR(ND)
     A            DDLTYP         2A  O  9 62OVRDTA
     A                                      DSPATR(HI)
     A N60                                  DSPATR(ND)
     A            DDSTYP         2A  B  9 65OVRDTA
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A N60
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N60                                  DSPATR(ND)
      *
      *                                              Start /CLE042/
     A            DDCLAS         4A  B  9 68OVRDTA
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A N60
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N60                                  DSPATR(ND)
      *
      *                                              End   /CLE042/
     A                                 10 42'Maturity Date' OVRATR
     A N60                                  DSPATR(ND)
     A            DDMDAT         6A  B 10 62OVRDTA
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A N60
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N60                                  DSPATR(ND)
      *
     A                                 11  2'Base Rate'
     A            DDBRAT         2A  B 11 23OVRDTA
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *
     A                                 12  2'Rate/Spread'
     A            DDRTSP        12A  B 12 23OVRDTA
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                 12 42'Funding Spread/Rate' OVRATR
     A N61                                  DSPATR(ND)
     A            DDFRAT        12A  B 12 62OVRDTA OVRATR
     A N61                                  DSPATR(ND)
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A N61
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *
     A                                 13  2'Currency'
     A            DDCURR         3A  B 13 23OVRDTA
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *
     A                                 14  2'Amount'
     A            DDAMNT        14A  B 14 23OVRDTA
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *
     A                                 14 42'Default Participation Loan:'
     A                                      OVRATR
     A N62                                  DSPATR(ND)
      *
     A                                 15  2'Stop Accruals'
     A            DDSTPA         1A  B 15 23OVRDTA
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                 15 42'Type/Subtype' OVRATR
     A N62                                  DSPATR(ND)
     A  62        DDDFTP         2A  B 15 62OVRDTA
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  62        DDDFST         2A  B 15 65OVRDTA
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *                                              Start /CLE042/
     A  62        DDDFCL         4A  B 15 68OVRDTA
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *                                              End   /CLE042/
      *
     A                                 17  2'Special Instructions'
     A            DDSPI1        30A  B 17 23OVRDTA
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
      *
     A            DDSPI2        30A  B 18 23OVRDTA
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
      *
     A            DDSPI3        30A  B 19 23OVRDTA
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
      *
      * *------------------------------------------------------------------*
      * * Main screen footer                                               *
      * *------------------------------------------------------------------*
      *
     A          R LELOAMF1                  TEXT('Screen Footer')
      *
     A                                      OVERLAY
     A                                      BLINK
      *
     A            DDFOTX        40   O 23  2DSPATR(HI)
     A  74                             23 65'F14=Settlements'
      *
     A                                 24  2'F3=Exit F12=Cancel'
     A  71                             24 22'F7=Prev'
     A  72                             24 31'F8=Next'
     A  75                             24 40'F15=Select'
