      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Invalid loan amendments browse')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LELOAMRDF - Invalid loan amendment details browse            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAP075  *CREATE    Date 11Jul02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP075 - Lending API enhancements - loan amendments          *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R LELOAMS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A            DDOPT          1A  B  7  2TEXT('Select option')
     A                                      DSPATR(UL)
     A            DDACTN         1   O  7  6DSPATR(HI)
     A            DDFRNT        20   O  7 10DSPATR(HI)
     A            DDLOAN         6A  O  7 32
     A            DDVDAT         6A  O  7 40
     A            D@ATXT        21A  O  7 49
     A            DDATYP         2A  H
     A            DDAFRT    R        H      REFFLD(FOTRNID)
     A            D@STMP    R     Z  H      REFFLD(TIMESTMP)
     A            DDBRSN    R        H      REFFLD(BRSN)
      *
      * -------------------------------------------------------------------*
      * * Select screen control                                            *
      * -------------------------------------------------------------------*
     A          R LELOAMS0                  SFLCTL(LELOAMS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Select screen control')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'LELOAMRPB'
     A                                  1 24'Invalid Loan +
     A                                      Amendment Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            S#RUNA         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A            S#TIME         6  0   2 72OVRDTA
     A                                      EDTWRD('  :  :  ')
     A                                  4 32'Loan'
     A                                  4 40'Value'
     A                                  4 49'Amendment type'
     A                                  5  1'Sel'
     A                                  5  5'Act'
     A                                  5 10'Front Office Id.'
     A                                  5 32'Number'
     A                                  5 40'Date'
      *
      * -------------------------------------------------------------------*
      * * Select screen footer                                             *
      * -------------------------------------------------------------------*
     A          R LELOAMF1                  TEXT('Select screen footer')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      *
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * End message                                                      *
      * -------------------------------------------------------------------*
     A          R LELOAMEND                 TEXT('End message')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'LELOAMRPB'
     A                                  1 24'Invalid Loan +
     A                                      Amendment Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            S#RUNA         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            S#TIME         6  0   2 72OVRDTA
     A                                      EDTWRD('  :  :  ')
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this Function'
