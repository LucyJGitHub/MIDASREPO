      *****************************************************************
/*S*DA***DSPFBASE******************************************************
/*E*I ***TEXT('Midas*BOE*Customer*Loans*Details*Window')***************
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England Module                               *
      *                                                               *
      *  LELOAN#0 - LE Customer Loans BoE Details Window              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd., 2005           *
      *                                                               *
      *  Last Amend No. MD034876 *RENAME   Date 03Jul15               *
      * Midas Plus 1.4 Base ------------------------------------------*
      *  Prev Amend No. LLN022  *CREATE    Date 24Jan05               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD034876 - BOE Upgrade to FBM2.1 Compatibility Amendments    *
      *  LLN022 - Upgrade BoE to Midas Plus                           *
      *                                                               *
      *****************************************************************
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A                                      CF12(12 'Previous')
     A          R WINDOWF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      1997')
     A                                      WINDOW(&SWROW &SWCOL 9 45)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            L#PGM         10A  H
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A            L#TITL        20A  O  2 12OVRDTA
      *
     A                                  4  2'Type of Loan...........+
     A                                      .........'
     A            L#LNTP         1A  B  4 35OVRDTA
     A N50N20                               DSPATR(HI UL)
     A  50N20                               DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                  5  2'Final Maturity Date....+
     A                                      .........'
     A            L#FMAT         6A  B  5 35OVRDTA
     A N51N20                               DSPATR(HI UL)
     A  51N20                               DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                  7  2'F3=Main Menu'
     A                                  7 17'F12=Previous'
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(8)
     A            MSGKEY                    SFLMSGKEY
     A            L#PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(02)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A                                      WINDOW(WINDOWF)
     A  25
     AON25                                  SFLEND
     A            L#PGM                     SFLPGMQ
