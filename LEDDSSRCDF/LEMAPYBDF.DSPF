      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Manual Repayment Input - Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending  API Module                         *
      *                                                               *
      *  LEMAPYBDF - Manual Repayment Input - Browse                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  LAST AMEND NO. CAP077  *CREATE    Date 12Jul02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP077 - Lending API enhancements - Manual Repayments        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(22)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * Message subfile                                                  *
      * -------------------------------------------------------------------*
      *
     A          R LEMAPYM1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            S#SFKY                    SFLMSGKEY
     A            S#PGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   Message subfile control                                          *
      * -------------------------------------------------------------------*
      *
     A          R LEMAPYM0                  TEXT('Message subfile CTL')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(LEMAPYM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A            S#PGMQ                    SFLPGMQ
      *
      ***  Select screen sub-file 1
      *
     A          R LEMAPYS1                  SFL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2002')
     A  40                                  SFLNXTCHG
     A            DDOPT          1A  B  8  2TEXT('SELECT OPTION')
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
 
     A            DDLNRF         6A  O  8  4DSPATR(HI)
     A            DDCUST         6A  O  8 11DSPATR(HI)
     A            DDCSNM        10A  O  8 18DSPATR(HI)
     A            DDVDAT         6A  O  8 29DSPATR(HI)
     A            DDSEQN         3A  O  8 36DSPATR(HI)
     A            DDSAMT        18A  O  8 40DSPATR(HI)
     A            DDCURR         3A  O  8 59DSPATR(HI)
     A            DDSTAT        15A  O  8 63DSPATR(HI)
     A            DDRECN         4S 0H
      *
      ***  Subfile control record 1
      *
     A          R LEMAPYC1                  SFLCTL(LEMAPYS1)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2
     A                                  1 13'LEMAPYB1'
     A                                  1 26'            Midas            '
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 21'Manual Repayment Input and Authori-
     A                                      sation'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  5  1'Sel'
     A                                  5  5'Loan'
     A                                  5 11'Customer Number'
     A                                  5 29'Value'
     A                                  5 40'Principal Amount'
     A                                  5 59'Ccy'
     A                                  5 63'Status'
     A                                  6  4'Number'
     A                                  6 11'And Shortname'
     A                                  6 29'Date   Seq'
      *
     A            S1LNRF         6A  B  7 05DSPATR(HI UL)
     A                                      DSPATR(ND PR)                       WAIT VALIDATION
      *
      ***  Select screen sub-file 2
      *
     A          R LEMAPYS2                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A  40                                  SFLNXTCHG
     A            DDOPT          1A  B  8  2TEXT('SELECT OPTION')
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
     A            DDFOTRANID    20A  O  8  5DSPATR(HI)
     A            DDLNRF         6A  O  8 27DSPATR(HI)
     A            DDCUST         6A  O  8 35DSPATR(HI)
     A            DDVDAT         6A  O  8 43DSPATR(HI)
     A            DDSEQN         3A  O  8 51DSPATR(HI)
     A            DDSAMT        18A  O  8 56DSPATR(HI)
     A            DDCURR         3A  O  8 75DSPATR(HI)
     A            DDRECN         4S 0H
      *
      ***  Select screen control 2
      *
     A          R LEMAPYC2                  SFLCTL(LEMAPYS2)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2
     A                                  1 13'LEMAPYB2'
     A                                  1 26'            Midas            '
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 21'Manual Repayment Input and Authori-
     A                                      stion'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  1'Sel'
     A                                  5  5'Front Office Id.'
     A                                  5 27'Loan'
     A                                  5 35'Cust.'
     A                                  5 43'Value'
     A                                  5 56'Principal Amount'
     A                                  5 75'Ccy'
     A                                  6 27'Number'
     A                                  6 35'Number'
     A                                  6 43'Date    Seq'
      *
     A            S2FRONTID     20A  B  7 05DSPATR(HI UL)
     A                                      DSPATR(ND PR)                       WAIT VALIDATION
      *
      ***  Select screen sub-file 1 - No records to display
      *
     A          R LEMAPYS3
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A            DDUSER        10A  O  1  2
     A                                  1 13'LEMAPYB1'
     A                                  1 26'            Midas            '
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 21'Manual Repayment Input and Authori-
     A                                      sation'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                  5  1'Sel'
     A                                  5  5'Loan'
     A                                  5 11'Customer Number'
     A                                  5 29'Value'
     A                                  5 40'Principal Amount'
     A                                  5 59'Ccy'
     A                                  5 63'Status'
     A                                  6  4'Number'
     A                                  6 11'And Shortname'
     A                                  6 29'Date   Seq'
      *
      ***  Select screen sub-file 2 - No records to display
      *
     A          R LEMAPYS4
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A            DDUSER        10A  O  1  2
     A                                  1 13'LEMAPYB2'
     A                                  1 26'            Midas            '
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 21'Manual Repayment Input and Authori-
     A                                      stion'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  1'Sel'
     A                                  5  5'Front Office Id.'
     A                                  5 27'Loan'
     A                                  5 35'Cust.'
     A                                  5 43'Value'
     A                                  5 56'Principal Amount'
     A                                  5 75'Ccy'
     A                                  6 27'Number'
     A                                  6 35'Number'
     A                                  6 43'Date    Seq'
      *
      ***  Select screen footer
      *
     A          R LEMAPYF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            TGLTXT2       35   O 23  2OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                 24  2'F3=Exit  F5=Refresh  F9=Insert'
     A            TGLTXT        30   O 24 34OVRDTA
     A                                 24 66'F12=Previous'
