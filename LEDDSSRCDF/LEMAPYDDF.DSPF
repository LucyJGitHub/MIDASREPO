      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Manual Repayment Input - Detail screen')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending  API Module                         *
      *                                                               *
      *  LEMAPYDDF - Manual Repayments input - Detail Screen          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG3114            Date 27Jul04               *
      *  Prev Amend No. CAP077  *CREATE    Date 12Jul02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG3114- Missing Rule of 78s (CLE028) change in the Lending  *
      *           APIs as description in the amendment history.       *
      *  CAP077 - Lending API enhancements - Manual Repayments        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
     A          R LEMAPYS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R LEMAPYC1                  TEXT('Program messages')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A  76                                  CF16
     A                                      SFLCTL(LEMAPYS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MAIN DISPLAY SCREEN                                              *
     A*   -------------------                                              *
     A*   CF03 = Exit                                                      *
     A*   CF12 = Cancel                                                    *
     A*   CF07 = Previous Record                                           *
     A*   CF08 = Next Record                                               *
     A*   CF10 = Confirm                                                   *
     A*   CF14 = Settlement Instructions                                   *
     A*   CF15 = Select Subfile                                            *
     A*   CF16 = Authorise                                                 *
     A*   *IN67 = Protect Customer number when validated                   *
     A*   *IN68 = If no action, Position Cursor on 'Action' field          *
     A*   *IN69 = Loans Key fields are correctly input                     *
     A*   *IN70 = Protect all fields                                       *
     A*   *IN71 = Enable F7                                                *
     A*   *IN72 = Enable F8                                                *
     A*   *IN73 = Enable F10                                               *
     A*   *IN74 = Enable F14                                               *
     A*   *IN75 = Enable F15                                               *
     A*   *IN76 = Enable F16                                               *
     A*   *IN77 = Working with a past due/repayment Scheduled              *
     A*   *IN78 = *77 + Output of principal                                *
     A*   *IN79 = *77 + Output of interest/Withholding tax                 *
     A*   *IN80 = *77 + Output of Total                                    *
     A*   *IN81 = *IN88 + When off ==> Display PD/RS Penalty               *
     A*   *IN82 = enable Write Off                                         *
     A*                                                                    *
     A*   *IN86 = Write off is not amendable                               *
     A*   *IN87 = Loan Proc. type 70 or (PROT=71 and CLE005 activated)     *
     A*   *IN88 = CLE003 is activated                                      *
     A*   *IN89 = CLE007 is activated                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R LEMAPYD1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A  76                                  CF16
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'LEMAPY1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 30'Manual Repayments Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'Action'
     A            DDACTN         1A  B  4 10TEXT('Action code')
     A                                      OVRATR
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  68
     AO 10N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  4 13'Loan no'
     A            DDLNRF         6A  B  4 27
     A                                      OVRATR
     A  11                                  DSPATR(RI)
     A N11                                  DSPATR(HI)
     A  11N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDRSTS        16A  O  4 35DSPATR(HI)
     A                                      OVRDTA
     A                                  5 13'Value Date '
     A            DDVALD         6A  B  5 27
     A                                      OVRATR
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(HI)
     A  13N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  5 35'Seq.'
     A            DDSEQN         3A  B  5 40
     A                                      OVRATR
     A  14                                  DSPATR(RI)
     A N14                                  DSPATR(HI)
     A  14N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  6 13'Expected Date'
     A            DDEVAL         6A  B  6 27
     A                                      OVRATR
     A  15                                  DSPATR(RI)
     A N15                                  DSPATR(HI)
     A  15N69                               DSPATR(PC)
     A  77
     AO 69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  6 35'Seq'
     A                                      OVRATR
     A            DDESEQ         3A  B  6 40
     A                                      OVRATR
     A  16                                  DSPATR(RI)
     A N16                                  DSPATR(HI)
     A  16N69                               DSPATR(PC)
     A  77
     AO 69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  7 13'Customer'
     A            DDCUST        10A  B  7 27
     A                                      OVRATR
     A  17                                  DSPATR(RI)
     A N17                                  DSPATR(HI)
     A  17N67                               DSPATR(PC)
     A  67                                  DSPATR(PR)
     A N67                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9  2'Current Principal'
     A            DDCURP        19A  O  9 24DSPATR(HI)
     A                                      OVRDTA
     A            DDSCCY         3A  O  9 45DSPATR(HI)
     A                                 10  2'O/s Interest to Date'
      ** Start of change BUG3114
     A                                      OVRATR
     A  07                                  DSPATR(ND)
     A**********  DDINTD        18A  O 10 25DSPATR(HI)
     A            DDINTD        18A  O 10 25OVRATR DSPATR(HI)
     A  07                                  DSPATR(ND)
      ** End of change BUG3114
     A                                      OVRDTA
     A                                 10 45'W/hold Tax Type'
     A            DDWTTP         1A  O 10 61DSPATR(HI)
     A                                      OVRDTA
     A                                 10 63'Past Due Y/N'
     A                                      OVRATR
     A N89
     AO 89 77                               DSPATR(ND)
     A            DDPASD         1A  B 10 77
     A                                      OVRATR
     A N89
     AO 89 77                               DSPATR(ND)
     A N22                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      ** Start of change BUG3114
     A                                 11  2'Interest Refunded'
     A                                      OVRATR
     A  08                                  DSPATR(ND)
     A            DDINRF        18A  O 11 25OVRATR DSPATR(HI)
     A  08                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 11 45'Charges Refunded'
     A                                      OVRATR
     A  09                                  DSPATR(ND)
     A            DDCGRF        17A  O 11 63OVRATR DSPATR(HI)
     A  09                                  DSPATR(ND)
     A                                      OVRDTA
      ** End of change BUG3114
     A                                 12  2'Repayment Details : '
     A                                 12 45'Past Due Details :'
     A                                      OVRATR
     A N77                                  DSPATR(ND)
     A                                 14 10'Principal'
     A            DDRPRI        14A  B 14 21
     A                                      OVRATR
     A N23                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            DDPSIG         1A  B 14 36
     A                                      OVRATR
     A N24                                  DSPATR(HI)
     A N87N70                               DSPATR(UL)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A  70
     AO 87                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14 50'Principal'
     A                                      OVRATR
     A  78                                  DSPATR(ND)
     A            DDEPRI        17A  O 14 61OVRATR DSPATR(HI)
     A                                      OVRDTA
     A  78                                  DSPATR(ND)
     A                                 15 10'Interest'
      ** Start of change BUG3114
     A                                      OVRATR
     A  07                                  DSPATR(ND)
      ** End of change BUG3114
     A            DDRINT        14A  B 15 21
     A                                      OVRATR
     A N26                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
      ** Start of change BUG3114
     A**70******                            DSPATR(PR)
     A  07
     AO 70                                  DSPATR(PR)
     A  07                                  DSPATR(ND)
     A                                      OVRDTA
      ** End of change BUG3114
     A            DDISIG         1A  B 15 36
     A                                      OVRATR
     A N27                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
      ** Start of change BUG3114
     A**70******                            DSPATR(PR)
     A  07
     AO 70                                  DSPATR(PR)
     A  07                                  DSPATR(ND)
      ** End of change BUG3114
     A                                      OVRDTA
     A                                 15 50'Interest'
     A                                      OVRATR
     A  79                                  DSPATR(ND)
     A            DDEINT        17A  O 15 61DSPATR(HI) OVRATR
     A                                      OVRDTA
     A  79                                  DSPATR(ND)
     A                                 16 10'Total'
     A            DDRTOT        14A  B 16 21
     A                                      OVRATR
     A N29                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  29                                  DSPATR(RI)
     A  29                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            DDRSIG         1A  B 16 36
     A                                      OVRATR
     A N30                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16 50'Total'
     A                                      OVRATR
     A  80                                  DSPATR(ND)
     A            DDETOT        17A  O 16 61DSPATR(HI)
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 17 50'W/hold Tax'
     A                                      OVRATR
     A  79                                  DSPATR(ND)
     A            DDWHTX        17A  O 17 61DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A  79                                  DSPATR(ND)
     A                                 19 10'Penalty'
     A                                      OVRATR
     A N88                                  DSPATR(ND)
     A            DDRPEN        14A  B 19 21
     A                                      OVRATR
     A N88                                  DSPATR(ND)
     A N33                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            DDRPIG         1A  B 19 36
     A                                      OVRATR
     A N88                                  DSPATR(ND)
     A N34                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19 50'Penalty'
     A                                      OVRATR
     A  81
     AON88N81                               DSPATR(ND)
     A            DDEPEN        17A  O 19 61DSPATR(HI)
     A  81
     AON88N81                               DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 20 10'Write-Off'
     A                                      OVRATR
     A  82                                  DSPATR(ND)
     A            DDWOFF         1A  B 20 21
     A                                      OVRATR
     A  82                                  DSPATR(ND)
     A N36                                  DSPATR(HI)
     A N70N86                               DSPATR(UL)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A  70
     AO 86                                  DSPATR(PR)
     A                                      OVRDTA
      *
      ***  Repayment details End   ----------------------
      *
      *
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R LEMAPYF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A  76                                  CF16
     A                                      OVERLAY
     A                                      BLINK
     A            DDFOTX        70A    23  2
     A                                      DSPATR(HI)
     A                                 24  2'F3=Exit F12=Cancel'
     A                                 24 21'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                 24 29'F8=Next'
     A N72                                  DSPATR(ND)
     A  73                             24 37'F10=Confirm'
     A N73                                  DSPATR(ND)
     A                                 24 62'F14=Settlements'
     A N74                                  DSPATR(ND)
     A                                 24 51'F15=Select'
     A N75                                  DSPATR(ND)
     A  76                             24 37'F16=Authorise'
     A N76                                  DSPATR(ND)
