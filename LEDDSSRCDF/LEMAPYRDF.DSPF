      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Invalid Manual Repayment - Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LEMAPYRDF - Invalid Manual Repayment - Browse                *
      *              ("REPAIR FUNCTION")                              *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAP077  *CREATE    Date 17Jul02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP077 - Lending API enhancements - Manual Repayments        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03
     A                                      CF12
      *
     A          R LEMAPYS1                  SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2002')
     A            DDOPT          1A  B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDACTN         1A  O  8  5DSPATR(HI)
     A            DDFOTRANID    20A  O  8  7DSPATR(HI)
     A            DDLNRF         6A  O  8 28DSPATR(HI)
     A            DDCUST        10A  O  8 35DSPATR(HI)
     A            DDVALD         6A  O  8 46DSPATR(HI)
     A            DDSEQN         3A  O  8 53DSPATR(HI)
     A            DDSAMT        18A  O  8 57DSPATR(HI)
     A            DDCURR         3A  O  8 76DSPATR(HI)
      *
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
     A*
     A****  Select screen control
     A*
     A          R LEMAPYS0                  SFLCTL(LEMAPYS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'LEMAPYR'
     A                                  1 22'Invalid Manual Repayments Maintena+
     A                                      nce'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 38'Browse'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  2'Sel'
     A                                  5  7'Front Office Id.'
     A                                  5 28'Loan'
     A                                  5 35'Customer'
     A                                  5 46'Value'
     A                                  5 53'Seq.'
     A                                  5 59'Principal Amount'
     A                                  5 76'Ccy'
     A                                  6  4'Act'
     A                                  6 28'Number'
     A                                  6 35'Number'
     A                                  6 46'Date'
      *
      ***  Select screen footer
      *
     A          R LEMAPYF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      *
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      ***  End message
      *
     A          R LEMAPYEND                 TEXT('END MESSAGE')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'LEMAPYR'
     A                                  1 22'Invalid Manual Repayments Maintena+
     A                                      nce'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 38'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
     A            DDFOTX        40A    24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
