      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Edit LE Profitability Authorities')
     A**************************************************************
     A*                                                            *
     A*  Midas - Customer Lending Module
     A*                                                            *
     A*   DSPF/LER400DF - Edit User Defined Authorities for        *
     A*                   Profitability Reports and Enquiries.     *
     A*                                                            *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                            *
     A*                                                            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    LAST AMEND NO. BH3422       DATE 21SEP92                *
     A*                                                            *
     A*------------------------------------------------------------*
     A*                                                            *
     A*  BH3422  -  Help text amendments for Lending Enhancements. *
     A*                                                            *
     A**************************************************************
     A*         SCREEN FORMATS SUMMARY
     A*        ~~~~~~~~~~~~~~~~~~~~~~~~
     A*   F3    Exit Screen
     A*   F9    Add mode
     A*
     A*         INDICATOR  SUMMARY
     A*        ~~~~~~~~~~~~~~~~~~~~
     A*   33    Error on User
     A*   34    Error on Account Officer
     A*   35    Error on Access Level
     A*
     A*
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
     A*                                                        /BH3422/
     A*                                                        /BH3422/
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(87 'Protect non-key fields.')
     A                                      INDTXT(88 'Protect key fields.')
     A                                      INDTXT(89 'ADD mode.')
     A                                      INDTXT(32 'Invalid #1SEL     ')
     A                                      INDTXT(33 'Invalid #1USER    ')
     A                                      INDTXT(34 'Invalid #1ACOF    ')
     A                                      INDTXT(35 'Invalid #1ACLV    ')
     A  84                                  SFLNXTCHG
     A            #1DBRC        13   H      TEXT('Existing record fields')
     A            #1SEL          1   B 10 17
     A                                      VALUES('D')
     A                                      CHECK(AB)
     A  87                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N87N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A            #1USER        10   B 10 20TEXT('LE User Name')
     A                                      CHECK(FE)
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            #1ACOF         2   B 10 35TEXT('Account Officer')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A            #1ACLV         1   B 10 47TEXT('Access Level')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N87N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A                                      INDTXT(89 'ADD mode.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2TMPN')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10A  O  1  2
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'LER400F1'
     A            ##JOB         10A  O 02  2
     A                                  1 25'Edit User Defined Authorities'
     A                                       DSPATR(UL HI)
     A            AGMRDT         7A  O 01 72
     A                                 02 71TIME
     A N89                              3  2'Pos.on User Name:'
     A N89        #2USER        10   B  3 20TEXT('LE User Name')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5  2'Type options, press Enter.'
     A                                  6  2'D=Delete request'
     A  81                              8 21'User Name'
     A  81                              8 32' Account'
     A  81                              9 32' Officer'
     A  81                              9 47'Access Level'
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A  89                             23  2'F3=Exit   F9=Go to ''Change'' mode-
     A                                                                         -
     A                                                 '
     A N89                             23  2'F3=Exit   F9=Go to ''Add'' mode   -
     A                                                                         -
     A                                                 '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
