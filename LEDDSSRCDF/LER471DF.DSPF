      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Profitability Rpt. Selection Prompt')
     A**************************************************************
     A*                                                            *
     A*  Midas - Customer Lending Module
     A*                                                            *
     A*   DSPF/LER471DF - Profitability Report Selection Prompt    *
     A*                                                            *
      *  (c) Finastra International Limited 2001                      *
     A*                                                            *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 112563             Date 27Jan97               *
      *                 BH3422             Date 21Sep92               *
     A*                                                            *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  BH3422 - Help text amendments for Lending enhancments        *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A*                                                        /BH3422/
     A*                                                        /BH3422/
     A
     A          R LER471D1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CF12(02 'Previous')
     A                                      CF03(03 'Cancel')
     A                                      CF10(04 'Delete')
     A            USER          10A  O  1  2
     A                                 01 13'LER471'
     A            JOB           10A  O 02  2
     A                                  1 20'Profitability Report +
     A                                      Selection Prompt'
     A                                       DSPATR(UL HI)
     A            AGMRDT         7A  O 01 72
     A                                 02 71TIME
     A                                 05 11'Enter:'
     A                                 07 23'Full Report (Y)'
     A            FULL           1A  B 07 45DSPATR(HI)
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL)
     A  05                                  DSPATR(PR)
     A                                      OVRDTA
     A
     A                                 09 20'Or Customer'
     A            CNUM          10A  B 09 45DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL)
     A  05                                  DSPATR(PR)
     A                                      OVRDTA
     A
     A  20                             11 20'Or Account Officer'
     A  20        ACOF           2A  B 11 45DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL)
     A  05                                  DSPATR(PR)
     A                                      OVRDTA
     A
     A  21                             13 20'Or Department Code'
     A  21        DEPT           3A  B 13 45DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(UL)
     A  05                                  DSPATR(PR)
     A                                      OVRDTA
     A
     A**                                             Begin /112563/
     A***********                      23  2'Press F3 To Exit, F12 for Previous-
     A***********                            screen'
     A                                 23  2'F3=Exit  F12=Previous'
     A**                                             End   /112563/
     A
     A          R LER471D2                  TEXT('MESSAGE SUBFILE FORMAT')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSQK                      SFLMSGKEY
     A            MSGQ                      SFLPGMQ
     A
     A          R LER471D3                  SFLCTL(LER471D2)
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A  44                                  SFLDSP SFLDSPCTL
     A                                      SFLINZ
     A  45                                  SFLEND
     A            MSGQ                      SFLPGMQ
     A
