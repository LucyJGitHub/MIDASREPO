      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Fclty History Report Prompt Screen')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*  DSPF/LER600DF - FACILITY HISTORY REPORT PROMPT SCREEN        *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. 112563             DATE 27JAN97               *
     A*                 BH3422             DATE 21SEP92               *
     A*                 BH3393             DATE 09JUL92               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CAAA02 - Remove use of LOCK keyword to enable WebFacing      *
      *           to process screens.                                 *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  BH3422  -  Help text amendments for Lending Enhancements.    *
     A*  BH3393 - BHF - Screen ID cut off because field too short     *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
      *
     A
     A                                      INDARA
     A*                                                        /BH3422/
     A*                                                        /BH3422/
     A                                      PRINT
     A                                      MSGLOC(24)
     A                                      CF03
     A                                      CF12
     A                                      CHGINPDFT
      *
     A          R LER600F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      OVERLAY
     A                                      PUTOVR
      ** Start of CAAA02
     A******************                    LOCK
      ** End   of CAAA02
     A                                      BLINK
     A*                                                        /BH3422/
     A*                                                        /BH3422/
     A
     A
     A**                                             Begin /112563/
     A                                  2  4'LER600'
     A**                                             End   /112563/
     A                               O  2 13'FACILITY HISTORY REPORT'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A
     A**                                                     /3375/
     A************WSID           6A  O  2 54TEXT('Workstation')
     A            WSID          10A  O  2 48TEXT('Workstation')
     A**                                                     /3375/
     A
     A            MRDT           7A  O  2 62
     A
     A                                  2 71TIME
     A
     A
     A                                  7  6'Customer :'
     A            SCUST         10A  B  7 17OVRDTA
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(UL)
     A N20                                  DSPATR(HI)
     A
     A
     A                                  7 28'Facility :'
     A
     A            SFACT          3A  B   + 1OVRDTA
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(UL)
     A N21                                  DSPATR(HI)
     A                                   + 1'/'
     A
     A            SFCNO          2A  B   + 1OVRDTA
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(UL)
     A N21                                  DSPATR(HI)
     A                                   + 2'History Start date'
     A
     A            SSDAT          6A  B   + 1OVRDTA
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(UL)
     A N22                                  DSPATR(HI)
     A
     A          R LER600F4                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A          R LER600F5                  TEXT('MESSAGE SUBFILE CONTROL')
     A                                      SFLCTL(LER600F4)
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  59                                  SFLEND
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A**                                             Begin /112563/
     A***********                      23  3'F3 to terminate'
     A***********                      23 21'F12 previous'
     A                                 23  2'F3=Exit'
     A                                 23 12'F12=Previous'
     A**                                             End   /112563/
