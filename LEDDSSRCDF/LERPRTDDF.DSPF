      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Risk Participant Display')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  DSPF/LERPRTDDF - Risk Participant Display                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CLE042             Date 06Sep05               *
      *  Prev Amend No. CAP069  *CREATE    Date 15Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CAP069 - Conversion of LE inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A*=========================================================================
     A          R LERPRTF2                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      OVERLAY
     A                                      KEEP
     A            S#USER    R        O  1  2REFFLD(USER)
     A            S#WSID    R        O  2  2REFFLD(JOJOB)
     A                                  1 14'LERPRTD'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A            S#RUNA    R        O  1 73REFFLD(RUNA)
     A                                  1 34'RISK PARTICIPANT DETAILS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*=========================================================================
     A          R LERPRTS1                  SFL
     A  35                                  SFLNXTCHG
     A            S#SEL          1A  B 12  3
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36                                  DSPATR(HI)
     A N36                                  DSPATR(UL)
     A            S#CNUM         6A  O 12  8DSPATR(HI)
     A            S#CSNM        10A  O 12 16DSPATR(HI)
     A            S#JDTE         6A  O 12 30DSPATR(HI)
     A            S#SHTP         1A  O 12 43DSPATR(HI)
     A            S#SHAR        16A  O 12 48DSPATR(HI)
     A*=========================================================================
     A          R LERPRTC1                  SFLCTL(LERPRTS1)
     A                                      SFLSIZ(0045)
     A                                      SFLPAG(0009)
     A N66                                  CF09(09 'Add')
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A  30                                  SFLDSP
     A  31                                  SFLDSPCTL
     A N31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A            ##SFRC         4S 0H      SFLRCDNBR(CURSOR)
     A                                  3  8'                  '
     A                                  4  2'Customer'
     A            S#PRCU         6A  O  4 11DSPATR(HI)
     A            S#PRNM        20A  O  4 19DSPATR(HI)
     A                                  4 43'Branch'
     A            S#BRCA         3A  O  4 50DSPATR(HI)
     A                                  4 56'Amount'
     A            S#FAMT        14A  O  4 63DSPATR(HI)
     A                                  5  2'Facility'
     A            S#PMFC         6A  O  5 11DSPATR(HI)
     A                                  5 18'-'
     A            S#PMFT         3A  O  5 20DSPATR(HI)
     A                                  5 24'-'
     A            S#PMFN         2A  O  5 26DSPATR(HI)
     A                                  5 30'Currency'
     A            S#FCCY         3A  O  5 39DSPATR(HI)
     A                                  5 47'Percentage Sold'
     A            S#PCSD        15A  O  5 63DSPATR(HI)
     A                                  7  2'Actions,'
     A                                  8  3'E=Enquire'
     A                                  8 16'A=Amend    D=Delete'
     A  66                                  DSPATR(ND)
     A                                 10  2'Sel   -----Customer-----    Joinin-
     A                                      g     ---------Share---------'
     A                                 11  8'Number  Shortname       Date      -
     A                                      Type  Amount/Percentage'
     A*=========================================================================
     A          R LERPRTF1
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F10=Delete' DSPATR(HI)
     A N68                                  DSPATR(ND)
     A                                 23 35'F12=Previous Screen'
     A                                 23 56'F14=Settlements'
     A N69                                  DSPATR(ND)
     A*=========================================================================
     A          R LERPRTF3
     A                                      CF12(12 'Previous')
     A  68                                  CF10(10 'Delete')
     A  69                                  CF14(14 'Settlement')
     A                                      OVERLAY
     A                                      PROTECT
     A                                  3  9'           '
     A                                  4  2'Customer'
     A            S#PRCU         6A  O  4 11DSPATR(HI)
     A            S#PRNM        20A  O  4 19DSPATR(HI)
     A                                  4 55'Branch'
     A            S#BRCA         3A  O  4 62DSPATR(HI)
     A                                  5  2'Facility'
     A            S#PMFC         6A  O  5 11
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A                                  5 18'-'
     A            S#PMFT         3A  O  5 20
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI)
     A                                  5 24'-'
     A            S#PMFN         2A  O  5 26
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A                                  5 31'Currency'
     A            S#FCCY         3A  O  5 40DSPATR(HI)
     A                                  7  5'Member Name   :'
     A            S#CSSN        10A  B  7 21
     A  67
     AON09                                  DSPATR(PR)
     A  44                                  DSPATR(RI PC)
     A N67 09                               DSPATR(UL)
     A N44                                  DSPATR(HI)
     A            S#SRNM        20   O  7 34DSPATR(HI)
     A            S#SRTN        10   O  7 56DSPATR(HI)
     A                                  9  5'Introduced by :'
     A            S#INUM        10A  B  9 21
     A  67                                  DSPATR(PR)
     A  45                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N45                                  DSPATR(HI)
     A            S#IRNM        20   O  9 34DSPATR(HI)
     A            S#IRTN        10   O  9 56DSPATR(HI)
     A                                 11  5'Joining Date  :'
     A            S#JDTE         6A  B 11 21
     A  67                                  DSPATR(PR)
     A  46                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N46                                  DSPATR(HI)
     A                                 13  5'Share Type (Amount or Percentage) -
     A                                      :'
     A            S#SHTP         1A  B 13 42
     A  67                                  DSPATR(PR)
     A  47                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N47                                  DSPATR(HI)
     A                                 14 21'Share . . . . . . :'
     A            S#SHAR        16A  B 14 42
     A  67                                  DSPATR(PR)
     A  48                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N48                                  DSPATR(HI)
     A                                 16 17'Fee Base Rate . . . . :'
     A            S#FEBR         2A  B 16 42
     A  67                                  DSPATR(PR)
     A  49                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N49                                  DSPATR(HI)
     A                                 17 21'Rate/Spread . . . :'
     A            S#FERA        12A  B 17 42
     A  67                                  DSPATR(PR)
     A  50                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N50                                  DSPATR(HI)
     A                                 18 21'Sign. . . . . . . :'
     A            S#FERS         1A  B 18 42
     A  67                                  DSPATR(PR)
     A  51                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N51                                  DSPATR(HI)
     A                                 20 17'Risk Participation'
     A                                 21 20'Loan Type/Sub-type :'
     A            S#DRTP         2A  B 21 42
     A  67                                  DSPATR(PR)
     A  52                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N52                                  DSPATR(HI)
     A                                 21 45'-'
     A            S#DRST         2A  B 21 47
     A  67                                  DSPATR(PR)
     A  53                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N53                                  DSPATR(HI)
      *                                              Start /CLE042/
     A            S#DRCL         4A  B 21 50
     A  67                                  DSPATR(PR)
     A  53                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N53                                  DSPATR(HI)
      *                                              End   /CLE042/
     A                                 22  9'         '
     A*=========================================================================
     A          R LERPRTF4
     A                                      OVERLAY
     A                                 13 34'NO RECORDS FOUND'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A*=========================================================================
     A          R LERPRTF5
     A                                      OVERLAY
     A                                 22  2'F3=Exit    F5=Refresh'
     A                                 22 27'F9=Insert'
     A  66                                  DSPATR(ND)
     A                                 22 40'F12=Previous Screen'
     A                                 23 71' '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A  25                                  SFLDSP
     A  25                                  SFLDSPCTL
     A  25                                  SFLINZ
     A  25                                  SFLCLR
     A  25                                  SFLEND
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
