/*STD *  DSPFBASE                                                    *
/*EXI *  TEXT('Midas LE Risk Participant Repair Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module.                             *
      *                                                               *
      *  LERPRTRDF - Risk Participant Repair Browse                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA03             Date 14Apr04               *
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CAP069 *Create     Date 15Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAAA03 - Webfacing changes.                                  *
      *  CAP069 - Conversion of LE inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R LEIRPRTS1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A            S#OPT          1A  B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            S#ACTN         1A  O  8  6DSPATR(HI)
     A            S#PMFC         6A  O  8 11DSPATR(HI)
     A                               O  8 18'-'
     A            S#PMFT         3A  O  8 20DSPATR(HI)
     A                               O  8 24'-'
     A            S#PMFN         2A  O  8 26DSPATR(HI)
     A            S#CSSN        10A  O  8 33DSPATR(HI)
     A            S#JDTE         6A  O  8 52
     A            S#SHTP         1A  O  8 62
     A            S#SHAR        16A  O  8 64
     A            @XREPN         5S 0H
     A            @XTMST        26A  H
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R LEIRPRTS0                 TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(LEIRPRTS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            S#SFRN         4S 0H      SFLRCDNBR
     A*
     A            S#USER    R        O  1  2REFFLD(USER)
     A            S#WSID    R        O  2  2REFFLD(JOJOB)
     A                                  1 14'LERPRTRPB'
      ** Start of change CAAA03
     A**********                        2 71TIME
     A                                  2 72TIME                                              CAAA03
      ** End of change CAAA03
     A                                      EDTWRD('  :  :  ')
     A                                  1 26'INVALID RISK PARTICIPANT MAIN-
     A                                      TENANCE'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            S#RUNA    R        O  1 73REFFLD(RUNA)
      ** Start of change CAAA03
     A**********                        6  1'Sel Act <- Primary Facility -> Syn-              CAAA03
     A**********                            dicate Member Joining Date -                      CAAA03
     A**********                            <---  Share --->'                                 CAAA03
     A                                  6  1'Sel'
     A                                  6  5'Act'
     A                                  6  9'<- Primary Facility ->'
     A                                  6 32'Syndicate Member'
     A                                  6 49'Joining'
     A                                  6 57'Date'
     A                                  6 62'<--- Share --->'
      ** End of change CAAA03
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R LEIRPRTF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A*
     A* -------------------------------------------------------------------*
     A* * END MESSAGE                                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R LEIRPRTEND                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
     A                                      PUTOVR
     A*
     A            S#USER    R        O  1  2REFFLD(USER)
     A            S#WSID    R        O  2  2REFFLD(JOJOB)
     A                                  1 14'LERPRTRPB'
      ** Start of change CAAA03
     A**********                        2 71TIME                                              CAAA03
     A                                  2 72TIME
      ** End of change CAAA03
     A                                      EDTWRD('  :  :  ')
     A                                  1 26'INVALID RISK PARTICIPANT MAIN-
     A                                      TENANCE'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            S#RUNA    R        O  1 73REFFLD(RUNA) OVRDTA
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
     A            S#FOTX        40A    24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
