      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Repayments schedule - Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LERPSCBDF - Midas LE Repayments schedule - Browse            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAP079  *CREATE    Date 28Jun02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP079 - Lending API enhancements - Repayments schedule      *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      ***  Select screen sub-file 1
      *
     A          R LERPSCS1                  SFL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2002')
     A            DDOPT          1A  B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDLNRF         6A  O  8  5DSPATR(HI)
     A            DDVDAT         6A  O  8 13DSPATR(HI)
     A            DDSEQN         3A  O  8 21DSPATR(HI)
     A            DDCUST         6A  O  8 26DSPATR(HI)
     A            DDCSNM        10A  O  8 33DSPATR(HI)
     A            DDCURR         3A  O  8 45DSPATR(HI)
     A            DDSAMT        18A  O  8 50DSPATR(HI)
     A            DDSTAT        10A  O  8 70DSPATR(HI)
      *
      ***  Subfile control record 1
      *
     A          R LERPSCC1                  SFLCTL(LERPSCS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2
     A                                  1 13'LERPSCB1'
     A                                  1 26'            Midas            '
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 23'Repayment schedule and Authorisati-
     A                                      on'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  1'Sel'
     A                                  5  5'Loan'
     A                                  5 13'Value'
     A                                  5 21'Seq'
     A                                  5 26'Customer number'
     A                                  6 26'and shortname'
     A                                  6 13'Date'
     A                                  6  5'number'
     A                                  5 62'Amount'
     A                                  5 70'Status'
     A                                  5 45'Ccy'
      *
      ***  Select screen sub-file 2
      *
     A          R LERPSCS2                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFOTRANID    20A  O  8  5DSPATR(HI)
     A            DDLNRF         6A  O  8 27DSPATR(HI)
     A            DDVDAT         6A  O  8 35DSPATR(HI)
     A            DDCURR         3A  O  8 43DSPATR(HI)
     A            DDCUST         6A  O  8 48DSPATR(HI)
     A            DDSEQN         3A  O  8 56DSPATR(HI)
     A            DDSAMT        18A  O  8 61DSPATR(HI)
      *
      ***  Select screen control 2
      *
     A          R LERPSCC2                  SFLCTL(LERPSCS2)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2
     A                                  1 13'LERPSCB2'
     A                                  1 26'            Midas            '
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 23'Repayment schedule and Authorisati-
     A                                      on'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  1'Sel'
     A                                  5  5'Front Office Id.'
     A                                  5 27'Loan'
     A                                  6 27'number'
     A                                  5 35'Value'
     A                                  6 35'Date'
     A                                  5 48'Cust.'
     A                                  5 43'Ccy'
     A                                  5 56'Seq.'
     A                                  5 73'Amount'
      *
      ***  Select screen footer
      *
     A          R LERPSCF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
