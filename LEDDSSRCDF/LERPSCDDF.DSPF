      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Repayments schedule - Detail screen')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LERPSCDDF - Midas LE Repayment schedule - Detail screen      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAP079  *CREATE    Date 28Jun02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP079 - Lending API enhancements - Repayments schedule      *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03
     A                                      CF12
     A  72                                  CF14
     A  73                                  CF15
     A  74                                  CF07
     A  74                                  CF08
     A  75                                  CF11
     A  76                                  CF10
      *
      ***  Details 1
      *
     A          R LERPSCD1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2002')
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2
     A                                  1 13'LERPSC1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 30'Repayment Schedule Input'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A            DDSTAT        10A  O  4  2DSPATR(HI)
     A                                      OVRDTA
     A                                  4 15'Loan no'
     A            DDLNRF         6A  B  4 26
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  10N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4 39'Action'
     A            DDACTN         1A  B  4 46
     A  11                                  DSPATR(RI)
     A N11                                  DSPATR(HI)
     A  11N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4 54'Sequence'
     A            DDSEQN         3A  B  4 63
     A  12                                  DSPATR(RI)
     A N12                                  DSPATR(HI)
     A  12N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  6 15'Value Date'
     A            DDVDAT         6A  B  6 26
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(HI)
     A  13N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  8 15'Customer'
     A            DDCUST        10A  B  8 39
     A  14                                  DSPATR(RI)
     A N14                                  DSPATR(HI)
     A  14N70                               DSPATR(PC)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  9 15'Currency'
     A            DDCURR         3A  B  9 39
     A  15                                  DSPATR(RI)
     A N15                                  DSPATR(HI)
     A  15N70                               DSPATR(PC)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 10 15'Amount'
     A            DDSAMT        14A  B 10 39
     A  16                                  DSPATR(RI)
     A N16                                  DSPATR(HI)
     A  16N70                               DSPATR(PC)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 11 15'Auto-settlement'
     A            DDAUTO         1A  B 11 39
     A* 17                                  DSPATR(RI PC)
     A*   N71                               DSPATR(PR)
     A* 71N17                               DSPATR(UL)
     A*N17                                  DSPATR(HI)
     A  17                                  DSPATR(RI)
     A N17                                  DSPATR(HI)
     A  17N70                               DSPATR(PC)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 13 15'Special Instructions'
     A            DDSPI1        30A  B 13 39DSPATR(HI)
     A*  70N71                               DSPATR(PR)
     A* N70
     A*O 71                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDSPI2        30A  B 14 39DSPATR(HI)
     A*  70N71                               DSPATR(PR)
     A* N70
     A*O 71                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDSPI3        30A  B 15 39DSPATR(HI)
     A*  70N71                               DSPATR(PR)
     A* N70
     A*O 71                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
      *
      ***  Footer for Detail screen
      *
     A          R LERPSCF1
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 10'F12=Prev'
     A  72                             23 19'F14=Settl Inst'
     A  73                             23 34'F15=Select'
     A  74                             23 45'F7=Prev F8=Next'
     A  75                             23 28'F11=Authorise'
     A  76                             23 19'F10=Delete'
     A            D#FOTX        70A  O 24  2
     A                                      DSPATR(HI)
      *
      ***  Message subfile record
      *
     A          R LERPSCS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ***  Message subfile control
      *
     A          R LERPSCC1                  TEXT('Program messages')
     A                                      SFLCTL(LERPSCS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
