      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas LE Invalid Repayments schedule - Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LERPSCRDF - Invalid Repayments schedule - Browse             *
      *              ("REPAIR FUNCTION")                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAP079  *CREATE    Date 01Jul02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP079 - Lending API enhancements - Repayments schedule      *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03
     A                                      CF12
      *
     A          R LERPSCS1                  SFL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2002')
     A            DDOPT          1A  B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFOTRANID    20A  O  8  5DSPATR(HI)
     A            DDLNRF         6A  O  8 27DSPATR(HI)
     A            DDVDAT         6A  O  8 35DSPATR(HI)
     A            DDCURR         3A  O  8 43DSPATR(HI)
     A            DDCUST        10A  O  8 48DSPATR(HI)
     A            DDSEQN         3A  O  8 60DSPATR(HI)
     A            DDSAMT        14A  O  8 65DSPATR(HI)
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
      *
      ****  Select screen control
      *
     A          R LERPSCS0                  SFLCTL(LERPSCS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'LERPSCR'
     A                                  1 23'Invalid Repayments Schedule mainte-
     A                                      nance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 40'Browse'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  1'Sel'
     A                                  5  5'Front Office Id.'
     A                                  5 27'Loan'
     A                                  5 35'Value'
     A                                  5 43'Ccy'
     A                                  5 48'Customer'
     A                                  5 60'Seq'
     A                                  5 73'Amount'
     A                                  6 27'Number'
     A                                  6 35'Date'
     A                                  6 48'Number'
      *
      ***  Select screen footer
      *
     A          R LERPSCF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      *
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      ***  End message
      *
     A          R LERPSCEND                 TEXT('END MESSAGE')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'LERPSCR'
     A                                  1 26'Invalid Customer +
     A                                      Loans Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
     A            DDFOTX        40A    24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
