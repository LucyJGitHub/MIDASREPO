      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Facility history amounts')                    *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module                              *
     A*                                                               *
     A*  PF/FACHISA - Facility History Amounts                        *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. AR674226           Date 04Dec19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 225591             Date 08Jul04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CLE023             Date 19Nov01               *
      *                 200289             Date 19Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 156585             Date 16Mar99               *
      *                 CLE005             Date 22May97               *
      *                 S01473             Date 11Jul94               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  AR674226 - Manual transaction reached 999. Increase the size *
      *             of FASQNO and FATSEQ to accomodate 9999           *
      *             transactions. (Child: AR674227)                   *
      *           - Applied for MD054782.                             *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  225591 - Facility history changes for repayments due to      *
      *           rollover                                            *
      *  CLE025 - Credit Lines                                        *
      *  CLE023 - Lending Facility History Improvements.              *
      *  200289 - Increase facility sequence number in FACHISA from   *
      *           2 to 3.  Introduced new field FATSEQ.               *
     A*  156585 - Add Assignment indicator                            *
     A*  CLE005 - Customer Lending Enhancement - Others (T3)          *
     A*   S01473 - Leo/Midas Integration, include L/C Reference.      *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
      *
     A          R FACHISAF                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            FARECI    R               REFFLD(RECI)
     A            BRCA      R
      * Begin /CSD027/
     A**********  FACNUM    R               REFFLD(CNUM)                                      CSD027
     A            FACNUM         6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            FAFCTY    R               REFFLD(FCLY)
     A            FADATE    R               REFFLD(DATE)
     A            FAFSEQ    R               REFFLD(FSEQ)
      **Start of change CLE148
     A**********  FALOAN    R               REFFLD(LNRF)                                      CLE148
     A            FALOAN         6A         TEXT('Loan number')
     A                                      COLHDG('Loan' 'Number')
      **End of change CLE148
     A            FAACTN    R
     A            FAAAMT    R
     A            FAPART    R
     A            FARCIN    R               REFFLD(RVCR)
     A            FACFAM    R               REFFLD(FAMT)
     A            FADRAM    R
     A            FAUNDR    R
     A            FAREVI    R               REFFLD(REVID)
      *                                              Begin /S01473/
     A            FALCRF    R               REFFLD(FCLN)
     A                                      TEXT('L/C Reference')
     A                                      COLHDG('L/C Reference')
      *                                              End   /S01473/
      *                                              Begin /CLE005/
                  FAGNDT    R               REFFLD(CCDT)
                                            TEXT('Generation Date')
                                            COLHDG('Gen. Date')
      **Start of Change AR674226
     A**********  FASQNO    R               REFFLD(AMSQ)                                    AR674226
     A**********                            TEXT('Generation Sequence No.')                 AR674226
     A**********                            COLHDG('Util.' +                                AR674226
     A**********                                   'Seq. No.')                              AR674226
     A            QQSQNO         3S 0
      **End of Change AR674226
                  FACFGI    R               REFFLD(CCIO)
                                            TEXT('Generation Indicator')
                                            COLHDG('Gen. Ind.')
      *                                              End   /CLE005/
      *                                              Begin /156585/
     A            FAGASS    R               REFFLD(FAPART)
     A                                      TEXT('Assignment Generate+
     A                                            d Ind.')
     A                                      COLHDG('Ass. Gen. Ind.')
      *                                              End   /156585/
      **Start of change AR674226
     A**Start of change 200289
     A**********  FATSEQ         3S 0       TEXT('Transaction Sequence +                    AR674226
     A**********                                 Number')                                   AR674226
     A**********                            COLHDG('Trans Seq No.')                         AR674226
     A            QQTSEQ         3S 0
     A**End of change 200289
      **End of change AR674226
     A**Start of change CLE023
     A            FALUCY         3A         TEXT('Loan/Mnfu/Tranche +
     A                                           Ccy')
     A                                      COLHDG('Loan/Mnfu/' +
     A                                      'Tranche Ccy')
     A            FALUAM        15P 0       TEXT('Amt of Actn in Ln/+
     A                                            Mnfu/Trch Ccy')
     A                                      COLHDG('Amt of Act in' +
     A                                             'Ln/Mnfu/Trch Ccy')
     A            FAEXRT        13P 8       TEXT('Ln/Mnfu/Trch to +
     A                                            fclty xrate')
     A                                      COLHDG('Ln/Mnfu/Trch' +
     A                                             'to fclty xrate')
     A            FARTMD         1A         TEXT('Exchange rate mult+
     A                                           /div indicator')
     A                                      COLHDG('Xrate M/D' +
     A                                             'indicator')
     A            FAPTYP         2S 0       TEXT('Loan Processing +
     A                                            Type')
     A                                      COLHDG('Loan Proc' +
     A                                             'Type')
     A            FARCSI         1A         TEXT('Recourse Indicator')
     A                                      COLHDG('Recourse' +
     A                                             'Indicator')
     A            FAORED         5P 0       TEXT('Original Entry Date')
     A                                      COLHDG('Orig Entry' +
     A                                             'Date.')
     A            FAAFAM        13P 0       TEXT('Actual Facility +
     A                                           Amount')
     A                                      COLHDG('Actual Facility' +
     A                                             'Amount')
     A            FAPAMT        15P 0       TEXT('Previous Action +
     A                                           Amount')
     A                                      COLHDG('Previous Action' +
     A                                             'Amount')
     A            FALPFI         1A         TEXT('Linked to Participant +
     A                                             Fclty Ind.')
     A                                      COLHDG('Linked to Prtcpt' +
     A                                             'Facility Ind.')
     A**End of change CLE023
      **Start of change CLE025
     A            FAOFFS        15P 0       TEXT('Offset Amount')
     A                                      COLHDG('Offset Amount')
     A            FADEAL         6S 0       TEXT('Dealing Deal Number')
     A                                      COLHDG('Dealing' 'Deal' 'Number')
     A            FAMCY          3A         TEXT('Margin Currency')
     A                                      COLHDG('Margin' 'Currency')
     A            FAACBR         3A         TEXT('Retail Account Branch')
     A                                      COLHDG('Retail Account' +
     A                                             '    Branch')
      * Begin /CSD027/
     A**********  FAACCU         6S 0       TEXT('Retail Account Customer')                   CSD027
     A            FAACCU         6A         TEXT('Retail Account Customer')
      * End /CSD027/
     A                                      COLHDG('Retail Account' +
     A                                             '   Customer')
     A            FAACCY         3A         TEXT('Retail Account Currency')
     A                                      COLHDG('Retail Account' +
     A                                             '   Currency')
     A            FAACCD        10S 0       TEXT('Retail Account Code')
     A                                      COLHDG('Retail Account' +
     A                                             '     Code')
     A            FAACSQ         2S 0       TEXT('Retail Account Sequence')
     A                                      COLHDG('Retail Account' +
     A                                             '   Sequence')
     A            FAACNO        10S 0       TEXT('Retail Account Number')
     A                                      COLHDG('Retail Account' +
     A                                             '    Number')
     A            FATTEO        15P 0       TEXT('Today''s Total +
     A                                      Exposure/Offset in FCCY')
     A                                      COLHDG('Today''s Total' +
     A                                      'Exposure/Offset' 'in FCCY')
     A            FAPTEO        15P 0       TEXT('Previous Total +
     A                                      Exposure/Offset Posted in FCCY')
     A                                      COLHDG('Previous Total' +
     A                                      'Exposure/Offset' 'Posted in FCCY')
     A            FAEXCY         3A         TEXT('Exposure Currency')
     A                                      COLHDG('Exposure' 'Currency')
     A            FATVAL        15P 0       TEXT('Today''s Valuation +
     A                                      in Exposure Currency')
     A                                      COLHDG('Today''s' 'Valuation in' +
     A                                      'Exposure Currency')
     A            FAWEIG         2S 0       TEXT('Weighting Code')
     A                                      COLHDG('Weighting' 'Code')
     A            FAWCPC         3P 0       TEXT('Weighting Code''s Percentage')
     A                                      COLHDG('Weighting' 'Code''s' +
     A                                      'Percentage')
     A            FAINOF         1A         TEXT('Include Offset')
     A                                      COLHDG('Include Offset')
     A            FAFXMT         3A         TEXT('FX Margin Type')
     A                                      COLHDG('FX' 'Margin' 'Type')
     A            FAFXPC         3P 0       TEXT('FX Margin Percentage')
     A                                      COLHDG('FX' 'Margin' 'Percentage')
      **End of change CLE025
     A**Start of change 225591
     A            FARIND         1A         TEXT('Rollover Action +
     A                                             Ind.')
     A                                      COLHDG('Roll' 'Act' +
     A                                             'Ind')
     A**End of change 225591
      **Start of Change AR674226
     A            FASQNO         4S 0
     A                                      TEXT('Generation Sequence No.')
     A                                      COLHDG('Util.' +
     A                                             'Seq. No.')
     A            FATSEQ         4S 0       TEXT('Transaction Sequence +
     A                                           Number')
     A                                      COLHDG('Trans Seq No.')
      **End of Change AR674226
     A          K BRCA
     A          K FACNUM
     A          K FAFCTY
