      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Facility history header')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*  PF/FACHISH - Facility History Header                         *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. CLE138             Date 02Nov21               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CLE023             Date 19Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CLE005             Date  22May97              *
      *                 CLE004             Date  02Dec96              *
     A*                 MOF60              Date  05Jul91              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CLE138 - Class Codes for Facilities                          *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE025 - Credit Lines                                        *
      *  CLE023 - Lending Facility History Improvements.              *
     A*   CLE005 - Customer Lending Enhancements - Others (T3)        *
     A*   CLE004 - Customer Lending Enhancements - Syndication        *
     A*   MOF60  - CUSTOMER PROFITABILITY & FEES PROCESSING           *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A******************************************************************
      *
     A                                      REF(MFRFL)
      *
     A          R FACHISHF                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            FHRECI    R               REFFLD(RECI)
      * Begin /CSD027/
     A**********  FHCNUM    R               REFFLD(CNUM)                                      CSD027
     A            FHCNUM         6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            FHFTYP    R
     A            FHFSEQ    R               REFFLD(FSEQ)
     A            FHBRCH    R               REFFLD(BRCA)
     A            FHFTIN    R               REFFLD(FCTI)
     A            FHFCCY    R               REFFLD(CCY)
     A            FHOFAM    R
     A            FHOLPC    R               REFFLD(OVPC)
     A            FHDAPP    R               REFFLD(DTAP)
     A            FHEXDT    R               REFFLD(DTEX)
     A            FHRCIN    R               REFFLD(RVCR)
     A            FHMCCY    R               REFFLD(MCCY)
     A            FHMCUS    R               REFFLD(MCUSF)
     A            FHCTYR    R               REFFLD(CRSK)
     A            FHNOMR    R               REFFLD(NOMR)
     A            FHACOF    R               REFFLD(ACOF)
     A            FHLFAM    R
     A            FHLDAM    R
      *                                              Begin /CLE004/
     A            FHPRTR    R               REFFLD(SPIN)
     A                                      TEXT('Participant Facility +
     A                                            Ind.')
     A                                      COLHDG('Part' +
     A                                             'Fac?')
      *                                              End   /CLE004/
      *                                              Begin /CLE005/
     A            FHPFTI    R               REFFLD(FCTI)
     A                                      TEXT('Prev. Facility type +
     A                                            Ind.')
     A                                      COLHDG('Prev. Fclty' +
     A                                             'Type Ind.')
      *                                              End   /CLE005/
     A**Start of change CLE023
     A            FHORED         5P 0       TEXT('Original Entry Date')
     A                                      COLHDG('Orig Entry' +
     A                                             'Date.')
     A            FHRWKR         1A         TEXT('Rework Required?')
     A                                      COLHDG('Rework' +
     A                                             'Required')
     A            FHRWDT         5P 0       TEXT('Rework from Date')
     A                                      COLHDG('Rework from' +
     A                                             'Date')
     A            FHRWTP         1A         TEXT('Rework Type')
     A                                      COLHDG('Rework type')
     A            FHLBRD         5P 0       TEXT('Lst Bckvaluatn Rwrk +
     A                                           frm Date')
     A                                      COLHDG('Lst Bckvalue' +
     A                                             'Rwrk frm Date')
     A            FHLMRD         5P 0       TEXT('Lst Manual Rwrk frm Date')
     A                                      COLHDG('Lst Manual Rwrk' +
     A                                             'frm Date')
     A            FHDLRW         5P 0       TEXT('Date of Last Rework')
     A                                      COLHDG('Date of Last' +
     A                                             'Rework')
     A**End of change CLE023
     A**Start of change CLE025
     A            FHLOAM        13P 0       TEXT('Last Offset Amount')
     A                                      COLHDG('Last Offset Amount')
     A**End of change CLE025
      **Start of change CLE138
     A            FHFCLS         4A         TEXT('Facility Class Code')
     A                                      COLHDG('Fcty Class Code')
     A            FHPCS          4A         TEXT('Previous Class Code')
     A                                      COLHDG('Prv Class Code')
      **End of change CLE138
     A          K FHCNUM
     A          K FHFTYP
     A          K FHFSEQ
