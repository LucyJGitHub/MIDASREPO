      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE History amount detail')                       *
     A**************************************************************************
     A*
     A*  Midas - Customer Lending Module
     A*
     A*   PF/HISTSHP    LOANS HISTORY FILE-AMOUNT DETAIL
     A*
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CAS005             Date 16Dec02               *
      *                 CLE028             Date 27Jun02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CPB002             Date 31Aug00               *
      *                 157669             Date 18Apr00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CLE004             Date 12Dec96               *
     A*                 CLE003             Date 05Dec96               *
     A*                 CAC001             Date 01Feb96               *
     A*                 S01270             Date 08Aug90               *
     A*                 S01117             DATE 19JUN87               *
     A*                 S01125             DATE 16OCT86               *
     A*
     A*----------------------------------------------------------------
     A*
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  CLE028 - Flat Rate Personal Loans (Rule of 78s)              *
     A*   CPB002  -  Meridian DBA Middleware Replication              *
     A*              Add Replication Indicator.                       *
     A*  157669 - Cost of Funds are being reported incorrectly        *
     A*           Add 4 new fields in order to keep a running total   *
     A*           of the cost of funds.                               *
     A*   CLE004  -  Customer Lending Enhancements - Syndications     *
     A*   CLE003  -  Customer Lending Backvaluation:                  *
     A*           Add 5 new fields in order to keep a running total   *
     A*           of the interest                                     *
     A*  CAC001 - Profit Centre Accounting Development:               *
     A*           New field COFA added to the end of file.            *
     A*   S01270  -  CUSTOMER LENDING CHANGES
     A*   S01117  -  MULTIBRANCHING
     A*   S01125  -  R21 FIX, MISSING FIELD ADDED
     A*
     A*************************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R HISTSHPF                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            RECI      R
     A            RCDT      R
     A*                                                   S01117
     A************BRCD      R
     A            BRCA      R
     A*                                                   S01117
      * Begin /CSD027/
     A**********  CNUM      R                                                                 CSD027
     A            CNUM           6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
      **Start of change CLE148
     A**********  LNRF      R                                                                 CLE148
     A            LNRF           6A         TEXT('Loan number')
     A                                      COLHDG('Loan' 'Number')
      **End of change CLE148
     A            VDAT      R               REFFLD(VDAT0 )
     A            PRSQ      R
     A            LASN      R               REFFLD(LASN0 )
      * Begin /CSD027/
     A**********  FCUS      R               REFFLD(FCUS0 )                                    CSD027
     A            FCUS           6A
     A                                      TEXT('Facility customer number')
     A                                      COLHDG('Fclty               ' +
     A                                      'Cust.               ' +
     A                                      'Numb.')
      * End /CSD027/
     A            FTYP      R               REFFLD(FTYP0 )
     A            FSEQ      R
     A            PTYP      R
     A            CCY       R
     A            AMTP      R
     A            PRAM      R
     A            INTA      R
     A            WTXA      R
     A            FEAM      R
     A            FECD      R
     A            WOIN      R
     A            EXVD      R
     A*                                     /*S01125*/
     A***         ZZ021     R
     A*                                                    /S01270/
     A************ZZ017     R
     A            LTYP      R
     A            SUTP      R
     A            ZZ013     R
     A*                                                    /S01270/
     A            LCD       R
     A            ZZ001     R
     A*                                     /*S01125*/
      *                                              Begin  /CAC001/
     A            COFA      R               REFFLD(AABC)
     A                                      TEXT('Cost of Funds Amount')
     A                                      COLHDG('Cost of' +
     A                                             'Funds' +
     A                                             'Amount')
      *                                              End  /CAC001/
      *                                              Begin  /CLE003/
     A            CPAM      R
     A            AITC      R
     A            INTR      R
     A            AIAP      R
     A            IPRD      R
      *                                              End    /CLE003/
      *                                              Begin /CLE004/
     A            MNSG      R               REFFLD(SPIN)
     A                                      TEXT('Manual/System +
     A                                            Generated')
     A                                      COLHDG('Man/Sys' +
     A                                             'Gen.')
      *
     A            GASS      R               REFFLD(SPIN)
     A                                      TEXT('Generated by +
     A                                            Assignment')
     A                                      COLHDG('Assign?')
      *
     A            GPRT      R               REFFLD(SPIN)
     A                                      TEXT('Generated by +
     A                                            Principal Tfr')
     A                                      COLHDG('Prin' +
     A                                             'Tfr?')
      *
      **Start of change CLE148
     A**********  ROLN      R               REFFLD(LNRF)                                      CLE148
     A            ROLN           6A
      **End of change CLE148
     A                                      TEXT('Receiving/Originating +
     A                                            Loan')
     A                                      COLHDG('Rec/Orig' +
     A                                             'Loan')
      *                                              End   /CLE004/
      *                                              Begin  /157669/
     A            ACDA      R               REFFLD(AABC)
     A                                      TEXT('Accrued COF to Control Date')
     A                                      COLHDG('Accrued' +
     A                                             'Cost of Funds' +
     A                                             'to Control Date')
      *
     A            COFR      R               REFFLD(RTSP)
     A                                      TEXT('Cost of Fund Rate')
     A                                      COLHDG('Cost of' +
     A                                             'Fund' +
     A                                             'Rate')
      *
     A            ACAP      R               REFFLD(AABC)
     A                                      TEXT('Accrued COF Adjustmnt Posted')
     A                                      COLHDG('Accrued' +
     A                                             'Cost of Funds' +
     A                                             'Adjustment Posted')
      *
     A            COFD      R               REFFLD(AABC)
     A                                      TEXT('COF Paid/Received to Date')
     A                                      COLHDG('Cost of Funds' +
     A                                             'Paid/Received' +
     A                                             'to Date')
      *                                              End  /157669/
      *                                              Begin /CPB002/
     A            REPI           1A
     A                                      TEXT('Replication Indicator')
     A                                      COLHDG('Replication' 'Indicator')
      *                                              End   /CPB002/
      **Start of change CLE028
     A            CHDU      R               REFFLD(AMNT)
     A                                      TEXT('Charge Amount due for +
     A                                            the period')
     A                                      COLHDG('Charge Amount' +
     A                                             'due for the' +
     A                                             'period')
      **End of change CLE028
      **Start of change CAS005
     A            AITN      R               REFFLD(AITC)
     A                                      TEXT('AITC Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('AITC Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
     A            AIAN      R               REFFLD(AIAP)
     A                                      TEXT('AIAP Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('AIAP Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
     A            IPRN      R               REFFLD(IPRD)
     A                                      TEXT('IPRD Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('IPRD Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
     A            INTN      R               REFFLD(INTA)
     A                                      TEXT('INTA Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('INTA Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
     A            FSPRAM    R               REFFLD(PRAM)
     A                                      TEXT('PRAM Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('PRAM Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
     A            FSCPAM    R               REFFLD(CPAM)
     A                                      TEXT('CPAM Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('CPAM Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
      **End of change CAS005
     A*                                              Begin /CLE042/
     A            CLAS           4A         TEXT('Loan Class')
     A                                      COLHDG('Loan' 'Class')
     A*                                              End   /CLE042/
