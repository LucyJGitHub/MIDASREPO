      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE History rate detail')                         *
     A**************************************************************************
     A*
     A*  Midas - Customer Lending Module
     A*
     A*   PF/HISTSHQ    LOANS HISTORY FILE-RATES DETAIL
     A*
      *  (c) Finastra International Limited 2001                      *
     A*
      *  Last Amend No. CLE172             Date 01Oct20               *
      *  Prev Amend No. CSD103             Date 10Aug20               *
      *                 MD046248           Date 27Oct17               *
      *                 CLE148             Date 23Jul12               *
      *                 CRE073             Date 06Dec10               *
      *                 CLE143             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CAS005             Date 16Dec02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 157669             Date 04Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CLE004             Date 12Dec96               *
     A*                 CLE003             Date 05Dec96               *
     A*                 CAC001             Date 01Feb96               *
     A*                 S01392             Date 14Sep92               *
     A*                 S01117             DATE 19/06/87              *
     A*                      S01125           DATE 16/10/86
     A*
     A*----------------------------------------------------------------
     A*
      *  CLE172 - LIBOR Deregulation - Lending                        *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CRE073 - Interest Rate Rounding                              *
      *  CLE143 - Original Loan Contract Date & Effective Interest    *
      *           Rate                                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
     A*  157669 - Cost of Funds are being reported incorrectly        *
     A*           Add 4 new fields in order to keep a running total   *
     A*           of the cost of funds.                               *
     A*   CLE004  -  Customer Lending Enhancements - Syndications     *
     A*   CLE003  -  Customer Lending Backvaluation:                  *
     A*           Add 5 new fields in order to keep a running total   *
     A*           of the interest                                     *
     A*  CAC001 - Profit Centre Accounting Development:               *
     A*           New fields FSRP, FSGN, IRCF, FRCF, and FPRC added   *
     A*   S01392 - JAPANESE SUB MODULE ENHANCEMENTS                   *
     A*   S01117  -  MULTIBRANCHING
     A*   S01125  -  R21 FIX, MISSING FIELD ADDED
     A*
     A*************************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R HISTSHQF                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            RECI      R
     A            RCDT      R
     A*                                                   S01117
     A************BRCD      R
     A            BRCA      R
     A*                                                   S01117
      * Begin /CSD027/
     A**********  CNUM      R                                                                 CSD027
     A            CNUM           6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
      **Start of change CLE148
     A**********  LNRF      R                                                                 CLE148
     A            LNRF           6A         TEXT('Loan number')
     A                                      COLHDG('Loan' 'Number')
      **End of change CLE148
     A            VDAT      R               REFFLD(VDAT0 )
     A            PRSQ      R
     A            LASN      R               REFFLD(LASN0 )
      * Begin /CSD027/
     A**********  FCUS      R               REFFLD(FCUS0 )                                    CSD027
     A            FCUS           6A
     A                                      TEXT('Facility customer number')
     A                                      COLHDG('Fclty               ' +
     A                                      'Cust.               ' +
     A                                      'Numb.')
      * End /CSD027/
     A            FTYP      R               REFFLD(FTYP0 )
     A            FSEQ      R
     A            PTYP      R
     A            CCY       R
     A            AMTP      R
      **Start of change CSD103
     A**********  BRTT      R
     A            BRTT           2A         TEXT('Base rate code')
     A                                      COLHDG('Base rate code')
      **End of change CSD103
     A            BRTE      R
     A            RTSP      R
     A            SPIN      R
     A            CALC      R
     A            STAI      R
     A*                                     /*S01125*/
     A***         ZZ038     R
     A**                                                       /S01392/
     A************ZZ034     R
     A            PRINRO    R
     A            DISCRO    R
     A            ZZ020     R
     A**                                                       /S01392/
     A            LCD       R
     A            ZZ001     R
     A*                                     /*S01125*/
      *                                              Begin  /CAC001/
     A            FSRP      R               REFFLD(RTSP)
     A                                      TEXT('Funding Spread/Rate')
     A                                      COLHDG('Funding' +
     A                                             'Spread' +
     A                                             'Rate')
      *
     A            FSGN      R               REFFLD(SPIN)
     A                                      TEXT('Funding Sign')
     A                                      COLHDG('Funding' +
     A                                             'Sign')
      *
     A            IRCF      R               REFFLD(SPIN)
     A                                      TEXT('Interest Rate Change Flag')
     A                                      COLHDG('Interest Rate' +
     A                                             'Change' +
     A                                             'Flag')
      *
     A            FRCF      R               REFFLD(SPIN)
     A                                      TEXT('Funding Rate Change Flag')
     A                                      COLHDG('Funding Rate' +
     A                                             'Change' +
     A                                             'Flag')
      *
     A            FPRC      R               REFFLD(PRFC)
     A                                      TEXT('Funding Profit Centre')
     A                                      COLHDG('Funding' +
     A                                             'Profit' +
     A                                             'Centre')
      *                                              End  /CAC001/
      *                                              Begin  /CLE003/
     A            CPAM      R
     A            AITC      R
     A            INTR      R
     A            AIAP      R
     A            IPRD      R
      *                                              End    /CLE003/
      *                                              Begin /CLE004/
     A            MNSG      R               REFFLD(SPIN)
     A                                      TEXT('Manual/System +
     A                                            Generated')
     A                                      COLHDG('Man/Sys' +
     A                                             'Gen.')
      *
     A            RLCY      R               REFFLD(CCY)
     A                                      TEXT('MC New Currency')
     A                                      COLHDG('MC New' +
     A                                             'Ccy')
      *                                              End   /CLE004/
      *                                              Begin  /157669/
     A            ACDA      R               REFFLD(AABC)
     A                                      TEXT('Accrued COF to Control Date')
     A                                      COLHDG('Accrued' +
     A                                             'Cost of Funds' +
     A                                             'to Control Date')
      *
     A            COFR      R               REFFLD(RTSP)
     A                                      TEXT('Cost of Fund Rate')
     A                                      COLHDG('Cost of' +
     A                                             'Fund' +
     A                                             'Rate')
      *
     A            ACAP      R               REFFLD(AABC)
     A                                      TEXT('Accrued COF Adjustmnt Posted')
     A                                      COLHDG('Accrued' +
     A                                             'Cost of Funds' +
     A                                             'Adjustment Posted')
      *
     A            COFD      R               REFFLD(AABC)
     A                                      TEXT('COF Paid/Received to Date')
     A                                      COLHDG('Cost of Funds' +
     A                                             'Paid/Received' +
     A                                             'to Date')
      *                                              End  /157669/
      **Start of change CAS005
     A            AITN      R               REFFLD(AITC)
     A                                      TEXT('AITC Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('AITC Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
     A            AIAN      R               REFFLD(AIAP)
     A                                      TEXT('AIAP Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('AIAP Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
     A            IPRN      R               REFFLD(IPRD)
     A                                      TEXT('IPRD Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('IPRD Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
     A            FSCPAM    R               REFFLD(CPAM)
     A                                      TEXT('CPAM Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('CPAM Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
      **End of change CAS005
      **Start of change CLE143
     A            OCDT           5  0       TEXT('Original Contract +
     A                                           Date')
     A                                      COLHDG('Original Contract' +
     A                                             'Date')
     A            EAIR          15  8       TEXT('Effective Annual +
     A                                           Interest Rate')
     A                                      COLHDG('Effective Annual' +
     A                                             'Interest Rate')
      **End of change CLE143
      **Start of change CRE073
     A            CNSP          11P 7       TEXT('Contractual Spread')
     A                                      COLHDG('Contractual Spread')

     A            CNSG           1A         TEXT('Contractual Spread Sign')
     A                                      COLHDG('Contractual' +
     A                                             'Spread Sign')

     A            RDMT           7A         TEXT('Rounding Method')
     A                                      COLHDG('Rounding Method')

     A            RDFD           4A         TEXT('Rounding Fraction/Decimal')
     A                                      COLHDG('Rounding Fraction/' +
     A                                             'Decimal')
      **End of change CRE073
      **Start of change CLE172
     A            BLRT           1A         TEXT('Backward Looking +
     A                                            Rate')
     A                                      COLHDG('Backward' +
     A                                             'Looking' +
     A                                             'Rate')
     A            RFRR          10A         TEXT('Risk Free Rate')
     A                                      COLHDG('Risk' 'Free' +
     A                                             'Rate')
     A            CALM           4A         TEXT('Calculation Method')
     A                                      COLHDG('Calc' +
     A                                             'Method')
     A            BADJ          11P 7       TEXT('Benchmark +
     A                                            Adjustment')
     A                                      COLHDG('Benchmark' +
     A                                             'Adjustment')
     A                                      EDTCDE(L)
     A            FLOR          11P 7       TEXT('Floor')
     A                                      COLHDG('Floor')
     A                                      EDTCDE(L)
     A            LBDY           2S 0       TEXT('Lookback Days')
     A                                      COLHDG('Look' 'back' +
     A                                             'Days')
     A            LODY           2S 0       TEXT('Lockout Days')
     A                                      COLHDG('Lock' 'out' +
     A                                             'Days')
     A            OPSH           1A         TEXT('Observation +
     A                                            Period Shift')
     A                                      COLHDG('Observation'
     A                                             'Period' 'Shift')
     A            RRDP           2S 0       TEXT('Rate Rounding +
     A                                            Decimal Places')
     A                                      COLHDG('Rate' +
     a                                             'Rounding' +
     A                                             'Dec Places')
     A            DBAV           1S 0       TEXT('Day Basis for +
     A                                            Average')
     A                                      COLHDG('Day' 'Basis' +
     A                                             'Average')
     A            RTKN           1A         TEXT('Rates Known')
     A                                      COLHDG('Rates' +
     A                                             'Known')
      **End of change CLE172
