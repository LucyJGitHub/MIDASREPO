      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('MIdas LE Funding Participant Play/Record')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LE2021PF - Funding Participant                            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CLE031             Date 03Feb03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU004             Date 02Feb98               *
      *                 CLE004  *Create    Date 24Jan97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE031 - Lending Enhancements.                               *
      *  CEU004 - LE Settlement Ccy Conversion Upgrade to DBA R2.0    *
      *  CLE004 - Customer Lending Enhancements - Syndications        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LE2021D0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            DTST      R               REFFLD(ZZ008)
     A                                      TEXT('Date Stamp')
     A                                      COLHDG('Date Stamp')
      *
     A            TMST      R               REFFLD(ZZ008)
     A                                      TEXT('Time Stamp')
     A                                      COLHDG('Time Stamp')
      *
     A            S#ANUM    R               REFFLD(ZZ006)
     A                                      TEXT('Agent Cus No.')
     A                                      COLHDG('Agent' 'Cust')
     A            S#CRNM    R               REFFLD(ZZ020)
     A                                      TEXT('Report Name.')
     A                                      COLHDG('Report' 'Name')
     A            S#BRCA    R               REFFLD(BRCA)
     A            S#PMFC    R               REFFLD(ZZ006)
     A                                      TEXT('Prime Facil. Cust.')
     A                                      COLHDG('Prime' 'Fac.Cust')
     A            S#PMFT    R               REFFLD(ZZ003)
     A                                      TEXT('Prime Facil. Type')
     A                                      COLHDG('Prime' 'Fac.Type')
     A            S#PMFN    R               REFFLD(ZZ002)
     A                                      TEXT('Prime Facil. No.')
     A                                      COLHDG('Prime' 'Fac.No')
     A            S#FCCY    R               REFFLD(FCCY)
     A            S#SOLD    R               REFFLD(ZZ016)
     A                                      TEXT('Percentage Sold.')
     A                                      COLHDG('Percentage' 'Sold')
     A            W#ACTN    R               REFFLD(ZZ001)
     A                                      TEXT('Action')
     A                                      COLHDG('Action')
     A            S#CNUM    R               REFFLD(ZZ006)
     A                                      TEXT('Customer No.')
     A                                      COLHDG('Customer' 'No.')
     A            S#FACT    R               REFFLD(ZZ003)
     A                                      TEXT('Facil. Type')
     A                                      COLHDG('Facility' 'Type')
     A            S#FCNO    R               REFFLD(ZZ002)
     A                                      TEXT('Facility  No.')
     A                                      COLHDG('Facility' 'No.')
     A            S#CSSN    R               REFFLD(ZZ010)
     A                                      TEXT('Shortname')
     A                                      COLHDG('Shortname')
     A            S#JDTE    R               REFFLD(ZZ006)
     A                                      TEXT('Joining Date')
     A                                      COLHDG('Joining' 'Date')
     A            S#VDTC    R               REFFLD(ZZ006)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value' 'Date')
     A            S#SHTP    R               REFFLD(ZZ001)
     A                                      TEXT('Type')
     A                                      COLHDG('Type')
     A            S#SHAR    R               REFFLD(ZZ016)
     A                                      TEXT('Share Amount')
     A                                      COLHDG('Share' 'Amount')
     A            S#INUM    R               REFFLD(ZZ010)
     A                                      TEXT('Introducing Customer')
     A                                      COLHDG('Intro' 'Customer')
     A            S#RCSI    R               REFFLD(RCSI)
     A            S#SKIR    R               REFFLD(ZZ012)
     A                                      TEXT('Skim Interest Rate')
     A                                      COLHDG('Skim' 'Int.')
     A            S#SKIS    R               REFFLD(RCSI)
     A                                      TEXT('Skim Interest Rate Sign')
     A                                      COLHDG('Skm.' 'Int.Sgn.')
     A            S#SKFR    R               REFFLD(ZZ012)
     A                                      TEXT('Skim Fees Rate')
     A                                      COLHDG('Skm.' 'Fees')
     A            S#SKFS    R               REFFLD(RCSI)
     A                                      TEXT('Skim Interest Rate Sign')
     A                                      COLHDG('Skm.' 'Fee.Sgn.')
      *
     A            PMROBR    R               REFFLD(ZZ003)
     A            PMPOBR    R               REFFLD(ZZ003)
     A            PMPSTM    R               REFFLD(PSTM)
     A            PMRSTM    R               REFFLD(RSTM)
     A**Start of change CGL029
     A**********  PMRONS    R               REFFLD(RONS)                                      CGL029
     A            QQRONS        12A
     A**********  PMPONS    R               REFFLD(PONS)                                      CGL029
     A            QQPONS        12A
     A**End of change CGL029
     A            PMRIBA    R               REFFLD(RIBA)
     A            PMAWBN    R               REFFLD(FACO)
     A                                      TEXT('A/C With Bank No')
     A                                      COLHDG('A/C With' +
     A                                             'Bank No')
     A            PMRIBN    R               REFFLD(FACO)
     A                                      TEXT('Receipt - +
     A                                            Intermediary Bank')
     A                                      COLHDG('Receipt' +
     A                                             'Int Bank')
     A            PMBENN    R               REFFLD(FACO)
     A                                      TEXT('Beneficiary No')
     A                                      COLHDG('Beneficiary')
     A            PMBTB1    R               REFFLD(BTB1)
     A            PMBTB2    R               REFFLD(BTB2)
     A            PMBTB3    R               REFFLD(BTB3)
     A            PMBTB4    R               REFFLD(BTB4)
     A            PMBTB5    R               REFFLD(BTB5)
     A            PMBTB6    R               REFFLD(BTB6)
     A            PMCVMR    R               REFFLD(CVMR)
     A            PMROBN    R               REFFLD(ZZ010)
     A                                      TEXT('Receipt - Ordering +
     A                                            Bank No')
     A                                      COLHDG('Receipt' +
     A                                             'Ord Bank')
      *
     A            PMROCN    R               REFFLD(ZZ010)
     A                                      TEXT('Receipt - Ordering +
     A                                            Customer')
     A                                      COLHDG('Receipt' +
     A                                             'Ord Cust')
     A            PMPOBN    R               REFFLD(FACO)
     A                                      TEXT('Pay - Ordering Bank +
     A                                            No')
     A                                      COLHDG('Pay' +
     A                                             'Ord Bank')
     A            PMPOCN    R               REFFLD(FACO)
     A                                      TEXT('Pay - Ordering Bank +
     A                                            Customer')
     A                                      COLHDG('Pay' +
     A                                             'Ord Cust')
     A            PMPIBN    R               REFFLD(FACO)
     A                                      TEXT('Pay - Intermediary +
     A                                            Bank')
     A                                      COLHDG('Pay' +
     A                                             'Int Bank')
     A            PMPIBA    R               REFFLD(PIBA)
     A            PMRCRN    R               REFFLD(FACO)
     A                                      TEXT('Receiver Correspondent +
     A                                            No')
     A                                      COLHDG('Receiver' +
     A                                             'Correspondent')
     A            PMRCRA    R               REFFLD(RCRA)
     A            PMRVNO    R               REFFLD(FACO)
     A                                      TEXT('Receiver Number')
     A                                      COLHDG('Receiver' +
     A                                             'Number')
     A            PMAWBA    R               REFFLD(AWBA)
     A            PMBENA    R               REFFLD(BENA)
     A            PMDTP1    R               REFFLD(DTP1)
     A            PMDTP2    R               REFFLD(DTP2)
     A            PMDTP3    R               REFFLD(DTP3)
     A            PMDTP4    R               REFFLD(DTP4)
     A            PMDCHG    R               REFFLD(DCHG)
      *
     A            RMSV      R               REFFLD(SPIN)
     A                                      TEXT('Return Message +
     A                                            Severity')
     A                                      COLHDG('Ret Msg' +
     A                                             'Severity')
      *
     A            RMID      R               REFFLD(ZZ007)
     A                                      TEXT('Return Message +
     A                                            Id')
     A                                      COLHDG('Ret Msg Id')
      *
     A            RMTY      R               REFFLD(ZZ137)
     A                                      TEXT('Return Message +
     A                                            Text')
     A                                      COLHDG('Ret Msg' +
     A                                             'Text')
      *                                              Begin /CEU004/
     A            PMSCCY    R               REFFLD(CCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
     A            PMICCY    R               REFFLD(CCY)
     A                                      TEXT('IN Currency in +
     A                                            Fld 72')
     A                                      COLHDG('IN Ccy')
      *                                              End   /CEU004/
      **Start of change CLE031
     A            S#COAG         1A         TEXT('Co-Agent')
     A                                      COLHDG('Co-Agent')
      *
     A            S#FCAG         1A         TEXT('Facility Agent')
     A                                      COLHDG('Facility Agent')
      *
     A            S#PHAG         1A         TEXT('Paying/Handling Agent')
     A                                      COLHDG('Paying/Handling' 'Agent')
      *
     A            S#SCAG         1A         TEXT('Security Agent')
     A                                      COLHDG('Security Agent')
      *
     A            PMREXR        14A         TEXT('Receipt Settlement  +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receipt' 'Settlement'+
     A                                           'Currency' 'Exchange  Rate')
      *
     A            PMREXI         1A         TEXT('Receipt Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Receipt' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
      *
     A            PMPSCY         3A         TEXT('Payment Settlement Currency')
     A                                      COLHDG('Payment Settlement' +
     A                                           'Currency')
      *
     A            PMPEXR        14A         TEXT('Payment Settlement Currency +
     A                                           Exchange Rate')
     A                                      COLHDG('Payment Settlement' +
     A                                           'Currency' 'Exchange Rate')
      *
     A            PMPEXI         1A         TEXT('Payment Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Payment Settlement' +
     A                                           'Currency Exchange' +
     A                                           'Rate Ind')
      **End of change CLE031
     A**Start of change CGL029
     A            PMRONS        18A
     A            PMPONS        18A
     A**End of change CGL029
