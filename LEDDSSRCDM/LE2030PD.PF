      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Loans/Parts Purchased/Sold Record File')      *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LE2030PD - Loans/Parts Purchased & Sold Record File       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               b
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CLE036             Date 22Sep03               *
      *                 CGL029             Date 01Sep03               *
      *                 CLE034             Date 05May03               *
      *                 CLE031             Date 03Feb03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CLE011             Date 07Nov00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU004             Date 02Feb98               *
      *                 124001             Date 07Oct97               *
      *                 CLE004 *Create     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus                                          *
      *  CLE036 - Lending Enhancements for Nordea Phase 1 Priority 2: *
      *           Allow specific base rates                           *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE034 - Lending Enhancements Phase 1 Priority 1A            *
      *  CLE031 - Lending Enhancements.                               *
      *  CLE011 - Rate Fixing Days for Lending Rollover Processing    *
      *  CEU004 - LE Settlement Ccy Conversion Upgrade to DBA R2.0    *
      *  124001 - Include the field 'Mirror Loan Number'              *
      *  CLE004 - Customer Lending Enhancements - Syndications        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LE2030D0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            DTST      R               REFFLD(AWBN)
     A                                      TEXT('Date Stamp')
     A                                      COLHDG('Date Stamp')
      *
     A            TMST      R               REFFLD(AWBN)
     A                                      TEXT('Time Stamp')
     A                                      COLHDG('Time Stamp')
      *
     A            LNRF      R               REFFLD(ZZ006)
     A                                      TEXT('Loan Reference')
     A                                      COLHDG('Loan Ref')
      *
     A            ACTN      R               REFFLD(SPIN)
     A                                      TEXT('Action')
     A                                      COLHDG('Action')
      *
     A            CNMR      R               REFFLD(FACO)
     A                                      TEXT('Customer Reference')
     A                                      COLHDG('Customer' +
     A                                             'Reference')
      *
     A            BRCA      R
     A            BOKC      R
      *
     A            FCUS      R               REFFLD(ZZ006)
     A                                      TEXT('Facility Customer')
     A                                      COLHDG('Facility' +
     A                                             'Customer')
      *
     A            FCTN      R               REFFLD(ZZ005)
     A                                      TEXT('Facility Type/No')
     A                                      COLHDG('Facility' +
     A                                             'Type/No')
      *
     A            LTYP      R
     A            SUTP      R
     A            PTFR      R
      *
     A            DDAT      R               REFFLD(ZZ006)
     A                                      TEXT('Deal Date')
     A                                      COLHDG('Deal' +
     A                                             'Date')
      *
     A            STDT      R               REFFLD(ZZ006)
     A                                      TEXT('Start Date')
     A                                      COLHDG('Start' +
     A                                             'Date')
      *
     A            MDAT      R               REFFLD(ZZ006)
     A                                      TEXT('Maturity Date')
     A                                      COLHDG('Maturity' +
     A                                             'Date')
      *
     A            CCY       R
      *
     A            CPAM      R               REFFLD(CCNM)
     A                                      TEXT('Current Principal')
     A                                      COLHDG('Current Principal')
      *
     A            LASQ      R               REFFLD(LTYP)
     A                                      TEXT('Loan A/C Sequence +
     A                                            Number')
     A                                      COLHDG('Loan A/C' +
     A                                             'Seq No')
      *
     A            PRFC      R
     A            ORBR      R
      *
     A            CALC      R               REFFLD(SPIN)
     A                                      TEXT('Calculation Basis')
     A                                      COLHDG('Calc' +
     A                                             'Basis')
      *
     A            BRTT      R               REFFLD(LTYP)
     A                                      TEXT('Base Rate Code')
     A                                      COLHDG('Base' +
     A                                             'Rate')
      *
     A            RTSP      R               REFFLD(OSDA)
     A                                      TEXT('Rate/Spread')
     A                                      COLHDG('Rate/Spread')
      *
     A            SPIN      R
     A            CHIN      R
      *
     A            DISC      R               REFFLD(CCNM)
     A                                      TEXT('Interest/Discount +
     A                                            Amount')
     A                                      COLHDG('Interest/Discount' +
     A                                             'Amount')
      *
     A            RLDT      R               REFFLD(ZZ006)
     A                                      TEXT('Rollover Date')
     A                                      COLHDG('Rollover' +
     A                                             'Date')
      *
     A            RLFQ      R               REFFLD(SPIN)
     A                                      TEXT('Rollover Frequency')
     A                                      COLHDG('Roll' +
     A                                             'Freq')
      *
     A            RLDY      R               REFFLD(LTYP)
     A                                      TEXT('Rollover Day Number')
     A                                      COLHDG('Roll' +
     A                                             'Day')
      *
     A            MARG      R               REFFLD(AWBN)
     A                                      TEXT('Margin')
     A                                      COLHDG('Margin')
      *
     A            NEGV      R               REFFLD(SPIN)
     A                                      TEXT('Negative Interest +
     A                                            Indicator')
     A                                      COLHDG('Neg' +
     A                                             'Int')
      *
     A            NIDT      R               REFFLD(ZZ006)
     A                                      TEXT('Interest Date')
     A                                      COLHDG('Int Date')
      *
     A            IPFR      R               REFFLD(SPIN)
     A                                      TEXT('Interest Frequency')
     A                                      COLHDG('Int' +
     A                                             'Freq')
      *
     A            IFDY      R               REFFLD(LTYP)
     A                                      TEXT('Interest Day Number')
     A                                      COLHDG('Int' +
     A                                             'Day')
      *
     A            INTC      R
      *
     A            WTRT      R               REFFLD(OSDA)
     A                                      TEXT('Withholding Tax Rate')
     A                                      COLHDG('Int' +
     A                                             'Freq')
      *
     A            WTIN      R               REFFLD(SPIN)
     A                                      TEXT('Withholding Tax +
     A                                            Indicator')
     A                                      COLHDG('With' +
     A                                             'Tax')
      *
     A            REPT      R               REFFLD(SPIN)
     A                                      TEXT('Repayment Type')
     A                                      COLHDG('Repay' +
     A                                             'Type')
      *
     A            RPAM      R               REFFLD(CCNM)
     A                                      TEXT('Repayment Amount')
     A                                      COLHDG('Repayment Amt')
      *
     A            RPDT      R               REFFLD(ZZ006)
     A                                      TEXT('Repayment Date')
     A                                      COLHDG('Repay' +
     A                                             'Date')
      *
     A            RPFQ      R               REFFLD(RFRQ)
      *
     A            RPDY      R               REFFLD(LTYP)
     A                                      TEXT('Repayment Day Number')
     A                                      COLHDG('Repay' +
     A                                             'Day')
      *
     A            BTIN      R
      *
     A            BLDY      R               REFFLD(LTYP)
     A                                      TEXT('Billing Days')
     A                                      COLHDG('Bill' +
     A                                             'Days')
      *
     A            NACD      R               TEXT('Alternative Address Code')
     A            ACOF      R
      *
     A            SECI      R               REFFLD(SPIN)
     A                                      TEXT('Secured Indicator')
     A                                      COLHDG('Sec' +
     A                                             'Ind')
      *
     A            DOCI      R               REFFLD(SPIN)
     A                                      TEXT('Documents Indicator')
     A                                      COLHDG('Doc' +
     A                                             'Ind')
      *
     A            FEEI      R               REFFLD(SPIN)
     A                                      TEXT('Fees Indicator')
     A                                      COLHDG('Fees' +
     A                                             'Ind')
      *
     A            FRBC      R               REFFLD(ZZ005)
     A                                      TEXT('FRB Code')
     A                                      COLHDG('FRB' +
     A                                             'Code')
      *
     A            LIND      R               REFFLD(BRCA)
     A                                      TEXT('Industry Code')
     A                                      COLHDG('Ind' +
     A                                             'Code')
      *
     A            CRSK      R               TEXT('Country Code')
     A            AUTO      R
      *
     A            BCXR      R               REFFLD(CCNM)
     A                                      TEXT('Base Currency +
     A                                            Exchange Rate')
     A                                      COLHDG('Base Ccy' +
     A                                             'Exch Rate')
      *
     A            BMIN      R               REFFLD(SPIN)
     A                                      TEXT('Base Currency +
     A                                            Mult/Div Ind')
     A                                      COLHDG('Base Ccy' +
     A                                             'Mult/Div')
      *
     A            FCXR      R               REFFLD(CCNM)
     A                                      TEXT('Facility Ccy +
     A                                            Exchange Rate')
     A                                      COLHDG('Fac Ccy' +
     A                                             'Exch Rate')
      *
     A            FMIN      R               REFFLD(SPIN)
     A                                      TEXT('Facility Ccy +
     A                                            Mult/Div Ind')
     A                                      COLHDG('Fac Ccy' +
     A                                             'Mult/Div')
      *
     A            RECE      R               REFFLD(FACO)
     A                                      TEXT('Guarantee Receiver')
     A                                      COLHDG('Guarantee Rec')
      *
     A            COCU      R               REFFLD(ZZ006)
     A                                      TEXT('Customer Confirmation')
     A                                      COLHDG('Cust Conf')
      *
     A            APMI      R               REFFLD(AUTO)
     A                                      TEXT('Auto Pay at Maturity')
     A                                      COLHDG('Aut' +
     A                                             'Pay' +
     A                                             'Ind')
      *
     A            NARR1     R               REFFLD(NAR1)
     A            NARR2     R               REFFLD(NAR2)
     A            NARR3     R               REFFLD(NAR3)
     A            NARR4     R               REFFLD(NAR4)
     A            ADDI      R               TEXT('Accrue Indicator')
      *
     A            RCFC      R               REFFLD(SPIN)
     A                                      TEXT('Round Down Indicator')
     A                                      COLHDG('Round' +
     A                                             'Down')
      *
     A            ORGB      R               REFFLD(FACO)
     A                                      TEXT('Original Borrower')
     A                                      COLHDG('Original' +
     A                                             'Borrower')
      *
     A            OLNO      R               REFFLD(ZZ006)
     A                                      TEXT('Sale of Loan Number')
     A                                      COLHDG('Sale of' +
     A                                             'Loan')
      *
     A            RCSI      R               TEXT('Recourse Indicator')
      *
     A            FRSP      R               REFFLD(OSDA)
     A                                      TEXT('Funding Rate/Spread')
     A                                      COLHDG('Funding' +
     A                                             'Rate/Spread')
      *
     A            FSGN      R               REFFLD(SPIN)
     A                                      TEXT('Funding Sign')
     A                                      COLHDG('Funding' +
     A                                             'Sign')
      *
     A            FPRC      R               REFFLD(PRFC)
     A                                      TEXT('Funding Profit Centre')
     A                                      COLHDG('Funding' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            OSDB      R               REFFLD(OSDB)
     A                                      TEXT('Receipt - Settle +
     A                                            Branch')
     A                                      COLHDG('Rec Set' +
     A                                             'Branch')
      *
     A            OMDB      R               REFFLD(OMDB)
     A                                      TEXT('Pay - Settle +
     A                                            Branch')
     A                                      COLHDG('Pay Set' +
     A                                             'Branch')
      *
     A            RSTM      R               REFFLD(LTYP)
     A                                      TEXT('Receipt - Settlement +
     A                                            Method')
     A                                      COLHDG('Rec' +
     A                                             'Set')
     A**Start of change CGL029
     A**********  RONS      R                                                                 CGL029
      *
     A            RONSQQ        12A
     A**End of change CGL029
     A            RIBN      R               REFFLD(FACO)
     A                                      TEXT('Receipt - +
     A                                            Intermediary Bank')
     A                                      COLHDG('Receipt' +
     A                                             'Int Bank')
      *
     A            RIBA      R
      *
     A            ROBN      R               REFFLD(FACO)
     A                                      TEXT('Receipt - Ordering +
     A                                            Bank No')
     A                                      COLHDG('Receipt' +
     A                                             'Ord Bank')
      *
     A            ROCN      R               REFFLD(FACO)
     A                                      TEXT('Receipt - Ordering +
     A                                            Customer')
     A                                      COLHDG('Receipt' +
     A                                             'Ord Cust')
      *
     A            PSTM      R               REFFLD(LTYP)
     A                                      TEXT('Pay - Settlement +
     A                                            Method')
     A                                      COLHDG('Pay' +
     A                                             'Set')
      *
     A**Start of change CGL029
     A**********  PONS      R                                                                 CGL029
      *
     A            PONSQQ        12A
     A**End of change CGL029
     A            PIBN      R               REFFLD(FACO)
     A                                      TEXT('Pay - Intermediary +
     A                                            Bank')
     A                                      COLHDG('Pay' +
     A                                             'Int Bank')
      *
     A            PIBA      R
      *
     A            POBN      R               REFFLD(FACO)
     A                                      TEXT('Pay - Ordering Bank +
     A                                            No')
     A                                      COLHDG('Pay' +
     A                                             'Ord Bank')
      *
     A            POCN      R               REFFLD(FACO)
     A                                      TEXT('Pay - Ordering Bank +
     A                                            Customer')
     A                                      COLHDG('Pay' +
     A                                             'Ord Cust')
      *
     A            RCRN      R               REFFLD(FACO)
     A                                      TEXT('Receiver Correspondent +
     A                                            No')
     A                                      COLHDG('Receiver' +
     A                                             'Correspondent')
      *
     A            RCRA      R
      *
     A            RVNO      R               REFFLD(FACO)
     A                                      TEXT('Receiver Number')
     A                                      COLHDG('Receiver' +
     A                                             'Number')
      *
     A            AWBN      R               REFFLD(FACO)
     A                                      TEXT('A/C With Bank No')
     A                                      COLHDG('A/C With' +
     A                                             'Bank No')
      *
     A            AWBA      R
      *
     A            BENN      R               REFFLD(FACO)
     A                                      TEXT('Beneficiary No')
     A                                      COLHDG('Beneficiary')
      *
     A            BENA      R
      *
     A            DTP1      R
     A            DTP2      R
     A            DTP3      R
     A            DTP4      R
     A            DCHG      R
     A            BTB1      R
     A            BTB2      R
     A            BTB3      R
     A            BTB4      R
     A            BTB5      R
     A            BTB6      R
     A            CVMR      R
      *
     A            SPI1      R
     A            SPI2      R
     A            SPI3      R
      *
     A            DFTP      R               REFFLD(LTYP)
     A                                      TEXT('Default Part. Loan +
     A                                            Type')
     A                                      COLHDG('Def Part' +
     A                                             'Loan Type')
      *
     A            DFST      R               REFFLD(SUTP)
     A                                      TEXT('Default Part. Loan +
     A                                            Subtype')
     A                                      COLHDG('Def Part' +
     A                                             'Loan Styp')
      *
     A            MCRA      R               REFFLD(SPIN)
     A                                      TEXT('Multi-ccy Rollover +
     A                                            Allowed')
     A                                      COLHDG('Multi' +
     A                                             'Ccy')
      *
     A            FCRA      R               REFFLD(CCNM)
     A                                      TEXT('Facility Ccy +
     A                                            Repayment Amt')
     A                                      COLHDG('Facility Ccy' +
     A                                             'Repayment Amt')
      *
     A            PDDI      R               REFFLD(SPIN)
     A                                      TEXT('Payment Due Day +
     A                                            Indicator')
     A                                      COLHDG('Pay Due' +
     A                                             'Day Ind')
      *
     A            PTDI      R               REFFLD(SPIN)
     A                                      TEXT('Payment Taken Day +
     A                                            Indicator')
     A                                      COLHDG('Pay Taken' +
     A                                             'Day Ind')
      *
     A            GDPR      R               REFFLD(LTYP)
     A                                      TEXT('Grace Days for +
     A                                            Principal')
     A                                      COLHDG('Grace for' +
     A                                             'Principal')
      *
     A            GDIN      R               REFFLD(LTYP)
     A                                      TEXT('Grace Days for +
     A                                            Interest')
     A                                      COLHDG('Grace for' +
     A                                             'Interest')
      *
     A            LNKS      R               REFFLD(SPIN)
     A                                      TEXT('Linked to Syndicate')
     A                                      COLHDG('Linked to' +
     A                                             'Synd.?')
      *
     A            IUSR      R               REFFLD(FACO)
     A                                      TEXT('Insert User Profile')
     A                                      COLHDG('Ins User' +
     A                                             'Profile')
      *
     A            AUSR      R               REFFLD(FACO)
     A                                      TEXT('Amend User Profile')
     A                                      COLHDG('Amd User' +
     A                                             'Profile')
      *
     A            XUSR      R               REFFLD(FACO)
     A                                      TEXT('Authorise User +
     A                                            Profile')
     A                                      COLHDG('Auth User' +
     A                                             'Profile')
      *
     A            RMSV      R               REFFLD(SPIN)
     A                                      TEXT('Return Message +
     A                                            Severity')
     A                                      COLHDG('Ret Msg' +
     A                                             'Severity')
      *
     A            RMID      R               REFFLD(ZZ007)
     A                                      TEXT('Return Message +
     A                                            Id')
     A                                      COLHDG('Ret Msg Id')
      *
     A            RMTX      R               REFFLD(ZZ078)
     A                                      TEXT('Return Message +
     A                                            Text')
     A                                      COLHDG('Ret Msg' +
     A                                             'Text')
      *                                              Begin /124001/
     A            MLNR      R               REFFLD(ZZ006)
     A                                      TEXT('Mirror Loan Number')
     A                                      COLHDG('Mirror' +
     A                                             'Loan')
      *                                              End   /124001/
      *                                              Begin /CEU004/
     A            SCCY      R               REFFLD(CCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
     A            ICCY      R               REFFLD(CCY)
     A                                      TEXT('IN Currency in +
     A                                            Fld 72')
     A                                      COLHDG('IN Ccy')
      *                                              End   /CEU004/
     A**Start of Change CLE011
     A            RFDY      R               REFFLD(RCVD)
     A                                      TEXT('Rollover Rate Fixing +
     A                                            Days')
     A                                      COLHDG('Rollover Rate' +
     A                                             'Fixing Days')
     A**End of Change CLE011
      **Start of change CLE031
     A            FPAM          14A         TEXT('Facility Currency Principal +
     A                                           Amount')
     A                                      COLHDG('Facility Currency' +
     A                                           'Principal Amount')
      *
     A            REXR          14A         TEXT('Receive Settlement  +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange  Rate')
      *
     A            REXI           1A         TEXT('Receive Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
      *
     A            PSCY           3A         TEXT('Pay Settlement Currency')
     A                                      COLHDG('Pay Settlement' 'Currency')
      *
     A            PEXR          14A         TEXT('Pay Settlement Currency +
     A                                           Exchange Rate')
     A                                      COLHDG('Pay Settlement' +
     A                                           'Currency' 'Exchange Rate')
      *
     A            PEXI           1A         TEXT('Pay Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Pay Settlement' 'Currency'+
     A                                           'Exchange' 'Rate Ind')
      **End of change CLE031
      **Start of Change CLE034
     A            STAL           1A
     A                                      TEXT('Settlement Allocation +
     A                                           Indicator')
     A                                      COLHDG('Settlement' +
     A                                           'Allocation Indicator')
      **End of Change CLE034
     A**Start of change CGL029
     A            RONS          18A
     A            PONS          18A
     A**End of change CGL029
      **Start of change CLE036
     A            BASR          12A         TEXT('Base Rate')
     A                                      COLHDG('Base Rate')
      **End of change CLE036
      ** Start of change CER020
     A            LOIC           3A         TEXT('Local Industry Code')
     A                                      COLHDG('Local Industry' +
     A                                             'Code')
      ** End of change CER020
