      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Loan Amendments')                             *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LE2040PD - Loan Amendments Record File                    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CGL029             Date 01Sep03               *
      *                 CLE034             Date 05May03               *
      *                 CLE031             Date 03Feb03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU004             Date 02Feb98               *
      *                 CLE004 *Create     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE034 - Lending Enhancements Phase 1 Priority 1A            *
      *  CLE031 - Lending Enhancements.                               *
      *  CEU004 - LE Settlement Ccy Conversion Upgrade to DBA R2.0    *
      *  CLE004 - Customer Lending Enhancements - Syndications        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LE2040D0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            DTST      R               REFFLD(AWBN)
     A                                      TEXT('Date Stamp')
     A                                      COLHDG('Date Stamp')
     A            TMST      R               REFFLD(AWBN)
     A                                      TEXT('Time Stamp')
     A                                      COLHDG('Time Stamp')
      **Start of change CLE148
     A**********  LNRF      R                                                                 CLE148
     A            LNRF           6A         TEXT('Loan number')
     A                                      COLHDG('Loan' 'Number')
      **End of change CLE148
     A            ACTN      R               REFFLD(SPIN)
     A                                      TEXT('Action')
     A                                      COLHDG('Action')
     A            AMTP      R
     A            VDAT      R               REFFLD(VDAT0 )
     A            LASN      R               REFFLD(LASN0 )
      * Begin /CSD027/
     A**********  CNUM      R                                                                 CSD027
     A            CNUM           6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            LTYP      R
     A            SUTP      R
     A            LSMD      R
      ** Start of change CSD103
     A**********  BRTT      R
     A            BRTT           2A
      ** End of change CSD103
     A            RTSP      R
     A            CCY       R
     A            TAMT      R
     A            STAI      R
     A            FSRP      R               REFFLD(RTSP)
     A                                      TEXT('Funding Spread/Rate')
     A                                      COLHDG('Funding Sprd/Rate')
     A            DFTP      R               REFFLD(LTYP)
     A                                      TEXT('Default Part. Loan +
     A                                            Type')
     A                                      COLHDG('Def Part' +
     A                                             'Loan Type')
     A            DFST      R               REFFLD(SUTP)
     A                                      TEXT('Default Part. Loan +
     A                                            Subtype')
     A                                      COLHDG('Def Part' +
     A                                             'Loan Styp')
     A            SPI1      R
     A            SPI2      R
     A            SPI3      R
     A            RSTM      R
     A**Start of change CGL029
     A**********  RONS      R                                                                 CGL029
     A            RONSQQ        12A
     A**End of change CGL029
     A            RIBN      R
     A            RIBA      R
      * Begin /CSD027/
     A**********  ROBN      R                                                                 CSD027
     A**********  ROCN      R                                                                 CSD027
     A            ROBN           6A
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            ROCN           6A
     A                                      TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            PSTM      R
     A**Start of change CGL029
     A**********  PONS      R                                                                 CGL029
     A            PONSQQ        12A
     A**End of change CGL029
     A            PIBN      R
     A            PIBA      R
      * Begin /CSD027/
     A**********  POBN      R                                                                 CSD027
     A**********  POCN      R                                                                 CSD027
     A            POBN           6A
     A                                      TEXT('pay - ordering bank no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            POCN           6A
     A                                      TEXT('pay - ordering customer no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            RCRN      R
     A            RCRA      R
     A            RVNO      R
     A            AWBN      R
     A            AWBA      R
     A            BENN      R
     A            BENA      R
     A            DTP1      R
     A            DTP2      R
     A            DTP3      R
     A            DTP4      R
     A            DCHG      R
     A            BTB1      R
     A            BTB2      R
     A            BTB3      R
     A            BTB4      R
     A            BTB5      R
     A            BTB6      R
     A            CVMR      R
     A            ASTS      R               REFFLD(SPIN)
     A                                      TEXT('Loan Amend Status +
     A                                            Indicator')
     A                                      COLHDG('Status')
     A            IUSR      R               REFFLD(FACO)
     A                                      TEXT('Insert User Profile')
     A                                      COLHDG('Ins User' +
     A                                             'Profile')
     A            AUSR      R               REFFLD(FACO)
     A                                      TEXT('Amend User Profile')
     A                                      COLHDG('Amd User' +
     A                                             'Profile')
     A            XUSR      R               REFFLD(FACO)
     A                                      TEXT('Authorise User +
     A                                            Profile')
     A                                      COLHDG('Auth User' +
     A                                             'Profile')
     A            MSVR      R               REFFLD(SPIN)
     A                                      TEXT('Message Severity')
     A                                      COLHDG('Message' +
     A                                             'Severity')
     A            MSID      R               REFFLD(ZZ007)
     A                                      TEXT('Message ID')
     A                                      COLHDG('Message ID')
     A            MSTX      R               REFFLD(ZZ078)
     A                                      TEXT('Message Text')
     A                                      COLHDG('Message Text')
      *                                              Begin /CEU004/
     A            SCCY      R               REFFLD(CCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
     A            ICCY      R               REFFLD(CCY)
     A                                      TEXT('IN Currency in +
     A                                            Fld 72')
     A                                      COLHDG('IN Ccy')
      *                                              End   /CEU004/
      **Start of change CLE031
     A            REXR          14A         TEXT('Receive Settlement  +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange  Rate')
      *
     A            REXI           1A         TEXT('Receive Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
      *
     A            PSCY           3A         TEXT('Pay Settlement Currency')
     A                                      COLHDG('Pay Settlement' 'Currency')
      *
     A            PEXR          14A         TEXT('Pay Settlement Currency +
     A                                           Exchange Rate')
     A                                      COLHDG('Pay Settlement' +
     A                                           'Currency' 'Exchange Rate')
      *
     A            PEXI           1A         TEXT('Pay Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Pay Settlement' 'Currency'+
     A                                           'Exchange' 'Rate Ind')
      **End of change CLE031
      **Start of Change CLE034
     A            STAL           1A
     A                                      TEXT('Settlement Allocation +
     A                                           Indicator')
     A                                      COLHDG('Settlement' +
     A                                           'Allocation Indicator')
      **End of Change CLE034
     A**Start of change CGL029
     A            RONS          18A
     A            PONS          18A
     A**End of change CGL029
