      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Rollover Records File')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LE2070PD - Rollover Records File                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE036             Date 22Sep03               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CLE034             Date 05May03               *
      *                 CLE031             Date 03Feb03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU004             Date 02Feb98               *
      *                 120025             Date 25Sep97               *
      *                 123335             Date 24Sep97               *
      *                 CLE004 *Create     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE036 - Lending Enhancements for Nordea Phase 1 Priority 2: *
      *           Allow specific base rates                           *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE034 - Lending Enhancements Phase 1 Priority 1A            *
      *  CLE031 - Lending Enhancements.                               *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *  CEU004 - LE Settlement Ccy Conversion Upgrade to DBA R2.0    *
      *  120025 - Include pay settlements for multi-currency rollover *
      *  123335 - Include new base currency exchange rate/indicator   *
      *  CLE004 - Customer Lending Enhancements - Syndications        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LE2070D0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            DTST      R               REFFLD(AWBN)
     A                                      TEXT('Date Stamp')
     A                                      COLHDG('Date Stamp')
     A            TMST      R               REFFLD(AWBN)
     A                                      TEXT('Time Stamp')
     A                                      COLHDG('Time Stamp')
      *
     A            LNRF      R               REFFLD(ZZ006)
     A                                      TEXT('Loan Reference')
     A                                      COLHDG('Loan Ref')
     A            ACTN      R               REFFLD(SPIN)
     A                                      TEXT('Action Code')
     A                                      COLHDG('Action' +
     A                                             'Code')
     A            RSTS      R               REFFLD(SPIN)
     A                                      TEXT('Rollover Status Indicator')
     A                                      COLHDG('Status')
     A            CNMR      R               REFFLD(FACO)
     A                                      TEXT('Customer Reference')
     A                                      COLHDG('Customer' +
     A                                             'Reference')
      *
     A            RLDT      R               REFFLD(ZZ006)
     A                                      TEXT('Rollover Date')
     A                                      COLHDG('Rollover' +
     A                                             'Date')
     A            RLFQ      R               REFFLD(SPIN)
     A                                      TEXT('Rollover Frequency')
     A                                      COLHDG('Rollover' +
     A                                             'Freq')
     A            RLDY      R               REFFLD(LTYP)
     A                                      TEXT('Rollover Day No.')
     A                                      COLHDG('Rollover' +
     A                                             'Day No')
      *
     A            NBRA      R               REFFLD(LTYP)
     A                                      TEXT('Next Base Rate Code')
     A                                      COLHDG('Next' +
     A                                             'Base Rate')
     A            NRTS      R               REFFLD(OSDA)
     A                                      TEXT('Next Spread/Rate')
     A                                      COLHDG('Next' +
     A                                             'Spread/Rate')
     A            NSIN      R               REFFLD(SPIN)
     A                                      TEXT('Next Spread Indicator')
     A                                      COLHDG('Next' +
     A                                             'Spread Ind')
     A            NCAS      R               REFFLD(SPIN)
     A                                      TEXT('Next Calculation Basis')
     A                                      COLHDG('Next' +
     A                                             'Calc Basis')
     A            NCHI      R               REFFLD(CHIN)
     A                                      TEXT('New Change Indicator')
     A                                      COLHDG('New Chg' +
     A                                             'Ind')
     A            NROD      R               REFFLD(ZZ006)
     A                                      TEXT('Next Rollover Date')
     A                                      COLHDG('Next Rollover' +
     A                                             'Date')
      *
     A            NPRAM     R               REFFLD(CCNM)
     A                                      TEXT('New Current Principal')
     A                                      COLHDG('New Curr. Principal')
     A            NDAM      R               REFFLD(CCNM)
     A                                      TEXT('Discount Amount')
     A                                      COLHDG('Discount Amount')
      *
     A            NIPD      R               REFFLD(ZZ006)
     A                                      TEXT('Interest Date')
     A                                      COLHDG('Int Date')
     A            NMDT      R               REFFLD(ZZ006)
     A                                      TEXT('Maturity Date')
     A                                      COLHDG('Mat Date')
      *
     A            NCCY      R               REFFLD(CCY)
     A                                      TEXT('New Currency')
     A                                      COLHDG('New'+
     A                                             'Ccy')
     A            NCPA      R               REFFLD(CCNM)
     A                                      TEXT('New Ccy Principal Amount')
     A                                      COLHDG('New Currency' +
     A                                             'Principal Amt')
     A            NFCE      R               REFFLD(CCNM)
     A                                      TEXT('New Fclty Ccy Exch Rate')
     A                                      COLHDG('New Fclty Ccy' +
     A                                             'Exchange Rate')
     A            NFMD      R               REFFLD(SPIN)
     A                                      TEXT('New Fclty Ccy Mult/Div Ind')
     A                                      COLHDG('New Fclty Ccy' +
     A                                             'Mult/Div Ind')
     A            NLRA      R               REFFLD(CCNM)
     A                                      TEXT('New Loan Ccy Repayment +
     A                                            Amount')
     A                                      COLHDG('New Loan Ccy' +
     A                                             'Repay Amount')
      *
     A            SPI1      R               REFFLD(SPI1)
     A                                      TEXT('Special Instructions - 1')
     A                                      COLHDG('Special Ins - 1')
     A            SPI2      R               REFFLD(SPI2)
     A                                      TEXT('Special Instructions - 2')
     A                                      COLHDG('Special Ins - 2')
     A            SPI3      R               REFFLD(SPI3)
     A                                      TEXT('Special Instructions - 3')
     A                                      COLHDG('Special Ins - 3')
      *
     A            NFRS      R               REFFLD(OSDA)
     A                                      TEXT('New funding spread/rate')
     A                                      COLHDG('New funding' +
     A                                             'Spread/Rate')
     A            NFSN      R               REFFLD(SPIN)
     A                                      TEXT('New funding sign')
     A                                      COLHDG('New funding' +
     A                                             'Sign')
     A            NFPC      R               REFFLD(PRFC)
     A                                      TEXT('New fundg profit centre')
     A                                      COLHDG('New funding' +
     A                                             'profit centre')
      *
     A            RRST      R               REFFLD(LTYP)
     A                                      TEXT('Receipt - Settlement +
     A                                            Method')
     A                                      COLHDG('Rec' +
     A                                             'Set')
     A**Start of change CGL029
     A**********  RRON      R               REFFLD(RONS)                                      CGL029
      *
     A            RRONQQ        12A
     A**End of change CGL029
     A            RRIB      R               REFFLD(FACO)
     A                                      TEXT('Receipt - +
     A                                            Intermediary Bank')
     A                                      COLHDG('Receipt' +
     A                                             'Int Bank')
      *
     A            RRBA      R               REFFLD(RIBA)
      *
     A            RROB      R               REFFLD(FACO)
     A                                      TEXT('Receipt - Ordering +
     A                                            Bank No')
     A                                      COLHDG('Receipt' +
     A                                             'Ord Bank')
      *
     A            RROC      R               REFFLD(FACO)
     A                                      TEXT('Receipt - Ordering +
     A                                            Customer')
     A                                      COLHDG('Receipt' +
     A                                             'Ord Cust')
      *
     A            RIUS      R               REFFLD(FACO)
     A                                      TEXT('Rollover Insert User Profile')
     A                                      COLHDG('Rollover Ins User' +
     A                                             'Profile')
      *
     A            RAUS      R               REFFLD(FACO)
     A                                      TEXT('Rollover Amend User Profile')
     A                                      COLHDG('Rollover Amd User' +
     A                                             'Profile')
      *
     A            RXUS      R               REFFLD(FACO)
     A                                      TEXT('Rollover Authorise User +
     A                                            Profile')
     A                                      COLHDG('Rollover Auth User' +
     A                                             'Profile')
      *
     A            RMSV      R               REFFLD(SPIN)
     A                                      TEXT('Return Message +
     A                                            Severity')
     A                                      COLHDG('Ret Msg' +
     A                                             'Severity')
      *
     A            RMID      R               REFFLD(ZZ007)
     A                                      TEXT('Return Message +
     A                                            Id')
     A                                      COLHDG('Ret Msg Id')
      *
     A            RMTX      R               REFFLD(ZZ137)
     A                                      TEXT('Return Message +
     A                                            Text')
     A                                      COLHDG('Ret Msg' +
     A                                             'Text')
      *                                              Begin /123335/
     A            NBCE      R               REFFLD(CCNM)
     A                                      TEXT('New Base Ccy Exch Rate')
     A                                      COLHDG('New Base Ccy' +
     A                                             'Exchange Rate')
     A            NBMD      R               REFFLD(SPIN)
     A                                      TEXT('New Base Ccy Mult/Div Ind')
     A                                      COLHDG('New Base Ccy' +
     A                                             'Mult/Div Ind')
      *                                              End   /123335/
      *                                              Begin /120025/
     A            RPST      R               REFFLD(LTYP)
     A                                      TEXT('Pay - Settlement     +
     A                                            Method')
     A                                      COLHDG('Pay' +
     A                                             'Set')
     A**Start of change CGL029
     A**********  RPON      R               REFFLD(PONS)                                      CGL029
      *
     A            RPONQQ        12A
     A**End of change CGL029
     A            RPIB      R               REFFLD(PIBN)
     A                                      TEXT('Pay - +
     A                                            Intermediary Bank')
     A                                      COLHDG('Receipt' +
     A                                             'Int Bank')
      *
     A            RPBA      R               REFFLD(PIBA)
      *
     A            RPOB      R               REFFLD(FACO)
     A                                      TEXT('Pay - Ordering +
     A                                            Bank No')
     A                                      COLHDG('Receipt' +
     A                                             'Ord Bank')
      *
     A            RPOC      R               REFFLD(FACO)
     A                                      TEXT('Pay - Ordering +
     A                                            Customer')
     A                                      COLHDG('Receipt' +
     A                                             'Ord Cust')
      *
     A            RRCR      R               REFFLD(RCRN)
     A            RRCA      R               REFFLD(RCRA)
     A            RRVN      R               REFFLD(RVNO)
     A            RAWB      R               REFFLD(AWBN)
     A            RAWA      R               REFFLD(AWBA)
     A            RBEN      R               REFFLD(BENN)
     A            RBNA      R               REFFLD(BENA)
     A            RDP1      R               REFFLD(DTP1)
     A            RDP2      R               REFFLD(DTP2)
     A            RDP3      R               REFFLD(DTP3)
     A            RDP4      R               REFFLD(DTP4)
     A            RDCH      R               REFFLD(DCHG)
     A            RBB1      R               REFFLD(BTB1)
     A            RBB2      R               REFFLD(BTB2)
     A            RBB3      R               REFFLD(BTB3)
     A            RBB4      R               REFFLD(BTB4)
     A            RBB5      R               REFFLD(BTB5)
     A            RBB6      R               REFFLD(BTB6)
     A            RCVM      R               REFFLD(CVMR)
      *                                              End   /120025/
      *                                              Begin /CEU004/
     A            RRSC      R               REFFLD(CCY)
     A                                      TEXT('Roll Rec Settlement +
     A                                            Ccy')
     A                                      COLHDG('Roll Rec Sett Ccy')
     A            RPSC      R               REFFLD(CCY)
     A                                      TEXT('Roll Pay Settlement +
     A                                            Ccy')
     A                                      COLHDG('Roll Pay Sett Ccy')
     A            RICY      R               REFFLD(CCY)
     A                                      TEXT('Roll IN Ccy in +
     A                                            Fld 72')
     A                                      COLHDG('Roll IN Ccy')
      *                                              End   /CEU004/
      **Start of change CAS001
     A            RFYLDC         5A         TEXT('Forward Yield Curve for +
     A                                      Rollover')
     A                                      COLHDG('Fwd Yld Curve for' +
     A                                             'Rollover')
      **End of change CAS001
      **Start of change CLE031
     A            RRSR          14A         TEXT('Receipt Currency +
     A                                           Exchange Rate')
     A                                      COLHDG('Receipt Currency' +
     A                                            'Exchange Rate')
      *
     A            RRSI           1A         TEXT('Receipt Currency Exchange +
     A                                           Rate Indicator')
     A                                      COLHDG('Receipt Currency' +
     A                                           'Exchange Rate' 'Indicator')
      *
     A            RPSR          14A         TEXT('Payment Currency +
     A                                           Exchange Rate')
     A                                      COLHDG('Payment Currency' +
     A                                            'Exchange Rate')
      *
     A            RPSI           1A         TEXT('Payment Currency Exchange +
     A                                           Rate Indicator')
     A                                      COLHDG('Payment Currency' +
     A                                           'Exchange Rate' 'Indicator')
      **End of change CLE031
      **Start of Change CLE034
     A            STAL           1A
     A                                      TEXT('Settlement Allocation +
     A                                           Indicator')
     A                                      COLHDG('Settlement' +
     A                                           'Allocation Indicator')
      **End of Change CLE034
     A**Start of change CGL029
     A            RRON          18A
     A            RPON          18A
     A**End of change CGL029
      **Start of change CLE036
     A            NBRT          12A         TEXT('New Base Rate')
     A                                      COLHDG('New Base Rate')
      **End of change CLE036
