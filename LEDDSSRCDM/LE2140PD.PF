      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('MIdas LE Manual Utilisation Test-harness')
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  PF/LEMNFUPD - Manual Utilisation Transaction                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. AR674226           Date 04Dec19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 148360             Date 25Nov98               *
      *                 CLE005 *C *CREATE  Date 22May97               *
      *                 Xnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR674226 - Manual transaction reached 999. Increase the size *
      *             of TFSEQ to accomodate 9999 transactions.         *
      *             (Child: AR674227)                                 *
      *           - Applied for MD054782.                             *
      *  MD046248 - Finastra Rebranding                               *
      *  148360 - Could not enter a negative utilisation amount.      *
      *  CLE005 - Customer Lending Enhancements - Others              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LE2140D0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            TDTST     R               REFFLD(AWBN)
     A                                      TEXT('Date Stamp')
     A                                      COLHDG('Date Stamp')
     A            TTMST     R               REFFLD(AWBN)
     A                                      TEXT('Time Stamp')
     A                                      COLHDG('Time Stamp')
     A            TACTN     R               REFFLD(SPIN)
     A                                      TEXT('Action')
     A                                      COLHDG('Action')
     A            TCNUM     R               REFFLD(RINO)
     A                                      TEXT('Facility Customer +
     A                                      Number')
     A                                      COLHDG('Fclty Cust.' +
     A                                             'No./Name')
     A            TFTYP     R               REFFLD(CCY)
     A                                      TEXT('Facility Type')
     A                                      COLHDG('Fclty Type')
     A            TFNUM     R               REFFLD(ACCS)
     A                                      TEXT('Facility Number')
     A                                      COLHDG('Fclty No.')
      **Start of Change AR674226
     A**********  TFSEQ     R               REFFLD(CCY)                                     AR674226
     A**********                            TEXT('Utilisation +                             AR674226
     A**********                            Sequence No.')                                  AR674226
     A**********                            COLHDG('Util. Seq.' +                           AR674226
     A**********                                   'No.')                                   AR674226
     A            QQSEQ          3A
      **End of Change AR674226
      *
     A            TUCCY     R               REFFLD(CCY)
     A                                      TEXT('Utilisation +
     A                                      Currency')
     A                                      COLHDG('Util. Ccy')
     A            TUAMT     R               REFFLD(BCNM)
     A                                      TEXT('Utilisation Amount')
     A                                      COLHDG('Util. Amount')
     A            TSTDT     R               REFFLD(RINO)
     A                                      TEXT('Start Date')
     A                                      COLHDG('Start Date')
     A            TEXDT     R               REFFLD(RINO)
     A                                      TEXT('Expiry Date')
     A                                      COLHDG('Expiry Date')
     A            TUXRT     R               REFFLD(ACFT)
     A                                      TEXT('Utilisation Exch+
     A                                      ange Rate')
     A                                      COLHDG('Util. Exch' +
     A                                             'Rate')
     A            TUXID     R               REFFLD(ACCL)
     A                                      TEXT('Utilisation Exch+
     A                                      ange Ind.')
     A                                      COLHDG('Util. Exch.' +
     A                                             'Ind.')
     A            TUNAR     R               REFFLD(TRNA)
     A            TIUSR     R               REFFLD(CTOW)
     A                                      TEXT('Insert User')
     A                                      COLHDG('Insert' +
     A                                             'User')
     A            TAUSR     R               REFFLD(CTOW)
     A                                      TEXT('Amend User')
     A                                      COLHDG('Amend' +
     A                                             'User')
     A            TXUSR     R               REFFLD(CTOW)
     A                                      TEXT('Authorise User')
     A                                      COLHDG('Auth.' +
     A                                             'User')
     A            TMSVR     R               REFFLD(SPIN)
     A                                      TEXT('Message Severity')
     A                                      COLHDG('Message' +
     A                                             'Severity')
     A            TMSID     R               REFFLD(ZZ007)
     A                                      TEXT('Message ID')
     A                                      COLHDG('Message ID')
     A            TMSTX     R               REFFLD(ZZ078)
     A                                      TEXT('Message Text')
     A                                      COLHDG('Message Text')
      *                                              Begin /148360/
     A            TAMTS     R               REFFLD(SPIN)
     A                                      TEXT('Utilisation Amount Sign')
     A                                      COLHDG('Util Amt Sign')
      *                                              End   /148360/
      **Start of Change AR674226
     A            TFSEQ          4A
     A                                      TEXT('Utilisation +
     A                                      Sequence No.')
     A                                      COLHDG('Util. Seq.' +
     A                                             'No.')
      **End of Change AR674226
