      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Fees Input Maintenance')                      *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LE2210PD - Fees Input Maintenance                         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CAS016             Date 28Feb06               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      *                 CLE034             Date 05May03               *
      *                 CLE031             Date 03Feb03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU004             Date 02Feb98               *
      *                 CLE005 *Create     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing                      *
      *  CAS016 - IAS18 EIR Recalculation (Fee Amortisation Over      *
      *           Whole Period)                                       *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE034 - Lending Enhancements Phase 1 Priority 1A            *
      *  CLE031 - Lending Enhancements.                               *
      *  CEU004 - LE Settlement Ccy Conversion Upgrade to DBA R2.0    *
      *  CLE005 - Customer Lending Enhancements - Others              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LE2210D0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            DTST      R               REFFLD(AWBN)
     A                                      TEXT('Date Stamp')
     A                                      COLHDG('Date Stamp')
     A            TMST      R               REFFLD(AWBN)
     A                                      TEXT('Time Stamp')
     A                                      COLHDG('Time Stamp')
     A            ACTN      R               REFFLD(SPIN)
     A                                      TEXT('Action')
     A                                      COLHDG('Action')
     A            THBRCA    R               REFFLD(BRCA)
     A            THCNUM    R               REFFLD(ZZ006)
     A                                      TEXT('Customer number')
     A                                      COLHDG('Cust No')
     A            THFACL    R               REFFLD(ZZ005)
     A                                      TEXT('Facility')
     A                                      COLHDG('Facility')
     A            THLOAN    R               REFFLD(ZZ006)
     A                                      TEXT('Loan number')
     A                                      COLHDG('Loan no')
     A            THFSEQ    R               REFFLD(ZZ002)
     A                                      TEXT('Fee Sequence no')
     A                                      COLHDG('Seq')
     A            THFCOD    R               REFFLD(ZZ002)
     A                                      TEXT('Fee Code')
     A                                      COLHDG('Fee' 'code')
     A            THFCCY    R               REFFLD(CCY)
     A            THFAMT    R               REFFLD(ZZ014)
     A                                      TEXT('Fee Amount')
     A                                      COLHDG('Fee Amount')
     A            THFSGN    R               REFFLD(SPIN)
     A                                      TEXT('Sign')
     A                                      COLHDG('Sign')
     A            THPSTD    R               REFFLD(ZZ006)
     A                                      TEXT('Start Date')
     A                                      COLHDG('Start Date')
     A            THPEND    R               REFFLD(ZZ006)
     A                                      TEXT('End Date')
     A                                      COLHDG('End Date')
     A            THPYON    R               REFFLD(FEPYON)
     A            THAUTO    R               REFFLD(AUTO)
     A            THCALT    R               REFFLD(FECALT)
     A            THFRT1    R               REFFLD(ZZ008)
     A                                      TEXT('Fee Rate 1')
     A                                      COLHDG('Rate 1')
     A            THFRT2    R               REFFLD(ZZ008)
     A                                      TEXT('Fee Rate 2')
     A                                      COLHDG('Rate 2')
     A            THFRT3    R               REFFLD(ZZ008)
     A                                      TEXT('Fee Rate 3')
     A                                      COLHDG('Rate 3')
     A            THFRT4    R               REFFLD(ZZ008)
     A                                      TEXT('Fee Rate 4')
     A                                      COLHDG('Rate 4')
     A            THFRT5    R               REFFLD(ZZ008)
     A                                      TEXT('Fee Rate 5')
     A                                      COLHDG('Rate 5')
     A            THAMT1    R               REFFLD(ZZ011)
     A                                      TEXT('Fee Amount 1')
     A                                      COLHDG('Amount 1')
     A            THAMT2    R               REFFLD(ZZ011)
     A                                      TEXT('Fee Amount 2')
     A                                      COLHDG('Amount 2')
     A            THAMT3    R               REFFLD(ZZ011)
     A                                      TEXT('Fee Amount 3')
     A                                      COLHDG('Amount 3')
     A            THAMT4    R               REFFLD(ZZ011)
     A                                      TEXT('Fee Amount 4')
     A                                      COLHDG('Amount 4')
     A            THAMT5    R               REFFLD(ZZ011)
     A                                      TEXT('Fee Amount 5')
     A                                      COLHDG('Amount 5')
     A            THCALC    R               REFFLD(ZZ001)
     A                                      TEXT('Calculation basis')
     A                                      COLHDG('Calc' 'basis')
     A            THERAT    R               REFFLD(ZZ014)
     A                                      TEXT('Exchange Rate')
     A                                      COLHDG('Exch. Rate')
     A            THMDIN    R               REFFLD(FEMDIN)
     A            THNPYD    R               REFFLD(ZZ006)
     A                                      TEXT('Next Payment Date')
     A                                      COLHDG('Next Pay' 'Date')
     A            THFREQ    R               REFFLD(FREQ)
     A            THDYIM    R               REFFLD(ZZ002)
     A                                      TEXT('Day in Month')
     A                                      COLHDG('Day' 'no')
     A            THPRFC    R               REFFLD(PRFC)
     A                                      TEXT('Profit Center')
     A                                      COLHDG('Prft Cntr')
     A            THPIND    R               REFFLD(FEPIND)
     A            THFSTS    R               REFFLD(SPIN)
     A                                      TEXT('Fee Status Indicator')
     A                                      COLHDG('Fee' +
     A                                             'Sts')
     A            THIUSR    R               REFFLD(FACO)
     A                                      TEXT('Insert User Profile')
     A                                      COLHDG('Ins User' +
     A                                             'Profile')
     A            THAUSR    R               REFFLD(FACO)
     A                                      TEXT('Amend User Profile')
     A                                      COLHDG('Amd User' +
     A                                             'Profile')
     A            THXUSR    R               REFFLD(FACO)
     A                                      TEXT('Authorise User +
     A                                            Profile')
     A                                      COLHDG('Auth User' +
     A                                             'Profile')
     A            THSHFI    R               REFFLD(SPIN)
     A                                      TEXT('Shared Fee Indicator')
     A                                      COLHDG('Shr Fee' +
     A                                             'Ind')
     A            THOSDB    R               REFFLD(BRCA)
     A                                      TEXT('Receipt - Settle Branch')
     A                                      COLHDG('Rec.' +
     A                                             'Brc')
     A            THOMDB    R               REFFLD(BRCA)
     A                                      TEXT('Pay - Settle Branch')
     A                                      COLHDG('Pay' +
     A                                             'Brc')
     A            THRSTM    R               REFFLD(ZZ002)
     A                                      TEXT('receipt - settlement method')
     A                                      COLHDG('rec' 'set' 'met')
     A**Start of change CGL029
     A**********  THRONS    R               REFFLD(RONS)                                      CGL029
     A            QQRONS        12A
     A**End of change CGL029
     A            THRIBN    R               REFFLD(RIBN)
     A            THRIBA    R               REFFLD(RIBA)
     A            THROBN    R               REFFLD(ZZ006)
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('recpt' 'ordering' 'bank')
     A            THROCN    R               REFFLD(ZZ006)
     A                                      TEXT('receipt - ordering customer')
     A                                      COLHDG('recpt' 'ordering' 'cust')
     A            THPSTM    R               REFFLD(ZZ002)
     A                                      TEXT('pay - settlement method')
     A                                      COLHDG('pay' 'set' 'met')
     A**Start of change CGL029
     A**********  THPONS    R               REFFLD(PONS)                                      CGL029
     A            QQPONS        12A
     A**End of change CGL029
     A            THPIBN    R               REFFLD(PIBN)
     A            THPIBA    R               REFFLD(PIBA)
     A            THPOBN    R               REFFLD(ZZ006)
     A                                      TEXT('pay - ordering bank no')
     A                                      COLHDG('pay' 'ordering' 'bank')
     A            THPOCN    R               REFFLD(ZZ006)
     A                                      TEXT('pay - ordering customer no')
     A                                      COLHDG('pay' 'ordering' 'cust')
     A            THRCRN    R               REFFLD(RCRN)
     A            THRCRA    R               REFFLD(RCRA)
     A            THRVNO    R               REFFLD(RVNO)
     A            THAWBN    R               REFFLD(AWBN)
     A            THAWBA    R               REFFLD(AWBA)
     A            THBENN    R               REFFLD(BENN)
     A            THBENA    R               REFFLD(BENA)
     A            THDTP1    R               REFFLD(DTP1)
     A            THDTP2    R               REFFLD(DTP2)
     A            THDTP3    R               REFFLD(DTP3)
     A            THDTP4    R               REFFLD(DTP4)
     A            THDCHG    R               REFFLD(DCHG)
     A            THBTB1    R               REFFLD(BTB1)
     A            THBTB2    R               REFFLD(BTB2)
     A            THBTB3    R               REFFLD(BTB3)
     A            THBTB4    R               REFFLD(BTB4)
     A            THBTB5    R               REFFLD(BTB5)
     A            THBTB6    R               REFFLD(BTB6)
     A            THCVMR    R               REFFLD(CVMR)
     A            MSVR      R               REFFLD(SPIN)
     A                                      TEXT('Message Severity')
     A                                      COLHDG('Message' +
     A                                             'Severity')
     A            MSID      R               REFFLD(ZZ007)
     A                                      TEXT('Message ID')
     A                                      COLHDG('Message ID')
     A            MSTX      R               REFFLD(ZZ078)
     A                                      TEXT('Message Text')
     A                                      COLHDG('Message Text')
      *                                              Begin /CEU004/
     A            THSCCY    R               REFFLD(CCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
     A            THICCY    R               REFFLD(CCY)
     A                                      TEXT('IN Currency in +
     A                                            Fld 72')
     A                                      COLHDG('IN Ccy')
      *                                              End   /CEU004/
      **Start of change CLE031
     A            THREXR        14A         TEXT('Receive Settlement  +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange  Rate')
      *
     A            THREXI         1A         TEXT('Receive Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
      *
     A            THPSCY         3A         TEXT('Pay Settlement Currency')
     A                                      COLHDG('Pay Settlement' 'Currency')
      *
     A            THPEXR        14A         TEXT('Pay Settlement Currency +
     A                                           Exchange Rate')
     A                                      COLHDG('Pay Settlement' +
     A                                           'Currency' 'Exchange Rate')
      *
     A            THPEXI         1A         TEXT('Pay Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Pay Settlement' 'Currency'+
     A                                           'Exchange' 'Rate Ind')
      **End of change CLE031
      **Start of Change CLE034
     A            STAL           1A
     A                                      TEXT('Settlement Allocation +
     A                                           Indicator')
     A                                      COLHDG('Settlement' +
     A                                           'Allocation Indicator')
      **End of Change CLE034
     A**Start of change CGL029
     A            THRONS        18A
     A            THPONS        18A
     A**End of change CGL029
      * Start of CAS016
     A            THAMRI         1A         TEXT('Amortise Separately Ind.')
     A                                      COLHDG('Amort Sep Ind')
     A            THSTPD         6A         TEXT('Amortisation Start Date')
     A                                      COLHDG('Amort Start Date')
     A            THENPD         6A         TEXT('Amortisation End Date')
     A                                      COLHDG('Amort End Date')
      * End of CAS016
      **Start of change CLE134
      *
     A            THAUT2         1A         COLHDG('Auto-settlement' 'Method')
     A            THPLNR         6A         COLHDG('Past Due' 'Call Loan')
     A            THFAMU         1A         COLHDG('Facility' 'Amount' +
     A                                             'Utilisation')
      *
     A            THRIDT         6A         COLHDG('Reporting' 'Past Due' +
     A                                             'Initial Date')
     A            THRCAT         1A         COLHDG('Reporting' 'Past Due' +
     A                                              'Classification')
      *
     A            THBIDT         6A         COLHDG('Basel II' 'Past Due'  +
     A                                             'Initial Date')
     A            THBCAT         1A         COLHDG('Basel II' 'Past Due'  +
     A                                              'Classification')
      *
     A            THFSFA         1A         COLHDG('Suspend Fee' 'Allowed')
     A            THSFA         13A         COLHDG('Suspended' 'Fee Amount')
     A            THSFAM        13A         COLHDG('Suspended' 'Fee Amount' +
     A                                             'Maximum')
     A            THSFP          3A         COLHDG('Suspended' 'Fee Payments')
     A            THSFPM         3A         COLHDG('Suspended' 'Fee Payments' +
     A                                             'Maximum')
      *
     A            THFPPA         1A         COLHDG('Partial' 'Payment' +
     A                                             'Allowed')
     A            THPSIN         1A         COLHDG('Partial' 'Indicator')
     A            THPSLM        13A         COLHDG('Partial' 'Limit')
     A            THFEMC         1A         COLHDG('Early' 'Maturity' +
     A                                             'Condition')
      *
      **End of change CLE134
