      *****************************************************************
/*S*D****PFBASE********************************************************                       CLE164
/*E*S****PF_STD********************************************************                       CLE164
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEACCNPD - Repayment Priority                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CLE164  *REDUNDANT Date 18Aug14               *
      *  Prev Amend No. CLE134  *CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE164 - CLE134 Enhancement F (Repayment Methodology)        *
      *  CLE134 - Past Due Call Loan Processing                       *
      *                                                               *
      *****************************************************************
     A          R LEACCND0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
      *
     A            RPRECI         1A         COLHDG('Record Id')
     A            RPBRCA         3A         COLHDG('Branch')
     A            RPACCY         3A         COLHDG('Currency')
     A            RPCNUM         6A         COLHDG('Customer' 'Number')
     A            RPACOD        10S 0       COLHDG('Account' 'Code')
     A            RPACSQ         2S 0       COLHDG('Account' 'Sequence')
     A            RPSTDT         5P 0       COLHDG('Start' 'Date')
      *
      ** Loan details
      *
     A            RPLNRF         6A         COLHDG('Loan' 'Reference')
     A            RPLCBR         3A         COLHDG('Loan' 'Customer' 'Branch')
     A            RPLCNU         6A         COLHDG('Loan' 'Customer' 'Number')
     A            RPLTYP         2A         COLHDG('Loan' 'Type')
     A            RPSUTP         2A         COLHDG('Loan' 'Sub-Type')
     A            RPLCLS         4A         COLHDG('Loan' 'Class')
      *
      ** Fee details
      *
     A            RPFCBR         3A         COLHDG('Fee' 'Customer' 'Branch')
     A            RPFCNM         6A         COLHDG('Fee' 'Customer' 'Number')
     A            RPFLNR         6A         COLHDG('Fee' 'Loan Number')
     A            RPFACT         5S 0       COLHDG('Fee' 'Facility' 'Number')
     A            RPFSE1         2S 0       COLHDG('Fee' 'Sequence')
     A            RPFCOD         2S 0       COLHDG('Fee' 'Code')
      *
      ** Facility details
      *
     A            RPFFCB         3A         COLHDG('Facility' 'Customer' +
     A                                             'Branch')
     A            RPFFCN         6A         COLHDG('Facility' 'Customer' +
     A                                             'Number')
     A            RPFFAC         3S 0       COLHDG('Facility' 'Type')
     A            RPFFNO         2S 0       COLHDG('Facility' 'Number')
      *
      ** Priorities
      *
     A            RPPCK1         5P 0       COLHDG('Fee Code or ' 'Loan' +
     A                                             'Type/Subt PCN')
     A            RPPCK2         5P 0       COLHDG('Facilty Type' 'or Loan' +
     A                                             'Type/Subt PCN')
     A            RPPCK3         5P 0       COLHDG('PCN Input by user')
     A            RPPCKO         5P 0       COLHDG('Lowest PCN among' +
     A                                             'PCN1, PCN2, PCN3')
      *
      ** Shadow postings unique reference
      *
     A            RPXRFI         3A         COLHDG('Extension' 'File' +
     A                                             'Reference Id')
     A            RPXRFN        10S 0       COLHDG('Extension' 'File' +
     A                                             'Reference Number')
      *
      ** System fields
      *
     A            RPFRNT        20A         COLHDG('Front' 'Office' +
     A                                             'Transaction ID')
     A            RPTMST          Z         COLHDG('Timestamp')
      *
