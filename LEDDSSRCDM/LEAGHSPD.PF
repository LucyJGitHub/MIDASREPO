      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Agg Facility History')                        *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEAGHSPD - Aggregated Facility History                    *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. CLE138             Date 02Nov21               *
      *  Prev Amend No. AR674226           Date 04Dec19               *
      *                 MD046248           Date 27Oct17               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CLE041  *CREATE    Date 11Oct04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE138 - Class Codes for Facilities                          *
      *  AR674226 - Manual transaction reached 999. Increase the size *
      *             of AIUSEQ to accomodate 9999 transactions.        *
      *             (Child: AR674227)                                 *
      *           - Applied for MD054782.                             *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE041 - Collateralised Lending Phase 2B                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R LEAGHSD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A            AIRECI         1A         TEXT('Record ID')
     A                                      COLHDG('Record ID')
     A            AIBRCA         3A         TEXT('Branch Code - Alpha')
     A                                      COLHDG('Branch Code - Alpha')
      * Begin /CSD027/
     A**********  AICNUM         6S 0       TEXT('Customer Number')                           CSD027
     A            AICNUM         6A         TEXT('Customer Number')
      * End /CSD027/
     A                                      COLHDG('Customer Number')
     A            AIFCTY         5S 0       TEXT('Facility')
     A                                      COLHDG('Facility')
     A            AIDATE         5P 0       TEXT('Value Date')
     A                                      COLHDG('Value Date')
     A            AISEQ          2S 0       TEXT('Account sequence number')
     A                                      COLHDG('Account +
     A                                      Sequence No')
     A            AIACTN         2A         TEXT('Action')
     A                                      COLHDG('Action')
     A            AIAAMT        15P 0       TEXT('Action amount')
     A                                      COLHDG('Action amount')
     A            AIRCIN         1          TEXT('Revolving credit indicator')
     A                                      COLHDG('Revolving +
     A                                      Credit Ind')
     A            AITSEQ         3S 0       TEXT('Tranche Sequence Number')
     A                                      COLHDG('Tranche +
     A                                      Seq No')
     A            AITRBR         3A         TEXT('Tranche - Branch Code +
     A                                      - Alpha')
     A                                      COLHDG('Tranche +
     A                                      Branch Code')
      * Begin /CSD027/
     A**********  AITRNM         6S 0       TEXT('Tranche - Customer Number')                 CSD027
     A            AITRNM         6A         TEXT('Tranche - Customer Number')
      * End /CSD027/
     A                                      COLHDG('Tranche-'+
     A                                      'Customer' +
     A                                      'No.')
     A            AITRFC         5S 0       TEXT('Tranche - Facility')
     A                                      COLHDG('Tranche - Facility')
     A            AITRAM        15P 0       TEXT('Tranche - Action Amount')
     A                                      COLHDG('Tranche - '+
     A                                      'Action' +
     A                                      'Amt')
     A            AITRCY         3A         TEXT('Tranche - Facility +
     A                                      Currency')
     A                                      COLHDG('Tranche - Facility' +
     A                                      'Ccy')
     A            AITRRC         1A         TEXT('Tranche - Revolving +
     A                                      Credit Indicator')
     A                                      COLHDG('Tranche - Revolving' +
     A                                      'Credit Ind')
     A            AIEXRT        13P 8       TEXT('Tranche to Aggregate +
     A                                      Facility Exchange Rate')
     A                                      COLHDG('Tranche to Aggregate' +
     A                                      'Facility Exchg Rate')
     A            AIMDIN         1A         TEXT('Exchange Rate Multiply/+
     A                                      Divide Indicator')
     A                                      COLHDG('Exchg Rate' +
     A                                      'Mult/Div Ind')
      **Start of change CLE148
     A**********  AILOAN         6S 0       TEXT('Loan Number')                               CLE148
     A            AILOAN         6A         TEXT('Loan Number')
      **End of change CLE148
     A                                      COLHDG('Loan Number')
     A            AIDEAL         6S 0       TEXT('Deal Number')
     A                                      COLHDG('Deal Number')
      **Start of Change AR674226
     A*           AIUSEQ         3S 0       TEXT('Utilisation Sequence +                    AR674226
     A*                                     Number')                                        AR674226
     A*                                     COLHDG('Utilisation Seq' +                      AR674226
     A*                                     'Number')                                       AR674226
     A            QQUSEQ         3S 0
      **End of Change AR674226
     A            AIMTID        20P 0       TEXT('Manual Transaction ID')
     A                                      COLHDG('Manual Trans ID')
     A            AIFCAM        15P 0       TEXT('Facility Amount')
     A                                      COLHDG('Facility Amount')
     A            AIAFAM        15P 0       TEXT('Actual Facility Amount')
     A                                      COLHDG('Actual Facility Amt')
     A            AIORED         5P 0       TEXT('Original Entry Date')
     A                                      COLHDG('Original Entry Date')
      **Start of Change AR674226
     A            AIUSEQ         4S 0       TEXT('Utilisation Sequence +
     A                                      Number')
     A                                      COLHDG('Utilisation Seq' +
     A                                      'Number')
      **End of Change AR674226
      **Start of change CLE138
     A            AIFCLS         4A         TEXT('Class Code')
     A                                      COLHDG('Class Code')
      **End of change CLE138
