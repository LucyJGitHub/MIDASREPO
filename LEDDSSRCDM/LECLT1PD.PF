      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Customer Loans - Italian')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LECLT1PD - Customer Loans - Italian                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. LUC139             Date 07Jan21               *
      *  Prev Amend No. CER009  *CREATE    Date 16Aug06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian Returns Upgrade to FM2.1                *
      *  CER009 - Puma II Upgrade to MidasPlus                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R LECLT1D0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
      *
     A            PFLNRF         6A         TEXT('Loan Number')
     A                                      COLHDG('Loan' ' Number')
     A            PFMOOP         1S 0       TEXT('Motivation of Operation')
     A                                      COLHDG('Motivation' 'of Operation')
     A            PFAGCT         3S 0       TEXT('Agricultural Credit Class')
     A                                      COLHDG('Agricultural' 'Credit' +
     A                                             'Class')
     A            PFAGCL         3S 0       TEXT('Agricultural Laws Class')
     A                                      COLHDG('Agricultural' 'Laws Class')
     A            PFECBD         2S 0       TEXT('Economical Branch of Dest')
     A                                      COLHDG('Economical' +
     A                                             'Branch of Dest')
     A            PFSBCR         1S 0       TEXT('Subsidized Credit Indicator')
     A                                      COLHDG('Subsidized' 'Credit' +
     A                                             'Indicator')
     A            PFINSB         1S 0       TEXT('Subsidized Int. for Export')
     A                                      COLHDG('Subsidized Int.' +
     A                                             'Export')
     A            PFTPPR         6S 0       TEXT('Type of Product')
     A                                      COLHDG('Type of Product')
     A            PFPEPR         6S 0       TEXT('Personalisation of Operation')
     A                                      COLHDG('Personalisation' +
     A                                             'of Operation')
     A            PFGLPA         1S 0       TEXT('Global/Partial Operation')
     A                                      COLHDG('Global/Partial' +
     A                                             'Operation')
     A            PFTPFD         1S 0       TEXT('Operation with Third Party')
     A                                      COLHDG('Global/Partial' +
     A                                             'Operation')
     A            PFSBLW         3A         TEXT('Law for Subsidized Credit Cd')
     A                                      COLHDG('Law for Subsidized' +
     A                                             'Credit Code')
     A            PFDPIV         5S 0       TEXT('Destination Province +
     A                                           Investment')
     A                                      COLHDG('Destination Province' +
     A                                             'Investment')
     A            PFEDIV         3A         TEXT('Economical Dest of +
     A                                           Investment')
     A                                      COLHDG('Economical Dest.' +
     A                                             'of Investment')
     A            PFCATC        11S 0       TEXT('Category Code CR')
     A                                      COLHDG('Category Code CR')
     A            PFTPAC         3S 0       TEXT('Type of Activity')
     A                                      COLHDG('Type of Activity')
     A            PFCLRL         3S 0       TEXT('Client Role CR')
     A                                      COLHDG('Client Role CR')
     A            PFACST         3S 0       TEXT('Account Status CR')
     A                                      COLHDG('Account Status CR')
     A            PFLNIT         7A         TEXT('Line Item of Usura Report')
     A                                      COLHDG('Line Item of' +
     A                                             'Usura Report')
     A            PFTEGL         8S 3       TEXT('Effective Global Rate')
     A                                      COLHDG('Effective' ' Global Rate')
     A            PFUNDT         5P 0       TEXT('Scaduto da')
     A                                      COLHDG('Scaduto da')
     A            PFPSAM        13P 0       TEXT('Quota aggiuntiva altrui')
     A                                      COLHDG('Quota aggiuntiva' +
     A                                             'altrui')
     A            PFLEAD         6A         TEXT('Capofila (Valutaria)')
     A                                      COLHDG('Capofila' '(Valutaria)')
      ** Start of LUC139
      *
     A            PFPTRP         3S 0       TEXT('Priority Risk Code Product')
     A                                      COLHDG('Prior.' 'Risk Cd' 'Product')
      *
     A            PFCMPC         8S 3       TEXT('Com. Rate Max. Uncovered Bal.-
     A                                      ')
     A                                      COLHDG('Com. Rate Max.' 'Uncovered'-
     A                                       'Balance')
      ** End of LUC139
