      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  REUSEDLT(*YES)                                               *                     CLE075AS
/*EXI *  TEXT('Midas Lending Fees History')                           *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*  PF/LEFHST - Lending Fees History                             *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. CLE071             Date 18Jul18               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE075AS           Date 06Aug12               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CSW208             Date 15Apr08               *
      *                 CAS016             Date 28Feb06               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 BUG8762            Date 01Oct05               *
      *                 233601             Date 20May05               *
      *                 CAS009             Date 04May04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 MOF60              Date 04Jul91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CLE071 - Value Dating of Rate Changes to Fees                *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE075AS - COB Restructure - LE COB Optimisation Phase 1     *
      *  CLE148 - Alpha Loan Reference                                *
     A*   CSW208 - SWIFT 2008 Changes                                 *
      *  CAS016 - IAS18 EIR Recalculation (Fee Amortisation Over      *
      *           Whole Period)                                       *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG8762 - Added fee rates and amount fields to accurately    *
      *            compute accruals of previous periods.              *
      *            Applied fix 209513.                                *
      *  233601 - Post Fee amount to be amortised non-linearly.       *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
     A*   MOF60  - CUSTOMER PROFITABILITY & FEES PROCESSING           *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
      *
     A          R LEFHSTF                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            HIRECI    R               REFFLD(RECI)
     A            HIBRCA    R               REFFLD(BRCA)
      * Begin /CSD027/
     A**********  HICNUM    R               REFFLD(CNUM)                                      CSD027
     A            HICNUM         6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            HIFACL    R               REFFLD(FCLY)
      **Start of change CLE148
     A**********  HILNRF    R               REFFLD(LNRF)                                      CLE148
     A            HILNRF         6A         TEXT('Loan Number')
     A                                      COLHDG('Loan' 'Number')
      **End of change CLE148
     A            HIFSEQ    R               REFFLD(FEFSEQ)
     A            HIFCOD    R               REFFLD(FEFCOD)
     A            HIFCCY    R               REFFLD(CCY)
     A            HIAMTS    R               REFFLD(FEAMTS)
     A* Date added to fee history file
     A            HIHDAT    R               REFFLD(ORED)
     A            HILCHD    R               REFFLD(LCD)
     A            HILCHT    R               REFFLD(CHTP)
      **Start of change CAS009
     A            HIAMTD        15P 0       TEXT('Non Linear Amortised +
     A                                       Fee to Date')
     A                                      COLHDG('Non Linear Amortised' +
     A                                       'Fee to Date')
     A            HIOSBR         3A         TEXT('Our Settlement Branch')
     A                                      COLHDG('Our Settlement' +
     A                                       'Branch')
     A            HISTTL         2S         TEXT('Settlement Type')
     A                                      COLHDG('Settlement Type')
     A            HIOURS        12A         TEXT('Our Settlement Nostro')
     A                                      COLHDG('Our Settlement' +
     A                                       'Nostro')
      **End of change CAS009
      **Start of change 233601
     A            FENLAI         1A         TEXT('Non-linear Amortisation Ind')
     A                                      COLHDG('Non-lin Amort Ind')
      **Start of change 233601
      **Start of change BUG8762
     A            HIFRT1    R               REFFLD(FEFRT1)
     A            HIFRT2    R               REFFLD(FEFRT2)
     A            HIFRT3    R               REFFLD(FEFRT3)
     A            HIFRT4    R               REFFLD(FEFRT4)
     A            HIFRT5    R               REFFLD(FEFRT5)
     A            HIAMT1    R               REFFLD(FEAMT1)
     A            HIAMT2    R               REFFLD(FEAMT2)
     A            HIAMT3    R               REFFLD(FEAMT3)
     A            HIAMT4    R               REFFLD(FEAMT4)
     A            HIAMT5    R               REFFLD(FEAMT5)
      **End of change BUG8762
      **Start of change CAS016
     A            HISAMT        15P 0       TEXT('Non-linear Settlement Amount')
     A                                      COLHDG('Non-lin. Stlm. Amt')
     A            HIAMRI         1A         TEXT('Amortise separately Ind.')
     A                                      COLHDG('Amortise sep. ind.')
     A            HISTPD         5P 0       TEXT('Amortisation Start Period')
     A                                      COLHDG('Amort Start Period')
     A            HIENPD         5P 0       TEXT('Amortisation End Period')
     A                                      COLHDG('Amort End Period')
      **End of change CAS016
      ** Start of change CSW208
     A            HIOSDB         3A         TEXT('Receipt - Settle Branch')
     A                                      COLHDG('Receipt'  '- Settle Branch')
     A            HIOMDB         3A         TEXT('Pay - Settle Branch')
     A                                      COLHDG('Pay'  '- Settle Branch')
     A            HIRSTM         2  0       TEXT('Receipt - Settlement Method')
     A                                      COLHDG('Receipt'  '- Settlement' +
     A                                             ' Method')
     A            HIRONS        18A         TEXT('Receipt - Our Nostro')
     A                                      COLHDG('Receipt'  '- Our Nostro')
     A            HIRIBN         8A         TEXT('Receipt - Intermediary Bank')
     A                                      COLHDG('Receipt'  'Intermediary' +
     A                                             ' Bank')
     A            HIRIBA        35A         TEXT('Receipt - Intermediary Bank')
     A                                      COLHDG('Receipt'  'Intermediary' +
     A                                             ' Bank')
     A            HIROBN         6  0       TEXT('Receipt - Ordering Bank No')
     A                                      COLHDG('Receipt'  '- Ordering' +
     A                                             ' Bank No.')
     A            HIROCN         6  0       TEXT('Receipt - Ordering Customer')
     A                                      COLHDG('Receipt'  '- Ordering' +
     A                                             ' Customer')
     A            HIPSTM         2  0       TEXT('Pay - Settlememt Method')
     A                                      COLHDG('Pay'  'Settlement' +
     A                                             ' Method')
     A            HIPONS        18A         TEXT('Pay - Our Nostro')
     A                                      COLHDG('Pay'  'Our Nostro')
     A            HIPIBN         8A         TEXT('Pay - Intermediary Bank no')
     A                                      COLHDG('Pay'  'Intermediary' +
     A                                             ' Bank No.')
     A            HIPIBA        35A         TEXT('Pay - Intermediary Bank A/C')
     A                                      COLHDG('Pay'  'Intermediary' +
     A                                             ' Bank A/C')
     A            HIPOBN         6  0       TEXT('Pay - Ordering No.')
     A                                      COLHDG('Pay'  'Ordering No')
     A            HIPOCN         6  0       TEXT('Pay - Ordering Cust. No.')
     A                                      COLHDG('Pay'  'Ordering' 'Cust.No.')
     A            HIRCRN         8A         TEXT('Receiver Correspondent No')
     A                                      COLHDG('Receiver' 'Correspondent')
     A            HIRCRA        35A         TEXT('Receiver Correspondent A/C')
     A                                      COLHDG('Receiver' 'Correspondent' +
     A                                             ' A/C Line')
     A            HIRVNO         8A         TEXT('Receiver Number')
     A                                      COLHDG('Receiver' 'Number')
     A            HIAWBN         8A         TEXT('A/C with Bank no')
     A                                      COLHDG('A/C with Bank no')
     A            HIAWBA        35A         TEXT('A/C with Bank a/c line')
     A                                      COLHDG('A/C with Bank' +
     A                                             'a/c line')
     A            HIBENN         8A         TEXT('Beneficiary No.')
     A                                      COLHDG('Beneficiary No.')
     A            HIBENA        35A         TEXT('Beneficiary a/c Line')
     A                                      COLHDG('Beneficiary a/c' +
     A                                            ' Line')
     A            HIDTP1        35A         TEXT('Details of Pay 1')
     A                                      COLHDG('Details of Pay 1')
     A            HIDTP2        35A         TEXT('Details of Pay 2')
     A                                      COLHDG('Details of Pay 2')
     A            HIDTP3        35A         TEXT('Details of Pay 3')
     A                                      COLHDG('Details of Pay 3')
     A            HIDTP4        35A         TEXT('Details of Pay 4')
     A                                      COLHDG('Details of Pay 4')
     A            HIDCHG         3A         TEXT('Details of Charges')
     A                                      COLHDG('Details of Charges')
     A            HIBTB1        35A         TEXT('Bank to Bank info 1')
     A                                      COLHDG('Bank to Bank ' 'info 1')
     A            HIBTB2        35A         TEXT('Bank to Bank info 2')
     A                                      COLHDG('Bank to Bank ' 'info 2')
     A            HIBTB3        35A         TEXT('Bank to Bank info 3')
     A                                      COLHDG('Bank to Bank ' 'info 3')
     A            HIBTB4        35A         TEXT('Bank to Bank info 4')
     A                                      COLHDG('Bank to Bank ' 'info 4')
     A            HIBTB5        35A         TEXT('Bank to Bank info 5')
     A                                      COLHDG('Bank to Bank ' 'info 5')
     A            HIBTB6        35A         TEXT('Bank to Bank info 6')
     A                                      COLHDG('Bank to Bank ' 'info 6')
     A            HICVMR         1A         TEXT('Cover Message Required')
     A                                      COLHDG('Cover Message ' +
     A                                             'Required')
     A            HIFSCY         3A         TEXT('Settlement Currency')
     A                                      COLHDG('Settlement Currency')
     A            HIFICY         3A         TEXT('IN Currency in Fld 72')
     A                                      COLHDG('IN Currency in ' +
     A                                             'Fld 72')
     A            HIPSCY         3A         TEXT('Payment Currency')
     A                                      COLHDG('Payment Currency')
      ** End of change CSW208
      **Start of change CLE071
     A            HIPIND         1A         TEXT('Percent Indicator')
     A                                      COLHDG('%' 'IND')
     A            HIRCVD         5P 0       TEXT('Rate Change Value +
     A                                            Date')
     A                                      COLHDG('Rate Change' +
     A                                             'Value Date')
     A            HIRCTP         2A         TEXT('Rate Change Type')
     A                                      COLHDG('Rate' 'Chg' 'Type')
      **End of change CLE071
