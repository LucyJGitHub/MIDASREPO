      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE ARR Loan BR amendments file')                 *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEFWBRPD - ARR Loan BR Amendments file                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2022            *
      *                                                               *
      *  Last Amend No. MD058517  *CREATE  Date 17Oct22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058517 - CLE172 LIBOR Lending- Change Control CC3.1        *
      *             Interest Projection Using Known Rates             *
      *             CC10 CLE172 Spool File Correspondence (MD059817)  *
      *                                                               *
      *****************************************************************
      *
     A          R LEFWBRD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2022')
     A            RECI           1A         TEXT('Record ID')
     A                                      COLHDG('Rec' +
     A                                             'ID')
      *
     A            LNRF           6A         TEXT('Loan Number')
     A                                      COLHDG('Loan' +
     A                                             'Number')
      *
     A            VDAT           5S 0       TEXT('Value Date')
     A                                      COLHDG('Value' +
     A                                             'Date')
      *
     A            LASN           3S 0       TEXT('Last amend sequence number')
     A                                      COLHDG('Amnd' +
     A                                             'Seq' +
     A                                             'No')
      *
     A            MRIN           1S 0       TEXT('Matching records indicator')
     A                                      COLHDG('Mat' +
     A                                             'Rec' +
     A                                             'Ind')
      *
     A            LTYP           2A         TEXT('Loan type')
     A                                      COLHDG('Loan' +
     A                                             'Type')
      *
     A            SUTP           2A         TEXT('Loan sub-type')
     A                                      COLHDG('Loan' +
     A                                             'Sub-' +
     A                                             'Type')
      *
     A            PTYP           2S 0       TEXT('Loan processing type')
     A                                      COLHDG('Loan' +
     A                                             'Proc' +
     A                                             'Type')
      *
     A            AMTP           2A         TEXT('Amendment type')
     A                                      COLHDG('Amnd' +
     A                                             'Type')
      *
     A            PRSQ           2S 0       TEXT('Processing sequence')
     A                                      COLHDG('Proc' +
     A                                             '-sng' +
     A                                             'Seq.')
      *
     A            BRTT           2A         TEXT('Base rate code')
     A                                      COLHDG('Base' +
     A                                             'Rate' +
     A                                             'Code')
      *
     A            RTSP          11P 7       TEXT('Rate/spread')
     A                                      COLHDG('Rate/spread')
      *
     A            CCY            3A         TEXT('Currency code')
     A                                      COLHDG('Curr' +
     A                                             'Code')
      *
     A            PRAM          13P 0       TEXT('Principal amount')
     A                                      COLHDG('Principal' +
     A                                             'Amount')
      *
     A            INTA          13P 0       TEXT('Interest amount')
     A                                      COLHDG('Interest' +
     A                                             'Amount')
      *
     A            WTXA          13P 0       TEXT('With-holding tax')
     A                                      COLHDG('With-holding tax')
      *
     A            FEAM          13P 0       TEXT('Fee amount')
     A                                      COLHDG('Fee Amount')
      *
     A            TAMT          13P 0       TEXT('Total amount')
     A                                      COLHDG('Total Amount')
      *
     A            BRCA           3A         TEXT('Branch Code - Alpha')
     A                                      COLHDG('Brch' +
     A                                             'Code')
      *
     A            CNUM           6A         TEXT('Customer number')
     A                                      COLHDG('Customer' +
     A                                             'Number')
      *
     A            REPT           1S 0       TEXT('Repayment type')
     A                                      COLHDG('Rep' +
     A                                             'Typ')
      *
     A            AUTO           1A         TEXT('Auto settlement indicator')
     A                                      COLHDG('Aut' +
     A                                             'Set' +
     A                                             'Ind')
      *
     A            STAI           1A         TEXT('Stop accruals indicator')
     A                                      COLHDG('Stp' +
     A                                             'Acc' +
     A                                             'Ind')
      *
     A            SETP           2S 0       TEXT('Settlement type')
     A                                      COLHDG('Set' +
     A                                             'mnt' +
     A                                             'Type')
     A            OSACQQ        12A
      *
     A            TSEN          10A         TEXT('Their settlement nostro')
     A                                      COLHDG('Their' +
     A                                             'Settlement' +
     A                                             'Nostro')
      *
     A            FACO          10A         TEXT('For account of')
     A                                      COLHDG('For' +
     A                                             'Account' +
     A                                             'of')
      *
     A            SPI1          30A         TEXT('Special instructions - 1')
     A                                      COLHDG('Special' +
     A                                             'Instruction-1')
      *
     A            SPI2          30A         TEXT('Special instructions - 2')
     A                                      COLHDG('Special' +
     A                                             'Instruction-2')
      *
     A            SPI3          30A         TEXT('Special instructions - 3')
     A                                      COLHDG('Special' +
     A                                             'Instruction-3')
      *
     A            LAIN           1A         TEXT('Loan amendment indicator')
     A                                      COLHDG('ln' +
     A                                             'Amd' +
     A                                             'Ind')
      *
     A            BRTE          11P 7       TEXT('Current Base rate')
     A                                      COLHDG('Current' +
     A                                             'Base' +
     A                                             'Rate')
      *
     A            SPIN           1A         TEXT('Spread indicator')
     A                                      COLHDG('Spr' +
     A                                             'Ind')
      *
     A            CALC           1S 0       TEXT('Calculation basis')
     A                                      COLHDG('Cal' +
     A                                             'Bas')
      *
     A            WTIN           1A         TEXT('Withholding tax indicator')
     A                                      COLHDG('Whl' +
     A                                             'Tax' +
     A                                             'Ind')
      *
     A            LLAG           1A         TEXT('Loan/loan amend generated')
     A                                      COLHDG('Loa' +
     A                                             'Amd' +
     A                                             'Gen')
      *
     A            LWOI           1A         TEXT('Loan write off indicator')
     A                                      COLHDG('L w' +
     A                                             'off' +
     A                                             'Ind')
      *
     A            XAVD           5S 0
      *
     A            XASQ           3S 0       TEXT('X-ref loan amend seq. number')
     A                                      COLHDG('X-ref' +
     A                                             'Ln am' +
     A                                             'Sq no')
      *
     A            FCUS           6A         TEXT('Facility customer')
     A                                      COLHDG('Facility' +
     A                                             'Customer')
      *
     A            FTYP           3S 0       TEXT('Facility type')
     A                                      COLHDG('Fclty' +
     A                                             'Type')
      *
     A            FSEQ           2S 0       TEXT('Facility sequence number')
     A                                      COLHDG('Fac.' +
     A                                             'Seq.' +
     A                                             'Numb')
      *
     A            RLON           6A         TEXT('Related loan number')
     A                                      COLHDG('Related' +
     A                                            'Loan' +
     A                                            'Number')
      *
     A            FECD           2S 0       TEXT('Fee code')
     A                                      COLHDG('Fee' +
     A                                             'Code')
      *
     A            PONI           1A
      *
     A            ACTI           1A         TEXT('Action indicator')
     A                                      COLHDG('Act' +
     A                                             'Ind')
      *
     A            CNFY           1A         TEXT('Confirmation indicator')
     A                                      COLHDG('Con' +
     A                                             'Ind')
      *
     A            TLXY           1A         TEXT('Pay/receive telex indicator')
     A                                      COLHDG('P/r' +
     A                                             'tlx' +
     A                                             'Ind')
      *
     A            CBLY           1A         TEXT('Customer bills indicator')
     A                                      COLHDG('Cus' +
     A                                             'Bll' +
     A                                             'Ind')
      *
     A            INTC           1A         TEXT('Interest captilisation ind')
     A                                      COLHDG('Int' +
     A                                             'Cap' +
     A                                             'Ind')
      *
     A            NACD           1A         TEXT('Name and address code')
     A                                      COLHDG('Nme' +
     A                                             'Adr' +
     A                                             'Cod')
      *
     A            ACBI           1A         TEXT('Amended cust. billing ind.')
     A                                      COLHDG('Amd' +
     A                                             'C b' +
     A                                             'Ind')
      *
     A            ZZ001          1A         TEXT('Filler')
     A                                      COLHDG('Filler')
      *
     A            LSWCC          3P 0
      *
     A            LSWSC          3P 0
      *
     A            OSBR           3A         TEXT('Our settlement branch')
     A                                      COLHDG('Osbr' +
     A                                             'Osbr')
      *
     A            FCLB           3A         TEXT('Facility Branch Code')
     A                                      COLHDG('Facil' +
     A                                             'Brnch' +
     A                                             'Code')
      *
     A            LSMD           5P 0       TEXT('Maturity Date for +
     A                                      change of sub-type')
     A                                      COLHDG('Mat Dat' +
     A                                             'SUTP' +
     A                                             'Change')
      *
     A            REBT          13P 0       TEXT('Rebate')
     A                                      COLHDG('Rebate')
      *
     A            ZZ034         34A         TEXT('Filler')
     A                                      COLHDG('Filler')
      *
     A            ORED           5P 0       TEXT('Original entry date')
     A                                      COLHDG('Originl' +
     A                                             'Entry' +
     A                                             'Date')
      *
     A            LCD            5P 0       TEXT('Last change date')
     A                                      COLHDG('Last' +
     A                                             'Change' +
     A                                             'Date')
      *
     A            CHTP           1A         TEXT('Type of last change')
     A                                      COLHDG('Lst' +
     A                                             'Chg' +
     A                                             'Typ')
      *
     A            TNLU           5P 0       TEXT('Transact.no of last update')
     A                                      COLHDG('Last' +
     A                                             'Trans' +
     A                                             'Number')
      *
     A            RSTM           2S 0       TEXT('receipt - settlement method')
     A                                      COLHDG('rcpt' +
     A                                             'sett' +
     A                                             'meth')
     A            RONSQQ        12A
      *
     A            RIBN           8A         TEXT('receipt - intermediary +
     A                                      bank number')
     A                                      COLHDG('rect' +
     A                                             'int' +
     A                                             'bank')
      *
     A            RIBA          35A         TEXT('receipt - intermediary +
     A                                      bank a/c line')
     A                                      COLHDG('rcpt' +
     A                                             'int' +
     A                                             'a/c')
      *
     A            ROBN           6A         TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt' +
     A                                             'ord' +
     A                                             'bank')
      *
     A            ROCN           6A         TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt' +
     A                                             'ord' +
     A                                             'cust')
      *
     A            PSTM           2S 0       TEXT('pay - settlement method')
     A                                      COLHDG('pay' +
     A                                             'setl' +
     A                                             'meth')
     A            PONSQQ        12A
      *
     A            PIBN           8A         TEXT('pay - intermediary bank no')
     A                                      COLHDG('pay' +
     A                                             'int' +
     A                                             'bank')
      *
     A            PIBA          35A         TEXT('pay - intermediary +
     A                                      bank a/c line')
     A                                      COLHDG('pay' +
     A                                             'int' +
     A                                             'bank')
      *
     A            POBN           6A         TEXT('pay - ordering bank no')
     A                                      COLHDG('pay' +
     A                                             'ord' +
     A                                             'bank')
      *
     A            POCN           6A         TEXT('pay - ordering customer no')
     A                                      COLHDG('pay' +
     A                                             'ord' +
     A                                             'cust')
      *
     A            RCRN           8A         TEXT('receiver correspondent no')
     A                                      COLHDG('recv' +
     A                                             'corr')
      *
     A            RCRA          35A         TEXT('receiver correspondent +
     A                                      a/c line')
     A                                      COLHDG('recv' +
     A                                             'corr' +
     A                                             'a/c')
      *
     A            RVNO           8A         TEXT('Receiver number')
     A                                      COLHDG('recv')
      *
     A            AWBN           8A         TEXT('a/c with bank no')
     A                                      COLHDG('a/c' +
     A                                             'bank' +
     A                                             'no')
      *
     A            AWBA          35A         TEXT('a/c with bank a/c line')
     A                                      COLHDG('a/c' +
     A                                             'bank' +
     A                                             'a/c')
      *
     A            BENN           8A         TEXT('beneficiary no')
     A                                      COLHDG('bene')
      *
     A            BENA          35A         TEXT('beneficiary a/c line')
     A                                      COLHDG('bene' +
     A                                             'a/c')
      *
     A            DTP1          35A         TEXT('Details of Pay 1')
     A                                      COLHDG('Details of Pay 1')
      *
     A            DTP2          35A         TEXT('Details of Pay 2')
     A                                      COLHDG('Details of Pay 2')
      *
     A            DTP3          35A         TEXT('Details of Pay 3')
     A                                      COLHDG('Details of Pay 3')
      *
     A            DTP4          35A         TEXT('Details of Pay 4')
     A                                      COLHDG('Details of Pay 4')
      *
     A            DCHG           3A         TEXT('Details of charges')
     A                                      COLHDG('chgs')
      *
     A            BTB1          35A         TEXT('bank to bank info 1')
     A                                      COLHDG('bank' +
     A                                             'to' +
     A                                             'bank 1')
      *
     A            BTB2          35A         TEXT('bank to bank info 2')
     A                                      COLHDG('bank' +
     A                                             'to' +
     A                                             'bank 2')
      *
     A            BTB3          35A         TEXT('bank to bank info 3')
     A                                      COLHDG('bank' +
     A                                             'to' +
     A                                             'bank 3')
      *
     A            BTB4          35A         TEXT('bank to bank info 4')
     A                                      COLHDG('bank' +
     A                                             'to' +
     A                                             'bank 4')
      *
     A            BTB5          35A         TEXT('bank to bank 5')
     A                                      COLHDG('bank to bank 5')
      *
     A            BTB6          35A         TEXT('bank to bank 6')
     A                                      COLHDG('bank to bank 6')
      *
     A            CVMR           1A         TEXT('COVER MESSAGE REQUIRED')
     A                                      COLHDG('COV' +
     A                                             'MSG' +
     A                                             'REQ')
      *
     A            FSRP          11P 7       TEXT('Funding Spread/Rate')
     A                                      COLHDG('Funding' +
     A                                             'Spread' +
     A                                             'Rate')
      *
     A            FSGN           1A         TEXT('Funding Sign')
     A                                      COLHDG('Funding' +
     A                                             'Sign')
      *
     A            FPRC           4A         TEXT('Funding Profit Centre')
     A                                      COLHDG('Funding' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            COFA          13P 0       TEXT('COF Amount')
     A                                      COLHDG('COF' +
     A                                             'Amount' +
     A                                             'Manual Adjustment')
      *
     A            IRCF           1A         TEXT('Interest Rate Change Flag')
     A                                      COLHDG('Interest Rate' +
     A                                             'Change' +
     A                                             'Flag')
      *
     A            FRCF           1A         TEXT('Funding Rate Change Flag')
     A                                      COLHDG('Funding Rate' +
     A                                             'Change' +
     A                                             'Flag')
      *
     A            DFTP           2A         TEXT('Default Part. Loan +
     A                                            Type')
     A                                      COLHDG('Def Part' +
     A                                             'Loan Type')
      *
     A            DFST           2A         TEXT('Default Part. Loan +
     A                                            Subtype')
     A                                      COLHDG('Def Part' +
     A                                             'Loan Styp')
      *
     A            MNSG           1A         TEXT('Manual/System +
     A                                            Generated')
     A                                      COLHDG('Man/Sys' +
     A                                             'Gen.')
      *
     A            GASS           1A         TEXT('Generated by +
     A                                            Assignment')
     A                                      COLHDG('Gen by' +
     A                                             'Assgn?')
      *
     A            GPRT           1A         TEXT('Generated by +
     A                                            Principal Tfr')
     A                                      COLHDG('Prin' +
     A                                             'Tfr?')
      *
     A            NRLI           1A         TEXT('New Receiving Loan +
     A                                            Indicator')
     A                                      COLHDG('New Rec' +
     A                                             'Loan?')
      *
     A            ROLN           6A         TEXT('Receiving/Originating +
     A                                            Loan')
     A                                      COLHDG('Rec/Orig' +
     A                                             'Loan')
      *
     A            ROSN           3S 0       TEXT('Receiving/Originating +
     A                                            Seq No.')
     A                                      COLHDG('Rec/Orig' +
     A                                             'Seq No')
      *
     A            ROBR           3A         TEXT('Receiving/Originating +
     A                                            Branch')
     A                                      COLHDG('Rec/Orig' +
     A                                             'Branch')
      *
     A            RORC           6A         TEXT('Receiving/Originating +
     A                                            Cust')
     A                                      COLHDG('Rec/Orig' +
     A                                             'Customer')
      *
     A            PDGN           1A         TEXT('PD A/C Key Generation')
     A                                      COLHDG('PD A/C' +
     A                                             'Gen')
      *
     A            GRIN           1A         TEXT('Grace Days Indicator')
     A                                      COLHDG('Grace' +
     A                                             'Days?')
      *
     A            RLCY           3A         TEXT('MC New Currency')
     A                                      COLHDG('MC New' +
     A                                             'Ccy')
      *
     A            PNAM          13P 0       TEXT('Penalty Amount')
     A                                      COLHDG('Penalty Amount')
      *
     A            PAST           1A         TEXT('Past Due Indicator')
     A                                      COLHDG('Past' +
     A                                             'Due?')
      *
     A            ASTS           1A         TEXT('Loan Amend Status +
     A                                            Indicator')
     A                                      COLHDG('Status')
      *
     A            IUSR          10A         TEXT('Insert User Profile')
     A                                      COLHDG('Ins User' +
     A                                             'Profile')
      *
     A            AUSR          10A         TEXT('Amend User Profile')
     A                                      COLHDG('Amd User' +
     A                                             'Profile')
      *
     A            XUSR          10A         TEXT('Authorise User +
     A                                            Profile')
     A                                      COLHDG('Auth User' +
     A                                             'Profile')
      *
     A            PCRF          15A         TEXT('PC Transaction +
     A                                            Reference')
     A                                      COLHDG('PC Trans' +
     A                                             'Reference')
      *
     A            PCOB           3A         TEXT('PC Originating +
     A                                            Branch')
     A                                      COLHDG('PC Orig' +
     A                                             'Branch')
      *
     A            MTPD           1A         TEXT('Matched to Past +
     A                                            Due')
     A                                      COLHDG('Matched' +
     A                                             'Past Due')
      *
     A            PDDI           1A         TEXT('Payment Due Day +
     A                                            Indicator')
     A                                      COLHDG('Pay Due' +
     A                                             'Day Ind')
      *
     A            PTDI           1A         TEXT('Payment Taken Day +
     A                                            Indicator')
     A                                      COLHDG('Pay Taken' +
     A                                             'Day Ind')
      *
     A            SCCY           3A         TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
      *
     A            ICCY           3A         TEXT('IN Currency in +
     A                                            Fld 72')
     A                                      COLHDG('IN Ccy')
      *
     A            ECIN           1A         TEXT('Euro Conversion Ind')
     A                                      COLHDG('Euro Conv Ind')
      *
     A            REPI           1A         TEXT('Replication Indicator')
     A                                      COLHDG('Replication' 'Indicator')
      *
     A            CHDU          13P 0       TEXT('Charge Amount due for +
     A                                            the period')
     A                                      COLHDG('Charge Amount' +
     A                                             'due for the' +
     A                                             'period')
      *
     A            INTN          13P 0       TEXT('INTA Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('INTA Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
      *
     A            FSPRAM        13P 0       TEXT('PRAM Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('PRAM Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
      *
     A            REXR          13P 8       TEXT('Receive Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receive' +
     A                                             'Settlement Currency' +
     A                                             'Exchange Rate')
      *
     A            REXI           1A         TEXT('Receive Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Receive' +
     A                                             'Settlement Currency' +
     A                                             'Exchange Rate Ind')
      *
     A            PSCY           3A         TEXT('Pay Settlement Currency')
     A                                      COLHDG('Pay' +
     A                                             'Settlement' +
     A                                             'Currency')
      *
     A            PEXR          13P 8       TEXT('Pay Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Pay' +
     A                                             'Settlement Currency' +
     A                                             'Exchange Rate')
      *
     A            PEXI           1A         TEXT('Pay Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Pay' +
     A                                             'Settlement Currency' +
     A                                             'Exchange Rate Ind')
      *
     A            STAL           1A         TEXT('Settlement Allocation +
     A                                           Indicator')
     A                                      COLHDG('Settlement' +
     A                                             'Allocation Indicator')
      *
     A            FRNT          20A         TEXT('Front Office transaction ID')
     A                                      COLHDG('Front Office' +
     A                                             'Transaction' +
     A                                             'Identifier')
      *
     A            AFRT          20A         TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
      *
     A            REPA           1A         TEXT('Repair location')
     A                                      COLHDG('Repair' +
     A                                             'Location')
      *
     A            TMST            Z         TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
      *
     A            OSAC          18A         TEXT('Our Settlement +
     A                                            Account Number')
     A                                      COLHDG('Our Sett.' +
     A                                             'A/c Number')
      *
     A            RONS          18A         TEXT('Receipt Settlement')
      *
     A            PONS          18A         TEXT('Pay Settlement')
      *
     A            AUTH           1A         TEXT('Automatic +
     A                                           Authorisation')
     A                                      COLHDG('Automatic' +
     A                                           'Authorisation')
      *
     A            FRCY           3A         TEXT('Fclty Rept Ccy')
     A                                      COLHDG('Fac ' +
     A                                             'Rept' +
     A                                             'Ccy' )
      *
     A            FRAM          13P 0       TEXT('Fclty Ccy Princ Amt')
     A                                      COLHDG('Fac Ccy' +
     A                                             'Principal' +
     A                                             'Amount')
      *
     A            FINT          13P 0       TEXT('Fclty Ccy Int Amt')
     A                                      COLHDG('Fac Ccy' +
     A                                             'Interest' +
     A                                             'Amount')
      *
     A            FTXA          13P 0       TEXT('Fclty Ccy Tax Amt')
     A                                      COLHDG('Fac Ccy' +
     A                                             'Tax' +
     A                                             'Amount')
      *
     A            FTOT          13P 0       TEXT('Fclty Ccy Total Amt')
     A                                      COLHDG('Fac Ccy' +
     A                                             'Total' +
     A                                             'Amount')
      *
     A            FPEN          13P 0       TEXT('Fclty Ccy Penalty Amt')
     A                                      COLHDG('Fac Ccy' +
     A                                             'Penalty' +
     A                                             'Amount')
      *
     A            BASR          11P 7       TEXT('Base Rate')
     A                                      COLHDG('Base Rate')
      *
     A            SLTP           2A         TEXT('Default Sub Part. +
     A                                            Loan Type')
     A                                      COLHDG('Def Sub Part' +
     A                                             'Loan Type')
      *
     A            SLST           2A         TEXT('Default Sub Part. +
     A                                            Loan Subtype')
     A                                      COLHDG('Def Sub Part' +
     A                                             'Loan Styp')
      *
     A            CLAS           4A         TEXT('Loan Class')
     A                                      COLHDG('Loan' 'Class')
      *
     A            DFCL           4A         TEXT('Default Part. Loan +
     A                                            Class')
     A                                      COLHDG('Def Part' +
     A                                             'Loan Class')
      *
     A            SLCL           4A         TEXT('Default Sub Part. +
     A                                            Loan Class')
     A                                      COLHDG('Def Sub Part' +
     A                                             'Loan Class')
