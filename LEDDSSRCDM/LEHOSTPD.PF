     A*****************************************************************
/*STDA*  PFBASE                                                       *
/*EXSA*  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Host Interface postings file')                *
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  PF/LEHOSTPD - Customer Lending Host Interface postings file  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 BUG8334            Date 29Aug05               *
      *                 CLE036             Date 20Oct03               *
      *                 218782             Date 18Jun03               *
      *                 CLE033  *CREATE    Date 20Mar03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *  BUG8334- Incorrect payment and receipt settlement account in *
      *           LEHOSTPD                                            *
      *  CLE036 - Lending Enhancements for Nordea Phase 1 Priority 2: *
      *           Correct rate field decimal places.                  *
      *  218782 - Add Program ref., Sequence Number & Status fields.  *
      *  CLE033 - Settlement method 09                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R LEHOSTD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A            HOACTN        10          TEXT('Action')
     A                                      COLHDG('Action')
     A            HOTRYP        20          TEXT('Transaction Type')
     A                                      COLHDG('Transaction' +
     A                                            'Type')
     A            HOETYP    R               REFFLD(ETYP  )
     A            HOETYD        20          TEXT('Event Type Description')
     A                                      COLHDG('Event Type' +
     A                                             'Description')
      * Begin /CSD027/
     A**********  HOCNUM    R               REFFLD(CNUM  )                                    CSD027
     A            HOCNUM         6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
      **Start of change CLE148
     A**********  HOLNRF    R               REFFLD(LNRF  )                                    CLE148
     A            HOLNRF         6A         TEXT('Loan Number')
     A                                      COLHDG('Loan' 'Number')
      **End of change CLE148
     A            HOLASN    R               REFFLD(LASNS )
     A            HOFTYP    R               REFFLD(FTYP0 )
     A            HOFSEQ    R               REFFLD(FSEQ  )
     A            HOFESQ         2S 0       TEXT('Fee sequence number')
     A                                      COLHDG('Fee sequence' +
     A                                              'number')
     A            HOBRCA    R               REFFLD(BRCA  )
     A            HOLTYP    R               REFFLD(LTYP  )
     A            HOSUTP    R               REFFLD(SUTP  )
     A            HOPSTD    R               REFFLD(PSTDA )
     A            HOVALD    R               REFFLD(VALDA )
     A            HOCCY     R               REFFLD(CCY   )
     A            HOPSTA    R               REFFLD(MHPSTA)
     A            HOPNAR    R               REFFLD(PNAR  )
     A            HOAUTO    R               REFFLD(AUTO  )
     A            HOTLXY    R               REFFLD(TLXY  )
     A            HOINTR    R               REFFLD(INTRAT)
     A            HOBRTT         2          TEXT('Base rate code')
     A                                      COLHDG('Base rate' 'code')
      **Start of change CLE036
     A**********  HOBRTE        11S 0       TEXT('Base rate')                                 CLE036
     A            HOBRTE    R               REFFLD(INTRAT)
     A                                      TEXT('Base rate')
      **End of Change CLE036
     A                                      COLHDG('Base' 'rate')
      **Start of Change CLE036
     A**********  HORTSP        11S 0       TEXT('Rate/Spread')                               CLE036
     A            HORTSP    R               REFFLD(INTRAT)
     A                                      TEXT('Rate/Spread')
      **End of Change CLE036
     A                                      COLHDG('Rate/Spread')
     A            HOSPIN    R               REFFLD(SPIN  )
     A            HOMARG         7S 5       TEXT('Margin')
     A                                      COLHDG('Margin')
     A            HOSCCY    R               REFFLD(SCCY  )
     A            HOREXR        13S 8       TEXT('Recp-Settl ccy Exchange rate')
     A                                      COLHDG('Recp-Settl' +
     A                                             'ccy' 'Exchange rate')
     A            HOREXI         1          TEXT('Recp-Settl ccy Mult/div Ind')
     A                                      COLHDG('Recp-Settl' +
     A                                             'ccy' 'Mult/div Ind')
     A            HORHBR         3          TEXT('Recp-Host branch')
     A                                      COLHDG('Recp-Host' 'branch')
      **Start of change BUG8334
     A**********  HORHAC        12          TEXT('Recp-Host account')                        BUG8334
     A            HORHAC        18          TEXT('Recp-Host account')
      **End of Change BUG8334
     A                                      COLHDG('Recp-Host' 'account')
      * Begin /CSD027/
     A**********  HOROBN    R               REFFLD(ROBN  )                                    CSD027
     A**********  HOROCN    R               REFFLD(ROCN  )                                    CSD027
     A            HOROBN         6A
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            HOROCN         6A
     A                                      TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            HORIBN    R               REFFLD(RIBN  )
     A            HORIBA    R               REFFLD(RIBA  )
     A            HOPSCY    R               REFFLD(PSCY  )
     A            HOPEXR        13S 8       TEXT('Paym-Settl ccy Exchange rate')
     A                                      COLHDG('Paym-Settl ccy' +
     A                                             'Exchange rate')
     A            HOPEXI         1          TEXT('Paym-Settl ccy Mult/div Ind ')
     A                                      COLHDG('Paym-Settl ccy' +
     A                                             'Mult/div Ind')
     A            HOPHBR         3          TEXT('Paym-Host branch')
     A                                      COLHDG('Paym-Host' 'branch')
      **Start of change BUG8334
     A**********  HOPHAC        12          TEXT('Paym-Host account')                        BUG8334
     A            HOPHAC        18          TEXT('Paym-Host account')
      **End of change BUG8334
     A                                      COLHDG('Paym-Host account')
      * Begin /CSD027/
     A**********  HOPOBN    R               REFFLD(POBN  )                                    CSD027
     A**********  HOPOCN    R               REFFLD(POCN  )                                    CSD027
     A            HOPOBN         6A
     A                                      TEXT('pay - ordering bank no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            HOPOCN         6A
     A                                      TEXT('pay - ordering customer no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            HOPIBA    R               REFFLD(PIBA  )
     A            HORVNO    R               REFFLD(RVNO  )
     A            HOCVMR    R               REFFLD(CVMR  )
     A            HORCRN    R               REFFLD(RCRN  )
     A            HORCRA    R               REFFLD(RCRA  )
     A            HOAWBN    R               REFFLD(AWBN  )
     A            HOAWBA    R               REFFLD(AWBA  )
     A            HOBENN    R               REFFLD(BENN  )
     A            HOBENA    R               REFFLD(BENA  )
     A            HODTP1    R               REFFLD(DTP1  )
     A            HODTP2    R               REFFLD(DTP2  )
     A            HODTP3    R               REFFLD(DTP3  )
     A            HODTP4    R               REFFLD(DTP4  )
     A            HODCHG    R               REFFLD(DCHG  )
     A            HOBTB1    R               REFFLD(BTB1  )
     A            HOBTB2    R               REFFLD(BTB2  )
     A            HOBTB3    R               REFFLD(BTB3  )
     A            HOBTB4    R               REFFLD(BTB4  )
     A            HOBTB5    R               REFFLD(BTB5  )
     A            HOBTB6    R               REFFLD(BTB6  )
     A            HOTNLU    R               REFFLD(TNLUN )
      * Start of Change 218782
     A            HOPROG        10          TEXT('Program Reference')
     A                                      COLHDG('Program' 'Reference')
     A            HOSEQN         6S 0       TEXT('Sequence Number')
     A                                      COLHDG('Seq' 'Number')
     A            HOSTAT        10          TEXT('Status')
     A                                      COLHDG('Status')
      * End of Change 218782
      *****************************************************************
     A*                                              Begin /CLE042/
     A            HOCLAS         4A         TEXT('Loan Class')
     A                                      COLHDG('Loan' 'Class')
     A*                                              End   /CLE042/
