      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Invalid fee settlement details')              *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEIFESTPD - Invalid Fee Settlement Details                *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE073             Date 12May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAP086             Date 08Jun05               *
      *                 CAP084             Date 07Aug03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP072  *CREATE    Date 27Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE073 - Calculated Loan Based Fees                          *
      *  CAP086 - Introduce Auto Authorisation to the API's           *
      *           without it.                                         *
      *  CAP084 - Java Wrappers project - add currency field          *
      *  CAP072 - Fee Adjustment/Settlements Transaction Details API  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R LEIFESTD0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A            XREPN          5S 0       TEXT('Repair Number')
     A                                      COLHDG('Repair' 'Number')
     A            XTMST           Z         TEXT('Timestamp')
     A                                      COLHDG('Time' 'Stamp')
      *
     A            DDFRNT    R               REFFLD(FOTRNID)
     A                                      TEXT('Front Office+
     A                                            Transaction Id')
     A                                      COLHDG('Front Office' +
     A                                             'Transaction Id')
     A            DDAFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            DDREPA    R               REFFLD(RPRLOC)
      *
      * The following fields are the screen details
      *
     A            S#ACTN         1A         TEXT('Action')
     A            S#CSSN        10A         TEXT('Customer Number')
     A            S#FACT         3A         TEXT('Facility Type')
     A            S#FCNO         2A         TEXT('Facility Number')
     A            S#FSEQ         2A         TEXT('Fee Sequence Number')
     A            S#VDAT         6A         TEXT('Value Date')
     A            S#SADJ        14A         TEXT('Fee Settlement Amount')
     A            S#TYPE         1A         TEXT('Writ-Off/Full/Part Pay Ind.')
     A            S#FCCY         3A         TEXT('Fee Currency')                              CAP084
      *
     A            F5PCOB         3A         TEXT('PC Originating Branch')
     A            F5TNRF        15A         TEXT('PC Transaction Reference')
     A            F5IUSR        10A         TEXT('Insert User Profile')
     A            F5XUSR        10A         TEXT('Authorise User Profile')
     A            F5AUSR        10A         TEXT('Amend User Profile')
     A**Start of change CAP086
     A            F5AUTH         1A         TEXT('Automatic Authorisation')
     A                                      COLHDG('Automatic' +
     A                                       'Authorisation')
     A**End of change CAP086
      *
      **Start of Change CLE073
     A            S#LOAN         6A         TEXT('Loan Reference')
     A                                      COLHDG('Loan' 'Ref.')
      **End of Change CLE073
