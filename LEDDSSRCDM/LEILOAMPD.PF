      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE API loan amendment - invalid transactions')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEILOAMPD - Invalid Loan Amendment Transaction Details    *
      *                                                               *
      *  Note: Some fields at the end of this file are only used to   *
      *        repair a transaction that failed while coming thru the *
      *        Loan Manager Interface (see module LELOAMCTU). These   *
      *        fields are never validated, nor part of the standard   *
      *        API Interface, but are intended to keep the values     *
      *        passed by Loan Manager.                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE042             Date 06Sep05               *
      *  Prev Amend No. CAP086             Date 08Jun05               *
      *                 CAP075  *CREATE    Date 08Jul02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CAP086 - Introduce Auto Authorisation to the API's           *
      *           without it.                                         *
      *  CAP075 - Lending API enhancements - Loan Amendment           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LEILOAMD0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A            DDFRNT    R               REFFLD(FOTRNID)
     A                                      TEXT('Front Office+
     A                                            Transaction Id')
     A                                      COLHDG('Front Office' +
     A                                             'Transaction Id')
     A            DDAFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            DDREPR    R               REFFLD(RPRLOC)
      *
      ** Timestamp
     A            DDSTMP    R               REFFLD(TIMESTMP)
     A                                      TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
      *
      ** The following fields are the Screen details
     A            DDACTN         1A         TEXT('Action Code')
     A                                      COLHDG('Action' 'Code')
     A            DDLOAN         6A         TEXT('Loan Number')
     A                                      COLHDG('Loan' 'Number')
     A            DDVDAT         6A         TEXT('Value Date')
     A                                      COLHDG('Value' 'Date')
     A            DDSEQN         3A         TEXT('Sequence Number')
     A                                      COLHDG('Sequence' 'Number')
     A            DDATYP         2A         TEXT('Amendment Type')
     A                                      COLHDG('Amendment' 'Type')
     A            DDCNUM        10A         TEXT('Customer Number')
     A                                      COLHDG('Customer' 'Number')
     A            DDBRAT         2A         TEXT('Base Rate')
     A                                      COLHDG('Base' 'Rate')
     A            DDRTSP        12A         TEXT('Rate/Spread')
     A                                      COLHDG('Rate/Spread')
     A            DDCURR         3A         TEXT('Currency')
     A                                      COLHDG('Currency')
     A            DDAMNT        14A         TEXT('Amount')
     A                                      COLHDG('Amount')
     A            DDSTPA         1A         TEXT('Start/Stop Accruals')
     A                                      COLHDG('Start/Stop' 'Accruals')
     A            DDSPI1        30A         TEXT('Special Instruction 1')
     A                                      COLHDG('Special' 'Instruction 1')
     A            DDSPI2        30A         TEXT('Special Instruction 2')
     A                                      COLHDG('Special' 'Instruction 2')
     A            DDSPI3        30A         TEXT('Special Instruction 3')
     A                                      COLHDG('Special' 'Instruction 3')
     A            DDFRAT        12A         TEXT('Funding Spread/Rate')
     A                                      COLHDG('Funding' 'Spread/Rate')
     A            DDSTYP         2A         TEXT('Loan Sub-Type')
     A                                      COLHDG('Loan' 'Sub-Type')
     A            DDMDAT         6A         TEXT('Maturity Date')
     A                                      COLHDG('Maturity' 'Date')
     A            DDDFTP         2A         TEXT('Dflt Particip. Loan Type')
     A                                      COLHDG('Dflt Particip.' 'Loan Type')
     A            DDDFST         2A         TEXT('Dflt Particip. Loan S/Type')
     A                                      COLHDG('Dflt Particip.'   +
     A                                             'Loan S/Type')
      *
      ** Fields from Loan Manager PC Interface - Message Format LEA1MFPD
     A            DDGASS         1A         TEXT('Generated by Assignment')
     A            DDPCOB         3A         TEXT('PC Originating Branch')
     A            DDIUSR        10A         TEXT('Insert User Profile')
     A            DDAUSR        10A         TEXT('Amend User Profile')
     A            DDXUSR        10A         TEXT('Authorise User Profile')
     A            DDTRDS         8A         TEXT('Transaction Date Stamp')
     A            DDNRLI         1A         TEXT('New Receiving Loan Indicator')
     A            DDROLN         6A         TEXT('Receiving/Originating Loan')
     A            DDROSN         3A         TEXT('Receiving/Originating Seq No')
     A            DDRORC         6A         TEXT('Receiving/Originating Cust')
     A            DDROBR         3A         TEXT('Receiving/Originating Branch')
     A            DDPCRF        15A         TEXT('PC Transaction Reference')
     A            DDPTIN         1A         TEXT('Principal Transfer Indicator')
     A            DDTRSN         4A         TEXT('Transaction Sequence No.')
     A**Start of change CAP086
     A            DDAUTH         1A         TEXT('Automatic Authorisation')
     A**End of change CAP086
     A*                                              Begin /CLE042/
     A            DDSLST    R               REFFLD(SUTP)
     A                                      TEXT('Default Sub-Part. Loan +
     A                                            Subtype')
     A                                      COLHDG('Def Sub-Part' +
     A                                             'Loan Styp')
     A            DDCLAS         4A         TEXT('Loan Class')
     A                                      COLHDG('Loan' 'Class')
     A            DDDFCL         4A         TEXT('Dflt Particip. Loan Class')
     A                                      COLHDG('Dflt Particip.'   +
     A                                             'Loan Class')
     A            DDSLCL         4A         TEXT('Default Sub Part. +
     A                                            Loan Class')
     A                                      COLHDG('Def Sub Part' +
     A                                             'Loan Class')
     A*                                              End   /CLE042/
