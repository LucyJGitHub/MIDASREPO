      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('MIdas LE Manual Utilisation Transaction')
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  PF/LEMNFUPD - Manual Utilisation Transaction                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD055657           Date 14May20               *
      *  Prev Amend No. AR674226           Date 04Dec19               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CLE106             Date 01Aug04               *
      *                 CLE039             Date 20Oct03               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CLE009             Date 11May99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CLE005  *CREATE    Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD055657 - Manual transaction from FFC TI Interface went to  *
      *             repair queue because the facility exchange rate is*
      *             longer than the defined facility exchange rate in *
      *             Midas. Increase the field definition of the Midas *
      *             facility exchange rate.                           *
      *  AR674226 - Manual transaction reached 999. Increase the size *
      *             of SQNO to accomodate 9999 transactions.          *
      *             (Child: AR674227)                                 *
      *           - Applied for MD054782.                             *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE106 - Syndications Manager.                               *
      *  CLE039 - General LE Enhancement                              *
      *  CLE025 - Credit Lines                                        *
      *  CLE009 - LE I/C Swift Message Generation for Fees            *
      *  CLE005 - Customer Lending Enhancements - Others              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LEMNFUPF                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            RECI      R
     A            BRCA      R
      * Begin /CSD027/
     A**********  CNUM      R               REFFLD(FCUS)                                      CSD027
     A            CNUM           6A
     A                                      COLHDG('Facility            ' +
     A                                      'Customer')
      * End /CSD027/
     A                                      TEXT('Facility Customer +
     A                                      Number')
     A            FACT      R
     A                                      TEXT('Facility Type')
     A            FCNO      R
     A                                      TEXT('Facility Number')
      **Start of Change AR674226
     A**********  SQNO      R               REFFLD(AMSQ)                                    AR674226
     A**********                            TEXT('Utilisation +                             AR674226
     A**********                            Sequence No.')                                  AR674226
     A**********                            COLHDG('Util. Seq.' +                           AR674226
     A**********                                   'No.')                                   AR674226
     A            QQSQNO         3S 0
      **End of Change AR674226
     A            STDT      R               REFFLD(MFED)
     A                                      TEXT('Start Date')
     A                                      COLHDG('Start Date')
     A            EXDT      R               REFFLD(MFED)
     A            UCCY      R               REFFLD(CCY)
     A                                      TEXT('Utilisation +
     A                                      Currency')
     A                                      COLHDG('Util. Ccy')
     A            UAMT      R               REFFLD(AFPY)
     A                                      TEXT('Utilisation Amount')
     A                                      COLHDG('Util. Amount')
      **Start of change MD055657
     A**********  UXRT      R               REFFLD(CCIR)                                    MD055657
     A**********                            TEXT('Utilisation Exch+                         MD055657
     A**********                            ange Rate')                                     MD055657
     A**********                            COLHDG('Util. Exch' +                           MD055657
     A**********                                   'Rate')                                  MD055657
     A            QQUXRT        11P 7
      **End of change MD055657
     A            UXID      R               REFFLD(ACCL)
     A                                      TEXT('Utilisation Exch+
     A                                      ange Ind.')
     A                                      COLHDG('Util. Exch.' +
     A                                             'Ind.')
     A            UNAR      R               REFFLD(TRNA)
     A            UADT      R               REFFLD(VDAT)
     A                                      TEXT('Utilisation Amend +
     A                                      Date')
     A                                      COLHDG('Util. Amend' +
     A                                             'Date')
     A            PUAM      R               REFFLD(AFPY)
     A                                      TEXT('Prev. Utilisation +
     A                                      Amount')
     A                                      COLHDG('Prev. Util.' +
     A                                             'Amount')
     A            USTS      R               REFFLD(RSTA)
     A                                      TEXT('Record Status')
     A            IUSR      R               REFFLD(CTOW)
     A                                      TEXT('Insert User')
     A                                      COLHDG('Insert' +
     A                                             'User')
     A            AUSR      R               REFFLD(CTOW)
     A                                      TEXT('Amend User')
     A                                      COLHDG('Amend' +
     A                                             'User')
     A            XUSR      R               REFFLD(CTOW)
     A                                      TEXT('Authorise User')
     A                                      COLHDG('Auth.' +
     A                                             'User')
     A            ORED      R               REFFLD(VDAT)
     A                                      TEXT('Original Entry +
     A                                      Date')
     A                                      COLHDG('Orig. Entry' +
     A                                             'Date')
     A            TLCD      R               REFFLD(VDAT)
     A                                      TEXT('Transaction Last +
     A                                      Change Date')
     A                                      COLHDG('Trans. Last' +
     A                                             'Chg. Date')
     A            CHTP      R
     A                                      TEXT('Last Change Type')
      *
      *                                              Begin /CLE009/
     A            AUTH      R               REFFLD(CCIO)
     A                                      TEXT('Authorised Indicator')
     A                                      COLHDG('Auth' 'Id')
      *
     A            SWRI      R               REFFLD(CCIO)
     A                                      TEXT('Fee Regeneration +
     A                                            Indicator')
     A                                      COLHDG('Fee' 'Regen' 'Id')
      *                                              End   /CLE009/
     A**Start of change CLE025
     A            FIXR           1A         TEXT('Use Fixed Rates for +
     A                                            Collateralised Lending')
     A                                      COLHDG('Use Fixed Rates for' +
     A                                             'Collateralised' +
     A                                             'Lending')
     A**End of change CLE025
     A**Start of change CLE039
     A            MTID          20P 0       TEXT('Manual Transaction ID')
     A                                      COLHDG('Manual' 'Transaction' 'ID')
     A**End of change CLE039
     A**Start of change CLE106
     A            FUTN          15A         TEXT('Facility UTN')
     A                                      COLHDG('Facility UTN')
     A            MUTN          15A         TEXT('Utilisation UTN')
     A                                      COLHDG('Utilisation UTN')
     A**End of change CLE106
      **Start of change AR674226
     A            SQNO           4S 0
     A                                      TEXT('Utilisation +
     A                                      Sequence No.')
     A                                      COLHDG('Util. Seq.' +
     A                                             'No.')
      **End of change AR674226
      **Start of change MD055657
     A            UXRT          13P 8       TEXT('Utilisation Exch+
     A                                      ange Rate')
     A                                      COLHDG('Util. Exch' +
     A                                             'Rate')
      **End of change MD055657
