      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Re-valued loans using N-T-R file')            *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LENPVLPD - Re-valued Loans Using Net-Treasury-Rate File   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CAS005  *MOVED     Date 16Dec02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004.  Move    *
      *           from DP to DM library.                              *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
     A          R LENPVLD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            LEBRCA         3          TEXT('Branch Code')
     A                                      COLHDG('Branch' +
     A                                             'Code')
     A            LECYCD         3          TEXT('Currency Code')
     A                                      COLHDG('Currency' +
     A                                             'Code')
     A            LEBKCD         2          TEXT('Book Code')
     A                                      COLHDG('Book' +
     A                                             'Code')
      * Begin /CSD027/
     A**********  LECNUM         6S 0       TEXT('Customer Number')                           CSD027
     A            LECNUM         6A         TEXT('Customer Number')
      * End /CSD027/
     A                                      COLHDG('Customer' +
     A                                             'Number')
      **Start of change CLE148
     A**********  LELNRF         6S 0       TEXT('Loan Number')                               CLE148
     A            LELNRF         6A         TEXT('Loan Number')
      **End of change CLE148
     A                                      COLHDG('Loan' +
     A                                             'Number')
     A            LELTYP         2          TEXT('Loan Type')
     A                                      COLHDG('Loan' +
     A                                             'Type')
     A            LELNST         2          TEXT('Loan Sub-type')
     A                                      COLHDG('Loan' +
     A                                             'Sub-type')
     A            LELAMT        13P 0       TEXT('Loan Amount')
     A                                      COLHDG('Loan' +
     A                                             'Amount')
     A            LENPVA        15P 0       TEXT('NPV Amount')
     A                                      COLHDG('NPV' +
     A                                             'Amount')
     A            LEYLDC         5          TEXT('Yield Curve')
     A                                      COLHDG('Yield' +
     A                                             'Curve')
     A            LEAINT        13P 0       TEXT('Accrued Interest')
     A                                      COLHDG('Accrued' +
     A                                             'Interest')
     A            LEURPL        13P 0       TEXT('Unrealized Profit or Loss')
     A                                      COLHDG('Unrealized' +
     A                                             'Profit or Loss')
     A            LEPUPL        13P 0       TEXT('Prev Unrealized P/L')
     A                                      COLHDG('Prev Unrealized' +
     A                                             'P/L')
     A            LETUPL        13P 0       TEXT('Today''s Profit or Loss')
     A                                      COLHDG('Today''s Profit' +
     A                                             'or Loss')
     A            LEYUPL        13P 0       TEXT('Unrealized P/L at Yr-End')
     A                                      COLHDG('Unrealized P/L' +
     A                                             'at Yr-End')
     A*                                              Begin /CLE042/
     A            LELNCL         4A         TEXT('Loan Class')
     A                                      COLHDG('Loan' 'Class')
     A*                                              End   /CLE042/
