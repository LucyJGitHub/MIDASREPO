      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Past Due & Original Fees Link')               *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEPDUFPD - Past Due & Original Fees Link                  *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134  *CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *                                                               *
      *****************************************************************
      *
     A          R LEPDUFD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2012')
      *
     A            YRECI          1A         COLHDG('Record Id')
     A            YPLON          6A         COLHDG('Past Due' 'Call Loan')
     A            YVDAT          5P 0       COLHDG('Past Due' 'Call Loan' +
     A                                             'Value Date')
     A            YECNUM         6A         COLHDG('Original Fee' +
     A                                             'Customer Number')
     A            YEFACL         5S 0       COLHDG('Original Fee' +
     A                                             'Facility Number')
     A            YESEQ          2S 0       COLHDG('Original Fee' 'Sequence')
     A            YPORG          6A         COLHDG('Original Fee' 'Loan Number')
     A            YBRCA          3A         COLHDG('Original Fee' 'Branch')
     A            YPCPAM        15P 0       COLHDG('Principal Amount' +
     A                                             'Transferred to PDCL')
     A            YPFAMU         1A         COLHDG('Facility' 'Amount' +
     A                                              'Utilisation')
     A            YPSIIN         1A         COLHDG('Separate' 'Indicator')
     A            YPCIIN         1A         COLHDG('Capitalized' 'Indicator')
      *
      ** PDCL created in I/C  - Principal Take-on a/c
      *
     A            YTKPACL        6A         COLHDG('Takeon Ac Pr' 'Client')
     A            YTKPABR        3A         COLHDG('Takeon Ac Pr' 'Branch')
     A            YTKPAAC       10S 0       COLHDG('Takeon Ac Pr' 'Account')
     A            YTKPASE        2S 0       COLHDG('Takeon Ac Pr' 'Sequence')
     A            YTKPACY        3A         COLHDG('Takeon Ac Pr' 'Ccy')
      *
      ** PDCL created in I/C  - Classification Fields - Reporting
      *
     A            YRIDT          5P 0       COLHDG('Reporting Past Due' +
     A                                             'Initial Date')
     A            YRCAT          1S 0       COLHDG('Reporting Past Due' +
     A                                             'Classification')
      *
      ** PDCL created in I/C  - Classification Fields - Basel II
      *
     A            YBIDT          5P 0       COLHDG('Basel II Past Due' +
     A                                             'Initial Date')
     A            YBCAT          1S 0       COLHDG('Basel II Past Due'  +
     A                                             'Classification')
      *
      ** PDCL created in I/C  - Decrease Indicator
      *
     A            YDECRE         1A         COLHDG('Decrease Indicator')
      *
     A            YPDVDT         5P 0       COLHDG('Value Date' 'of Repayment')
      *
     A            YLCD           5P 0       COLHDG('Last Change Date')
     A            YTYLC          1A         COLHDG('Last Change Type')
      *
     A            YPIUSE        10A         COLHDG('Input' 'User')
     A            YPCBIC         1A         COLHDG('I-I/C' 'C-COB')
      *
      ** Fee repayment amount
      *
     A            YPOREP        15P 0       COLHDG('Fee Original ' +
     A                                             'Repayment Amnt')
      *
      ** PDCL system fields
      *
     A            YPFRNT        20A         COLHDG('Front' 'Office' +
     A                                             'Transaction ID')
     A            YPTMST          Z         COLHDG('Timestamp')
      *
