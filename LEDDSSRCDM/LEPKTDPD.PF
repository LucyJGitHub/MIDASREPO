      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Fee Past Due Call Loans Acc Keys Detail')     *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *   PF/LEPKTDPD - Fee Past Due Call Loans Account Keys Details  *
      *                 Processed tomorrow                            *
      *                 Output by LER135, processed on the following  *
      *                 COB by LER134.                                *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE134  *CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *                                                               *
      *****************************************************************
      *
     A          R LKEYFEDF                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2012')
      *
     A            LKRECI         1A         TEXT('Record ID')
     A                                      COLHDG('Record ID')
     A            QQAKEY        10A
     A            LKLNNO         6A         TEXT('Loan No.')
     A                                      COLHDG('Loan No.')
     A            LKCNUM         6A         TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
     A            LKBRCD         3A         TEXT('Branch Code')
     A                                      COLHDG('Branch' 'Code')
     A            LKACSQ         2S 0       TEXT('Account Sequence')
     A                                      COLHDG('Account' 'Sequence')
     A            LKEDAT         5P 0       TEXT('Event Date')
     A                                      COLHDG('Event Date')
     A            LKEAMT        13P 0       TEXT('Event Amount')
     A                                      COLHDG('Event Amount')
     A            LKECCY         3A         TEXT('Event Currency')
     A                                      COLHDG('Event Ccy')
     A            LKSETP         2S 0       TEXT('Settlement Type')
     A                                      COLHDG('Settlement' 'Type')
     A            QQOSAC        12A
     A            LKREVI         1S 0       TEXT('Reversal Indicator')
     A                                      COLHDG('Reversal' 'Ind')
     A            LKOTHA        13P 0       TEXT('Other Amount')
     A                                      COLHDG('Other Anount')
     A            LKOTHC         3A         TEXT('Other Currency')
     A                                      COLHDG('Other Ccy')
     A            LKEXRT        13P 8       TEXT('Exchange Rate')
     A                                      COLHDG('Exchange Rate')
     A            LKSTDT         5P 0       TEXT('Facility Fee +
     A                                            Start Date')
     A                                      COLHDG('Facility Fee' +
     A                                              'Start Date')
     A            LKSLID         5P 0       TEXT('Start Date or +
     A                                            Last Int. Date')
     A                                      COLHDG('Start Date or' +
     A                                              'Last Int. Date')
     A            LKMDAT         5P 0       TEXT('Maturity Date')
     A                                      COLHDG('Maturity Date')
      ** Start of change CSD103
     A**********  LKBRTT         2S 0       TEXT('Base Rate Code')
     A            LKBRTT         2A         TEXT('Base Rate Code')
      ** End of change CSD103
     A                                      COLHDG('Base Rate' 'Code')
     A            LKBRTE        11P 7       TEXT('Current Base Rate')
     A                                      COLHDG('Current' +
     A                                              'Base Rate')
     A            LKRTSP        11P 7       TEXT('Rate/spread')
     A                                      COLHDG('Rate/spread')
     A            LKINTR        11P 7       TEXT('Interest Rate')
     A                                      COLHDG('Interest Rate')
     A            LKSSEQ         1S 0       TEXT('Sort Sequence')
     A                                      COLHDG('Sort Seq.')
     A            LKCPAM        13P 0       TEXT('Current Principal +
     A                                            Amount')
     A                                      COLHDG('Current Principal' +
     A                                             'Amount')
     A            LKPACD         5P 0       TEXT('Previous Interest +
     A                                           Accrual Control Date')
     A                                      COLHDG('Prev Principal' +
     A                                             'Accrual Date')
     A            LKCFTP         1A         TEXT('C/Fee Type')
     A                                      COLHDG('C/Fee Type')
     A            LKFAMT        13P 0       TEXT('Facility Amount')
     A                                      COLHDG('Facility Amount')
     A            LKPRFC         4A         TEXT('Profit Centre')
     A                                      COLHDG('Profit Centre')
     A            LKOSBR         3A         TEXT('Our Settlement +
     A                                            Branch')
     A                                      COLHDG('Our Sett.' +
     A                                             'Branch')
     A            LKCMPY         3A         TEXT('Company Code')
     A                                      COLHDG('Company Code')
     A            LKBOKC         2A         TEXT('Book Code')
     A                                      COLHDG('Book Code')
     A            LKADAT        13P 0       TEXT('Event Amount')
     A                                      COLHDG('Event Amount')
     A            LKSCCY         3A         TEXT('Settlement Currency')
     A                                      COLHDG('Sett. CCY')
     A            F1FFLG         1A         TEXT('Fee Flag')
     A                                      COLHDG('Fee Flag')
     A            F1FVAL        13P 0       TEXT('Fee Value')
     A                                      COLHDG('Fee Value')
     A            F1FPCT         5S 2       TEXT('Fee Percent')
     A                                      COLHDG('Fee Percent')
     A            F1RACT        20A         TEXT('Receipt - Origin +
     A                                      Account')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Account')
     A            F1RCTY         2A         TEXT('Receipt - Origin +
     A                                      Country')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Country')
     A            F1RBNK        20A         TEXT('Receipt - Origin +
     A                                      Bank')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Bank')
     A            F1RCDE        11A         TEXT('Receipt - Origin +
     A                                      Code')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Code')
     A            F1PACT        20A         TEXT('Pay - Origin +
     A                                      Account')
     A                                      COLHDG('Pay - Origin +
     A                                      Account')
     A            F1PCTY         2A         TEXT('Pay - Origin +
     A                                      Country')
     A                                      COLHDG('Pay - Origin +
     A                                      Country')
     A            F1PBNK        20A         TEXT('Pay - Origin Bank')
     A                                      COLHDG('Pay - Origin Bank')
     A            F1PCDE        11A         TEXT('Pay - Origin Code')
     A                                      COLHDG('Pay - Origin Code')
     A            F1DACT        35A         TEXT('Destination Account')
     A                                      COLHDG('Destination +
     A                                      Account')
     A            F1DCTY         2A         TEXT('Destination Country')
     A                                      COLHDG('Destination' +
     A                                      'Country')
     A            F1DBNK        20A         TEXT('Destination Bank')
     A                                      COLHDG('Destination Bank')
     A            F1SDAT         5P 0       TEXT('Settlement Date')
     A                                      COLHDG('Settlement Date')
     A            LKSIND         1A         TEXT('Settlement Event Indicator')
     A                                      COLHDG('Settlement' 'Event' +
     A                                           'Indicator')
     A            LKRIND         1A         TEXT('Rollover Event Indicator')
     A                                      COLHDG('Rollover' 'Event' +
     A                                           'Indicator')
     A            QQOKEY        10A
     A            LKOSAC        18A         TEXT('Our Settlement +
     A                                            Account Number')
     A                                      COLHDG('Our Sett.' +
     A                                             'A/c Number')
     A            LKAKEY        14A         TEXT('Account Key')
     A                                      COLHDG('Account' 'Key')
     A            LKOKEY        14A         TEXT('Originating Account Key')
     A                                      COLHDG('Originating' 'Account' +
     A                                           'Key')
     A            LKOLNO         6A         TEXT('Original Loan Number')
      *                                     COLHDG('Original' 'Loan No.')
     A            LKAAMT        15P 0       TEXT('Available Amount')
     A                                      COLHDG('Available' 'Amount')
     A            LKCNU2         6A         TEXT('Fee Customer Number')
     A                                      COLHDG('Fee' 'Customer' 'Number')
     A            LKLOAN         6A         TEXT('Fee Loan')
     A                                      COLHDG('Fee' 'Loan')
     A            LKFACL         5S 0       TEXT('Fee Facility')
     A                                      COLHDG('Fee' 'Facility')
     A            LKFSEQ         2S 0       TEXT('Fee Sequence')
     A                                      COLHDG('Fee' 'Sequence')
     A            LKFCOD         2S 0       TEXT('Fee Code')
     A                                      COLHDG('Fee Code')
     A            LKFBRC         3A         TEXT('Fee Branch')
     A                                      COLHDG('Fee' 'Branch')
     A            LKTYPE         1A         TEXT('Write Off +
     A                                            Full/Partial Amnt')
     A                                      COLHDG('Write off' +
     A                                            'Full/Partial Amnt')
     A            LKIPFL         1A         COLHDG('processed')
      *
