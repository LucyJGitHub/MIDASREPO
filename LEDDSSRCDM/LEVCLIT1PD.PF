      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Customer Loans Valids - Italian')             *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEVCLIT1PD - Customer Loans Valids - Italian              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. LUC139             Date 22Dec20               *
      *  Prev Amend No. CER009  *CREATE    Date 16Aug06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UIC Italian returns upgrade to FM2.1                *
      *  CER009 - Puma II Upgrade to MidasPlus                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R LEVCLIT1D0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
      *
     A**********  VPFLNRF   R               TEXT('Loan Number')                         LUC139
     A            VPFLNRF        6A         TEXT('Loan Number')                         LUC139
     A                                      COLHDG('Loan' ' Number')
     A**********                            REFFLD(LOAN)                                LUC139
     A            VPFMOOP        1S 0       TEXT('Motivation of Operation')
     A                                      COLHDG('Motivation' ' of Operation')
     A            VPFAGCT        3S 0       TEXT('Agricultural Credit Class')
     A                                      COLHDG('Agricultural' 'Credit' +
     A                                             'Class')
     A            VPFAGCL        3S 0       TEXT('Agricultural Laws Class')
     A                                      COLHDG('Agricultural' 'Laws Class')
     A            VPFECBD        2S 0       TEXT('Economical Branch of Dest')
     A                                      COLHDG('Economical' +
     A                                             'Branch of Dest')
     A            VPFSBCR        1S 0       TEXT('Subsidized Credit Indicator')
     A                                      COLHDG('Subsidized' 'Credit' +
     A                                             'Indicator')
     A            VPFINSB        1S 0       TEXT('Subsidized Int. for Export')
     A                                      COLHDG('Subsidized Int.' +
     A                                             'Export')
     A            VPFTPPR        6S 0       TEXT('Type of Product')
     A                                      COLHDG('Type of Product')
     A            VPFPEPR        6S 0       TEXT('Personalisation of Operation')
     A                                      COLHDG('Personalization' +
     A                                             'of Operation')
     A            VPFGLPA        1S 0       TEXT('Global/Partial Operation')
     A                                      COLHDG('Global/Partial' +
     A                                             'Operation')
     A            VPFTPFD        1S 0       TEXT('Operation with Third Party')
     A                                      COLHDG('Global/Partial' +
     A                                             'Operation')
     A            VPFSBLW        3A         TEXT('Law for Subsidized Credit Cd')
     A                                      COLHDG('Law for Subsidized' +
     A                                             'Credit Code')
     A            VPFDPIV        5S 0       TEXT('Destination Province +
     A                                           Investment')
     A                                      COLHDG('Destination Province' +
     A                                             'Investment')
     A            VPFEDIV        3A         TEXT('Economical Dest of +
     A                                           Investment')
     A                                      COLHDG('Economical Dest.' +
     A                                             'of Investment')
     A            VPFCATC       11S 0       TEXT('Category Code CR')
     A                                      COLHDG('Category Code CR')
     A            VPFTPAC        3S 0       TEXT('Type of Activity')
     A                                      COLHDG('Type of Activity')
     A            VPFCLRL        3S 0       TEXT('Client Role CR')
     A                                      COLHDG('Client Role CR')
     A            VPFACST        3S 0       TEXT('Account Status CR')
     A                                      COLHDG('Account Status CR')
     A            VPFLNIT        7A         TEXT('Line Item of Usura Report')
     A                                      COLHDG('Line Item of' +
     A                                             'Usura Report')
     A            VPFTEGL        8S 3       TEXT('Effective Global Rate')
     A                                      COLHDG('Effective' ' Global Rate')
     A            VPFUNDT        5P 0       TEXT('Scaduto da')
     A                                      COLHDG('Scaduto da')
     A            VPFPSAM       13P 0       TEXT('Quota aggiuntiva altrui')
     A                                      COLHDG('Quota aggiuntiva' +
     A                                             'altrui')
     A            VPFLEAD        6A         TEXT('Capofila (Valutaria)')
     A                                      COLHDG('Capofila' '(Valutaria)')
     A            VPFFRNT   R               TEXT('Front Office ID')
     A                                      COLHDG('Front Office ID')
     A                                      REFFLD(FOTRNID)
     A            VPFTMES   R               TEXT('Time stamp')
     A                                      COLHDG('Time stamp')
     A                                      REFFLD(TIMESTMP)
      ** Start of LUC139
     A            VPFPTRP        3S 0       TEXT('Priority Risk Code Prd')
     A                                      COLHDG('Pty Rsk Cd')
     A            VPFCMPC        8S 3       TEXT('Comm Rate Max U Balance')
     A                                      COLHDG('Comm Rt' 'Max UBal')
      ** End of LUC139
