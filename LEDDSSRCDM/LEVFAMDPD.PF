      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Facility amendment transaction file')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEVFAMDPD - Valid Facility Amendment Transaction File     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 05Dec05               *
      *  Prev Amend No. CLE106             Date 01Aug04               *
      *                 CAP086             Date 08Jun05               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP071  *CREATE    Date 20Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE106 - Syndications Manager. Added Pro Rata Indicator.     *
      *  CAP086 - Introduce Auto Authorisation to the API's           *
      *           without it.                                         *
      *  CAP071 - Conversion of LE inputs into modular structure to   *
      *           use as APIs. Assignments Transaction Details API    *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LEVFAMDD0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            AMRECI    R               REFFLD(RECI)
     A            AMBRCA    R               REFFLD(BRCA)
      * Begin /CSD027/
     A**********  AMCNUM    R               REFFLD(CNUM)                                      CSD027
     A            AMCNUM         6A
      * End /CSD027/
     A                                      TEXT('Facility Customer +
     A                                            Number')
     A                                      COLHDG('Fclty Cust' +
     A                                             'Number')
     A            AMFACT    R               REFFLD(FACT)
     A            AMFCNO    R               REFFLD(FCNO)
     A            AMSQNO    R               REFFLD(FACT)
     A                                      TEXT('Amendment Sequence +
     A                                            Number')
     A                                      COLHDG('Amend Seq.' +
     A                                             'Number')
     A            AMVLDT    R               REFFLD(VLDT)
     A            AMFATP    R               REFFLD(AMTP)
     A                                      TEXT('Facility Amend Type')
     A                                      COLHDG('Fclty Amend' +
     A                                             'Type')
     A            AMACCY    R               REFFLD(ACCY)
     A                                      TEXT('Amendment Currency')
     A                                      COLHDG('Amend' 'Ccy')
     A            AMAAMT    R               REFFLD(AAMT)
     A                                      TEXT('Amendment Amount')
     A                                      COLHDG('Amend' 'Amt')
     A            AMRVCR    R               REFFLD(RVCR)
     A            AMNDEX    R               REFFLD(MFED)
     A                                      TEXT('New Expiry Date')
     A                                      COLHDG('New Expiry' +
     A                                             'Date')
     A            AMASTS    R               REFFLD(ASTS)
     A                                      TEXT('Record Status')
     A                                      COLHDG('Rec' 'Sts')
     A            AMIUSR    R               REFFLD(RSTU)
     A                                      TEXT('Insert User')
     A                                      COLHDG('Insert' 'User')
     A            AMAUSR    R               REFFLD(RSTU)
     A                                      TEXT('Amend User')
     A                                      COLHDG('Amend' 'User')
     A            AMXUSR    R               REFFLD(RSTU)
     A                                      TEXT('Authorise User')
     A                                      COLHDG('Auth.' 'User')
     A            AMORED    R               REFFLD(ORED)
     A            AMTLCD    R               REFFLD(TLCD)
     A                                      TEXT('Transaction Last +
     A                                            Change Date')
     A                                      COLHDG('Trans Last' +
     A                                             'Chnge Date')
     A            AMCHTP    R               REFFLD(CHTP)
     A            AMPCRF    R               REFFLD(ZZ015)
     A                                      TEXT('PC Transaction +
     A                                            Reference')
     A                                      COLHDG('PC Trans' +
     A                                             'Reference')
     A            AMPCOB    R               REFFLD(BRCA)
     A                                      TEXT('PC Originating +
     A                                            Branch')
     A                                      COLHDG('PC Orig' +
     A                                             'Branch')
      *
     A            AMGASSA   R               REFFLD(SPIN)
     A                                      TEXT('Generated by +
     A                                            Assignment')
     A                                      COLHDG('Gen by' +
     A                                             'Assgn?')
      *
     A            AMAUTH    R               REFFLD(CCIO)
     A                                      TEXT('Authorised Indicator')
     A                                      COLHDG('Auth' 'Id')
      *
     A            AMSWRI    R               REFFLD(CCIO)
     A                                      TEXT('Fee Regeneration +
     A                                            Indicator')
     A                                      COLHDG('Fee' 'Regen' 'Id')
     A            AMFRNT    R               REFFLD(FOTRNID)
     A            AMAFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            AMREPA    R               REFFLD(RPRLOC)
 
      ** Timestamp
     A            AMSTMP    R               REFFLD(TIMESTMP)
      ** Start of change CAP086
      ** Automatic Authorisation
     A            AMAUTO         1A         TEXT('Automatic Authorisation')
     A                                      COLHDG('Automatic' 'Authorisation')
      **End of change CAP086
     A** Start of change CLE106
     A            AMNPRT         1A         TEXT('Pro Rata Indicator')
     A                                      COLHDG('Pro Rata Indicator')
     A** End of change CLE106
