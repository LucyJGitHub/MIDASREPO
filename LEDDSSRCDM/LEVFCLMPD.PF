      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Facility detail record A')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *   PF/LEVFCLMPD - Valid Funding Participant Details Record 'A' *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE064             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CGL029             Date 01Sep03               *
      *                 CLE029             Date 04Sep02               *
      *                 CLE031             Date 03Feb03               *
      *                 CLE028             Date 27Jun02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP068  *CREATE    Date 20Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE064 - Enhancement of Lending Frequencies                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *           THE FOLLOWING FILES SHOULD HAVE THE SAME LAYOUT:    *
      *           GZFCLTYFM = GZLEAGFMPD = LEVFCLMPD = LEVFCIMPD =    *
      *           FCLUPFM                                             *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE029 - Addition of Severity code and Limit ID to FCLTYFM   *
      *  CLE031 - Lending Enhancements.                               *
      *  CLE028 - Flat Rate Personal Loans (Rule of 78s)              *
      *  CAP068 - Conversion of LE inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R LEVFCLMD0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A            FPRECI    R               REFFLD(RECI)
      * Begin /CSD027/
     A**********  FPCNUM    R               REFFLD(CNUM)                                      CSD027
     A            FPCNUM         6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            FPFACT    R               REFFLD(FACT)
     A            FPFCNO    R               REFFLD(FCNO)
     A            FPRCTP    R               REFFLD(RCTP)
     A            FPBRCA    R               REFFLD(BRCA)
     A            FPFCTI    R               REFFLD(FCTI)
     A            FPFCCY    R               REFFLD(FCCY)
     A            FPFAMT    R               REFFLD(FAMT)
     A            FPOVPC    R               REFFLD(OVPC)
     A            FPDTAP    R               REFFLD(DTAP)
     A            FPDTEX    R               REFFLD(DTEX)
     A            FPRVCR    R               REFFLD(RVCR)
     A            FPSECI    R               REFFLD(SECI)
     A            FPMCCY    R               REFFLD(MCCY)
     A            FPMCSI    R               REFFLD(MCSI)
     A            FPNACD    R               REFFLD(NACD)
     A            FPCRSK    R               REFFLD(CRSK)
     A            FPDLFS    R               REFFLD(DLFS)
     A            FPNOMR    R               REFFLD(NOMR)
     A            FPRVDT    R               REFFLD(RVDT)
     A            FPRVFR    R               REFFLD(RVFR)
     A            FPRVDY    R               REFFLD(RVDY)
     A            FPNAR1    R               REFFLD(NAR1)
     A            FPNAR2    R               REFFLD(NAR2)
     A            FPNAR3    R               REFFLD(NAR3)
     A            FPNAR4    R               REFFLD(NAR4)
     A            FPMTDH    R               REFFLD(MTDH)
     A            FPMTDL    R               REFFLD(MTDL)
     A            FPMTDA    R               REFFLD(MTDA)
     A            FPQTDA    R               REFFLD(QTDA)
     A            FPYTDA    R               REFFLD(YTDA)
     A            FPACOF    R               REFFLD(ACOF)
     A            FPORBR    R               REFFLD(ORBR)
     A            FPZZ001   R               REFFLD(ZZ001)
     A            FPRVCD    R               REFFLD(RVCD)
     A            FPZZ002A  R               REFFLD(ZZ002)
     A            FPORED    R               REFFLD(ORED)
     A            FPLCD     R               REFFLD(LCD)
     A            FPCHTP    R               REFFLD(CHTP)
     A            FPTNLU    R               REFFLD(TNLU)
      *
     A            FPSYIN    R               REFFLD(SPIN)
     A                                      TEXT('Syndication Indicator')
     A                                      COLHDG('Synd' +
     A                                             'Ind')
      *
      * Begin /CSD027/
     A**********  FPANUM    R               REFFLD(CNUM)                                      CSD027
     A            FPANUM         6A
      * End /CSD027/
     A                                      TEXT('Agent Customer Number')
     A                                      COLHDG('Agent'+
     A                                             'Customer')
      *
     A            FPDTPS    R               REFFLD(ZZ005)
     A                                      TEXT('Drawing Types (x5)')
     A                                      COLHDG('Drawing' +
     A                                             'Types')
      *
     A            FPDICB    R               REFFLD(MRIN)
     A                                      TEXT('Default Interest Basis')
     A                                      COLHDG('Def' +
     A                                             'Int')
      *
     A            FPRTPS    R               REFFLD(ZZ006)
     A                                      TEXT('Rollover Types (x6)')
     A                                      COLHDG('Rollover' +
     A                                             'Types')
      *
     A            FPRLPD    R               REFFLD(RVDY)
     A                                      TEXT('Irregular Rollover +
     A                                            Period')
     A                                      COLHDG('Irreg' +
     A                                             'Rol Pd')
      *
     A            FPCSEL    R               REFFLD(SPIN)
     A                                      TEXT('Currency Selection +
     A                                            Ind')
     A                                      COLHDG('Ccy' +
     A                                             'Sel?')
      *
     A            FPALCY    R               REFFLD(ZZ036)
     A                                      TEXT('Allowable Currencies')
     A                                      COLHDG('Allowable Currencies')
      *
     A            FPCOMD    R               REFFLD(DDAT)
     A                                      TEXT('Date Committed')
     A                                      COLHDG('Date' +
     A                                             'Comm')
      *
     A            FPCDTE    R               REFFLD(DDAT)
     A                                      TEXT('Closing Date')
     A                                      COLHDG('Closing' +
     A                                             'Date')
      *
     A            FPAVLD    R               REFFLD(DDAT)
     A                                      TEXT('Last Availability Date')
     A                                      COLHDG('Last Avail' +
     A                                             'Date')
      *
     A            FPLPTP    R               REFFLD(SPIN)
     A                                      TEXT('Late Payment Penalty +
     A                                            Type')
     A                                      COLHDG('Late Pay' +
     A                                             'Penalty')
      *
     A            FPLPBR    R               REFFLD(RVDY)
     A                                      TEXT('Late Penalty Base Rate')
     A                                      COLHDG('Late Pen' +
     A                                             'Base Rt')
      *
     A            FPLPAM    R               REFFLD(FAMT)
     A                                      TEXT('Late Payment Penalty +
     A                                            Amount')
     A                                      COLHDG('Late Pay' +
     A                                             'Penalty Amt')
      *
     A            FPLPPS    R               REFFLD(SPIN)
     A                                      TEXT('Late Payment Penalty +
     A                                            Sign')
     A                                      COLHDG('Late Pay' +
     A                                             'Pen Sign')
      *
     A            FPDFTP    R               REFFLD(LTYP)
     A                                      TEXT('Default Part. Loan +
     A                                            Type')
     A                                      COLHDG('Def Part' +
     A                                             'Loan Type')
      *
     A            FPDFST    R               REFFLD(SUTP)
     A                                      TEXT('Default Part. Loan +
     A                                            Subtype')
     A                                      COLHDG('Def Part' +
     A                                             'Loan Styp')
      *
     A            FPTRCA    R               REFFLD(LTYP)
     A                                      TEXT('Tranche/Credit +
     A                                            Agreement Ind.')
     A                                      COLHDG('Tranche/' +
     A                                             'Credit')
      *
      * Begin /CSD027/
     A**********  FPCANM    R               REFFLD(CNUM)                                      CSD027
     A            FPCANM         6A
      * End /CSD027/
     A                                      TEXT('Credit Agreement +
     A                                            Customer')
     A                                      COLHDG('Cred Agree' +
     A                                             'Customer')
      *
     A            FPCAFT    R               REFFLD(FACT)
     A                                      TEXT('Credit Agreement +
     A                                            Facility Type')
     A                                      COLHDG('Cred Agree' +
     A                                             'Fac Type')
      *
     A            FPCAFN    R               REFFLD(FCNO)
     A                                      TEXT('Credit Agreement +
     A                                            Facility No.')
     A                                      COLHDG('Cred Agree' +
     A                                             'Fac No.')
      *
     A            FPACSQ    R               REFFLD(ACSQ)
      *
     A            FPFSTS    R               REFFLD(SPIN)
     A                                      TEXT('Facility Status +
     A                                            Indicator')
     A                                      COLHDG('Fac' +
     A                                             'Sts')
      *
     A            FPIUSR    R               REFFLD(FACO)
     A                                      TEXT('Insert User Profile')
     A                                      COLHDG('Ins User' +
     A                                             'Profile')
      *
     A            FPAUSR    R               REFFLD(FACO)
     A                                      TEXT('Amend User Profile')
     A                                      COLHDG('Amd User' +
     A                                             'Profile')
      *
     A            FPXUSR    R               REFFLD(FACO)
     A                                      TEXT('Authorise User +
     A                                            Profile')
     A                                      COLHDG('Auth User' +
     A                                             'Profile')
      *
     A            FPSYCP    R               REFFLD(SPIN)
     A                                      TEXT('Syndication Complete')
     A                                      COLHDG('Synd' +
     A                                             'Comp')
      *
     A            FPPCRF    R               REFFLD(ZZ015)
     A                                      TEXT('PC Transaction +
     A                                            Reference')
     A                                      COLHDG('PC Trans' +
     A                                             'Reference')
      *
     A            FPPRTR    R               REFFLD(SPIN)
     A                                      TEXT('Participant Facility +
     A                                            Ind.')
     A                                      COLHDG('Part' +
     A                                             'Fac?')
      *
      * Begin /CSD027/
     A**********  FPPMFC    R               REFFLD(CNUM)                                      CSD027
     A            FPPMFC         6A
      * End /CSD027/
     A                                      TEXT('Prime Facility +
     A                                            Customer')
     A                                      COLHDG('Prime Fac' +
     A                                             'Customer')
      *
     A            FPPMFT    R               REFFLD(FACT)
     A                                      TEXT('Prime Facility +
     A                                            Type')
     A                                      COLHDG('Prime Fac' +
     A                                             'Type')
      *
     A            FPPMFN    R               REFFLD(FCNO)
     A                                      TEXT('Prime Facility +
     A                                            Number')
     A                                      COLHDG('Prime Fac' +
     A                                             'Number')
      *
      * Begin /CSD027/
     A**********  FPINUM    R               REFFLD(CNUM)                                      CSD027
     A            FPINUM         6A
      * End /CSD027/
     A                                      TEXT('Introduced By')
     A                                      COLHDG('Introduced' +
     A                                             'By')
      *
     A            FPJDTE    R               REFFLD(DDAT)
     A                                      TEXT('Joining Date')
     A                                      COLHDG('Joining' +
     A                                             'Date')
      *
     A            FPSHTP    R               REFFLD(SPIN)
     A                                      TEXT('Share Type')
     A                                      COLHDG('Share' +
     A                                             'Type')
      *
     A            FPSHPC    R               REFFLD(SINP)
     A                                      TEXT('Share Percentage')
     A                                      COLHDG('Share' +
     A                                             'Percentage')
      *
     A            FPRCSI    R               REFFLD(RCSI)
      *
     A            FPSKIR    R               REFFLD(INTR)
     A                                      TEXT('Skim Interest Rate')
     A                                      COLHDG('Skim Int Rate')
      *
     A            FPSKIS    R               REFFLD(SPIN)
     A                                      TEXT('Skim Interest Rate +
     A                                            Sign')
     A                                      COLHDG('Skim Int' +
     A                                             'Rate Sign')
      *
     A            FPSKFR    R               REFFLD(INTR)
     A                                      TEXT('Skim Fees Rate')
     A                                      COLHDG('Skim Fees Rate')
      *
     A            FPSKFS    R               REFFLD(SPIN)
     A                                      TEXT('Skim Fees Rate Sign')
     A                                      COLHDG('Skim Fees' +
     A                                             'Rate Sign')
      *
     A            FPVDTC    R               REFFLD(VDAT)
     A                                      TEXT('Value Date of This +
     A                                            Change')
     A                                      COLHDG('Value Date' +
     A                                             'of this Chg')
      *
     A            FPOSDB    R               REFFLD(OSDB)
     A                                      TEXT('Receipt - Settle +
     A                                            Branch')
     A                                      COLHDG('Rec Set' +
     A                                             'Branch')
      *
     A            FPOMDB    R               REFFLD(OMDB)
     A                                      TEXT('Pay - Settle +
     A                                            Branch')
     A                                      COLHDG('Pay Set' +
     A                                             'Branch')
      *
     A            FPRSTM    R               REFFLD(RSTM)
     A**Start of change CGL029
     A**********  FPRONS    R               REFFLD(RONS)                                      CGL029
     A            QQRONS        12A
     A**End of change CGL029
     A            FPRIBN    R               REFFLD(RIBN)
     A            FPRIBA    R               REFFLD(RIBA)
      * Begin /CSD027/
     A**********  FPROBN    R               REFFLD(ROBN)                                      CSD027
     A**********  FPROCN    R               REFFLD(ROCN)                                      CSD027
     A            FPROBN         6A
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            FPROCN         6A
     A                                      TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            FPPSTM    R               REFFLD(PSTM)
     A**Start of change CGL029
     A**********  FPPONS    R               REFFLD(PONS)                                      CGL029
     A            QQPONS        12A
     A**End of change CGL029
     A            FPPIBN    R               REFFLD(PIBN)
     A            FPPIBA    R               REFFLD(PIBA)
      * Begin /CSD027/
     A**********  FPPOBN    R               REFFLD(POBN)                                      CSD027
     A**********  FPPOCN    R               REFFLD(POCN)                                      CSD027
     A            FPPOBN         6A
     A                                      TEXT('pay - ordering bank no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            FPPOCN         6A
     A                                      TEXT('pay - ordering customer no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            FPRCRN    R               REFFLD(RCRN)
     A            FPRCRA    R               REFFLD(RCRA)
     A            FPRVNO    R               REFFLD(RVNO)
     A            FPAWBN    R               REFFLD(AWBN)
     A            FPAWBA    R               REFFLD(AWBA)
     A            FPBENN    R               REFFLD(BENN)
     A            FPBENA    R               REFFLD(BENA)
     A            FPDTP1    R               REFFLD(DTP1)
     A            FPDTP2    R               REFFLD(DTP2)
     A            FPDTP3    R               REFFLD(DTP3)
     A            FPDTP4    R               REFFLD(DTP4)
     A            FPDCHG    R               REFFLD(DCHG)
     A            FPBTB1    R               REFFLD(BTB1)
     A            FPBTB2    R               REFFLD(BTB2)
     A            FPBTB3    R               REFFLD(BTB3)
     A            FPBTB4    R               REFFLD(BTB4)
     A            FPBTB5    R               REFFLD(BTB5)
     A            FPBTB6    R               REFFLD(BTB6)
     A            FPCVMR    R               REFFLD(CVMR)
      *
     A            FPPCOB    R               REFFLD(BRCA)
     A                                      TEXT('PC Originating +
     A                                            Branch')
     A                                      COLHDG('PC Orig' +
     A                                             'Branch')
      *
     A            FPCAXR    R               REFFLD(CHPC)
     A                                      TEXT('Credit Agreement +
     A                                            Exch. rate')
     A                                      COLHDG('C/A Exch.' +
     A                                             'Rate')
     A            FPCMDI    R               REFFLD(CMDI)
     A                                      TEXT('C/A Mult/Div +
     A                                            Indicator')
     A                                      COLHDG('C/A M/D' +
     A                                             'Ind.')
     A            FPCACY    R               REFFLD(CCY)
     A                                      TEXT('Credit Agreement +
     A                                            currency')
     A                                      COLHDG('C/A Ccy')
     A            FPPFTI    R               REFFLD(FATP)
     A                                      TEXT('Previous Facility +
     A                                            Type Ind.')
     A                                      COLHDG('Prev. Fclty' +
     A                                             'Type Ind.')
     A            FPSCCY    R               REFFLD(CCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
     A            FPICCY    R               REFFLD(CCY)
     A                                      TEXT('IN Currency in +
     A                                            Fld 72')
     A                                      COLHDG('IN Ccy')
     A            FPLAID    R               REFFLD(ACOF)
     A                                      TEXT('Loan Administrator Id')
     A                                      COLHDG('Loan'+
     A                                             'Adm. Id')
     A            FPPASI    R               REFFLD(SPIN)
     A                                      TEXT('Assignment/ +
     A                                            Participant')
     A                                      COLHDG('Assign?')
     A            FPREPI         1          TEXT('Replication Indicator')
     A                                      COLHDG('Replication' 'Indicator')
      ** Front office transaction ID.
     A            FPFRNT    R               REFFLD(FOTRNID)
     A                                      TEXT('Front Office +
     A                                            Transaction Id       ')
     A                                      COLHDG('Front Office' +
     A                                             'Transaction Id')
      ** Associated Front office transaction ID.
     A            FPAFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
      ** Repair location
     A            FPREPA    R               REFFLD(RPRLOC)
     A                                      TEXT('Repair Location')
     A                                      COLHDG('Repair' +
     A                                             'Location')
 
      ** Timestamp
     A            FPSTMP    R               REFFLD(TIMESTMP)
     A                                      TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
      * Start of CLE028
     A            FPDTPX         6A         TEXT('Drawing Types (x6)')
     A                                      COLHDG('Drawing Types (x6)')
      * End of CLE028
      *
      ** Start CLE031
     A            FPCOAG         1A         TEXT('Co-Agent')
     A                                      COLHDG('Co-Agent')
     A
     A            FPFCAG         1A         TEXT('Facility Agent')
     A                                      COLHDG('Facility' 'Agent')
     A
     A            FPPHAG         1A         TEXT('Paying/Handling +
     A                                           Agent')
     A                                      COLHDG('Paying/Handling' +
     A                                           'Agent')
     A
     A            FPSCAG         1A         TEXT('Security Agent')
     A                                      COLHDG('Security' 'Agent')
     A
     A            FPREXR        13P 8       TEXT('Receive Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A
     A            FPREXI         1A         TEXT('Receive Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
     A
     A            FPPSCY         3A         TEXT('Pay Settlement Currency')
     A                                      COLHDG('Pay' 'Settlement' +
     A                                           'Currency')
     A
     A            FPPEXR        13P 8       TEXT('Pay Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A
     A            FPPEXI         1A         TEXT('Pay Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
      ** End  CLE031
     A            FPOVAM        13P 0
     A                                      TEXT('Overline Amount')
     A                                      COLHDG('Overline Amount')
      *
      ** Start CLE029
     A            FPSVCD         1
     A                                      TEXT('Severity Code')
     A                                      COLHDG('Severity Code')
      ** Limit ID
     A            FPLMTI        10
     A                                      TEXT('Limit ID')
     A                                      COLHDG('Limit ID')
      ** End CLE029
     A**Start of change CGL029
     A            FPRONS        18A
     A            FPPONS        18A
     A**End of change CGL029
     A*                                              Begin /CLE042/
     A            FPAUTH         1A
     A                                      TEXT('Automatic Authorisation')
     A                                      COLHDG('Automatic' 'Authorisation')
     A            FPDFCL         4A         TEXT('Default Part. Loan +
     A                                            Class')
     A                                      COLHDG('Def Part' +
     A                                             'Loan Class')
     A*                                              End   /CLE042/
      **Start of change CLE064
     A            FPRTS15       15A         TEXT('Rollover Types 7-21')
     A                                      COLHDG('Rollover' +
     A                                             'Types 7-21')
      **End of change CLE064
