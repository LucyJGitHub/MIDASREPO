      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Fee Valid file Input Detail')                 *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEVFEEMPD - Valid Fee Input Details File                  *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      *                 CLE139             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *               . CAS019             Date 25Apr07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 BUG9496            Date 06Dec05               *
      *                 CLE106             Date 01Aug04               *
      *                 CAP086             Date 08Jun05               *
      *                 CGL029             Date 01Sep03               *
      *                 CAP078  *CREATE    Date 12Jul02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CLE148 - Alpha Loan Reference                                *
      *  CLE139 - Lending Fee Capitalisation                          *
      *  CAS019 - Upgrade of CAS016 to Midas Plus                     *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *           THE FOLLOWING FILES SHOULD HAVE THE SAME LAYOUT:    *
      *           GZLEFEED = LEVFEEMPD                                *
      *  BUG9496- Include CAS011 changes in GZLEFEED to align valids  *
      *           file format.                                        *
      *  CLE106 - Syndications Manager                                *
      *  CAP086 - Introduce Auto Authorisation to the API's           *
      *           without it.                                         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP078 - Conversion of LE inputs into modular structure to   *
      *           use as APIs. Fees Input Details API                 *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
     A          R LEVFEEMD0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A            VFRECI    R               REFFLD(RECI)
     A            VFBRCA    R               REFFLD(BRCA)
      * Begin /CSD027/
     A**********  VFCNUM    R               REFFLD(CNUM)                                      CSD027
     A            VFCNUM         6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            VFFACL    R               REFFLD(FCLY)
      **Start of change CLE148
     A**********  VFLOAN    R               REFFLD(LNRF)                                      CLE148
     A            VFLOAN         6A         TEXT('Loan number')
     A                                      COLHDG('Loan' 'Number')
      **End of change CLE148
     A            VFFSEQ    R               REFFLD(FEFSEQ)
     A            VFFCOD    R               REFFLD(FEFCOD)
     A            VFACOF    R               REFFLD(ACOF)
     A            VFFCCY    R               REFFLD(CCY)
     A            VFFAMT    R               REFFLD(FEFAMT)
     A            VFPSTD    R               REFFLD(FEPSTD)
     A            VFPEND    R               REFFLD(FEPEND)
     A            VFPYON    R               REFFLD(FEPYON)
     A            VFAUTO    R               REFFLD(AUTO)
     A            VFSTTL    R               REFFLD(SETT)
     A**Start of change CGL029
     A**********  VFOURS    R               REFFLD(OURS)                                      CGL029
     A            QQOURS        12A
     A**End of change CGL029
     A            VFTHER    R               REFFLD(THEIRS)
     A            VFCALT    R               REFFLD(FECALT)
     A            VFFRT1    R               REFFLD(FEFRT1)
     A            VFFRT2    R               REFFLD(FEFRT2)
     A            VFFRT3    R               REFFLD(FEFRT3)
     A            VFFRT4    R               REFFLD(FEFRT4)
     A            VFFRT5    R               REFFLD(FEFRT5)
     A            VFAMT1    R               REFFLD(FEAMT1)
     A            VFAMT2    R               REFFLD(FEAMT2)
     A            VFAMT3    R               REFFLD(FEAMT3)
     A            VFAMT4    R               REFFLD(FEAMT4)
     A            VFAMT5    R               REFFLD(FEAMT5)
     A            VFCALC    R               REFFLD(CALC)
     A            VFERAT    R               REFFLD(EXRT)
     A            VFMDIN    R               REFFLD(FEMDIN)
     A            VFNPYD    R               REFFLD(NPYD)
     A            VFFREQ    R               REFFLD(FREQ)
     A            VFDYIM    R               REFFLD(DYIM)
     A            VFPIND    R               REFFLD(FEPIND)
     A            VFDIND    R               REFFLD(FEDIND)
     A            VFAMTP    R               REFFLD(FEAMTP)
     A            VFAMTA    R               REFFLD(FEAMTA)
     A            VFAMTS    R               REFFLD(FEAMTS)
     A            VFSDAT    R               REFFLD(FESDAT)
     A            VFLPDT    R               REFFLD(FELPDT)
     A            VFLADT    R               REFFLD(FELADT)
     A            VFADAT    R               REFFLD(ADNP)
     A            VFDDAT    R               REFFLD(FEDDAT)
     A            VFAMTO    R               REFFLD(FEAMTO)
     A            VFLTYP    R               REFFLD(LTYP)
     A            VFSUTP    R               REFFLD(FESUTP)
     A            VFORED    R               REFFLD(ORED)
     A            VFPLPD         5P 0
     A            VFSPTI         1A
     A            VFPPTI         1A
     A            VFEPTI         1A
     A            VFSBTI         1A
     A            VFPBTI         1A
     A            VFEBTI         1A
     A            VFLCHT    R               REFFLD(CHTP)
     A            VFLCHD    R               REFFLD(LCD)
     A            VFTNLU    R               REFFLD(TNLU)
     A            VFOSBR    R               REFFLD(OSBR)
     A            VFLPAM    R               REFFLD(FEFAMT)
     A            VFPAMS    R               REFFLD(FEAMTS)
     A            VFPAMP    R               REFFLD(FEAMTP)
     A            VFADBP    R               REFFLD(ADNP)
     A            VFFSTS    R               REFFLD(SPIN)
     A                                      TEXT('Fee Status Indicator')
     A                                      COLHDG('Fee' +
     A                                             'Sts')
      *
     A            VFIUSR    R               REFFLD(FACO)
     A                                      TEXT('Insert User Profile')
     A                                      COLHDG('Ins User' +
     A                                             'Profile')
      *
     A            VFAUSR    R               REFFLD(FACO)
     A                                      TEXT('Amend User Profile')
     A                                      COLHDG('Amd User' +
     A                                             'Profile')
      *
     A            VFXUSR    R               REFFLD(FACO)
     A                                      TEXT('Authorise User +
     A                                            Profile')
     A                                      COLHDG('Auth User' +
     A                                             'Profile')
      *
     A            VFPCRF    R               REFFLD(ZZ015)
     A                                      TEXT('PC Transaction +
     A                                            Reference')
     A                                      COLHDG('PC Trans' +
     A                                             'Reference')
      *
     A            VFPTFI    R               REFFLD(SPIN)
     A                                      TEXT('Participant Fee +
     A                                            Indicator')
     A                                      COLHDG('Part Fee' +
     A                                             'Ind')
      *
     A            VFSHFI    R               REFFLD(SPIN)
     A                                      TEXT('Shared Fee Indicator')
     A                                      COLHDG('Shr Fee' +
     A                                             'Ind')
      *
     A            VFOSDB    R               TEXT('Receipt - Settle +
     A                                            Branch')
     A                                      COLHDG('Rec Set' +
     A                                             'Branch')
     A                                      REFFLD(OSDB)
      *
     A            VFOMDB    R               TEXT('Pay - Settle +
     A                                            Branch')
     A                                      COLHDG('Pay Set' +
     A                                             'Branch')
     A                                      REFFLD(OMDB)
      *
     A            VFRSTM    R               REFFLD(RSTM)
     A**Start of change CGL029
     A**********  VFRONS    R               REFFLD(RONS)                                      CGL029
     A            QQRONS        12A
     A**End of change CGL029
     A            VFRIBN    R               REFFLD(RIBN)
     A            VFRIBA    R               REFFLD(RIBA)
      * Begin /CSD027/
     A**********  VFROBN    R               REFFLD(ROBN)                                      CSD027
     A**********  VFROCN    R               REFFLD(ROCN)                                      CSD027
     A            VFROBN         6A
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            VFROCN         6A
     A                                      TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            VFPSTM    R               REFFLD(PSTM)
     A**Start of change CGL029
     A**********  VFPONS    R               REFFLD(PONS)                                      CGL029
     A            QQPONS        12A
     A**End of change CGL029
     A            VFPIBN    R               REFFLD(PIBN)
     A            VFPIBA    R               REFFLD(PIBA)
      * Begin /CSD027/
     A**********  VFPOBN    R               REFFLD(POBN)                                      CSD027
     A**********  VFPOCN    R               REFFLD(POCN)                                      CSD027
     A            VFPOBN         6A
     A                                      TEXT('pay - ordering bank no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            VFPOCN         6A
     A                                      TEXT('pay - ordering customer no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            VFRCRN    R               REFFLD(RCRN)
     A            VFRCRA    R               REFFLD(RCRA)
     A            VFRVNO    R               REFFLD(RVNO)
     A            VFAWBN    R               REFFLD(AWBN)
     A            VFAWBA    R               REFFLD(AWBA)
     A            VFBENN    R               REFFLD(BENN)
     A            VFBENA    R               REFFLD(BENA)
     A            VFDTP1    R               REFFLD(DTP1)
     A            VFDTP2    R               REFFLD(DTP2)
     A            VFDTP3    R               REFFLD(DTP3)
     A            VFDTP4    R               REFFLD(DTP4)
     A            VFDCHG    R               REFFLD(DCHG)
     A            VFBTB1    R               REFFLD(BTB1)
     A            VFBTB2    R               REFFLD(BTB2)
     A            VFBTB3    R               REFFLD(BTB3)
     A            VFBTB4    R               REFFLD(BTB4)
     A            VFBTB5    R               REFFLD(BTB5)
     A            VFBTB6    R               REFFLD(BTB6)
     A            VFCVMR    R               REFFLD(CVMR)
     A            VFGFPT    R               REFFLD(SPIN)
     A                                      TEXT('Generated by Fund +
     A                                            Participant')
     A                                      COLHDG('Gen by' +
     A                                             'Fund Part')
      *
     A            VFPTAI    R               REFFLD(SPIN)
     A                                      TEXT('Participant/Assign+
     A                                            ment Ind')
     A                                      COLHDG('Part/' +
     A                                             'Assg?')
      *
      * Begin /CSD027/
     A**********  VFSMAC    R               REFFLD(CNUM)                                      CSD027
     A            VFSMAC         6A
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A                                      TEXT('Syndicate Member/+
     A                                            Assignee')
      *
     A            VFPCOB    R               REFFLD(BRCA)
     A                                      TEXT('PC Originating +
     A                                           Branch')
     A                                      COLHDG('PC Orig' +
     A                                             'Branch')
     A            VFPRFC    R               REFFLD(PRFC)
     A            VFLSWC    R               REFFLD(LSWC)
     A            VFLSWS    R               REFFLD(LSWS)
     A            VFTADJ    R               REFFLD(D1AT)
     A                                      TEXT('Total Fee +
     A                                          Adjustment Amount')
     A                                      COLHDG('Total Fee' +
     A                                             'Adj. Amt.')
     A            VFSCCY    R               REFFLD(CCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
     A            VFICCY    R               REFFLD(CCY)
     A                                      TEXT('IN Currency in +
     A                                            Fld 72')
     A                                      COLHDG('IN Ccy')
     A            VFWOFF    R               REFFLD(D1AT)
     A                                      TEXT('Total Fee +
     A                                          Write-off Amount')
     A                                      COLHDG('Total Fee' +
     A                                             'W/Off Amt.')
     A            VFINDC    R               REFFLD(##KEY)
     A                                      TEXT('Fee Indicator+
     A                                            Y=OLD ')
     A                                      COLHDG('Fee Indicator')
     A            VFSWRI    R               REFFLD(CCIO)
     A                                      TEXT('Swift Msg Regenerati+
     A                                            on Indicator')
     A                                      COLHDG('Msg' 'Regen' 'Id')
     A            VFFEAMTW  R               REFFLD(D1AT)
     A                                      TEXT('Fee amount written off')
     A                                      COLHDG('Write off amount')
     A            VFFEREXR      13P 8
     A                                      TEXT('Receive Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A            VFFEREXI       1A
     A                                      TEXT('Receive Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
     A            VFFEPSCY       3A
     A                                      TEXT('Pay Settlement Currency')
     A                                      COLHDG('Pay' 'Settlement' +
     A                                           'Currency')
     A            VFFEPEXR      13P 8
     A                                      TEXT('Pay Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A            VFFEPEXI       1A
     A                                      TEXT('Pay Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                          'Currency' 'Exchange Rate'+
     A                                           'Ind')
     A            VFFRNT    R               REFFLD(FOTRNID)
     A            VFAFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            VFREPA    R               REFFLD(RPRLOC)
 
      ** Timestamp
     A            VFSTMP    R               REFFLD(TIMESTMP)
      *
     A            VFSTAL         1A
     A                                      TEXT('Settlement Allocation +
     A                                           Indicator')
     A                                      COLHDG('Settlement' +
     A                                           'Allocation Indicator')
     A**Start of change CGL029
     A            VFOURS        18A
     A            VFRONS        18A
     A            VFPONS        18A
     A**End of change CGL029
     A**Start of change CAP086
     A            VFAUTH         1A         TEXT('Automatic +
     A                                      Authorisation')
     A                                      COLHDG('Automatic' +
     A                                      'Authorisation')
     A**End of change CAP086
     A**Start of change BUG9496
     A            VFADJY         1          TEXT('Adjustment to Yield')
     A                                      COLHDG('Adjustment to Yield')
     A            VFAMTD        15P 0       TEXT('Non-Linear Amortised +
     A                                            Fee to Date')
     A                                      COLHDG('Non-Linear Amortised' +
     A                                             'Fee to Date')
     A            VFFEAM        15P 0       TEXT('Fee Amount to be amortised +
     A                                            for the EIR period')
     A                                      COLHDG('Fee Amount to be' +
     A                                             'amortised for the' +
     A                                             'EIR period')
     A            VFFEAC        15P 0       TEXT('Fee Amount to be accrued +
     A                                            for the EIR period')
     A                                      COLHDG('Fee Amount to be' +
     A                                             'accrued for the' +
     A                                             'EIR period')
     A            VFAFES        15P 0       TEXT('Amortised Fee from EIR +
     A                                            start date')
     A                                      COLHDG('Amortised Fee from' +
     A                                             'EIR start date')
     A            VFAFEE        15P 0       TEXT('Accrued Fee from EIR +
     A                                            start date')
     A                                      COLHDG('Accrued Fee from' +
     A                                             'EIR start date')
     A            VFAFOF        15P 0       TEXT('One off Fee from EIR +
     A                                            start date')
     A                                      COLHDG('One off Fee from' +
     A                                             'EIR start date')
     A            VFTFTD        15P 0       TEXT('Total Fee to Date')
     A                                      COLHDG('Total Fee to Date')
     A            VFAFLS        15P 0       TEXT('Accrued/Amortised Fee +
     A                                            from Last Fee Start Date')
     A                                      COLHDG('Accrued/Amortised' +
     A                                             'Fee from Last Fee' +
     A                                             'Start Date')
     A            VFUAFA        15P 0       TEXT('Unamortised Back-Valued +
     A                                            Fee Amount')
     A                                      COLHDG('Unamortised Back-' +
     A                                             'Valued Fee Amount')
     A            VFAFLD        15P 0       TEXT('Accrued fee from Last +
     A                                            Start Date')
     A                                      COLHDG('Accrued fee from' +
     A                                             'Last Start Date')
     A            VFNLAI         1A         TEXT('Non-linear Amortisation Ind')
     A                                      COLHDG('Non-lin Amort Ind')
     A**End of change BUG9496
      **Start of change CLE106
     A            VFMASF         1A         TEXT('Manual Allocation')
     A                                      COLHDG('Manual Allocation')
     A            VFSYIN         1A         TEXT('Syndication Indicator')
     A                                      COLHDG('Syndication' +
     A                                             'Indicator')
      **End of change CLE106
     A*                                              Begin /CLE042/
     A            VFCLAS         4A         TEXT('Loan Class')
     A                                      COLHDG('Loan' 'Class')
     A*                                              End   /CLE042/
      **Start of change CAS019
     A            VFSAMT        15P 0       TEXT('Non linear +
     A                                            Settlement Amount')
     A                                      COLHDG('Non linear' +
     A                                             'Settlement Amount')
     A            VFAMRI         1A         TEXT('Amortise Separately +
     A                                            Indicator')
     A                                      COLHDG('Amortise' +
     A                                             'Separately' +
     A                                             'Indicator')
     A            VFSTPD         5P 0       TEXT('Amortisation Start +
     A                                            Period')
     A                                      COLHDG('Amortisation' +
     A                                             'Start' +
     A                                             'Period')
     A            VFENPD         5P 0       TEXT('Amortisation End +
     A                                            Period')
     A                                      COLHDG('Amortisation' +
     A                                             'End' +
     A                                             'Period')
      **End of change CAS019
      **Start of change CLE139
     A            VFCAPI         1A         TEXT('Fee Capitalisation +
     A                                            Indicator')
     A                                      COLHDG('Fee Capitalisation' +
     A                                             'Indicator')
      **End of change CLE139
      **Start of change CLE134
     A            VFEAUT2        1A         COLHDG('Auto-settlement' 'Method')
     A            VFEPLNR        6A         COLHDG('Past Due' 'Call Loan')
      *
      ** Reporting Past Due Details...
      *
     A            VFERIDT        5P 0       COLHDG('Reporting Past Due' +
     A                                             'Initial Date')
     A            VFERIDC        3P 0       COLHDG('Reporting Past Due'  +
     A                                             'Initial Date' +
     A                                             'Changes Today')
     A            VFERCAT        1S 0       COLHDG('Reporting Past Due' +
     A                                             'Classification')
     A            VFERCAC        3P 0       COLHDG('Reporting' 'Past Due' +
     A                                             'Classification Chgs')
     A            VFERCSD        5P 0       COLHDG('Reporting Past Due' +
     A                                             'Classification' +
     A                                             'Start Date')
     A            VFERCDY        3P 0       COLHDG('Reporting Past Due' +
     A                                             'Classification' +
     A                                             'Days')
     A            VFERICC        3P 0       COLHDG('Reporting Past Due' +
     A                                             'Initial Dt/Class' +
     A                                             'Changes')
      *
      ** Basel II Past Due Details...
      *
     A            VFEBIDT        5P 0       COLHDG('Basel II Past Due' +
     A                                             'Initial Date')
     A            VFEBIDC        3P 0       COLHDG('Basel II Past Due'  +
     A                                             'Initial Date Changes')
     A            VFEBCAT        1S 0       COLHDG('Basel II Past Due'  +
     A                                             'Classification')
     A            VFEBCAC        3P 0       COLHDG('Basel II Past Due' +
     A                                             'Classification Chgs')
     A            VFEBCSD        5P 0       COLHDG('Basel II Past Due' +
     A                                             'Classification' +
     A                                             'Start Date')
     A            VFEBCDY        3P 0       COLHDG('Basel II Past Due' +
     A                                             'Classification' +
     A                                             'Days')
     A            VFEBICC        3P 0       COLHDG('Basel II Past Due' +
     A                                             'Initial Dt/Class' +
     A                                             'Changes')
      *
      ** Suspended Fees Details...
      *
     A            VFEFSFA        1A         COLHDG('Suspend Fee' 'Allowed')
     A            VFESFA        13P 0       COLHDG('Suspended' 'Fee Amount')
     A            VFESFAM       13P 0       COLHDG('Suspended' 'Fee Amount' +
     A                                             'Maximum')
     A            VFESFP         3P 0       COLHDG('Suspended' 'Fee Payments')
     A            VFESFPM        3P 0       COLHDG('Suspended' 'Fee Payments' +
     A                                             'Maximum')
     A            VFEFPPA        1A         COLHDG('Partial' 'Payment' +
     A                                             'Allowed')
     A            VFEPSIN        1A         COLHDG('Partial' 'Indicator')
     A            VFEPSLM       13P 0       COLHDG('Partial' 'Limit')
     A            VFEFEMC        1A         COLHDG('Early' 'Maturity' +
     A                                             'Condition')
      *
      ** Shadow Postings Unique Reference...
      *
     A            VFEXRFI        3A         COLHDG('Extension' 'File' +
     A                                             'Reference Id')
     A            VFEXRFN       10S 0       COLHDG('Extension' 'File' +
     A                                             'Reference Number')
      **End of change CLE134
