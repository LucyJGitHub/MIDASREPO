      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE API loan amendment - valid transactions')     *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending ILE Module                          *
      *                                                               *
      *  PF/LEVLOAMPD - Valid Loan Amendment Transaction Details      *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                  LE148             Date 23Jul12               *
      *                 CRE073             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CLE106             Date 01Aug04               *
      *                 CAP086             Date 08Jun05               *
      *                 CGL029             Date 01Sep03               *
      *                 CLE034             Date 01Dec03               *
      *                 CLE031             Date 12Jun03               *
      *                 CAS005             Date 16Dec02               *
      *                 CLE028             Date 27Jun02               *
      *                 CAP075  *CREATE    Date 08Jul02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CRE073 - Interest Rate Rounding                              *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CLE106 - Syndications Manager.                               *
      *  CAP086 - Introduce Auto Authorisation to the API's           *
      *           without it.                                         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE034 - Lending Enhancements Phase 1 Priority 1A            *
      *  CLE031 - Lending Enhancements.                               *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  CLE028 - Flat Rate Personal Loans (Rule of 78s)              *
      *  CAP075 - Lending API enhancements - Loan Amendment           *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
     A          R LEVLOAMD0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A            LARECI    R               REFFLD(RECI)
     A                                      TEXT('Record Id')
     A                                      COLHDG('Record' 'Id')
      *
      **Start of change CLE148
     A**********  LALNRF    R               REFFLD(LNRF)                                      CLE148
     A            LALNRF         6A
      **End of change CLE148
     A                                      TEXT('Loan number')
     A                                      COLHDG('Loan' 'number')
      *
     A            LAVDAT    R               REFFLD(VDAT0)
     A                                      TEXT('Value date')
     A                                      COLHDG('Value' 'date')
      *
     A            LALASN    R               REFFLD(LASN0)
     A                                      TEXT('Last amend sequence number')
     A                                      COLHDG('Last amend' +
     A                                             'sequence' 'number')
      *
     A            LAMRIN    R               REFFLD(MRIN)
     A                                      TEXT('Matching records indicator')
     A                                      COLHDG('Matching' 'records' +
     A                                             'indicator')
      *
     A            LALTYP    R               REFFLD(LTYP)
     A                                      TEXT('Loan type')
     A                                      COLHDG('Loan' 'type')
      *
     A            LASUTP    R               REFFLD(SUTP)
     A                                      TEXT('Loan subtype')
     A                                      COLHDG('Loan' 'subtype')
      *
     A            LAPTYP    R               REFFLD(PTYP)
     A                                      TEXT('Loan processing type')
     A                                      COLHDG('Loan' 'processing' 'type')
      *
     A            LAAMTP    R               REFFLD(AMTP)
     A                                      TEXT('Amendment type')
     A                                      COLHDG('Amendment' 'type')
      *
     A            LAPRSQ    R               REFFLD(PRSQ)
     A                                      TEXT('Processing sequence')
     A                                      COLHDG('Processing' 'sequence')
      *
      ** Start of change CSD103
     A**********  LABRTT    R               REFFLD(BRTT)
     A            LABRTT         2A
      ** End of change CSD103
     A                                      TEXT('Base rate code')
     A                                      COLHDG('Base' 'rate' 'code')
      *
     A            LARTSP    R               REFFLD(RTSP)
     A                                      TEXT('Rate/spread')
     A                                      COLHDG('Rate/spread')
      *
     A            LACCY     R               REFFLD(CCY)
     A                                      TEXT('Currency code')
     A                                      COLHDG('Currency' 'code')
      *
     A            LAPRAM    R               REFFLD(PRAM)
     A                                      TEXT('Principal amount')
     A                                      COLHDG('Principal' 'amount')
      *
     A            LAINTA    R               REFFLD(INTA)
     A                                      TEXT('Interest amount')
     A                                      COLHDG('Interest' 'amount')
      *
     A            LAWTXA    R               REFFLD(WTXA)
     A                                      TEXT('Withholding tax')
     A                                      COLHDG('Withholding' 'tax')
      *
     A            LAFEAM    R               REFFLD(FEAM)
     A                                      TEXT('Fee amount')
     A                                      COLHDG('Fee' 'amount')
      *
     A            LATAMT    R               REFFLD(TAMT)
     A                                      TEXT('Total amount')
     A                                      COLHDG('Total' 'amount')
      *
     A            LABRCA    R               REFFLD(BRCA)
     A                                      TEXT('Branch Code - Alpha')
     A                                      COLHDG('Branch' 'Code')
      *
      * Begin /CSD027/
     A**********  LACNUM    R               REFFLD(CNUM)                                      CSD027
     A            LACNUM         6A
      * End /CSD027/
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer' 'number')
      *
     A            LAREPT    R               REFFLD(REPT)
     A                                      TEXT('Repayment type')
     A                                      COLHDG('Repayment' 'type')
      *
     A            LAAUTO    R               REFFLD(AUTO)
     A                                      TEXT('Auto settlement indicator')
     A                                      COLHDG('Auto' 'settlement' +
     A                                             'indicator')
      *
     A            LASTAI    R               REFFLD(STAI)
     A                                      TEXT('Stop accruals indicator')
     A                                      COLHDG('Stop' 'accruals' +
     A                                             'indicator')
      *
     A            LASETP    R               REFFLD(SETP)
     A                                      TEXT('Settlement type')
     A                                      COLHDG('Settlement' 'type')
      *
     A**Start of change CGL029
     A**********  LAOSAC    R               REFFLD(OSAC)                                      CGL029
     A**********                            TEXT('Our settlement account num.')               CGL029
     A**********                            COLHDG('Our settlement' +                         CGL029
     A**********                                   'account num.')                            CGL029
      *
     A            QQOSAC        12A
     A**End of change CGL029
     A            LATSEN    R               REFFLD(TSEN)
     A                                      TEXT('Their settlement nostro')
     A                                      COLHDG('Their settlement' 'nostro')
      *
     A            LAFACO    R               REFFLD(FACO)
     A                                      TEXT('For account of')
     A                                      COLHDG('For account of')
      *
     A            LASPI1    R               REFFLD(SPI1)
     A                                      TEXT('Special instructions - 1')
     A                                      COLHDG('Special' 'instructions 1')
      *
     A            LASPI2    R               REFFLD(SPI2)
     A                                      TEXT('Special instructions - 2')
     A                                      COLHDG('Special' 'instructions 2')
      *
     A            LASPI3    R               REFFLD(SPI3)
     A                                      TEXT('Special instructions - 3')
     A                                      COLHDG('Special' 'instructions 3')
      *
     A            LALAIN    R               REFFLD(LAIN)
     A                                      TEXT('Loan amendment indicator')
     A                                      COLHDG('Loan' 'amendment' +
     A                                             'indicator')
      *
     A            LABRTE    R               REFFLD(BRTE)
     A                                      TEXT('Current Base rate')
     A                                      COLHDG('Current' 'Base' 'rate')
      *
     A            LASPIN    R               REFFLD(SPIN)
     A                                      TEXT('Spread indicator')
     A                                      COLHDG('Spread' 'indicator')
      *
     A            LACALC    R               REFFLD(CALC)
     A                                      TEXT('Calculation basis')
     A                                      COLHDG('Calculation' 'basis')
      *
     A            LAWTIN    R               REFFLD(WTIN)
     A                                      TEXT('Withholding tax indicator')
     A                                      COLHDG('Withholding' 'tax' +
     A                                             'indicator')
      *
     A            LALLAG    R               REFFLD(LLAG)
     A                                      TEXT('Loan/loan amend generated')
     A                                      COLHDG('Loan/loan' 'amend' +
     A                                             'generated')
      *
     A            LALWOI    R               REFFLD(LWOI)
     A                                      TEXT('Loan write off indicator')
     A                                      COLHDG('Loan' 'write off' +
     A                                             'indicator')
      *
     A            LAXAVD    R               REFFLD(XAVD0)
     A                                      TEXT('X-ref loan amend value date')
     A                                      COLHDG('X-ref' 'loan amend' +
     A                                             'value date')
      *
     A            LAXASQ    R               REFFLD(XASQ)
     A                                      TEXT('X-ref loan amend seq. number')
     A                                      COLHDG('X-ref' 'loan amend' +
     A                                             'seq. number')
      *
      * Begin /CSD027/
     A**********  LAFCUS    R               REFFLD(FCUS)                                      CSD027
     A            LAFCUS         6A
      * End /CSD027/
     A                                      TEXT('Facility customer')
     A                                      COLHDG('Facility' 'customer')
      *
     A            LAFTYP    R               REFFLD(FTYP0)
     A                                      TEXT('Facility type')
     A                                      COLHDG('Facility' 'type')
      *
     A            LAFSEQ    R               REFFLD(FSEQ)
     A                                      TEXT('Facility sequence number')
     A                                      COLHDG('Facility' 'sequence' +
     A                                             'number')
      *
      **Start of change CLE148
     A**********  LARLON    R               REFFLD(RLON)                                      CLE148
     A            LARLON         6A
      **End of change CLE148
     A                                      TEXT('Related loan number')
     A                                      COLHDG('Related' 'loan' 'number')
      *
     A            LAFECD    R               REFFLD(FECD)
     A                                      TEXT('Fee code')
     A                                      COLHDG('Fee' 'code')
      *
     A            LAPONI    R               REFFLD(PONI0)
     A                                      TEXT('Past due notices indicator')
     A                                      COLHDG('Past due' 'notices' +
     A                                             'indicator')
      *
     A            LAACTI    R               REFFLD(ACTI)
     A                                      TEXT('Action indicator')
     A                                      COLHDG('Action' 'indicator')
      *
     A            LACNFY    R               REFFLD(CNFY)
     A                                      TEXT('Confirmation indicator')
     A                                      COLHDG('Confirmation' 'indicator')
      *
     A            LATLXY    R               REFFLD(TLXY)
     A                                      TEXT('Pay/receive telex indicator')
     A                                      COLHDG('Pay/receive' 'telex' +
     A                                             'indicator')
      *
     A            LACBLY    R               REFFLD(CBLY)
     A                                      TEXT('Customer bills indicator')
     A                                      COLHDG('Customer' 'bills' +
     A                                             'indicator')
      *
     A            LAINTC    R               REFFLD(INTC)
     A                                      TEXT('Interest capitalisation ind')
     A                                      COLHDG('Interest' 'capitalisation' +
     A                                             'ind')
      *
     A            LANACD    R               REFFLD(NACD)
     A                                      TEXT('Name and address code')
     A                                      COLHDG('Name and' 'address' 'code')
      *
     A            LAACBI    R               REFFLD(ACBI)
     A                                      TEXT('Amended cust. billing ind.')
     A                                      COLHDG('Amended cust.' 'billing' +
     A                                             'ind.')
      *
     A            LAZZ01    R               REFFLD(ZZ001)
     A                                      TEXT('Filler')
     A                                      COLHDG('Filler')
      *
     A            LALSWC    R               REFFLD(LSWCC)
     A                                      TEXT('Last SWIFT confirmation seq.')
     A                                      COLHDG('Last SWIFT' 'confirmation' +
     A                                             'seq.')
      *
     A            LALSWS    R               REFFLD(LSWSC)
     A                                      TEXT('Last SWIFT sequence')
     A                                      COLHDG('Last' 'SWIFT' 'sequence')
      *
     A            LAOSBR    R               REFFLD(OSBR)
     A                                      TEXT('Our settlement branch')
     A                                      COLHDG('Our' 'settlement' 'branch')
      *
     A            LAFCLB    R               REFFLD(FCLB)
     A                                      TEXT('Facility branch code')
     A                                      COLHDG('Facility' 'branch' 'code')
      *
     A            LALSMD    R               REFFLD(LSMD)
     A                                      TEXT('Maturity date for change +
     A                                           of subtype')
     A                                      COLHDG('Maturity date' +
     A                                             'for subtype' 'change')
      *
     A            LAREBT    R               REFFLD(REBT)
     A                                      TEXT('Rebate')
     A                                      COLHDG('Rebate')
      *
     A            LAZZ34    R               REFFLD(ZZ034)
     A                                      TEXT('Filler')
     A                                      COLHDG('Filler')
      *
     A            LAORED    R               REFFLD(ORED)
     A                                      TEXT('Original entry date')
     A                                      COLHDG('Original' 'entry' 'date')
      *
     A            LALCD     R               REFFLD(LCD )
     A                                      TEXT('Last change date')
     A                                      COLHDG('Last' 'change' 'date')
      *
     A            LACHTP    R               REFFLD(CHTP)
     A                                      TEXT('Type of last change')
     A                                      COLHDG('Type of last change')
      *
     A            LATNLU    R               REFFLD(TNLU)
     A                                      TEXT('Transact. no of last update')
     A                                      COLHDG('Transact. no' +
     A                                             'of last update')
      *
     A            LARSTM    R               REFFLD(RSTM)
     A                                      TEXT('Receipt - settlement method')
     A                                      COLHDG('Receipt' 'settlement' +
     A                                             'method')
      *
     A**Start of change CGL029
     A**********  LARONS    R               REFFLD(RONS)                                      CGL029
     A**********                            TEXT('Receipt - our nostro')                      CGL029
     A**********                            COLHDG('Receipt' 'our' 'nostro')                  CGL029
      *
     A            QQRONS        12A
     A**End of change CGL029
     A            LARIBN    R               REFFLD(RIBN)
     A                                      TEXT('Receipt - intermediary +
     A                                            bank number')
     A                                      COLHDG('Receipt' 'intermediary' +
     A                                             'bank number')
      *
     A            LARIBA    R               REFFLD(RIBA)
     A                                      TEXT('Receipt - intermediary +
     A                                            bank a/c line')
     A                                      COLHDG('Receipt' 'intermediary' +
     A                                             'bank a/c line')
      *
      * Begin /CSD027/
     A**********  LAROBN    R               REFFLD(ROBN)                                      CSD027
     A            LAROBN         6A
      * End /CSD027/
     A                                      TEXT('Receipt - ordering bank no')
     A                                      COLHDG('Receipt' 'ordering' +
     A                                             'bank no')
      *
      * Begin /CSD027/
     A**********  LAROCN    R               REFFLD(ROCN)                                      CSD027
     A            LAROCN         6A
      * End /CSD027/
     A                                      TEXT('Receipt - ordering +
     A                                            customer no')
     A                                      COLHDG('Receipt' 'ordering' +
     A                                             'customer no')
      *
     A            LAPSTM    R               REFFLD(PSTM)
     A                                      TEXT('Pay - settlement method')
     A                                      COLHDG('Pay' 'settlement' 'method')
      *
     A**Start of change CGL029
     A**********  LAPONS    R               REFFLD(PONS)                                      CGL029
     A**********                            TEXT('Pay - our nostro')                          CGL029
     A**********                            COLHDG('Pay' 'our' 'nostro')                      CGL029
      *
     A            QQPONS        12A
     A**End of change CGL029
     A            LAPIBN    R               REFFLD(PIBN)
     A                                      TEXT('Pay - intermediary bank no')
     A                                      COLHDG('Pay' 'intermediary' +
     A                                             'bank no')
      *
     A            LAPIBA    R               REFFLD(PIBA)
     A                                      TEXT('Pay - intermediary +
     A                                            bank a/c line')
     A                                      COLHDG('Pay' 'intermediary' +
     A                                             'bank a/c line')
      *
      * Begin /CSD027/
     A**********  LAPOBN    R               REFFLD(POBN)                                      CSD027
     A            LAPOBN         6A
      * End /CSD027/
     A                                      TEXT('Pay - ordering bank no')
     A                                      COLHDG('Pay' 'ordering' 'bank no')
      *
      * Begin /CSD027/
     A**********  LAPOCN    R               REFFLD(POCN)                                      CSD027
     A            LAPOCN         6A
      * End /CSD027/
     A                                      TEXT('Pay - ordering customer no')
     A                                      COLHDG('Pay' 'ordering' +
     A                                             'customer no')
      *
     A            LARCRN    R               REFFLD(RCRN)
     A                                      TEXT('Receiver correspondent no')
     A                                      COLHDG('Receiver' +
     A                                             'correspondent' 'no')
      *
     A            LARCRA    R               REFFLD(RCRA)
     A                                      TEXT('Receiver correspondent +
     A                                            a/c line')
     A                                      COLHDG('Receiver' 'correspondent' +
     A                                             'a/c line')
      *
     A            LARVNO    R               REFFLD(RVNO)
     A                                      TEXT('Receiver number')
     A                                      COLHDG('Receiver' 'number')
      *
     A            LAAWBN    R               REFFLD(AWBN)
     A                                      TEXT('A/c with bank no')
     A                                      COLHDG('A/c with' 'bank no')
      *
     A            LAAWBA    R               REFFLD(AWBA)
     A                                      TEXT('A/c with bank a/c line')
     A                                      COLHDG('A/c with' 'bank' 'a/c line')
      *
     A            LABENN    R               REFFLD(BENN)
     A                                      TEXT('Beneficiary no')
     A                                      COLHDG('Beneficiary' 'no')
      *
     A            LABENA    R               REFFLD(BENA)
     A                                      TEXT('Beneficiary a/c line')
     A                                      COLHDG('Beneficiary' 'a/c line')
      *
     A            LADTP1    R               REFFLD(DTP1)
     A                                      TEXT('Details of pay 1')
     A                                      COLHDG('Details' 'of pay 1')
      *
     A            LADTP2    R               REFFLD(DTP2)
     A                                      TEXT('Details of pay 2')
     A                                      COLHDG('Details' 'of pay 2')
      *
     A            LADTP3    R               REFFLD(DTP3)
     A                                      TEXT('Details of pay 3')
     A                                      COLHDG('Details' 'of pay 3')
      *
     A            LADTP4    R               REFFLD(DTP4)
     A                                      TEXT('Details of pay 4')
     A                                      COLHDG('Details' 'of pay 4')
      *
     A            LADCHG    R               REFFLD(DCHG)
     A                                      TEXT('Details of charges')
     A                                      COLHDG('Details' 'of charges')
      *
     A            LABTB1    R               REFFLD(BTB1)
     A                                      TEXT('Bank to bank info 1')
     A                                      COLHDG('Bank to bank info 1')
      *
     A            LABTB2    R               REFFLD(BTB2)
     A                                      TEXT('Bank to bank info 2')
     A                                      COLHDG('Bank to bank info 2')
      *
     A            LABTB3    R               REFFLD(BTB3)
     A                                      TEXT('Bank to bank info 3')
     A                                      COLHDG('Bank to bank info 3')
      *
     A            LABTB4    R               REFFLD(BTB4)
     A                                      TEXT('Bank to bank info 4')
     A                                      COLHDG('Bank to bank info 4')
      *
     A            LABTB5    R               REFFLD(BTB5)
     A                                      TEXT('Bank to bank 5')
     A                                      COLHDG('Bank to bank 5')
      *
     A            LABTB6    R               REFFLD(BTB6)
     A                                      TEXT('Bank to bank 6')
     A                                      COLHDG('Bank to bank 6')
      *
     A            LACVMR    R               REFFLD(CVMR)
     A                                      TEXT('Cover message required')
     A                                      COLHDG('Cover' 'message' 'required')
      *
     A            LAFSRP    R               REFFLD(RTSP)
     A                                      TEXT('Funding spread/rate')
     A                                      COLHDG('Funding' +
     A                                             'spread' +
     A                                             'rate')
      *
     A            LAFSGN    R               REFFLD(SPIN)
     A                                      TEXT('Funding sign')
     A                                      COLHDG('Funding' +
     A                                             'sign')
      *
     A            LAFPRC    R               REFFLD(PRFC)
     A                                      TEXT('Funding profit centre')
     A                                      COLHDG('Funding' +
     A                                             'profit' +
     A                                             'centre')
      *
     A            LACOFA    R               REFFLD(AABC)
     A                                      TEXT('COF amount')
     A                                      COLHDG('COF' +
     A                                             'cmount' +
     A                                             'manual cdjustment')
      *
     A            LAIRCF    R               REFFLD(SPIN)
     A                                      TEXT('Interest rate change flag')
     A                                      COLHDG('Interest rate' +
     A                                             'change' +
     A                                             'flag')
      *
     A            LAFRCF    R               REFFLD(SPIN)
     A                                      TEXT('Funding rate change flag')
     A                                      COLHDG('Funding rate' +
     A                                             'change' +
     A                                             'flag')
      *
     A            LADFTP    R               REFFLD(LTYP)
     A                                      TEXT('Default part. loan +
     A                                            type')
     A                                      COLHDG('Def part' +
     A                                             'loan type')
      *
     A            LADFST    R               REFFLD(SUTP)
     A                                      TEXT('Default part. loan +
     A                                            subtype')
     A                                      COLHDG('Def part' +
     A                                             'loan styp')
      *
     A            LAMNSG    R               REFFLD(SPIN)
     A                                      TEXT('Manual/system +
     A                                            generated')
     A                                      COLHDG('Man/sys' +
     A                                             'gen.')
      *
     A            LAGASS    R               REFFLD(SPIN)
     A                                      TEXT('Generated by +
     A                                            assignment')
     A                                      COLHDG('Gen by' +
     A                                             'assgn?')
      *
     A            LAGPRT    R               REFFLD(SPIN)
     A                                      TEXT('Generated by +
     A                                            principal tfr')
     A                                      COLHDG('prin' +
     A                                             'tfr?')
      *
     A            LANRLI    R               REFFLD(SPIN)
     A                                      TEXT('New receiving loan +
     A                                            indicator')
     A                                      COLHDG('New rec' +
     A                                             'loan?')
      *
      **Start of change CLE148
     A**********  LAROLN    R               REFFLD(LNRF)                                      CLE148
     A            LAROLN         6A
      **End of change CLE148
     A                                      TEXT('Receiving/originating +
     A                                            loan')
     A                                      COLHDG('Rec/orig' +
     A                                             'loan')
      *
     A            LAROSN    R               REFFLD(XASQ)
     A                                      TEXT('Receiving/originating +
     A                                            seq no.')
     A                                      COLHDG('Rec/orig' +
     A                                             'seq no')
      *
     A            LAROBR    R               REFFLD(BRCA)
     A                                      TEXT('Receiving/originating +
     A                                            branch')
     A                                      COLHDG('Rec/orig' +
     A                                             'branch')
      *
      * Begin /CSD027/
     A**********  LARORC    R               REFFLD(CNUM)                                      CSD027
     A            LARORC         6A
      * End /CSD027/
     A                                      TEXT('Receiving/originating +
     A                                            cust')
     A                                      COLHDG('Rec/orig' +
     A                                             'customer')
      *
     A            LAPDGN    R               REFFLD(SPIN)
     A                                      TEXT('PD A/C key generation')
     A                                      COLHDG('PD A/C' +
     A                                             'gen')
      *
     A            LAGRIN    R               REFFLD(SPIN)
     A                                      TEXT('Grace days indicator')
     A                                      COLHDG('Grace' +
     A                                             'days?')
      *
     A            LARLCY    R               REFFLD(CCY)
     A                                      TEXT('MC new currency')
     A                                      COLHDG('MC new' +
     A                                             'ccy')
      *
     A            LAPNAM    R               REFFLD(CPAM)
     A                                      TEXT('Penalty amount')
     A                                      COLHDG('Penalty amount')
      *
     A            LAPAST    R               REFFLD(SPIN)
     A                                      TEXT('Past due indicator')
     A                                      COLHDG('Past' +
     A                                             'due?')
      *
     A            LAASTS    R               REFFLD(SPIN)
     A                                      TEXT('Loan amend status +
     A                                            indicator')
     A                                      COLHDG('Status')
      *
     A            LAIUSR    R               REFFLD(FACO)
     A                                      TEXT('Insert user profile')
     A                                      COLHDG('Ins user' +
     A                                             'profile')
      *
     A            LAAUSR    R               REFFLD(FACO)
     A                                      TEXT('Amend user profile')
     A                                      COLHDG('Amd user' +
     A                                             'profile')
      *
     A            LAXUSR    R               REFFLD(FACO)
     A                                      TEXT('Authorise user +
     A                                            profile')
     A                                      COLHDG('Auth user' +
     A                                             'profile')
      *
     A            LAPCRF    R               REFFLD(ZZ015)
     A                                      TEXT('PC transaction +
     A                                            reference')
     A                                      COLHDG('PC trans' +
     A                                             'reference')
      *
     A            LAPCOB    R               REFFLD(BRCA)
     A                                      TEXT('PC originating +
     A                                            branch')
     A                                      COLHDG('PC orig' +
     A                                             'branch')
      *
     A            LAMTPD    R               REFFLD(SPIN)
     A                                      TEXT('Matched to past +
     A                                            due')
     A                                      COLHDG('Matched' +
     A                                             'past due')
      *
     A            LAPDDI    R               REFFLD(SPIN)
     A                                      TEXT('Payment due day +
     A                                            indicator')
     A                                      COLHDG('Pay due' +
     A                                             'day ind')
      *
     A            LAPTDI    R               REFFLD(SPIN)
     A                                      TEXT('Payment taken day +
     A                                            indicator')
     A                                      COLHDG('Pay taken' +
     A                                             'day ind')
      *
     A            LASCCY    R               REFFLD(CCY)
     A                                      TEXT('Settlement currency')
     A                                      COLHDG('Sett. ccy')
      *
     A            LAICCY    R               REFFLD(CCY)
     A                                      TEXT('IN currency in +
     A                                            fld 72')
     A                                      COLHDG('IN ccy')
      *
     A            LAECIN         1A         TEXT('Euro conversion ind')
     A                                      COLHDG('Euro conv ind')
      *
     A            LAREPI         1          TEXT('Replication indicator')
     A                                      COLHDG('Replication' 'indicator')
      *                                              End   /CPB001/
      * Start of change CLE028
     A            LACHDU    R               REFFLD(AMNT)
     A                                      TEXT('Charge Amount due for +
     A                                            the period')
     A                                      COLHDG('Charge Amount' +
     A                                             'due for the' +
     A                                             'period')
      * End of change CLE028
      * Start of change CAS005
     A            LAINTN    R               REFFLD(INTA)
     A                                      TEXT('INTA Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('INTA Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
     A            LAFSPRAM  R               REFFLD(PRAM)
     A                                      TEXT('PRAM Using Net Treasury +
     A                                      Rate')
     A                                      COLHDG('PRAM Using' +
     A                                             'Net Treasury' +
     A                                             'Rate')
      * End of change CAS005
      * Start of Change CLE031
     A            LAREXR        13P 8
     A                                      TEXT('Receive Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A            LAREXI         1A
     A                                      TEXT('Receive Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
     A            LAPSCY         3A
     A                                      TEXT('Pay Settlement Currency')
     A                                      COLHDG('Pay' 'Settlement' +
     A                                           'Currency')
     A            LAPEXR        13P 8
     A                                      TEXT('Pay Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A            LAPEXI         1A
     A                                      TEXT('Pay Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                          'Currency' 'Exchange Rate'+
     A                                           'Ind')
      * End of Change CLE031
      * Start of Change CLE034
     A            LASTAL         1A
     A                                      TEXT('Settlement Allocation +
     A                                           Indicator')
     A                                      COLHDG('Settlement' +
     A                                           'Allocation Indicator')
      * End of Change CLE034
     A            LAFRNT    R               REFFLD(FOTRNID)
     A                                      TEXT('Front office transaction Id.')
     A                                      COLHDG('Front office' +
     A                                             'Transaction Id')
      *
     A            LAAFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Associated front +
     A                                            office transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'front office' +
     A                                             'transaction Id')
      *
     A            LAREPA    R               REFFLD(RPRLOC)
     A                                      TEXT('Repair location')
     A                                      COLHDG('Repair' 'location')
      *
     A            LATMST    R               REFFLD(TIMESTMP)
     A                                      TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
     A**Start of change CGL029
     A            LAOSAC        18A         TEXT('Our settlement account num.')
     A            LARONS        18A         TEXT('Receipt - our nostro')
     A            LAPONS        18A         TEXT('Pay - our nostro')
     A**End of change CGL029
     A**Start of change CAP086
     A            LAAUTH         1A         TEXT('Automatic Authorisation')
     A                                      COLHDG('Automatic' 'Authorisation')
     A**End of change CAP086
     A**Start of change CLE106
     A            LAFRCY         3A
     A                                      TEXT('Fclty Rept Ccy')
     A                                      COLHDG('Fac ' +
     A                                           'Rept' +
     A                                           'Ccy' )
     A            LAFRAM    R               REFFLD(CPAM)
     A                                      TEXT('Fclty Ccy Princ Amt')
     A                                      COLHDG('Fac Ccy' +
     A                                              'Principal' +
     A                                              'Amount')
     A            LAFINT    R               REFFLD(INTA)
     A                                      TEXT('Fclty Ccy Int Amt')
     A                                      COLHDG('Fac Ccy' +
     A                                              'Interest' +
     A                                              'Amount')
     A            LAFTXA    R               REFFLD(WTXA)
     A                                      TEXT('Fclty Ccy Tax Amt')
     A                                      COLHDG('Fac Ccy' +
     A                                              'Tax' +
     A                                              'Amount')
     A            LAFTOT    R               REFFLD(TAMT)
     A                                      TEXT('Fclty Ccy Total Amt')
     A                                      COLHDG('Fac Ccy' +
     A                                              'Total' +
     A                                              'Amount')
     A            LAFPEN    R               REFFLD(CPAM)
     A                                      TEXT('Fclty Ccy Penalty Amt')
     A                                      COLHDG('Fac Ccy' +
     A                                              'Penalty' +
     A                                              'Amount')
      *
     A            LABASR    R               REFFLD(RTSP)
     A                                      TEXT('Base rate')
     A                                      COLHDG('Base' +
     A                                             'rate')
      *
     A            LASLTP    R               REFFLD(LTYP)
     A                                      TEXT('Default Sub-Part. Loan +
     A                                            Type')
     A                                      COLHDG('Def Sub-Part' +
     A                                             'Loan Type')
      *
     A            LASLST    R               REFFLD(SUTP)
     A                                      TEXT('Default Sub-Part. Loan +
     A                                            Subtype')
     A                                      COLHDG('Def Sub-Part' +
     A                                             'Loan Styp')
     A**End of change CLE106
     A*                                              Begin /CLE042/
     A            LACLAS         4A         TEXT('Loan Class')
     A                                      COLHDG('Loan' 'Class')
     A            LADFCL         4A         TEXT('Default part. loan +
     A                                            class')
     A                                      COLHDG('Def part' +
     A                                             'loan class')
     A            LASLCL         4A         TEXT('Default Sub Part. +
     A                                            Loan Class')
     A                                      COLHDG('Def Sub Part' +
     A                                             'Loan Class')
     A*                                              End   /CLE042/
      **Start of change CRE073
     A            LACNSP        11P 7       TEXT('Contractual Spread')
     A                                      COLHDG('Contractual Spread')

     A            LACNSG         1A         TEXT('Contractual Spread Sign')
     A                                      COLHDG('Contractual' +
     A                                             'Spread Sign')

     A            LARDMT         7A         TEXT('Rounding Method')
     A                                      COLHDG('Rounding Method')

     A            LARDFD         4A         TEXT('Rounding Fraction/Decimal')
     A                                      COLHDG('Rounding Fraction/' +
     A                                             'Decimal')
      **End of change CRE073
