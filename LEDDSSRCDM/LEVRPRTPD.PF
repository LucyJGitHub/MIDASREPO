      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Valid risk participant details')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEIRPRTPD - Valid Risk Participant Details                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 05Dec05               *
      *  Prev Amend No. CLE042             Date 06Sep05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP069  *CREATE    Date 27Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *           THE FOLLOWING FILES SHOULD HAVE THE SAME LAYOUT:    *
      *           GZLEPARTPD = LEVRPRTPD = LEVASGNPD                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP069 - Risk Participants Transaction Details API           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LEVRPRTD0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A            ASBRCA    R               REFFLD(BRCA)
      * Begin /CSD027/
     A**********  ASCNUM    R               REFFLD(CNUM)                                      CSD027
     A            ASCNUM         6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            ASFACT    R               REFFLD(FACT)
     A            ASFCNO    R               REFFLD(FCNO)
      *
      * Begin /CSD027/
     A**********  ASMNUM    R               REFFLD(CNUM)                                      CSD027
     A            ASMNUM         6A
      * End /CSD027/
     A                                      TEXT('Member Customer Number')
     A                                      COLHDG('Member' +
     A                                             'Customer')
      *
      * Begin /CSD027/
     A**********  ASINUM    R               REFFLD(CNUM)                                      CSD027
     A            ASINUM         6A
      * End /CSD027/
     A                                      TEXT('Introduced By')
     A                                      COLHDG('Introduced By')
      *
     A            ASJDTE    R               REFFLD(DDAT)
     A                                      TEXT('Joining Date')
     A                                      COLHDG('Joining' +
     A                                             'Date')
      *
     A            ASCCY     R               REFFLD(CCY)
      *
     A            ASSHTP    R               REFFLD(SPIN)
     A                                      TEXT('Share Type')
     A                                      COLHDG('Share' +
     A                                             'Type')
      *
     A            ASSHPC    R               REFFLD(QTDA)
     A                                      TEXT('Share Percentage')
     A                                      COLHDG('Share' +
     A                                             'Percentage')
      *
     A            ASSHAM    R               REFFLD(CPAM)
     A                                      TEXT('Share Amount')
     A                                      COLHDG('Share' +
     A                                             'Amount')
      *
     A            ASFEBR    R               REFFLD(FCNO)
     A                                      TEXT('Fee Base Rate')
     A                                      COLHDG('Fee Base' +
     A                                             'Rate')
      *
     A            ASFERA    R               REFFLD(INTR)
     A                                      TEXT('Fee Rate/Spread')
     A                                      COLHDG('Fee Rate/Spread')
      *
     A            ASFERS    R               REFFLD(SPIN)
     A                                      TEXT('Fee Rate Sign')
     A                                      COLHDG('Fee Rate' +
     A                                             'Sign')
      *
     A            ASPTAI    R               REFFLD(SPIN)
     A                                      TEXT('Participant +
     A                                            Assignment Ind.')
     A                                      COLHDG('Part/Assg' +
     A                                             'Ind?')
      *
     A            ASDRTP    R               REFFLD(LTYP)
     A                                      TEXT('Default Risk Part +
     A                                            Loan Type')
     A                                      COLHDG('Def Risk' +
     A                                             'Loan Type')
      *
     A            ASDRST    R               REFFLD(SUTP)
     A                                      TEXT('Default Risk Part +
     A                                            Loan Subtype')
     A                                      COLHDG('Def Risk' +
     A                                             'Loan Styp')
      *
     A            ASVDTC    R               REFFLD(VDAT)
     A                                      TEXT('Value Date of This +
     A                                            Change')
     A                                      COLHDG('Value Date' +
     A                                             'of this Chg')
      *
     A            ASOSDB    R               REFFLD(OSDB)
     A                                      TEXT('Receipt - Settle +
     A                                            Branch')
     A                                      COLHDG('Rec Set' +
     A                                             'Branch')
      *
     A            ASOMDB    R               REFFLD(OMDB)
     A                                      TEXT('Pay - Settle +
     A                                            Branch')
     A                                      COLHDG('Pay Set' +
     A                                             'Branch')
      *
     A            ASRSTM    R               REFFLD(RSTM)
     A**Start of change CGL029
     A**********  ASRONS    R               REFFLD(RONS)                                      CGL029
     A            QQRONS        12A
     A**End of change CGL029
     A            ASRIBN    R               REFFLD(RIBN)
     A            ASRIBA    R               REFFLD(RIBA)
      * Begin /CSD027/
     A**********  ASROBN    R               REFFLD(ROBN)                                      CSD027
     A**********  ASROCN    R               REFFLD(ROCN)                                      CSD027
     A            ASROBN         6A
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            ASROCN         6A
     A                                      TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            ASPSTM    R               REFFLD(PSTM)
     A**Start of change CGL029
     A**********  ASPONS    R               REFFLD(PONS)                                      CGL029
     A            QQPONS        12A
     A**End of change CGL029
     A            ASPIBN    R               REFFLD(PIBN)
     A            ASPIBA    R               REFFLD(PIBA)
      * Begin /CSD027/
     A**********  ASPOBN    R               REFFLD(POBN)                                      CSD027
     A**********  ASPOCN    R               REFFLD(POCN)                                      CSD027
     A            ASPOBN         6A
     A                                      TEXT('pay - ordering bank no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            ASPOCN         6A
     A                                      TEXT('pay - ordering customer no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            ASRCRN    R               REFFLD(RCRN)
     A            ASRCRA    R               REFFLD(RCRA)
     A            ASRVNO    R               REFFLD(RVNO)
     A            ASAWBN    R               REFFLD(AWBN)
     A            ASAWBA    R               REFFLD(AWBA)
     A            ASBENN    R               REFFLD(BENN)
     A            ASBENA    R               REFFLD(BENA)
     A            ASDTP1    R               REFFLD(DTP1)
     A            ASDTP2    R               REFFLD(DTP2)
     A            ASDTP3    R               REFFLD(DTP3)
     A            ASDTP4    R               REFFLD(DTP4)
     A            ASDCHG    R               REFFLD(DCHG)
     A            ASBTB1    R               REFFLD(BTB1)
     A            ASBTB2    R               REFFLD(BTB2)
     A            ASBTB3    R               REFFLD(BTB3)
     A            ASBTB4    R               REFFLD(BTB4)
     A            ASBTB5    R               REFFLD(BTB5)
     A            ASBTB6    R               REFFLD(BTB6)
     A            ASCVMR    R               REFFLD(CVMR)
      *
     A            ASPCRF    R               REFFLD(ZZ015)
     A                                      TEXT('PC Transaction +
     A                                            Reference')
     A                                      COLHDG('PC Trans' +
     A                                             'Reference')
      *
     A            ASPCOB    R               REFFLD(BRCA)
     A                                      TEXT('PC Originating +
     A                                            Branch')
     A                                      COLHDG('PC Orig' +
     A                                             'Branch')
     A            ASSCCY    R               REFFLD(CCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
     A            ASICCY    R               REFFLD(CCY)
     A                                      TEXT('IN Currency in +
     A                                            Fld 72')
     A                                      COLHDG('IN Ccy')
     A            ASTYLC         1          TEXT('Type of Last Change')
     A                                      COLHDG('Type of' +
     A                                             'Last Change' )
      *
     A            ASFRNT    R               REFFLD(FOTRNID)
     A            ASAFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            ASREPA    R               REFFLD(RPRLOC)
 
      ** Timestamp
     A            ASSTMP    R               REFFLD(TIMESTMP)
     A*                                              Begin /CLE042/
     A            ASREXR        13P 8
     A                                      TEXT('Receive Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A            ASREXI         1A
     A                                      TEXT('Receive Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
     A            ASPSCY         3A
     A                                      TEXT('Pay Settlement Currency')
     A                                      COLHDG('Pay' 'Settlement' +
     A                                           'Currency')
     A            ASPEXR        13P 8
     A                                      TEXT('Pay Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A            ASPEXI         1A
     A                                      TEXT('Pay Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                          'Currency' 'Exchange Rate'+
     A                                           'Ind')
     A*                                              End   /CLE042/
     A**Start of change CGL029
     A            ASRONS        18A
     A            ASPONS        18A
     A**End of change CGL029
     A*                                              Begin /CLE042/
     A            ASPRTR         1A
     A                                      TEXT('Participant Type')
     A                                      COLHDG('Participant' +
     A                                           'Type')
     A            ASAFEE        13P 0
     A                                      TEXT('Agent Fee Amount')
     A                                      COLHDG('Agent Fee' +
     A                                           'Amount')
     A            ASAGFS         1A
     A                                      TEXT('Agent Fee Amount Sign')
     A                                      COLHDG('Agent Fee' +
     A                                           'Amount Sign')
     A            ASAFCD         2A
     A                                      TEXT('Agent Fee Code')
     A                                      COLHDG('Agent Fee Code')
     A
     A            ASASFE        13P 0
     A                                      TEXT('Assignee Fee Amount')
     A                                      COLHDG('Assignee Fee' +
     A                                           'Amount')
     A            ASASFS         1A
     A                                      TEXT('Assignee Fee Amount Sign')
     A                                      COLHDG('Assignee Fee' +
     A                                           'Amount Sign')
     A            ASASCD         2A
     A                                      TEXT('Assignee Fee Code')
     A                                      COLHDG('Assignee Fee' +
     A                                           'Code')
     A            ASGTYP         1A
     A                                      TEXT('Generation Type')
     A                                      COLHDG('Generation' +
     A                                           'Type')
     A            ASSEQN         2S 0       TEXT('Sequence Number')
     A                                      COLHDG('Sequence Number')
     A            ASDRCL         4A         TEXT('Default Risk Part +
     A                                            Loan Class')
     A                                      COLHDG('Def Risk' +
     A                                             'Loan Class')
     A*                                              End   /CLE042/
