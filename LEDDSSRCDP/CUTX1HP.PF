/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE CUST BILLS/TELEX MOVEMENTS')                  *
     A****************************************************************
     A*
     A*  Midas - Customer Lending Module
     A*
     A*   PF/CUTX1HP    CUSTOMER BILLS/TELEXES-MOVEMENT RECORD
     A*
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CLE031             Date 03Feb03               *
      *                 CLE028             Date 27Jun02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU004             Date 02Feb98               *
      *                 CLE004             Date 02Dec96               *
     A*                 S01117             Date 30Jun87               *
     A*                 E11351             Date 28Sep87               *
     A*
     A*---------------------------------------------------------------
     A*
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  CLE031 - Lending Enhancements.                               *
      *  CLE028 - Flat Rate Personal Loans (Rule of 78s)              *
     A*  CEU004 - LE Settlement Ccy Conversion Upgrade to DBA R2.0    *
     A*  CLE004 - Customer Lending Enhancements - Syndications        *
     A*        S01117 - MULTIBRANCHING
     A*        E11351 - FIELD ACUP WRONG LENGTH
     A*
     A****************************************************************
     A                                      REF(MFRFL)
     A          R CUTX1HPF                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            RECI      R
     A            RCDT      R
     A*                                          /S01117/
     A*********** BRCD      R
     A            BRCA      R
     A*                                          /S01117/
      * Begin /CSD027/
     A**********  CNUM      R                                                                 CSD027
     A            CNUM           6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
      **Start of change CLE148
     A**********  LNRF      R                                                                 CLE148
     A            LNRF           6A         TEXT('Loan number')
     A                                      COLHDG('Loan' 'Number')
      **End of change CLE148
     A            VDAT      R               REFFLD(VDAT0 )
     A            PRSQ      R
     A            LASN      R               REFFLD(LASN0 )
      * Begin /CSD027/
     A**********  FCUS      R                                                                 CSD027
     A            FCUS           6A
     A                                      TEXT('Facility customer')
     A                                      COLHDG('Facility            ' +
     A                                      'Customer')
      * End /CSD027/
     A            FTYP      R               REFFLD(FTYP0 )
     A            FSEQ      R
     A            PTYP      R
     A            CCY       R
     A            AMTP      R
     A            PRAM      R
     A            INTA      R
     A            WTXA      R
     A            FEAM      R
     A            FECD      R
     A            WOIN      R
     A            EXVD      R
     A            DAYS      R
     A*                                                    /E11351/
     A************ACUP      R
     A            ACUP0     R
     A*                                                    /E11351/
     A            ACBI      R
     A            ZZ006     R
     A            CNAC      R
     A            REPT      R
     A            BTRQ      R
     A            BTIN      R
      *                                              Begin /CLE004/
     A            CPAM      R
     A            AITC      R
     A            INTR      R
     A            AIAP      R
     A            IPRD      R
     A            BVMV      R               REFFLD(SPIN)
     A                                      TEXT('Back-value Movement')
     A                                      COLHDG('BV Mvmt')
      *                                              End   /CLE004/
      *                                              Begin /CEU004/
     A            SCCY      R               REFFLD(CCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
      *                                              End   /CEU004/
      **Start of change CLE028
     A            CHDU      R               REFFLD(AMNT)
     A                                      TEXT('Charge Amount due for +
     A                                            the period')
     A                                      COLHDG('Charge Amount' +
     A                                             'due for the' +
     A                                             'period')
      **End of change CLE028
      *                                              End   /CEU004/
      **Start of Change CLE031
     A            SEXR          13P 8
     A                                      TEXT('Settlement Currency +
     A                                           Exchange Rate')
     A                                      COLHDG('Settlement' 'Currency'+
     A                                           'Exchange' 'Rate')
     A            SEXI           1A
     A                                      TEXT('Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Settlement' 'Currency'+
     A                                           'Exchange' 'Rate Ind')
      **End of Change CLE031
