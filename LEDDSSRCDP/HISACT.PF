      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE History actions')                             *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*  PF/HISACT - History Actions                                  *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. AR674226           Date 04Dec19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 225591             Date 08Jul04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CLE023             Date 19Nov01               *
      *                 200289             Date 19Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CLE014             Date 20Mar01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 156585             Date 15Mar99               *
      *                 144712             Date  3Mar99               *
      *                 146936             Date 06Nov98               *
      *                 CLE005             Date 22May97               *
      *                 CLE004             Date 02Dec96               *
     A*                 S01473             Date 13Jul94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  AR674226 - Manual transaction reached 999. Increase the size *
      *             of HASQNO and HATSEQ to accomodate 9999           *
      *             transactions. (Child: AR674227)                   *
      *           - Applied for MD054782.                             *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  225591 - Facility history changes for repayments due to      *
      *           rollover                                            *
      *  CLE025 - Credit Lines                                        *
      *  CLE023 - Lending Facility History Improvements.              *
      *  200289 - Increase facility sequence number in HISACT from    *
      *           2 to 3.  Introduced new field HATSEQ.               *
      *  CLE014 - Customer Driven Lending Enhancements                *
      *  156585 - Add Assignment indicator                            *
      *  144712 - Change revolving to non-revolving, changes history  *
      *  146936 - Non-revolving credit facility overdrawn due to      *
      *           multi-ccy rollover.                                 *
     A*  CLE005 - Customer Lending Enhancements - Others (T3)         *
     A*  CLE004 - Customer Lending Enhancements - Syndications        *
     A*  S01493 - Leo Midas Integration. Addition of LC reference.    *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
      *
     A          R HISACTF                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            BRCA      R
      * Begin /CSD027/
     A**********  HACNUM    R               REFFLD(CNUM)                                      CSD027
     A            HACNUM         6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            HAFCTY    R               REFFLD(FCLY)
     A            HADATE    R               REFFLD(DATE)
     A            HASEQ     R               REFFLD(ACSQ)
      **Start of change CLE148
     A**********  HALOAN    R               REFFLD(LNRF)                                      CLE148
     A            HALOAN         6A         TEXT('Loan number')
     A                                      COLHDG('Loan' 'Number')
      **End of change CLE148
     A            HAACTN    R               REFFLD(FAACTN)
     A            HAAAMT    R               REFFLD(FAMT)
     A            HAPART    R               REFFLD(FAPART)
      *                                              Begin /S01473/
     A            HALCRF    R               REFFLD(FCLN)
     A                                      TEXT('L/C Reference')
     A                                      COLHDG('L/C' 'Ref')
      *                                              End   /S01473/
      *                                              Begin /CLE004/
     A            GEDT      R               REFFLD(DATE)
     A                                      TEXT('Generation Date')
     A                                      COLHDG('Gen Date')
      *                                              End   /CLE004/
      *                                              Begin /CLE005/
      **Start of Change AR674226
     A**********  HASQNO    R               REFFLD(AMSQ)                                    AR674226
     A**********                            TEXT('Utilisation Sequenc+                      AR674226
     A**********                                  e No.')                                   AR674226
     A**********                            COLHDG('Util. Seq' 'No.')                       AR674226
     A            QQSQNO         3S 0
      **End of Change AR674226
      *                                              End   /CLE005/
      *                                              Begin /146936/
     A            HAECIN    R               REFFLD(SPIN)
     A                                      TEXT('Euro Conversion Ind')
     A                                      COLHDG('Euro Conv Ind')
      *                                              End   /146936/
      *                                              Begin /144712/
     A            HARCIN    R               REFFLD(RVCR)
      *                                              End   /144712/
      *                                              Begin /156585/
     A            HAGASS    R               REFFLD(FAPART)
     A                                      TEXT('Assignment Generate+
     A                                            d Ind.')
     A                                      COLHDG('Ass. Gen. Ind.')
      *                                              End   /156585/
     A**Start of Change CLE014
     A            HATRRC    R               REFFLD(RVCR)
     A                                      TEXT('Tranche Revolving +
     A                                      Credit Indicator')
     A                                      COLHDG('Tranche Revolving' +
     A                                      'Credit Indicator')
     A            HAOLRC    R               REFFLD(RVCR)
     A                                      TEXT('Old Revolving +
     A                                      Credit Indicator')
     A                                      COLHDG('Old Revolving' +
     A                                      'Credit Indicator')
     A**End of Change CLE014
      **Start of change AR674226
     A**Start of change 200289
     A**********  HATSEQ         3S 0       TEXT('Transaction Sequence +                    AR674226
     A**********                                 Number')                                   AR674226
     A**********                            COLHDG('Trans Seq No.')                         AR674226
     A            QQTSEQ         3S 0
     A**End of change 200289
      **End of change AR674226
     A**Start of change CLE023
     A            HALUCY         3A         TEXT('Loan/Mnfu/Tranche +
     A                                            Ccy')
     A                                      COLHDG('Loan/Mnfu/' +
     A                                      'Tranche Ccy')
     A            HALUAM        15P 0       TEXT('Amt of Actn in Ln/ +
     A                                            Mnfu/Trch Ccy')
     A                                      COLHDG('Amt of Act in' +
     A                                             'Ln/Mnfu/Trch Ccy')
     A            HAEXRT        13P 8       TEXT('Ln/Mnfu/Trch to +
     A                                            fclty xrate')
     A                                      COLHDG('Ln/Mnfu/Trch' +
     A                                             'to fclty xrate')
     A            HARTMD         1A         TEXT('Exchange rate mult +
     A                                            /div indicator')
     A                                      COLHDG('Xrate M/D' +
     A                                             'indicator')
     A            HAPTYP         2S 0       TEXT('Loan Processing +
     A                                            Type')
     A                                      COLHDG('Loan Proc' +
     A                                             'Type')
     A            HARCSI         1A         TEXT('Recourse Indicator')
     A                                      COLHDG('Recourse' +
     A                                             'Indicator')
     A            HALPFI         1A         TEXT('Linked to Participant +
     A                                             Fclty Ind.')
     A                                      COLHDG('Linked to Prtcpt' +
     A                                             'Facility Ind.')
     A**End of change CLE023
      **Start of change CLE025
     A            HADEAL         6S 0       TEXT('Dealing Deal Number')
     A                                      COLHDG('Dealing Deal Number')
     A            HAMCY          3A         TEXT('Margin Currency')
     A                                      COLHDG('Margin Currency')
     A            HAACBR         3A         TEXT('Retail Account Branch')
     A                                      COLHDG('Retail Account' +
     A                                                 'Branch')
      * Begin /CSD027/
     A**********  HAACCU         6S 0       TEXT('Retail Account Customer')                   CSD027
     A            HAACCU         6A         TEXT('Retail Account Customer')
      * End /CSD027/
     A                                      COLHDG('Retail Account' +
     A                                                'Customer')
     A            HAACCY         3A         TEXT('Retail Account Currency')
     A                                      COLHDG('Retail Account' +
     A                                                'Currency')
     A            HAACCD        10S 0       TEXT('Retail Account Code')
     A                                      COLHDG('Retail Account' +
     A                                                  'Code')
     A            HAACSQ         2S 0       TEXT('Retail Account Sequence')
     A                                      COLHDG('Retail Account' +
     A                                                'Sequence')
     A            HAACNO        10S 0       TEXT('Retail Account Number')
     A                                      COLHDG('Retail Account' +
     A                                                 'Number')
     A            HATTEO        15P 0       TEXT('Today''s Total Exposure/+
     A                                      Offset Posted in FCCY')
     A                                      COLHDG('Today''s Total' +
     A                                      'Exposure/Offset' 'Posted in +
     A                                      FCCY')
     A            HAPTEO        15P 0       TEXT('Previous Total Exposure/+
     A                                      Offset Posted in FCCY')
     A                                      COLHDG('Previous Total' +
     A                                      'Exposure/Offset' 'Posted in +
     A                                      FCCY')
     A            HAEXCY         3A         TEXT('Exposure Currency')
     A                                      COLHDG('Exposure Currency')
     A            HATVAL        15P 0       TEXT('Today''s Valuation +
     A                                      in Exposure Currency')
     A                                      COLHDG('Today''s' 'Valuation in' +
     A                                      'Exposure Currency')
     A            HAWEIG         2S 0       TEXT('Weighting Code')
     A                                      COLHDG('Weighting' 'Code')
     A            HAWCPC         3P 0       TEXT('Weighting Code''s Percentage')
     A                                      COLHDG('Weighting' 'Code''s' +
     A                                      'Percentage')
     A            HAINOF         1A         TEXT('Include Offset')
     A                                      COLHDG('Include Offset')
     A            HAFXMT         3A         TEXT('FX Margin Type')
     A                                      COLHDG('FX' 'Margin' 'Type')
     A            HAFXPC         3P 0       TEXT('FX Margin Percentage')
     A                                      COLHDG('FX' 'Margin' 'Percentage')
      **End of change CLE025
      **Start of change 225591
     A            HARIND         1A         TEXT('Rollover Action +
     A                                             Ind.')
     A                                      COLHDG('Roll' 'Act' +
     A                                             'Ind')
      **End of change 225591
      **Start of Change AR674226
     A            HASQNO         4S 0
     A                                      TEXT('Utilisation Sequenc+
     A                                            e No.')
     A                                      COLHDG('Util. Seq' 'No.')
     A            HATSEQ         4S 0       TEXT('Transaction Sequence +
     A                                           Number')
     A                                      COLHDG('Trans Seq No.')
      **End of Change AR674226
