      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Facility amendments returned msg format')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEA6MFPD - PC Subsystem Interface                         *
      *                Lending Download                               *
      *                Fee Settlements Transmitted Message Format     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CPK014             Date 20Jun01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 129467             Date 26Jan98               *
      *                 CLE005 *Create     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPK014 - Change field references to remove multi-layered     *
      *            dependencies; this is for compilation reasons.     *
      *  129467 - Sequence number returned as zero to Loan Manager    *
      *           as field is not defined in message format file.     *
      *  CLE005 - Customer Lending Enhancements - Tranche 3           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LEA6MFD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            A6MSGT    R               REFFLD(X1MSGT LEX1MFPD)
     A            A6TRUS    R               REFFLD(X1TRUS LEX1MFPD)
     A            A6TRWS    R               REFFLD(X1TRWS LEX1MFPD)
     A            A6TNRF    R               REFFLD(X1TNRF LEX1MFPD)
     A            A6TRSN    R               REFFLD(X1TRSN LEX1MFPD)
     A            A6ACTN    R               REFFLD(X1ACTN LEX1MFPD)
     A            A6TRDS    R               REFFLD(X1TRDS LEX1MFPD)
     A            A6TRTS    R               REFFLD(X1TRTS LEX1MFPD)
      *
     A            A6NRBA    R               REFFLD(X1BTNM LEX1MFPD)
     A                                      TEXT('Number of records in batch')
     A                                      COLHDG('No.' 'Records' +
     A                                             'in batch')
     A            A6FIL3    R               REFFLD(ZZ004)
      *
      * Start CPK014
     A**********  A6MSGS    R               REFFLD(F2MSGS LEF2MFPD)
     A**********  A6MSGI    R               REFFLD(F2MSGI LEF2MFPD)
     A**********  A6MSTX    R               REFFLD(F2MSTX LEF2MFPD)
     A            A6MSGS    R               REFFLD(MSG)
     A                                      TEXT('Message Severity')
     A                                      COLHDG('Msg' 'Severity')
     A            A6MSGI    R               REFFLD(MSID)
     A                                      TEXT('Message ID')
     A                                      COLHDG('Msg' 'ID')
     A            A6MSTX    R               REFFLD(ZZ078)
     A                                      TEXT('Message Text')
     A                                      COLHDG('Msg' 'Text')
      * End CPK014
      *
     A            A6CNUM    R               REFFLD(RINO)
     A                                      TEXT('Customer Number')
     A                                      COLHDG('Cust' 'No.')
     A            A6FACT    R               REFFLD(CCY)
     A                                      TEXT('Facility Type')
     A                                      COLHDG('Fclty' 'Type')
     A            A6FCNO    R               REFFLD(AMTP)
     A                                      TEXT('Facility Number')
     A                                      COLHDG('Fclty' 'No.')
     A            A6VDTC    R               REFFLD(ZZ008)
     A                                      TEXT('Value date this change')
     A                                      COLHDG('Value date of' +
     A                                             'this change')
      *                                              Begin /129467/
     A            A6FASQ    R               REFFLD(LASN0)
     A                                      TEXT('Amend Seq No.')
     A                                      COLHDG('Seq' 'No.')
      *                                              End   /129467/
      *
