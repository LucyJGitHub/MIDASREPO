      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Cust. Loans details defn file - All Flds.')   *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LECLIPPD - API Incoming Message Definition File for       *
      *                Customer Loans Transaction Details  - All Flds *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CLE134             Date 01Aug12               *
      *  Prev Amend No. CRE073             Date 06Dec10               *
      *                 CLE143             Date 06Dec10               *
      *                 AR655783           Date 12Oct10               *
      *                 CER059             Date 19Jul10               *
      *                 CER020             Date DDMmmYY               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG10391R          Date 17Feb06               *
      *                 BUG10671R          Date 07Mar06               *
      *                 BUG10574           Date 03Mar06               *
      *                 CLE042             Date 06Sep05               *
      *                 CLE106             Date 01Aug04               *
      *                 CLE035             Date 22Sep03               *
      *                 CLE034             Date 05May03               *
      *                 CLE031             Date 10Feb03               *
      *                 BUG3721 (CLE028)   Date 19Jul04               *
      *                 CAP074  *CREATE    Date 25Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CRE073 - Interest Rate Rounding                              *
      *  CLE143 - Original Loan Contract Date & Effective Interest    *
      *           Rate                                                *
      *  AR655783 - Database Issues                                   *
      *           - Add missing fields from LECLI1PD.                 *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus                                          *
      *  BUG10391R - Funding Rate and Funding Spread Rate fields      *
      *              should be shown separately when CLE036 is *ON    *
      *            - Also apply 225959; Missing CAS00x changes        *
      *  BUG10671R - Auto generated error on completion of agented    *
      *              loan.                                            *
      *  BUG10574 - Added the loan class field.                       *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CLE106 - Syndications Manager.                               *
      *  CLE035 - Nordea LE Enh. Phase 1 Pty 2 - Income Splits. Added *
      *           Base Rate and Fixed Rate fields.                    *
      *  CLE034 - Lending Enhancements Phase 1 Priority 1A. Added     *
      *           Settlement Allocation indicator field.              *
      *  CLE031 - Lending Enhancements. Additional fields.            *
      *  BUG3721 - Flat Rate Personal Loans (Rule of 78s) (CLE028)    *
      *  CAP074 - Lending API enhancements - Loans input.             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R LECLI1D0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
      ***  Screen 1
      *
     A            DDNARR        15A         TEXT('Narrative')
     A            DDACTN         1A         TEXT('Action Code')
     A            DDLSTS        15A         TEXT('Loan Status')
     A            DDLNRF         6A         TEXT('Loan number')
     A            DDCUST        10A         TEXT('Customer number')
     A            DDFCUS         6A         TEXT('Facility customer')
     A            DDFTYP         3A         TEXT('Facility type')
     A            DDFSEQ         2A         TEXT('Facility sequence number')
     A            DDBRCA         3A         TEXT('Branch Code - Alpha')
     A            DDLTYP         2A         TEXT('Loan type')
     A            DDSUTP         2A         TEXT('Loan sub-type')
     A            DDOLNO         6A         TEXT('Originating Loan No')
     A            DDPFRM        10A         TEXT('Purchased From')
     A            DDMLNR         6A         TEXT('Mirror Loan Number')
     A            DDCURR         3A         TEXT('Currency code')
     A            DDCPAM        14A         TEXT('Current principal amount')
     A            DDDDAT         6A         TEXT('Deal date')
     A            DDAUTO         1A         TEXT('Auto settlement indicator')
     A            DDVDAT         6A         TEXT('Value date')
     A            DDRCSI         1A         TEXT('Recourse ind.')
     A            DDMDAT         6A         TEXT('Maturity date')
     A            DDAPMI         1A         TEXT('Automatic Pay at Maturity')
     A            DDMCRA         1A         TEXT('Multi-ccy Rollover Allowed')
     A            DDRLDT         6A         TEXT('Rollover date')
     A            DDRLFQ         1A         TEXT('Rollover freq')
     A            DDRLDY         2A         TEXT('Rollover day')
     A            DDRFDY         2A         TEXT('Rollover Rate Fixing Days')
     A            DDDFTP         2A         TEXT('Default Part. Loan Type')
     A            DDDFST         2A         TEXT('Default Part. Loan Subtype')
     A            DDLNKS         1A         TEXT('Linked to Syndicate')
     A            DDPTFC         6A         TEXT('Participant Facility Cust.')
     A            DDPTFN         2A         TEXT('Participant Facility Number')
     A            DDPTFT         3A         TEXT('Participant Facility Type')
     A            DDASSN         1A         TEXT('Assignment/ Participant')
      **Start of change CLE031
     A            DDFCCY         3A         TEXT('Facility Currency Code')
     A            DDFPAM        14A         TEXT('Facility Principal Amount')
      **End of change CLE031
      **Start of change CLE034
     A            DDSTAL         1A         TEXT('Settlement Allocation Ind.')
      **End of change CLE034
      **Start of change CLE106
     A            DDSLTP         2A         TEXT('Default Sub-Part. Loan Type')
     A            DDSLST         2A         TEXT('Def Sub-Part. Loan Subtype')
      **End of change CLE106
     A*                                              Begin /CLE042/
     A            DDCLAS         4A         TEXT('Loan Class')
     A            DDDFCL         4A         TEXT('Default Part. Loan Class')
     A            DDSLCL         4A         TEXT('Default Sub-Part. Loan Class')
     A*                                              End   /CLE042/
      **Start of change BUG10671R
     A            DDLPFI         1A         TEXT('Linked to Part. Facility Ind')
      **End of change BUG10671R
      **Start of change AR655783
     A            DDPNTF         1A         TEXT('Principal Tranfer indicator')
     A            DDTOLN         6A         TEXT('Tranfer Originating Loan')
     A            DDFPTF         1A         TEXT('From Principal Tfr Trans')
      **End of change AR655783
      ** Start of change CER020
     A            DDISDT         6A         TEXT('Initial Start +
     A                                           Date')
     A                                      COLHDG('Initial Start ' +
     A                                             'Date')
      *
     A            DDFMDT         6A         TEXT('Final Maturity +
     A                                           Date')
     A                                      COLHDG('Final Maturity ' +
     A                                             'Date')
      ** End of change CER020
      *
      ***  Screen 2
      *
     A            DDLNR2         6A         TEXT('Loan number')
     A            DDACT2         1A         TEXT('Action Code')
     A            DDLST2        15A         TEXT('Loan Status Indicator')
     A            DDBRTT         2A         TEXT('Base rate code')
     A            DDCHIN         1A         TEXT('Change ind.')
     A            DDRTSP        12A         TEXT('Rate/spread')
     A            DDSPIN         1A         TEXT('Spread indicator')
     A            DDNIDT         6A         TEXT('Next interest payment date')
     A            DDIPFR         1A         TEXT('Interest payment frequency')
     A            DDIFDY         2A         TEXT('Int freq day in month')
     A            DDCALC         1A         TEXT('Calculation')
     A            DDIPRD        14A         TEXT('Int. paid/recd to date')
     A            DDMARG         8A         TEXT('Margin')
     A            DDNEGV         1A         TEXT('Negative')
     A            DDWTRT        12A         TEXT('Withholding tax rate')
     A            DDWTIN         1A         TEXT('Withholding tax indicator')
     A            DDINTC         1A         TEXT('Interest captilisation ind.')
     A            DDRDFC         1A         TEXT('Round Down Interest Ind.')
     A            DDADDI         1A         TEXT('Additional Day Indicator')
     A            DDREPT         1A         TEXT('Repayment type')
     A            DDRAMT        15A         TEXT('Rebate amount')
     A            DDFCRA        15A         TEXT('Facility Ccy Repayment Amt.')
     A            DDNRPD         6A         TEXT('Next repayment date')
     A            DDRFRQ         1A         TEXT('Repayment frequency')
     A            DDRDYN         2A         TEXT('Repayment day no.')
     A            DDBTIN         1A         TEXT('Billing/telex indicator')
     A            DDBLDY         2A         TEXT('Billing days')
     A            DDPDDI         1A         TEXT('Payment Due Day Indicator')
     A            DDGDIN         2A         TEXT('Grace Days for Interest')
     A            DDGDPR         2A         TEXT('Grace Days for Principal')
     A            DDPTDI         1A         TEXT('Payment Taken Day Indicator')
     A**Start of change BUG3721
     A            DDADIF         1A         TEXT('Add on Interest Flag')
     A            DDADCF         1A         TEXT('Add on Charges Flag')
     A            DDICIF         1A         TEXT('Include Charges in Interest +
     A                                           Flag')
     A            DDCHGA        14A         TEXT('Charge Amount')
     A**End of change BUG3721
      **Start of change CLE035
     A            DDBASR        12A         TEXT('Base Rate')
     A            DDFXRT        12A         TEXT('Fixed Rate')
      **End of change CLE035
      *
      ***  Screen 3
      *
     A            DDLNR3         6A         TEXT('Loan number')
     A            DDACT3         1A         TEXT('Action Code')
     A            DDLST3        15A         TEXT('Loan Status Indicator')
     A            DDACOF         2A         TEXT('Account officer')
     A            DDLIND         3A         TEXT('Local industry code')
     A            DDFEEI         1A         TEXT('Fees ind.')
     A            DDORBR         3A         TEXT('Originating branch')
     A            DDCRSK         2A         TEXT('Country of risk')
     A            DDDOCI         1A         TEXT('Documents ind.')
     A            DDBOKC         2A         TEXT('Book code')
     A            DDFRBC         5A         TEXT('Frb code')
     A            DDSECI         1A         TEXT('Secured ind.')
     A            DDACSQ         2A         TEXT('Account sequence number')
     A            DDPTFR         4A         TEXT('Portfolio reference')
     A            DDNACD         1A         TEXT('Name and address code')
     A            DDPRFC         4A         TEXT('Profit centre')
     A            DDNAR1        35A         TEXT('Narrative 1')
     A            DDBCXR        14A         TEXT('Base currency exchange rate')
     A            DDBMIN         1A         TEXT('Base currency indicator')
     A            DDNAR2        35A         TEXT('Narrative 2')
     A            DDNAR3        35A         TEXT('Narrative 3')
     A            DDFCXR        14A         TEXT('Facility ccy exch. rate')
     A            DDFMIN         1A         TEXT('FCLTY CURRENCY INDICATOR')
     A            DDNAR4        35A         TEXT('Narrative 4')
     A            DDRECE        10A         TEXT('Receiver of the Guarantee')
     A            DDCOCU         6A         TEXT('Customer Confirmation')
     A            DDFRSP        12A         TEXT('Spread/Rate')
     A            DDOMDT         6A         TEXT('Original Maturity Date')
     A            DDFSGN         1A         TEXT('Funding Sign')
     A            DDFPRC         4A         TEXT('Funding Profit Centre')
      ** Start of change BUG10391R
     A            DDYLDC         5A         TEXT('Yield Curve')
     A            DDFYLD         5A         TEXT('Forward Yield Curve')
     A            DDFUND        12A         TEXT('Funding Rate')
      ** End of change BUG10391R
      *
      ***  Screen 4
      *
     A            DDLNR4         6A         TEXT('Loan number')
     A            DDACT4         1A         TEXT('Action Code')
     A            DDLST4        15A         TEXT('Loan Status Indicator')
     A            DDSPI1        30A         TEXT('Special instructions 1')
     A            DDSPI2        30A         TEXT('Special instructions 2')
     A            DDSPI3        30A         TEXT('Special instructions 3')
      **Start of change BUG10574
     A            DDLNCS         2A         TEXT('Loan Class')
      **End of change BUG10574
      **Start of change CLE143
     A            DDOCDT         6A         TEXT('Original Contract Date')
     A            DDEAIR        16A         TEXT('Effective Annual Interest +
     A                                           Rate')
      **End of change CLE143
      **Start of change CRE073
     A            DDCNSP        12A         TEXT('Contractual Spread')
     A            DDCNSG         1A         TEXT('Contractual Spread Sign')
     A            DDRDMT         7A         TEXT('Rounding Method')
     A            DDRDFD         4A         TEXT('Rounding Fraction/Decimal')
      **End of change CRE073
      **Start of change CLE134
     A            DDFAMU         1A         COLHDG('Facility' 'Amount' +
     A                                             'Utilisation')
      *
      ** Reporting Past Due Details...
      *
     A            DDRIDT         6A         COLHDG('Reporting Past Due' +
     A                                             'Initial Date')
     A            DDRCAT         1A         COLHDG('Reporting Past Due' +
     A                                             'Classification')
      *
      ** Basel II Past Due Details...
      *
     A            DDBIDT         6A         COLHDG('Basel II Past Due' +
     A                                             'Initial Date')
     A            DDBCAT         1A         COLHDG('Basel II Past Due'  +
     A                                             'Classification')
     A            DDORLN         6A         COLHDG('Origin' +
     A                                             'Loan Number')
     A            DDOFCU         6A         COLHDG('Origin' +
     A                                             'Facility')
     A            DDOFAC         5A         COLHDG('Origin' +
     A                                             'Facility Sequence')
     A            DDOFES         6A         COLHDG('Origin' +
     A                                             'Fee Code')
     A            DDOFSQ         2A         COLHDG('Origin' +
     A                                             'Fee Sequence')
     A            DDOFLN         6A         COLHDG('Origin' +
     A                                             'Fee Loan')
      **End of change CLE134
