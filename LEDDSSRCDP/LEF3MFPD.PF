      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Fees transmitted message format')             *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEF3MFPD - PC Subsystem Interface                         *
      *                Lending Download                               *
      *                Fees Transmitted Message Format                *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. 262992             Date 01Aug12               *
      *                 CLE134             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      *                 CAP078             Date 21Aug03               *
      *                 CLE034             Date 05May03               *
      *                 CLE031             Date 03Feb03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 157286 (BT0888)    Date 17Mar99               *
      *                 133237             Date  2Mar99               *
      *                 CEU004             Date 02Feb98               *
      *                 CLE005 *Create     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  262992 - P2: CLE134 API Invalid length of RIDT and BIDT      *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP078 - Conversion of MN inputs into modular structure to   *
      *           use as APIs. Fee input Transaction Details          *
      *  CLE034 - Lending Enhancements Phase 1 Priority 1A            *
      *  CLE031 - Lending Enhancements.                               *
      *  157286 - Add fee branch                                      *
      *  133237 - Add profit centre field                             *
      *  CEU004 - LE Settlement Ccy Conversion Upgrade to DBA R2.0    *
      *  CLE005 - Customer Lending Enhancements - Tranche 3           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LEF3MFD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            F3MSGT    R               REFFLD(X1MSGT LEX1MFPD)
     A            F3TRUS    R               REFFLD(X1TRUS LEX1MFPD)
     A            F3TRWS    R               REFFLD(X1TRWS LEX1MFPD)
     A            F3TNRF    R               REFFLD(X1TNRF LEX1MFPD)
     A            F3TRSN    R               REFFLD(X1TRSN LEX1MFPD)
     A            F3ACTN    R               REFFLD(X1ACTN LEX1MFPD)
     A            F3TRDS    R               REFFLD(X1TRDS LEX1MFPD)
     A            F3TRTS    R               REFFLD(X1TRTS LEX1MFPD)
      *
     A            F3NRBA    R               REFFLD(X1BTNM LEX1MFPD)
     A                                      TEXT('Number of records in batch')
     A                                      COLHDG('No.' 'Records' +
     A                                             'in batch')
     A            F3LINK    R               REFFLD(##KEY)
     A                                      TEXT('Linked Transaction Action')
     A                                      COLHDG('Linked' 'Trans' 'Action')
     A            F3FIL3    R               REFFLD(ZZ003)
      *
     A            F3CNUM    R               REFFLD(RINO)
     A                                      TEXT('Customer Number')
     A                                      COLHDG('Cust' 'No.')
     A            F3FACT    R               REFFLD(CCY)
     A                                      TEXT('Facility Type')
     A                                      COLHDG('Fclty' 'Type')
     A            F3FCNO    R               REFFLD(AMTP)
     A                                      TEXT('Facility Number')
     A                                      COLHDG('Fclty' 'No.')
      **Start of change CLE134
     A**********  F3LOAN    R               REFFLD(RINO)                                      CLE134
     A            F3LOAN         6A
      **End of change CLE134
     A                                      TEXT('Loan Number')
     A                                      COLHDG('Loan' 'No.')
     A            F3SEQR    R               REFFLD(AMTP)
     A                                      TEXT('Fee Sequence Number')
     A                                      COLHDG('Fee' 'Seq.' 'No.')
     A            F3FCOD    R               REFFLD(AMTP)
     A                                      TEXT('Fee Code')
     A                                      COLHDG('Fee' 'Code')
     A            F3FCCY    R               REFFLD(CCY1)
     A            F3FAMT    R               REFFLD(BCNM)
     A                                      TEXT('Flat Fees Amount')
     A                                      COLHDG('Flat' 'Fees' 'Amount')
     A            F3FAMI    R               REFFLD(ACCL)
     A                                      TEXT('Flat Fees Amount Sign Ind')
     A                                      COLHDG('Flat' 'Fees' 'Sign')
     A            F3PSTD    R               REFFLD(DPGM)
     A                                      TEXT('Fee Period Start +
     A                                           Date')
     A                                      COLHDG('Fee Period' +
     A                                             'Start' 'Date')
     A            F3PEND    R               REFFLD(DPGM)
     A                                      TEXT('Fee Period End +
     A                                           Date')
     A                                      COLHDG('Fee Period' +
     A                                             'End' 'Date')
     A            F3PYON    R               REFFLD(FEPYON)
     A                                      TEXT('Payment On Ind+
     A                                           icator')
     A            F3AUTO    R               REFFLD(AUTO)
     A            F3CALT    R               REFFLD(FECALT)
     A            F3FRT1    R               REFFLD(DPGM)
     A                                      TEXT('Fee Rate - Tier 1')
     A                                      COLHDG('Fee Rate' +
     A                                             'Tier 1')
     A            F3FRT2    R               REFFLD(DPGM)
     A                                      TEXT('Fee Rate - Tier 2')
     A                                      COLHDG('Fee Rate' +
     A                                             'Tier 2')
     A            F3FRT3    R               REFFLD(DPGM)
     A                                      TEXT('Fee Rate - Tier 3')
     A                                      COLHDG('Fee Rate' +
     A                                             'Tier 3')
     A            F3FRT4    R               REFFLD(DPGM)
     A                                      TEXT('Fee Rate - Tier 4')
     A                                      COLHDG('Fee Rate' +
     A                                             'Tier 4')
     A            F3FRT5    R               REFFLD(DPGM)
     A                                      TEXT('Fee Rate - Tier 5')
     A                                      COLHDG('Fee Rate' +
     A                                             'Tier 5')
     A            F3AMT1    R               REFFLD(CCTN)
     A                                      TEXT('Fee Amount - Tier 1')
     A                                      COLHDG('Fee Amount' +
     A                                             'Tier 1')
     A            F3AMT2    R               REFFLD(CCTN)
     A                                      TEXT('Fee Amount - Tier 2')
     A                                      COLHDG('Fee Amount' +
     A                                             'Tier 2')
     A            F3AMT3    R               REFFLD(CCTN)
     A                                      TEXT('Fee Amount - Tier 3')
     A                                      COLHDG('Fee Amount' +
     A                                             'Tier 3')
     A            F3AMT4    R               REFFLD(CCTN)
     A                                      TEXT('Fee Amount - Tier 4')
     A                                      COLHDG('Fee Amount' +
     A                                             'Tier 4')
     A            F3AMT5    R               REFFLD(CCTN)
     A                                      TEXT('Fee Amount - Tier 5')
     A                                      COLHDG('Fee Amount' +
     A                                             'Tier 5')
     A            F3PIND    R               REFFLD(ACCL)
     A                                      TEXT('Fee Percent/Amount +
     A                                           Indicator')
     A                                      COLHDG('Fee' 'Prcnt/Amnt'+
     A                                             'Indicator')
     A            F3CALC    R               REFFLD(ACCL)
     A                                      TEXT('Fee Calculation +
     A                                           Basis')
     A                                      COLHDG('Fee' 'Calc' +
     A                                             'Basis')
     A            F3FREQ    R               REFFLD(FREQ)
     A                                      TEXT('Fee Payment Freq+
     A                                           uency')
     A                                      COLHDG('Fee' 'Pay' 'Freq')
     A            F3DYIM    R               REFFLD(AMTP)
     A                                      TEXT('Fee Frequency Day +
     A                                           Number')
     A                                      COLHDG('Freq.' 'Day' 'No.')
     A            F3NPYD    R               REFFLD(DPGM)
     A                                      TEXT('Next Fee Payment +
     A                                           Date')
     A                                      COLHDG('Next Fee' +
     A                                             'Pay Date')
     A            F3ERAI    R               REFFLD(ACCL)
     A                                      TEXT('Facility Exchange Rate Ind')
     A                                      COLHDG('Exchange' 'Rate' 'Ind')
     A            F3ERAT    R               REFFLD(BCNM)
     A                                      TEXT('Facility Currency +
     A                                           Exch Rate')
     A                                      COLHDG('Fclty' 'Ccy Exch' +
     A                                             'Rate')
     A            F3PSTM    R               REFFLD(AMTP)
     A                                      TEXT('Pay - Settlement +
     A                                           Type')
     A                                      COLHDG('Pay' 'Set.' 'Typ.')
     A**Start of change CGL029
     A**********  F3PONS    R               REFFLD(ACRF)                                      CGL029
     A**********                            TEXT('Pay - Our Nostro')                          CGL029
     A**********                            COLHDG('Pay' 'Our' +                              CGL029
     A**********                                   'Nostro')                                  CGL029
     A            QQPONS        15A
     A**End of change CGL029
     A            F3RVNO    R               REFFLD(RVNO)
     A                                      TEXT('Pay - Recipient of +
     A                                           Pay Inst')
     A            F3CVMR    R               REFFLD(CVMR)
     A                                      TEXT('Pay - Cover MT202 +
     A                                           Req Ind')
     A                                      COLHDG('Pay' 'Cover' +
     A                                             'Req Ind')
     A            F3POCN    R               REFFLD(RINO)
     A                                      TEXT('Pay - Ordering +
     A                                           Customer')
     A            F3POBN    R               REFFLD(RINO)
     A                                      TEXT('Pay - Ordering +
     A                                           Bank')
     A                                      COLHDG('Pay' 'Ordering' +
     A                                             'Bank')
     A            F3RCRN    R               REFFLD(RCRN)
     A                                      TEXT('Pay - Recipients Corres')
     A                                      COLHDG('Pay' 'Recip' 'Corres')
     A            F3RCRA    R               REFFLD(RCRA)
     A                                      TEXT('Pay - Recipients Corres +
     A                                            Account')
     A                                      COLHDG('Pay' 'Recip' 'Accnt')
     A            F3PIBN    R               REFFLD(PIBN)
     A            F3PIBA    R               REFFLD(PIBA)
     A            F3AWBN    R               REFFLD(AWBN)
     A                                      TEXT('Pay - a/c with bank +
     A                                           no.')
     A                                      COLHDG('Pay' 'a/c w/' +
     A                                             'bank no')
     A            F3AWBA    R               REFFLD(AWBA)
     A                                      TEXT('Pay - a/c with bank +
     A                                           a/c line')
     A                                      COLHDG('Pay' 'a/c w/' +
     A                                             'bank a/c')
     A            F3BENN    R               REFFLD(BENN)
     A                                      TEXT('Pay - beneficiary +
     A                                           no.')
     A                                      COLHDG('Pay' 'benef' +
     A                                             'no')
     A            F3BENA    R               REFFLD(BENA)
     A                                      TEXT('Pay - beneficiary +
     A                                           a/c line')
     A                                      COLHDG('Pay' ' benef' +
     A                                             'a/c line')
     A            F3RSTM    R               REFFLD(AMTP)
     A                                      TEXT('Receipt - Settle+
     A                                           ment Type')
     A                                      COLHDG('Rcp' 'Set.' 'Typ.')
     A**Start of change CGL029
     A**********  F3RONS    R               REFFLD(ACRF)                                      CGL029
     A**********                            TEXT('Receipt - Our +                             CGL029
     A**********                                 Nostro')                                     CGL029
     A**********                            COLHDG('Rcp' 'Our' 'Nost')                        CGL029
     A            QQRONS        15A
     A**End of change CGL029
     A            F3ROCN    R               REFFLD(RINO)
     A                                      TEXT('Receipt - Ordering +
     A                                           Customer')
     A                                      COLHDG('Rcp' 'Ordering' +
     A                                             'Customer')
     A            F3ROBN    R               REFFLD(RINO)
     A                                      TEXT('Receipt - Ordering +
     A                                           Bank')
     A                                      COLHDG('Rcp' 'Ordering' +
     A                                             'Bank')
     A            F3RIBN    R               REFFLD(RIBN)
     A            F3RIBA    R               REFFLD(RIBA)
     A            F3DTPN    R               REFFLD(ZZ140)
     A                                      TEXT('Details of Payments +
     A                                           1-4')
     A                                      COLHDG('Detls' ' of Pay' +
     A                                             '1-4')
     A            F3DCHG    R               REFFLD(DCHG)
     A            F3BTBN    R               REFFLD(ZZ210)
     A                                      TEXT('Bank to Bank Info +
     A                                           1-6')
     A                                      COLHDG('Bank to' 'Bank' +
     A                                             'Info 1-6')
     A            F3SHFI    R               REFFLD(SPIN)
     A                                      TEXT('Shared Fee Indicator')
     A                                      COLHDG('Shr Fee' +
     A                                             'Ind')
     A            F3GFPT    R               REFFLD(SPIN)
     A                                      TEXT('Generated by Fund +
     A                                           Participant')
     A                                      COLHDG('Gen by' +
     A                                             'Fund Part')
     A            F3PTAI    R               REFFLD(SPIN)
     A                                      TEXT('Participant/Assign+
     A                                            ment Ind')
     A                                      COLHDG('Part/' +
     A                                             'Assg?')
     A            F3SMAC    R               REFFLD(RINO)
     A                                      TEXT('Synd Member or +
     A                                           Assign Cust No.')
     A                                      COLHDG('Syn Mem/' 'Assign'+
     A                                             'Cust No.')
     A            F3IUSR    R               REFFLD(RSTU)
     A                                      TEXT('User Profile - +
     A                                           Insert')
     A                                      COLHDG('Insert' 'User')
     A            F3AUSR    R               REFFLD(RSTU)
     A                                      TEXT('User Profile - +
     A                                           Amend')
     A                                      COLHDG('Amend' 'User')
     A            F3XUSR    R               REFFLD(RSTU)
     A                                      TEXT('User Profile - +
     A                                           Authorise')
     A                                      COLHDG('Auth.' 'User')
     A            F3PCOB    R               REFFLD(ORBR)
     A                                      TEXT('PC Originating Branch')
     A                                      COLHDG('Branch')
      *                                              Begin /CEU004/
     A            F3SCCY    R               REFFLD(CCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
     A            F3ICCY    R               REFFLD(CCY)
     A                                      TEXT('IN Currency in +
     A                                            Fld 72')
     A                                      COLHDG('IN Ccy')
      *                                              End   /CEU004/
     A*                                      Begin /133237/
     A            F3PRFC    R               REFFLD(PRFC)
     A*                                      End   /133237/
     A*                                      Begin /157286/
     A            F3BRCA    R               REFFLD(BRCA)
     A*                                      End   /157286/
      **Start of change CLE031
     A            F3REXR        14A         TEXT('Receive Settlement  +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange  Rate')
      *
     A            F3REXI         1A         TEXT('Receive Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
      *
     A            F3PSCY         3A         TEXT('Pay Settlement Currency')
     A                                      COLHDG('Pay Settlement' 'Currency')
      *
     A            F3PEXR        14A         TEXT('Pay Settlement Currency +
     A                                           Exchange Rate')
     A                                      COLHDG('Pay Settlement' +
     A                                           'Currency' 'Exchange Rate')
      *
     A            F3PEXI         1A         TEXT('Pay Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Pay Settlement' 'Currency'+
     A                                           'Exchange' 'Rate Ind')
      **End of change CLE031
      **Start of Change CLE034
     A            F3STAL         1A
     A                                      TEXT('Settlement Allocation +
     A                                           Indicator')
     A                                      COLHDG('Settlement' +
     A                                           'Allocation Indicator')
      **End of Change CLE034
      **Start of Change CAP078
     A            F3RPLC    R               REFFLD(SYLL)
     A                                      TEXT('Repair Location')
     A                                      COLHDG('Repair' 'Location')
      **End of Change CAP078
     A**Start of change CGL029
     A            F3PONS        21A         TEXT('Pay - Our Nostro')
     A                                      COLHDG('Pay' 'Our' +
     A                                             'Nostro')
     A            F3RONS        21A         TEXT('Receipt - Our +
     A                                           Nostro')
     A                                      COLHDG('Rcp' 'Our' 'Nost')
     A**End of change CGL029
      **Start of change CLE134
      *
     A            F3AUT2         1A         COLHDG('Auto-settlement' 'Method')
     A            F3PLNR         6A         COLHDG('Past Due' 'Call Loan')
     A            F3FAMU         1A         COLHDG('Facility' 'Amount' +
     A                                             'Utilisation')
      *
      **Start of change 262992
     A**********  F3RIDT         6A         COLHDG('Reporting' 'Past Due' +                   262992
     A            F3RIDT         8A         COLHDG('Reporting' 'Past Due' +
      **End of change 262992
     A                                             'Initial Date')
     A            F3RCAT         1A         COLHDG('Reporting' 'Past Due' +
     A                                              'Classification')
      *
      **Start of change 262992
     A**********  F3BIDT         6A         COLHDG('Basel II' 'Past Due'  +                   262992
     A            F3BIDT         8A         COLHDG('Basel II' 'Past Due'  +
      **End of change 262992
     A                                             'Initial Date')
     A            F3BCAT         1A         COLHDG('Basel II' 'Past Due'  +
     A                                              'Classification')
      *
     A            F3FSFA         1A         COLHDG('Suspend Fee' 'Allowed')
     A            F3SFAM        13A         COLHDG('Suspended' 'Fee Amount' +
     A                                             'Maximum')
     A            F3SFPM         3A         COLHDG('Suspended' 'Fee Payments' +
     A                                             'Maximum')
      *
     A            F3FPPA         1A         COLHDG('Partial' 'Payment' +
     A                                             'Allowed')
     A            F3PSIN         1A         COLHDG('Partial' 'Indicator')
     A            F3PSLM        13A         COLHDG('Partial' 'Limit')
     A            F3FEMC         1A         COLHDG('Early' 'Maturity' +
     A                                             'Condition')
      *
      **End of change CLE134
