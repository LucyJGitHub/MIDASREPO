      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Fees Returned Message Format')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEF4MFPD - PC Subsystem Interface                         *
      *                Lending Download                               *
      *                Fees Returned Message Format                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CPK014             Date 20Jun01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CLE005 *Create     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPK014 - Change field references to remove multi-layered     *
      *            dependencies; this is for compilation reasons.     *
      *  CLE005 - Customer Lending Enhancements - Tranche 3           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LEF4MFD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            F4MSGT    R               REFFLD(X1MSGT LEX1MFPD)
     A            F4TRUS    R               REFFLD(X1TRUS LEX1MFPD)
     A            F4TRWS    R               REFFLD(X1TRWS LEX1MFPD)
     A            F4TNRF    R               REFFLD(X1TNRF LEX1MFPD)
     A            F4TRSN    R               REFFLD(X1TRSN LEX1MFPD)
     A            F4ACTN    R               REFFLD(X1ACTN LEX1MFPD)
     A            F4TRDS    R               REFFLD(X1TRDS LEX1MFPD)
     A            F4TRTS    R               REFFLD(X1TRTS LEX1MFPD)
      *
     A            F4NRBA    R               REFFLD(X1BTNM LEX1MFPD)
     A                                      TEXT('Number of records in batch')
     A                                      COLHDG('No.' 'Records' +
     A                                             'in batch')
     A            F4LINK    R               REFFLD(##KEY)
     A                                      TEXT('Linked Transaction Action')
     A                                      COLHDG('Linked' 'Trans' 'Action')
     A            F4FIL3    R               REFFLD(ZZ003)
      *
      * Start CPK014
     A**********  F4MSGS    R               REFFLD(F2MSGS LEF2MFPD)
     A**********  F4MSGI    R               REFFLD(F2MSGI LEF2MFPD)
     A**********  F4MSTX    R               REFFLD(F2MSTX LEF2MFPD)
     A            F4MSGS    R               REFFLD(MSG)
     A                                      TEXT('Message Severity')
     A                                      COLHDG('Msg' 'Severity')
     A            F4MSGI    R               REFFLD(MSID)
     A                                      TEXT('Message ID')
     A                                      COLHDG('Msg' 'ID')
     A            F4MSTX    R               REFFLD(ZZ078)
     A                                      TEXT('Message Text')
     A                                      COLHDG('Msg' 'Text')
      * End CPK014
     A            F4CNUM    R               REFFLD(RINO)
     A                                      TEXT('Customer Number')
     A                                      COLHDG('Cust' 'No.')
     A            F4FACT    R               REFFLD(CCY)
     A                                      TEXT('Facility Type')
     A                                      COLHDG('Fclty' 'Type')
     A            F4FCNO    R               REFFLD(AMTP)
     A                                      TEXT('Facility Number')
     A                                      COLHDG('Fclty' 'No.')
     A            F4LOAN    R               REFFLD(RINO)
     A                                      TEXT('Loan Number')
     A                                      COLHDG('Loan' 'No.')
     A            F4SEQR    R               REFFLD(AMTP)
     A                                      TEXT('Fee Sequence Number')
     A                                      COLHDG('Fee' 'Seq.' 'No.')
     A            F4PTAI    R               REFFLD(SPIN)
     A                                      TEXT('Participant/Assign+
     A                                            ment Ind')
     A                                      COLHDG('Part/' +
     A                                             'Assg?')
     A            F4SMAC    R               REFFLD(RINO)
     A                                      TEXT('Synd Member or +
     A                                           Assign Cust No.')
     A                                      COLHDG('Syn Mem/' 'Assign'+
     A                                             'Cust No.')
