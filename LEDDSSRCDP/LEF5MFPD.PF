      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Fees Settlements Transmitted Msg Format')     *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEF5MFPD - PC Subsystem Interface                         *
      *                Lending Download                               *
      *                Fee Settlements Transmitted Message Format     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CLE073             Date 12May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CLE034             Date 05May03               *
      *                 CLE031             Date 03Feb03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU004             Date 02Feb98               *
      *                 CLE005 *Create     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE073 - Calculated Loan Based Fees                          *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE034 - Lending Enhancements Phase 1 Priority 1A            *
      *  CLE031 - Lending Enhancements.                               *
      *  CEU004 - LE Settlement Ccy Conversion Upgrade to DBA R2.0    *
      *  CLE005 - Customer Lending Enhancements - Tranche 3           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LEF5MFD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            F5MSGT    R               REFFLD(X1MSGT LEX1MFPD)
     A            F5TRUS    R               REFFLD(X1TRUS LEX1MFPD)
     A            F5TRWS    R               REFFLD(X1TRWS LEX1MFPD)
     A            F5TNRF    R               REFFLD(X1TNRF LEX1MFPD)
     A            F5TRSN    R               REFFLD(X1TRSN LEX1MFPD)
     A            F5ACTN    R               REFFLD(X1ACTN LEX1MFPD)
     A            F5TRDS    R               REFFLD(X1TRDS LEX1MFPD)
     A            F5TRTS    R               REFFLD(X1TRTS LEX1MFPD)
      *
     A            F5NRBA    R               REFFLD(X1BTNM LEX1MFPD)
     A                                      TEXT('Number of records in batch')
     A                                      COLHDG('No.' 'Records' +
     A                                             'in batch')
     A            F5FIL3    R               REFFLD(ZZ004)
      *
     A            F5CNUM    R               REFFLD(RINO)
     A                                      TEXT('Customer Number')
     A                                      COLHDG('Cust' 'No.')
     A            F5FACT    R               REFFLD(CCY)
     A                                      TEXT('Facility Type')
     A                                      COLHDG('Fclty' 'Type')
     A            F5FCNO    R               REFFLD(AMTP)
     A                                      TEXT('Facility Number')
     A                                      COLHDG('Fclty' 'No.')
     A            F5SEQR    R               REFFLD(AMTP)
     A                                      TEXT('Fee Sequence Number')
     A                                      COLHDG('Fee' 'Seq.' 'No.')
     A            F5VDTC    R               REFFLD(ZZ008)
     A                                      TEXT('Value date this change')
     A                                      COLHDG('Value date of' +
     A                                             'this change')
     A            F5SADJ    R               REFFLD(BCNM)
     A                                      TEXT('Fee Settlement +
     A                                           Amount')
     A                                      COLHDG('Fee' 'Settlement' +
     A                                             'Amount')
     A            F5SIGN    R               REFFLD(ACCL)
     A                                      TEXT('Fee Settlement +
     A                                           Amount Sign')
     A                                      COLHDG('Settlement' +
     A                                             'Amount' 'Sign')
     A            F5TYPE    R               REFFLD(ACCL)
     A                                      TEXT('Writ-Off/Full/Part +
     A                                           Pay Ind')
     A                                      COLHDG('Full/Part' 'Pay' +
     A                                             'Ind')
     A            F5PSTM    R               REFFLD(AMTP)
     A                                      TEXT('Pay - Settlement +
     A                                           Type')
     A                                      COLHDG('Pay' 'Set.' 'Typ.')
     A**Start of change CGL029
     A**********  F5PONS    R               REFFLD(ACRF)                                      CGL029
     A**********                            TEXT('Pay - Our Nostro')                          CGL029
     A**********                            COLHDG('Pay' 'Our' +                              CGL029
     A**********                                   'Nostro')                                  CGL029
     A            QQPONS        15A
     A**End of change CGL029
     A            F5RVNO    R               REFFLD(RVNO)
     A                                      TEXT('Pay - Recepient of +
     A                                           Pay Inst')
     A                                      COLHDG('Pay' 'Ordering' +
     A                                             'Customer')
     A            F5CVMR    R               REFFLD(CVMR)
     A                                      TEXT('Pay - Cover MT202 +
     A                                           Req Ind')
     A                                      COLHDG('Pay' 'Cover' +
     A                                             'Req Ind')
     A            F5POCN    R               REFFLD(RINO)
     A                                      TEXT('Pay - Ordering +
     A                                           Customer')
     A                                      COLHDG('Pay' 'Ordering' +
     A                                             'Customer')
     A            F5POBN    R               REFFLD(RINO)
     A                                      TEXT('Pay - Ordering +
     A                                           Bank')
     A                                      COLHDG('Pay' 'Ordering' +
     A                                             'Bank')
     A            F5RCRN    R               REFFLD(RCRN)
     A            F5RCRA    R               REFFLD(RCRA)
     A            F5PIBN    R               REFFLD(PIBN)
     A            F5PIBA    R               REFFLD(PIBA)
     A            F5AWBN    R               REFFLD(AWBN)
     A                                      TEXT('Pay - a/c with bank +
     A                                           no.')
     A                                      COLHDG('Pay' 'a/c w/' +
     A                                             'bank no')
     A            F5AWBA    R               REFFLD(AWBA)
     A                                      TEXT('Pay - a/c with bank +
     A                                           a/c line')
     A                                      COLHDG('Pay' 'a/c w/' +
     A                                             'bank a/c')
     A            F5BENN    R               REFFLD(BENN)
     A                                      TEXT('Pay - beneficiary +
     A                                           no.')
     A                                      COLHDG('Pay' 'benef' +
     A                                             'no')
     A            F5BENA    R               REFFLD(BENA)
     A                                      TEXT('Pay - beneficiary +
     A                                           a/c line')
     A                                      COLHDG('Pay' ' benef' +
     A                                             'a/c line')
     A            F5RSTM    R               REFFLD(AMTP)
     A                                      TEXT('Receipt - Settle+
     A                                           ment Type')
     A                                      COLHDG('Rcp' 'Set.' 'Typ.')
     A**Start of change CGL029
     A**********  F5RONS    R               REFFLD(ACRF)                                      CGL029
     A**********                            TEXT('Receipt - Our +                             CGL029
     A**********                                 Nostro')                                     CGL029
     A**********                            COLHDG('Rcp' 'Our' 'Nost')                        CGL029
     A            QQRONS        15A
     A**End of change CGL029
     A            F5ROCN    R               REFFLD(RINO)
     A                                      TEXT('Receipt - Ordering +
     A                                           Customer')
     A                                      COLHDG('Rcp' 'Ordering' +
     A                                             'Customer')
     A            F5ROBN    R               REFFLD(RINO)
     A                                      TEXT('Receipt - Ordering +
     A                                           Bank')
     A                                      COLHDG('Rcp' 'Ordering' +
     A                                             'Bank')
     A            F5RIBN    R               REFFLD(RIBN)
     A            F5RIBA    R               REFFLD(RIBA)
     A            F5DTPN    R               REFFLD(ZZ140)
     A                                      TEXT('Details of Payments +
     A                                           1-4')
     A                                      COLHDG('Detls' ' of Pay' +
     A                                             '1-4')
     A            F5DCHG    R               REFFLD(DCHG)
     A            F5BTBN    R               REFFLD(ZZ210)
     A                                      TEXT('Bank to Bank Info +
     A                                           1-6')
     A                                      COLHDG('Bank to' 'Bank' +
     A                                             'Info 1-6')
     A            F5IUSR    R               REFFLD(RSTU)
     A                                      TEXT('User Profile - +
     A                                           Insert')
     A                                      COLHDG('Insert' 'User')
     A            F5AUSR    R               REFFLD(RSTU)
     A                                      TEXT('User Profile - +
     A                                           Amend')
     A                                      COLHDG('Amend' 'User')
     A            F5XUSR    R               REFFLD(RSTU)
     A                                      TEXT('User Profile - +
     A                                           Authorise')
     A                                      COLHDG('Auth.' 'User')
     A            F5PCOB    R               REFFLD(ORBR)
     A                                      TEXT('PC Originating Branch')
     A                                      COLHDG('Branch')
      *                                              Begin /CEU004/
     A            F5SCCY    R               REFFLD(CCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
     A            F5ICCY    R               REFFLD(CCY)
     A                                      TEXT('IN Currency in +
     A                                            Fld 72')
     A                                      COLHDG('IN Ccy')
      *                                              End   /CEU004/
      **Start of change CLE031
     A            F5REXR        14A         TEXT('Receive Settlement  +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange  Rate')
      *
     A            F5REXI         1A         TEXT('Receive Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
      *
     A            F5PSCY         3A         TEXT('Pay Settlement Currency')
     A                                      COLHDG('Pay Settlement' 'Currency')
      *
     A            F5PEXR        14A         TEXT('Pay Settlement Currency +
     A                                           Exchange Rate')
     A                                      COLHDG('Pay Settlement' +
     A                                           'Currency' 'Exchange Rate')
      *
     A            F5PEXI         1A         TEXT('Pay Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Pay Settlement' 'Currency'+
     A                                           'Exchange' 'Rate Ind')
      **End of change CLE031
      **Start of Change CLE034
     A            F5STAL         1A
     A                                      TEXT('Settlement Allocation +
     A                                           Indicator')
     A                                      COLHDG('Settlement' +
     A                                           'Allocation Indicator')
      **End of Change CLE034
     A**Start of change CGL029
     A            F5PONS        21A         TEXT('Pay - Our Nostro')
     A                                      COLHDG('Pay' 'Our' +
     A                                             'Nostro')
     A            F5RONS        21A         TEXT('Receipt - Our +
     A                                           Nostro')
     A                                      COLHDG('Rcp' 'Our' 'Nost')
     A**End of change CGL029
      **Start of Change CLE073
     A            F5LOAN         6A         TEXT('Loan Reference')
     A                                      COLHDG('Loan' 'Ref.')
      **End of Change CLE073
