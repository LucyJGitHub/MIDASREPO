      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Temp Credit Agreement Activity for Fees')     *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEFEECPD - Temporary Credit Agreement Activity for Fees   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. AR674226           Date 04Dec19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CLE014  *CREATE    Date 20Mar01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR674226 - Manual transaction reached 999. Increase the size *
      *             of CFUSEQ to accomodate 9999 transactions.        *
      *             (Child: AR674227)                                 *
      *           - Applied for MD054782.                             *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE014 - Customer Driven Lending Enhancements                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LEFEECD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            CFRECI    R               REFFLD(RECI)
     A                                      TEXT('Record ID')
     A                                      COLHDG('Record ID')
     A            CFBRCA    R               REFFLD(BRCA)
     A                                      TEXT('Branch Code')
     A                                      COLHDG('Branch Code')
      * Begin /CSD027/
     A**********  CFCNUM    R               REFFLD(CNUM)                                      CSD027
     A            CFCNUM         6A
      * End /CSD027/
     A                                      TEXT('Credit Agreement Facility +
     A                                      Customer')
     A                                      COLHDG('Credit Agreement' +
     A                                      'Facility Customer')
     A            CFFCTY    R               REFFLD(FCLY)
     A                                      TEXT('Credit Agreement Facility')
     A                                      COLHDG('Credit Agreement' +
     A                                      'Facility')
     A            CFADAT    R               REFFLD(DATE)
     A                                      TEXT('Action Date')
     A                                      COLHDG('Action Date')
      **Start of change CLE148
     A**********  CFLOAN    R               REFFLD(LNRF)                                      CLE148
     A            CFLOAN         6A
      **End of change CLE148
     A                                      TEXT('Loan Number')
     A                                      COLHDG('Loan Number')
     A            CFACTN    R               REFFLD(FAACTN)
     A                                      TEXT('Action Type')
     A                                      COLHDG('Action Type')
     A            CFAAMT    R               REFFLD(EAMT)
     A                                      TEXT('Action Amount')
     A                                      COLHDG('Action Amount')
     A            CFRCIN    R               REFFLD(RVCR)
     A                                      TEXT('Revolving Credit Indicator')
     A                                      COLHDG('Revolving Credit' +
     A                                      'Indicator')
     A            CFCFAM    R               REFFLD(FAMT)
     A                                      TEXT('Initial Facility Amount')
     A                                      COLHDG('Initial Facility' +
     A                                      'Amount')
     A            CFDRAM    R               REFFLD(FADRAM)
     A                                      TEXT('Initial Facility Drawn +
     A                                      Amount')
     A                                      COLHDG('Initial Facility' +
     A                                      'Drawn Amount')
     A            CFUNDR    R               REFFLD(FAUNDR)
     A                                      TEXT('Initial Undrawn Amount')
     A                                      COLHDG('Initial Undrawn' +
     A                                      'Amount')
      **Start of Change AR674226
     A**********  CFUSEQ    R               REFFLD(AMSQ)                                    AR674226
     A**********                            TEXT('Utilisation Sequence Number')             AR674226
     A**********                            COLHDG('Utilisation Sequence' +                 AR674226
     A**********                            'Number')                                       AR674226
     A            QQUSEQ         3S 0
      **End of Change AR674226
     A            CFTRRC    R               REFFLD(RVCR)
     A                                      TEXT('Tranche Revolving Credit +
     A                                      Indicator')
     A                                      COLHDG('Tranche Revolving' +
     A                                      'Credit Indicator')
      *
      **Start of Change AR674226
     A            CFUSEQ         4S 0
     A                                      TEXT('Utilisation Sequence Number')
     A                                      COLHDG('Utilisation Sequence' +
     A                                      'Number')
      **End of Change AR674226
