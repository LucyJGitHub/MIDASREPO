/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Fee settlement details')                      *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module.                             *
      *                                                               *
      *  PF/LEFESTPD - Fee Settlement Details                         *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSF011A            Date 28Nov11               *
      *                 CLE073             Date 12May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CLE034             Date 01Aug04               *
      *                 223947             Date 02Apr04               *
      *                 CAP084             Date 07Aug03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP072  *CREATE    Date 27Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSF011A - CCR015: Display Order on Confirmation Page         *
      *  CLE073 - Calculated Loan Based Fees                          *
      *  CLE034 - Lending Enhancements Phase 1 Priority 1A. Added     *
      *           Settlement Allocation field.                        *
      *  223947  - Correct column headings and field text. Bug00678   *
      *  CAP084 - Java Wrappers project - add currency field          *
      *  CAP072 - Conversion of LE inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A          R LEFESTPDF
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A            S#ACTN         1A         TEXT('Action')
     A                                      COLHDG('Action')                                  223947
     A            S#CSSN        10A         TEXT('Customer Number')
     A                                      COLHDG('Customer' +                               223947
     A                                             'Number')                                  223947
      **Start of change CSF011A
     A            S#CNSN        10A         TEXT('Customer Shortname')
     A                                      COLHDG('Customer Shortname')
     A            S#CNNM        20A         TEXT('Customer Report Name')
     A                                      COLHDG('Customer Report Name')
     A            S#CNTN        10A         TEXT('Customer Report Town')
     A                                      COLHDG('Customer Report Town')
      **End of change CSF011A
     A            S#FACT         3A         TEXT('Facility Type')
     A                                      COLHDG('Fac' +                                    223947
     A                                             'Type')                                    223947
     A            S#FCNO         2A         TEXT('Facility Number')
     A                                      COLHDG('Fac' +                                    223947
     A                                             'Num')                                     223947
     A            S#FSEQ         2A         TEXT('Fee Sequence Number')
     A                                      COLHDG('Fee' +                                    223947
     A                                             'Seq')                                     223947
     A            S#VDAT         6A         TEXT('Value Date')
     A                                      COLHDG('Value' +                                  223947
     A                                             'Date')                                    223947
     A            S#SADJ        14A         TEXT('Fee Settlement Amount')
     A                                      COLHDG('Fee Settlement' +                         223947
     A                                             'Amount')                                  223947
     A            S#TYPE         1A         TEXT('Writ-Off/Full/Part Pay Ind.')
     A                                      COLHDG('W, F' +                                   223947
     A                                             'or P')                                    223947
      * Start of CAP084
     A            S#FCCY         3A         TEXT('Fee Currency')
     A                                      COLHDG('Fee' +                                    223947
     A                                             'Ccy')                                     223947
      * End of CAP084
      **Start of change CLE034
     A            S#STAL         1A         TEXT('Settlement Allocation')
     A                                      COLHDG('Settlement' +
     A                                             'Allocation')
      **End of change CLE034
      **Start of Change CLE073
     A            S#LOAN         6A         TEXT('Loan Reference')
     A                                      COLHDG('Loan' 'Ref.')
      **End of Change CLE073
