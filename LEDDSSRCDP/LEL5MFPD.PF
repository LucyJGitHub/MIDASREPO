      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE PC Msg Format Fees')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEL5MFPD - PC Subsystem Interface                         *
      *                Lending Download                               *
      *                Fees Message Format                            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CLE034             Date 05May03               *
      *                 CLE031             Date 03Feb03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU030             Date 08Jun99               *
      *                 148756             Date 30Nov98               *
      *                 157286 (BT0888)    Date 22Jun98               *
      *                 133237             Date 07Apr98               *
      *                 CLE005 *CREATE     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE034 - Lending Enhancements Phase 1 Priority 1A            *
      *  CLE031 - Lending Enhancements.                               *
      *  CEU030 - EMU Loan Manager Enhancement                        *
      *  148756 - Negative amounts wrong for fees.                    *
      *  157286 - Add PC Originating Branch                           *
      *  133237 - Add profit centre field                             *
      *  CLE005 - Customer Lending Enhancements - Others              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LEL5MFD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            L5MSGT    R               REFFLD(X1MSGT LEX1MFPD)
     A            L5TRUS    R               REFFLD(X1TRUS LEX1MFPD)
     A            L5TRWS    R               REFFLD(X1TRWS LEX1MFPD)
     A            L5TNRF    R               REFFLD(X1TNRF LEX1MFPD)
     A            L5TRSN    R               REFFLD(X1TRSN LEX1MFPD)
     A            L5ACTN    R               REFFLD(X1ACTN LEX1MFPD)
     A            L5TRDS    R               REFFLD(X1TRDS LEX1MFPD)
     A            L5TRTS    R               REFFLD(X1TRTS LEX1MFPD)
     A            L5BTNM    R               REFFLD(X1BTNM LEX1MFPD)
     A            L5BTML    R               REFFLD(X1BTML LEX1MFPD)
     A            L5CONT    R               REFFLD(X1CONT LEX1MFPD)
     A            L5FIL1    R               REFFLD(X1FIL1 LEX1MFPD)
      *
     A            L5CNUM    R               REFFLD(RINO)
     A                                      TEXT('Customer Number')
     A                                      COLHDG('Cust' 'No.')
     A            L5FACT    R               REFFLD(CCY)
     A                                      TEXT('Facility Type')
     A                                      COLHDG('Fclty' 'Type')
     A            L5FCNO    R               REFFLD(AMTP)
     A                                      TEXT('Facility Number')
     A                                      COLHDG('Fclty' 'No.')
     A            L5LOAN    R               REFFLD(RINO)
     A                                      TEXT('Loan Number')
     A                                      COLHDG('Loan' 'No.')
     A            L5FSEQ    R               REFFLD(AMTP)
     A                                      TEXT('Fee Sequence Number')
     A                                      COLHDG('Fee' 'Seq.' 'No.')
     A            L5FCOD    R               REFFLD(AMTP)
     A                                      TEXT('Fee Code')
     A                                      COLHDG('Fee' 'Code')
     A            L5FCCY    R               REFFLD(CCY)
     A            L5FAMT    R               REFFLD(BCNM)
     A                                      TEXT('Flat Fees Amount')
     A                                      COLHDG('Flat' 'Fees' +
     A                                             'Amount')
     A            L5FAMI    R               REFFLD(ACCL)
     A                                      TEXT('Flat Fees Amount +
     A                                           Sign Ind')
     A                                      COLHDG('Amount' 'Sign' +
     A                                             'Indicator')
     A            L5PSTD    R               REFFLD(DATDFO)
     A                                      TEXT('Fee Period Start +
     A                                           Date')
     A                                      COLHDG('Fee Period' +
     A                                             'Start' 'Date')
     A            L5PEND    R               REFFLD(DATDFO)
     A                                      TEXT('Fee Period End +
     A                                           Date')
     A                                      COLHDG('Fee Period' +
     A                                             'End' 'Date')
     A            L5PYON    R               REFFLD(FEPYON)
     A                                      TEXT('Payment On Ind+
     A                                           icator')
     A            L5AUTO    R               REFFLD(AUTO)
     A            L5CALT    R               REFFLD(FECALT)
     A            L5FRT1    R               REFFLD(DPGM)
     A                                      TEXT('Fee Rate - Tier 1')
     A                                      COLHDG('Fee Rate' +
     A                                             'Tier 1')
     A            L5FRT2    R               REFFLD(DPGM)
     A                                      TEXT('Fee Rate - Tier 2')
     A                                      COLHDG('Fee Rate' +
     A                                             'Tier 2')
     A            L5FRT3    R               REFFLD(DPGM)
     A                                      TEXT('Fee Rate - Tier 3')
     A                                      COLHDG('Fee Rate' +
     A                                             'Tier 3')
     A            L5FRT4    R               REFFLD(DPGM)
     A                                      TEXT('Fee Rate - Tier 4')
     A                                      COLHDG('Fee Rate' +
     A                                             'Tier 4')
     A            L5FRT5    R               REFFLD(DPGM)
     A                                      TEXT('Fee Rate - Tier 5')
     A                                      COLHDG('Fee Rate' +
     A                                             'Tier 5')
     A            L5AMT1    R               REFFLD(CCTN)
     A                                      TEXT('Fee Amount - Tier 1')
     A                                      COLHDG('Fee Amount' +
     A                                             'Tier 1')
     A            L5AMT2    R               REFFLD(CCTN)
     A                                      TEXT('Fee Amount - Tier 2')
     A                                      COLHDG('Fee Amount' +
     A                                             'Tier 2')
     A            L5AMT3    R               REFFLD(CCTN)
     A                                      TEXT('Fee Amount - Tier 3')
     A                                      COLHDG('Fee Amount' +
     A                                             'Tier 3')
     A            L5AMT4    R               REFFLD(CCTN)
     A                                      TEXT('Fee Amount - Tier 4')
     A                                      COLHDG('Fee Amount' +
     A                                             'Tier 4')
     A            L5AMT5    R               REFFLD(CCTN)
     A                                      TEXT('Fee Amount - Tier 5')
     A                                      COLHDG('Fee Amount' +
     A                                             'Tier 5')
     A            L5PIND    R               REFFLD(ACCL)
     A                                      TEXT('Fee Percent/Amount +
     A                                           Indicator')
     A                                      COLHDG('Fee' 'Prcnt/Amnt'+
     A                                             'Indicator')
     A            L5CALC    R               REFFLD(ACCL)
     A                                      TEXT('Fee Calculation +
     A                                           Basis')
     A                                      COLHDG('Fee' 'Calc' +
     A                                             'Basis')
     A            L5FREQ    R               REFFLD(FREQ)
     A                                      TEXT('Fee Payment Freq+
     A                                           uency')
     A                                      COLHDG('Fee' 'Pay' 'Freq')
     A            L5DYIM    R               REFFLD(AMTP)
     A                                      TEXT('Fee Frequency Day +
     A                                           Number')
     A                                      COLHDG('Freq.' 'Day' 'No.')
     A            L5NPYD    R               REFFLD(DATDFO)
     A                                      TEXT('Next Fee Payment +
     A                                           Date')
     A                                      COLHDG('Next Fee' +
     A                                             'Pay Date')
     A            L5MDIN    R               REFFLD(ACCL)
     A                                      TEXT('Multiply/Divide +
     A                                           Indicator')
     A                                      COLHDG('M/D' 'Ind')
     A            L5ERAT    R               REFFLD(BCNM)
     A                                      TEXT('Facility Currency +
     A                                           Exch Rate')
     A                                      COLHDG('Fclty' 'Ccy Exch' +
     A                                             'Rate')
     A            L5PSTM    R               REFFLD(AMTP)
     A                                      TEXT('Pay - Settlement +
     A                                           Type')
     A                                      COLHDG('Pay' 'Set.' 'Typ.')
     A**Start of change CGL029
     A**********  L5PONS    R               REFFLD(ACRF)                                      CGL029
     A**********                            TEXT('Pay - Our Nostro')                          CGL029
     A**********                            COLHDG('Pay' 'Our' +                              CGL029
     A**********                                   'Nostro')                                  CGL029
     A            QQPONS        15A
     A**End of change CGL029
     A            L5RVNO    R               REFFLD(RVNO)
     A                                      TEXT('Pay - Recepient of +
     A                                           Pay Inst')
     A                                      COLHDG('Pay' 'Ordering' +
     A                                             'Customer')
     A            L5CVMR    R               REFFLD(CVMR)
     A                                      TEXT('Pay - Cover MT202 +
     A                                           Req Ind')
     A                                      COLHDG('Pay' 'Cover' +
     A                                             'Req Ind')
     A            L5POCN    R               REFFLD(RINO)
     A                                      TEXT('Pay - Ordering +
     A                                           Customer')
     A                                      COLHDG('Pay' 'Ordering' +
     A                                             'Customer')
     A            L5POBN    R               REFFLD(RINO)
     A                                      TEXT('Pay - Ordering +
     A                                           Bank')
     A                                      COLHDG('Pay' 'Ordering' +
     A                                             'Bank')
     A            L5RCRN    R               REFFLD(RCRN)
     A            L5RCRA    R               REFFLD(RCRA)
     A            L5PIBN    R               REFFLD(PIBN)
     A            L5PIBA    R               REFFLD(PIBA)
     A            L5AWBN    R               REFFLD(AWBN)
     A                                      TEXT('Pay - a/c with bank +
     A                                           no.')
     A                                      COLHDG('Pay' 'a/c w/' +
     A                                             'bank no')
     A            L5AWBA    R               REFFLD(AWBA)
     A                                      TEXT('Pay - a/c with bank +
     A                                           a/c line')
     A                                      COLHDG('Pay' 'a/c w/' +
     A                                             'bank a/c')
     A            L5BENN    R               REFFLD(BENN)
     A                                      TEXT('Pay - beneficiary +
     A                                           no.')
     A                                      COLHDG('Pay' 'benef' +
     A                                             'no')
     A            L5BENA    R               REFFLD(BENA)
     A                                      TEXT('Pay - beneficiary +
     A                                           a/c line')
     A                                      COLHDG('Pay' ' benef' +
     A                                             'a/c line')
     A            L5RSTM    R               REFFLD(AMTP)
     A                                      TEXT('Receipt - Settle+
     A                                           ment Type')
     A                                      COLHDG('Rcp' 'Set.' 'Typ.')
     A**Start of change CGL029
     A**********  L5RONS    R               REFFLD(ACRF)                                      CGL029
     A**********                            TEXT('Receipt - Our +                             CGL029
     A**********                                 Nostro')                                     CGL029
     A**********                            COLHDG('Rcp' 'Our' 'Nost')                        CGL029
     A            QQRONS        15A
     A**End of change CGL029
     A            L5ROCN    R               REFFLD(RINO)
     A                                      TEXT('Receipt - Ordering +
     A                                           Customer')
     A                                      COLHDG('Rcp' 'Ordering' +
     A                                             'Customer')
     A            L5ROBN    R               REFFLD(RINO)
     A                                      TEXT('Receipt - Ordering +
     A                                           Bank')
     A                                      COLHDG('Rcp' 'Ordering' +
     A                                             'Bank')
     A            L5RIBN    R               REFFLD(RIBN)
     A            L5RIBA    R               REFFLD(RIBA)
     A            L5DTPN    R               REFFLD(ZZ140)
     A                                      TEXT('Details of Payments +
     A                                           1-4')
     A                                      COLHDG('Detls' ' of Pay' +
     A                                             '1-4')
     A            L5DCHG    R               REFFLD(DCHG)
     A            L5BTBN    R               REFFLD(ZZ210)
     A                                      TEXT('Bank to Bank Info +
     A                                           1-6')
     A                                      COLHDG('Bank to' 'Bank' +
     A                                             'Info 1-6')
     A            L5SHFI    R               REFFLD(SPIN)
     A                                      TEXT('Shared Fee Indicator')
     A                                      COLHDG('Shr Fee' +
     A                                             'Ind')
     A            L5GFPT    R               REFFLD(SPIN)
     A                                      TEXT('Generated by Fund +
     A                                           Participant')
     A                                      COLHDG('Gen by' +
     A                                             'Fund Part')
     A            L5PTAI    R               REFFLD(SPIN)
     A                                      TEXT('Participant/Assign+
     A                                            ment Ind')
     A                                      COLHDG('Part/' +
     A                                             'Assg?')
     A            L5SMAC    R               REFFLD(RINO)
     A                                      TEXT('Synd Member or +
     A                                           Assign Cust No.')
     A                                      COLHDG('Syn Mem/' 'Assign'+
     A                                             'Cust No.')
     A            L5IUSR    R               REFFLD(RSTU)
     A                                      TEXT('User Profile - +
     A                                           Insert')
     A                                      COLHDG('Insert' 'User')
     A            L5AUSR    R               REFFLD(RSTU)
     A                                      TEXT('User Profile - +
     A                                           Amend')
     A                                      COLHDG('Amend' 'User')
     A            L5XUSR    R               REFFLD(RSTU)
     A                                      TEXT('User Profile - +
     A                                           Authorise')
     A                                      COLHDG('Auth.' 'User')
     A            L5RECI    R               REFFLD(RECI)
     A                                      TEXT('Record Identifier')
     A            L5BRCA    R               REFFLD(BRCA)
     A                                      TEXT('Branch')
     A                                      COLHDG('Branch')
     A            L5ACOF    R               REFFLD(ACOF)
     A            L5DIND    R               REFFLD(SPIN)
     A                                      TEXT('Fee Due Indicator')
     A                                      COLHDG('Fee Due' 'Ind')
     A            L5AMTP    R               REFFLD(BCNM)
     A                                      TEXT('Fee Amount Past Due')
     A                                      COLHDG('Fee Amnt' +
     A                                             'Past Due')
     A            L5AMTA    R               REFFLD(BCNM)
     A                                      TEXT('Fee Adjustment +
     A                                           Amount')
     A                                      COLHDG('Fee Adj' +
     A                                             'Amount')
     A            L5AMTS    R               REFFLD(BCNM)
     A                                      TEXT('Fee Settled +
     A                                           Amount')
     A                                      COLHDG('Fee Set' +
     A                                             'Amount')
     A            L5SDAT    R               REFFLD(DATDFO)
     A                                      TEXT('Fee Settled Date')
     A                                      COLHDG('Fee Set'  +
     A                                             'Date')
     A            L5LPDT    R               REFFLD(DATDFO)
     A                                      TEXT('Last Payment Date')
     A                                      COLHDG('Last Pay' +
     A                                             'Date')
     A            L5LADT    R               REFFLD(DATDFO)
     A                                      TEXT('Last Accrual Date')
     A                                      COLHDG('Last Acc' +
     A                                             'Date')
     A            L5ADAT    R               REFFLD(BCNM)
     A                                      TEXT('Total Fees Accrued +
     A                                           to Date')
     A                                      COLHDG('Tot Fees' +
     A                                             'Acrd')
     A            L5DDAT    R               REFFLD(DATDFO)
     A                                      TEXT('Past Due Date')
     A                                      COLHDG('Past Due' +
     A                                             'Date')
     A            L5AMTO    R               REFFLD(BCNM)
     A                                      TEXT('Outstanding Fee +
     A                                           Amount')
     A                                      COLHDG('Outstand' +
     A                                             'Fee Amnt')
     A            L5LTYP    R               REFFLD(LTYP)
     A            L5SUTP    R               REFFLD(SUTP)
     A            L5PLPD    R               REFFLD(DATDFO)
     A                                      TEXT('Previous Last Pay+
     A                                           ment Date')
     A                                      COLHDG('Prev Last' +
     A                                             'Pay Date')
     A            L5SPTI    R               REFFLD(SPTI)
     A                                      TEXT('Start Pay/Receive +
     A                                           Tel Ind')
     A                                      COLHDG('Start' 'Pay/Rec' +
     A                                             'Tel Ind')
     A            L5PPTI    R               REFFLD(FEPPTI)
     A                                      TEXT('Next Pay/Receive +
     A                                           Tel Ind')
     A                                      COLHDG('Next' 'Pay/Rec' +
     A                                             'Tel Ind')
     A            L5EPTI    R               REFFLD(FEEPTI)
     A                                      TEXT('End Pay/Receive +
     A                                           Tel Ind')
     A                                      COLHDG('End' 'Pay/Rec' +
     A                                             'Tel Ind')
     A            L5SBTI    R               REFFLD(FESBTI)
     A                                      TEXT('Start Pay Bill/+
     A                                           Telex Ind')
     A                                      COLHDG('St Pay' 'Bill/Tel'+
     A                                             'Ind')
     A            L5PBTI    R               REFFLD(FEPBTI)
     A                                      TEXT('Next Pay Bill/+
     A                                           Telex Ind')
     A                                      COLHDG('Nx Pay' 'Bill/Tel'+
     A                                             'Ind')
     A            L5EBTI    R               REFFLD(FEEBTI)
     A                                      TEXT('End Pay Bill/+
     A                                           Telex Ind')
     A                                      COLHDG('En Pay' 'Bill/Tel'+
     A                                             'Ind')
     A            L5LPAM    R               REFFLD(BCNM)
     A                                      TEXT('Last Pay Fee +
     A                                           Amount')
     A                                      COLHDG('Lst Pay' +
     A                                             'Fee Amnt')
     A            L5PAMS    R               REFFLD(BCNM)
     A                                      TEXT('Last Pay Fee +
     A                                           Settled Amount')
     A                                      COLHDG('Lst Pay' +
     A                                             'Fee Set' +
     A                                             'Amount')
     A            L5PAMP    R               REFFLD(BCNM)
     A                                      TEXT('Last Pay Fee +
     A                                           Amount Past Due')
     A                                      COLHDG('Lst Pay' +
     A                                             'Fee Amnt' +
     A                                             'Past Due')
     A            L5ADBP    R               REFFLD(BCNM)
     A                                      TEXT('Last Pay Fees +
     A                                           Accr''d to Date')
     A                                      COLHDG('Lst Pay' +
     A                                             'Fee Acrd' +
     A                                             'to Date')
     A            L5FSTS    R               REFFLD(SPIN)
     A                                      TEXT('Fee Status Indicator')
     A                                      COLHDG('Fee' +
     A                                             'Sts')
     A            L5PCRF    R               REFFLD(ZZ015)
     A                                      TEXT('PC Transaction +
     A                                            Reference')
     A                                      COLHDG('PC Trans' +
     A                                             'Reference')
     A            L5PTFI    R               REFFLD(SPIN)
     A                                      TEXT('Participant Fee +
     A                                            Indicator')
     A                                      COLHDG('Part Fee' +
     A                                             'Ind')
     A*                                      Begin /133237/
     A            L5PRFC    R               REFFLD(PRFC)
     A*                                      End   /133237/
     A*                                      Begin /157286/
     A            L5PCOB    R               REFFLD(ORBR)
     A*                                      End   /157286/
     A*                                      Begin /148756/
     A            L5FAPI    R               REFFLD(ACCL)
     A                                      TEXT('Fee Amount Past +
     A                                           Due Sign Ind')
     A                                      COLHDG('Amount' 'Sign' +
     A                                             'Indicator')
     A*                                      End   /148756/
      *                                      Begin /CEU030/
     A            L5SCCY    R               REFFLD(CCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
     A            L5ICCY    R               REFFLD(CCY)
     A                                      TEXT('IN Currency in +
     A                                            Fld 72')
     A                                      COLHDG('IN Ccy')
      *                                      End   /CEU030/
      **Start of Change CLE031
     A            L5REXR        14A
     A                                      TEXT('Receive Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A            L5REXI         1A
     A                                      TEXT('Receive Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
     A            L5PSCY         3A
     A                                      TEXT('Pay Settlement Currency')
     A                                      COLHDG('Pay' 'Settlement' +
     A                                           'Currency')
     A            L5PEXR        14A
     A                                      TEXT('Pay Settlement +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                           'Currency' 'Exchange'+
     A                                           'Rate')
     A            L5PEXI         1A
     A                                      TEXT('Pay Settlement +
     A                                           Currency Exchange Rate Ind')
     A                                      COLHDG('Pay' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
      **End of Change CLE031
      **Start of Change CLE034
     A            L5STAL         1A
     A                                      TEXT('Settlement Allocation +
     A                                           Indicator')
     A                                      COLHDG('Settlement' +
     A                                           'Allocation Indicator')
      **End of Change CLE034
     A**Start of change CGL029
     A            L5PONS        21A         TEXT('Pay - Our Nostro')
     A                                      COLHDG('Pay' 'Our' +
     A                                             'Nostro')
     A            L5RONS        21A         TEXT('Receipt - Our +
     A                                           Nostro')
     A                                      COLHDG('Rcp' 'Our' 'Nost')
     A**End of change CGL029
