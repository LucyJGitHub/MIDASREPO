      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE PC Msg Format Manual Repayment')              *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEM1MFPD - PC Subsystem Interface                         *
      *                Lending Download                               *
      *                Manual Repayment                               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CLE036             Date 29Sep03               *
      *                 CGL029             Date 01Sep03               *
      *                 CLE034             Date 05May03               *
      *                 CLE031             Date 03Feb03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CPK014             Date 20Jun01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 143802             Date 02Feb98               *
      *                 CEU004             Date 02Feb98               *
      *                 CLE004 *Create     Date 08MAY97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE036 - Lending Enhancements for Nordea Phase 1 Priority 2: *
      *           Allow repayments in facility currency.              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE034 - Lending Enhancements Phase 1 Priority 1A            *
      *  CLE031 - Lending Enhancements.                               *
      *  CPK014 - Change field references to remove multi-layered     *
      *            dependencies; this is for compilation reasons.     *
      *  143802 - Assignment flag added to file                       *
      *  CEU004 - LE Settlement Ccy Conversion Upgrade to DBA R2.0    *
      *  CLE004 - Customer Lending Enhancements - Syndications        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LEM1MFD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            M1MSGT    R               REFFLD(X1MSGT LEX1MFPD)
     A            M1TRUS    R               REFFLD(X1TRUS LEX1MFPD)
     A            M1TRWS    R               REFFLD(X1TRWS LEX1MFPD)
     A            M1TNRF    R               REFFLD(X1TNRF LEX1MFPD)
     A            M1TRSN    R               REFFLD(X1TRSN LEX1MFPD)
     A            M1ACTN    R               REFFLD(X1ACTN LEX1MFPD)
     A            M1TRDS    R               REFFLD(X1TRDS LEX1MFPD)
     A            M1TRTS    R               REFFLD(X1TRTS LEX1MFPD)
      * Start CPK014
     A**********  M1NRBA    R               REFFLD(F1NRBA LEF1MFPD)
     A            M1NRBA    R               REFFLD(NREC)
     A                                      TEXT('Number of records in batch')
     A                                      COLHDG('No.' 'Records' +
     A                                             'in batch')
      * End CPK014
     A            M1FIL4    R               REFFLD(ZZ004)
      *
      **Start of change CLE148
     A**********  M1LNRF    R               REFFLD(LNRF)                                      CLE148
     A            M1LNRF         6A         TEXT('Loan number')
     A                                      COLHDG('Loan' 'Number')
      **End of change CLE148
     A            M1VDAT    R               REFFLD(DATDFO)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value' +
     A                                             'Date')
     A            M1LASN    R               REFFLD(LASN LOAMSDK)
     A            M1XAVD    R               REFFLD(DATDFO)
     A                                      TEXT('X-ref loan amend value date')
     A                                      COLHDG('X-ref' +
     A                                             'ln amnd' +
     A                                             'Val dte')
     A            M1XASQ    R               REFFLD(XASQ)
      * Begin /CSD027/
     A**********  M1CNUM    R               REFFLD(CNUM)                                      CSD027
     A            M1CNUM         6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            M1PRAM    R               REFFLD(ZZ014)
     A                                      TEXT('Principal Amount')
     A                                      COLHDG('Principal' +
     A                                             'Amt')
     A            M1PRAS    R               REFFLD(SGN1)
     A                                      TEXT('Principal Amount')
     A                                      COLHDG('Principal' +
     A                                             'Amt')
     A            M1INTA    R               REFFLD(ZZ014)
     A                                      TEXT('Interest Amount')
     A                                      COLHDG('Interest' +
     A                                             'Amt')
     A            M1INTS    R               REFFLD(SGN1)
     A                                      TEXT('Interest Amount')
     A                                      COLHDG('Interest' +
     A                                             'Amt')
     A            M1TAMT    R               REFFLD(ZZ014)
     A                                      TEXT('Total Amount')
     A                                      COLHDG('Total' +
     A                                             'Amt')
     A            M1TAMS    R               REFFLD(SGN1)
     A                                      TEXT('Total Amount')
     A                                      COLHDG('Total' +
     A                                             'Amt')
     A            M1LWOI    R               REFFLD(LWOI)
     A            M1PSTM    R               REFFLD(PSTM)
     A**Start of change CGL029
     A**********  M1PONS    R               REFFLD(TNUM)                                      CGL029
     A**********                            TEXT('Pay - Our Nostro')                          CGL029
     A**********                            COLHDG('Pay - Our' +                              CGL029
     A**********                                   'Nostro' )                                 CGL029
     A            QQPONS        15A
     A**End of change CGL029
     A            M1RVNO    R               REFFLD(RVNO)
     A            M1CVMR    R               REFFLD(CVMR)
      * Begin /CSD027/
     A**********  M1POCN    R               REFFLD(POCN)                                      CSD027
     A**********  M1POBN    R               REFFLD(POBN)                                      CSD027
     A            M1POCN         6A
     A                                      TEXT('pay - ordering customer no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'cust')
     A            M1POBN         6A
     A                                      TEXT('pay - ordering bank no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'bank')
      * End /CSD027/
     A            M1RCRN    R               REFFLD(RCRN)
     A            M1RCRA    R               REFFLD(RCRA)
     A            M1PIBN    R               REFFLD(PIBN)
     A            M1PIBA    R               REFFLD(PIBA)
     A            M1AWBN    R               REFFLD(AWBN)
     A            M1AWBA    R               REFFLD(AWBA)
     A            M1BENN    R               REFFLD(BENN)
     A            M1BENA    R               REFFLD(BENA)
     A            M1RSTM    R               REFFLD(RSTM)
     A**Start of change CGL029
     A**********  M1RONS    R               REFFLD(TNUM)                                      CGL029
     A**********                            TEXT('Receipt - Our Nostro')                      CGL029
     A**********                            COLHDG('Receipt - Our Nostro' )                   CGL029
     A            QQRONS        15A
     A**End of change CGL029
      * Begin /CSD027/
     A**********  M1ROCN    R               REFFLD(ROCN)                                      CSD027
     A**********  M1ROBN    R               REFFLD(ROBN)                                      CSD027
     A            M1ROCN         6A
     A                                      TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'cust')
     A            M1ROBN         6A
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'bank')
      * End /CSD027/
     A            M1RIBN    R               REFFLD(RIBN)
     A            M1RIBA    R               REFFLD(RIBA)
     A            M1DTP1    R               REFFLD(ZZ140)
     A                                      TEXT('Details of Payments +
     A                                           1-4')
     A                                      COLHDG('Detls' ' of Pay' +
     A                                             '1-4')
     A            M1DCHG    R               REFFLD(DCHG)
     A            M1BTB1    R               REFFLD(ZZ210)
     A                                      TEXT('Bank to Bank Info +
     A                                           1-6')
     A                                      COLHDG('Bank to' 'Bank' +
     A                                             'Info 1-6')
     A            M1PAST    R               REFFLD(PAST LOAMSDK)
     A            M1PNAM    R               REFFLD(ZZ014)
     A                                      TEXT('Penalty Amount')
     A                                      COLHDG('Penalty Amount')
     A            M1PNAS    R               REFFLD(SGN1)
     A                                      TEXT('Penalty Amount')
     A                                      COLHDG('Penalty Amount')
     A            M1IUSR    R               REFFLD(IUSR LOAMSDK)
     A            M1AUSR    R               REFFLD(AUSR LOAMSDK)
     A            M1XUSR    R               REFFLD(XUSR LOAMSDK)
      * Start CPK014
     A**********  M1PCOB    R               REFFLD(F1PCOB LEF1MFPD)
     A            M1PCOB    R               REFFLD(BRCA)
     A                                      TEXT('PC Originating Branch')
     A                                      COLHDG('PC' 'Branch')
      * End CPK014
      *                                              Begin /CEU004/
     A            M1SCCY    R               REFFLD(CCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
     A            M1ICCY    R               REFFLD(CCY)
     A                                      TEXT('IN Currency in +
     A                                            Fld 72')
     A                                      COLHDG('IN Ccy')
      *                                              End   /CEU004/
      *                                              Begin /143802/
     A            M1GASS    R               REFFLD(GASS CLOANCL)
     A                                      TEXT('Assignment Flag')
     A                                      COLHDG('Ass.')
      *                                              End   /143802/
      **Start of change CLE031
     A            M1REXR        14A         TEXT('Receive Settlement  +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange  Rate')
      *
     A            M1REXI         1A         TEXT('Receive Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
      *
     A            M1PSCY         3A         TEXT('Pay Settlement Currency')
     A                                      COLHDG('Pay Settlement' 'Currency')
      *
     A            M1PEXR        14A         TEXT('Pay Settlement Currency +
     A                                           Exchange Rate')
     A                                      COLHDG('Pay Settlement' +
     A                                           'Currency' 'Exchange Rate')
      *
     A            M1PEXI         1A         TEXT('Pay Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Pay Settlement' 'Currency'+
     A                                           'Exchange' 'Rate Ind')
      **End of change CLE031
      **Start of Change CLE034
     A            M1STAL         1A
     A                                      TEXT('Settlement Allocation +
     A                                           Indicator')
     A                                      COLHDG('Settlement' +
     A                                           'Allocation Indicator')
      **End of Change CLE034
     A**Start of change CGL029
     A            M1PONS        21A         TEXT('Pay - Our Nostro')
     A            M1RONS        21A         TEXT('Receipt - Our Nostro')
     A**End of change CGL029
      **Start of change CLE036
     A            M1CCY          3A
     A                                      TEXT('Repay +
     A                                            Ccy')
     A                                      COLHDG('Repay' +
     A                                             'Ccy')
      **End of change CLE036
