      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Past Due & Original Loan Link - History')     *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEPDFHPD - Past Due & Original Loan Link - History        *
      *                                                               *
      *                This history is used to determine the          *
      *                balance as at Repayment Value Date of all      *
      *                PDCLs for each Origin Fee.                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CLE134  *CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *                                                               *
      *****************************************************************
      *
     A          R LEPDFHD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
      *
      ** Field Details
      ** D = Detail    T = Total
      *
     A            YPRECI         1A         COLHDG('Record' 'Type')
     A            YECNUM         6A         COLHDG('Original Fee' +
     A                                             'Customer Number')
     A            YEFACL         5S 0       COLHDG('Original Fee' +
     A                                             'Facility Number')
     A            YESEQ          2S 0       COLHDG('Original Fee' 'Sequence')
     A            YPORG          6A         COLHDG('Original Fee' 'Loan Number')
     A            YBRCA          3A         COLHDG('Original Fee' 'Branch')
     A            YPLON          6A         COLHDG('Past Due' 'Call Loan')
     A            YPDVDT         5P 0       COLHDG('Repayment' 'Value Date')
     A            YPDSDT         5P 0       COLHDG('PDCL' 'Start Date')
     A            YPOPNB        15S 0       COLHDG('Opening' 'Balance')
     A            YPMOVA        15S 0       COLHDG('Movement' 'Amount')
     A            YPCLOB        15S 0       COLHDG('Closing' 'Balance')
     A            YPCPAM        15P 0       COLHDG('Principal Total' +
     A                                             'Transferred to PDCL')
     A            YPDINT        15P 0       COLHDG('Interest Total' +
     A                                             'Transferred to PDCL')
      *
      ** Key Details
      *
     A          K YECNUM
     A          K YEFACL
     A          K YESEQ
     A          K YPORG
     A          K YBRCA
     A          K YPLON
     A          K YPDVDT                    DESCEND
