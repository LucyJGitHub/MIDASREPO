      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Past Due & Original Loan Link - History')     *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEPDLHPD - Past Due & Original Loan Link - History        *
      *                                                               *
      *                This history file is used to determine the     *
      *                balance as at Repayment Value Date of all      *
      *                PDCLs for each Origin Loan/Fee.                *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134  *CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *                                                               *
      *****************************************************************
      *
     A          R LEPDLHD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2012')
      *
      ** Field Details...
      *
     A            YPRECI         1A         COLHDG('Record' 'Type')
      *                                     D = Detail    T = Total
      *
     A            YPORG          6A         COLHDG('Past Due' 'Call Loan' +
     A                                             'Original Loan')
     A            YPLON          6A         COLHDG('Past Due' 'Call Loan')
      *
     A            YPDVDT         5P 0       COLHDG('Repayment' 'Value Date')
     A            YPDSDT         5P 0       COLHDG('PDCL' 'Start Date')
      *
     A            YPOPNB        15S 0       COLHDG('Opening' 'Balance')
     A            YPMOVA        15S 0       COLHDG('Movement' 'Amount')
     A            YPCLOB        15S 0       COLHDG('Closing' 'Balance')
      *
     A            YPCPAM        15P 0       COLHDG('Principal Total' +
     A                                             'Transferred to PDCL')
     A            YPDINT        15P 0       COLHDG('Interest Total' +
     A                                             'Transferred to PDCL')
      *
      ** Key Details...
      *
     A          K YPORG
     A          K YPLON
     A          K YPDVDT                    DESCEND
      *
