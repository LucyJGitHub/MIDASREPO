      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE Past Due Call Loans Account Keys Detail')     *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LEPK1DPD - Past Due Call Loans Account Keys Details       *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE164             Date 18Aug14               *
      *                 AR1056323          Date 14Nov12               *
      *                 263074             Date 01Aug12               *
      *                 CLE134  *CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE164 - CLE134 Enhancement F (Repayment Methodology)        *
      *  AR1056323 - Revert back changes of CLE134 for LKEY1DP and    *
      *              LKEYFED                                          *
      *  263074 - Wrong Postings when there is Interest and           *
      *           Principal paid on the same day                      *
      *           CLE134 / SWIFT                                      *
      *  CLE134 - Past Due Call Loan Processing                       *
      *                                                               *
      *****************************************************************
      *
     A          R LKEY1DPF                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2012')
      *
     A            RECI           1A         TEXT('Record ID')
     A                                      COLHDG('Recored ID')
     A            QQAKEY        10A
      ** Start of change AR1056323
     A**********  QQLNNO         6P 0       TEXT('Loan Number')                            AR1056323
     A            LNNO           6A         TEXT('Loan Number')
      ** End of change AR1056323
     A                                      COLHDG('Loan' 'Number')
     A            CNUM           6A         TEXT('Customer Number')
     A                                      COLHDG('Customer' +
     A                                             'Number')
     A            BRCA           3A         TEXT('Branch Code')
     A                                      COLHDG('Branch' 'Code')
     A            ACSQ           2S 0       TEXT('Account Sequence')
     A                                      COLHDG('A/c Seq.')
     A            EDAT           5P 0       TEXT('Event Date')
     A                                      COLHDG('Event Date')
     A            EAMT          13P 0       TEXT('Event Amount')
     A                                      COLHDG('Event Amount')
     A            ECCY           3A         TEXT('Event Currency')
     A                                      COLHDG('Event Ccy')
     A            SETP           2S 0       TEXT('Settlement Type')
     A                                      COLHDG('Sett. Type')
     A            OSACQQ        12A
     A            REVI           1S 0       TEXT('Reversal Indicator')
     A                                      COLHDG('Reversal' 'Ind.')
     A            OTHA          13P 0       TEXT('Other Amount')
     A                                      COLHDG('Other Amount')
     A            OTHC           3A         TEXT('Other Currency')
     A                                      COLHDG('Other Ccy')
     A            EXRT          13P 8       TEXT('Echange Rate')
     A                                      COLHDG('Exchange' 'Rate')
     A            STDT           5P 0       TEXT('Start Date')
     A                                      COLHDG('Start Date')
     A            SLID           5P 0       TEXT('Start Date of Last +
     A                                            Interest Date')
     A                                      COLHDG('Start Date' +
     A                                             'of Last' +
     A                                             'Interest Date')
     A            MDAT           5P 0       TEXT('Maturity Date')
     A                                      COLHDG('Maturity' 'Date')
      **Start of change CSD103
     A**********  BRTT           2S 0       TEXT('Base Rate Code')
     A            BRTT           2A         TEXT('Base Rate Code')
      **End of change CSD103
     A                                      COLHDG('Base Rate' 'Code')
     A            BRTE          11P 7       TEXT('Current Base Rate')
     A                                      COLHDG('Current' +
     A                                             'Base Rate')
     A            RTSP          11P 7       TEXT('Rate/spread')
     A                                      COLHDG('Rate/spread')
     A            INTR          11P 7       TEXT('Interest Rate')
     A                                      COLHDG('Interest' 'Rate')
     A            SSEQ           1S 0       TEXT('Sort Sequence')
     A                                      COLHDG('Sort' 'Sequence')
     A            CPAM          13P 0       TEXT('Current Principal +
     A                                            Amount')
     A                                      COLHDG('Current' +
     A                                             'Principal Amt')
     A            PACD           5P 0       TEXT('Previous Interest +
     A                                          Accrual Control Date')
     A                                      COLHDG('Prev. Interest' +
     A                                       'Accrual' 'Control Date')
     A            ICBS           1S 0       TEXT('Interest Calculation +
     A                                            Basis')
     A                                      COLHDG('Int. Calc.' +
     A                                            'Basis')
     A            ACOF           2A         TEXT('Account Officer')
     A                                      COLHDG('A/c Officer')
     A            ZZ002          2A         TEXT('Filler')
     A                                      COLHDG('Filler')
     A            FACO          10A         TEXT('For Account of')
     A                                      COLHDG('For A/c of')
     A            PRFC           4A         TEXT('Profit Centre')
     A                                      COLHDG('Profit' +
     A                                             'Centre')
     A            OSBR           3A         TEXT('Our Settlement +
     A                                            Branch')
     A                                      COLHDG('Our Sett.' +
     A                                             'Branch')
     A            BOKC           2A         TEXT('Book Code')
     A                                      COLHDG('Book' 'Code')
     A            PELAP          5P 0       TEXT('Previous Accrual +
     A                                            Profit Date')
     A                                      COLHDG('Prev. Accrual' +
     A                                             'Profit Date')
     A            ORED           5P 0       TEXT('Original Entry +
     A                                            Date')
     A                                      COLHDG('Original' +
     A                                             'Entry Date')
     A            OLNO           6A         TEXT('Original Loan No.')
     A                                      COLHDG('Original' +
     A                                             'Loan No.')
     A            PTFR           4A         TEXT('Portfolio +
     A                                            Reference')
     A                                      COLHDG('Portfolio' +
     A                                             'Ref.')
     A            SCCY           3A         TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
     A            ANUM           6A         TEXT('Agent Customer Number')
     A                                      COLHDG('Agent' +
     A                                             'Customer')
     A            ROLN           6A         TEXT('Receiving/Originating +
     A                                            Loan')
     A                                      COLHDG('Rec/Orig' +
     A                                             'Loan')
     A            GASS           1A         TEXT('Assignment Generate+
     A                                            d Ind.')
     A                                      COLHDG('Ass. Gen. Ind.')
     A            F1FFLG         1A         TEXT('Fee Flag')
     A                                      COLHDG('Fee Flag')
     A            F1FVAL        13P 0       TEXT('Fee Value')
     A                                      COLHDG('Fee Value')
     A            F1FPCT         5S 2       TEXT('Fee Percent')
     A                                      COLHDG('Fee Percent')
     A            F1RACT        20A         TEXT('Receipt - Origin +
     A                                      Account')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Account')
     A            F1RCTY         2A         TEXT('Receipt - Origin +
     A                                      Country')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Country')
     A            F1RBNK        20A         TEXT('Receipt - Origin +
     A                                      Bank')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Bank')
     A            F1RCDE        11A         TEXT('Receipt - Origin +
     A                                      Code')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Code')
     A            F1PACT        20A         TEXT('Pay - Origin +
     A                                      Account')
     A                                      COLHDG('Pay - Origin +
     A                                      Account')
     A            F1PCTY         2A         TEXT('Pay - Origin +
     A                                      Country')
     A                                      COLHDG('Pay - Origin +
     A                                      Country')
     A            F1PBNK        20A         TEXT('Pay - Origin Bank')
     A                                      COLHDG('Pay - Origin Bank')
     A            F1PCDE        11A         TEXT('Pay - Origin Code')
     A                                      COLHDG('Pay - Origin Code')
     A            F1DACT        35A         TEXT('Destination Account')
     A                                      COLHDG('Destination +
     A                                      Account')
     A            F1DCTY         2A         TEXT('Destination Country')
     A                                      COLHDG('Destination' +
     A                                      'Country')
     A            F1DBNK        20A         TEXT('Destination Bank')
     A                                      COLHDG('Destination Bank')
     A            F1SDAT         5P 0       TEXT('Settlement Date')
     A                                      COLHDG('Settlement Date')
     A            SIND           1A         TEXT('Settlement Event Indicator')
     A                                      COLHDG('Settlement' 'Event' +
     A                                           'Indicator')
     A            RIND           1A         TEXT('Rollover Event Indicator')
     A                                      COLHDG('Rollover' 'Event' +
     A                                           'Indicator')
     A            OKEY          10A         TEXT('Originating Account Key')
     A                                      COLHDG('Originating' 'Account' +
     A                                           'Key')
     A            OSAC          18A         TEXT('Our Settlement +
     A                                            Account Number')
     A                                      COLHDG('Our Sett.' +
     A                                             'A/c Number')
     A            AKEY          14A         TEXT('Account Key')
     A                                      COLHDG('Account' 'Key')
     A            AAMT          15P 0       TEXT('Available Amount')
     A                                      COLHDG('Available' 'Amount')
      *
      ** Fields from ACNTnDK record
      *
     A            PAMT          15P 0       TEXT('Principal Amount')
     A                                      COLHDG('Principal' 'Amount')
      *
     A            IAMT          15P 0       TEXT('Interest Amount')
     A                                      COLHDG('Interest' 'Amount')
     A            ABRCA          3A         TEXT('Branch Code')
     A                                      COLHDG('Branch' 'Code')
     A            ACNUM          6A         TEXT('Customer Number')
     A                                      COLHDG('Customer' 'Number')
     A            AVDAT          5P 0       TEXT('Value Date')
     A                                      COLHDG('Value Date')
     A            APRSQ          2S 0       TEXT('Processing Sequence')
     A                                      COLHDG('Processing' 'Seq.')
     A            ALASN          3P 0       TEXT('Loan Amend Sequence')
     A                                      COLHDG('Loan Amend' 'Seq.')
      *
      ** Settlement account
      *
     A            SEBRCA         3A         TEXT('Branch')
     A                                      COLHDG('Branch')
     A            SEACCY         3A         TEXT('Currency')
     A                                      COLHDG('Currency')
     A            SECNUM         6A         TEXT('Customer Number')
     A                                      COLHDG('Customer' 'Number')
     A            SEACOD        10S 0       TEXT('Account Code')
     A                                      COLHDG('Account' 'Code')
     A            SEACSQ         2S 0       TEXT('Account Sequence')
     A                                      COLHDG('Account' 'Sequence')
      **Start of change CLE164
     A**********  SEPCKO         5P 0       TEXT('Lowest PCN among +                          CLE164
     A**********                                  PCN1, PCN2, PCN3')                          CLE164
     A**********                            COLHDG('Lowest PCN among' +                       CLE164
     A**********                                   'PCN1, PCN2, PCN3')                        CLE164
     A            QQPCKO         5P 0       COLHDG('Redundant')
     A                                      TEXT('Redundant')
      **End of change CLE164
     A            SEVDAT         5P 0       TEXT('Value date')
     A                                      COLHDG('Value date')
      **Start of change CLE164
     A**********  SEPCKM         5P 0       TEXT('Module Priority')                           CLE164
     A**********                            COLHDG('Module Priority')                         CLE164
     A            QQPCKM         5P 0       TEXT('Redundant')
     A                                      COLHDG('Redundant')
      **End of change CLE164
     A            SELNRF         6A         TEXT('Transaction Ref')
     A                                      COLHDG('Transaction Ref')
      **Start of change CLE164
      *****************************************************************                       CLE164
      ***Flag*to*identify*if*record*has*been*processed*****************                       CLE164
      *****************************************************************                       CLE164
     A**********  SEFILE         2A         TEXT('File')                                      CLE164
     A**********                            COLHDG('File')                                    CLE164
     A            QQFILE         2A         TEXT('Replaced by SEFILE')
     A                                      COLHDG('Replaced by SEFILE')
      **End of Change CLE164
      *
      ** Flag to identify if record has been processed
      *
     A            IPFL           1A         TEXT('P-Processed +
     A                                            Y-Availab Bal')
     A                                      COLHDG('P-Processed' +
     A                                             'Y-Availab Bal')
      **Start of change 263074
      **Start of Change CLE164
     A**********  SEPCKT         5P 0       TEXT('Transaction Priority')                      CLE164
     A**********                            COLHDG('Transaction Priority')                    CLE164
     A            QQPCKT         5P 0       TEXT('Redundant')
     A                                      COLHDG('Redundant')
      **End of Change CLE164
      ** Start of change AR1056323
     A**********  LNNO           6A         TEXT('Loan Number')                            AR1056323
     A**********                            COLHDG('Loan' 'Number')                        AR1056323
      ** End of change AR1056323
      **End of change 263074
      **Start of Change CLE164
     A            SEFILE        10A         TEXT('File')
     A                                      COLHDG('File')
     A            SECATC         5A         TEXT('Category Code')
     A                                      COLHDG('Category Code')
     A            SEREPR         8  0       TEXT('Repayment Priority')
     A                                      COLHDG('Repayment Priority')
     A            SECAPR         8  0       TEXT('Category Code Default +
     A                                           Repayment Priority')
     A                                      COLHDG('Category' +
     A                                          'Code Default' +
     A                                          'Repayment Priority')
     A            SETREF        11A         TEXT('Transaction Reference')
     A                                      COLHDG('Transaction' +
     A                                           'Reference')
     A            SESDAT         5P 0       TEXT('Start Date')
     A                                      COLHDG('Start Date')
     A            SEMDAT         5P 0       TEXT('Maturity Date')
     A                                      COLHDG('Maturity Date')
     A            SEVDPD         5P 0       TEXT('Value Date of PDCL')
     A                                      COLHDG('Value Date' +
     A                                      'of PDCL')
     A            SEVDPY         5P 0       TEXT('Value Date of Payment')
     A                                      COLHDG('Value Date' +
     A                                      'of Payment')
     A            SEPAMT        15P 0       TEXT('Payment Amount')
     A                                      COLHDG('Payment' +
     A                                      'Amount')
     A            SEOVRD        15P 0       TEXT('Overdraft Amount')
     A                                      COLHDG('Overdraft Amount')
     A            SETRID         8P 0       TEXT('Unique Transaction ID')
     A                                      COLHDG('Unique' 'Transaction ID')
     A            SEOVFL         1A         TEXT('Overdraft Flag')
     A                                      COLHDG('Overdraft' 'Flag')
     A            SETMST          Z         TEXT('Time Stamp')
     A                                      COLHDG('Time Stamp')
      **End of change CLE164
