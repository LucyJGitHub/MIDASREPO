      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas LE PC msg format risk partcpnts/assignments')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PF/LER1MFPD - PC Subsystem Interface                         *
      *                Risk Participants/Assignments Message Formats  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 05Dec05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CLE031             Date 03Feb03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP070             Date 08Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CPK014             Date 20Jun01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU004             Date 02Feb98               *
      *                 CLE004 *Create     Date 08MAY97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE031 - Lending Enhancements.                               *
      *  CAP070 - Conversion of LE inputs into modular structure to   *
      *           use as APIs. Assignments Transaction Details API    *
      *  CPK014 - Change field references to remove multi-layered     *
      *            dependencies; this is for compilation reasons.     *
      *  CEU004 - LE Settlement Ccy Conversion Upgrade to DBA R2.0    *
      *  CLE004 - Customer Lending Enhancements - Syndications        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LER1MFD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            R1MSGT    R               REFFLD(X1MSGT LEX1MFPD)
     A            R1TRUS    R               REFFLD(X1TRUS LEX1MFPD)
     A            R1TRWS    R               REFFLD(X1TRWS LEX1MFPD)
     A            R1TNRF    R               REFFLD(X1TNRF LEX1MFPD)
     A            R1TRSN    R               REFFLD(X1TRSN LEX1MFPD)
     A            R1ACTN    R               REFFLD(X1ACTN LEX1MFPD)
     A            R1TRDS    R               REFFLD(X1TRDS LEX1MFPD)
     A            R1TRTS    R               REFFLD(X1TRTS LEX1MFPD)
      * Start CPK014
     A**********  R1NRBA    R               REFFLD(F1NRBA LEF1MFPD)
     A            R1NRBA    R               REFFLD(NREC)
     A                                      TEXT('Number of records in batch')
     A                                      COLHDG('No.' 'Records' +
     A                                             'in batch')
      * End CPK014
      * Start of change CAP070
     A************R1FIL4    R               REFFLD(ZZ004)
     A            R1RPLC         1A         TEXT('Repair Location')
     A            R1FIL3    R               REFFLD(ZZ003)
      * End of change CAP070
      *
      * Begin /CSD027/
     A**********  R1CNUM    R               REFFLD(CNUM)                                      CSD027
     A            R1CNUM         6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            R1FACT    R               REFFLD(FACT)
     A            R1FCNO    R               REFFLD(FCNO)
      * Begin /CSD027/
     A**********  R1MNUM    R               REFFLD(MNUM LEPARTPD)                             CSD027
     A**********  R1INUM    R               REFFLD(INUM LEPARTPD)                             CSD027
     A            R1MNUM         6A
     A                                      TEXT('Member Customer Number')
     A                                      COLHDG('Member              ' +
     A                                      'Customer')
     A            R1INUM         6A
     A                                      TEXT('Introduced By')
     A                                      COLHDG('Introduced By')
      * End /CSD027/
     A            R1JDTE    R               REFFLD(ZZ008)
     A                                      TEXT('Joining Date')
     A                                      COLHDG('Joining' +
     A                                             'Date')
     A            R1VDTC    R               REFFLD(ZZ008)
     A                                      TEXT('Value Date of This +
     A                                            Change')
     A                                      COLHDG('Value Date' +
     A                                             'of this Chg')
     A            R1SHTP    R               REFFLD(SHTP LEPARTPD)
     A            R1SHPC    R               REFFLD(ZZ016)
     A                                      TEXT('Share Percentage')
     A                                      COLHDG('Share' +
     A                                             'Percentage')
     A            R1SHAM    R               REFFLD(ZZ014)
     A                                      TEXT('Share Amount')
     A                                      COLHDG('Share' +
     A                                             'Amount')
     A            R1FEBR    R               REFFLD(FEBR LEPARTPD)
     A            R1FERS    R               REFFLD(FERS LEPARTPD)
     A            R1FERA    R               REFFLD(ZZ012)
     A                                      TEXT('Fee Rate/Spread')
     A                                      COLHDG('Fee Rate/Spread')
     A            R1DRTP    R               REFFLD(DRTP LEPARTPD)
     A            R1DRST    R               REFFLD(DRST LEPARTPD)
     A            R1PSTM    R               REFFLD(PSTM)
     A**Start of change CGL029
     A**********  R1PONS    R               REFFLD(TNUM)                                      CGL029
     A**********                            TEXT('Pay - Our Nostro')                          CGL029
     A**********                            COLHDG('Pay - Our' +                              CGL029
     A**********                                   'Nostro' )                                 CGL029
     A            QQPONS        15A
     A**End of change CGL029
     A            R1RVNO    R               REFFLD(RVNO)
     A            R1CVMR    R               REFFLD(CVMR)
      * Begin /CSD027/
     A**********  R1POCN    R               REFFLD(POCN)                                      CSD027
     A**********  R1POBN    R               REFFLD(POBN)                                      CSD027
     A            R1POCN         6A
     A                                      TEXT('pay - ordering customer no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'cust')
     A            R1POBN         6A
     A                                      TEXT('pay - ordering bank no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'bank')
      * End /CSD027/
     A            R1RCRN    R               REFFLD(RCRN)
     A            R1RCRA    R               REFFLD(RCRA)
     A            R1PIBN    R               REFFLD(PIBN)
     A            R1PIBA    R               REFFLD(PIBA)
     A            R1AWBN    R               REFFLD(AWBN)
     A            R1AWBA    R               REFFLD(AWBA)
     A            R1BENN    R               REFFLD(BENN)
     A            R1BENA    R               REFFLD(BENA)
     A            R1RSTM    R               REFFLD(RSTM)
     A**Start of change CGL029
     A**********  R1RONS    R               REFFLD(TNUM)                                      CGL029
     A**********                            TEXT('Receipt - Our Nostro')                      CGL029
     A**********                            COLHDG('Receipt - Our Nostro' )                   CGL029
     A            QQRONS        15A
     A**End of change CGL029
      * Begin /CSD027/
     A**********  R1ROCN    R               REFFLD(ROCN)                                      CSD027
     A**********  R1ROBN    R               REFFLD(ROBN)                                      CSD027
     A            R1ROCN         6A
     A                                      TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'cust')
     A            R1ROBN         6A
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'bank')
      * End /CSD027/
     A            R1RIBN    R               REFFLD(RIBN)
     A            R1RIBA    R               REFFLD(RIBA)
     A            R1DTP1    R               REFFLD(ZZ140)
     A                                      TEXT('Details of Payments +
     A                                           1-4')
     A                                      COLHDG('Detls' ' of Pay' +
     A                                             '1-4')
     A            R1DCHG    R               REFFLD(DCHG)
     A            R1BTB1    R               REFFLD(ZZ210)
     A                                      TEXT('Bank to Bank Info +
     A                                           1-6')
     A                                      COLHDG('Bank to' 'Bank' +
     A                                             'Info 1-6')
      * Start CPK014
     A**********  R1PCOB    R               REFFLD(F1PCOB LEF1MFPD)
     A            R1PCOB    R               REFFLD(BRCA)
     A                                      TEXT('PC Originating Branch')
     A                                      COLHDG('PC' 'Branch')
      * End CPK014
      *                                              Begin /CEU004/
     A            R1SCCY    R               REFFLD(CCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Sett. ccy')
     A            R1ICCY    R               REFFLD(CCY)
     A                                      TEXT('IN Currency in +
     A                                            Fld 72')
     A                                      COLHDG('IN Ccy')
      *                                              End   /CEU004/
      **Start of change CLE031
     A            R1REXR        14A         TEXT('Receive Settlement  +
     A                                           Currency Exchange Rate')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange  Rate')
      *
     A            R1REXI         1A         TEXT('Receive Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Receive' 'Settlement'+
     A                                           'Currency' 'Exchange Rate'+
     A                                           'Ind')
      *
     A            R1PSCY         3A         TEXT('Pay Settlement Currency')
     A                                      COLHDG('Pay Settlement' 'Currency')
      *
     A            R1PEXR        14A         TEXT('Pay Settlement Currency +
     A                                           Exchange Rate')
     A                                      COLHDG('Pay Settlement' +
     A                                           'Currency' 'Exchange Rate')
      *
     A            R1PEXI         1A         TEXT('Pay Settlement Currency +
     A                                           Exchange Rate Ind')
     A                                      COLHDG('Pay Settlement' 'Currency'+
     A                                           'Exchange' 'Rate Ind')
      **End of change CLE031
     A**Start of change CGL029
     A            R1PONS        21A         TEXT('Pay - Our Nostro')
     A            R1RONS        21A         TEXT('Receipt - Our Nostro')
     A**End of change CGL029
