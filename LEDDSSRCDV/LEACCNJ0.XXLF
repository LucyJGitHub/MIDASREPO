      *****************************************************************
/*S*D****LFBASE********************************************************                       CLE164
/*E*S****LF_IM*********************************************************                       CLE164
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LF/LEACCNJ0 - Repayment Priority - Non Loan/Fee Postings     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CLE164  *REDUNDANT Date 18Aug14               *
      *  Prev Amend No. CLE134  *CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE164 - CLE134 Enhancement F (Repayment Methodology)        *
      *  CLE134 - Past Due Call Loan Processing                       *
      *                                                               *
      *****************************************************************
     A                                      JDFTVAL DYNSLT
     A          R RSACMTPO                  JFILE(RSACMTPD LEACCNPD)
      *
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(BRCA RPBRCA)
     A                                      JFLD(CUSN RPCNUM)
     A                                      JFLD(CCYD RPACCY)
     A                                      JFLD(ACDE RPACOD)
     A                                      JFLD(ASNC RPACSQ)
      *
      ** Settlement Account
      *
     A            XTBRCA                    RENAME(BRCA)
     A            XTCNUM                    RENAME(CUSN)
     A            XTACCY                    RENAME(CCYD)
     A            XTACOD                    RENAME(ACDE)
     A            XTACSQ                    RENAME(ASNC)
      *
      ** Extension Reference
      *
     A            XTXRFI                    RENAME(XRFI)
     A            XTXRFN                    RENAME(XRFN)
      *
      ** Shadow Posting Details
      *
     A            XTVUDT                    RENAME(VUDT)
     A            XTTRYP                    RENAME(TRYP)
      *
     A            XTMVAM                    RENAME(MVAM)
     A            XTDORC                    RENAME(DORC)
      *
      ** Key Details
      *
     A          K XTBRCA
     A          K XTCNUM
     A          K XTACCY
     A          K XTACOD
     A          K XTACSQ
     A          K XTXRFI
     A          K XTXRFN
     A          K XTVUDT
     A          K XTTRYP
     A          K XTMVAM
     A          K XTDORC
      *
      ** Record Selection
      *
     A          O XTXRFI                    VALUES('CLI' 'FEE')
      *****************************************************************
