      *****************************************************************
/*S*D****LFBASE********************************************************                       CLE164
/*E*S****LF_IM*********************************************************                       CLE164
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LF/LEACCNJ3 - Repayment Priority - Fee Shadow Postings       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CLE164  *REDUNDANT Date 18Aug14               *
      *  Prev Amend No. CLE134  *CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE164 - CLE134 Enhancement F (Repayment Methodology)        *
      *  CLE134 - Past Due Call Loan Processing                       *
      *                                                               *
      *****************************************************************
     A                                      JDFTVAL DYNSLT
     A          R LEACCND0                  JFILE(LEACCNPD RSACMTPD)
      *
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(RPBRCA BRCA)
     A                                      JFLD(RPCNUM CUSN)
     A                                      JFLD(RPACCY CCYD)
     A                                      JFLD(RPACOD ACDE)
     A                                      JFLD(RPACSQ ASNC)
     A                                      JFLD(RPXRFI XRFI)
     A                                      JFLD(RPXRFN XRFN)
      *
      ** Settlement Account
      *
     A            XTBRCA                    RENAME(RPBRCA)
     A            XTCNUM                    RENAME(RPCNUM)
     A            XTACCY                    RENAME(RPACCY)
     A            XTACOD                    RENAME(RPACOD)
     A            XTACSQ                    RENAME(RPACSQ)
      *
      ** Pre-check Order
      *
     A            XTPCKO                    RENAME(RPPCKO)
      *
      ** Loan
      *
     A            XTLNRF                    RENAME(RPFLNR)
     A            XTLCBR                    RENAME(RPLCBR)
     A            XTLCNU                    RENAME(RPFCNM)
     A            XTLTYP                    RENAME(RPLTYP)
     A            XTSUTP                    RENAME(RPSUTP)
     A            XTLCLS                    RENAME(RPLCLS)
      *
      ** Fee
      *
     A            XTFSEQ                    RENAME(RPFSE1)
     A            XTFCOD                    RENAME(RPFCOD)
      *
      ** Facility
      *
     A            XTFCBR                    RENAME(RPFFCB)
     A            XTFCNU                    RENAME(RPFFCN)
     A            XTFCTY                    RENAME(RPFFAC)
     A            XTFCSQ                    RENAME(RPFFNO)
      *
      ** Extension Reference
      *
     A            XTXRFI                    RENAME(RPXRFI)
     A            XTXRFN                    RENAME(RPXRFN)
      *
      ** Shadow Posting Details
      *
     A            XTVUDT                    RENAME(VUDT)
     A            XTTRYP                    RENAME(TRYP)
     A            XTMVAM                    RENAME(MVAM)
     A            XTDORC                    RENAME(DORC)
      *
      ** Key Details
      *
     A          K XTBRCA
     A          K XTCNUM
     A          K XTACCY
     A          K XTACOD
     A          K XTACSQ
     A          K XTPCKO
     A          K XTXRFI
     A          K XTXRFN
      *
      ** Record Selection
      *
     A          S XTXRFI                    VALUES('FEE')
      *****************************************************************
