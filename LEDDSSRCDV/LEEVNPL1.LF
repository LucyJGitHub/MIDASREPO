      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas LE Princ movmts, Int payments & Rate changes')   *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LF/LEEVNPL1 - Pricipal movements, Interest payemnts and      *
      *                Rate changes file.                             *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE154             Date 06Aug12               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 232628  *CREATE    Date 14Mar05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE154 - Loan Repayment Schedule Enhancement (Recompile)     *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CSD027 - Conversion of Customer Number to alpha.             *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  232628 - CAS009 Rollover patch.                              *
      *                                                               *
      *****************************************************************
     A          R LVENTELF                  PFILE(LVENTEL)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            E1LNRF                    RENAME(LNRF)
     A            E1CUNR                    RENAME(CUNR)
     A            E1ETYP                    RENAME(ETYP)
     A            E1EDAT                    RENAME(EDAT)
     A            E1EAMT                    RENAME(EAMT)
     A            E1ECCY                    RENAME(ECCY)
     A            E1INOI                    RENAME(INOI)
     A            E1AUTO                    RENAME(AUTO)
     A            E1IBRE                    RENAME(IBRE)
      *
     A          K E1LNRF
     A          K E1EDAT
      ** Inter-branch
     A          O E1IBRE                    VALUES('Y')
      ** Auto Settle
     A          S E1AUTO                    VALUES('Y')
      ** Principal Increase
     A            E1ETYP                    VALUES('61V3' '62V3' '64V3' '68V3' +
     A                                      '66V3' '69V3' +
      ** Repayment Schedule
     A                                      '61R1' '62R1' '64R1' '68R1' +
     A                                      '61R2' '62R2' '64R2' '68R2' +
     A                                      '66R1' '69R1' '66R2' '69R2' +
      ** Repayment
     A                                      '62R9' '63R9' '64R9' '65R9' +
     A                                      '66R9' '67R9' '69R9' +
      ** Draw
     A                                      '62V7' '63V7' '64V7' '65V7' +
     A                                      '66V7' '67V7' '69V7' +
      ** Transfer from
     A                                      '61R7' '62R7' '63R7' '64R7' +
     A                                      '65R7' '68R7' +
     A                                      '66R7' '67R7' '69R7' +
      ** Transfer to
     A                                      '61V5' '62V5' '63V5' '64V5' +
     A                                      '65V5' '68V5' +
     A                                      '66V5' '67V5' '69V5' +
      ** Interest
     A                                      '61I2' '62I2' '64I2' '68I2' +
     A                                      '66I2' '69I2' +
      ** Maturity Interest
     A                                      '61M2' '62M2' '64M2' '68M2' +
     A                                      '66M2' '69M2')
      *
     A          R LOAMSDKF                  PFILE(LESCAMPD)
      *
     A            E1LNRF                    RENAME(LNRF)
     A            E1EDAT                    RENAME(VDAT)
     A            E1AMTP                    RENAME(AMTP)
     A            E1BRTT                    RENAME(BRTT)
     A            E1RTSP                    RENAME(RTSP)
      *
     A          K E1LNRF
     A          K E1EDAT
