      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas LE History / Action Rate file')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LF/LEHISTLJ - LOANS HISTORY FILE / ACTION FILE               *
      *                                                               *
      *  (c) Misys Banking 2015                                       *
      *                                                               *
      *  Last Amend No. MD031736           Date 02Mar15               *
      *  Prev Amend No. MDnnnnnn *Create   Date ddMmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD031736 - Incorrect accrued interest amount due to wrong    *
      *             NIAD. Update NIAD with VDAT of the latest event   *
      *             from ACTN3DK/HISTSHQ/HISTSHP. Patterned fix after *
      *             AR1045621.                                        *
      *                                                               *
      *****************************************************************
     A          R HISTSHPF                  PFILE(HISTSHP)
      *
     A            RECI
     A            LNRF
     A            VDAT
     A            AMTP
      *
     A          K LNRF
     A          K VDAT
      *
     A          S RECI                      COMP(EQ 'D')
     A          O AMTP                      VALUES('LS' 'MI')
      *
     A          R HISTSHQF                  PFILE(HISTSHQ)
     A            RECI
     A            LNRF
     A            VDAT
     A            AMTP
      *
     A          K LNRF
     A          K VDAT
      *
     A          S RECI                      COMP(EQ 'D')
      *
     A          R ACTN3DKF                  PFILE(ACTN3DK)
     A            RECI
     A            LNRF
     A            VDAT
     A            AMTP
     A            ACTI
     A            BRTT
     A            CCY
      *
     A          K LNRF
     A          K VDAT
      *
     A          S RECI                      COMP(EQ 'D')
     A          S ACTI                      COMP(EQ 'Y')
     A          O AMTP                      VALUES('LS' 'MI' 'PD')
      *
