      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas LE History / Action Rate file')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LF/LEHISTLJ - LOANS HISTORY FILE / ACTION FILE               *
      *                                                               *
      *  (c) Finastra International Limited 2015                      *
      *                                                               *
      *  Last Amend No. MD060560           Date 10Dec22               *
      *  Prev Amend No. CLE172             Date 01Oct20               *
      *                 CSD103             Date 10Aug20               *
      *                 MD046248           Date 27Oct17               *
      *                 CLE154             Date 27Jul16               *
      *                 MD031736           Date 02Mar15               *
      *                 MDnnnnnn *Create   Date ddMmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD060560 - After a spread change and a base rate code change *
      *             on loans, the system stopped accruing. Amend Omit *
      *             selection to correctly filter records             *
      *  CLE172 - LIBOR Deregulation - Lending (Recompile)            *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *             (Recompile)                                       *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE154 - Loan Repayment Schedule Enhancement (Recompile)     *
      *  MD031736 - Incorrect accrued interest amount due to wrong    *
      *             NIAD. Update NIAD with VDAT of the latest event   *
      *             from ACTN3DK/HISTSHQ/HISTSHP. Patterned fix after *
      *             AR1045621.                                        *
      *                                                               *
      *****************************************************************
     A          R HISTSHPF                  PFILE(HISTSHP)
      *
     A            RECI
     A            LNRF
     A            VDAT
     A            AMTP
      *
     A          K LNRF
     A          K VDAT
      *
     A**********S RECI                      COMP(EQ 'D')                                    MD060560
     A          O AMTP                      VALUES('LS' 'MI')
     A          S RECI                      COMP(EQ 'D')                                    MD060560
      *
     A          R HISTSHQF                  PFILE(HISTSHQ)
     A            RECI
     A            LNRF
     A            VDAT
     A            AMTP
      *
     A          K LNRF
     A          K VDAT
      *
     A          S RECI                      COMP(EQ 'D')
      *
     A          R ACTN3DKF                  PFILE(ACTN3DK)
     A            RECI
     A            LNRF
     A            VDAT
     A            AMTP
     A            ACTI
     A            BRTT
     A            CCY
      *
     A          K LNRF
     A          K VDAT
      *
     A**********S RECI                      COMP(EQ 'D')                                    MD060560
     A**********S ACTI                      COMP(EQ 'Y')                                    MD060560
     A          O AMTP                      VALUES('LS' 'MI' 'PD')
     A          S RECI                      COMP(EQ 'D')                                    MD060560
     A            ACTI                      COMP(EQ 'Y')                                    MD060560
      *
