      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas Prfty Loan Extract Joined LogFile')              *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*  LF/PELNJL0  - PROFITABILITY LOAN EXTRACT JOINED LOGICAL.     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CLE042             Date 06Sep05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 FS0058             Date 06APR92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
     A*  FS0058 - Order by facility customer followed by Customer.    *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A                                      JDFTVAL
     A          R PELNJL0F
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      JFILE(LEPELND LEPEMGD LEPEFLD)
     A          J                           JOIN(LEPELND LEPEMGD)
     A                                      JFLD(LDCNUM MDCNUM)
     A                                      JFLD(LDLNRF MDLNRF)
     A          J                           JOIN(LEPELND LEPEFLD)
     A                                      JFLD(LDCNUM FLDCNM)
     A                                      JFLD(LDLNRF FLDLON)
     A*
     A            BRCA                      JREF(1)
     A            LDCNUM
     A            LDLNRF
     A            LDLTYP
     A            LDSUTP
     A            LDFCUS
     A            LDFTYP
     A            LDFSEQ
     A            LDGPID
     A            LDGPNA
     A            LDDEPT
     A            LDACOF
     A            LDSTDY
     A            LDENDY
     A            LDLSDY
     A            LDLEDY
     A            LDJANA
     A            LDFEBA
     A            LDMARA
     A            LDAPRA
     A            LDMAYA
     A            LDJUNA
     A            LDJULA
     A            LDAUGA
     A            LDSEPA
     A            LDOCTA
     A            LDNOVA
     A            LDDECA
     A            LDOACO
     A*
     A            MDCNUM
     A            MDLNRF
     A            MDJANA
     A            MDFEBA
     A            MDMARA
     A            MDAPRA
     A            MDMAYA
     A            MDJUNA
     A            MDJULA
     A            MDAUGA
     A            MDSEPA
     A            MDOCTA
     A            MDNOVA
     A            MDDECA
     A            MDADJA
     A*
     A            FLDCNM
     A            FLDLON
     A            FLDJAN
     A            FLDFEB
     A            FLDMAR
     A            FLDAPR
     A            FLDMAY
     A            FLDJUN
     A            FLDJUL
     A            FLDAUG
     A            FLDSEP
     A            FLDOCT
     A            FLDNOV
     A            FLDDEC
     A*                                              Begin /CLE042/
     A            LDCLAS
     A*                                              End   /CLE042/
     A*
     A          K LDGPID
     A          K LDDEPT
     A          K LDACOF
     A**********             START OF CHANGE      /FS0058/
     A          K LDFCUS
     A**********             END OF CHANGE        /FS0058/
     A          K LDCNUM
     A*
