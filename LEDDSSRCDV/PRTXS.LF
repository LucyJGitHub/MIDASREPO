      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas LE Pay/receive telexes extract file')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module                              *
     A*                                                               *
     A*  LF/PRTXS     PAY/RECEIVE TELEXES EXTRACT FILE                *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 230521             Date 28Oct04               *
      *                 TDA099             Date 29Mar04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 156586             Date 04Mar99               *
      *                 CEU004             Date 02Feb98               *
     A*                 S01518             DATE 03Oct94               *
     A*                 E45727             DATE 19Oct92               *
     A*                 S01117             DATE 02OCT87               *
     A*                 S01117             DATE 26MAR87               *
     A*                 S0480              DATE 14JUL83               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  CLE042 - Extended Loan Sub Type                              *
     A*  230521 - Added Field for PF/LEPXTAB. Applied fix 215643      *
      *  TDA099 - TDA COB Errors                                      *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
     A*  156586 - Exclude zero-amount records from LETX3PR (cf. STX3) *
     A*  CEU004 - LE Settlement Ccy Conversion Upgrade to DBA R2.0    *
     A*           Recompiled due to changes in file LETX1PR, LETX2PR, *
     A*           LETX3PR and LEPXTAB.                                *
     A*  S01518 - BLI Lending Swift Enhancements.                     *
     A*           Recompiled due to amended PF/LEPXTAB.               *
     A*  E45727 - Recompile as LETX3PR changed to include fee sequence*
     A*  S01117 - MULTIBRANCHING                                      *
     A*                                                               *
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *****************************************************************
     A          R LETX1HHF                  PFILE(LETX1HH)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A          K RECI
     A          K *NONE
     A          R LETX2HHF                  PFILE(LETX2HH)
     A          K RECI
     A          K *NONE
     A          R LETX3HHF                  PFILE(LETX3HH)
     A          K RECI
     A          K *NONE
     A          R LEPXTAAF                  PFILE(LEPXTAA)
     A            RECI
     A          K RECI
     A          K *NONE
     A          R LETX1PRF                  PFILE(LETX1PR)
     A          K *NONE
     A***                          /S01117/
     A          K BRCA
     A***                          /S01117/
     A          K FCLN
     A          K LASN
     A          K VDAT
     A          K *NONE
     A          R LETX2PRF                  PFILE(LETX2PR)
     A          K *NONE
     A***                          /S01117/
     A          K BRCA
     A***                          /S01117/
     A          K FCLN
     A          K LASN
     A          K VDAT
     A          K *NONE
     A          R LETX3PRF                  PFILE(LETX3PR)
     A          K *NONE
     A***                          /S01117/
     A          K BRCA
     A***                          /S01117/
     A          K FCLN
     A          K LASN
     A          K VDAT
     A          K *NONE
      *                                              Begin /156586/
     A          S EAMT                      COMP(NE 0)
      *                                              End   /156586/
     A          R LEPXTABF                  PFILE(LEPXTAB)
     A
     A            RECI
     A            PYRC
     A            SETP
     A            CCY
      **Start of change TDA099
     A**********  OSAC                                                                        TDA099
     A            OSACQQ
      **End of change TDA099
     A            VDAT
     A            TSEN
     A            CNUM
     A            FCLN                      RENAME(LNRFA)
     A            EAMT
     A            RCDT
     A            AMTP
     A            LASNS
     A            FACO
     A            SPI                       CONCAT(SPI1 SPI2 SPI3)
     A            PRI1
     A            PRI2
     A            PRI3
     A            PRI4
     A            BICN
     A            LTYP
     A            SUTP
     A            PTYP
     A            TIND
     A*                                            /S01117/
     A***         BRCD
     A            BRCA
     A*                                            /S01117/
     A            INTC
     A            ZZ002
      **Start of change TDA099
     A            ZZ001
     A            ZZ001A
     A            ZZ028
     A            OSAC
      **End of change TDA099
     A*                                              Begin /CLE042/
     A            CLAS
     A*                                              End   /CLE042/
      **Start of change 230521
     A            LFCN
      **End of change 230521
     A          K *NONE
     A***                          /S01117/
     A          K BRCA
     A***                          /S01117/
     A          K LNRFA
     A          K LASNS
     A          K VDAT
     A          K *NONE
     A          R LETX1ZZF                  PFILE(LETX1ZZ)
     A          K RECI
     A          K *NONE
     A          R LETX2ZZF                  PFILE(LETX2ZZ)
     A          K RECI
     A          K *NONE
     A          R LETX3ZZF                  PFILE(LETX3ZZ)
     A          K RECI
     A          K *NONE
     A          R LEPXTACF                  PFILE(LEPXTAC)
     A            RECI
     A            NORE
     A            HRWN
     A            HRDC
     A          K RECI
     A          K *NONE
