      *****************************************************************
/*STD *  PRTFAU                                                       *
/*EXI *  TEXT('Midas LE Initialisation program - Audit file')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000012AU - Audit Report for initialisation program    *
      *                    LE000012                                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
      *
     A                                     6'REFERENCE LE000012AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                    +1PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'INITIALIZE LENDING MODULE +
     A                                      TRANSACTIONS - AUDIT REPORT'
      *
     A                                      SPACEA(1)
     A                                    40'--------------------------+
     A                                      ---------------------------'
     A                                      SPACEA(3)
      *
      *----------------------*
      ***  File Controls     *
      *----------------------*
      *
     A          R CONTROL
     A                                    84'CALCULATED'
     A                                      SPACEA(1)
     A                                    84'----------'
     A                                      SPACEA(1)
      *
     A                                     6'TOTAL NUMBER OF LIVE NON-GUARA+
     A                                      NTEE ISSUED LOANS IN CUSTOMER +
     A                                      LOANS FILE'
      *
     A            COUNT1         9  0     85EDTCDE(3) SPACEA(2)
      *
     A                                     6'NO. OF RECORDS UPDATED IN +
     A                                      FILE CLOANCL:'
     A            COUNT2         9  0     85EDTCDE(3) SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS                    SPACEB(3)
     A                                    52'*** No Details to Report ***'
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(3)
      *
      *----------------------*
      ***  Database Error    *
      *----------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
