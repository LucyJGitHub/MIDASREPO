      *****************************************************************
/*STD *  PRTFAU                                                       *
/*EXI *  TEXT('Midas LE Initialisation program(CAS016)-Audit file')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000021AU - Audit Report for initialisation program    *
      *                    LE000021                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CAS016  *CREATE    Date 28Feb06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
      *
     A                                      SKIPB(3)
      *
     A                                     6'REFERENCE LE000021AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                    +1PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    34'IAS18 EIR Recalculation File +
     A                                      Initialisation - Audit Report'
      *
     A                                      SPACEA(1)
     A                                    34'-----------------------------+
     A                                      -----------------------------'
     A                                      SPACEA(3)
      *
      *----------------------*
      ***  File Controls     *
      *----------------------*
      *
     A          R CONTROL
     A                                    84'CALCULATED'
     A                                      SPACEA(1)
     A                                    84'----------'
     A                                      SPACEA(1)
      *
     A                                     6'NO. OF RECORDS INSERTED IN +
     A                                      PF/LECFLSPD        :'
     A            WCOUNTCF       9  0     85EDTCDE(3) SPACEA(2)
      *
     A                                     6'NO. OF RECORDS UPDATED IN +
     A                                      PF/LEFEED           :'
     A            WCOUNTFE       9  0     85EDTCDE(3) SPACEA(2)
      *
     A                                     6'NO. OF RECORDS UPDATED IN +
     A                                      PF/CLOANCL          :'
     A            WCOUNTCL       9  0     85EDTCDE(3) SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS                    SPACEB(3)
     A                                    52'*** No Details to Report ***'
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(3)
      *
      *----------------------*
      ***  Database Error    *
      *----------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
