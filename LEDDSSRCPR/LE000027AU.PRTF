      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas LE CLE169 Take-on Program Audit Report')         *
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  PRTF/LE000027AU - CLE169 Take-on Program Audit Report        *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. MD052994  *CREATE  Date 25Nov19               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD052994 - CL Facility event not generated for retail        *
      *             account debit capitalised interests               *
      *                                                               *
      *****************************************************************
      **  Report Header Record Format
     A          R HEADAU                    TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited 2019')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE000027AU'
     A                                   101'DATE'
     A            BJMRDT         7       106TEXT('RUNDATE')
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    39'RETAIL ACCOUNTS FOR FACILITY HISTO-
     A                                      RY REBUILD AUDIT'
      *
     A                                    35'-----------------------------------
     A                                      ------------------------'
     A                                      SPACEB(1)
      **  File Control
     A          R CONTROLAU
     A                                    31'Total Number of Retail Accounts Li-
     A                                      nked to Facility:'
     A                                      SPACEB(3)
     A            RREAD          6S 0     85EDTCDE(3)
     A                                    21'TotaL Number of Retail Accounts fo-
     A                                      r Facility History Rebuild:'
     A                                      SPACEB(2)
     A            RRBLD          6S 0     85EDTCDE(3)
      **  Database Error
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE         '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
