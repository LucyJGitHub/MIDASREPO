      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE CLE169 Take-on Program Detail Report')        *
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  PRTF/LE000027P1 - CLE169 Take-on Program Detail Report       *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. MD052994  *CREATE  Date 25Nov19               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD052994 - CL Facility event not generated for retail        *
      *             account debit capitalised interests               *
      *                                                               *
      *****************************************************************
      ** Report Header Record Format
     A          R LEHEADP1                  TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited 2019')
      *
     A                                      SKIPB(2)
      *
     A                                     6'REFERENCE LE000027P1'
     A                                      SPACEB(1)
     A                                   101'DATE'
     A            BJMRDT         7       106TEXT('RUNDATE')
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    41'RETAIL ACCOUNTS FOR FACILITY HISTO-
     A                                      RY REBUILD'
     A                                      SPACEA(1)
      *
     A                                    40'-----------------------------------
     A                                      ------------'
     A                                      SPACEA(1)
     A                                    54'Facility '
     A                                    74'Last Facility'
     A                                    97'Facility'
     A                                      SPACEA(1)
     A                                    18'Retail Account Number'
     A                                    49'Customer'
     A                                    58'Type'
     A                                    63'No.'
     A                                    75'Change Date'
     A                                    96'Change Date'
     A                                       SPACEA(2)
      **  Detail Record Format
     A          R LEDETLP1
     A            RACNO         10  0     23
     A            RFCCU          6        49
     A            RFACT          3  0     58
     A            RFCNO          2  0     63
     A            RLFCD          7A       77
     A            RFCHD          7A       98SPACEA(1)
      **  Report Trailer
     A          R TRAILP1                   SPACEB(3)
     A                                    51'*** END OF REPORT ***'
