      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Non-RVCR Facility Update - Printer File')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000031P1 - Non-Revolving Credit Facility Update for   *
      *                    Maturing Money Market Loans - Printer      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      *  Prev Amend No. BUG3282 *CREATE    Date 17Jun04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG3282 - Non-RVCR facility amount reduction for maturing    *
      *            MM loans                                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R LE000031H1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SKIPB(3)
     A                                     6'Reference'
     A                                    16'LE000031P1'
     A            BJURPT        53        30
     A                                   100'Date'
     A            BJMRDT         7       105
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    21'Report of Facility Reduction-
     A                                      s due to MM Maturity Today'
     A
     A N02                                  SPACEA(1)
     A
     A  02                                81'Branch'
     A  02        BRCA      R             88
     A  02        BRNM      R             92
     A  02                                  SPACEA(1)
     A                                    21'-----------------------------
     A                                      --------------------------'
     A                                      SPACEA(1)
     A
     A                                     5'Customer'
     A
     A                                    18'Facility'
     A                                    +2'Currency'
     A                                    38'Old Facility Amount'
     A                                    59'Reduced Facility Amt.'
     A                                    96'Diff.'
     A
     A                                      SPACEA(1)
     A
     A                                     5'--------'
     A                                    18'--------'
     A                                    +2'--------'
     A                                    38'-------------------'
     A                                    59'---------------------'
     A                                    96'-----'
     A
     A                                      SPACEA(1)
     A
     A          R LE000031D1                SPACEA(1)
     A
      * Begin /CSD027/
     A**********  CNUM           6  0      6                                                  CSD027
     A            CNUM           6A        6
      * End /CSD027/
     A            FACT           3  0     18
     A                                    21'/'
     A            FCNO           2  0     22
     A            FCCY           3        30
     A     03     OFAMT         13  0     40EDTWRD(' ,   ,   ,   , 0 -')
     A     04     OFAM1         13  1     40EDTWRD('   ,   ,   , 0 . -')
     A     05     OFAM2         13  2     40EDTWRD('  ,   ,   , 0 .  -')
     A     06     OFAM3         13  3     40EDTWRD(' ,   ,   , 0 .   -')
     A     03     RFAMT         13  0     62EDTWRD(' ,   ,   ,   , 0 -')
     A     04     RFAM1         13  1     62EDTWRD('   ,   ,   , 0 . -')
     A     05     RFAM2         13  2     62EDTWRD('  ,   ,   , 0 .  -')
     A     06     RFAM3         13  3     62EDTWRD(' ,   ,   , 0 .   -')
     A     03     DFAMT         13  0     84EDTWRD(' ,   ,   ,   , 0 -')
     A     04     DFAM1         13  1     84EDTWRD('   ,   ,   , 0 . -')
     A     05     DFAM2         13  2     84EDTWRD('  ,   ,   , 0 .  -')
     A     06     DFAM3         13  3     84EDTWRD(' ,   ,   , 0 .   -')
     A
     A          R LE000031T1                SPACEB(3)
     A N02                                56'*** END OF REPORT ***'
     A  02                                51'*** END OF REPORT FOR BRANCH ***'
     A
