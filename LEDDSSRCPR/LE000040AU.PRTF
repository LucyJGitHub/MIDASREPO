      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas LE Financial Loan Diary Events Extract - AU')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000040AU - Financial Loan Diary Events Extract -      *
      *                    Collateral Events - Audit                  *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE025  *CREATE    Date 20Oct03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE025 - Credit Lines                                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE000040AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    33'FINANCIAL LOAN DIARY EVENTS +
     A                                      EXTRACT (COLLATERAL EVENTS) +
     A                                      - FILE CONTROLS'
     A                                      SPACEA(1)
      *
     A                                    33'----------------------------+
     A                                      ----------------------------+
     A                                      ---------------'
     A                                      SPACEA(3)
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
     A          R CONTROL
     A                                     6'LVEDIF1 - RUN CONTROLS'
     A                                    50'NUMBER'
     A                                    75'VALUE'
     A                                      SPACEA(1)
      *
     A                                     6'----------------------'
     A                                    48'OF RECORDS'
     A                                    72'OF RECORDS'
     A                                      SPACEA(3)
      *
     A                                     7'TOTAL RECORDS ON FILE'
      *
     A            RLNOREC        5  0     51EDTCDE(Z)
     A            RLVALUE       15  0     68EDTCDE(Z)
     A            RLVALDC        3  0     83
     A                                      SPACEA(2)
      *
     A                                     6'GLCOEVPD - RUN CONTROLS'
     A                                      SPACEA(1)
      *
     A                                     6'----------------------'
     A                                      SPACEA(3)
      *
     A                                     7'TOTAL RECORDS ON FILE'
      *
     A            RGNOREC        5  0     51EDTCDE(Z)
     A            RGVALUE       15  0     71EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
