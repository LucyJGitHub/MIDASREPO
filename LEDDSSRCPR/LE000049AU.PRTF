      *****************************************************************
/*STD *  PRTFAU                                                       *
/*EXI *  TEXT('Midas LE CLE025 Agg. Facility Initialisation-Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000049AU - CLE025 Aggregated Facility Initialisation  *
      *                    Audit Report                               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE041  *CREATE    Date 11Oct04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE041 - Collateralised Lending Phase 2B                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A                                      SKIPB(3)
      *
     A                                     6'REFERENCE LE000049AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                    +1PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    39'AGGREGATE FACILITIES INIT+
     A                                      IALISATION - AUDIT REPORT'
      *
     A                                      SPACEA(1)
     A                                    39'-------------------------+
     A                                      -------------------------'
     A                                      SPACEA(3)
      *
      *----------------------*
      ***  File Controls     *
      *----------------------*
      *
     A          R CONTROL
     A                                    84'INITIALISED'
     A                                      SPACEA(1)
     A                                    84'----------'
     A                                      SPACEA(1)
      *
     A                                     6'Aggregate Facilities +
     A                                      Header file (PF/LEAGHHPD)'
     A            WLEAGHHPD      3        88SPACEA(2)
      *
     A                                     6'Aggregate Facilities +
     A                                      Trailer file (PF/LEAGFZPD)'
     A            WLEAGFZPD      3        88SPACEA(2)
      *
     A                                     6'Aggregate Facility +
     A                                      Amendment transaction +
     A                                      trailer (PF/LEAGAMZZ)'
     A            WLEAGAMZZ      3        88SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS                    SPACEB(3)
     A                                    52'*** No Details to Report ***'
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(3)
      *
      *----------------------*
      ***  Database Error    *
      *----------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
