      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Aggr. Facility Reductions for Repayments')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000051P1 - Aggregated Facility Reductions for         *
      *                    Repayments Report                          *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE041  *CREATE    Date 11Oct04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE041 - Collateralised Lending Phase 2B                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE000051P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
      *
     A                                    25'AGGREGATED FACILITIES +
     A                                       REDUCTION DUE TO REPAYMENTS +
     A                                       TODAY'
      *
     A  37                                +3'BRANCH -'
     A  37        A8BRCD    R             +1REFFLD(BRCA)
     A  37        A8BRNM    R             +2REFFLD(BRNM)
      *
     A                                    +1' '
     A                                      SPACEA(1)
     A                                    25'----------------------+
     A                                       ----------------------------+
     A                                       -----'
     A                                      SPACEA(2)
     A                                     7'Customer'
     A                                    17'Facility'
     A                                    27'Currency'
     A                                    38'Old Facility Amount'
     A                                    60'Reduced Facility Amount'
     A                                    93'Difference'
     A                                      SPACEA(1)
     A                                     7'--------'
     A                                    17'--------'
     A                                    27'--------'
     A                                    37'---------------------'
     A                                    60'-----------------------'
     A                                    87'---------------------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            R1CUST         6         8
     A            R1FCLT         6        18
     A            R1CURR         3        29
     A            R1OAMT        21        37
     A            R1RAMT        21        61
     A            R1DIFF        21        87
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH '
     A                                      SPACEB(3)
     A  37        A8BRCD         3        76
     A  37                                81'***'
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
