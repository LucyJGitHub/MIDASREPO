      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Aggregated Facility Amendments Report')       *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000052P1 - Aggragated Facility Amendments Processing  *
      *                    Actions Today Report                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE041  *CREATE    Date 11Oct04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,LEH00388                                 *
      *             WNCPYSRC,LEH00389                                 *
      *             WNCPYSRC,LEH00390                                 *
      *             WNCPYSRC,LEH00391                                 *
      *             WNCPYSRC,LEH00392                                 *
      *             WNCPYSRC,LEH00393                                 *
      *  CLE041 - Collateralised Lending Phase 2B                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE000052P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    23'AGGREGATED FACILITY AMEN+
     A                                       DMENTS PROCESSING - ACTIONS TODAY'
     A                                   + 3'BRANCH - '
     A            RBRCA          3       + 1
     A            RBRNM         30       + 2
     A                                      SPACEA(1)
      *
     A                                    23'------------------------+
     A                                      ---------------------------------'
     A                                      SPACEA(3)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     6'Customer Name/Number'
     A                                    32'Facility'
     A*/COPY WNCPYSRC,LEH00388
     A                                    47'Currency'
     A*/COPY WNCPYSRC,LEH00389
     A                                    62'Value Date'
     A                                    88'Action'
     A                                   116'Details'
     A                                      SPACEA(1)
     A                                     6'--------------------'
     A                                    32'--------'
     A*/COPY WNCPYSRC,LEH00390
     A                                    47'--------'
     A*/COPY WNCPYSRC,LEH00391
     A                                    62'----------'
     A                                    79'------------------------'
     A                                   107'------------------------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A            RCNAME        10         7
     A            RCNUM          6        19
     A            RFCLT          3        33
     A                                    36'/'
     A            RFCNO          2        37
     A*/COPY WNCPYSRC,LEH00392
     A            RCCY           3        49
     A*/COPY WNCPYSRC,LEH00393
     A            RVDAT          7        63
     A            RACTN         24        79
     A            RDTLS         24       107
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
