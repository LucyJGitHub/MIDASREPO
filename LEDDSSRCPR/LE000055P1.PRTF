      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Aggregate Facility History Update Report')    *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000055P1 - Aggragate Facility History Update Report   *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CLE041  *CREATE    Date 11Oct04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,LEH00063                                 *
      *             WNCPYSRC,LEH00064                                 *
      *             WNCPYSRC,LEH00073                                 *
      *             WNCPYSRC,LEH00065                                 *
      *             WNCPYSRC,LEH00066                                 *
      *             WNCPYSRC,LEH00067                                 *
      *  CLE041 - Collateralised Lending Phase 2B                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE000055P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    42'AGGREGATE FACILITY HISTORY +
     A                                       UPDATE REPORT'
     A                                   + 3'BRANCH - '
     A            RBRCA          3       + 1
     A            RBRNM         30       + 2
     A                                      SPACEA(1)
      *
     A                                    42'---------------------------+
     A                                       -------------'
     A                                      SPACEA(3)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                    45'------HISTORY-------'
     A                                    70'------FACILITY------'
     A                                      SPACEA(1)
     A                                     6'Customer'
     A                                    19'Facility'
     A*/COPY WNCPYSRC,LEH00063
     A                                    32'Currency'
     A*/COPY WNCPYSRC,LEH00064
     A*/COPY WNCPYSRC,LEH00073
     A                                    47'Facility Amount'
     A                                    72'Facility Amount'
     A                                    95'Status'
     A                                      SPACEA(1)
     A                                     6'--------'
     A                                    19'--------'
     A*/COPY WNCPYSRC,LEH00065
     A                                    32'--------'
     A                                    45'--------------------'
     A                                    70'--------------------'
     A                                    95'------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A            RCNUM          6         7
     A            RFCLT          3        20
     A                                    23'/'
     A            RFCNO          2        24
     A*/COPY WNCPYSRC,LEH00066
     A            RCCY           3        34
     A*/COPY WNCPYSRC,LEH00067
     A            RFAMTH        20        45
     A            RFAMTF        20        70
     A            RSTAT         10        95
     A                                      SPACEA(1)
      *
      *------------------------*
      ***  Error Record Format *
      *------------------------*
      *
     A          R ERROR                      SPACEB(3)
     A                                     6'THE HISTORY DETAILS FOR SOME OF +
     A                                       THE ABOVE FACILITIES DO NOT'
     A                                       SPACEA(1)
     A                                     6'RECONCILE TO THE FACILITY DETAILS +
     A                                       AND SHOULD BE INVESTIGATED'
     A                                       SPACEA(3)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
