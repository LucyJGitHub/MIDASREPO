      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Aggregated Facility History Report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000056P1 - Aggregated Facility History Report         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE041  *CREATE    Date 11Oct04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE041 - Collateralised Lending Phase 2B                     *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Format
      *
     A          R LE000056R0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE LE000056P1'
     A            BJURPT    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            BJMRDT         7A      105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    45'AGGREGATED FACILITY HISTORY +
     A                                      REPORT'
      *
     A  27                                89'BRANCH -'
     A  27        RBBRC     R             98REFFLD(BRCA)
     A  27        RBRNM     R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    45'----------------------------+
     A                                      ------'
     A                                      SPACEA(3)
      *
     A                                     1'CUSTOMER'
     A            RFCUST         6A       10
     A            RFCRNM        20A       17
     A                                    41'FACILITY'
     A            RFFACT         3A       50
     A                                    53'/'
     A            RFFCNO         2A       54
     A                                    60'CURRENCY'
     A            RFFCCY         3A       69
     A                                    76'REVOLVING CREDIT +
     A                                      INDICATOR'
     A            RFRCVR         1A      103
     A                                   132' '
     A                                      SPACEA(1)
      *
     A                                     1'FACILITY AMOUNT'
     A            RFFAMT        21A       17
     A                                    41'DRAWN AMOUNT'
     A            RFOAM1        21A       55
     A                                   132' '
     A                                      SPACEA(2)
      *
     A          R LE000056R1
     A                                     1'FACILITY AMOUNT HISTORY'
     A                                      SPACEA(1)
     A                                     1'-----------------------'
     A                                      SPACEA(2)
     A                                    12'--------------'
     A                                    26'AGGREGATE FACILITY'
     A                                    44'--------------'
     A                                    59'-------------------'
     A                                    78'TRANCHE'
     A                                    85'--------------------'
     A                                      SPACEA(1)
      *
     A                                     2'Date'
     A                                     7'Action'
     A                                    16'Action Amount'
     A                                    37'Facility Amount'
     A                                    55'RCI'
     A                                    63'Tranche'
     A                                    80'Action Amount'
     A                                    98'CCY'
     A                                   102'RCI'
     A                                   107'Exchange Rate'
     A                                   123'Mod'
     A                                   128'Trans'
     A                                      SPACEA(1)
     A                                     2'----'
     A                                     7'------'
     A                                    16'-------------'
     A                                    37'---------------'
     A                                    55'---'
     A                                    63'-------'
     A                                    80'-------------'
     A                                    98'---'
     A                                   102'---'
     A                                   106'----------------'
     A                                   123'---'
     A                                   128'-----'
     A                                      SPACEA(2)
      *
     A          R LE000056R2
     A                                     1'FACILITY DRAWN AMOUNT +
     A                                      AGGREGATES'
     A                                      SPACEA(1)
     A                                     1'----------------------+
     A                                      ----------'
     A                                      SPACEA(2)
      *
     A                                     6'Tranche'
     A                                    23'Drawn Amount'
     A                                    41'CCY'
     A                                    46'Exchange Rate'
     A                                    62'Aggregate Facility +
     A                                      Drawn Amount'
     A                                      SPACEA(1)
     A                                     6'-------'
     A                                    23'------------'
     A                                    41'---'
     A                                    45'----------------'
     A                                    62'-------------------+
     A                                      ------------'
     A                                      SPACEA(2)
      *
      ** Format to Output Detail Lines
      *
     A          R LE000056R3                TEXT('DETAIL OUTPUT FMT 1')
      *
     A  75        RDATE          7A        1TEXT('Date')
     A  75        RACTN          2A        9TEXT('Action')
     A  75        RAAMT         21A       12TEXT('Action Amount')
     A  75        RAFAM         21A       34TEXT('Facility Amount')
     A  75        RRCIN          1A       56TEXT('RCI')
     A  75        RTRN1         17A       58TEXT('Tranche')
     A  75        RTRAM         21A       76TEXT('Action Amount')
     A  75        RTRCY          3A       98TEXT('CCY')
     A  75        RTRRC          1A      103TEXT('RCI')
     A  75        REXRT1        14A      106TEXT('Exchange Rate')
     A  75        RMDIN1         1A      121TEXT('Mult/Div Ind.')
     A  75        RMODL          2A      123TEXT('Module')
     A  75        RTRAN          6A      127TEXT('Transaction')
     A                                      SPACEA(1)
     A N75                                52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEA(1)
      *
     A          R LE000056R4                TEXT('DETAIL OUTPUT FMT 2')
      *
     A  76        RTRN2         17A        1TEXT('Tranche')
     A  76        ROAM1         21A       19TEXT('Drawn Amount')
     A  76        RFCCY          3A       41TEXT('CCY')
     A  76        REXRT2        14A       45TEXT('Exchange Rate')
     A  76        RMDIN2         1A       60TEXT('Mult/Div Ind.')
     A  76        RAFAMT        21A       67TEXT('Aggregate Facility +
     A                                           Drawn Amount')
     A N76                                52'*** NO DETAILS TO REPORT ***'
     A                                   132' '
     A                                      SPACEA(1)
      *
      ** Format Total Detail Lines
      *
     A          R LE000056R5                TEXT('TOTAL DETAIL OUTPUT FMT')
     A                                      SPACEB(1)
     A                                    67'---------------------'
     A                                      SPACEA(1)
     A                                    45'TOTAL'
     A            RTFAMT        21A       67TEXT('Total Agg. Fac. Drawn Amount')
     A                                      SPACEA(2)
      *
      ** End of Report (Branch)
      *
     A          R LE000056R6                SPACEB(2)
     A  27                                46'***  END OF REPORT FOR +
     A                                      BRANCH '
     A  27        RBBRC          3A       76
     A  27                                81'***'
      *
      ** End of Report Format
      *
     A          R LE000056R7
     A                                      SPACEB(2)
     A                                    52'*** END OF REPORT ***'
      *
