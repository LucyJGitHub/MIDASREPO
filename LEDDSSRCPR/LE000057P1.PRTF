      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Reorg. & Archive of Agg. Fac. Hist Report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000057P1 - Reorganise & Archive of Aggregated         *
      *                    Facility Report                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE041  *CREATE    Date 11Oct04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE041 - Collateralised Lending Phase 2B                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE000057P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    36'ARCHIVED AGGREGATED FACILITY +
     A                                       HISTORY REPORT'
     A                                    83'BRANCH - '
     A            RBRCA          3A       93
     A            RBRNM         30A      + 2
     A                                      SPACEA(1)
      *
     A                                    36'-----------------------------+
     A                                      ---------------'
     A                                      SPACEA(1)
      *
      ***  Sub-Heading 1:
      ***  ------------
      *
     A          R SUBHD1
     A                                     6'CUSTOMER '
     A                                      SPACEB(3)
     A            RCUST          6A       +2
     A            RCNAM         20A       +1
     A                                    48'FACILITY'
     A            RFCTY          3A       +3
     A                                    +0'/'
     A            RFSEQ          2A       +0
     A                                    72'CURRENCY'
     A            RCCY           3A       +3
     A                                    91'REVOLVING CREDIT INDICATOR'
     A            RRVCR          1A       +3
     A                                      SPACEA(1)
     A                                     6'ORIGINAL FACILITY AMOUNT'
     A            ROAMT         16A       +3
     A                                      SPACEA(1)
      *
      ***  Sub-Heading 2:
      ***  ------------
      *
     A          R SUBHD2
     A                                     6'FACILITY AMOUNT HISTORY'
     A                                      SPACEB(1)
     A                                      SPACEA(1)
     A                                     6'-----------------------'
     A                                      SPACEA(2)
     A                                    21'----------AGGREGATE FACILITY----+
     A                                       -----'
     A                                    60'-----------------TRANCHE----------+
     A                                       --------'
     A                                      SPACEA(1)
     A                                     7'Date'
     A                                    14'Action'
     A                                    23'Action Amount'
     A                                    39'Facility Amount'
     A                                    55'RCI'
     A                                    65'Tranche'
     A                                    80'Action Amount'
     A                                    95'CCY'
     A                                    99'RCI'
     A                                   103'Exchange Rate'
     A                                   117'Mod'
     A                                   121'Transaction'
     A                                      SPACEA(1)
     A                                     6'-------'
     A                                    14'------'
     A                                    21'----------------'
     A                                    38'----------------'
     A                                    55'---'
     A                                    60'-----------------'
     A                                    78'----------------'
     A                                    95'---'
     A                                    99'---'
     A                                   103'-------------'
     A                                   117'---'
     A                                   121'-----------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A            RDATE1         7A        6
     A            RACTN1         2A       +3
     A            RAAMT1        16A       +3
     A            RFAMT1        16A       +1
     A            RREVC1         1A       +2
     A            RBRAN2         3A       +3
     A                                    +0'-'
     A            RCNUM2         6A       +0
     A                                    +0'-'
     A            RFACT2         3A       +0
     A                                    +0'-'
     A            RFCNO2         2A       +0
     A            RAAMT2        16A       +1
     A            RCURR2         3A       +1
     A            RREVC2         1A       +2
     A            REXRT2        11A       +2
     A            RMDIN2         1A       +1
     A            RMOD2          2A       +2
     A            RTRAN2         6A       +4
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
