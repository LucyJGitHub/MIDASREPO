      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas LE Aggregated Facility Processing - Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000058AU - Aggregated Facility Processing - Audit     *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE041  *CREATE    Date 11Oct04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE041 - Collateralised Lending Phase 2B                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE000058AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    39'AGGREGATED FACILITIES +
     A                                      PROCESSING - FILE CONTROLS'
     A                                      SPACEA(1)
      *
     A                                    39'----------------------+
     A                                      --------------------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    58'Number of records'
     A                                    +3'Value of records'
     A                                      SPACEA(1)
     A                                    58'-----------------'
     A                                    +3'----------------'
     A                                      SPACEA(2)
      *
     A                                     6'AGFCTY - RUN CONTROLS'
     A                                      SPACEA(1)
     A                                     6'---------------------'
     A                                      SPACEA(1)
     A                                    10'Total records on file'
     A            RCOUN1         5  0     64EDTCDE(Z)
     A            RHASH1        18  0     +9EDTCDE(Z)
     A                                      SPACEA(1)
     A                                    10'Total records on file -+
     A                                      calculated'
     A            RCOUN2         5  0     64EDTCDE(Z)
     A            RHASH2        18  0     +9EDTCDE(Z)
     A                                      SPACEA(1)
     A  30                               100'*** DIFFERENCE ***'
     A                                      SPACEA(1)
      *
     A                                     6'AGNFTY - RUN CONTROLS'
     A                                      SPACEA(1)
     A                                     6'---------------------'
     A                                      SPACEA(1)
     A                                    10'Total records on file'
     A            RCOUN3         5  0     64EDTCDE(Z)
     A            RHASH3        18  0     +9EDTCDE(Z)
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
