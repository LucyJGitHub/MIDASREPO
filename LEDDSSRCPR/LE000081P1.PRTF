      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Past Due Call Loans/Origin Loan report')      *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000081P1 - Past Due Call Loans/Original Loans report  *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134  *CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *                                                               *
      *****************************************************************
     A          R HEAD01                    TEXT('(c) Finastra International +
     A                                            Limited 2012')
     A                                      SKIPB(003)
     A                                     2'Reference'
     A            SFILE1        10        12
     A            BJURPT        53        40
     A                                   123'Page'
     A                                   128PAGNBR
     A                                      EDTCDE(Z)
     A            BJMRDT         7       115
     A                                      SKIPB(004)
     A                                   124TIME
     A                                    43'PAST DUE CALL LOANS by ORIGIN +
     A                                       LOAN / ORIGIN FEE'
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A                                      SKIPB(005)
     A                                     2'Branch'
     A                                      SKIPB(007)
     A            H#BRCA         3         9
     A                                      HIGHLIGHT
     A            H#BRNM        30        13
     A                                      HIGHLIGHT
 
      ** Origin Loan details
 
     A          R DETAIL01L                 SKIPB(009)
     A                                    61'Origin Loan'
     A                                      HIGHLIGHT
     A                                     2'Customer  . . . . . .'
     A                                      SKIPB(011)
     A            #1CNUM         6        25
     A            #1CRNM        20        40
     A                                     2'Loan Number . . . . .'
     A                                      SKIPB(013)
     A            #1LNRF         6        25
     A            #1LTSTCLTX    20         2
     A                                      SKIPB(014)
     A            #1LTSTCL      10        25
     A            #1LNTD        30        40
     A                                     2'Facility  . . . . . .'
     A                                      SKIPB(015)
     A            #1FCLT        13        25
     A                                     2'Facility Type . . . .'
     A                                      SKIPB(016)
     A            #1FCTI         1        25
     A            #1FCTIDSC     10        40
     A                                     2'Currency  . . . . . .'
     A                                      SKIPB(017)
     A            #1CCY          3        25
     A                                     2'Deal Date . . . . . .'
     A                                      SKIPB(019)
     A            #1DDAT         8        25
     A                                    80'Initial Amount  . . .'
     A            #1OPAM        21       103
     A                                     2'Start Date  . . . . .'
     A                                      SKIPB(020)
     A            #1VDAT         8        25
     A                                    80'Current Amount  . . .'
     A            #1CPAM        21       103
     A                                     2'Maturity Date . . . .'
     A                                      SKIPB(021)
     A            #1MDAT         8        25
     A                                    80'Accrued Interests . .'
     A            #1ACRINT      21       103
     A                                     2'Reporting Past Due:'
     A                                      SKIPB(023)
     A                                     4'Classification  . .'
     A                                      SKIPB(024)
     A            #1RCAT         1        25
     A            #1RCATDSC     25        40
     A                                     4'Start Date  . . . .'
     A                                      SKIPB(025)
     A            #1RCSD         8        25
     A                                     4'Initial Date. . . .'
     A                                      SKIPB(026)
     A            #1RIDT         8        25
     A                                     2'Basel II Past Due:'
     A                                      SKIPB(028)
     A                                     4'Classification  . .'
     A                                      SKIPB(029)
     A            #1BCAT         1        25
     A            #1BCATDSC     25        40
     A                                     4'Start Date  . . . .'
     A                                      SKIPB(030)
     A            #1BCSD         8        25
     A                                     4'Initial Date. . . .'
     A                                      SKIPB(031)
     A            #1BIDT         8        25
 
      ** Origin Fee details
 
     A          R DETAIL01F                 SKIPB(009)
     A                                    61'Origin Fee'
     A                                      HIGHLIGHT
     A                                     2'Customer  . . . . . .'
     A                                      SKIPB(011)
     A            #1CNUM         6        25
     A            #1CRNM        20        40
     A                                     2'Loan Number . . . . .'
     A                                      SKIPB(013)
     A            #1LNRF         6        25
     A                                     2'Facility  . . . . . .'
     A                                      SKIPB(014)
     A            #1FCLT        13        25
     A                                     2'Fee Sequence. . . . .'
     A                                      SKIPB(016)
     A            #1FSEQ         2        25
     A                                     2'Fee Code  . . . . . .'
     A                                      SKIPB(018)
     A            #1FCOD         2        25
     A                                     2'Currency  . . . . . .'
     A                                      SKIPB(019)
     A            #1CCY          3        25
     A                                    80'Fee Amount  . . . . .'
     A            #1FAMT        21       103
     A                                     2'Period Start Date . .'
     A                                      SKIPB(020)
     A            #1PSTD         8        25
     A                                     2'Period End Date . . .'
     A                                      SKIPB(021)
     A            #1PEND         8        25
     A                                     2'Reporting Past Due:'
     A                                      SKIPB(023)
     A                                     4'Classification  . .'
     A                                      SKIPB(024)
     A            #1RCAT         1        25
     A            #1RCATDSC     25        40
     A                                     4'Start Date  . . . .'
     A                                      SKIPB(025)
     A            #1RCSD         8        25
     A                                     4'Initiial Date . . .'
     A                                      SKIPB(026)
     A            #1RIDT         8        25
     A                                     2'Basel II Past Due:'
     A                                      SKIPB(028)
     A                                     4'Classification  . .'
     A                                      SKIPB(029)
     A            #1BCAT         1        25
     A            #1BCATDSC     25        40
     A                                     4'Start Date  . . . .'
     A                                      SKIPB(030)
     A            #1BCSD         8        25
     A                                     4'Initiial Date . . .'
     A                                      SKIPB(031)
     A            #1BIDT         8        25
 
      ** Past Due Call Loan details
 
     A          R DETAIL02
     A  30                                10'Past Due Call Loan'
     A  30                                  SKIPB(036)
     A  30                                  HIGHLIGHT
     A N30                                57'Past Due Call Loans'
     A N30                                  SKIPB(035)
     A N30                                  HIGHLIGHT
     A N30                                10'Principal'
     A N30                                  SKIPB(036)
     A N30                                  HIGHLIGHT
     A N30                                85'Interest'
     A N30                                  HIGHLIGHT
 
     A                                     2'Loan Number . . . . .'
     A                                      SKIPB(038)
     A            #2LNRF         6        25
     A            #3LNRF         6        80
 
     A            #1LTSTCLTX    20         2
     A                                      SKIPB(039)
     A            #2LTSTCL      10        25
     A            #2LNTD        30        40
     A            #3LTSTCL      10        80
     A            #3LNTD        30        95
 
     A                                     2'Facility  . . . . . .'
     A                                      SKIPB(040)
     A            #2FCLT        13        25
     A            #3FCLT        13        80
 
     A                                     2'Facility Type . . . .'
     A                                      SKIPB(041)
     A            #2FCTI         1        25
     A            #2FCTIDSC     10        40
     A            #3FCTI         1        80
     A            #3FCTIDSC     10        95
 
     A                                     2'Currency  . . . . . .'
     A                                      SKIPB(042)
     A            #2CCY          3        25
     A            #3CCY          3        80
 
     A                                     2'Initial Amount  . . .'
     A                                      SKIPB(044)
     A            #2OPAM        21        25
     A            #3OPAM        21        80
 
     A                                     2'Current Amount  . . .'
     A                                      SKIPB(045)
     A            #2CPAM        21        25
     A            #3CPAM        21        80
 
     A                                     2'Accrued Interests . .'
     A                                      SKIPB(046)
     A            #2ACRINT      21        25
     A            #3ACRINT      21        80
 
     A                                     2'Deal Date . . . . . .'
     A                                      SKIPB(048)
     A            #2DDAT         8        25
     A            #3DDAT         8        80
 
     A                                     2'Start Date  . . . . .'
     A                                      SKIPB(049)
     A            #2VDAT         8        25
     A            #3VDAT         8        80
 
     A                                     2'Maturity Date . . . .'
     A                                      SKIPB(050)
     A            #2MDAT         8        25
     A            #3MDAT         8        80
 
     A          R FOOT01
     A                                      SPACEB(003)
     A                                    48'*** End of Report for Branch'
     A            H#BRCA         3        77
     A                                    81'***'
 
     A          R FOOT02
     A                                      SPACEB(003)
     A                                    56'*** End of Report ***'
