      *****************************************************************
/*S*D****PRTFBASE******************************************************                       CLE164
/*E*S****PR_STD60******************************************************                       CLE164
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000083P1 - Pre-Check Auto-Settlements Report          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CLE164  *REDUNDANT Date 18Aug14               *
      *  Prev Amend No. CLE134  *CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE164 - CLE134 Enhancement F (Repayment Methodology)        *
      *  CLE134 - Past Due Call Loan Processing                       *
      *                                                               *
      *****************************************************************
 
     A          R P1H001                    SKIPB(3)
      *
     A                                      TEXT('(c) Misys International +
     A                                            Banking Systems Ltd. 2012')
      *
     A                                     2'Reference'
     A            SFILE1        10        12
     A            BJURPT        53        40
     A                                   123'Page'
     A                                   128PAGNBR
     A                                      EDTCDE(Z)
     A            BJMRDT         7       115
     A                                      SKIPB(4)
     A                                   124TIME
      *
     A                                    54'PRE-CHECK AUTO SETTLEMENT'
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A                                      SKIPB(5)
      *
     A                                     2'Branch'
     A                                      SKIPB(7)
     A            H#BRCA         3         9
     A                                      HIGHLIGHT
     A            H#BRNM        30        13
     A                                      HIGHLIGHT
     A          R P1H002                    SPACEB(3)
      *
     A                                     2'Settlement Account:'
     A                                      HIGHLIGHT
     A            P1ACCNT       24        22
     A                                      HIGHLIGHT
     A            P1ANAM        20        48
      *
     A                                    92'Opening Balance:'
     A                                      HIGHLIGHT
     A            P1OPNBAL      21       111
     A                                      HIGHLIGHT  SPACEA(1)
      *
     A                                    92'Projected Balance:'
     A                                      HIGHLIGHT
     A            P1PRJBAL      21       111
     A                                      HIGHLIGHT
     A          R P1H003                    SPACEB(2) SPACEA(1)
      *
     A                                     2'Value'
     A                                      HIGHLIGHT
     A                                    11'Seq'
     A                                      HIGHLIGHT
     A                                    16'Customer'
     A                                      HIGHLIGHT
     A                                    26'Facility'
     A                                      HIGHLIGHT
     A                                    36'Loan'
     A                                      HIGHLIGHT
     A                                    44'Fee'
     A                                      HIGHLIGHT
     A                                    49'Type'
     A                                      HIGHLIGHT
     A                                    56'Description'
     A                                      HIGHLIGHT
     A                                   103'Amount'
     A                                      HIGHLIGHT
     A                                   118'Balance After'
     A                                      HIGHLIGHT
     A                                     2'Date'
     A                                      HIGHLIGHT
     A                                      SPACEB(1)
     A                                    44'Seq'
     A                                      HIGHLIGHT
     A          R P1D001                    SPACEB(1)
      *
     A            P1VDDMMM       5         2
     A          R P1D002                    SPACEA(1)
      *
     A            P1PCKO         5         9
     A            P1CUST         6        16
     A            P1FACLTY       6        26
     A            P1LOAN         6        36
     A            P1FEESEQ       2        44
     A            P1TYPSTP       5        49
     A N30        P1MOVDSC      30        56
     A  30                                56'Non-lending Consolidated'
     A            P1AMOUNT      21        89
     A            P1BALAFT      21       111
 
      ** Footer Lines...
 
     A          R P1T000                    SPACEB(3)
      *
     A                                    36'***  NO DETAILS TO REPORT  ***'
      *
     A          R P1T001                    SPACEB(3)
      *
     A                                    48'*** End of Report for Branch'
     A            H#BRCA         3        77
     A                                    81'***'
      *
     A          R P1T002                    SPACEB(3)
      *
     A                                    40'***  END OF REPORT  ***'
