      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Past Due Overdraft report')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000091P1 - Past Due Overdraft report                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CLE134  *CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *                                                               *
      *****************************************************************
 
     A          R HEAD01                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                            Banking Systems Ltd. 2012')
 
     A                                     2'Reference'
     A            SFILE1        10        12
     A            BJURPT        53        40
     A                                   123'Page'
     A                                   128PAGNBR
     A                                      EDTCDE(Z)
     A            BJMRDT         7       115
     A                                      SKIPB(4)
 
     A                                   124TIME
     A                                    51'PAST DUE OVERDRAFT - '
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A                                      SKIPB(5)
 
     A  30                                72'Basel II mode'
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A N30                                72'Normal mode'
     A                                      HIGHLIGHT
     A                                      UNDERLINE
 
     A                                     2'Branch'
     A                                      SKIPB(7)
     A            H#BRCA         3         9
     A                                      HIGHLIGHT
     A            H#BRNM        30        13
     A                                      HIGHLIGHT
 
     A                                     2'Account'
     A                                      HIGHLIGHT
     A                                      SKIPB(9)
     A                                    29'Retail'
     A                                      HIGHLIGHT
     A                                    42'Name'
     A                                      HIGHLIGHT
     A                                    68'Cleared Balance'
     A                                      HIGHLIGHT
     A                                    90'Overdraft'
     A                                      HIGHLIGHT
     A                                   102'Days in'
     A                                      HIGHLIGHT
     A                                   115'Last'
     A                                      HIGHLIGHT
 
     A                                    29'A/c Number'
     A                                      HIGHLIGHT
     A                                      SKIPB(10)
     A                                    68'/Ledger Balance'
     A                                      HIGHLIGHT
     A                                    94'Limit'
     A                                      HIGHLIGHT
     A                                   102'Past Due'
     A                                      HIGHLIGHT
     A                                   113'Payment'
     A                                      HIGHLIGHT
     A                                      SKIPA(11)
 
      ** Basel II Past Due Class Description
 
     A          R HEAD02                    SPACEB(1)
     A                                      SPACEA(1)
     A            H#PDCLDSC     25         2
     A                                      HIGHLIGHT
 
     A          R DETAIL01                  SPACEB(1) SPACEA(1)
     A            #1GLACNT      24         2
     A            #1RTACNO      10        29
     A            #1ANAM        20        42
     A            #1CLBL        21        63
     A            #1ODLN        14        85
     A            #1DAYSPD       6       103
     A            #1LSTPYD       8       112
     A                                      SPACEA(1)
 
     A            #1CRNM        20        42
     A            #1LDBL        21        63
 
     A          R FOOT01                    SPACEB(3)
     A                                    48'*** End of Report for Branch'
     A            H#BRCA         3        77
     A                                    81'***'
 
     A          R FOOT02                    SPACEB(3)
     A                                    56'*** End of Report ***'
