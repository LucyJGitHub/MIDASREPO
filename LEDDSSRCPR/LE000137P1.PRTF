      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Fee PDCL Account Key Generation')                *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000137P1 - Print File for calculated facility/loan    *
      *                  fees report - PDCL process                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CLE134  *CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *                                                               *
      *****************************************************************
     A          R LER137H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
      *
     A                                    16'LE000137P1'
      *
     A            BJURPT        53        40
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    35'SUSPEND  FEES  ACCOUNT +
     A                                       KEYS GENERATED'
     A  35                                  SPACEA(2)
     A N35                                78'BRANCH'
     A N35        LKBRCD         3A       85
     A N35                                89'-'
     A N35        A8BRNM        30A       91
     A N35                                  SPACEA(2)
      *
     A
     A          R LER137H2
     A                                     5'CUSTOMER'
     A                                    16'FAC/LOAN'
      *
     A                                    26'FEE SEQ'
     A                                    36'ACCOUNT KEY'
     A                                    65'AMOUNT'
     A                                    73'CURRENCY'
     A                                    82'REV.IND.'
     A                                    92'    AVAILABLE BALANCE'
      *
     A                                      SPACEA(1)
     A                                     5'--------'
     A                                    16'--------'
      *
     A                                    26'-------'
     A                                    36'-----------'
     A                                    50'---------------------'
     A                                    73'--------'
     A                                    82'--------'
     A                                    92'---------------------'
      *
     A                                      SPACEA(2)
     A
     A          R LER137D1                  SPACEA(1)
      *
     A            LKCNUM         6A        6
     A  34        LKLNNO         6A       18
     A N34        WFAC03         3  0     18
     A N34                                21'/'
     A N34        WFAC02         2  0     22
      *
     A            LKFSEQ         2  0     28
      *
     A            LKAKEY        14        36
      *
     A            OEAMT         21        50
     A            LKECCY         3        75
     A            LKREVI         1  0     85
     A            OAAMT         21        92
      *
     A
     A          R LER137T1                  SPACEB(3)
     A                                     5'TOTAL ACCOUNT KEYS GENERATED'
     A            LKNORE         5  0     +3EDTCDE(Z)
     A                                      SPACEA(3)
     A
     A                                    40'***  END OF REPORT  ***'
     A
