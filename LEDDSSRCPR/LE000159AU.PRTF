      *****************************************************************
/*STD *  PRTFAU                                                       *
/*EXI *  TEXT('Midas LE Facility Fee History Extract Audit')          *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000159AU - Facility Fee History Extract Audit         *
      *                                                               *
      *  (c) Finastra International Limited 2018                      *
      *                                                               *
      *  Last Amend No. CLE159  *Create    Date 18Jul18               *
      *  Prev Amend No. xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE159 - Facility Fee Amortization Enhancement               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited 2018')
      *
     A                                      SKIPB(3)
     A                                      SPACEA(2)
      *
     A                                     6'REFERENCE LE000159AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                    +1PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    32'Facility History Extract for +
     A                                       Accelerated Fee Amortization +
     A                                      - AUDIT REPORT'
     A                                      UNDERLINE
     A                                      SPACEA(3)
      *
      *----------------------*
      ***  File Controls     *
      *----------------------*
      *
     A          R CONTROL
     A                                    69'CALCULATED'
     A                                      UNDERLINE
     A                                      SPACEA(4)
      *
     A                                     6'NUMBER OF FEE RECORDS PROCESSED:'
     A            RCNTFE         9  0     70EDTCDE(3) SPACEA(2)
      *
     A                                     6'NUMBER OF FEE ADJUSTMENTS FOR +
     A                                      NON-CALCULATED FEES:'
     A            RCNTAD         9  0     70EDTCDE(3) SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS                    SPACEB(3)
     A  79                                52'*** No Details to Report ***'
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(3)
      *
      *----------------------*
      ***  Database Error    *
      *----------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
