      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Outstanding loans against agent fac rprt')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000311P1 - Outstanding Loans Against Agent Facilities *
      *                    Detail Report                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE042             Date 06Sep05               *
      *  Prev Amend No. CLE031  *CREATE    Date 03Feb03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CLE031 - Lending Enhancements.                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
 
      ** Report Header Record Format *
 
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
 
     A                                      SKIPB(003)
     A                                     6'Reference LE000311P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
 
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(002)
 
     A                                    40'OUTSTANDING LOANS AGAINST AGENT -
     A                                      FACILITIES'
     A  01                                88'BRANCH -'
     A  01        RBRCD     R             97REFFLD(BRCA)
     A  01        RBRNM     R            101REFFLD(BRNM)
     A                                      SPACEA(1)
 
     A                                    40'-----------------------+
     A                                      -------------------'
     A                                      SPACEA(003)
 
      ** Facility Details
 
     A          R DETAIL1
     A                                      SPACEB(001)
     A                                     2'FACILITY CUSTOMER: '
     A            RCNUM          6A       21
     A            RCSSN         10A       28
     A                                    45'FACILITY '
     A            RFACT          3A       54
     A                                    58'/'
     A            RFCNO          2A       60
     A                                      SPACEA(002)
 
      ** Loan Details
 
     A          R DETAIL2
     A                                    11'BRANCH '
     A            RBRCA          3A       19
     A                                    28'LOAN '
     A            RLNRF          6A       34
     A                                    45'CUSTOMER'
     A            RCNUM          6A       55
     A                                    82'LOAN TYPE/SUBTYPE '
     A            RLTYP          2A      101
     A                                   103'/'
     A            RSUTP          2A      105
     A*                                              Begin /CLE042/
     A            RCLAS          4A      107                                                  CLE042
     A*                                              End   /CLE042/
     A                                      SPACEA(002)
     A                                    11'PRINCIPAL'
     A            RCPAM         21A       22
     A                                    45'CURRENCY'
     A            RCCY      R             55REFFLD(CCY)
     A                                    74'START DATE'
     A            RVDAT          7A       85
     A                                    94'MATURITY'
     A            RMDAT          7A      103
     A                                      SPACEA(004)
 
      ** Report Trailer
 
     A          R TRAILP1
     A  01                                46'***  END OF REPORT FOR BRANCH'
     A  01        RBRCD     R             +1REFFLD(BRCA)
     A  01                                +2'***'
 
     A N01                                51'***  END OF REPORT  ***'
