      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas LE Manual Diary Report Audit')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LE000352AU - Midas LE Manual Diary Report Audit              *
      *                                                               *
      *  Last Amend No. CLE070   *CREATE   Date 19Jun19               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE070 - Manual Diary                                        *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      INDARA
      *
      ** Format for Whole Report
      *
     A          R CONTROL
     A                                      TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited  2015')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE LE000352AU'
     A            RRTITL        53        36TEXT('TITLE')
     A                                   100'DATE'
     A            RRRUNA         7       105TEXT('RUNDATE')
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      SPACEA(3)
     A                                      EDTWRD('0 :  :  ')
      *
     A                                     6'RUN CONTROLS - INPUT REPORT'
     A                                    54'----------'
     A                                    65'REPORT'
     A                                    72'----------'
      *
     A                                    54'NUMBER'
     A                                      SPACEB(1)
      *
     A                                    10'RECORDS LISTED IN THIS REPORT'
     A                                      SPACEB(2)
     A            RRTOTR         4  0     56TEXT('TOTAL RECORDS IN THE REPORT')
     A                                      EDTWRD('  0 ')
     A                                    13'RECORDS INSERTED'
     A                                      SPACEB(2)
     A            RRINSR         4  0     56TEXT('Records INSERTED - REPORT')
     A                                      EDTWRD('  0 ')
     A                                    13'RECORDS AMENDED'
     A                                      SPACEB(1)
     A            RRAMNR         4  0     56TEXT('Records AMENDED - REPORT')
     A                                      EDTWRD('  0 ')
     A                                    13'RECORDS DELETED'
     A                                      SPACEB(1)
     A            RRDELR         4  0     56TEXT('RECORDS DELETED - REPORT')
     A                                      EDTWRD('  0 ')
      *
     A          R TRAILER
      *
     A N54N80                             48'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      ** Database Error Message
      *
     A          R DBERROR                   TEXT('DATABASE ERROR MSG')
     A                                      SPACEB(3)
     A                                    48 '***   PRINT TERMINATED   ***'
     A                                      SPACEA(2)
     A                                    45 'DATABASE ERROR'
     A            DBASE          3  0     60
     A                                    65 'FILE'
     A            DBFILE         8        70
     A                                      SPACEA(1)
     A                                    45 'KEY'
     A            DBKEY         29        49
      *
