      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Manual Diary Details List Report')            *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LE000352P1 - Midas LE Manual Diary Audit List Report         *
      *                                                               *
      *  Last Amend No. CLE070   *CREATE   Date 19Jun19               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE070 - Manual Diary                                        *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      INDARA
      *
      ** Report Header Record Format
     A          R LE000352R0                TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited  2019')
     A                                      SKIPB(3)
      *
     A                                     5'REFERENCE LE000352P1'
     A            RRTITL        53        36TEXT('TITLE')
     A                                   100'DATE'
     A            RRRUNA         7       105TEXT('RUNDATE')
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      SPACEA(1)
     A                                      EDTWRD('0 :  :  ')
      *
     A                                    49'MANUAL DIARY DETAILS LIST +
     A                                       REPORT'
     A                                      SPACEA(1)
     A                                    49'--------------------------+
     A                                       ------'
     A                                      SPACEA(1)
     A                                     5'EVENT'
     A                                    14'DIARY'
     A                                    23'CUST NO/'
     A                                    32'CUSTOMER/'
     A                                    60'START'
     A                                    70'END'
     A                                    79'FREQ'
     A                                    85'DAY'
     A                                    92'NOTICE'
     A                                   100'DIARY'
     A                                   107'DOCUMENT'
     A                                      SPACEA(1)
     A                                     5'DATE'
     A                                    14'REF'
     A                                    23'LOAN REF'
     A                                    38'NARRATIVE'
     A                                    60'DATE'
     A                                    70'DATE'
     A                                    79'CODE'
     A                                    85'NO'
     A                                    92'DAYS'
     A                                   100'TYPE'
     A                                   107'CODE'
     A                                   116'ACTN'
     A                                      SPACEA(1)
      *
      ** Record format for Details
     A          R LE000352R1
     A            RRACTD         7         5TEXT('EVENT DATE')
     A            RRDIAR         6        14TEXT('DIARY REF')
     A            RRCUSN         6        23TEXT('CUST NO')
     A            RRCUST        10        32TEXT('CUST NAME')
     A            RRSTRT         7        60TEXT('START DATE')
     A            RRENDT         7        70TEXT('END DATE')
     A            RRFREQ         1        79TEXT('FREQ NO')
     A            RRDAYN         2        85TEXT('DAY NO')
     A            RRNTCD         2        92TEXT('NOTICE DAYS')
     A            RRDTYP         2       100TEXT('DIARY TYPE')
     A            RRDOCC         2       107TEXT('DOCUMENT CODE')
     A            RRACTN         1       116TEXT('ACTION')
     A                                      SPACEA(1)
     A            RRLNRF         6        23TEXT('LOAN REFERENCE')
     A            RRNAR1        30        38TEXT('NARRATIVE')
     A            RRNAR2        30        69TEXT('NARRATIVE')
     A                                      SPACEA(1)
      *
      ** Logical break for change in Due Diligence Date
     A          R LE000352R2
     A                                      SPACEB(1)
      *
      ** Record format for no records
     A          R LE000352R3
      *
     A                                    52'*** END OF REPORT ***'
     A                                      SPACEB(2)
      *****************************************************************
