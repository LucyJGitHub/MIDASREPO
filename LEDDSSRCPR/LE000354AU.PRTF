      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000354AU - Manual Diary Update                        *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. CLE070  *CREATE    Date 19Jun19               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE070 - Manual Diary                                        *
      *                                                               *
      *****************************************************************
      *
      *-----------------------*
      ** Report Header        *
      *-----------------------*
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2019')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE000354'
     A            BJURPT        53A       37
     A                                   101'DATE'
     A            BJMRDT         7A      106
     A                                   119'PAGE    1'
     A                                      SPACEA(1)
      *
     A                                    47'DAILY DIARY - EXTRACT - +
     A                                      RUN CONTROLS'
     A                                      SPACEA(1)
      *
     A                                    47'------------------------+
     A                                      ------------'
     A                                      SPACEA(3)
      *
      *-----------------------*
      ** File Control         *
      *-----------------------*
     A          R CONTROL
     A                                     7'RUN CONTROLS - EXTRACT PROGRAM'
     A                                    54'------------ REPORT +
     A                                       -----------'
     A                                    94'--------- FILE CONTROL +
     A                                      --------'
     A                                      SPACEA(2)
     A                                    67'NUMBER'
     A                                   107'NUMBER'
      *
     A                                     9'TOTAL RECORDS ON INPUT +
     A                                      FILE'
     A                                      SPACEB(2)
     A            RECRDS         5  0     68EDTCDE(3)
     A            NORE           5  0    108EDTCDE(3)
     A  15                               127'***'
      *
     A                                     9'NUMBER OF RECORDS INSERTED'
     A                                      SPACEB(2)
     A            NINS           5  0     68EDTCDE(3)
      *
     A                                     9'NUMBER OF RECORDS AMENDED'
     A                                      SPACEB(2)
     A            NAMD           5  0     68EDTCDE(3)
      *
     A                                     9'NUMBER OF RECORDS DELETED'
     A                                      SPACEB(2)
     A            NDEL           5  0     68EDTCDE(3)
      *
     A                                     9'LIVE RECORDS AFTER RUN'
     A                                      SPACEB(2)
     A            NLIVE          5  0    108EDTCDE(3)
      *
     A  15                                79'***   DIFFERENCE   ***'
     A                                      SPACEB(2)
      *
     A                                    53'*** END OF REPORT ***'
     A                                      SPACEB(2)
      *
      *-----------------------*
      ** Database Error       *
      *-----------------------*
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
