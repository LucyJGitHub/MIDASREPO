      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_CONF42                                                    *
/*EXI *  TEXT('Midas LE Loan Advice with Fwd ARR Rates Report')       *
/*ADD *  CHGPRTF FILE(LE000372P1) PAGESIZE(68 90) OVRFLW(68)          *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000372P1 - Loan Advice with Fwd ARR rates             *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. MD060897 *AMEND     Date 10Jan23              *
      *  Prev Amend No. MD058517 *CREATE    Date 17Oct22              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD060897 - CC10 Loan Interest Confirmation Spool file issues *
      *             - Loan record is reported twice                   *
      *             - Reprint function failed for matured loans       *
      *             - Some labels does not match user story reqmnt    *
      *             Amended this program as follows                   *
      *             - Do not perform print routine if record is not   *
      *               found in loans file                             *
      *             - Use union to combine current and forward rates  *
      *               excluding records which exist on both files     *
      *  MD058517 - CLE172 LIBOR Lending- Change Control CC3.1        *
      *             Interest Projection Using Known Rates             *
      *             CC10 CLE172 Spool File Correspondence (MD059817)  *
      *                                                               *
      *****************************************************************
      *
     A          R DETLP1
     A                                    11'Date:'
     A                                      SPACEB(1)
     A            P1DATE         9        22
     A                                    11'Customer:'
     A                                      SPACEB(1)
     A            P1CUST         6        22HIGHLIGHT
     A            P1CNA1        35        22HIGHLIGHT
     A                                      SPACEB(1)
     A            P1CNA2        35        22HIGHLIGHT
     A                                      SPACEB(1)
     A            P1CNA3        35        22HIGHLIGHT
     A                                      SPACEB(1)
     A            P1CNA4        35        22HIGHLIGHT
     A                                      SPACEB(1)
      *
     A                                    11'Subject:'
     A                                      SPACEB(2)
     A                                    22'Notification of Loan Confirmation'
     A                                    11'We are pleased to advise you on +
     A                                       the Loan Details following the '
     A                                      SPACEB(2)
     A                                    11'determination of the Risk Free +
     A                                       Rate:'
     A                                      SPACEB(1)
      *
     A                                    11'Loan Details'
     A                                      SPACEB(2)
     A                                      HIGHLIGHT
     A                                    13'Our Reference'
     A                                      SPACEB(1)
     A            P1LNRF         6        51
     A                                    13'Principal'
     A                                      SPACEB(1)
     A            P1CCY          3        51
     A            P1CPAM        21        55
     A                                    13'Interest Start Date'
     A                                      SPACEB(1)
     A            P1STDT         9        51
     A                                    13'Interest End Date'
     A                                      SPACEB(1)
     A            P1NIDT         9        51
     A            P1NRDY        12        61
     A                                    13'Interest Rate'
     A                                      SPACEB(1)
     A            P1INTR        28        51
     A                                    13'Gross Interest'
     A                                      SPACEB(1)
     A            P1ICCY         3        51
     A            P1INTA        21        55
      *
     A          R TRAILP1
     A            ARRT01        90         1
     A                                      SPACEB(2)
     A            ARRT02        90         1
     A                                      SPACEB(1)
     A            ARRT03        90         1
     A                                      SPACEB(1)
     A            ARRT04        90         1
     A                                      SPACEB(1)
     A            ARRT05        90         1
     A                                      SPACEB(1)
     A            ARRT06        90         1
     A                                      SPACEB(1)
     A            ARRT07        90         1
     A                                      SPACEB(1)
     A            ARRT08        90         1
     A                                      SPACEB(1)
     A            ARRT09        90         1
     A                                      SPACEB(1)
     A            ARRT10        90         1
     A                                      SPACEB(1)
     A            ARRT11        90         1
     A                                      SPACEB(1)
     A            ARRT12        90         1
     A                                      SPACEB(1)
     A            ARRT13        90         1
     A                                      SPACEB(1)
     A            ARRT14        90         1
     A                                      SPACEB(1)
     A            ARRT15        90         1
     A                                      SPACEB(1)
     A            ARRT16        90         1
     A                                      SPACEB(1)
     A            ARRT17        90         1
     A                                      SPACEB(1)
     A            ARRT18        90         1
     A                                      SPACEB(1)
     A            ARRT19        90         1
     A                                      SPACEB(1)
     A            ARRT20        90         1
     A                                      SPACEB(1)
     A            ARRT21        90         1
     A                                      SPACEB(1)
     A            ARRT22        90         1
     A                                      SPACEB(1)
     A            ARRT23        90         1
     A                                      SPACEB(1)
     A            ARRT24        90         1
     A                                      SPACEB(1)
     A            ARRT25        90         1
     A                                      SPACEB(1)
     A            ARRT26        90         1
     A                                      SPACEB(1)
     A            ARRT27        90         1
     A                                      SPACEB(1)
      *
     A          R DETLP2
     A                                      SKIPB(2)
     A                                    11'ARR Details'
     A                                    13'Risk Free Rate'
     A                                      SPACEB(1)
     A            P1RFRR        10        47
     A                                    13'Calculation Method'
     A                                      SPACEB(1)
     A            P1CALM        50        47
     A                                    13'Benchmark Adjustment'
     A                                      SPACEB(1)
     A            P1BADJ        21        47
     A                                    13'Day Basis for Average'
     A                                      SPACEB(1)
     A            P1DBAV        50        47
     A                                    13'Floor'
     A                                      SPACEB(1)
     A            P1FLOR        21        47
     A                                    13'Lockout Days'
     A                                      SPACEB(1)
     A            P1LODY         2  0     47
     A                                    13'Lookback Days'
     A                                      SPACEB(1)
     A            P1LBDY         2  0     47
     A                                    13'Observation Period Shift'
     A                                      SPACEB(1)
     A            P1OPSH         1        47
     A                                    13'Rates Known Indicator'
     A                                      SPACEB(1)
     A            P1RTKN         1        47
     A**********                          13'Rate Rounding Precision'                       MD060897
     A            P1RLBL        23        13                                                MD060897
     A                                      SPACEB(1)
     A            P1RRDP         2  0     47
      *
     A          R HEADP3
     A                                      SKIPB(2)
     A                                    40'Interest Details'
     A                                    26'For Interest Period'
     A                                      SPACEB(1)
     A            P1SLID         9        46
     A                                    56'to'
     A            P1NIDT         9        59
     A                                    11'-----------------------------+
     A                                       -----------------------------+
     A                                       -----------------'
     A                                      SPACEB(1)
     A                                    11'Interest'
     A                                      SPACEB(1)
     A                                    23'Interest'
     A                                    35'Published'
     A                                    52'Rate Applied'
     A                                    68'Average/Compounded'
     A                                    11'Date'
     A                                      SPACEB(1)
     A                                    23'Days'
     A                                    35'Risk Free Rate'
     A                                    68'Rate'
     A                                    11'-----------------------------+
     A                                       -----------------------------+
     A                                       -----------------'
     A                                      SPACEB(1)
      *
     A          R DETLP3
     A            P2INTD         9        11
     A                                      SPACEB(1)
     A            P2INDY         5        23
     A            P2BRFR        15        35
     A            P2RTAP        15        52
     A            P2AVCR        15        68
      *
     A          R ENDREP
     A*
     A                                       SPACEB(2)
     A                                    37'*** End of report ***'
