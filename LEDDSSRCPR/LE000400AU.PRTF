      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Upd ORBR Field from Customer Lending Loan File')       *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000400AU - Updated Originating Branch Field from      *
      *                    Customer Lending Loan File - Audit         *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. MD054707 *CREATE   Date 06Nov19               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD054707 - CER050 Comp LEC06A 00010 failed during COB Run    *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)

      **  Report Header Record Format
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2019')

     A                                      SKIPB(3)
     A                                     6'REFERENCE LE000400AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)

     A                                     6'TIME'
     A                                    12TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)

     A                                    34'UPDATED ORIGINATING BRAN+
     A                                      CH FIELD FROM CUSTOMER LE+
     A                                      NDING LOAN FILE DETAILS -+
     A                                       AUDIT'
     A                                      SPACEA(1)

     A                                    34'------------------------+
     A                                      -------------------------+
     A                                      -------------------------+
     A                                      ------'
     A                                      SPACEA(3)

      ** File Control
     A          R CONTROL
     A                                    10'No. of Records updated from +
     A                                       CLOANCL....:'
     A            RCOUNT         6  0     63EDTCDE(3)
     A                                      SPACEA(1)

      ** No Details
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)

      ** Database Error
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
