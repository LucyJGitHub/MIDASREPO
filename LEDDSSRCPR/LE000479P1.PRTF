      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Loans PDCL Account Key AVAILABLE BALANCE')       *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *   PRTF/LE000479P1 - Print File for Loans account keys         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. AR402058*CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR402058 - CLE134 Overdraft PDCL repayments                  *
      *             (Child: AR402059)                                 *
      *                                                               *
      *****************************************************************
     A          R LE000479H1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
      *
     A                                    16'LE000479P1'
      *
     A            BJURPT        53        40
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    35'Past Due Call Loans SETTLEME+
     A                                        NT A/C AVAILABLE BALANCE'
      *
     A
     A          R LE000479H2
     A                                      SPACEB(3)
     A                                     2'SETTLEMENT ACCOUNT:'
     A            SEBRCA         3A       25
     A                                    28'-'
     A            SEACCY         3A       29
     A                                    32'-'
     A            SECNUM         6A       33
     A                                    39'-'
     A            SEACOD        10  0     40
     A                                    50'-'
     A            SEACSQ         2  0     51
     A                                    60'RETAIL NUMBER:'
     A            SEACNO        10        75
      *
     A                                     2'OPENING BALANCE:'
     A                                      SPACEB(1)
     A            W@OPEN        21        18
     A                                    40'HELD:'
     A            W@HELD        21        46
     A                                    68'OVERDRAFT:'
     A            W@OVLN        21        79
     A                                      SPACEA(2)
      *
     A
     A          R LE000479H3
     A                                      SPACEA(1)
     A                                    10' LOAN/  '
     A                                    19' FEE  '
     A                                    26'FEE'
     A                                    30'FEE'
     A                                    35' ACCOUNT  '
     A                                    47' PCN '
     A                                    53' PCN '
     A                                    59' PCN '
     A                                    65' VALUE '
     A                                    73'               EVENT '
     A                                    94'           AVAILABLE '
     A                                   116'REV '
     A                                   121'PROC'
     A                                   126'OVRDRF'
      *
     A                                       SPACEA(2)
     A                                     1'FILE    '
     A                                    10'FACILITY'
     A                                    19' CUST '
     A                                    26'SEQ'
     A                                    30'CD '
     A                                    35'   KEY    '
     A                                    47'MODUL'
     A                                    53' LOW '
     A                                    59'TRANS'
     A                                    65' DATE  '
     A                                    73'              AMOUNT '
     A                                    94'             BALANCE '
     A                                   116'IND'
     A                                   121'FLAG'
     A                                   126' FLAG '
      *
     A                                      SPACEA(1)
      *
     A                                     1'--------'
     A                                    10'--------'
     A                                    19'------'
     A                                    26'---'
     A                                    30'---'
     A                                    35'----------'
     A                                    47'-----'
     A                                    53'-----'
     A                                    59'-----'
     A                                    65'-------'
     A                                    73'---------------------'
     A                                    94'---------------------'
     A                                   116'---'
     A                                   121'----'
     A                                   126'------'
      *
      *
     A
     A          R LE000479D1                SPACEA(1)
      *
     A            W@FILE        10         1
     A            W@LNRF         6        11
     A            W@FCUS         6        19
     A            W@FSEQ         2        26
     A            W@FCOD         2        30
     A            W@ACKEY       10A       35
     A            W@PCKM         5  0     47EDTCDE(Z)
     A            W@PCKO         5  0     53EDTCDE(Z)
     A            W@PCKT         5  0     59EDTCDE(Z)
     A            W@VDAT         7A       65
      *
     A            W@EAMT        21        73
     A            W@AMT         21        94
     A            W@REVI         1       116
     A            W@IPFL         1A      121
     A            W@OVRD         1A      127
      *
     A
     A          R LE000479T1                SPACEB(3)
     A
     A                                    40'***  END OF REPORT  ***'
     A
