      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE PDCL Sett Acct Available Balance Report')     *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *   PRTF/LE000496P1 - Print File for Loans account keys         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. CLE164 *CREATE     Date 18Aug14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE164 - CLE134 Enhancement F (Repayment Methodology)        *
      *                                                               *
      *****************************************************************
     A          R LE000496H1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2014')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
      *
     A                                    16'LE000496P1'
      *
     A            BJURPT        53        40
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    35'Past Due Call Loans SETTLEME+
     A                                        NT A/C AVAILABLE BALANCE'
      *
     A          R LE000496H2
     A                                      SPACEB(3)
     A                                     2'SETTLEMENT ACCOUNT:'
     A            SEBRCAH        3A       25
     A                                    28'-'
     A            SEACCYH        3A       29
     A                                    32'-'
     A            SECNUMH        6A       33
     A                                    39'-'
     A            SEACODH       10  0     40
     A                                    50'-'
     A            SEACSQH        2  0     51
     A                                    60'RETAIL NUMBER:'
     A            SEACNOH       10S 0     75
     A                                    96'BLOCK DEBITS:'
     A            SERETBH        3A      110
     A  50                               116'ABC FLAG:'
     A  50        SEABCFH        3A      127
      *
     A                                     3'OPEN AVAIL BAL:'
     A                                      SPACEB(1)
     A            W@OPEN        22        21
     A                                    89'CLEARED BAL +/- ADJ:'
     A            W@CLBL        22       106
     A                                      SPACEA(1)
     A                                     7'HELD ITEMS:'
     A            W@HELD        21        21
     A                                    54'OVERDRAFT:'
     A            W@OVLN        21        67
     A  50                                90'BLOCKED COLLATERAL:'
     A  50        W@COLL        22       106
      *
     A
     A          R LE000496H3
     A                                      SPACEB(2)
     A                                      SPACEA(1)
     A                                    15'TRANSACTION'
     A                                    32'CATE'
     A                                    39'REPYMNT'
     A                                    50'VALUE'
     A                                    71'AVAILABLE'
     A                                    99'EVENT'
     A                                   122'AMOUNT'
     A                                   129'OV'
      *
     A                                       SPACEA(2)
     A                                     3'FILE'
     A                                    16'REFERENCE'
     A                                    32'CODE'
     A                                    39'PRIORITY'
     A                                    50'DATE'
     A                                    73'BALANCE'
     A                                    98'AMOUNT'
     A                                   122'REPAID'
     A                                   129'FL '
      *
     A                                      SPACEA(1)
      *
     A                                     1'--------'
     A                                    11'-------------------'
     A                                    32'------'
     A                                    39'--------'
     A                                    49'-------'
     A                                    58'----------------------'
     A                                    82'----------------------'
     A                                   106'----------------------'
     A                                   129'--'
      *
      *
     A
     A          R LE000496D1                SPACEA(1)
      *
     A            P1FILE        10         1
     A            P1REFN        11        13
     A            P1CATE         5A       32
     A            P1REPR         8S 0     39
     A            P1VDAT         7A       49
     A            P1AAMT        22        58
     A            P1EAMT        22        82
     A            P1RAMT        22       106
     A            P1OVRD         1       130
     A
     A          R LE000496T1                SPACEB(3)
     A
     A                                    40'***  END OF REPORT  ***'
     A
