      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE PDCL Sett Acct Available Balance Report')     *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *   PRTF/LE000496P2 - Print File for Loans account keys         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. CLE164 *CREATE     Date 18Aug14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE164 - CLE134 Enhancement F (Repayment Methodology)        *
      *                                                               *
      *****************************************************************
     A          R LE496P2H1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2014')
     A                                      SKIPB(3)
     A                                     2'REFERENCE'
      *
     A                                    12'LE000496P2'
      *
     A            BJURPT        53        40
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A            PAGEP2         5  0    124EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    35'PAST DUE CALL LOANS SETTLEME+
     A                                        NT A/C AVAILABLE BALANCE +
     A                                        BY KEY'
      *
     A          R LE496P2H2
     A                                      SPACEB(3)
     A                                     2'SETTLEMENT ACCOUNT:'
     A            SEBRCA         3A       23
     A                                    26'-'
     A            SEACCY         3A       27
     A                                    30'-'
     A            SECNUM         6A       31
     A                                    37'-'
     A            SEACOD        10  0     38
     A                                    48'-'
     A            SEACSQ         2  0     49
     A                                    60'RETAIL NUMBER:'
     A            SEACNO        10S 0     75
      *
     A                                     3'OPEN AVAIL BAL:'
     A                                      SPACEB(1)
     A            W@OPEN        22        19
     A                                    42'HELD:'
     A            W@HELD        21        48
     A                                    71'OVERDRAFT:'
     A            W@OVLN        21        82
     A                                      SPACEA(2)
      *
     A
     A          R LE496P2H3
     A                                      SPACEA(1)
     A                                     2'FILE'
     A                                    14'REFERENCE'
     A                                      SPACEA(1)
     A                                     2'REPAYMENTPR'
     A            W@HEAD       119        14
      *
     A                                      SPACEA(1)
     A          R LE496P2HZ
     A            ULINE        130         1UNDERLINE
     A                                      SPACEA(1)
      *
     A
     A          R LE496P2D1                 SPACEA(1)
      *
     A            W@FILE        10         2
     A            W@TREF        11        14
     A                                      SPACEA(1)
     A            W@REPR         8         2
     A            W@DTLS       119        14
     A
     A          R LE496P2T1                 SPACEB(3)
     A
     A                                    40'***  END OF REPORT  ***'
     A
