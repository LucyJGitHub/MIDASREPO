      *****************************************************************
/*S*D****PRTFBASE******************************************************                       CLE164
/*E*S****PR_STD60******************************************************                       CLE164
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000695P1 - Midas LE Settlement Accounts with          *
      *                  'Block All Debits' = 'Y' Report.             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CLE164  *REDUNDANT Date 18Aug14               *
      *  Prev Amend No. CLE134  *CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE164 - CLE134 Enhancement F (Repayment Methodology)        *
      *  CLE134 - Past Due Call Loan Processing                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADER
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2012')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(002)
     A                                     6
     A                                      'REFERENCE LE000695P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      SPACEA(001)
      *
     A                                    37
     A                                      'Settlement Accounts with Block All-
     A                                       Debits = Y report'
     A                                      SPACEA(001)
      *
     A                                    37
     A                                      '-----------------------------------
     A                                      ------------------'
      **************************************************************************
     A          R DETAIL1
     A                                      SPACEB(001)
     A                                     2'Settlement Account:'
     A            P#BRCA    R        O    25REFFLD(BRCA)
     A                                    28'-'
     A            P#ACCY    R        O    29REFFLD(CCYS)
     A                                    32'-'
     A            P#CNUM         6A  O    33
     A                                    39'-'
     A            P#ACOD    R        O    40REFFLD(ACOD)
     A                                    50'-'
     A            P#ACSQ    R        O    51REFFLD(ACSQ)
     A                                    60'Retail Number:'
     A            P#ACNO    R        O    75REFFLD(ACNO)
     A                                      SPACEA(001)
      *
     A                                     2'Customer No./Name:'
     A            P#CUST         6A  O    25
     A            P#CSSN    R        O    32REFFLD(CSSN)
     A                                    60'Account Name:'
     A            P#ANAM    R        O    75REFFLD(ANAM)
     A                                      SPACEA(001)
      *
     A                                     2'Ledger Balance:'
     A            P#LDBL        21A  O    18
     A                                    41'Cleared Balance:'
     A            P#CLBL        21A  O    58
     A                                    81'Minimum Balance:'
     A            P#MINB        12A  O    98
     A                                      SPACEA(001)
      **************************************************************************
     A          R HEADER2
     A                                      SPACEB(001)
     A                                     5'Lending Transactions'
     A                                    28'Next Payment Amount'
     A                                    49'Value Date'
     A                                      SPACEA(001)
     A                                     5'--------------------'
     A                                    28'-------------------'
     A                                    49'----------'
     A                                      SPACEA(001)
      **************************************************************************
     A          R DETAIL2
     A            P#LNRF         6A  O     5
     A  81        P#LTYP         2A  O    12
     A  82        P#FFAC         3A  O    12
     A  82        P#FFNO         2A  O    16
     A  83        P#FACT         5A  O    12
     A  83        P#FSE1         2A  O    18
     A  83        P#FCOD         2A  O    21
     A            P#PRAM        21A  O    26
     A            P#VDAT         7A  O    51
     A                                      SPACEA(001)
      **************************************************************************
     A          R HEADER3
     A                                      SPACEB(001)
     A                                     5'PDCL Transactions'
     A                                    27'Current Principal'
     A                                    46'Outstanding Accr. Int.'
     A                                    70'Link to Original'
     A                                      SPACEA(001)
     A                                     5'-----------------'
     A                                    27'-----------------'
     A                                    46'----------------------'
     A                                    70'----------------'
     A                                      SPACEA(001)
      **************************************************************************
     A          R DETAIL3
     A            P#LNRF         6A  O     5
     A            P#LTYP         2A  O    12
     A            P#CPAM        21A  O    23
     A            P#OAI         21A  O    47
     A            P#LINK         6A  O    73
     A                                      SPACEA(001)
      **************************************************************************
     A          R NODETAIL
     A                                    48'***  NO DETAILS TO REPORT ***'
     A                                      SPACEB(002)
      **************************************************************************
     A          R TRAILER
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
      **************************************************************************
