      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Commitment Fees Detail Report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE000712P1 - Commitments Fees Detail Report             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE037  *CREATE    Date 09Dec03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE037 - Nordea Commitment Fee Update                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
 
      ** Report Header Record Format *
 
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
 
     A                                      SKIPB(003)
     A                                     6'Reference LE000712P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
 
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(002)
 
     A N01                                53'COMMITMENT FEES REPORT'
     A N01                                  SPACEA(1)
     A  01                                53'COMMITMENT FEES REPORT'
     A  01                                81'BRANCH -'
     A  01        RBRCD     R             90REFFLD(BRCA)
     A  01        RBRNM     R             94REFFLD(BRNM)
     A  01                                  SPACEA(1)
 
     A                                    53'----------------------'
     A                                      SPACEA(003)
 
      ** Facility Fee Details
 
     A          R DETAIL1
     A                                      SPACEB(001)
     A                                     2'CUSTOMER NAME'
     A            RCNAM         20A       17
     A                                    43'CUSTOMER NUMBER'
     A            RCNUM          6A       60
     A                                    68'FACILITY'
     A            RFACT          3A       77
     A                                    80'/'
     A            RFCNO          2A       81
     A                                      SPACEA(001)
     A                                     2'FEE SEQ'
     A            RFSEQ          2A       12
     A                                    19'FEE CCY'
     A            RFCCY          3A       27
     A                                    43'PERIOD:  START'
     A            RPSTD          7A       60
     A                                    68'END'
     A            RPEND          7A       77
     A                                      SPACEA(001)
     A                                     2'FEE TYPE'
     A            RFTYP          2A       12
     A            RFTYN         42A       19
     A                                      SPACEA(001)
     A                                     2'LAST PAYMENT DATE'
     A            RLPDT          7A       20
     A                                    36'NEXT PAYMENT DATE'
     A            RNPYD          7A       55
     A                                      SPACEA(002)
 
      ** Fee Detail Header
 
     A          R DETAIL2
     A                                    16'PERIOD'
     A  20N21N22                          28'OUTSTANDING AMOUNT'
     A N20 21N22                          28'   FACILITY AMOUNT'
     A N20N21 22                          28'  AVAILABLE AMOUNT'
     A                                    57'PERCENTAGE'
     A                                    71'RATE'
     A                                    77'NO. DAYS'
     A                                    96'FEE AMOUNT'
     A                                      SPACEA(002)
 
      ** Fee Details
 
     A          R DETAIL3
     A  11        RPRDS          7A       12
     A  11                                19'-'
     A  11        RPRDE          7A       20
     A            RAMNT         19A       28
     A            RPCT           7A       60
     A            RRATE          9A       69
     A  12        RNIDY          3A       81
     A  12                                84'/'
     A  12        RNDYS          3A       85
     A            RFAMT         19A       91
     A                                      SPACEA(001)
 
      ** Fee Detail Header for Tiered computations
 
     A          R DETAIL4
     A                                    16'PERIOD'
     A  20N21N22                          28'OUTSTANDING AMOUNT'
     A N20 21N22                          28'   FACILITY AMOUNT'
     A N20N21 22                          28'  AVAILABLE AMOUNT'
     A                                    53'  TIERED CALC'
     A                                    77'PERCENTAGE'
     A                                    95'RATE'
     A                                   102'NO. DAYS'
     A                                   118'FEE AMOUNT'
     A                                      SPACEA(002)
 
      ** Fee Details for Tiered computations
 
     A          R DETAIL5
     A  11        RPRDS          7A       12
     A  11                                19'-'
     A  11        RPRDE          7A       20
     A            RAMNT         19A       28
     A            RTAMT         19A       48
     A            RPCT           7A       80
     A            RRATE          9A       90
     A  12        RNIDY          3A      103
     A  12                               106'/'
     A  12        RNDYS          3A      107
     A            RFAMT         19A      110
     A                                      SPACEA(001)
 
      ** Total for fee
 
     A          R TOTAL
 
     A                                      SPACEB(001)
     A                                    66'TOTAL'
     A            RTNIDY         3A       81
     A                                    84'/'
     A            RNDYS          3A       85
     A            RTFAMT        19A       91
     A                                      SPACEA(001)
 
      ** Total for fee (Tiered calculations)
 
     A          R TOTAL2
 
     A                                      SPACEB(001)
     A                                    66'TOTAL'
     A            RTNIDY         3A      103
     A                                   106'/'
     A            RNDYS          3A      107
     A            RTFAMT        19A      110
     A                                      SPACEA(001)
 
      ** No records to report
 
     A          R NOREC
 
     A                                      SPACEB(003)
     A                                    51'  No Records to report '
     A                                      SPACEA(003)
     A                                    51'***  END OF REPORT  ***'
 
      ** Report Trailer
 
     A          R TRAILP1
     A  01                                46'***  END OF REPORT FOR BRANCH'
     A  01        RBRCD     R             +1REFFLD(BRCA)
     A  01                                +2'***'
 
     A N01                                51'***  END OF REPORT  ***'
