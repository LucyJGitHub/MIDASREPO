      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Add part sold records to facility history')   *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE0008P1 - Printer File for Initialisation Program      *
      *                  LE0008 (Add Parts Sold records to FACHISA)   *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 30Nov05               *
      * Midas Release 4.01 -------------------------------------------*
      *                 199570 *CREATE     Date 30May02               *
      *                                    Date                       *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  199570 - Printer file to show facility with missing parts    *
      *           sold events in Facility History File (Audit mode)   *
      *           or records added by LE0008 (update mode).           *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
      *
     A          R HEADER1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     5
     A                                      'Reference LE0008P1'
     A            BJURPT        53A  O    36
     A                                   100
     A                                      'Date'
     A            BJMRDT         7A  O   105
     A                                   121
     A                                      'Page'
     A                                    +2
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A N16                                 5
     A                                      'Audit Mode'
     A                                      SPACEB(001)
     A  16                                 5
     A                                      'Update Mode'
     A                                      SPACEB(001)
     A N16                                27
     A                                      'Facilities with Parts +
     A                                       Sold Events missing +
     A                                       from the history file'
     A                                      SPACEB(001)
     A  16                                27
     A                                      'Records for Parts Sold +
     A                                       Events which were added +
     A                                       to the facility history'
     A                                      SPACEB(001)
     A N16                                27
     A                                      '-------------------------+
     A                                      --------------------------+
     A                                      ------------'
     A                                      SPACEB(001)
     A  16                                27
     A                                      '-------------------------+
     A                                      --------------------------+
     A                                      -------------------'
     A                                      SPACEB(001)
     A                                     3
     A                                      'Branch'
     A                                      SPACEB(001)
     A                                    10
     A                                      'Customer'
     A                                    19
     A                                      'Facility'
     A                                    28
     A                                      'Ccy'
     A                                    32
     A                                      'MCcy'
     A                                    37
     A                                      'Rev.'
     A                                    43
     A                                      'Loan'
     A                                    50
     A                                      'Act'
     A                                    59
     A                                      'Amount in '
     A                                    79
     A                                      'Date'
     A                                    89
     A                                      'Fixed Rate'
     A                                      SPACEA(001)
     A                                    37
     A                                      'Cred'
     A                                    59
     A                                      'Loan Ccy'
     A                                      SPACEA(001)
      *
      ** Report Detail 1
      *
     A          R DETAIL1
     A                                      SPACEA(001)
     A            FBRCA          3A  O     5
      * Begin /CSD027/
     A**********  FCNUM          6S 0O    11                                                  CSD027
     A            FCNUM          6A  O    11
      * End /CSD027/
     A            FACT           3S 0O    20
     A                                    23
     A                                      '/'
     A            FCNO           2S 0O    24
     A            FCCY           3A  O    28
     A            MCCY           1A  O    33
     A            RVCR           1A  O    38
      *
      ** Report Detail 2
      *
     A          R DETAIL2
     A                                      SPACEA(001)
      **Start of change CLE148
     A**********  LNRF           6S 0O    42                                                  CLE148
     A            LNRF           6A  O    42
      **End of change CLE148
     A            AMTP           2A  O    50
     A  60        SPRAM0        13S 0O    53EDTWRD(' ,   ,   ,   , 0 -')
     A  61        SPRAM1        13S 1O    53EDTWRD('   ,   ,   , 0 . -')
     A  62        SPRAM2        13S 2O    53EDTWRD('  ,   ,   , 0 .  -')
     A  63        SPRAM3        13S 3O    53EDTWRD(' ,   ,   , 0 .   -')
     A            CCY            3A  O    73
     A            WADATE         7A  O    78
     A            FCXR          13S 8O    85EDTWRD('  , 0 .        -')
     A            FMDI           1A  O   101
      *
      ** Report Summary
      *
     A          R TRAILER1
     A                                    35
     A                                      'Number of Facilities read '
     A                                      SPACEB(003)
     A                                    81'='
     A            FACAUD         5S 0O    84
     A                                      EDTCDE(3)
     A N16                                32
     A                                      'Number of Facilities with +
     A                                      missing events'
     A                                      SPACEB(001)
     A  16                                32
     A                                      'Number of Updated Facilities'
     A                                      SPACEB(001)
     A                                    81'='
     A            FACERR         5S 0O    84
     A                                      EDTCDE(3)
      *
     A  16                                32
     A                                      'Number of Records Added +
     A                                      to Facility History'
     A                                      SPACEB(001)
     A  16                                81'='
     A  16        FACREC         5S 0O    84
     A                                      EDTCDE(3)
     A N12                                51
     A                                      '   ***  End of report  ***'
     A                                      SPACEB(003)
     A  12                                51
     A                                      '***  No details to report+
     A                                        ***'
     A                                      SPACEB(003)
      *
      ** Database Error
      *
     A          R DBERROR1
     A                                      SKIPB(17)
     A                                    34'**** Database error ****'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SPACEA(1)
     A                                    20'*    Error'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    Key'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    in file '
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    Program '
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SKIPA(43)
     A                                    35'*** End of report ***'
