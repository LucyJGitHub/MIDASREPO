      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Fee Rate Change Amendment Report - PRTF')     *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE001200P1 - Print File for Fee Rate Change Amendment   *
      *                    Audit Report                               *
      *                                                               *
      *  (c) Finastra International Limited 2018                      *
      *                                                               *
      *  Last Amend No. CLE071  *CREATE    Date 18Jul18               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE071 - Value Dating of Rate Changes to Fees                *
      *                                                               *
      *****************************************************************
     A          R LE001200H1                TEXT('(c) Finastra Interna +
     A                                      tional 2018')
      *
     A                                      SKIPB(3)
     A                                     2'REFERENCE LE001200P1'
     A            BJURPT        53        33
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
      ** Main Heading
      *
     A            RHEAD         30        39
     A                                      SPACEA(1)
     A  20                                39'-----------------------------'
     A  21                                39'----------------------------'
     A                                      SPACEA(2)
      *
      *
      ** Sub Heading
      *
     A                                     2'Branch'
     A                                    10'Customer'
     A                                    20'Loan/'
     A                                    27'Seq'
     A                                    32'Value'
     A                                    40'CCY'
     A                                    45'Calc'
     A                                    51'Rate'
     A                                    60'Amount'
     A                                    72'Perc'
     A                                    77'Input User'
     A                                    89'Amend User'
     A                                   101'Authorise'
     A                                   112'Last Change'
     A                                       SPACEA(1)
      *
     A                                    20'Fclty'
     A                                    32'Date'
     A                                    45'Type'
     A                                    72'Ind'
     A                                   136'User'
     A                                   112'Type'
     A                                      SPACEA(1)
      *
     A                                     2'------'
     A                                    10'--------'
     A                                    20'-----'
     A                                    27'---'
     A                                    32'-----'
     A                                    40'---'
     A                                    45'----'
     A                                    51'----'
     A                                    60'------'
     A                                    72'----'
     A                                    77'----------'
     A                                    89'----------'
     A                                   101'---------'
     A                                   112'-----------'
     A                                       SPACEA(1)
      *
      ** Details
      *
     A          R LE001200D1                SPACEA(1)
      *
      ** Branch Code
     A            RBRCA          3         2
      *
      ** Customer Number
     A            RCUST          6        10
      *
      ** Facilty Type/Faciltiy No or Loan
     A N15        RFACL          6        20
     A  15        RLOAN          6        20
      *
      ** Fee Sequence
     A            RSEQ           2        28
      *
      ** Fee Rate Change Value Date
     A            RCVD           7        32
      *
      ** Currency code
     A            RCCY           3        40
      *
      ** Calculation Type
     A            RCALT          2        45
      *
      ** Fee Rate
     A            RFRAT          8        51
      *
      ** Fee Amount
     A            RFAMT         11        60
      *
      ** Percentage Indicator
     A            RPIND          1        74
      *
      ** Input User
     A            RIUSER        10        77
      *
      ** Amend User
     A            RAUSER        10        89
      *
      ** Authorise user
     A            RATUSER       10       101
      *
      ** Last Change Type
     A            RLCTP         10       112
      *
      ** End of the Report
      *
     A          R LE001200T1                SPACEB(2)
     A                                    56'*** END OF REPORT ***'
      *
     A          R LE001200T2                SPACEB(2)
     A                                    56'*** NO RECORDS FOUND ***'
