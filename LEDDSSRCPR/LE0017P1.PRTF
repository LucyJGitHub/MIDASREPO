      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Unmatched facility history report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE0017P1 - Print File for Facility History Utility      *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 216822  *CREATE    Date 29May03               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  216822 - Print a report showing records which have been      *
      *           dropped from facility history.                      *
      *                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADER1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A                                      SKIPB(3)
     A                                     6'Reference'
     A                                    16'LE0017P1'
      *
     A            BJURPT        53        30
     A                                   100'Date'
     A            BJMRDT         7       105
     A                                   119'Page'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                    56'Unmatched Facility History Report'
     A                                      SPACEB(2)
     A                                    56'----------------------------------'
     A                                      SPACEB(1)
     A          R HEADER2
     A                                      SPACEB(3)
     A                                     4'Branch'
     A                                    15'Customer'
     A                                    29'Facility'
     A                                    45'Expiry Date'
     A                                    61'Currency'
     A                                    71'Original Facility Amount'
     A                                   100'No. of FACHISA Records Dropped'
     A                                      SPACEA(3)
     A          R DETAIL1
     A            FHBRCH         3         6
      * Begin /CSD027/
     A**********  FHCNUM         6  0     15                                                  CSD027
     A            FHCNUM         6A       15
      * End /CSD027/
     A            FHFTYP         3  0     29
     A                                    32'/'
     A            FHFSEQ         2  0     33
     A            EXPDAT         7        45
     A            FHFCCY         3        61
     A            ZFIELD        16        71
     A            FADROP        13  0    105
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
     A          R TRAILER1
     A                                      SPACEB(3)
     A                                    20'Total Number of Facilities Read +
     A                                      from History Header File'
     A            FHTOT         13  0     77
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
     A                                    20'Number of Facilities for which +
     A                                      Records have been Dropped'
     A            FHDROP        13  0     77
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
     A                                    20'Total Number of Records Dropped +
     A                                      from FACHISA'
     A            FADTOT        13  0     77
     A                                      EDTCDE(3)
     A                                      SPACEA(3)
     A                                    40'***  End of Report  ***'
