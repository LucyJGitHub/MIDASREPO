      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Initialisation Program for CAS005 Report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE0019P1 - Initialisation Program for CAS005 Report     *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS005  *CREATE    Date 16Dec02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE0019P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'Interest Calculations Using Net +
     A                                      Treasury Rate'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'--------------------------------+
     A                                      -------------'
     A                                      SPACEA(2)
      *
     A                                     2'Event'
     A                                     9'Value'
     A                                    17'Days'
     A                                    22'ICBS'
     A                                    34'Event'
     A                                    50'Principal'
     A                                    64'Interest'
     A                                    76'Net Treasury'
     A                                    96'Calculated Int Amount Using'
     A                                      SPACEA(1)
     A                                     9'Date'
     A                                    34'Amount'
     A                                    51'Amount'
     A                                    67'Rate'
     A                                    80'Rate'
     A                                    94'Interest Rate'
     A                                   112'Net Treasury Rate'
     A                                      SPACEA(2)
      *
     A          R SUBHP1
     A                                     1'Loan Number:'
     A            RLNRF          6A       14
     A                                    21'Customer:'
     A            RCUST          6A       31
     A                                    41'Currency:'
     A            RCCY           3A       51
     A                                    58'Margin Rate:'
     A            RMARG          7S 0     71EDTWRD('0 .     -')
     A                                      SPACEA(2)
      *
     A          R DETAIL1
     A                                      SPACEA(1)
     A            REVNT          5A        2
     A            RVDAT          7A        8
     A            RNDAY          5A       17
     A            RCALC          1A       23
     A            REAMT         17A       28
     A            RPRIN         17A       46
     A            ROINTR        10S 0     64EDTWRD(' 0 .       -')
     A            RINTR         10S 0     77EDTWRD(' 0 .       -')
     A            RAAMT         17A       92
     A            RNAMT         17A      112
      *
     A          R ERROR1                    SPACEA(1)
     A                                    90'FROM CLOANCL'
      *
     A          R BLANK1                    SPACEA(1)
     A                                   132' '
      *
     A          R TRAILP1                   SPACEB(3)
     A                                    45'No. or Loans In Error'
     A            WCOUNT         5S       72
     A                                      SPACEA(2)
     A                                    51'***  END OF REPORT  ***'
