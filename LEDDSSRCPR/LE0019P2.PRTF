      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Initialisation Program for CAS005 Report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE0019P2 - Initialisation Program for CAS005 Report     *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS005  *CREATE    Date 16Dec02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADP2                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE0019P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'Interest Calculations Using Net +
     A                                      Treasury Rate'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'--------------------------------+
     A                                      -------------'
     A                                      SPACEA(2)
      *
     A                                     2'Event'
     A                                     9'Value'
     A                                    18'Accrued Interest to Control Date'
     A                                    57'Accrued Interest Posted to Date'
     A                                    94'Interest Paid/Received to Date'
     A                                      SPACEA(1)
      *
     A                                     9'Date'
     A                                    19'All in Rate'
     A                                    36'Net Treasury Rate'
     A                                    58'All in Rate'
     A                                    74'Net Treasury Rate'
     A                                    97'All in Rate'
     A                                   111'Net Treasury Rate'
     A                                      SPACEA(2)
      *
     A          R SUBHP2
     A                                     1'Loan Number:'
     A            RLNRF          6A       14
     A                                    21'Customer:'
     A            RCUST          6A       31
     A                                    41'Currency:'
     A            RCCY           3A       51
     A                                    58'Margin Rate:'
     A            RMARG          7S 0     71EDTWRD('0 .     -')
     A                                    85'Calc Method:'
     A            RCALC          1A       99
      *
     A                                      SPACEA(2)
      *
     A          R DETAIL2
      *
     A                                      SPACEA(1)
     A            REVNT          5A        2
     A            RVDAT          7A        8
     A            RAAITC        17A       17
     A  32                                34'*'
     A            RNAITC        17A       36
     A            RAAIPD        17A       55
     A  33                                72'*'
     A            RNAIPD        17A       74
     A            RAIPRD        17A       93
     A  34                               110'*'
     A            RNIPRD        17A      112
      *
     A          R ERROR2
     A                                     5'From CLOANCL'
     A            RCAITC        17A       17
     A            RCAIPD        17A       55
     A            RCIPRD        17A       93
      *
     A          R BLANK2                    SPACEA(1)
     A                                   132' '
      *
     A          R TRAILP2                   SPACEB(3)
     A                                    45'No. or Loans In Error'
     A            WCOUNT         5S       72
     A                                      SPACEA(2)
     A                                    51'***  END OF REPORT  ***'
