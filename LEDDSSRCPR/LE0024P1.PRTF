      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Calculation for AIPN, IPRN & AITN in CLOANCL')
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  PRTF/LE0024P1 - Calculation for AIPN, IPRN & AITN in CLOANCL *
      *                  Report                                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS005  *CREATE    Date 26Aug03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE0024P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    36'Interest Calculations Using Net +
     A                                      Treasury Rate'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'--------------------------------+
     A                                      -------'
     A                                      SPACEA(2)
      *
     A                                     2'Event'
     A                                     8'Val.Date'
     A                                    18'Days/ICBS'
     A                                    29'Principal'
     A                                    43'Interest Rate'
     A                                    60'Calc Interest'
     A                                    84'AITN'
     A                                   103'AIPN'
     A                                   121'IPRN'
     A                                      SPACEA(2)
      *
     A          R HEADP2
     A                                     1'Loan Number:'
     A            RLNRF          6A       14
     A                                    21'Customer:'
     A            RCUST          6A       31
     A                                    41'Currency:'
     A            RCCY           3A       51
     A                                    58'Margin Rate:'
     A            RMARG          7S 0     71EDTWRD('0 .     -')
     A N35                                  SPACEA(2)
      *
     A  35                                84'* * *    NOT FOUND    * * *'
     A  35                                  SPACEA(2)
      *
      *
     A          R DETAIL
      *
     A                                      SPACEA(1)
     A            REVNT          5A        2
     A            RVDAT          7A        8
     A            RNDAY          5A       16
     A            RCALC          2A       22
     A            RPRIN         17A       25
     A            RINTR         10S 0     44EDTWRD(' 0 .       -')
     A            RCALI         16A       58
     A            RAITCM        17A       76
     A  32                                93'*'
     A            RAIPDM        17A       94
     A  33                               112'*'
     A            RIPRDM        17A      113
     A  34                               130'*'
      *
     A          R DETAIL2
      *
     A                                      SPACEA(1)
     A                                    58'FROM CLOANCL'
     A            RAITC         17A       76
     A            RAIPD         17A       94
     A            RIPRD         17A      113
      *
     A          R BLANK                     SPACEA(1)
     A                                   132' '
      *
     A          R TRAILP1                   SPACEB(3)
     A                                    45'No. of Records in Error'
     A            WCOUNT         5S       72
     A                                      SPACEA(2)
     A                                    51'***  END OF REPORT  ***'
