      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE EIR Calculation Report')                      *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE004611P1 - EIR Calculation Report                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. 262628  *CREATE    Date 03Apr09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  262628 - Produce an audit report showing the cash flows      *
      *           sent to ZAEIRCALC for the calculation of the EIR.   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      * -------------------------------*
      ** Report Header Record Format 1 *
      *--------------------------------*
      *
     A          R LE004611H1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2010')
     A                                      SKIPB(3)
     A                                     6'Reference LE004611P1'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    35'EFFECTIVE INTEREST RATE CALCULATIO'
     A                                    +0'N DETAILS'
     A                                      SPACEA(1)
     A                                    35'----------------------------------'
     A                                    +0'---------'
      *
      * -------------------------------*
      ** Report Header Record Format 2 *
      *--------------------------------*
      *
     A          R LE004611H2
     A                                      SPACEB(2)
      *
     A                                     2'Loan Ref.:'
     A            RLNRF          6A       +2TEXT('LOAN NUMBER')
     A                                    +4'Customer:'
     A            RCNUM          6A       +2TEXT('CUSTOMER')
     A                                    +4'Currency:'
     A            RCCY           3A       +2TEXT('CURRENCY')
     A  50                                +4'Fee:'
     A  50        RFSEQ          2A       +2TEXT('FEE SEQ')
     A                                    +4'EIR Period:'
     A            RSTPD          7A       +2TEXT('EIR START DATE')
     A                                    +2'-'
     A            RENPD          7A       +2TEXT('EIR END DATE')
      *
     A                                     3'Cash Flow'
     A                                      SPACEB(2)
     A                                    20'Cash Flow'
     A                                    39'Sign'
     A                                    45'Amount'
      *
     A                                     6'Date'
     A                                      SPACEB(1)
     A                                    22'Amount'
     A                                    46'Type'
     A                                      SPACEA(1)
      *
      *--------------------------------*
      ** Report Detail Record Format 1 *
      *--------------------------------*
      *
     A          R LE004611D1
     A                                      SPACEB(1)
      *
     A            RCDATE         7A        4TEXT('CASHFLOW DATE')
     A            RCSFL         21A       15TEXT('CASH FLOW AMOUNT')
     A            RSIGN          1A       41TEXT('AMOUNT SIGN')
     A            RTYPE          4A       46TEXT('AMOUNT TYPE')
      *
      *--------------------------------*
      ** Report Detail Record Format 2 *
      *--------------------------------*
      *
     A          R LE004611D2
     A                                      SPACEB(1)
      *
     A                                     2'EIR Calculated:'
     A            REIR          21A       +2TEXT('EIR')
      *
      *------------------------------*
      ** Report Footer Record Format *
      *------------------------------*
      *
     A          R LE004611F1
     A                                      SPACEB(3)
      *
     A N51                                48'*** NO DETAILS TO REPORT ***'
     A N51                                  SPACEA(3)
      *
     A                                    51'*** END OF REPORT ***'
