      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas LE EIR & Cash Flows Audit Report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE004621AU - EIR & Cash Flows Audit Report              *
      *                                                               *
      *  (c) Finastra International Limited 2006                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CAS016  *CREATE    Date 28Feb06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS016 - IAS18 EIR Recalculation (Fee Amortisation Over      *
      *           Whole Period)                                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R LE004621HA                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2006')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE004621AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    37'EFFECTIVE INTEREST RATE DETAILS'
     A                                    +1'(NON-LINEAR) - AUDIT'
     A                                      SPACEA(1)
     A                                    37'-------------------------------'
     A                                    +0'---------------------'
     A                                      SPACEA(1)
      *
      *-----------------------*
      ** No of Records Read   *
      *-----------------------*
      *
     A          R DTLS
     A                                    50'RECORDS CALCULATED:'
     A                                      SPACEB(10)
     A            RCOUNT         5S 0     +1EDTCDE(3)
     A                                    51'***  END OF REPORT ***'
     A                                      SPACEB(3)
      *
      *-----------------------*
      ** No Details to Report *
      *-----------------------*
      *
     A          R NODTLS
     A                                    46'*** No Details to report +
     A                                      ***'
     A                                      SPACEB(10)
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
