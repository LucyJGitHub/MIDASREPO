      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE EIR & Cash Flows Details Report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE004621P2 - EIR & Cash Flows Details Report            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CAS016  *CREATE    Date 28Feb06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS016 - IAS18 EIR Recalculation (Fee Amortisation Over      *
      *           Whole Period)                                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      * -------------------------------*
      ** Report Header Record Format 2 *
      *--------------------------------*
      *
     A          R LE004621H3                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                      SKIPB(3)
     A                                     6'Reference LE004621P2'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    35'EFFECTIVE INTEREST RATE CASH FLOWS'
     A                                    +1'DETAILS (NON-LINEAR)'
     A                                      SPACEA(1)
     A                                    35'----------------------------------'
     A                                    +0'---------------------'
     A                                      SPACEA(2)
     A  37                                 6'BRANCH:'
     A  37        RBRCA          3A       +1TEXT('BRANCH')
     A  37        RBRNM         30A       +2TEXT('BRANCH DESCRIPTION')
     A  37                                  SPACEA(1)
      *
      *--------------------------------*
      ** Report Detail Record Format 2 *
      *--------------------------------*
      *
     A          R LE004621D4
      *
     A                                      SPACEB(2)
     A                                     2'Loan Ref.'
     A                                    14'Loan Type/'
     A                                    27'Customer'
     A                                    37'Currency'
     A                                    50'Current Balance'
     A                                    75'Current EIR'
     A                                      SPACEA(1)
     A                                    15'Sub Type'
     A                                      SPACEA(1)
     A            RLNRF          6A        4TEXT('LOAN NUMBER')
     A            RLTYP          5A       16TEXT('LOAN TYPE')
     A            RCNUM          6A       28TEXT('CUSTOMER')
     A            RCCY           3A       40TEXT('CURRENCY')
     A            RCBAL         21A       47TEXT('CURRENT BALANCE')
     A            REIR          21A       76TEXT('EIR')
     A                                      SPACEA(2)
      *
     A                                     3'Cash Flow'
     A                                    20'Cash Flow'
     A                                    39'Amount'
     A                                    49'Part'
     A                                    60'Interest'
     A                                    74'Calc'
     A                                    80'Yield'
     A                                    90'Discount'
     A                                      SPACEA(1)
     A                                     6'Date'
     A                                    22'Amount'
     A                                    40'Type'
     A                                    49'Sold'
     A                                    74'Meth'
     A                                    80'Curve'
     A                                      SPACEA(2)
      *
      *--------------------------------*
      ** Report Detail Record Format 3 *
      *--------------------------------*
      *
     A          R LE004621D5
      *
     A            RCDATE         7A        4TEXT('CASHFLOW DATE')
     A            RCSFL         21A       15TEXT('CASH FLOW AMOUNT')
     A            RTYPE          4A       40TEXT('AMOUNT TYPE')
     A            RSOLD          6A       48TEXT('PART SOLD')
     A            RINTR         14A       57TEXT('INTEREST')
     A            RCALC          1A       75TEXT('CALCULATION METHOD')
     A            RYIELD         5A       80TEXT('YIELD')
     A            RDISC         14A       88TEXT('DISCOUNT')
     A                                      SPACEA(1)
      *
      *------------------------------*
      ** Report Footer Record Format *
      *------------------------------*
      *
     A          R LE004621F2
     A                                      SPACEB(3)
      *
     A  37                                48'*** END OF REPORT FOR BRANCH'
     A  37        RBRCA          3A       +1TEXT('BRANCH')
     A  37                                +1'***'
     A N37                                51'*** END OF REPORT ***'
