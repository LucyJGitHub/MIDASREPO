      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Non-Linear Fee Amortisation Report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE004650P1 - Non-Linear Fee Amortisation Report         *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE042             Date 06Sep05               *
      *                 CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A                                      INDARA
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE004650P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    48'FEE ACCOUNT KEYS GENERATED +
     A                                      (NON-LINEAR)'
     A                                      SPACEA(1)
     A                                    48'---------------------------+
     A                                      ------------'
     A                                      SPACEA(2)
     A  32                                 6'BRANCH:'
     A  32        RBRCA     R             15REFFLD(BRCA)
     A  32        RBRNM     R             19REFFLD(BRNM)
     A  32                                  SPACEA(2)
      *
     A                                    10'CUSTOMER'
     A                                    25'LOAN'
     A                                    33'FEE SEQ'
     A                                    43'ACCOUNT KEY'
     A                                    65'AMOUNT'
     A                                    80'CURRENCY'
     A                                    92'REV IND'
     A                                      SPACEA(1)
      *
     A                                    10'--------'
     A                                    24'------'
     A                                    33'-------'
     A                                    43'-----------'
     A                                    65'------'
     A                                    80'--------'
     A                                    92'-------'
     A                                      SPACEA(1)
      *
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
      *
     A          R DETAIL11
      *
     A                                      SPACEB(1)
     A            RCNUM          6        11
     A            RLOAN          6        24
     A            RFSEQ          2        35
     A*                                              Begin /CLE042/
     A************RAKEY*********10        43                                                  CLE042
     A            RAKEY         14        42                                                  CLE042
     A*                                              End   /CLE042/
     A            RAMNT         18        58
     A            RCCY           3        83
     A            RRIND          1        95
      *
     A          R DETAIL12
      *
     A                                      SPACEB(3)
     A                                    32'TOTAL ACCOUNT KEYS GENERATED'
     A            RFEESCNT       5  0     67EDTCDE(3)
      *
     A          R DETAIL13
      *
     A                                      SPACEB(3)
     A                                    52'***  NO DETAILS TO REPORT  ***'
     A          R DETAIL14
      *
     A                                      SPACEB(3)
     A                                    47'*** REFERENCE LE004650 DATABASE ER+
     A                                      ROR ***'
      *
      *-----------------*
      ** Report Trailer *
      *-----------------*
      *
     A          R TRAILP1
     A                                      SPACEB(3)
     A N32                                54'***  END OF REPORT  ***'
     A  32                                47'***  END OF REPORT FOR BRANCH'
     A  32        RBRCA     R             77REFFLD(BRCA)
     A  32                                82'***'
