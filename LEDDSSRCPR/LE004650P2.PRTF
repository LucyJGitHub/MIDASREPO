      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Non-Linear Discount Amortisation Report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE004650P2 - Non-Linear Discount Amortisation Report    *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE042             Date 06Sep05               *
      *                 CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A                                      INDARA
     A          R HEADP2                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE004650P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    48'LOAN ACCOUNT KEYS GENERATED +
     A                                      (NON-LINEAR)'
     A                                      SPACEA(1)
     A                                    48'----------------------------+
     A                                      ------------'
     A                                      SPACEA(2)
     A  32                                 6'BRANCH - '
     A  32        RBRCA     R             15REFFLD(BRCA)
     A  32        RBRNM     R             19REFFLD(BRNM)
     A  32                                  SPACEA(2)
      *
     A                                    10'CUSTOMER'
     A                                    20'ORIG BORROWER'
     A                                    38'LOAN NO'
     A                                    50'ACCOUNT KEY'
     A                                    70'AMOUNT'
     A                                    85'CURRENCY'
     A                                    95'REV IND'
     A                                      SPACEA(1)
      *
     A                                    10'--------'
     A                                    20'-------------'
     A                                    38'-------'
     A                                    50'-----------'
     A                                    70'------'
     A                                    85'--------'
     A                                    95'-------'
     A                                      SPACEA(1)
      *
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
      *
     A          R DETAIL21
      *
     A                                      SPACEB(1)
     A            RCNUM          6        11
     A            ROLNO          6        23
     A            RLOAN          6        38
     A*                                              Begin /CLE042/
     A************RAKEY*********10        50                                                  CLE042
     A            RAKEY         14        49                                                  CLE042
     A*                                              End   /CLE042/
     A            RAMNT         18        65
     A            RCCY           3        88
     A            RRIND          1        98
      *
     A          R DETAIL22
      *
     A                                      SPACEB(3)
     A                                    32'TOTAL ACCOUNT KEYS GENERATED'
     A            RLOANCNT       5  0     67EDTCDE(3)
      *
     A          R DETAIL23
      *
     A                                      SPACEB(3)
     A                                    52'***  NO DETAILS TO REPORT  ***'
     A          R DETAIL24
      *
     A                                      SPACEB(3)
     A                                    47'*** REFERENCE LE004650 DATABASE +
     A                                       ERROR ***'
      *
      *-----------------*
      ** Report Trailer *
      *-----------------*
      *
     A          R TRAILP2
     A                                      SPACEB(3)
     A N32                                54'***  END OF REPORT  ***'
     A  32                                47'***  END OF REPORT FOR BRANCH'
     A  32        RBRCA     R             77REFFLD(BRCA)
     A  32                                82'***'
