      *****************************************************************
/*S*D****PRTFBASE******************************************************                       CLE164
/*E*S****PR_STD60******************************************************                       CLE164
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *   PRTF/LE007000P1 - LE PDCL Standing Data Validation          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CLE164  *REDUNDANT Date 18Aug14               *
      *  Prev Amend No. CLE134  *CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE164 - CLE134 Enhancement F (Repayment Methodology)        *
      *  CLE134 - Past Due Call Loan Processing                       *
      *                                                               *
      *****************************************************************
      *
      ** Main Headings...
      *
     A          R LE07000H1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2012')
     A                                      SKIPB(3)
      *
     A                                     6'REFERENCE'
     A                                    16'LE007000P1'
      *
     A            BJURPT        53        40
      *
     A                                   100'DATE'
     A            BJMRDT         7       105
      *
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    35'Past Due Call Loans +
     A                                       Standing Data Validation'
     A  88                                  SPACEA(2)
      *
     A N88                                83'BRANCH'
     A N88        P1BRCA         3A       90
     A N88                                94'-'
     A N88        P1BRNM        30A       96
     A N88                                  SPACEA(2)
      *
     A N88                                 2'Item'
     A N88                                36'Value'
     A N88                                61'Error Details'
     A N88                               112'File'
     A N88                                  SPACEA(1)
      *
     A N88                                 2'----'
     A N88                                36'-----'
     A N88                                61'-------------'
     A N88                               112'----'
     A N88                                  SPACEA(1)
      *
      ** Key Details...
      *
     A          R LE07000H2
     A  70                                  SPACEB(1)
     A            P1FLD0        30A        2
     A  71                                  HIGHLIGHT
     A            P1VAL0        24A       36
     A  71                                  HIGHLIGHT
     A            P1MSG0        50A       61
     A            P1FIL0        21A      112
     A                                      SPACEA(1)
      *
      ** Detail Lines...
      *
     A          R LE07000D1                 SPACEA(1)
      *
     A            P1FLD1        30A        4
     A            P1VAL1        24A       36
     A            P1MSG1        50A       61
     A            P1FIL1        21A      112
      **
     A          R LE07000D2                 SPACEA(1)
      *
     A            P1MSG2        50A       61
      *
      ** Footer Lines...
      *
     A          R LE07000T1                 SPACEB(3)
     A
     A                                    36'***  NO DETAILS TO REPORT  ***'
      *
     A          R LE07000T2                 SPACEB(3)
     A
     A                                    40'***  END OF REPORT  ***'
