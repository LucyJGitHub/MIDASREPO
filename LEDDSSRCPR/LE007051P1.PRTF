      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Loans PDCL amended maturity date')            *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE007051P1 - PDCL maturity                              *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR700107 *CREATE   Date 01Aug12               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR700107 - Past Due Call Loan cannot mature if there is      *
      *             outstanding interest/principal and account has    *
      *             block all debit Y. (Child: AR700108)              *
      *                                                               *
      *****************************************************************
     A          R LE0705H1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2012')
     A                                      SKIPB(001)
     A                                      SPACEA(002)
     A                                     6
     A                                      'REFERENCE'
     A                                      SPACEB(002)
      *
     A                                    16
     A                                      'LE007051P1'
      *
     A            BJURPT        53        40
     A                                   100
     A                                      'DATE'
     A            BJMRDT         7       105
     A                                   119
     A                                      'PAGE'
     A            PAGE           4  0    124
     A                                      EDTCDE(Z)
      *
     A                                    49
     A                                      'Past Due Call Amended Maturity Dat-
     A                                      e'
     A                                      SPACEB(002)
      *
     A          R LE0705H2
      *
     A                                      SPACEB(001)
     A                                    10
     A                                      ' Loan  '
     A                                    20
     A                                      'Outstanding Principal'
     A                                    42
     A                                      ' Outstanding Interest'
     A                                    65
     A                                      'Old Mat Dt'
     A                                    77
     A                                      'New Mat Dt'
     A                                    89
     A                                      'Sett Type'
     A                                   100
     A                                      'Settlement Account'
     A                                   120
     A                                      'Block DR'
     A                                    10
     A                                      '-------'
     A                                      SPACEB(001)
     A                                    20
     A                                      '---------------------'
     A                                    42
     A                                      '---------------------'
     A                                    65
     A                                      '----------'
     A                                    77
     A                                      '----------'
     A                                    89
     A                                      '---------'
     A                                   100
     A                                      '------------------'
     A                                   120
     A                                      '--------'
      *
     A          R LE0705D1
     A                                      SPACEB(001)
      *
     A            OLNRF          6        11
     A            OCPAM         21        20
     A            OINTD         21        42
     A            OOMDAT         7        66
     A            ONMDAT         7        78
     A            ORSTM          2        92
     A            ORONS         18       100
     A            WBLCKD         1       124
      *
     A          R LE0705T1
     A                                      SPACEB(001)
      *
     A                                    55
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(002)
      *
