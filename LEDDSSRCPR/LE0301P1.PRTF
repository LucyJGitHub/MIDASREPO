      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Internal fclty ref for synd agent fclties')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE0301P1 - Midas LE Internaly Facility References for   *
      *                  Syndicated Agent Facilities - Detail Report  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Last Amend No. 161257  *CREATE    Date 21Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  161257 - Profitability and facility averages not showing     *
      *           for syndicated loan fees.                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE0301P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    21'INTERNAL FACILITY +
     A                                      REFERENCES FOR SYNDICATED +
     A                                      AGENT FACILITIES'
      *
     A  37                                87'BRANCH -'
     A  37        A8BRCD    R             96REFFLD(BRCA)
     A  37        A8BRNM    R            100REFFLD(BRNM)
      *
     A                                    21'------------------+
     A                                      --------------------------+
     A                                      ----------------'
     A                                      SPACEB(1)
     A                                      SPACEA(3)
      *
     A                                     6'FACILITY TYPE:'
     A            RFTYP     R             21REFFLD(FACT)
     A            RFCNM     R             25REFFLD(SPI1)
     A                                      SPACEA(2)
      ***  Sub-Heading:
      ***  ------------
      *
     A                                    63'TR/'
     A                                    67'START'
     A                                    75'EXPIRY'
     A                                    84'INTERNAL'
     A                                   118'START'
     A                                   126'EXPIRY'
     A                                      SPACEA(1)
     A                                     6'AGENT FCLTY'
     A                                    21'CUSTOMER NAME'
     A                                    43'CCY'
     A                                    55'AMOUNT'
     A                                    63'CA'
     A                                    67'DATE'
     A                                    75'DATE'
     A                                    84'FACILITY'
     A                                    99'CCY'
     A                                   110'AMOUNT'
     A                                   118'DATE'
     A                                   126'DATE'
     A                                      SPACEA(1)
      *
     A                                     6'-------------'
     A                                    21'--------------------'
     A                                    43'---'
     A                                    48'-------------'
     A                                    63'---'
     A                                    67'-------'
     A                                    75'-------'
     A                                    84'--------------'
     A                                    99'---'
     A                                   103'-------------'
     A                                   118'-------'
     A                                   126'-------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RPMFC     R              6REFFLD(TYST)
     A            RPCSN     R             21REFFLD(RPNM)
     A            RPCCY     R             43REFFLD(ACCY)
     A            RPAMT     R             48REFFLD(AM14)
     A            RTRCA     R             63REFFLD(EVTP)
     A            RPSTD     R             67REFFLD(VLUE)
     A            RPEND     R             75REFFLD(VLUE)
     A            RINFC     R             84REFFLD(TYST)
     A            RICCY     R             99REFFLD(ACCY)
     A            RIAMT     R            103REFFLD(AM14)
     A            RISTD     R            118REFFLD(VLUE)
     A            RIEND     R            126REFFLD(VLUE)
     A                                      SPACEA(1)
     A
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    46'***  END OF REPORT FOR BRANCH'
     A                                      SPACEB(3)
     A  37        RBRCA          3A  O    76
     A  37                                81
     A                                      '***'
     A N37                                51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
