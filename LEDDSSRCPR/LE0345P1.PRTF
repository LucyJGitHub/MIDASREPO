      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Transaction Income Splits Report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE0345P1 - Transactions Income Splits Report            *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE035  *CREATE    Date 22Sep03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE035 - Lending Enhancement for Nordea Phase 1 Priority 2   *
      *           Income Splits                                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE0345P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  36                                50'TRANSACTION INCOME SPL+
     A                                      ITS TODAY '
     A N36                                50'TRANSACTION INCOME SPL+
     A                                      ITS '
      *
     A  37                                85'BRANCH -'
     A  37        A8BRCD    R             94REFFLD(BRCA)
     A  37        A8BRNM    R             98REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A  36                                50'----------------------+
     A                                      ---------'
     A  36                                  SPACEA(2)
     A N36                                50'----------------------+
     A                                      ---'
     A N36                                  SPACEA(2)
      *
     A                                    36'LOAN'
     A                                    46'CUSTOMER'
     A                                    63'LOAN /'
     A                                    74'FEE'
     A                                    81'SPLIT CUSTOMER'
     A                                    98'PERCENT'
     A N36                                  SPACEA(1)
     A  36                               113'CHANGE'
     A  36                                  SPACEA(1)
     A                                    63'FACILITY'
     A                                    74'SEQ'
     A                                    98'ALLOCATION'
     A N36                                  SPACEA(1)
     A  36                               113'TYPE'
     A  36                                  SPACEA(1)
     A                                    36'------'
     A                                    46'--------'
     A                                    63'--------'
     A                                    74'---'
     A                                    81'--------------'
     A                                    98'----------'
     A N36                                  SPACEA(1)
     A  36                               113'------'
     A  36                                  SPACEA(1)
      *
     A          R DETAIL
      *
     A            RLNRF          6A       36
     A            RCNUM          6A       47
     A            RFACL          6A       64
     A            RFESQ          2A       75
     A            RISCN         10A       81
     A            RIPAL         12A       98
     A            RCTYP          6A      113
     A                                      SPACEA(1)
      *
     A          R TRAILP1
     A                                    50'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
