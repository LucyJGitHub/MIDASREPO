      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Matured loans reactivated')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE0373P1 - Matured Loans Reactivated                    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD057550           Date 29Apr21               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 127731             Date 04Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD057550 - When MBIN = 'N', trailer line TOTAL OF MATURED    *
      *             LOANS REACTIVATED overlaps with END OF REPORT.    *
      *             Add 3 spaces before END OF REPORT.                *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  127731 - Reactivate Matured Loans                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE0373P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    46'MATURED LOANS REACTIVATED'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'-------------------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                    15'LOAN NO.'
     A                                    30'CUSTOMER NO.'
     A                                    50'FACILITY'
     A                                    68'START DATE'
     A                                    85'MATURITY DATE'
     A                                   114'AMOUNT'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RLNRF          6        16
      * Begin /CSD027/
     A**********  RCNUM          6  0     32                                                  CSD027
     A**********  RFCUS          6  0     48                                                  CSD027
     A            RCNUM          6A       32
     A            RFCUS          6A       48
      * End /CSD027/
     A                                    54'/'
     A            RFTYP          3  0     55
     A                                    58'/'
     A            RFSEQ          2  0     59
     A            RSLID          7        69
     A            RMDAT          7        88
     A            RPRAM         17       105
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    40'TOTAL OF MATURED LOANS +
     A                                        REACTIVATED'
     A                                      SPACEB(3)
     A            RCOUNT         5  0     77EDTCDE(3)
     A  37                                44'*** END OF REPORT FOR BRANCH'
     A                                      SPACEB(3)
     A  37        RBRCA          3        73
     A  37                                77'***'
     A N37                                50'***  END OF REPORT  ***'
      **Start of change MD057550
     A                                      SPACEB(3)
      **End of change MD057550
      *
