      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_CONF42                                                    *
/*EXI *  TEXT('Midas LE Rollover confirmation advice')                *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *   PRTF/LE0426P1 - Rollover Confirmation Advices               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CLE033             Date 02Apr03               *
      *                 CLE031             Date 18Feb03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CLE015             Date 13Dec01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 179807           Date 26Jan01                 *
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 091679           Date 30Oct97                 *
      *                 CLE006           Date 02Dec96                 *
      *                 S01408           DATE 03JAN96                 *
      *                 BH3452           DATE 13SEP92                 *
      *                 BH3413           DATE 14JUL92                 *
      *                 S01270           DATE 09JAN92                 *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CLE033 - Settlement method 09                                *
      *  CLE031 - Lending Enhancements.                               *
      *  CLE015 - Functionality moved from Syndicated Lending to      *
      *           Customer Lending                                    *
      *  179807 - Rollover Confirmations should print on same conf    *
      *           printer as normal Loan Confirmation but is totally  *
      *           different layout and size - amended Rollover layout *
      *           to standard Deal/Loan confirmation layout.          *
      *         - Source code-stripped as 75% of the code hade been   *
      *           commented out.                                      *
      *  091679 - Incorrect editing of Interest and Principal         *
      *           amounts. Change these to edited 21A fields.         *
      *  CLE006 - Customer Lending Enhancements - Rollovers           *
      *  S01408 - Addition of core hook LE0426P101                    *
      *           Addition of core hook LE0426P102                    *
      *  BH3452 - Addition of rollover date to confirmation           *
      *  BH3413 - Changes to layout to match other confirmations      *
      *  S01270 - New Printer File for Rollover Confirmations         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A*/COPY WNCPYSRC,LE0426P106
     A          R LE0426D1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,LE0426P103
     A  85                              6 40'DUPLICATE'
     A  10        BRNM      R           9 35TEXT('BRANCH NAME')
     A                                 12 35'ROLLOVER CONFIRMATION'
      **Start of change CLE148
     A**********  LNRFC1         6  0  14 12                                                  CLE148
     A            LNRFC1         6A    14 12
      **End of change CLE148
      *
     A            AGMRDT    R          14 30REFFLD(RUNA)
      *
     A                                 16  6'We confirm the rollover of you+
     A                                      r loan: '
     A            CCY            3A    16 44
      **Start of change CLE148
     A**********  LNRFC          6  0  16 52                                                  CLE148
     A            LNRFC          6A    16 52
      **End of change CLE148
     A                                 16 62'on'
     A            DATER          7     16 65
     A                                 20  6'Currency'
     A                                 21  6'~~~~~~~~'
     A                                 20 25'Principal'
     A                                 21 25'~~~~~~~~~'
     A                                 20 45'Interest Rate'
     A                                 21 45'~~~~~~~~~~~~~'
     A            CNAME         14A    22  6
     A            CPAMA         21     22 21
      *
     A N12        FRATE         11  7  22 45EDTCDE(J)
     A  12                             22 45'No details'
      * Start of CLE031
     A                                 23  6'Settlement Currency'
     A            RSPCY          3A    23 27
     A                                 23 35'Converted Amount :'
     A            RCONA         15S    23 54
      * End of CLE031
      *
     A     31                          25 11'Interest to maturity +
     A                                      date'
     A     32                          25 11'Interest to next inter+
     A                                      est payment date'
     A*
     A            TODATE         7A       +1
     A                                    +1' will total: '
     A            CCY2           3A    26 11
     A            ZINTRA        21A       +1
      *
     A*/COPY WNCPYSRC,LE0426P101
      *
     A  11                             29 11' '
      *
     A*/COPY WNCPYSRC,LE0426P102
      *
     A            BBCNA1        35A    38 15
     A            BBCNA2        35A    39 15
     A            BBCNA3        35A    40 15
     A            BBCNA4        35A    41 15
      *
     A*/COPY WNCPYSRC,LE0426P105
      *
     A          R LE0426D2
      *
     A  85                              6 40'DUPLICATE'
     A  10        BRNM      R           9 35TEXT('Branch name')
     A                                 12 35'ROLLOVER CONFIRMATION'
     A            PARTIC        13A    12 60
      *
      **Start of change CLE148
     A**********  LNRFC1         6  0  14 12                                                  CLE148
     A            LNRFC1         6A    14 12
      **End of change CLE148
      *
     A            AGMRDT    R          14 30REFFLD(RUNA)
      *
     A                                 16  8'ORIG CUST'
     A            RONA1         35A    16 20TEXT('Orig Cust name & addr 1')
     A            RONA2         35A    16 55TEXT('Orig Cust name & addr 2')
      *
     A                                 18  8'ORIG CCY & PRINCIPAL'
     A                                 18 32'NEW CCY & PRINCIPAL'
     A                                 18 56'FROM'
     A                                 18 64'TO'
     A                                 18 73'INTEREST RATE'
      *
     A            RCCY      R          19  8REFFLD(CYCD)
     A  45        RPAM0         12S 0  19 12TEXT('Principal amt 0 dec')
     A                                      EDTCDE(A *)
     A  46        RPAM1         13S 1  19 12TEXT('Principal amt 1 dec')
     A                                      EDTCDE(A *)
     A  47        RPAM2         14S 2  19 12TEXT('Principal amt 2 dec')
     A                                      EDTCDE(A *)
     A  48        RPAM3         15S 3  19 12TEXT('Principal amt 3 dec')
     A                                      EDTCDE(A *)
     A            RNCCY     R          19 32REFFLD(CYCD)
     A  40        RNPAM0        12S 0  19 36TEXT('New Prin. amt 0 dec')
     A                                      EDTCDE(A *)
     A  41        RNPAM1        13S 1  19 36TEXT('New Prin. amt 1 dec')
     A                                      EDTCDE(A *)
     A  42        RNPAM2        14S 2  19 36TEXT('New Prin. amt 2 dec')
     A                                      EDTCDE(A *)
     A  43        RNPAM3        15S 3  19 36TEXT('New Prin. amt 3 dec')
     A                                      EDTCDE(A *)
     A            RFRDT          7A    19 56TEXT('From date')
     A            RTODT          7A    19 64TEXT('To date')
     A            RINTR         11S 7  19 73TEXT('Interest Rate')
     A                                      EDTCDE(A *)
      * Start of CLE031
     A                                 20  8'SETTLEMENT CURRENCY'
     A            RSPCY          3A    20 28
     A                                 20 36'CONVERTED AMOUNT: '
     A            RCONA         15S    20 55
      * End of CLE031
     A
     A                                 21 45'INTEREST'
     A  40        RINT0         12S 0  21 64TEXT('Interest 0 dec')
     A                                      EDTCDE(A *)
     A  41        RINT1         13S 1  21 64TEXT('Interest 1 dec')
     A                                      EDTCDE(A *)
     A  42        RINT2         14S 2  21 64TEXT('Interest 2 dec')
     A                                      EDTCDE(A *)
     A  43        RINT3         15S 3  21 64TEXT('Interest 3 dec')
     A                                      EDTCDE(A *)
      * Start of CLE031
     A                                 22 45'SETTLE EQUIV. '
     A            RIEQA         15S    22 64
      * End of CLE031
     A                                 23 45'TOTAL'
     A  40        RTOT0         12S 0  23 64TEXT('Total amount 0 dec')
     A                                      EDTCDE(A *)
     A  41        RTOT1         13S 1  23 64TEXT('Total amount 1 dec')
     A                                      EDTCDE(A *)
     A  42        RTOT2         14S 2  23 64TEXT('Total amount 2 dec')
     A                                      EDTCDE(A *)
     A  43        RTOT3         15S 3  23 64TEXT('Total amount 3 dec')
     A                                      EDTCDE(A *)
      * Start of CLE031
     A                                 24 45'SETTLE EQUIV. '
     A            RTEQA         15S    24 64
      * End of CLE031
      *
     A            RTIT2         27     25  7
     A            RTIT1         40     25 43
     A            RNAM2         35     26 10
     A            RNAM1         35     26 46
      *                                                 /*Start CLE033*/
     A************RTWN2         11     27 10
     A************RTWN1         11     27 46
     A            RTWN2         15     27 10
     A            RTWN1         15     27 46
      *                                                 /* End  CLE033*/
     A            RTIT4         10     28  7
     A            RTIT3         10     28 43
     A            RNAM4         35     29 10
     A            RNAM3         35     29 46
     A            RTWN4         20     30 10
     A            RTWN3         20     30 46
      *
     A N23                             32 10'FOR A/C OF'
     A  26                             32 55'BENEFICIARY BANK'
     A N23        RORAC         25     33 10
     A  26        RBTB4         35     33 55
     A N23        RCPNM         35     34 10
     A*                                                 /*Start CLE015*/
     A*N23********RCPTN*********20     34 10
     A N23        RCPTN         20     35 10
     A*                                                 /* End  CLE015*/
      *
     A                                 36 55'FINAL MATURITY DATE :'
     A            OMDAT          7     36 80
      *
     A            RCNA1         35A    38 15TEXT('Cust name & addr 1')
     A N24        RTIT6         20     38 55
     A            RCNA2         35A    39 15TEXT('Cust name & addr 2')
     A N24        RBTB1         35     39 55
     A            RCNA3         35A    40 15TEXT('Cust name & addr 3')
     A N24        RBTB2         35     40 55
     A            RCNA4         35A    41 15TEXT('Cust name & addr 4')
     A N24        RBTB3         35     41 55
      *
     A*/COPY WNCPYSRC,LE0426P104
