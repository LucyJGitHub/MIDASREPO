      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas LE Drop repay/past due for mat loans - Audit')
      *****************************************************************
      *                                                               *
      *  Midas - (Module Name) Module                                 *
      *                                                               *
      *  PRTF/LE0453AU - Drop Repay/Past Due for Matured Loans Audit  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. AR1058057          Date 07Jul16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CLE005 *CREATE     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR1058057 - LEC06A 10008 failed.  Increase the length of the *
      *              record counters to avoid truncation of actual    *
      *              number of records when dropped transactions are  *
      *              more than 999. (Child: AR1058060)                *
      *            - Applied for MD-38982                             *
      *  CLE005 - Customer Lending Enhancements - Others              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE0453AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    33'DROPPED REPAYMENT/PAST DU+
     A                                      E FOR MATURED LOANS - +
     A                                      RUN CONTROL'
     A                                      SPACEA(1)
      *
     A                                    33'-------------------------+
     A                                      ----------------------+
     A                                      -----------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    30'NO. OF MATURED LOANS REA+
     A                                      D           -'
      **Start of change AR1058057
     A**********  RNMAL          3  0     69                                               AR1058057
     A            RNMAL          5  0     68
      **End of change AR1058057
     A                                      SPACEA(1)
     A                                    30'NO. OF REPAYMENT SCHEDULE+
     A                                      S DROPPED  -'
      **Start of change AR1058057
     A**********  RNREP          3  0     69                                               AR1058057
     A            RNREP          5  0     68
      **End of change AR1058057
     A                                      SPACEA(1)
     A                                    30'NO. OF PAST DUE SCHEDULE+
     A                                      S DROPPED   -'
      **Start of change AR1058057
     A**********  RNPAL          3  0     69                                               AR1058057
     A            RNPAL          5  0     68
      **End of change AR1058057
     A                                    74'Loan Amendments File (+
     A                                      LOAMSDK)'
     A                                      SPACEA(1)
      **Start of change AR1058057
     A**********  RNPAP          3  0     69                                               AR1058057
     A            RNPAP          5  0     68
      **End of change AR1058057
     A                                    74'Past Due Repay by Day +
     A                                      File (PDUEDU)'
     A                                      SPACEA(2)
      *
      *--------------------------------*
      ***  File Control Out of Balance *
      *--------------------------------*
      *
     A          R FCOOBCT
     A                                    30'FILE CONTROL OUT OF BALAN+
     A                                      CE - LF/MATLS'
     A                                      SPACEA(1)
     A                                    30'=========================+
     A                                      ============='
     A                                      SPACEA(1)
     A                                    35'NO. OF MATURED RECORDS RE+
     A                                      AD    -'
     A            RNMAR          3  0     69
     A                                    35'NO. OF MATURED RECORDS RE+
     A                                      AD    -'
     A                                      SPACEA(1)
     A                                    74'*** DIFFERENCE ***'
     A            RNTRL          3  0     69
     A                                      SPACEA(1)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    57'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
