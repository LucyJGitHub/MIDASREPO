      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Drop repay/past due for mat loans - det')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE0454P1 - Drop Repay/Past Due for Matured Loans Report *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. 205823 *CREATE     Date 26Jun02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  205823 - Patterned from LE0453P1.                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE0454P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    33'DROPPED REPAYMENT/PAST DU+
     A                                      E FOR MATURED LOANS - +
     A                                      REPORT'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    33'-------------------------+
     A                                      ----------------------+
     A                                      ------'
     A                                      SPACEA(3)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     6'Customer'
     A                                    39'Loan No.'
     A                                    49'Amendment Type'
     A                                    71'Value Date'
     A                                    83'Ccy'
     A                                    98'Amount'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAL1
      *
     A  38        RCNUM          6         6
     A                                      SPACEB(1)
     A  38        RCNAM         20        14
     A  39        RLNRF          6        39
     A  40        RAMTP         18        49
     A  40        RVDAT          7        71
     A  40        RLCCY          3        83
     A  40        RTAMT         21        91
     A                                      SPACEA(1)
     A N40                                49'*** NO SCHEDULES DROPPED +
     A                                      FOR THIS LOAN ***'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                48'***  END OF REPORT FOR +
     A                                      BRANCH'
     A                                      SPACEB(3)
     A  37        RBRCA     R             78REFFLD(BRCA)
     A  37                                83'***'
     A N37                                56'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
