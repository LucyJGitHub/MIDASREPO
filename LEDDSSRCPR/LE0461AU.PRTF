      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Facilities Processing Audit Report')
     A********************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*   PRTF/LE0461AU - Facilities Processing - Audit               *
     A*                                                               * **
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               * **
     A*                                                               *
      *  Last Amend No. AR927286A          Date 12Aug22               *
      *  Prev Amend No. 255502             Date 12Aug22               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 Xnnnnn             DATE ddmmmyy               *
     A*                                                               * **
     A*****************************************************************
     A*                                                               * **
      *  AR927286A - Redelivery of 255502                             *
      *  255502 - LEC0609D failed due to database error in FCLTYFM.   *
      *           Include audit of dropped records from LEFCAMPD due  *
      *           to expiring facilities.                             *
     A*     MD046248 - Finastra Rebranding                            *
     A*                                                               * **
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R LE0461A1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'LE0461AU'
     A            TITL          53        37
     A                                   100'DATE'
     A            BJMRDT    R            105REFFLD(RUNA)
     A                                   119'PAGE'
     A            PAGE1          4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A
     A                                    38'FACILITY PROCESSING - FILE CONTROL-
     A                                      S'
     A                                      SPACEA(1)
     A                                    38'-----------------------------------
     A                                      -'
     A                                      SPACEA(4)
     A
     A                                     7'S/FCLTY - RUN CONTROLS'
     A                                    54'NUMBER'
     A                                    80'VALUE'
     A                                      SPACEA(1)
     A                                     7'----------------------'
     A                                    50'OF RECORDS'
     A                                    76'OF RECORDS'
     A                                      SPACEA(3)
     A
     A                                     9'TOTAL RECORDS ON FILE'
     A            NORE      R             55EDTCDE(Z)
     A            VLAFA     R             67EDTCDE(Z)
     A                                      REFFLD(VLAF)
     A            VLALA     R             82REFFLD(VLAL)
     A                                      SPACEA(2)
     A                                     9'TOTAL RECORDS ON FILE-
     A                                       - CALCULATED'
     A            NFCLTY         5  0     55EDTCDE(Z)
     A            FFTOTI        15  0     67EDTCDE(Z)
     A            FFTOTD         3  0     82
     A N81                                  SPACEA(3)
     A  81                                90'***   DIFFERENCE   ***'
     A                                      SPACEA(3)
     A                                     7'S/FCUPD - RUN CONTROLS'
     A                                      SPACEA(1)
     A                                     7'----------------------'
     A                                      SPACEA(2)
     A
     A                                     9'TOTAL RECORDS ON FILE'
     A            NFCUPD         5  0     55EDTCDE(Z)
     A            VLAFB     R             67EDTCDE(Z)
     A                                      REFFLD(VLAF)
     A            VLALB     R             82REFFLD(VLAL)
     A                                      SPACEA(3)
     A
      **Start of change 255502
     A                                     7'S/LEFCAM - RUN CONTROLS'
     A                                      SPACEA(1)
     A                                     7'----------------------'
     A                                      SPACEA(2)
     A                                     9'TOTAL RECORDS DROPPED +
     A                                      FROM FILE'
     A            RDROP          5  0     55EDTCDE(Z)
     A                                      SPACEA(3)
      **End of change 255502
     A          R DBERROR                   SPACEB(3)
     A                                    30'DATABASE ERROR'
     A            DBASE          3  0     46
     A                                    52'IN PROGRAM'
     A            DBPGM     R             63
     A                                      SPACEA(2)
     A                                    30'FILE'
     A            DBFILE    R             35
     A                                    46'KEY'
     A            DBKEY     R             50
