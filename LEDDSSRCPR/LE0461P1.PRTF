      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Facilities Processing Report')
     A********************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*   PRTF/LE0461P1 - Facilities Processing Detail Report         *
     A*                                                               * **
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               * **
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      *  Prev Amend No. CLE042             Date 06Sep05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 Xnnnnn             Date ddmmmyy               *
     A*                                                               * **
     A*****************************************************************
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R P1HDRS                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'LE0461P1'
     A            TITL          53        37
     A                                   100'DATE'
     A            BJMRDT    R            105REFFLD(RUNA)
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    48'FACILITY ACCOUNT KEYS GENERATED'
     A  65                                  SPACEA(1)
     A N65                                83'BRANCH'
     A N65        BRCA      R             90
     A N65                                93'-'
     A N65        BRNM          30        94
     A                                      SPACEA(1)
     A                                    48'-------------------------------'
     A                                      SPACEA(3)
     A                                    17'CUSTOMER     FACILITY'
     A                                    43'ACCOUNT KEY'
     A                                    66'AMOUNT    CURRENCY'
     A                                    90'REV IND'
     A                                      SPACEA(2)
     A          R EX41O                     SPACEA(1)
      * Begin /CSD027/
     A**********  CNUM      R             18                                                  CSD027
     A            CNUM           6A       18TEXT('Customer number')
      * End /CSD027/
     A            FACT      R             31
     A                                    34'/'
     A            FCNO      R             35
     A*                                              Begin /CLE042/
     A************AKEY**********10        43                                                  CLE042
     A            AKEY          14        42                                                  CLE042
     A*                                              End   /CLE042/
     A  30        EAMT0         13  0     57EDTWRD(' ,   ,   ,   , 0 -')
     A  31        EAMT1         13  0     57EDTWRD('   ,   ,   , 0 . -')
     A  32        EAMT2         13  0     57EDTWRD('  ,   ,   , 0 .  -')
     A  33        EAMT3         13  0     57EDTWRD(' ,   ,   , 0 .   -')
     A            FCCY      R             78
     A            RVIND          1  0     93
     A          R TRAIL1                    SPACEB(3)
     A                                    32'TOTAL ACCOUNT KEYS GENERATED'
     A            NKYGEN         5  0     65EDTCDE(3)
     A                                      SPACEA(1)
     A          R TRAIL2                    SPACEB(3)
     A                                    46'*** END OF REPORT FOR BRANCH '
     A            SAVBR          3        75
     A                                    78' ***'
     A          R TRAIL3                    SPACEB(3)
     A                                    46'***   END OF REPORT   ***'
     A          R TRAIL4                    SPACEB(3)
     A                                    29'*** REFERENCE'
     A            DBPGM     R             43
     A                                    61'DATABASE ERROR'
     A                                      SPACEA(2)
