      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas LE Facility Amendments Dropped Report')          *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE0461P2 - Facility Amendments Dropped Today Report     *
      *                                                               *
      *  (C) Copyright Misys Banking Systems Ltd. 2012                *
      *                                                               *
      *  Last Amend No. AR927286A          Date 12Aug22               *
      *  Prev Amend No. AR927286           Date 12Aug22               *
      *                 255502 *CREATE     Date 12Aug22               *
      *                 Xnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR927286A - Redelivery of AR927286 (Child: AR927287)         *
      *  AR927286 - Insert narrative for other dropped facility       *
      *             amendment not only for FI type. (Child: AR927287) *
      *  255502 - LEC0609D failed due to database error in FCLTYFM.   *
      *           Reports dropped facility amendments of expired      *
      *           facilities.                                         *
      *         - Applied for AR927286 (Child: AR927287)              *
      *                                                               *
      *****************************************************************
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(C) Misys Banking +
     A                                      Systems Ltd. 2012')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE0461P2'
     A            TITL          53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    45'FACILITY AMENDMENTS DROPP+
     A                                      ED TODAY'
      *
     A N65                                89'BRANCH -'
     A N65        A8BRCD         3        98
     A N65        A8BRNM        30       102
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    45'-------------------------+
     A                                      --------'
     A                                      SPACEA(3)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                    10'CUSTOMER NAME/NO.'
     A                                    37'FACILITY'
     A                                    49'CCY'
     A                                    55'VALUE DATE'
     A                                    73'ACTION'
     A                                   101'DETAILS'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAL1
      *
     A            RCNAM         20         6
     A            RCNUM          6        28
     A            RFACT          3        38
     A                                    41'/'
     A            RFCNO          2        42
     A            RHCCY          3        49
     A            RVDAT          7        56
      **Start of change AR927286
     A**********                          69'FACILITY INCREASE'                             AR927286
     A  30                                69'FACILITY INCREASE'
     A  31                                69'FACILITY DECREASE'
     A  32                                69'REVOLVING CREDIT CHANGE'
      **End of change AR927286
     A            RHAMT         21        96
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A N65                                48'***  END OF REPORT FOR +
     A                                      BRANCH'
     A                                      SPACEB(3)
     A N65        RBRCA          3        78
     A N65                                83'***'
     A  65                                56'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
