      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Dropped Participant Contribution History')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*   PRTF/LE0464P1 - Drop Participant's Contribution History     *
     A*                   Records.                                    *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2005            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*  Last Amend No. CLE106  *CREATE    Date 01Aug04               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CLE106 - Syndications Manager.                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      *-----------*
      ***  Header *
      *-----------*
      *
     A          R HEADER
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2005')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE LE0464P1'
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
     A                                    90'BRANCH'
     A            BRCA      R             97
     A                                   100'-'
     A            BRNM      R            101
     A                                      SPACEA(001)
     A                                    46'DROPPED PARTICIPANT CONTRIBUTION-
     A                                       HISTORY'
     A                                      SPACEA(001)
     A                                    46'--------------------------------
     A                                      ---------'
     A                                      SPACEA(002)
     A                                    10'PRIME'
     A                                    23'PARTICIPANT'
     A                                    38'PARTICIPANT'
     A                                    54'EVENT'
     A                                    74'PRIME'
     A                                    93'SHARE'
     A                                      SPACEA(001)
     A                                    09'FACILITY'
     A                                    25'FACILITY'
     A                                    41'TYPE'
     A                                    55'DATE'
     A                                    63'SEQ'
     A                                    69'FACILITY AMOUNT'
     A                                    90'PERCENTAGE'
     A                                   110'SHARE AMOUNT'
     A                                      SPACEA(001)
      *
      *-----------*
      ***  Detail *
      *-----------*
      *
     A          R DETFCLT
     A            PRIMEFACT     13A       06
     A            PARTIFACT     13A       22
     A            PARTITYPE     10A       38
     A            EVENTDATE      7A       53
     A            SEQUENCE       2A       63
     A            FACTAMT       19A       69
     A            SHAREPRCNT    17S14     90EDTCDE(3)
     A            SHAREAMT      19A      110
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A N80                                51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A  80                                49'*** NO DETAILS TO PRINT ***'
     A                                      SPACEB(3)
