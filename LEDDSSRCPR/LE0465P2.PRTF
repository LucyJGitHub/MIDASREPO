      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Unknown Transaction Reference Types')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module                              *
     A*                                                               *
     A*   PRTF/LE0465P2 - Unknown Transaction Reference Types on      *
     A*                   LETREFPD                                    *
     A*                                                               *
     A*  (c) Finastra International Limited 2005                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE106  *CREATE    Date 01Aug04               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CLE106 - Syndications Manager.                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      *-----------*
      ***  Header *
      *-----------*
      *
     A          R HEADER2
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE0465P2'
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    90'BRANCH'
     A            BRCA      R             97
     A                                   100
     A                                      '-'
     A            BRNM      R            101
     A                                      SPACEA(1)
     A                                    41'UNKNOWN TRANSACTION REFERENCE TYPE-
     A                                      S ON LETREFPD'
     A                                      SPACEA(1)
     A                                    41'-----------------------------------
     A                                      -------------'
     A                                      SPACEA(2)
     A                                     7'TRANSACTION'
     A                                      SPACEA(1)
     A                                    10'TYPE'
     A                                    36'API REFERENCE'
     A                                    75'PC REFERENCE'
     A                                    92'TRANSACTION DESCRIPTION'
     A                                      SPACEA(1)
      *
      *-----------*
      ***  Detail *
      *-----------*
      *
     A          R DETLE2
     A            RTRTP2        12A        6
     A            RAREF2        52A  O    20
     A            RPCRF2        15A  O    75
     A            RTDSC2        35A  O    92
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY2
     A                                    29'These records have been reported a-
     A                                      s'
     A                                      SPACEB(2)
     A                                    66'in error because the value in'
     A                                    29'field TRTP was not recognised by L-
     A                                      E0465. These records will not be'
     A                                      SPACEB(1)
     A                                    29'dropped until either the'
     A                                      SPACEB(1)
     A                                    55'TRTP field is'
     A                                    70'updated'
     A                                    79'with the correct'
     A                                    29'value, or LE0465 is amended to rec-
     A                                      ognise the new transaction type.'
     A                                      SPACEB(1)
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
