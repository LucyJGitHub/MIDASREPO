      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Facility and Event Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Lending Module
     A*                                                               *
     A*  LE0491AU - Facility and Event Processing                     *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 105140             Date 19Nov96               *
      *                 XNNNNN             Date DDMmmYY               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  105140 - Recompile over changed LVENTZZ.                     *
     A*           Field NORE attribute changed.                       *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *------------------*
      ***  Report Header *
      *------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE LE0491AU'
     A            BJURPT    R             37REFFLD(BJURPT SDBANKPD)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(BJMRDT SDBANKPD)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A*                                                   /START 0050/
     A***********                           SPACEA(1)
     A                                      SPACEA(2)
      *
     A***********                        101'TIME'                        0050
     A***********                        106TIME                          0050
     A***********                           EDTWRD('  :  :  ')            0050
     A***********                           SPACEA(1)                     0050
     A*                                                     /END 0050/
      *
     A                                    48'FACILITY AND EVENT PROCESSING -
     A                                       - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    48'-------------------------------
     A                                      --------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
      *
     A                                     6'FACILITIES FILE - LF/FCUPD'
     A                                      SPACEA(1)
     A                                     6'--------------------------'
     A                                      SPACEA(3)
      *
     A                                     6'NUMBER OF RECORDS ON TRA-
     A                                      ILER'
     A            NOREFC         5  0     59EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                     6'VALUE - TRAILER'
     A            VLAFFC        15  0     46EDTCDE(3)
     A            VLALFC         3  0     61EDTCDE(4)
     A                                      SPACEA(2)
      *
     A                                     6'VALUE - CALCULATED'
     A            VLAFFX        15  0     46EDTCDE(3)
     A            VLALFX         3  0     61EDTCDE(4)
     A N85                                  SPACEA(2)
     A  85                                69'** DIFFERENCE **'
     A                                      SPACEA(2)
      *
     A                                     6'NUMBER OF RECORDS READ IN'
     A            COUNT1         5  0     59EDTCDE(3)
     A N86                                  SPACEA(2)
     A  86                                69'** DIFFERENCE **'
     A                                      SPACEA(2)
      *
     A                                      SPACEB(2)
     A                                     6'NEW FACILITIES FILE'
     A                                      SPACEA(1)
     A                                     6'-------------------'
     A                                      SPACEA(3)
      *
     A                                     6'NUMBER OF RECORDS OUTPUT'
     A            COUNTF         5  0     59EDTCDE(3)
     A N87                                  SPACEA(2)
     A  87                                69'** DIFFERENCE **'
     A                                      SPACEA(2)
      *
     A                                     6'EVENTS FILE - LF/LVNTS'
     A                                      SPACEA(1)
     A                                     6'----------------------'
     A                                      SPACEA(3)
      *
     A                                     6'NUMBER OF RECORDS ON TRA-
     A                                      ILER'
     A*                                                   Begin /105140/
     A*********** NORELV         5  0     59EDTCDE(3)
     A            NORELV         9  0     59EDTCDE(3)
     A*                                                   End   /105140/
     A                                      SPACEA(2)
      *
     A                                     6'NUMBER OF RECORDS READ IN'
     A*                                                   Begin /105140/
     A*********** COUNT2         5  0     59EDTCDE(3)
     A            COUNT2         9  0     59EDTCDE(3)
     A*                                                   End   /105140/
     A N88                                  SPACEA(2)
     A  88                                69'** DIFFERENCE **'
     A                                      SPACEA(2)
      *
     A                                     6'NUMBER OF DUMMY EVENTS '
     A************                          DROPPED'                 0076
     A            COUNTD         5  0     59EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                     6'NEW EVENTS FILE'
     A                                      SPACEA(1)
     A*                                                   /START 0050/
     A***********                          6'----------------------'
     A                                     6'---------------'
     A                                      SPACEA(3)
     A*                                                     /END 0050/
      *
     A                                     6'NUMBER OF RECORDS OUTPUT'
     A            COUNTL         5  0     59EDTCDE(3)
     A N89                                  SPACEA(2)
     A  89                                69'** DIFFERENCE **'
     A*                                                   /START 0050/
     A***********                           SPACEA(2)
     A                                      SPACEA(3)
      *
     A                                    55'*** END OF REPORT ***'
     A*                                                     /END 0050/
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    47'DATABASE ERROR'
     A                                      SPACEB(2)
     A            DBASE          3  0     62
     A                                    66'IN PROGRAM'
     A            DBPGM         10        77
     A                                      SPACEA(2)
     A                                    47'FILE'
     A            DBFILE         8        52
     A                                    61'KEY'
     A            DBKEY         29        65
      *
