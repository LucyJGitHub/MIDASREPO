      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Dropped collateral records report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE0494P1 - Dropped Collateral Records - Report          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CLE005 *Create     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE005 - Customer Lending Enhancements - Others (T3)         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE0494P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'DROPPED COLLATERAL RECOR+
     A                                       DS - REPORT'
      *
     A                                    89'BRANCH -'
     A            A8BRCD    R             98REFFLD(BRCA)
     A            A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'------------------------+
     A                                      ------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'CUSTOMER'
     A                                    16'FACLTY'
     A                                    24'LOAN NO'
     A                                    32'SEQ NO'
     A                                    40'START DATE'
     A                                    52'END DATE'
     A                                    62'CCY'
     A                                    69'UNIT AMOUNT'
     A                                    86'NO OF UNITS'
     A                                   100'TOTAL AMOUNT'
     A                                   120'TYPE'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A  02        PCNUM          6         6
     A  01 04     PFCTY          6        16
     A  01N04     PLNRF          6        24
     A            PGUSN          3        32
     A            PSTTD          7        41
     A            PEDDT          7        52
     A            PCCY           3        62
     A            PUAMT         17        67
     A            PNOUN          5  0     88EDTCDE(3)
     A            PTAMT         17        98
     A            PGSCI         10       118
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    46'***  END OF REPORT FOR +
     A                                      BRANCH'
     A                                      SPACEB(3)
     A            PBRCA          3        76
     A                                    81'***'
