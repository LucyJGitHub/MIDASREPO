      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas LE Manual facility utilisation audit report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE0495AU - Manual Utilisation Processing - Audit        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CLE005 *Create     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE005 - Customer Lending Enhancements - Others (T3)         *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE0495AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    45'MANUAL UTILISATION PROCESSING +
     A                                        - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    45'------------------------------+
     A                                      -------'
     A                                      SPACEA(2)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
      *
     A                                     6'MANUAL UTILISATION FILE - +
     A                                      LF/LEMNFUL1'
     A                                      SPACEA(1)
      *
     A                                     6'----------------------+
     A                                      ---------------'
     A                                      SPACEA(1)
      *
     A                                     6'NUMBER OF RECORDS DROPPED'
     A            RCOUNT         5  0     66EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'NUMBER OF RECORDS ON TRAILER'
     A            RTRAIL         5  0     66EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'VALUE - TRAILER'
     A            RVALUE        18  0     53EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                     6'HISTORY ACTION FILE - +
     A                                      PF/HISACT'
     A                                      SPACEA(1)
      *
     A                                     6'----------------------+
     A                                      ---------'
     A                                      SPACEA(1)
      *
     A                                     6'NO OF RECORDS ADDED'
     A            RHISCN         5  0     66EDTCDE(3)
     A                                      SPACEA(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
      *
     A                                    36'***************************+
     A                                      ****************************'
     A                                      SPACEB(2)
     A                                    36'*                  DATA BASE ERROR'
     A                                      SPACEB(1)
     A                                    90'*'
     A                                    36'*'
     A                                      SPACEB(1)
     A                                    90'*'
     A                                    36'*    FILE NAME:    '
     A                                      SPACEB(1)
     A            DBFILE         8A       55
     A                                    90'*'
     A                                    36'*    PROGRAM  :    '
     A                                      SPACEB(1)
     A                                    55'LE0495'
     A                                    90'*'
     A                                    36'*    AT       :    '
     A                                      SPACEB(1)
     A            DBASE          3  0     55
     A                                    90'*'
     A                                    36'*    KEY      :    '
     A                                      SPACEB(1)
     A            DBKEY         29        55
     A                                    90'*'
     A                                    36'*'
     A                                      SPACEB(1)
     A                                    90'*'
     A                                    36'***************************+
     A                                      ****************************'
     A                                      SPACEB(1)
      *
