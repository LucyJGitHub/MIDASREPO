      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Manual facility utilisation report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE0495P1 - Manual Utilisation Processing Report         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CLE005 *C *CREATE  Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE005 - Customer Lending Enhancements - Others (T3)         *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE0495P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   105'DATE'
     A            BJMRDT    R            110REFFLD(MRDT)
     A                                   124'PAGE'
     A                                   129PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    41'MANUAL UTILISATION PROCESSING +
     A                                        - ACTIONS TODAY'
      *
     A                                    90'BRANCH '
     A            A8BRCD    R             97REFFLD(BRCA)
     A                                   100'-'
     A            A8BRNM    R            101REFFLD(BRNM)
     A                                      SPACEA(1)
      *
     A                                    41'------------------------+
     A                                      ---------------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                    10'CUSTOMER NAME/NO.'
     A                                    37'FACILITY'
     A                                    48'SEQ NO'
     A                                    57'CCY'
     A                                    63'VALUE DATE'
     A                                    81'ACTION'
     A                                   110'AMOUNT'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RCNAM         20         6
      * Begin /CSD027/
     A**********  RCNUM          6  0     28                                                  CSD027
     A            RCNUM          6A       28
      * End /CSD027/
     A            RFACT          3  0     38
     A                                    41'/'
     A            RFCNO          2  0     42
     A            RSEQN          3  0     49
     A            RCCY           3        57
     A            RDATE     R             64REFFLD(MRDT)
     A  41                                77'UTILISATION START'
     A  42                                77'UTILISATION INCREASE'
     A  43                                77'UTILISATION DECREASE'
     A  44                                77'UTILISATION EXPIRY'
     A  45                                77'REVERSAL'
     A            RAMNT         17       107
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                44'*** END OF REPORT FOR BRANCH'
     A                                      SPACEB(3)
     A  37        RBRCA          3        73
     A  37                                77'***'
     A N37                                50'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
