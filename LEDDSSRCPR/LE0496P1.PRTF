      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Facility amendments today report')            *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE0496P1 - Facility Amendments Processing - Actions     *
      *                  Today                                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 246191             Date 06Mar07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *                 CLE014             Date 20Mar01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CLE005  *CREATE    Date 22May97               *
      *                 Xnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,LEH00068                                 *
      *             WNCPYSRC,LEH00069                                 *
      *             WNCPYSRC,LEH00070                                 *
      *             WNCPYSRC,LEH00071                                 *
      *  246191 - Display the amount for reactivated facilities       *
      *  CLE014 - Customer Driven Lending Enhancements                *
      *  CLE005 - Customer Lending Enhancements - Others              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE0496P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    37'FACILITY AMENDMENTS PROCE+
     A                                      SSING - ACTIONS TODAY'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    37'-------------------------+
     A                                      ---------------------'
     A                                      SPACEA(3)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                    10'CUSTOMER NAME/NO.'
     A                                    37'FACILITY'
     A                                    49'CCY'
     A                                    55'VALUE DATE'
     A                                    73'ACTION'
     A                                   101'DETAILS'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAL1
      *
     A            RCNAM         20         6
     A            RCNUM          6        28
     A*/COPY WNCPYSRC,LEH00068
     A            RFACT          3        38
     A                                    41'/'
     A            RFCNO          2        42
     A*/COPY WNCPYSRC,LEH00069
     A            RHCCY          3        49
     A            RVDAT          7        56
     A  30                                69'FACILITY INCREASE'
     A  31                                69'FACILITY DECREASE'
     A  32                                69'REVOLVING CREDIT CHANGE'
     A  35                                69'FACILITY REACTIVATION'
     A*/COPY WNCPYSRC,LEH00070
      ** Start of change 246191
     A**********                            SPACEA(1)                                         246191
      ** End of change 246191
      *
     A  30
      ** Start of change 246191
     AO 35                                                                                    246191
      ** End of change 246191
     AO 31        RHAMT         21        96
     A                                      SPACEA(1)
     A  32 33                             96'FROM Y TO N'
     A                                      SPACEA(1)
     A  32 34                             96'FROM N TO Y'
     A                                      SPACEA(1)
      **Start of Change CLE014
     A  32 38                             96'FROM Y TO T'
     A                                      SPACEA(1)
     A  32 39                             96'FROM N TO T'
     A                                      SPACEA(1)
     A  32 40                             96'FROM T TO Y'
     A                                      SPACEA(1)
     A  32 41                             96'FROM T TO N'
     A                                      SPACEA(1)
     A*/COPY WNCPYSRC,LEH00071
      **End of Change CLE014
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                48'***  END OF REPORT FOR +
     A                                      BRANCH'
     A                                      SPACEB(3)
     A  37        RBRCA     R             78REFFLD(BRCA)
     A  37                                83'***'
     A N37                                56'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
