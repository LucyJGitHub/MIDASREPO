      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Contingent facility a/c keys gen audit')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE0497P1 - Contingent Facility A/c Keys Generation      *
      *                  Report                                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE042             Date 06Sep05               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CLE007 *CREATE     Date 22May97               *
      *                 Xnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CLE025 - Credit Lines                                        *
      *  CLE007 - Customer Lending Enhancements - Others              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE0497P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    37'CONTINGENT FACILITY +
     A                                      ACCOUNT KEYS GENERATED'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    37'--------------------+
     A                                      ----------------------'
     A                                      SPACEA(3)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                    10'CUSTOMER'
     A                                    23'FACILITY'
     A                                    35'ACCOUNT KEY'
     A                                    58'AMOUNT'
     A                                    74'CURRENCY'
     A                                    87'REV IND'
      **Start of change CLE025
     A**********                         105'AMENDMENT ACTION'                                CLE025
     A                                    99'AMENDMENT ACTION'
      **End of change CLE025
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAL1
      *
     A            RCNUM          6        11
     A            RFACT          3        24
     A                                    27'/'
     A            RFCNO          2        28
     A*                                              Begin /CLE042/
     A************RAKEY*********10        35                                                  CLE042
     A            RAKEY         14        34                                                  CLE042
     A*                                              End   /CLE042/
     A            RAAMT         21        50
     A            RACCY          3        77
     A            RREVI          1        90
      **Start of change CLE025
     A**********  RACTN         30       105                                                  CLE025
     A            RACTN         32        99
      **End of change CLE025
     A                                      SPACEA(1)
      *
     A          R DETAL2
      *
     A                                    38'TOTAL ACCOUNT KEYS +
     A                                      GENERATED'
     A                                      SPACEB(1)
     A            RTGEN          5  0     70EDTCDE(4)
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                48'***  END OF REPORT FOR +
     A                                      BRANCH'
     A                                      SPACEB(3)
     A  37        RBRCA     R             78REFFLD(BRCA)
     A  37                                83'***'
     A N37                                56'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
