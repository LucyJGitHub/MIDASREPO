      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Customer Security Positions Extract')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Customer Lending Module
     A*                                                                  *
     A*    PRTF/LE0752AU  LE0752 RUN CONTROL REPORT                      *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*      COMPANY CONFIDENTIAL                                        *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    LAST AMEND NO. 00       DATE 00/00/00                         *
     A*                                                                  *
     A********************************************************************
     A*
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(TABTB10)
     A**
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A**
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     7'REFERENCE LE0752AU'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE1        004S 0    124EDTCDE(Z) SPACEA(2)
     A*
     A                                    37'CUSTOMER LENDING CUSTOMER SECURITY-
     A                                       POSITIONS EXTRACT - RUN CONTROLS'
     A                                      SPACEA(1)
     A                                    37'-----------------------------------
     A                                      ---------------------------------'
     A                                      SPACEA(1)
     A**
     A**   *******************
     A**   CONTROL REPORT
     A**   *******************
     A**
     A          R CONTROL
     A                                    60'FILE CONTROL' SPACEB(2)
     A                                    91'CALCULATED' SPACEA(1)
     A                                    53'NUMBER'
     A                                    74'VALUE   NUMBER'
     A                                   103'VALUE' SPACEA(2)
     A**
     A                                     6'FILE - CUSTOMER POSITIONS JOIN FIL-
     A                                      E 2 - JF/CPOSNJ2'
     A                                      SPACEA(1)
     A                                     6'-----------------------------------
     A                                      ----------------' SPACEA(2)
     A                                     6'COUNT OF RECORDS'
     A            NORE           5  0     54EDTCDE(3)
     A            CPOSCT         5  0     83EDTCDE(3)
     A**
     A  50                               112'*** DIFFERENCE ***'
     A                                   131' '
     A                                      SPACEA(3)
     A**
     A**
     A                                     6'FILE - LOAN PORTFOLIO EXTRACT AUDI-
     A                                      T - PF/FPORTT2'
     A                                     6'-----------------------------------
     A                                      --------------' SPACEB(1) SPACEA(2)
     A                                     6'NUMBER AND HASH TOTALS OF RECORDS -
     A                                      WRITTEN'
     A            EXTREC         5  0     83EDTCDE(3)
     A            OHASH1        15  0     90EDTCDE(3)
     A            OHASH2         3  0    105
     A**
     A**
     A**   **********************
     A**   NO DETAILS TO REPORT
     A**   **********************
     A**
     A          R NONE                      SPACEB(3)
     A                                    52'*** NO DETAILS TO REPORT ***'
     A**
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAILAU
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(3)
     A**
     A**   *******************
     A**   REPORT DATABASE ERROR
     A**   *******************
     A**
     A          R ERROR
     A                                      SPACEB(3)
     A                                    35'**** DATABASE ERROR ****'
     A                                    21'************************'
     A                                      SPACEB(2)
     A                                    45'************************'
     A                                    69'******'
     A                                    21'*'
     A                                      SPACEB(1)
     A                                    74'*'
     A                                    21'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         5        40
     A                                    45''''
     A                                    74'*'
     A                                    21'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        40
     A                                    69 '''    *'
     A                                    21'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM          8        40
     A                                    48''''
     A                                    74'*'
     A                                    21'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          2  0     40
     A                                    42''''
     A  97                                44''''
     A  97        DBSTAT         5        45
     A  97                                50''''
     A                                    74'*'
     A                                    21'*'
     A                                      SPACEB(1)
     A                                    74'*'
     A                                    21'************************'
     A                                      SPACEB(1)
     A                                    45'************************'
     A                                    69'******'
