      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_CONF42                                                    *
/*EXI *  TEXT('Midas LE Rollover confirmation advice')
/*ADD *  CHGPRTF FILE(LE1170P1) PAGESIZE(48 90) OVRFLW(48)            *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module                              *
     A*                                                               *
     A*  PRTF/LE1170P1 - Rollover Confirmation Advices                *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. 252771             Date 04Sep19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CLE033             Date 02Apr03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CLE006             Date 02Dec96               *
     A*                 S01408             DATE 07MAR96               *
     A*                 088807             DATE 21JUN95               *
     A*                 S01465             DATE 22MAR94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  252771 - Align name to the left and if customer 1 and        *
      *           customer 2 are the same, print only one. Partially  *
      *           applied fix 166266.                                 *
      *         - Applied fix for MD054047.                           *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE033 - Settlement method 09                                *
     A*  CLE006 - Customer Lending Enhancements - Rollovers           *
     A*  S01408 - Addition of core hook LE1170P101                    *
     A*  088807 - Output pay/receive instructions                     *
     A*  S01465 - BLI Lending Enhancements                            *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R LE1170D1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A*                                              Begin /088807/
     A**85*******                       6 40'DUPLICATE'
     A**25******* BRNM      R           9 35TEXT('Branch name')
     A***********                      12 35'ROLLOVER CONFIRMATION'
     A***********                      13 35'---------------------'
     A***********                      15 66'DATE'
     A*********** AGMRDT    R          15 73REFFLD(RUNA)
     A***********                      16 66'REF'
     A*********** RREF          13A    16 73TEXT('Loan no. & cust no.')
     A***********
     A*********** RCNA1         35A    18 50TEXT('Cust name & addr 1')
     A*********** RCNA2         35A    19 50TEXT('Cust name & addr 2')
     A*********** RCNA3         35A    21 50TEXT('Cust name & addr 3')
     A*********** RCNA4         35A    22 50TEXT('Cust name & addr 4')
     A*********** RONA1         35A    21 12TEXT('Cust name & addr 1')
     A*********** RONA2         35A    22 12TEXT('Cust name & addr 2')
     A***********
     A***********                      27 12'CCY'
     A***********                      27 21'PRINCIPAL'
     A***********                      27 38'FROM'
     A***********                      27 47'TO'
     A***********                      27 56'INTEREST RATE'
     A***********                      27 77'INTEREST'
     A***********
     A*********** RCCY      R          29 12REFFLD(CYCD)
     A**40******* RPAM0         12S 0  29 18TEXT('Principal amt 0 dec')
     A***********                           EDTCDE(A *)
     A**41******* RPAM1         13S 1  29 18TEXT('Principal amt 1 dec')
     A***********                           EDTCDE(A *)
     A**42******* RPAM2         14S 2  29 18TEXT('Principal amt 2 dec')
     A***********                           EDTCDE(A *)
     A**43******* RPAM3         15S 3  29 18TEXT('Principal amt 3 dec')
     A***********                           EDTCDE(A *)
     A*********** RFRDT          7A    29 38TEXT('From date')
     A*********** RTODT          7A    29 47TEXT('To date')
     A*********** RINTR         11S 7  29 56TEXT('Interest Rate')
     A***********                           EDTCDE(A *)
     A**40******* RINT0         12S 0  29 72TEXT('Interest 0 dec')
     A***********                           EDTCDE(A *)
     A**41******* RINT1         13S 1  29 72TEXT('Interest 1 dec')
     A***********                           EDTCDE(A *)
     A**42******* RINT2         14S 2  29 72TEXT('Interest 2 dec')
     A***********                           EDTCDE(A *)
     A**43******* RINT3         15S 3  29 72TEXT('Interest 3 dec')
     A***********                           EDTCDE(A *)
     A***********
     A***********                      32 56'TOTAL'
     A**40******* RTOT0         12S 0  32 72TEXT('Total amount 0 dec')
     A***********                           EDTCDE(A *)
     A**41******* RTOT1         13S 1  32 72TEXT('Total amount 1 dec')
     A***********                           EDTCDE(A *)
     A**42******* RTOT2         14S 2  32 72TEXT('Total amount 2 dec')
     A***********                           EDTCDE(A *)
     A**43******* RTOT3         15S 3  32 72TEXT('Total amount 3 dec')
     A***********                           EDTCDE(A *)
     A*
     A*/COPY WNCPYSRC,LE1170P105
     A  25        BRNM      R           2 50TEXT('Branch name')
     A  85                              3 40'DUPLICATE'
     A                                  4 55'ROLLOVER CONFIRMATION'
     A                                  5 55'---------------------'
     A            PARTIC        13A     6 55
     A                                  8 61'DATE'
     A            AGMRDT    R           8 67REFFLD(RUNA)
     A                                  9 61'REF.'
     A            RREF          13A     9 67TEXT('Loan no. & cust no.')
     A*
     A*/COPY WNCPYSRC,LE1170P102
      **Start of change 252771
     A**********  RCNA1         35A    12 44TEXT('Cust name & addr 1')                        252771
     A**********  RCNA2         35A    13 44TEXT('Cust name & addr 2')                        252771
     A**********  RONA1         35A    15  8TEXT('Cust name & addr 1')                        252771
     A**********  RCNA3         35A    15 44TEXT('Cust name & addr 3')                        252771
     A**********  RONA2         35A    16  8TEXT('Cust name & addr 2')                        252771
     A**********  RCNA4         35A    16 44TEXT('Cust name & addr 4')                        252771
     A            RCNA1         35A    12  8TEXT('Cust name & addr 1')
     A            RCNA2         35A    13  8TEXT('Cust name & addr 2')
     A            RCNA3         35A    14  8TEXT('Cust name & addr 3')
     A            RONA1         35A    14 44TEXT('Cust name & addr 1')
     A            RCNA4         35A    15  8TEXT('Cust name & addr 4')
     A            RONA2         35A    15 44TEXT('Cust name & addr 2')
      **End of change 252771
     A*/COPY WNCPYSRC,LE1170P103
     A*
     A                                 21  8'CCY'
     A                                 21 14'PRINCIPAL'
     A                                 21 32'FROM'
     A                                 21 42'TO'
     A                                 21 47'INTEREST RATE'
     A                                 21 69'INTEREST'
     A*
     A            RCCY      R          23  8REFFLD(CYCD)
     A  40        RPAM0         12S 0  23 12TEXT('Principal amt 0 dec')
     A                                      EDTCDE(A *)
     A  41        RPAM1         13S 1  23 12TEXT('Principal amt 1 dec')
     A                                      EDTCDE(A *)
     A  42        RPAM2         14S 2  23 12TEXT('Principal amt 2 dec')
     A                                      EDTCDE(A *)
     A  43        RPAM3         15S 3  23 12TEXT('Principal amt 3 dec')
     A                                      EDTCDE(A *)
     A            RFRDT          7A    23 31TEXT('From date')
     A            RTODT          7A    23 39TEXT('To date')
     A            RINTR         11S 7  23 47TEXT('Interest Rate')
     A                                      EDTCDE(A *)
     A  40        RINT0         12S 0  23 62TEXT('Interest 0 dec')
     A                                      EDTCDE(A *)
     A  41        RINT1         13S 1  23 62TEXT('Interest 1 dec')
     A                                      EDTCDE(A *)
     A  42        RINT2         14S 2  23 62TEXT('Interest 2 dec')
     A                                      EDTCDE(A *)
     A  43        RINT3         15S 3  23 62TEXT('Interest 3 dec')
     A                                      EDTCDE(A *)
     A*
     A                                 26 43'TOTAL'
     A  40        RTOT0         12S 0  26 62TEXT('Total amount 0 dec')
     A                                      EDTCDE(A *)
     A  41        RTOT1         13S 1  26 62TEXT('Total amount 1 dec')
     A                                      EDTCDE(A *)
     A  42        RTOT2         14S 2  26 62TEXT('Total amount 2 dec')
     A                                      EDTCDE(A *)
     A  43        RTOT3         15S 3  26 62TEXT('Total amount 3 dec')
     A                                      EDTCDE(A *)
     A*                                                                   S01408
     A*/COPY WNCPYSRC,LE1170P101                                          S01408
     A*                                                                   S01408
     A*
     A            RTIT2         27     32  7
     A            RTIT1         40     32 43
     A            RNAM2         35     33 10
     A            RNAM1         35     33 46
     A*                                                 /*Start CLE033*/
     A************RTWN2         11     34 10
     A************RTWN1         11     34 46
     A            RTWN2         15     34 10
     A            RTWN1         15     34 46
     A*                                                 /* End  CLE033*/
     A            RTIT4         10     35  7
     A            RTIT3         10     35 43
     A            RNAM4         35     36 10
     A            RNAM3         35     36 46
     A            RTWN4         20     37 10
     A            RTWN3         20     37 46
     A*/COPY WNCPYSRC,LE1170P109
     A N23                             39  7'FOR A/C OF'
     A N23        RORAC         25     39 46
     A N23        RCPNM         35     40 10
     A N23        RCPTN         20     41 10
     A N24        RTIT6         20     42  7
     A N24        RBTB1         35     43 10
     A N24        RBTB2         35     44 10
     A N24        RBTB3         35     45 10
     A  26                             46  7'BENEFICIARY BANK'
     A  26        RBTB4         35     47 10
     A                                 48  5'FINAL MATURITY DATE :'
     A            OMDAT          7     48 34
     A*                                              End   /088807/
     A*/COPY WNCPYSRC,LE1170P106
     A*
      *                                              Begin /CLE006/
     A          R LE1170D2
      *
     A*/COPY WNCPYSRC,LE1170P107
     A  25        BRNM      R           2 50TEXT('Branch name')
     A  85                              3 40'DUPLICATE'
     A                                  4 55'ROLLOVER CONFIRMATION'
     A                                  5 55'---------------------'
     A            PARTIC        13A     6 55
     A                                  8 61'DATE'
     A            AGMRDT    R           8 67REFFLD(RUNA)
     A                                  9 61'REF.'
     A            RREF          13A     9 67TEXT('Loan no. & cust no.')
      *
      **Start of change 252771
     A**********  RCNA1         35A    12 44TEXT('Cust name & addr 1')                        252771
     A**********  RCNA2         35A    13 44TEXT('Cust name & addr 2')                        252771
     A**********  RONA1         35A    15  8TEXT('Cust name & addr 1')                        252771
     A**********  RCNA3         35A    15 44TEXT('Cust name & addr 3')                        252771
     A**********  RONA2         35A    16  8TEXT('Cust name & addr 2')                        252771
     A**********  RCNA4         35A    16 44TEXT('Cust name & addr 4')                        252771
     A            RCNA1         35A    12  8TEXT('Cust name & addr 1')
     A            RCNA2         35A    13  8TEXT('Cust name & addr 2')
     A            RCNA3         35A    14  8TEXT('Cust name & addr 3')
     A            RONA1         35A    14 44TEXT('Cust name & addr 1')
     A            RCNA4         35A    15  8TEXT('Cust name & addr 4')
     A            RONA2         35A    15 44TEXT('Cust name & addr 2')
      **End of change 252771
     A*/COPY WNCPYSRC,LE1170P108
      *
     A                                 20  8'ORIG'
     A                                 20 32'NEW'
     A                                 21  8'CCY'
     A                                 21 14'PRINCIPAL'
     A                                 21 32'CCY'
     A                                 21 36'PRINCIPAL'
     A                                 21 56'FROM'
     A                                 21 64'TO'
     A                                 21 73'INTEREST RATE'
      *
     A            RCCY      R          23  8REFFLD(CYCD)
     A  40        RPAM0         12S 0  23 12TEXT('Principal amt 0 dec')
     A                                      EDTCDE(A *)
     A  41        RPAM1         13S 1  23 12TEXT('Principal amt 1 dec')
     A                                      EDTCDE(A *)
     A  42        RPAM2         14S 2  23 12TEXT('Principal amt 2 dec')
     A                                      EDTCDE(A *)
     A  43        RPAM3         15S 3  23 12TEXT('Principal amt 3 dec')
     A                                      EDTCDE(A *)
     A            RNCCY     R          23 32REFFLD(CYCD)
     A  30        RNPAM0        12S 0  23 36TEXT('New Prin. amt 0 dec')
     A                                      EDTCDE(A *)
     A  31        RNPAM1        13S 1  23 36TEXT('New Prin. amt 1 dec')
     A                                      EDTCDE(A *)
     A  32        RNPAM2        14S 2  23 36TEXT('New Prin. amt 2 dec')
     A                                      EDTCDE(A *)
     A  33        RNPAM3        15S 3  23 36TEXT('New Prin. amt 3 dec')
     A                                      EDTCDE(A *)
     A            RFRDT          7A    23 56TEXT('From date')
     A            RTODT          7A    23 64TEXT('To date')
     A            RINTR         11S 7  23 73TEXT('Interest Rate')
     A                                      EDTCDE(A *)
      *
     A                                 25 45'INTEREST'
     A  30        RINT0         12S 0  25 64TEXT('Interest 0 dec')
     A                                      EDTCDE(A *)
     A  31        RINT1         13S 1  25 64TEXT('Interest 1 dec')
     A                                      EDTCDE(A *)
     A  32        RINT2         14S 2  25 64TEXT('Interest 2 dec')
     A                                      EDTCDE(A *)
     A  33        RINT3         15S 3  25 64TEXT('Interest 3 dec')
     A                                      EDTCDE(A *)
     A                                 27 45'TOTAL'
     A  30        RTOT0         12S 0  27 64TEXT('Total amount 0 dec')
     A                                      EDTCDE(A *)
     A  31        RTOT1         13S 1  27 64TEXT('Total amount 1 dec')
     A                                      EDTCDE(A *)
     A  32        RTOT2         14S 2  27 64TEXT('Total amount 2 dec')
     A                                      EDTCDE(A *)
     A  33        RTOT3         15S 3  27 64TEXT('Total amount 3 dec')
     A                                      EDTCDE(A *)
      *
      */COPY WNCPYSRC,LE1170P101
      *
     A            RTIT2         27     32  7
     A            RTIT1         40     32 43
     A            RNAM2         35     33 10
     A            RNAM1         35     33 46
     A*                                                 /*Start CLE033*/
     A************RTWN2         11     34 10
     A************RTWN1         11     34 46
     A            RTWN2         15     34 10
     A            RTWN1         15     34 46
     A*                                                 /* End  CLE033*/
     A            RTIT4         10     35  7
     A            RTIT3         10     35 43
     A            RNAM4         35     36 10
     A            RNAM3         35     36 46
     A            RTWN4         20     37 10
     A            RTWN3         20     37 46
     A*/COPY WNCPYSRC,LE1170P110
     A N23                             39  7'FOR A/C OF'
     A N23        RORAC         25     39 46
     A N23        RCPNM         35     40 10
     A N23        RCPTN         20     41 10
     A N24        RTIT6         20     42  7
     A N24        RBTB1         35     43 10
     A N24        RBTB2         35     44 10
     A N24        RBTB3         35     45 10
     A  26                             46  7'BENEFICIARY BANK'
     A  26        RBTB4         35     47 10
     A                                 48  5'FINAL MATURITY DATE :'
     A            OMDAT          7     48 34
     A*/COPY WNCPYSRC,LE1170P104
      *                                              End   /CLE006/
