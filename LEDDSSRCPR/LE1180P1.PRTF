      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_CONF42                                                    *
/*EXI *  TEXT('Midas LE Consolidated confirmation print')             *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE1180P1 - Consolidated Confirmation Print              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CLE004 *Create     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CLE004 - Customer Lending Enhancements - Syndication         *
      *                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R LE1180D1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(9)
     A  10        RBRNM     R             33REFFLD(BRNM)
     A                                    70'Page'
     A            PAGE           4  0     75EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    35'CONSOLIDATED CONFIRMATION'
     A                                      SPACEA(1)
     A                                    35'-------------------------'
     A                                      SPACEA(1)
     A                                    70'DATE'
     A            RRUNA     R             75REFFLD(RUNA)
     A                                      SPACEA(1)
     A            RCRNM     R              8REFFLD(CRNM)
     A                                      SPACEA(1)
     A            RCNA1     R              8REFFLD(CNA1)
     A                                      SPACEA(1)
     A            RCNA2     R              8REFFLD(CNA2)
     A                                      SPACEA(1)
     A            RCNA3     R              8REFFLD(CNA3)
     A                                      SPACEA(1)
     A            RCNA4     R              8REFFLD(CNA4)
     A                                      SPACEA(2)
     A  09                                71'(Continuation)'
     A                                     8'Please be advised that the +
     A                                       following actions are scheduled:'
     A                                      SPACEA(2)
     A                                     1'Loan'
     A                                    57'Period/'
     A                                      SPACEA(1)
     A                                     1'Ref.'
     A                                     8'Action'
     A                                    24'Amount'
     A                                    42'Rate'
     A                                    56'Value Date'
     A                                    70'Balance'
     A                                    88'CCY'
     A                                      SPACEA(1)
      *
      *--------------------------------*
      ***  Report Detail Record Format *
      *--------------------------------*
      *
     A          R LE1180D2                  SPACEA(1)
      *
      **Start of change CLE148
     A**********  RLNRF     R              1REFFLD(LNRF)                                      CLE148
     A            RLNRF          6A        1
      **End of change CLE148
     A            RACTN     R              8REFFLD(ZZ015)
     A            RAMNT     R             24REFFLD(ZZ017)
     A            RRATE     R             41REFFLD(ZZ012)
     A  11        RDATE     R             57REFFLD(RUNA)
     A N11        RPERD     R             54REFFLD(ZZ015)
     A            RBALC     R             70REFFLD(ZZ017)
     A            RCURR     R             88REFFLD(CCY)
      *
      *--------------------------------*
      ***  End of Report Record Format *
      *--------------------------------*
      *
     A          R LE1180D3                  SKIPB(15)
     A  24                                27'*** End of Report for Branch'
     A  24        BRCAT     R             56REFFLD(BRCA)
     A  24                                60'***'
     A N24                                38'*** End of Report ***'
      *
