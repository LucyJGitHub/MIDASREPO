      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Released transactions print')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE1310P1 - Released Transactions Print                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE030  *CREATE    Date 20Sep02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE030 - Customer Lending Enhancements                       *
      *           - Release Authorisation                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE  LE1310P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   125PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   107TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  10                                40'LE RELEASED TRANSACTION+
     A                                      S - LISTING'
     A N10                                40'LE RELEASED TRANSACTION+
     A                                      S - AUDIT LIST'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A  10                                40'------------------------+
     A                                      -----------'
     A N10                                40'------------------------+
     A                                      --------------'
     A                                      SPACEA(1)
      *
     A          R SUB1
      *
     A                                      SPACEB(1)
     A                                     1'Loan'
     A                                    81'Previous Auth.'
     A                                    96'Release'
     A                                   108'Release'
     A                                   116'Release'
     A                                      SPACEA(1)
     A*
     A                                     1'Number'
     A                                     9'Description'
     A                                    60'Amount'
     A                                    76'CCY'
     A                                    83'User'
     A                                    98'User'
     A                                   110'Date'
     A                                   118'Time'
     A                                      SPACEA(1)
      *
     A          R DTL1
      *
     A                                      SPACEA(1)
     A            PLNRF          6         1
     A            PDESC         40         9
     A            PCPAM         23        51
     A            PCCY           3        76
     A            PXUSR         10        82
     A            PRUSR         10        96
     A            PLCD           7       108
     A            PTIME          6  0    116EDTWRD('  :  :  ')
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
