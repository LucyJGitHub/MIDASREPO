      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas LE Lending PC interface')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE2010AU - Lending PC INTERFACE                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 133783             Date 08May98               *
      *  Prev Amend No. CLE004  *CREATE    Date 07MAY97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  133783 - QMQM message unclear                                *
      *  CLE004 - Created for Customer Lending Enhancements           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE2010AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    42'Lending PC Interface    +
     A                                                      - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    42'------------------------+
     A                                      -----------------------'
     A                                      SPACEA(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A  30                                11'*  MQ REASON CODE '''
     A  30                                  SPACEB(1)
     A  30        P#RSCOD        9  0     30
     A  30                                39''''
     A  30                                64'*'
      *                                              Begin /133783/
     A  31                                11'*    ERROR MSG '
     A  31                                  SPACEB(1)
     A  31        DBMSG1        29        30
     A  31                                64'*'
     A  31                                11'*'
     A  31                                  SPACEB(1)
     A  31        DBMSG2        29        30
     A  31                                64'*'
     A  31                                11'*'
     A  31                                  SPACEB(1)
     A  31        DBMSG3        29        30
     A  31                                64'*'
     A  31                                11'*'
     A  31                                  SPACEB(1)
     A  31        DBMSG4        29        30
     A  31                                64'*'
      *                                              End   /133783/
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
