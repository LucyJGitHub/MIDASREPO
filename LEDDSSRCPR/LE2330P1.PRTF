      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Unauthorized loans check')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE2330P1 - Unauthorised Loans Check                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Last Amend No. 201123             Date 07Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 122603             Date 05Sep97               *
      *                 CLE002 *Create     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  201123 - Include Original Borrower in this report when       *
      *           processing type of loan is either 64, 65, 68 or 71. *
      *  122603 - Value date in non-standard format                   *
      *  CLE002 - Customer Lending Enhancements - Authorisation       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE2330P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    51'Report of Unauthorized T+
     A                                      ransactions'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    51'------------------------+
     A                                      -----------'
     A                                      SPACEA(2)
     A                                    35'Loan'
     A                                    44'Cust'
     A**Start of change 201123
     A**********                          54'Facility'                                        201123
     A**********                          66'Value'                                           201123
     A**********                          79'Sequence'                                        201123
     A                                    53'Orig'
     A                                    61'Facility'
     A                                    73'Value'
     A                                    82'Sequence'
     A**End of change 201123
     A                                    97'Insert User'
     A                                   117'Amend User'
     A                                      SPACEA(1)
     A                                     1'Transaction Type'
     A                                    24'Branch'
     A                                    34'Number'
     A                                    43'Number'
     A**Start of change 201123
     A**********                          54'Number'                                          201123
     A**********                          66'Date'                                            201123
     A**********                          79'Number'                                          201123
     A                                    51'Borrower'
     A                                    61'Number'
     A                                    73'Date'
     A                                    82'Number'
     A**End of change 201123
     A                                    97'Profile'
     A                                   117'Profile'
     A                                      SPACEA(1)
     A                                     1'----------------'
     A                                    24'------'
     A                                    34'------'
     A                                    43'------'
     A**Start of change 201123
     A**********                          54'------'                                          201123
     A**********                          66'-----'                                           201123
     A**********                          79'--------'                                        201123
     A                                    51'--------'
     A                                    61'------'
     A                                    73'-----'
     A                                    82'--------'
     A**End of change 201123
     A                                    97'-----------'
     A                                   117'----------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            PTRAN         22         1
     A            PBRCA          3        24
     A            PLNRF          6        34
     A            PCNUM          6        43
     A**Start of change 201123
     A**********  PFACT          6        54                                                  201123
     A            OLNOZ          6        52
     A            PFACT          6        61
     A**End of change 201123
      *                                      Begin /122603/
     A************PVDAT          6  0     64EDTWRD('  /  /  ')
     A**Start of change 201123
     A**********  PVDAT     R             64REFFLD(MRDT)                                      201123
     A*/COPY WNCPYSRC,LEH00459
     A            PVDAT     R             71REFFLD(MRDT)
     A*/COPY WNCPYSRC,LEH00460
     A**End of change 201123
      *                                      End   /122603/
     A**Start of change 201123
     A**********  PLASN          3        79                                                  201123
     A            PLASN          3        82
     A**End of change 201123
     A            PIUPR         10        97
     A            PAUPR         10       117
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
