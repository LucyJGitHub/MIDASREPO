      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Outstanding loans by part rept (ord)')        *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE3110P1 - Outstanding Loans by Participant Report      *
      *                  for Ordinary Loans                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CLE042             Date 06Sep05               *
      *                 CLE004 *Create     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 -  Alpha Loan Reference
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CLE004 - Customer Lending Enhancements - Syndications        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE3110P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                     6'ORDINARY LOANS'
      *
     A                                    48'OUTSTANDING LOANS BY +
     A                                      PARTICIPANT'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    48'---------------------+
     A                                      -----------'
     A                                      SPACEA(2)
      *
      ** Detail Sub-Heading:
      *
     A                                    30'LOAN'
     A                                    38'LOAN TYPE/'
     A                                    50'BRANCH'
     A                                      SPACEA(1)
     A                                     6'CUSTOMER'
     A                                    30'NUMBER'
     A                                    38'SUBTYPE'
     A                                    50'CODE'
     A                                    60'FACILITY'
     A                                    74'CCY'
     A                                    81'PRINCIPAL AMOUNT'
     A                                   100'START DATE'
     A                                   112'MATURITY DATE'
     A                                      SPACEA(1)
     A                                     6'--------------------'
     A                                    30'------'
     A                                    38'----------'
     A                                    50'------'
     A                                    58'-------------'
     A                                    74'---'
     A                                    81'----------------'
     A                                   100'----------'
     A                                   112'-------------'
     A                                      SPACEA(2)
      *
      ** Detail Record Format
      *
     A          R DETAIL1
     A N34        RCRNM     R              6REFFLD(CRNM)
      **Start of change CLE148
     A**********  RLNRF     R             30REFFLD(LNRF)                                      CLE148
     A            RLNRF          6A       30
      **End of change CLE148
     A            RLTYP     R             40REFFLD(LTYP)
                                          42'/'
     A            RSUTP     R             43REFFLD(SUTP)
     A*                                              Begin /CLE042/
     A            RCLAS          4A       45                                                  CLE042
     A*                                              End   /CLE042/
     A            RBRCA     R             51REFFLD(BRCA)
      * Begin /CSD027/
     A**********  RFCUS     R             58REFFLD(FCUS)                                      CSD027
     A            RFCUS          6A       58TEXT('Facility customer')
      * End /CSD027/
     A            RFTYP     R             65REFFLD(FTYP)
     A                                    68'/'
     A            RFSEQ     R             69REFFLD(FSEQ)
     A            RCCY      R             74REFFLD(CCY)
     A            RCPAM         18        81
     A            RVDAT     R            102REFFLD(MRDT)
     A            RMDAT     R            115REFFLD(MRDT)
      *
     A                                      SPACEA(1)
      *
      ** Report Trailer
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH'
     A                                      SPACEB(3)
     A  37        RBRCH     R             76REFFLD(BRCA)
     A  37                                79'  ***'
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
