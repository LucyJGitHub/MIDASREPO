      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Syndicated loans exception report')           *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE3120P1 - Syndicated Loans Exception Report            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CLE042             Date 06Sep05               *
      *                 CLE004 *Create     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CLE004 - Customer Lending Enhancements - Syndications        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(001)
     A                                     6
     A                                      'REFERENCE LE3120P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            107
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   107
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    45
     A                                      'SYNDICATED LOANS EXCEPTION REPORT'
     A                                      SPACEB(002)
      *
     A  37                                89
     A                                      'BRANCH -'
     A  37        A8BRCD    R             98
     A                                      REFFLD(BRCA)
     A  37        A8BRNM    R            102
     A                                      REFFLD(BRNM)
     A                                    45
     A                                      '---------------------------------'
     A                                      SPACEB(001)
     A                                    40
     A                                      'LOAN'
     A                                      SPACEB(002)
     A                                    48
     A                                      'LOAN TYPE/'
     A                                    89
     A                                      'INTEREST'
     A                                   105
     A                                      'START'
     A                                   117
     A                                      'MATURITY'
     A                                     6
     A                                      'CUSTOMER'
     A                                      SPACEB(001)
     A                                   +25
     A                                      'NUMBER'
     A                                    49
     A                                      'SUBTYPE'
     A                                    61
     A                                      'CCY'
     A                                    69
     A                                      'PRINCIPAL AMOUNT'
     A                                    91
     A                                      'RATE'
     A                                   105
     A                                      'DATE'
     A                                   119
     A                                      'DATE'
     A                                     6
     A                                      SPACEB(001)
     A                                      '----------------------------'
     A                                    39
     A                                      '------'
     A                                    48
     A                                      '----------'
     A                                    61
     A                                      '---'
     A                                    68
     A                                      '-----------------'
     A                                    89
     A                                      '---------'
     A                                   104
     A                                      '-------'
     A                                   117
     A                                      '--------'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A                                      SPACEB(001)
      * Begin /CSD027/
     A**********  RCNUM          6S  O     6                                                  CSD027
     A            RCNUM          6A  O     6
      * End /CSD027/
     A            RBCRNM        20A  O    14
      **Start of change CLE148
     A**********  RLNRF          6S  O    39                                                  CLE148
     A            RLNRF          6A  O    39
      **End of change CLE148
     A            RLTYP          2A  O    50
     A                                    52
     A                                      '/'
     A            RSUTP          2A  O    53
     A*                                              Begin /CLE042/
     A            RCLAS          4A  O    55                                                  CLE042
     A*                                              End   /CLE042/
     A            RCCY           3A  O    61
     A            RCPAM         17A  O    68
     A            RINTR          9S 5O    89
     A                                      EDTCDE(3)
     A            RVDAT          7A  O   104
     A            RMDAT          7A  O   118
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46
     A                                      '***  END OF REPORT FOR BRANCH'
     A                                      SPACEB(003)
     A  37        A8BRCD         3A  O    76
     A  37                                81
     A                                      '***'
     A N37                                51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
