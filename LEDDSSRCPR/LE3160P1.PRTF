      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Manual facility utilisation report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE3160P1 - Manual Facility Utilisation Report           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD055657           Date 14May20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *                 198714             Date 13Feb02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 147935             Date 04Dec98               *
      *                 CLE005 *Create     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD055657 - Manual transaction from FFC TI Interface went to  *
      *             repair queue because the facility exchange rate is*
      *             longer than the defined facility exchange rate in *
      *             Midas. Increase the field definition of the Midas *
      *             facility exchange rate.                           *
      *  MD046248 - Finastra Rebranding                               *
      *  198714 - Printer overflow problem encountered when there is  *
      *           more than 15 Facility Utilizations to be reported.  *
      *  147935 - Addition of insert, authorise and amend user        *
      *           profiles to lending reports                         *
      *  CLE005 - Customer Lending Enhancements - Others              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE3160P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    47'MANUAL FACILITY UTILISAT+
     A                                      ION AUDIT REPORT'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    47'------------------------+
     A                                      ----------------'
     A                                      SPACEA(3)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A**Start of change 198714
     A N11                                 6'Facility'
     A N11                                26'Utilisation'
     A N11                                44'Currency'
     A N11                                57'Exchange'
     A N11                                72'Start'
     A N11                                84'Expiry'
     A N11                                95'Narrative'
     A N11                                  SPACEA(1)
     A N11                                29'Amount'
     A N11                                59'Rate'
     A N11                                72'Date'
     A N11                                85'Date'
     A N11                                  SPACEA(2)
     A**********                           6'Facility'                                        198714
     A**********                          26'Utilisation'                                     198714
     A**********                          44'Currency'                                        198714
     A**********                          57'Exchange'                                        198714
     A**********                          72'Start'                                           198714
     A**********                          84'Expiry'                                          198714
     A**********                          95'Narrative'                                       198714
     A**********                            SPACEA(1)                                         198714
     A**********                          29'Amount'                                          198714
     A**********                          59'Rate'                                            198714
     A**********                          72'Date'                                            198714
     A**********                          85'Date'                                            198714
     A**********                            SPACEA(2)                                         198714
     A**End of change 198714
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RCNUM          6         3
     A                                     9'-'
     A            RFACT          3        10
     A                                    13'-'
     A            RFCNO          2        14
     A            RUAMT         21        21
     A            RUCCY          3        47
      **Start of Change MD055657
     A**********  RUXRT         12        54                                                MD055657
     A            RUXRT         14        54
      **End of Change MD055657
     A            RSTDT          7        71
     A            REXDT          7        83
     A            RUNAR         30        95
     A  40                               126'DELETED'
     A                                   125' '
     A                                      SPACEA(1)
      *                                              Begin /147935/
     A                                    26'INSERTING USER'
     A            RIUSR     R             42REFFLD(ZZ010)
     A                                    57'AMENDING USER'
     A            RAUSR     R             72REFFLD(ZZ010)
     A                                    88'AUTHORISING USER'
     A            RXUSR     R            106REFFLD(ZZ010)
     A**Start of change 198714
     A**********                            SPACEA(2)                                         198714
     A N10                                  SPACEA(2)
     A**End of change 198714
      *                                              End   /147935/
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                48'***  END OF REPORT FOR +
     A                                      BRANCH'
     A                                      SPACEB(3)
     A  37        RBRCA     R             78REFFLD(BRCA)
     A  37                                83'***'
     A N37                                56'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
