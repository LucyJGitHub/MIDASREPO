      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Rollover Rate Fixing Advice')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE3165P1 - Rollover Rate Fixing Advice                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CLE011  *CREATE    Date 07Nov00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE011 - Rate Fixing Days for Lending Rollover Processing    *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-------------------------------*
      ** Report Header Record Format **
      *-------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2000')
     A                                      SKIPB(3)
      *
     A            BJURPT    R             37REFFLD(URPT)
     A                                      SPACEA(6)
      *
     A                                    46'ROLLOVER RATE +
     A                                      FIX ADVICE'
     A                                      SPACEA(2)
      *
     A  22                                52'CANCELLATION'
     A                                    91'Date'
     A            BJMRDT    R             96REFFLD(MRDT)
     A                                      SPACEA(2)
      *
     A                                    91'Ref'
     A            RREF           6        96
     A                                      SPACEA(2)
      *
      *------------------------*
      ** Detail Record Format **
      *------------------------*
      *
     A          R DTLP1                     TEXT('Detail format')
     A                                      SPACEB(4)
      *
     A            RPNAME        35         6TEXT('Cust/Part bank name')
     A            RBNAME        35        50TEXT('Borrower name')
     A                                      SPACEA(2)
      *
     A            RPADR2        35         6TEXT('Cust/Part address 2')
     A            RBADR2        35        50TEXT('Borrower address 2')
     A                                      SPACEA(2)
      *
     A            RPADR3        35         6TEXT('Cust/Part address 3')
     A            RBADR3        35        50TEXT('Borrower address 3')
     A                                      SPACEA(2)
      *
     A            RPADR4        35         6TEXT('Cust/Part address 4')
     A            RBADR4        35        50TEXT('Borrower address 4')
     A                                      SPACEA(6)
      *
     A            RCONF1       122         6
     A                                      SPACEA(2)
     A  22        RCONF2       122         6
     A                                      SPACEA(2)
     A                                   132' '
     A                                      SPACEA(4)
      *
     A                                     6'Ccy'
     A                                    16'Principal'
     A                                    39'From'
     A                                    53'To'
     A                                    67'Int. Rate'
     A                                    86'Rollover Date'
     A  23                               105'New Ccy'
     A N22N23                            105'New Fixed Rate'
     A N22 23                            115'New Fixed Rate'
     A                                   132' '
     A                                      SPACEA(2)
      *
     A            RCCY           3         6TEXT('Currency')
     A            RAMNT         16        16TEXT('Principal')
     A            RVDAT          7        39TEXT('From Date')
     A            RMDAT          7        53TEXT('To Date')
     A            RINTR         12        67TEXT('Interest Rate')
     A            RRDAT          7        86TEXT('Rollover Date')
     A  23        RNCCY          3       105TEXT('New Currency')
     A N22N23     RRFRT1        12       105TEXT('New Fixed Rate')
     A N22 23     RRFRT2        12       115TEXT('New Fixed Rate')
     A                                   132' '
     A                                      SPACEA(2)
