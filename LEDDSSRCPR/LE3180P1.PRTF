      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Facility history audit report amounts')       *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE3180P1 - Datapatch Printer File Program               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4.01 -------------------------------------------*
      *                 196768  *CREATE    Date 10May02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,LEH00205                                 *
      *             WNCPYSRC,LEH00206                                 *
      *             WNCPYSRC,LEH00207                                 *
      *             WNCPYSRC,LEH00208                                 *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  196768   This is a report which identifies facilities with   *
      *           an error in either their drawn amount or their      *
      *           facility amount.                                    *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
      *
     A          R HEADER1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     6
     A                                      'LE3180P1'
     A            BJURPT        53A  O    29
     A                                    86
     A                                      'BRANCH'
     A            BRCA           3A  O    94
     A                                   104
     A                                      'DATE'
     A            BJMRDT         7A  O   109
     A                                   122
     A                                      'PAGE'
     A                                    +2
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                    35
     A                                      'FACILITIES WITH AN ERROR EXCEEDING'
     A                                      SPACEB(001)
     A            ROUND          2S 0O    70
     A                                    73
     A                                      'IN EITHER DRAWN OR FACILITY AMOUNT'
     A                                    35
     A                                      '-------------------------+
     A                                      --------------------------+
     A                                      ----------------------'
     A                                      SPACEB(001)
     A                                    31
     A                                      'FACILITY DRAWN AMOUNT'
     A                                      SPACEB(002)
     A                                    70
     A                                      'FACILITY AMOUNT'
     A                                    92
     A                                      'REV. CRED'
     A                                   114
     A                                      HIGHLIGHT 'DIFFERENCE'
     A                                    01
     A                                      'CUSTOMER'
     A                                      SPACEB(001)
     A                                    10
     A                                      'FACILITY'
     A*/COPY WNCPYSRC,LEH00205
     A                                    19
     A                                      'CCY'
     A                                    23
     A                                      'MCCY'
     A*/COPY WNCPYSRC,LEH00206
     A                                    29
     A                                      '(FCLTY DET)'
     A                                    42
     A                                      '(FCLTY HIST)'
     A                                    61
     A                                      '(FCLTY DET)'
     A                                    76
     A                                      '(FCLTY HIST)'
     A                                    91
     A                                      'FCLTY'
     A                                    98
     A                                      'HIST'
     A                                   109
     A                                      'DRAWN'
     A                                   123
     A                                      'FAC AMT'
     A                                      SPACEA(001)
      *
      ** Report Detail
      *
     A          R DETAIL1
     A                                      SPACEA(001)
      * Begin /CSD027/
     A**********  CNUM           6S 0O     2                                                  CSD027
     A*/COPY WNCPYSRC,LEH00207
     A            CNUM           6A  O     2
      * End /CSD027/
     A                                      HIGHLIGHT
     A            FACT           3S 0O     9
     A                                      HIGHLIGHT
     A                                    12
     A                                      '/'
     A            FCNO           2S 0O    13
     A                                      HIGHLIGHT
     A            FCCY           3A  O    19
     A*/COPY WNCPYSRC,LEH00208
     A            MCCY           1A  O    24
      *
     A  60        SOAM10        13S 0O    24EDTWRD(' ,   ,   ,   , 0 -')
     A  61        SOAM11        13S 1O    24EDTWRD('   ,   ,   , 0 . -')
     A  62        SOAM12        13S 2O    24EDTWRD('  ,   ,   , 0 .  -')
     A  63        SOAM13        13S 3O    24EDTWRD(' ,   ,   , 0 .   -')
     A  60        SFDRM0        13S 0O    39EDTWRD(' ,   ,   ,   , 0 -')
     A  61        SFDRM1        13S 1O    39EDTWRD('   ,   ,   , 0 . -')
     A  62        SFDRM2        13S 2O    39EDTWRD('  ,   ,   , 0 .  -')
     A  63        SFDRM3        13S 3O    39EDTWRD(' ,   ,   , 0 .   -')
     A  60        SFAMT0        13S 0O    57EDTWRD(' ,   ,   ,   , 0 -')
     A  61        SFAMT1        13S 1O    57EDTWRD('   ,   ,   , 0 . -')
     A  62        SFAMT2        13S 2O    57EDTWRD('  ,   ,   , 0 .  -')
     A  63        SFAMT3        13S 3O    57EDTWRD(' ,   ,   , 0 .   -')
     A  60        SFCAM0        13S 0O    74EDTWRD(' ,   ,   ,   , 0 -')
     A  61        SFCAM1        13S 1O    74EDTWRD('   ,   ,   , 0 . -')
     A  62        SFCAM2        13S 2O    74EDTWRD('  ,   ,   , 0 .  -')
     A  63        SFCAM3        13S 3O    74EDTWRD(' ,   ,   , 0 .   -')
     A            RVCR           1A  O    95
     A            FARCIN         1A  O    98
     A  60        DIFOA0        13S 0O   101EDTWRD(' ,   ,   ,   , 0 -')
     A  61        DIFOA1        13S 1O   101EDTWRD('   ,   ,   , 0 . -')
     A  62        DIFOA2        13S 2O   101EDTWRD('  ,   ,   , 0 .  -')
     A  63        DIFOA3        13S 3O   101EDTWRD(' ,   ,   , 0 .   -')
     A  60        DIFAM0        13S 0O   116EDTWRD(' ,   ,   ,   , 0 -')
     A  61        DIFAM1        13S 1O   116EDTWRD('   ,   ,   , 0 . -')
     A  62        DIFAM2        13S 2O   116EDTWRD('  ,   ,   , 0 .  -')
     A  63        DIFAM3        13S 3O   116EDTWRD(' ,   ,   , 0 .   -')
      *
      ** Report Summary by branch
      *
     A          R BRCHEN1
     A                                    35
     A                                      'Number of Facilities in +
     A                                      file for Branch '
     A                                      SPACEB(003)
     A            PREBRC         3A  O    76
     A                                    81'='
     A            BFCAUD         5S 0O    84
     A                                      EDTCDE(3)
     A                                    35
     A                                      'Number of Facilities in +
     A                                      Error in Branch '
     A                                      SPACEB(001)
     A                                    81'='
     A            BFCER1         5S 0O    84
     A                                      EDTCDE(3)
     A N12                                51
     A                                      ' ***  END OF REPORT FOR BRANCH ***'
     A                                      SPACEB(003)
      *
      ** Report Summary
      *
     A          R TRAILER1
     A                                    35
     A                                      'Number of Facilities in +
     A                                      Facility Detail File'
     A                                      SPACEB(003)
     A                                    81'='
     A            FACAUD         5S 0O    84
     A                                      EDTCDE(3)
     A                                    35
     A                                      'Number of Facilities in +
     A                                      Error'
     A                                      SPACEB(001)
     A                                    81'='
     A            FAMERR         5S 0O    84
     A                                      EDTCDE(3)
     A N12                                51
     A                                      '   ***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A  12                                51
     A                                      '***  NO DETAILS TO REPORT+
     A                                        ***'
     A                                      SPACEB(003)
      *
      ** Database Error
      *
     A          R DBERROR1
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SKIPA(43)
     A                                    35'*** END OF REPORT ***'
