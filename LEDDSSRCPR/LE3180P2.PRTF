      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Facility history audit report rev. credit')   *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE3180P2 - Datapatch Printer File Program               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. MD022057           Date 08Nov16               *
      *  Prev Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 30Nov05               *
      * Midas Release 4.01 -------------------------------------------*
      *                 196768  *CREATE    Date 20May02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD022057 - Increased variable size to report actual number   *
      *             of facilities.                                    *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,LEH00209                                 *
      *             WNCPYSRC,LEH00210                                 *
      *             WNCPYSRC,LEH00211                                 *
      *             WNCPYSRC,LEH00212                                 *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  196768   This is a report which identifies facilities with   *
      *           an error in their revolving credit indicator.       *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
      *
     A          R HEADER2
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE LE3180P2'
     A            BJURPT        53A  O    29
     A                                    86
     A                                      'BRANCH'
     A            BRCA           3A  O    94
     A                                   104
     A                                      'DATE'
     A            BJMRDT         7A  O   109
     A                                   122
     A                                      'PAGE'
     A                                    +2
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                    35
     A                                      'FACILITIES WITH AN INCORRECT REVOL+
     A                                      VING CREDIT INDICATOR'
     A                                      SPACEB(001)
     A                                    35
     A                                      '-------------------------+
     A                                      --------------------------------'
     A                                      SPACEB(001)
     A                                    40
     A                                      'REVOLVING CREDIT IND.'
     A                                      SPACEB(001)
     A*/COPY WNCPYSRC,LEH00209
     A                                    11
     A                                      'CUSTOMER'
     A                                      SPACEB(001)
     A                                    20
     A                                      'FACILITY'
     A*/COPY WNCPYSRC,LEH00210
     A                                    29
     A                                      'CCY'
     A                                    33
     A                                      'MCCY'
     A                                    42
     A                                      'FCLTY'
     A                                    50
     A                                      'HISTORY'
     A                                      SPACEA(001)
      *
      ** Report Detail
      *
     A          R DETAIL2
     A                                      SPACEA(001)
      * Begin /CSD027/
     A**********  CNUM           6S 0O    12                                                  CSD027
     A*/COPY WNCPYSRC,LEH00211
     A            CNUM           6A  O    12
      * End /CSD027/
     A            FACT           3S 0O    21
     A                                    24
     A                                      '/'
     A            FCNO           2S 0O    25
     A*/COPY WNCPYSRC,LEH00212
     A            FCCY           3A  O    29
     A            MCCY           1A  O    34
      *
     A            RVCR           1A  O    43
     A            FARCIN         1A  O    52
      *
      ** Report Summary by branch
      *
     A          R BRCHEN2
     A                                    35
     A                                      'Number of Facilities in +
     A                                      file for Branch '
     A                                      SPACEB(003)
     A            PREBRC         3A  O    76
     A                                    81'='
      ** Start of change MD022057
     A**********  BFCAUD         5S 0O    84                                                MD022057
     A            BFCAUD         7S 0O    84
      ** End of change MD022057
     A                                      EDTCDE(3)
     A                                    35
     A                                      'Number of Facilities in +
     A                                      Error in Branch '
     A                                      SPACEB(001)
     A                                    81'='
      ** Start of change MD022057
     A**********  BFCER2         5S 0O    84                                                MD022057
     A            BFCER2         7S 0O    84
      ** End of change MD022057
     A                                      EDTCDE(3)
     A N13                                51
     A                                      ' ***  END OF REPORT FOR BRANCH ***'
     A                                      SPACEB(003)
      *
      ** Report Summary
      *
     A          R TRAILER2
     A                                    35
     A                                      'Number of Facilities in +
     A                                      Facility Detail File'
     A                                      SPACEB(003)
     A                                    81'='
      ** Start of change MD022057
     A**********  FACAUD         5S 0O    84                                                MD022057
     A            FACAUD         7S 0O    84
      ** End of change MD022057
     A                                      EDTCDE(3)
     A                                    35
     A                                      'Number of Facilities with incor+
     A                                      rect Revolving Credit Indicator'
     A                                      SPACEB(001)
     A                                    98'='
      ** Start of change MD022057
     A**********  REVERR         5S 0O   100                                                MD022057
     A            REVERR         7S 0O   100
      ** End of change MD022057
     A                                      EDTCDE(3)
     A N13                                51
     A                                      '   ***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A  13                                51
     A                                      '***  NO DETAILS TO REPORT+
     A                                        ***'
     A                                      SPACEB(003)
      *
      ** Database Error
      *
     A          R DBERROR2
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SKIPA(43)
     A                                    35'*** END OF REPORT ***'
