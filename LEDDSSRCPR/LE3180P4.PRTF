      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Facility history audit report summary')       *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE3180P4 - Datapatch Printer File Program               *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD022057           Date 08Nov16               *
      *                 AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 30Nov05               *
      * Midas Release 4.01 -------------------------------------------*
      *                 196768  *CREATE    Date 20May02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD022057 - Increased variable size to report actual number   *
      *             of facilities.                                    *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,LEH00213                                 *
      *             WNCPYSRC,LEH00214                                 *
      *             WNCPYSRC,LEH00215                                 *
      *             WNCPYSRC,LEH00216                                 *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  196768   This is a summary report, produced by LE3180, which *
      *           lists all facilities which may have errors in their *
      *           facility history file. An alphabetic code is used to*
      *           identify what types of error each facility has.     *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
      *
     A          R HEADER4
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     3
     A                                      'LE3180P4'
     A            BJURPT        53A  O    16
     A                                    73
     A                                      'BRANCH'
     A            BRCA           3A  O    81
     A                                    91
     A                                      'DATE'
     A            BJMRDT         7A  O    96
     A                                   104
     A                                      TIME
     A                                   113
     A                                      'PAGE'
     A                                   118
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                    25
     A                                      HIGHLIGHT 'FACILITY HISTORY AUDIT +
     A                                      REPORT - SUMMARY OF FACILITIES WI+
     A                                      TH ERRORS'
     A                                      SPACEB(001)
     A                                    25
     A                                      '-------------------------------+
     A                                      --------------------------+
     A                                      ---------'
     A                                      SPACEB(001)
     A                                    35
     A                                      'KEY TO ERROR CODES'
     A                                      SPACEB(001)
     A                                    35
     A                                      'A = Incorrect Drawn Amount'
     A                                      SPACEB(001)
     A                                    35
     A                                      'B = Incorrect Facility Amount'
     A                                      SPACEB(001)
     A                                    35
     A                                      'C = Incorrect Revolving Credit Ind+
     A                                      icator'
     A                                      SPACEB(001)
     A*/COPY WNCPYSRC,LEH00213
     A                                    11
     A                                      'CUSTOMER'
     A                                      SPACEB(003)
     A                                    20
     A                                      'FACILITY'
     A*/COPY WNCPYSRC,LEH00214
     A                                    29
     A                                      'CCY'
     A                                    33
     A                                      'MCCY'
     A                                    38
     A                                      'ERROR CODES'
      *
      ** Report Detail
      *
     A          R DETAIL4
     A                                      SPACEA(001)
      * Begin /CSD027/
     A**********  CNUM           6S 0O    12                                                  CSD027
     A*/COPY WNCPYSRC,LEH00215
     A            CNUM           6A  O    12
      * End /CSD027/
     A            FACT           3S 0O    21
     A                                    24
     A                                      '/'
     A            FCNO           2S 0O    25
     A*/COPY WNCPYSRC,LEH00216
     A            FCCY           3A  O    29
     A            MCCY           1A  O    34
     A            ERR1           1A  O    38
     A            ERR2           1A  O    40
     A            ERR3           1A  O    42
     A            ERR4           1A  O    44
     A            ERR5           1A  O    46
      *                                              Begin /198781/
     A  14N15                             49
     A                                      ' Recurring Difference - PLEASE +
     A                                      INVESTIGATE '
     A  15                                49
     A                                      ' Revolving Credit Ind - PLEASE +
     A                                      INVESTIGATE '
      *                                              End   /198781/
      *
      *
      ** Report Summary by branch
      *
     A          R BRCHEN4
     A                                    35
     A                                      'Number of Facilities in +
     A                                      file for Branch '
     A                                      SPACEB(003)
     A            PREBRC         3A  O    76
     A                                    81'='
      ** Start of change MD022057
     A**********  BFCAUD         5S 0O    84                                                MD022057
     A            BFCAUD         7S 0O    84
      ** End of change MD022057
     A                                      EDTCDE(3)
     A                                    35
     A                                      'Number of Facilities in +
     A                                      Error in this Branch '
     A                                      SPACEB(001)
     A                                    81'='
     A                                    81'='
      ** Start of change MD022057
     A**********  BFCER4         5S 0O    84                                                MD022057
     A            BFCER4         7S 0O    84
      ** End of change MD022057
     A                                      EDTCDE(3)
     A N13                                51
     A                                      ' ***  END OF REPORT FOR BRANCH ***'
     A                                      SPACEB(003)
      *
      ** Report Summary
      *
     A          R TRAILER4
     A                                    35
     A                                      'Number of Facilities in +
     A                                      Facility Detail File'
     A                                      SPACEB(003)
     A                                    81'='
      ** Start of change MD022057
     A**********  FACAUD         5S 0O    84                                                MD022057
     A            FACAUD         7S 0O    84
      ** End of change MD022057
     A                                      EDTCDE(3)
     A                                    35
     A                                      'Total Number of Facilities in +
     A                                      Error'
     A                                      SPACEB(001)
      ** Start of change MD022057
     A**********  FACERR         5S 0O    84                                                MD022057
     A            FACERR         7S 0O    84
      ** End of change MD022057
     A                                      EDTCDE(3)
     A N13                                51
     A                                      '   ***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A  13                                51
     A                                      '***  NO DETAILS TO REPORT+
     A                                        ***'
     A                                      SPACEB(003)
      *
      ** Database Error
      *
     A          R DBERROR4
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SKIPA(43)
     A                                    35'*** END OF REPORT ***'
