      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Facility Currency Conv. - Facilities')
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  PRTF/LE3580P1 - Midas Lending Facility Currency Conversion   *
      *                  Facilities Report.                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CEU021  *CREATE    Date 15Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CEU021 - EMU LE Facility Currency Conversion                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R P1H010                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(1)
     A                                     6'REFERENCE LE3580P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    46'Lending Facility Currency'
     A                                    72'Conversion - Facilities'
     A                                      SPACEA(2)
      *
     A                                     1'Facility:'
     A                                      SPACEB(1)
     A            P1CNUM         6        11
     A            P1FACT         3        18
     A            P1FCNO         2        22
      *
     A                                    43'Status:'
     A            P1STS          9        51
     A                                      SPACEA(2)
      *
     A                                     1'Field'
     A                                    43'Original Value'
     A                                    58'(in Ccy)'
     A                                    88'Converted Value'
     A                                   104'(in Euro)'
     A                                      SPACEA(1)
      *
     A                                     1'_____'
     A                                    43'_______________'
     A                                    58'________'
     A                                    88'________________'
     A                                   104'_________'
     A                                      SPACEA(2)
      *
     A                                     1'Facility Currency'
     A            P1OCCY         3        43
     A                                    47'-'
     A            P1OCYN        14        49
     A            P1CCCY         3        88
     A                                    92'-'
     A            P1CCYN        14        94
     A                                      SPACEA(2)
      *
     A                                     1'Multi Currency Indicator'
     A            P1OCI          1        43
     A            P1CCI          1        88
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R P1D010
      *
     A            P1FLD         35         1
     A            P1OVAL        20        46
     A            P1CVAL        20        93
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  No Details To Report *
      *-------------------------*
      *
     A          R P1NDR
     A                                    48'***  NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *------------------*
      ***  End Of Report *
      *------------------*
      *
     A          R P1EOR
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
