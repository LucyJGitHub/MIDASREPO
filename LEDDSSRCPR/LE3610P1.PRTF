      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Facility Currency Conv. - Facility Amend.')
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  PRTF/LE3610P1 - Midas Lending Facility Currency Conversion   *
      *                  Facility Amendments.                         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CEU021  *CREATE    Date 21Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CEU021 - EMU LE Facility Currency Conversion                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R P1H010                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(1)
     A                                     6'REFERENCE LE3610P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    46'Lending Facility Currency'
     A                                    72'Conversion - Facility Amendments'
     A                                      SPACEA(2)
      *
     A                                     1'Facility'
     A                                    15'Sequence'
     A                                    28'Original'
     A                                    70'Converted'
     A                                      SPACEA(1)
      *
     A                                    28'Amendment Ccy'
     A                                    48'Amendment Amount'
     A                                    70'Amendment Ccy'
     A                                    90'Amendment Amount'
     A                                      SPACEA(1)
      *
     A                                     1'_____________'
     A                                    15'________'
     A                                    28'_____________'
     A                                    44'____________________'
     A                                    70'_____________'
     A                                    86'____________________'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R P1D010
      *
     A            P1CNUM         6         1
     A            P1FACT         3         8
     A            P1FCNO         2        12
     A            P1SEQ          3        20
     A            P1OACY         3        32
     A            P1OAMT        20        47
     A            P1CACY         3        74
     A            P1CAMT        20        89
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  No Details To Report *
      *-------------------------*
      *
     A          R P1NDR
     A                                    48'***  NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *------------------*
      ***  End Of Report *
      *------------------*
      *
     A          R P1EOR
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
