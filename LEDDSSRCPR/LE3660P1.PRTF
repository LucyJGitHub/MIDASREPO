      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Facility Currency Conv. - History Header')
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  PRTF/LE3660P1 - Midas Lending Facility Currency Conversion   *
      *                  History Header Report                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 156376             Date 26Mar99               *
      *                 CEU021  *CREATE    Date 06Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  156376 - Header should include the word 'Conversion'.        *
      *  CEU021 - EMU LE Facility Currency Conversion                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R P1H010                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(1)
     A                                     6'REFERENCE LE3660P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
      *                                                            Begin/156376/
     A***********                         46'Lending Facility Currency'
     A                                    36'Lending Facility Currency'
     A                                    62'Conversion'
     A***********                         72'- Facility History Header'
     A                                    73'- Facility History Header'
     A*                                                              End/156376/
     A                                      SPACEA(2)
      *
     A                                    35'Original Facility'
     A                                    53'(in original currency)'
     A                                    84'Converted Facility'
     A                                   103'(in Euro)'
     A                                      SPACEA(1)
      *
     A                                    35'__________________'
     A                                    53'______________________'
     A                                    84'___________________'
     A                                   103'_________'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R P1D010
      *
     A                                     1'Facility:'
     A                                      SPACEB(1)
     A            P1CNUM         6        31
     A            P1FACT         3        38
     A            P1FCNO         2        42
     A            P1CNAM        30        45
     A                                      SPACEA(1)
      *
     A                                     1'Currency:'
     A            P1OCCY         3        54
     A                                    58'-'
     A            P1OCYN        15        60
     A            P1CCCY         3        91
     A                                    95'-'
     A            P1CCYN        15        97
     A                                      SPACEA(1)
      *
     A                                     1'Currency Indicator:'
     A            P1OCI          1        63
     A                                    65'-'
     A            P1OCIN         8        67
     A            P1CCI          1       100
     A                                   102'-'
     A            P1CCIN         8       104
     A                                      SPACEA(1)
      *
     A                                     1'Original Facility Amount:'
     A            P1OOAM        20        55
     A            P1COAM        20        92
     A                                      SPACEA(1)
      *
     A                                     1'Last Facility Amount:'
     A            P1OLAM        20        55
     A            P1CLAM        20        92
     A                                      SPACEA(1)
      *
     A                                     1'Last Total Drawn:'
     A            P1ODAM        20        55
     A            P1CDAM        20        92
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  No Details To Report *
      *-------------------------*
      *
     A          R P1NDR
     A                                    48'***  NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *------------------*
      ***  End Of Report *
      *------------------*
      *
     A          R P1EOR
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
