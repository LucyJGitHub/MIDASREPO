      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Facility Currency Conv. - History Amount')
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  PRTF/LE3670P1 - Midas Lending Facility Currency Conversion   *
      *                  History Amount Report                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE042             Date 06Sep05               *
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CLE023             Date 19Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 156376             Date 17May99               *
      *                 CEU021  *CREATE    Date 07Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CLE023 - Lending Facility History Improvements.              *
      *  156376 - Various problems with report LE03670P1 :            *
      *           a) Header should include the word 'Conversion'      *
      *           b) Loan number should be printed on report          *
      *           c) Original facility currency should be shown.      *
      *           d) Only print loan number for loan actions.         *
      *  CEU021 - EMU LE Facility Currency Conversion                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R P1H010                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(1)
     A                                     6'REFERENCE LE3670P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
      *                                                            Begin/156376/
     A***********                         46'Lending Facility Currency'
     A                                    36'Lending Facility Currency'
     A                                    62'Conversion'
     A***********                         72'- Facility History Amounts'
     A                                    73'- Facility History Amounts'
     A*                                                              End/156376/
     A                                      SPACEA(2)
      *
     A                                    35'Original Facility'
     A                                    53'(in original currency)'
     A                                    84'Converted Facility'
     A                                   103'(in Euro)'
     A                                      SPACEA(1)
      *
     A                                    35'__________________'
     A                                    53'______________________'
     A                                    84'___________________'
     A                                   103'_________'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R P1D010
      *
     A                                     1'Facility:'
     A                                      SPACEB(1)
     A*                                                            Begin/156376/
     A            P1ORCY         3        27
     A*                                                              End/156376/
     A            P1CNUM         6        31
     A            P1FACT         3        38
     A            P1FCNO         2        42
     A            P1CNAM        30        45
     A                                     1'Change Type:'
     A                                      SPACEB(1)
     A*                                                            Begin/156376/
     A***********                                                  Begin/156376/
     A***********                         31'Loan:'
     A*********** P1FALN         6        36
     A N26                                31'Loan:'
     A N26        P1FALN         6        36
     A***********                                                    End/156376/
     A*                                                              End/156376/
     A            P1CTYP         2        46
     A            P1CTXT        26        49
     A                                     1'Action Amount:'
     A                                      SPACEB(1)
     A            P1OAMT        20        55
     A            P1CAMT        20        92
     A                                     1'Facility Amount:'
     A                                      SPACEB(1)
     A            P1OFAM        20        55
     A            P1CFAM        20        92
     A                                     1'Drawn Amount:'
     A                                      SPACEB(1)
     A            P1ODAM        20        55
     A            P1CDAM        20        92
     A                                     1'Undrawn Amount:'
     A                                      SPACEB(1)
     A            P1ONDR        20        55
     A            P1CNDR        20        92
     A**Start of change CLE023
     A  23                                 1'Actual Facilty Amount:'
     A  23                                  SPACEB(1)
     A  23        P1OAFM        20        55
     A  23        P1CAFM        20        92
     A  23                                 1'Previous Action Amount:'
     A  23                                  SPACEB(1)
     A  23        P1OPMT        20        55
     A  23        P1CPMT        20        92
     A**End of change CLE023
     A                                     1'Account Keys:'
     A                                      SPACEB(1)
     A*                                              Begin /CLE042/
     A************P1OKY1********10        35                                                  CLE042
     A************P1OKY2********10        55                                                  CLE042
     A************P1CKY1********10        82                                                  CLE042
     A************P1CKY2********10       102                                                  CLE042
     A            P1OKY1        14        35                                                  CLE042
     A            P1OKY2        14        55                                                  CLE042
     A            P1CKY1        14        82                                                  CLE042
     A            P1CKY2        14       102                                                  CLE042
     A*                                              End   /CLE042/
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  No Details To Report *
      *-------------------------*
      *
     A          R P1NDR
     A                                    48'***  NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *------------------*
      ***  End Of Report *
      *------------------*
      *
     A          R P1EOR
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
