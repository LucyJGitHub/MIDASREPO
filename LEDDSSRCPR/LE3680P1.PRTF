      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Customer Advices For Converted Facilities')   *
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  PRTF/LE3680P1 - Customer Advices                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CEU021  *CREATE    Date 08Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,LEH00217                                 *
      *             WNCPYSRC,LEH00218                                 *
      *  CEU021 - EMU LE Facility Currency Conversion                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Advice Detail Record Format *
      *--------------------------------*
      *
     A          R P1D010                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(2)
      *
     A                                     1'Branch:'
     A            P1BRCA         3         9
     A                                    13'-'
     A            P1BRCN        30        15
     A                                      SPACEA(2)
      *
     A                                     1'Date:'
     A            BJMRDT    R              7REFFLD(MRDT)
     A                                      SPACEA(2)
      *
     A                                    29'FACILITY CURRENCY'
     A                                    47'CONVERSION ADVICE'
     A                                      SPACEA(1)
      *
     A                                    29'__________________'
     A                                    47'_________________'
     A                                      SPACEA(2)
      *
     A                                     1'Your facility number -'
     A            P1CNUM         6        24
     A            P1FACT         3        31
     A            P1FCNO         2        35
     A*/COPY WNCPYSRC,LEH00217
     A                                    38'- has been converted'
     A                                    59'as follows:'
     A*/COPY WNCPYSRC,LEH00218
     A                                      SPACEA(3)
      *
     A                                     1'From currency'
     A            P1OCCY         3        15
     A                                    19'-'
     A            P1OCYN        14        21
     A                                    37'amount'
     A            P1OAMT        20        44
     A                                      SPACEA(2)
      *
     A                                     1'To currency'
     A            P1CCCY         3        15
     A                                    19'-'
     A            P1CCYN        14        21
     A                                    37'amount'
     A            P1CAMT        20        44
     A                                      SPACEA(4)
      *
     A            P1CST1        35        15
     A                                      SPACEA(1)
     A            P1CST2        35        15
     A                                      SPACEA(1)
     A            P1CST3        35        15
     A                                      SPACEA(1)
     A            P1CST4        35        15
     A                                      SPACEA(1)
      *
