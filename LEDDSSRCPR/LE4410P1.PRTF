      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Reval AK generation w/ All in Rate Report')
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  PRTF/LE4410P1 - Revaluation A/Key Generation Using All In    *
      *                  Rate - Report                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE042             Date 06Sep05               *
      *  Prev Amend No. CAS005  *CREATE    Date 16Dec02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ** Report Header Record Format 1 *
      *--------------------------------*
      *
     A          R LE4410D1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SKIPB(1)
     A                                     6'Reference LE4410P1'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    26'CUSTOMER LENDING REVALUATION +
     A                                      USING ALL IN RATE +
     A                                      ACCOUNT KEYS GENERATION'
     A N31                                  SPACEA(1)
     A  31                                +1'- UP TO'
     A  31        PEVCD          7A       +1TEXT('Event Control Date')
     A  31                                  SPACEA(1)
     A N31                                26'-----------------------------+
     A                                      ------------------+
     A                                      -----------------------'
     A N31                                  SPACEA(2)
     A  31                                26'-----------------------------+
     A                                      ------------------+
     A                                      -----------------------+
     A                                      ----------------'
     A  31                                  SPACEA(2)
      *
     A                                    23'BRANCH:   '
     A            PBRCA          3A       +2TEXT('BRANCH')
     A            PBRNM         30A       +2TEXT('BRANCH DESCRIPTION')
     A                                      SPACEA(2)
      *
     A                                    23'CUSTOMER'
     A                                    36'LOAN NO.'
     A                                    49'ACCOUNT KEY'
     A                                    71'AMOUNT'
     A                                    88'CURRENCY'
     A                                   101'REV IND'
     A                                      SPACEA(2)
      *
      *--------------------------------*
      ** Report Detail Record Format 2 *
      *--------------------------------*
      *
     A          R LE4410D2
      *
     A            PCNUM          6A       24TEXT('Customer Number')
     A            PLNRF          6A       37TEXT('Loan Number')
     A*                                              Begin /CLE042/
     A            PACKY         14A       49TEXT('Account Key')                               CLE042
     A************PACKY         10A       50TEXT('Account Key')                               CLE042
     A*                                              End   /CLE042/
     A            PYAMT         18A       65TEXT('Loan Amount')
     A            PCYCD2         3A       91TEXT('Currency')
     A            PREVI          1A      104TEXT('Reversal Indicator')
     A                                      SPACEA(1)
      *
      *-----------------------*
      ** Totals Record Format *
      *-----------------------*
      *
     A          R LE4410D3
     A                                      SPACEB(1)
      *
     A                                    50'TOTAL ACCOUNT KEYS GENERATED'
     A            PTOTAL         5S 0     82TEXT('Total A/C Keys Generated')
     A                                      SPACEA(2)
      *
      *------------------------------*
      ** Report Footer Record Format *
      *------------------------------*
      *
     A          R LE4410D4
     A                                      SPACEB(3)
      *
     A                                    51'*** END OF REPORT FOR +
     A                                      BRANCH ***'
