      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Accruals Using Net-Treasury-Rate - Report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LE5210P1 - Accruals Using Net-Treasury-Rate Report      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS005  *CREATE    Date 16Dec02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LE5210P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    27'Interest Accruals Using +
     A                                      Net-Treasury-Rate - Up to +
     A                                      and excluding - '
     A            RDATE          7A       93
      *
     A                                   103'BRANCH -'
     A            A8BRCD    R            112REFFLD(BRCA)
     A            A8BRNM    R            116REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(2)
     A                                    27'------------------------+
     A                                      --------------------------+
     A                                      ----------------'
      *
     A                                      SPACEA(2)
     A                                     2'CUST.'
     A                                     9'NAME'
     A                                    32'LOAN'
     A                                    39'ACCRUAL PERIOD'
     A                                    59'RATE'
     A                                    70'CCY'
     A                                    82'PRINCIPAL'
     A                                    95'ACCRUED TO DATE'
     A                                   120'TO ACCRUE'
      *
     A                                      SPACEA(1)
     A                                     2'NUMBER'
     A                                     9'TOWN'
     A                                    32'NUMBER'
     A                                    40'FROM'
     A                                    49'TO'
      *
     A          R HEADP2
     C                                      SPACEB(2)
     C            RNARR         30A       10
      *
     A          R DETAIL1
     A                                      SPACEB(2)
     A            RCNUM          6A        2
     A            RCNAME        20A        9
     A            RLNRF          6A       31
     A            RACFR          7A       39
     A            RACTD          7A       47
     A            RINTR         13A       55
     A            RCCY           3A       70
     A            RCPAM         18A       74
     A            RAMAD         18A       93
     A            RAMTA         18A      112
      *
     A          R DETAIL2                   SPACEB(1)
      *
     A            RTOWN         10A        9
      *
     A          R TOTAL1                    SPACEB(2)
      *
     A            RPSLN          6A       48
     A                                    55'LOAN NET TOTAL'
     A            RLNPR         18A       74
     A            RLNIAD        18A       93
     A            RLNITA        18A      112
      *
     A          R TOTAL2                    SPACEB(2)
      *
     A                                    45'LOAN TYPE/SUB TYPE TOTAL'
     A            RLTPR         18A       74
     A            RLTIAD        18A       93
     A            RLTITA        18A      112
      *
     A          R TOTAL3                    SPACEB(2)
      *
     A                                    55'CURRENCY TOTAL '
     A            RCCYT          3A       70
     A            RCYPR         18A       74
     A            RCYIAD        18A       93
     A            RCYITA        18A      112
      *
     A          R TRAILP
     A                                      SPACEB(2)
     A                                    46'***  END OF REPORT FOR +
     A                                      BRANCH  '
     A            RBRCA          3A       76
     A                                    80'***'
      *
