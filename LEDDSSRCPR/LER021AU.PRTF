      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas LE Part fclty reductions due to repay audit')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LER021AU - Participant Facility Reductions Due          *
      *                  to Repayments Today Report - Audit           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CLE004 *Create     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE004 - Customer Lending Enhancements - Syndications        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LER021AH                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LER021AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    25'REPORT OF PARTICIPANT +
     A                                      FACILITY REDUCTIONS DUE TO +
     A                                      REPAYMENTS TODAY - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    25'----------------------+
     A                                      ---------------------------+
     A                                      ------------------------'
     A                                      SPACEA(3)
      *
     A          R LER021A1
     A                                     9'TOTAL NO. OF FACILITIES REDUCED'
     A                                      SPACEB(2)
     A            RCNTR          5  0     50EDTCDE(Z)
     A                                    13'VALUE OF FACILITIES BEFORE +
     A                                      REDUCTION'
     A                                      SPACEB(1)
     A            RVALB         18  0     50EDTCDE(Z)
     A                                    13'VALUE OF FACILITIES AFTER +
     A                                      REDUCTION'
     A                                      SPACEB(1)
     A            RVALA         18  0     50EDTCDE(Z)
     A                                    13'VALUE OF REDUCTION AMOUNT'
     A                                      SPACEB(1)
     A            RVALR         18  0     50EDTCDE(Z)
     A                                    60'***  END OF REPORT  ***'
     A                                      SPACEB(3)
     A
     A          R LER021A2
     A                                    60'* NO DETAILS TO REPORT *'
     A                                      SPACEB(3)
      *
     A          R DBERRAU
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
