      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Part facility reductions due to repay')       *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LER021P1 - Participant Facility Reductions Due          *
      *                  to Repayments Today Report                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CLE004 *C *CREATE  Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,LEH00113                                 *
      *             WNCPYSRC,LEH00114                                 *
      *             WNCPYSRC,LEH00115                                 *
      *             WNCPYSRC,LEH00116                                 *
      *             WNCPYSRC,LEH00117                                 *
      *             WNCPYSRC,LEH00122                                 *
      *             WNCPYSRC,LEH00118                                 *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE004 - Customer Lending Enhancements - Syndications        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LER021H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LER021P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    22'REPORT OF PARTICIPANT FACILITY +
     A                                      REDUCTIONS DUE TO REPAYMENTS +
     A                                      TODAY'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    22'-------------------------------+
     A                                      -----------------------------+
     A                                      -----'
     A                                      SPACEA(2)
      *
     A                                     5'CUSTOMER'
     A*/COPY WNCPYSRC,LEH00113
     A                                    18'FACILITY'
     A                                    +2'CURRENCY'
     A*/COPY WNCPYSRC,LEH00114
     A                                    38'OLD FACILITY AMOUNT'
     A                                    59'REDUCED FACILITY AMT.'
     A                                    96'DIFF.'
     A                                      SPACEA(1)
      *
     A                                     5'--------'
     A*/COPY WNCPYSRC,LEH00115
     A                                    18'--------'
     A                                    +2'--------'
     A*/COPY WNCPYSRC,LEH00116
     A                                    38'-------------------'
     A                                    59'---------------------'
     A                                    96'-----'
     A                                      SPACEA(1)
      *
     A          R LER021D1                  SPACEA(1)
      * Begin /CSD027/
     A**********  RCNUM          6  0      6                                                  CSD027
     A            RCNUM          6A        6
      * End /CSD027/
     A*/COPY WNCPYSRC,LEH00117
     A            RFACT          3  0     18
     A                                    21'/'
     A            RFCNO          2  0     22
     A*/COPY WNCPYSRC,LEH00122
     A            RFCCY          3        30
     A*/COPY WNCPYSRC,LEH00118
     A            RSAMT         18        40
     A            RTAMT         18        62
     A            RUAMT         18        84
     A     31                            103'HISTORIC RC IND USED'
      *
     A          R LER021T1                  SPACEB(3)
     A N37                                51'*** END OF REPORT ***'
     A  37                                46'*** END OF REPORT FOR BRANCH'
     A  37        RBRCH     R             76REFFLD(BRCA)
     A  37                                79'  ***'
