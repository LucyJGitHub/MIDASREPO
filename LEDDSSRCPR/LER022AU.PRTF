      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas LE Credit agree update due to repay audit')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LER022AU - Credit Agreement Update Due to               *
      *                  Repayments - Audit                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CLE005 *Create     Date 22May97               *
      *                 xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE005 - Customer Lending Enhancements - Others              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LER022AH                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LER022AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    28'REPORT OF CREDIT AGREE+
     A                                      MENT REDUCTION DUE TO +
     A                                      REPAYMENTS TODAY    +
     A                                      RUN CONTROLS'
     A                                      SPACEA(1)
      *
     A                                    28'----------------------+
     A                                      ---------------------+
     A                                      ----------------'
     A                                      SPACEA(3)
      *
     A          R LER022A1
     A                                     9'TOTAL NO. OF FACILITIES REDUCED'
     A                                      SPACEB(2)
     A            RCNTR     R             50REFFLD(BNCU)
     A                                      EDTCDE(Z)
     A                                    13'VALUE OF FACILITIES BEFORE +
     A                                      REDUCTION'
     A                                      SPACEB(1)
     A            RVALB         18  0     50EDTCDE(Z)
     A                                    13'VALUE OF FACILITIES AFTER +
     A                                      REDUCTION'
     A                                      SPACEB(1)
     A            RVALA         18  0     50EDTCDE(Z)
     A                                    13'VALUE OF REDUCTION AMOUNT'
     A                                      SPACEB(1)
     A            RVALR         18  0     50EDTCDE(Z)
     A                                    60'***  END OF REPORT  ***'
     A                                      SPACEB(3)
     A
     A          R LER022A2
     A                                    60'* NO DETAILS TO REPORT *'
     A                                      SPACEB(3)
      *
     A          R DBERRAU
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE    R             30REFFLD(DPGM)
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY     R             30REFFLD(MFLK)
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM     R             30REFFLD(CEXA)
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE     R             30REFFLD(AMSQ)
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
