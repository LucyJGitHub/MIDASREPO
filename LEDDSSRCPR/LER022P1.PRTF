      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Part facility reductions due to repay')       *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LER022P1 - Participant Facility Reductions Due          *
      *                  to Repayments Today Report                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CLE138             Date 02Nov21               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CLE005 *C *CREATE  Date 22May97               *
      *                 xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE138 - Class Codes for Facilities                          *
      *  CLE005 - Customer Lending Enhancements - Others              *
      *  MD046248 - Finastra Rebranding                               *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,LEH00133                                 *
      *             WNCPYSRC,LEH00134                                 *
      *             WNCPYSRC,LEH00135                                 *
      *             WNCPYSRC,LEH00136                                 *
      *             WNCPYSRC,LEH00137                                 *
      *             WNCPYSRC,LEH00138                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R LER022H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LER022P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    22'REPORT OF CREDIT AGREE+
     A                                      MENT REDUCTION DUE TO +
     A                                      REPAYMENTS TODAY'
     A
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    22'-------------------------------+
     A                                      -----------------------------+
     A                                      -----'
     A                                      SPACEA(2)
      *
     A                                     5'CUSTOMER'
     A*/COPY WNCPYSRC,LEH00133
      **Start of change CLE138
     A**********                          18'FACILITY'                                        CLE138
     A**********                          +2'CURRENCY'                                        CLE138
     A                                    17'FACILITY'
     A                                    +3'CURRENCY'
      **End of change CLE138
     A*/COPY WNCPYSRC,LEH00134
     A                                    38'OLD FACILITY AMOUNT'
     A                                    59'REDUCED FACILITY AMT.'
     A                                    96'DIFF.'
     A                                      SPACEA(1)
      *
     A                                     5'--------'
     A*/COPY WNCPYSRC,LEH00135
      **Start of change CLE138
     A**********                          18'--------'                                        CLE138
     A                                    16'----------'
      **End of change CLE138
     A*/COPY WNCPYSRC,LEH00136
     A                                    +2'--------'
     A                                    38'-------------------'
     A                                    59'---------------------'
     A                                    96'-----'
     A                                      SPACEA(1)
      *
     A          R LER022D1                  SPACEA(1)
     A            RCNUM     R              6REFFLD(TNMR)
     A*/COPY WNCPYSRC,LEH00137
      **Start of change CLE138
     A**********  RFACT     R             18REFFLD(BRCA)                                      CLE138
     A**********                          21'/'                                               CLE138
     A**********  RFCNO     R             22REFFLD(TRET)                                      CLE138
     A            RFACT          3  0     16
     A                                    19'/'
     A            RFCNO          2  0     20
     A     40     RFCLS          4        23
      **End of change CLE138
     A*/COPY WNCPYSRC,LEH00138
     A            RFCCY     R             30REFFLD(BRCA)
     A            RSAMT     R             40REFFLD(FULA)
     A            RTAMT     R             62REFFLD(FULA)
     A            RUAMT     R             84REFFLD(FULA)
     A     31                            103'HISTORIC RC IND USED'
      *
     A          R LER022T1                  SPACEB(3)
     A N37                                51'*** END OF REPORT ***'
     A  37                                46'*** END OF REPORT FOR BRANCH'
     A  37        RBRCH     R             76REFFLD(BRCA)
     A  37                                79'  ***'
