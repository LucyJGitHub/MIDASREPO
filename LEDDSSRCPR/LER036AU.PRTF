      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Non-RVCR Fclty Upd. - LEO Audit')
     A*****************************************************************
     A*                                                               *
     A*   Midas - Customer Lending Module                         *
     A*                                                               *
     A*   PRTF/LER036AU - Facility update for LEO Repayments against  *
     A*                   non-revolving credit facilities.            *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01473             Date 21Jul94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01473 - Created for Leo Midas Integration Project           *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R LER036AH                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'LER036AU'
     A            BJURPT        53        30
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    21'REPORT OF FACILITY REDUCTION-
     A                                      S DUE TO LEO REPAYMENTS TODAY'
     A                                    81'RUN CONTROLS'
     A                                      SPACEA(1)
     A                                    21'-----------------------------
     A                                      -----------------------------'
     A
     A                                      SPACEA(1)
     A
     A          R LER036A1
     A                                    60'* NO DETAILS TO REPORT *'
     A                                      SPACEB(3)
     A
     A          R LER036A2
     A                                     9'TOTAL NO. OF FACILITIES REDUCED'
     A                                      SPACEB(2)
     A            RPTDET         5  0     50EDTCDE(Z)
     A                                    13'VALUE OF FACILITIES BEFORE REDUCTI-
     A                                      ON'
     A                                      SPACEB(1)
     A            ZFAMTO        18  0     50EDTCDE(Z)
     A                                    13'VALUE OF FACILITIES AFTER REDUCTIO-
     A                                      N'
     A                                      SPACEB(1)
     A            ZFAMTR        18  0     50EDTCDE(Z)
     A                                    13'VALUE OF REDUCTION AMOUNT'
     A                                      SPACEB(1)
     A            ZFAMTD        18  0     50EDTCDE(Z)
     A                                    60'***  END OF REPORT  ***'
     A                                      SPACEB(3)
     A
     A          R DBFMTAU                   SPACEB(3)
     A
     A                                    26'*** DATABASE ERROR'
     A            DBASE          3  0     +1
     A                                    +1'FILE:'
     A            DBFILE         8        +1
     A                                    +1'KEY:'
     A            DBKEY         29        +1
     A                                    +1'***'
     A
