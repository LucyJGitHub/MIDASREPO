      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Non-RVCR Fclty Upd. - LEO Detail')
     A*****************************************************************
     A*                                                               *
     A*   Midas - Customer Lending Module                         *
     A*                                                               *
     A*   PRTF/LER036P1 - Facility update for LEO Repayments against  *
     A*                   non-revolving credit facilities.            *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. S01473             Date 21Jul94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  S01473 - Created for Leo Midas Integration Project           *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R LER036H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'LER036P1'
     A            BJURPT        53        30
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    21'REPORT OF FACILITY REDUCTION-
     A                                      S DUE TO LEO REPAYMENTS TODAY'
     A
     A N02                                  SPACEA(1)
     A
     A  02                                81'BRANCH'
     A  02        BRCA      R             88
     A  02        BRNM      R             92
     A  02                                  SPACEA(1)
     A                                    21'-----------------------------
     A                                      -----------------------------'
     A                                      SPACEA(1)
     A
     A                                     5'CUSTOMER'
     A
     A                                    18'FACILITY'
     A                                    +2'CURRENCY'
     A                                    38'OLD FACILITY AMOUNT'
     A                                    59'REDUCED FACILITY AMT.'
     A                                    96'DIFF.'
     A
     A                                      SPACEA(1)
     A
     A                                     5'--------'
     A                                    18'--------'
     A                                    +2'--------'
     A                                    38'-------------------'
     A                                    59'---------------------'
     A                                    96'-----'
     A
     A                                      SPACEA(1)
     A
     A          R LER036D1                  SPACEA(1)
     A
      * Begin /CSD027/
     A**********  CNUM           6  0      6                                                  CSD027
     A            CNUM           6A        6
      * End /CSD027/
     A            FACT           3  0     18
     A                                    21'/'
     A            FCNO           2  0     22
     A            FCCY           3        30
     A     03     OFAMT         13  0     40EDTWRD(' ,   ,   ,   , 0 -')
     A     04     OFAM1         13  1     40EDTWRD('   ,   ,   , 0 . -')
     A     05     OFAM2         13  2     40EDTWRD('  ,   ,   , 0 .  -')
     A     06     OFAM3         13  3     40EDTWRD(' ,   ,   , 0 .   -')
     A     03     RFAMT         13  0     62EDTWRD(' ,   ,   ,   , 0 -')
     A     04     RFAM1         13  1     62EDTWRD('   ,   ,   , 0 . -')
     A     05     RFAM2         13  2     62EDTWRD('  ,   ,   , 0 .  -')
     A     06     RFAM3         13  3     62EDTWRD(' ,   ,   , 0 .   -')
     A     03     DFAMT         13  0     84EDTWRD(' ,   ,   ,   , 0 -')
     A     04     DFAM1         13  1     84EDTWRD('   ,   ,   , 0 . -')
     A     05     DFAM2         13  2     84EDTWRD('  ,   ,   , 0 .  -')
     A     06     DFAM3         13  3     84EDTWRD(' ,   ,   , 0 .   -')
     A     07                            103'HISTORIC RC IND USED'
     A
     A
     A          R LER036T1                  SPACEB(3)
     A N02                                56'*** END OF REPORT ***'
     A  02                                51'*** END OF REPORT FOR BRANCH ***'
     A
