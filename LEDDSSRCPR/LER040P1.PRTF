      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Re-organise Facility history & Report')       *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*   PRTF/LER040P1 - Print File for reorganistion and            *
     A*                   archiving of facility history               *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      *                 AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.03 ----------------------------------------*
      *                 201127             Date 28Jan02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 178425             Date 07Feb02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 Xnnnnn             Date ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,LEH00062                                 *
      *  201127 - Should show START DATE rather than DATE APPROVED.   *
      *  178425 - The report should also reflect the customer number. *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A          R LER040H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'LER040'
     A            BJURPT        53        30
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A*
     A                                    50'FACILITY HISTORY REPORT'
     A  33                                  SPACEA(1)
     A  33                                50'-----------------------'
     A N33                                79'BRANCH'
     A N33        STRBRC         3A       86
     A N33                                90'-'
     A N33        BRNM          30A       92
     A                                      SPACEA(2)
     A
     A
     A          R LER040D1
     A                                      SPACEB(3)
     A                                     6'CUSTOMER'
      *                                              Begin /178425/
     A            BBCUST         6        16
     A            BBCRNM        20        24
     A*********** BBCRNM        20        16
      *                                              End   /178425/
     A                                    +4'FACILITY'
     A            FHFTYP         3  0     +2
     A                                    +1'/'
     A            FHFSEQ         2  0     +1
     A            AMFCNM        30        +2
     A            @FACT         10        +1
     A                                      SPACEA(1)
     A            BBCRTN        10        16
     A*/COPY WNCPYSRC,LEH00062
     A                                      SPACEA(1)
     A                                     6'A/C OFFICER'
     A            AZACON        30        +1
     A**Start of change 201127
     A**********                          +1'DATE APPROVED'                                   201127
     A                                    +1'START DATE'
     A**End of change 201127
     A            APPDAT         7        +1
     A                                    +1'DATE EXPIRED'
     A            EXPDAT         7        +1
     A                                      SPACEA(1)
     A  44                                 6'MULTI CUSTOMER'
     A                                    22'ORIGINAL FACILITY AMOUNT'
     A            FHFCCY         3        49
     A  40        OFAM0         13  0     53EDTCDE(J)
     A  41        OFAM1         13  1     53EDTCDE(J)
     A  42        OFAM2         13  2     53EDTCDE(J)
     A  43        OFAM3         13  3     53EDTCDE(J)
     A  45                                71'MULTI CURRENCY'
     A                                     6'COUNTRY OF RISK'
     A                                      SPACEB(1)
     A            A4CNNM        30        +1
     A                                    +2'OVERLINE PERCENTAGE'
     A            FHOLPC         5  3     +1EDTCDE(J)
     A                                    +1'NOMINAL RATE'
     A            FHNOMR        11  7     +1EDTCDE(J)
     A                                      SPACEA(3)
     A                                     7'DATE'
     A                                    +5'ACTION'
     A                                   +20'LOAN'
     A                                    +2'AMOUNT (FAC. CCY)'
     A                                    +1'REV'
     A                                    +3'FACILITY AMOUNT'
     A                                    +6'DRAWN AMOUNT'
     A                                    +4'UNDRAWN AMOUNT'
     A                                      SPACEA(1)
     A                                    66'CR?'
     A                                      SPACEA(2)
     A          R LER040D2
     A                                      SPACEB(1)
     A  46        FADAT          7         6
     A            ACTN          26        14
      **Start of change CLE148
     A**********  FALOAN         6  0     41                                                  CLE148
     A            FALOAN         6A       41
      **End of change CLE148
     A  40        AAMT0         13  0     48EDTCDE(J)
     A  41        AAMT1         13  1     48EDTCDE(J)
     A  42        AAMT2         13  2     48EDTCDE(J)
     A  43        AAMT3         13  3     48EDTCDE(J)
     A  47                                66'REV'
     A  40        CFAM0         13  0     70EDTCDE(J)
     A  41        CFAM1         13  1     70EDTCDE(J)
     A  42        CFAM2         13  2     70EDTCDE(J)
     A  43        CFAM3         13  3     70EDTCDE(J)
     A  40        DRAM0         13  0     88EDTCDE(J)
     A  41        DRAM1         13  1     88EDTCDE(J)
     A  42        DRAM2         13  2     88EDTCDE(J)
     A  43        DRAM3         13  3     88EDTCDE(J)
     A  40        UNDR0         13  0    106EDTCDE(J)
     A  41        UNDR1         13  1    106EDTCDE(J)
     A  42        UNDR2         13  2    106EDTCDE(J)
     A  43        UNDR3         13  3    106EDTCDE(J)
     A*
     A          R LER040T1
     A                                      SPACEB(3)
     A  33                                60'***  END OF REPORT  ***'
     A
     A N33                                55'***  END OF REPORT FOR +
     A                                      BRANCH'
     A N33        STRBRC         3        85
     A N33                                90'***'
     A
