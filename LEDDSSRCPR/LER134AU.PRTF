      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Fee Payments Due A/C Keys - Audit')
/*ADD *  CHGPRTF FILE(LER134AU) SAVE(*YES)                            *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*  PRTF/LER134AU - Print File for Fee Payments Due A/C Key      *
     A*                  Generation report - Audit                    *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. BH3479             DATE 17AUG92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  BH3479  -  New Report. No explanation here. Try looking in   *
     A*             RPG/LER134's header box, or call Ruth.            *
     A*                                                               *
     A*****************************************************************
     A*
     A          R LER134C1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE LER134AU'
     A            BJURPT        53        30
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z) SPACEA(2)
     A                                    43'FEE PAYMENTS DUE A/C KEYS GENERATI-
     A                                      ON - AUDIT' SPACEA(1)
     A                                    43'-----------------------------------
     A                                      ----------' SPACEA(2)
     A
     A          R LER134C2
     A                                      SPACEB(3)
     A                                     5'FILE CONTROL TOTALS:'
     A                                      SPACEA(1)
     A                                     5'--------------------'
     A
     A                                    12'ACCOUNT KEYS FILE'
     A                                      SPACEB(3)
     A                                      SPACEA(1)
     A                                    12'-----------------'
     A*
     A                                    12'RECORDS ON FILE - BEFORE'
     A                                      SPACEB(2)
     A            NORE           5  0     50EDTCDE(Z)
     A            VLRF          15  0     60EDTCDE(Z)
     A            VLRL           3  0     75
     A                                    12'RECORDS ON FILE - AFTER'
     A                                      SPACEB(2)
     A            LKNORE         5  0     50EDTCDE(Z)
     A            LKVLRF        15  0     60EDTCDE(Z)
     A            LKVLRL         3  0     75
     A
     A                                    45'***  END OF REPORT  ***'
     A                                      SPACEB(4)
     A
     A          R LER134C3
     A                                      SPACEB(4)
     A                                    43'NO ACCOUNT KEYS HAVE BEEN +
     A                                      GENERATED TODAY'
     A*    *********************
     A*    REPORT DATABASE ERROR
     A*    *********************
     A*
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8        30
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
